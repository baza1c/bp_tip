#Область ПрограммныйИнтерфейс

// Совершает звонок переданный из контакной информации
//
// Параметры:
//  КонтактнаяИнформация - Структура:
//       * Вид - СправочникСсылка.ВидыКонтактнойИнформации - Вид контактной информации	
//       * Значение - Строка - параметры контактной информации в формате JSON
//       * Представление - Строка - представление контактной информации
//       * Тип - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации Телефон	
//  ДополнительныеПараметры - Структура:
//       * АсинхронныйВызов	- Булево - тип вызова
//       * ВладелецКонтактнойИнформации - ОпределяемыйТип.ВладелецКонтактнойИнформации.
//       * ИмяРеквизита	- Строка - имя реквизита на форме
//       * Форма - ФормаКлиентскогоПриложения - форма из которой была вызвана команда    
// 
Процедура ПодготовитьПараметрыИПозвонить(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт
	
	Если ПустаяСтрока(КонтактнаяИнформация.Представление) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для совершения звонка введите номер телефона.'"), , ДополнительныеПараметры.ИмяРеквизита);
	Иначе
		
		ПозвонитьПоТелефону(КонтактнаяИнформация.Представление);
		
	КонецЕсли;
	
КонецПроцедуры

// Создает SMS по номеру переданному из контакной информации
//
// Параметры:
//  КонтактнаяИнформация - Структура:
//       * Вид - СправочникСсылка.ВидыКонтактнойИнформации - Вид контактной информации	
//       * Значение - Строка - параметры контактной информации в формате JSON
//       * Представление - Строка - представление контактной информации
//       * Тип - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации Телефон	
//  ДополнительныеПараметры - Структура:
//       * АсинхронныйВызов	- Булево - тип вызова
//       * ВладелецКонтактнойИнформации - ОпределяемыйТип.ВладелецКонтактнойИнформации.
//       * ИмяРеквизита	- Строка - имя реквизита на форме
//       * Форма - ФормаКлиентскогоПриложения - форма из которой была вызвана команда    
// 
Процедура ПодготовитьПараметрыИОтправитьSMS(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт
	
	Если ПустаяСтрока(КонтактнаяИнформация.Представление) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для отправки SMS введите номер телефона.'"), , ДополнительныеПараметры.ИмяРеквизита);
	Иначе
		
		НачатьСозданиеSMS(КонтактнаяИнформация.Представление);
		
	КонецЕсли;
	
КонецПроцедуры

// Совершает звонок на переданный номер телефона.
//
// Параметры:
//  НомерТелефона -Строка - номер телефона, на который будет осуществлен звонок.
//
Процедура ПозвонитьПоТелефону(НомерТелефона) Экспорт
	
	НомерТелефона = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("()_- ", НомерТелефона, "");
	
	ИмяПротокола = "tel"; // по умолчанию используем "tel".
	
	СтрокаЗапуска = ИмяПротокола + ":" + НомерТелефона;
	
	ЗапуститьПриложение(СтрокаЗапуска,, Ложь);
	
КонецПроцедуры 

// Создает SMS на переданный номер телефона.
//
// Параметры:
//  НомерТелефона -Строка - номер телефона.
//
Процедура НачатьСозданиеSMS(НомерТелефона) Экспорт
	
	НомерТелефона = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("()_- ", НомерТелефона, "");
	
	ИмяПротокола = "sms";
	
	СтрокаЗапуска = ИмяПротокола + ":" + НомерТелефона;
	
	ЗапуститьПриложение(СтрокаЗапуска,, Ложь);
	
КонецПроцедуры

Функция ДоступенМобильныйПочтовыйКлиент() Экспорт
	
	#Если МобильныйКлиент Тогда
		Возврат СредстваПочты.ПоддерживаетсяОтправка();
	#Иначе
		Возврат Ложь;
	#КонецЕсли
	
КонецФункции

Функция ДоступноСканированиеШтрихкодов() Экспорт
	
	#Если МобильныйКлиент Тогда
		Возврат СредстваМультимедиа.ПоддерживаетсяСканированиеШтрихКодов();
	#Иначе
		Возврат Ложь;
	#КонецЕсли
	
КонецФункции
	
Процедура ПоказатьФормуНедоступнаяФункциональность(ОписаниеНедоступнойФункции,
		Владелец = Неопределено,
		ОткрытаВнутриОкна = Ложь,
		ПараметрыОткрытияИсходнойФормы = Неопределено) Экспорт
	
	ПараметрыФормыНедоступно = МобильныйКлиентБПКлиентСервер.НовыйПараметрыФормаНедоступноНаМобильном();
	ПараметрыФормыНедоступно.ОписаниеНедоступнойФункции = ОписаниеНедоступнойФункции;
	ПараметрыФормыНедоступно.ОткрытаВнутриОкна = ОткрытаВнутриОкна;
	ПараметрыФормыНедоступно.ФормаДоступнаСПредупреждением = 
		ЗначениеЗаполнено(ПараметрыОткрытияИсходнойФормы);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрыФормыНедоступно", ПараметрыФормыНедоступно);
	
	Если ПараметрыОткрытияИсходнойФормы = Неопределено Тогда
		ОткрытьФорму("ОбщаяФорма.НедоступноНаМобильномКлиенте", ПараметрыФормы, Владелец);
	Иначе
		
		ФормаНедоступности = ОткрытьФорму("ОбщаяФорма.НедоступноНаМобильномКлиенте",
			ПараметрыФормы,
			ПараметрыОткрытияИсходнойФормы.Владелец,
			ПараметрыОткрытияИсходнойФормы.ИмяФормы,
			ПараметрыОткрытияИсходнойФормы.Окно,
			ПараметрыОткрытияИсходнойФормы.НавигационнаяСсылка);
		ФормаНедоступности.ПараметрыОткрытияКлиент = ПараметрыОткрытияИсходнойФормы;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИмяФормыСохраненияПечатнойФормы() Экспорт
	Возврат "ОбщаяФорма.ВыборФорматаВложенийМК";
КонецФункции

&НаКлиенте
Процедура ОтправитьФайлыЧерезПоделиться(СписокФайловВоВременномХранилище) Экспорт
	
	ИмяКаталога = ПутьКВременномуКаталогуДокументов();
	
	СохраняемыеФайлы = Новый Массив;
	
	Для Каждого ФайлДляЗаписи Из СписокФайловВоВременномХранилище Цикл
		ИмяФайла = ФайлДляЗаписи.Представление;
		СохраняемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(ИмяФайла, ФайлДляЗаписи.АдресВоВременномХранилище));
	КонецЦикла;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОтправитьФайлыЧерезПоделитьсяПослеПолученияФайлов", МобильныйКлиентБПКлиент);
	
	ПараметрыПолученияАрхиваФайлов       = Новый ПараметрыПолученияАрхиваФайлов();
	ПараметрыПолученияАрхиваФайлов.Режим = РежимПолученияАрхиваФайлов.ПолучатьАрхивПриНеобходимости;
	
	НачатьПолучениеФайловССервера(ОповещениеОЗавершении,
		СохраняемыеФайлы,
		ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяКаталога),
		ПараметрыПолученияАрхиваФайлов);
	
КонецПроцедуры

Процедура ОтправитьФайлыЧерезПоделитьсяПослеПолученияФайлов(Результат, ДопПараметры) Экспорт
	
	ИменаФайлов = Новый Массив;

	Для каждого ОписаниеФайла Из Результат Цикл
		ИменаФайлов.Добавить(ОписаниеФайла.ПолноеИмя);
	КонецЦикла;
	
	Если ИменаФайлов.Количество() > 0 Тогда
		ПоделитьсяФайлами(ИменаФайлов);
	КонецЕсли;
	
КонецПроцедуры

Функция НачатьСканированиеШтрихКодов(Заголовок, Оповещение) Экспорт
	
	ДополнительныеПараметры = Новый Структура("ОповещениеВнешнее", Оповещение);
	
	ОбработкаСканированияШтрихКода = Новый ОписаниеОповещения("ОбработкаСканированияШтрихКода",
		МобильныйКлиентБПКлиент,
		ДополнительныеПараметры);
	#Если МобильныйКлиент Тогда
		СредстваМультимедиа.ПоказатьСканированиеШтрихКодов(Заголовок, ОбработкаСканированияШтрихКода);
	#КонецЕсли
	
КонецФункции

Функция ОбработкаСканированияШтрихКода(ШтрихКод, Результат, Сообщение, ДополнительныйПараметры) Экспорт
	
	ЗакрытьИнтерфейс = Ложь;
	Если Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныйПараметры.ОповещениеВнешнее, ШтрихКод);
		ЗакрытьИнтерфейс = Истина;
	КонецЕсли;
	
	Если ЗакрытьИнтерфейс Тогда
		#Если МобильныйКлиент Тогда
			СредстваМультимедиа.ЗакрытьСканированиеШтрихКодов();
		#КонецЕсли
	КонецЕсли;
	
КонецФункции

Функция ЭтоМобильныйКлиент() Экспорт
	#Если МобильныйКлиент Тогда
		Возврат Истина;
	#КонецЕсли
	Возврат Ложь;
КонецФункции

Функция ПолучитьВременныйКаталогДокументов() Экспорт
	
	ПутьККаталогу = ПутьКВременномуКаталогуДокументов();
	Попытка
		//Удалим временный каталог и все его содержимое
		УдалитьФайлы(ПутьККаталогу, "*");
	Исключение
		// Если присутствует файл, который доступен только на чтение
	КонецПопытки;
	
	СоздатьКаталог(ПутьККаталогу);
	
	Возврат ПутьККаталогу;
	
КонецФункции

Функция ПутьКВременномуКаталогуДокументов() Экспорт
	
	ПутьКВременномуКаталогу = КаталогДокументов() + "TempMobClientAcc";
	
	Возврат ПутьКВременномуКаталогу;
	
КонецФункции

Процедура ОтправитьНаПочту(ПараметрыПисьма) Экспорт
	
	#Если МобильныйКлиент Тогда
	Если СредстваПочты.ПоддерживаетсяОтправка() Тогда
		Сообщение = Новый ИнтернетПочтовоеСообщение;
		
		ПолучательСообщения = Сообщение.Получатели.Добавить();
		ПолучательСообщения.Адрес = ПараметрыПисьма.Получатель;
		Сообщение.Тексты.Добавить(ПараметрыПисьма.Текст, ТипТекстаПочтовогоСообщения.HTML);
		Сообщение.Тема = ПараметрыПисьма.Тема;
		
		Если ЗначениеЗаполнено(ПараметрыПисьма.Вложения) Тогда
			Для каждого ОписаниеВложения Из ПараметрыПисьма.Вложения Цикл
				Если ТипЗнч(ОписаниеВложения) = Тип("Структура") Тогда
					Сообщение.Вложения.Добавить(ОписаниеВложения.ДвоичныеДанные, ОписаниеВложения.Представление);
				Иначе 
					ВызватьИсключение "Неверный формат вложения";
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		СредстваПочты.Послать(Сообщение);
	Иначе
		Если ЭтоiOS() Тогда
			//При отсуствии учетной записи можно вызвать создание учетной записи и отправку текста через mailto
			//но через mailto нельзя отправить файлы
			Если ЗначениеЗаполнено(ПараметрыПисьма.Вложения) Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Для отправки писем необходимо в настройках iOS добавить ваш почтовый ящик: Настройки-Почта-Учетные записи'"));
			Иначе
				Текст = Текст + Символы.ПС +"mailto";
				ОтправитьСоообщениеПоСхемеURI("", ПараметрыПисьма.Текст, ПараметрыПисьма.Тема, ПараметрыПисьма.Получатель);
			КонецЕсли;
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'При установке приложения не установлено разрешение на работу с почтой. Отправка невозможна'"));
		КонецЕсли;
	КонецЕсли;
	#Иначе
		ОтправитьСоообщениеПоСхемеURI("", ПараметрыПисьма.Текст, ПараметрыПисьма.Тема, ПараметрыПисьма.Получатель);
	#КонецЕсли
	
КонецПроцедуры

Функция ЭтоiOS() Экспорт
	
	СисИнфо = Новый СистемнаяИнформация;
	
	Возврат (СтрНайти(СисИнфо.ВерсияОС, "pple") > 0);
	
КонецФункции

Функция ЭтоAndroid() Экспорт
	
	СисИнфо = Новый СистемнаяИнформация;
	Возврат (СтрНайти(СисИнфо.ВерсияОС, "Android") > 0);
	
КонецФункции

Процедура ОтправитьСоообщение(Приложение, ТекстСообщения, ТемаСообщения = "", ПутьКФайлу = "") Экспорт
	
	Если ЭтоAndroid() Тогда 
		
		ОтправитьСоообщениеАндройд(Приложение, ТекстСообщения, ТемаСообщения, ПутьКФайлу)
		
	Иначе
		
		ОтправитьСоообщениеПоСхемеURI(Приложение, ТекстСообщения, ТемаСообщения);
		
	КонецЕсли;
	
КонецПроцедуры  

Процедура ОтправитьСоообщениеАндройд(Приложение, ТекстСообщения, ТемаСообщения, ПутьКФайлу = "", Получатели = "")
	
	ИдПриложения = "";
	Если Врег(Приложение) =Врег( "WhatsApp") Тогда
		ИдПриложения = "com.whatsapp"
	ИначеЕсли Врег(Приложение) = Врег("Viber") Тогда
		ИдПриложения = "com.viber.voip"
	ИначеЕсли Врег(Приложение) = Врег("Telegram") Тогда
		ИдПриложения = "org.telegram.messenger";
	КонецЕсли;
	
	#Если МобильныйКлиент Тогда
		НовВз = Новый ЗапускПриложенияМобильногоУстройства();
		Если Не НовВз.ПоддерживаетсяЗапуск() Тогда
			ВызватьИсключение "Метод недоступен на устройстве";
		КонецЕсли;
		НовВз.Действие = "android.intent.action.SEND";
		НовВз.Тип = "text/plain";
		Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
			НовВз.ДополнительныеДанные.Добавить("android.intent.extra.STREAM", ПутьКФайлу,"Uri");
		КонецЕсли;
		Если ЗначениеЗаполнено(ИдПриложения) Тогда
			НовВз.Приложение = ИдПриложения;
		КонецЕсли;
		НовВз.ДополнительныеДанные.Добавить("android.intent.extra.SUBJECT", ТемаСообщения);
		НовВз.ДополнительныеДанные.Добавить("android.intent.extra.TEXT",ТекстСообщения);
		НовВз.Запустить(Ложь); 
	#КонецЕсли 
	
КонецПроцедуры

Процедура ОтправитьСоообщениеПоСхемеURI(Приложение, ТекстСообщения, ТемаСообщения = "", Получатели = "")
	
	СтрокаПриложения = "";
	Если Врег(Приложение) =Врег( "WhatsApp") Тогда
		СтрокаПриложения = "whatsapp://send/?text=%1";
	ИначеЕсли Врег(Приложение) = Врег("Viber") Тогда
		СтрокаПриложения = "viber://forward?text=%1";
	ИначеЕсли Врег(Приложение) = Врег("Telegram") Тогда
		СтрокаПриложения = "tg://msg?text=%1";
	Иначе //почта
		СтрокаПриложения = "mailto:%3?subject=%1&body=%2";
	КонецЕсли;
	
	ТекстСообщенияВУРЛ = ТекстСообщения;
	
	Если СтрЧислоВхождений(СтрокаПриложения,"%") = 3 Тогда
		ТемаСообщенияВУРЛ = ТемаСообщения;
		ПолучателиВУРЛ = Получатели;
		
		СтрокаЗапуска = СтрШаблон(СтрокаПриложения,
			ТемаСообщенияВУРЛ, ТекстСообщенияВУРЛ, ПолучателиВУРЛ);
			
	ИначеЕсли СтрЧислоВхождений(СтрокаПриложения,"%") = 1 Тогда
		СтрокаЗапуска = СтрШаблон(СтрокаПриложения,
			ТекстСообщенияВУРЛ);
	Иначе
		ВызватьИсключение "Не поддерживается тип сообщения";
	КонецЕсли;
	
	ЗапуститьПриложение(СтрокаЗапуска);
	
КонецПроцедуры

Процедура ПоделитьсяФайлами(ИменаФайлов) Экспорт
	
	Успешно = Ложь;
	#Если МобильныйКлиент Тогда
	Если СредстваПередачиДанныхНаУстройстве.ПоддерживаетсяВозможностьПоделитьсяДанными() Тогда
		
		ОбъектыПоделиться = Новый Массив;
		
		Для каждого ПолноеИмя Из ИменаФайлов Цикл
			Данные = Новый ДанныеМультимедиа(ПолноеИмя);
			ОбъектыПоделиться.Добавить(Данные);
		КонецЦикла;
		
		Успешно = СредстваПередачиДанныхНаУстройстве.ПоделитьсяДанными(ОбъектыПоделиться, "*/*");
		
		Если Успешно Тогда
			ОткрытьФормуОценки = Ложь;
			МобильныйКлиентБПВызовСервера.ПроверитьИЗарегистроватьСобытиеДляОценки("поделитьсяфайлом", ОткрытьФормуОценки);
			
			Если ОткрытьФормуОценки Тогда
				ОткрытьФорму("ОбщаяФорма.ОценитьПриложение");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	#КонецЕсли

	//Обход для исключительной ситуации
	Если Не Успешно Тогда
		Для каждого ПолноеИмя Из ИменаФайлов Цикл
			ЗапуститьПриложение(ПолноеИмя);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаСОтчетами

Процедура ОткрытьОтчетПродажи(КлючВарианта) Экспорт
	
	ГруппировкаОтчета = Новый Массив;
	Расшифровка = СтруктураРасшифровкиНаСервере(КлючВарианта);
	Если ГруппировкаОтчета <> Неопределено Тогда
		Расшифровка.Вставить("ГруппировкаОтчета", ГруппировкаОтчета);
	КонецЕсли;
	ОткрытьОтчет(Расшифровка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчет(Расшифровка)
	
	Отчет = Расшифровка.ФормаОтчета;
	
	ГруппировкаОтчета = ?(Расшифровка.Свойство("ГруппировкаОтчета"), Расшифровка.ГруппировкаОтчета, Неопределено);
	КлючВарианта      = ?(Расшифровка.Свойство("КлючВарианта"),      Расшифровка.КлючВарианта,      Неопределено);
	Отборы            = ?(Расшифровка.Свойство("Отбор"),             Расшифровка.Отбор,             Неопределено);
	Показатели        = ?(Расшифровка.Свойство("Показатели"),        Расшифровка.Показатели,        Неопределено);
	НачалоПериода     = ?(Расшифровка.Свойство("НачалоПериода"),     Расшифровка.НачалоПериода,     Неопределено);
	КонецПериода      = ?(Расшифровка.Свойство("КонецПериода"),      Расшифровка.КонецПериода,      Неопределено);
	КлючеваяОперация  = ?(Расшифровка.Свойство("КлючеваяОперация"),  Расшифровка.КлючеваяОперация,  Неопределено);
	
	СтандартныеИнтервалы = ?(Расшифровка.Свойство("СтандартныеИнтервалы"),  Расшифровка.СтандартныеИнтервалы,  Истина);

	ПользовательскиеНастройки = ПользовательскиеНастройкиДляРасшифровки(НачалоПериода,
		КонецПериода, СтандартныеИнтервалы);
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	
	Если ГруппировкаОтчета <> Неопределено И ТипЗнч(ГруппировкаОтчета) = Тип("Массив") Тогда
		ДополнительныеСвойства.Вставить("Группировка", ГруппировкаОтчета);
	КонецЕсли;
	
	Если Отборы <> Неопределено И ТипЗнч(Отборы) = Тип("Массив") Тогда
		НастройкаОтбора = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		НастройкаОтбора.ИдентификаторПользовательскойНастройки = "Отбор";
		Для Каждого Отбор Из Отборы Цикл
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(НастройкаОтбора, Отбор.Поле, Отбор.Значение, Отбор.ВидСравнения);
		КонецЦикла;
	КонецЕсли;
	
	Если Показатели <> Неопределено И ТипЗнч(Показатели) = Тип("Массив") Тогда
		
		Для Каждого Показатель Из Показатели Цикл
			ДополнительныеСвойства.Вставить(Показатель, Истина);
		КонецЦикла;
		
	КонецЕсли;
	
	Если КлючВарианта <> Неопределено Тогда
		ДополнительныеСвойства.Вставить("КлючВарианта", КлючВарианта);
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("РежимРасшифровки",          Истина);
	ПараметрыОтчета.Вставить("ВидРасшифровки",            2);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	
	ОткрытьФорму(Отчет, ПараметрыОтчета, ЭтотОбъект, Истина);

КонецПроцедуры

&НаКлиенте
Функция СтруктураРасшифровкиНаСервере(КлючВарианта)
	
	СтруктураРасшифровки = Новый Структура;
	СтруктураРасшифровки.Вставить("СсылкаНаОтчет", "e1cib/app/Отчет.Продажи");
	СтруктураРасшифровки.Вставить("ФормаОтчета",   "Отчет.Продажи.Форма.ФормаОтчета");
	СтруктураРасшифровки.Вставить("КлючВарианта",  );
	
	ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	СтруктураРасшифровки.Вставить("СтандартныеИнтервалы", Истина);
	
	ЭтоПервыйМесяцГода = Месяц(ТекущаяДата) = 1;
	
	СтруктураРасшифровки.Вставить("НачалоПериода",    НачалоМесяца(ТекущаяДата));
	СтруктураРасшифровки.Вставить("КонецПериода",     ТекущаяДата);
	
	Возврат СтруктураРасшифровки;
	
КонецФункции

&НаКлиенте
Функция ПользовательскиеНастройкиДляРасшифровки(
	Знач НачалоПериода = Неопределено, Знач КонецПериода = Неопределено, СтандартныеИнтервалы = Истина)
	
	ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	Если НачалоПериода = Неопределено Тогда
		НачалоПериода = НачалоМесяца(ТекущаяДата);
	КонецЕсли;
	
	// Инициализация пользовательских настроек
	// Добавим в настройки все параметры которые могут использоваться в отчетах руководителю
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("РежимРасшифровки", Истина);
	ДополнительныеСвойства.Вставить("НачалоПериода",    НачалоПериода);
	ДополнительныеСвойства.Вставить("КонецПериода",     КонецПериода);
	ДополнительныеСвойства.Вставить("Период",           ТекущаяДата);
	ДополнительныеСвойства.Вставить("ОчищатьТаблицуГруппировок", Истина);
	
	Возврат ПользовательскиеНастройки;
	
КонецФункции

#КонецОбласти

// Открывает форму, неадаптированную для мобильного экрана
//
// Параметры:
//  ПараметрыОткрытия - Структура - см. ПараметрыОткрытияФормы() .
//  ПараметрыФормы - Структура или Неопределено - параметры, которые нужно передать в тяжелую форму.
//
Процедура ОткрытьОсновнуюФорму(ПараметрыОткрытия, ПараметрыФормы = Неопределено) Экспорт

	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
	
	// Отключаем подписку на событие ОбработкаПолученияФормы(), если она определена для данного объекта.
	ПараметрыФормы.Вставить("ОтключитьПроверкуПриОткрытииФорм", Истина);
	
	ОткрытьФорму(ПараметрыОткрытия.ИмяФормы,
		ПараметрыФормы,
		ПараметрыОткрытия.Владелец,
		ПараметрыОткрытия.Уникальность,
		ПараметрыОткрытия.Окно,
		ПараметрыОткрытия.НавигационнаяСсылка,
		ПараметрыОткрытия.ОписаниеОповещенияОЗакрытии,
		ПараметрыОткрытия.РежимОткрытияОкна);
	
	
КонецПроцедуры

Процедура НачатьОтправкуПисьмаРазработчикам(ТекстВопроса) Экспорт
	
	ПараметрыПисьмаРазработчикам = МобильныйКлиентБПВызовСервера.ПараметрыПисьмаРазработчикам();
	
	НазваниеПриложения = ?(ПараметрыПисьмаРазработчикам.ЭтоБизнесСтарт,
		"1С:БизнесСтарт",
		"1С:Бухгалтерия");
	Тема = СтрШаблон(
		НСтр("ru = 'Отзыв о новом мобильном приложении %1'"),
		НазваниеПриложения);
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = Тема;
	Сообщение = НСтр("ru='%1...
	|
	|
	|
	|
	|==Не удаляйте эту часть письма==
	|%2'");
	ПараметрыПисьма.Текст = СтрШаблон(Сообщение,
		ТекстВопроса,
		ПараметрыПисьмаРазработчикам.ТекстТехническойИнформации);
	
	Если ПараметрыПисьмаРазработчикам.ЕстьДоступныеУчетныеЗаписиДляОтправки Тогда
		РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	Иначе
		МобильныйКлиентБПКлиент.ОтправитьНаПочту(ПараметрыПисьма);
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает параметры для создания взаимодействия Электронное письмо исходящее,
// если создание письма происходит из формы контрагента или лида.
//
// Параметры:
//  КонтактнаяИнформация - Структура:
//       * Вид - СправочникСсылка.ВидыКонтактнойИнформации - Вид контактной информации
//       * Значение - Строка - параметры контактной информации в формате JSON
//       * Представление - Строка - представление контактной информации
//       * Тип - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации Адрес электронной почты
//  ДополнительныеПараметры - Структура:
//       * АсинхронныйВызов - Булево - тип вызова
//       * ВладелецКонтактнойИнформации - ОпределяемыйТип.ВладелецКонтактнойИнформации.
//       * ИмяРеквизита - Строка - имя реквизита на форме
//       * Форма - ФормаКлиентскогоПриложения - форма из которой была вызвана команда
// 
Процедура ПодготовитьПараметрыИНаписатьПисьмо(КонтактнаяИнформация, ДополнительныеПараметры) Экспорт

	Получатель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"%1 <%2>", Строка(ДополнительныеПараметры.ВладелецКонтактнойИнформации), КонтактнаяИнформация.Представление);
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Получатель", Получатель);
	
	Если ДоступенМобильныйПочтовыйКлиент() Тогда
		ОтправкаПочтовыхСообщенийКлиент.ОтправитьНовоеПисьмоМК(ПараметрыПисьма);
	Иначе
		ПараметрыПисьма = УправлениеКонтактнойИнформациейКлиент.ПараметрыПисьмаSMS();
		ПараметрыПисьма.Представление = КонтактнаяИнформация.Представление;
		ПараметрыПисьма.ОжидаемыйВид = КонтактнаяИнформация.Тип;
		ПараметрыПисьма.ИсточникКонтактнойИнформации = ДополнительныеПараметры.ВладелецКонтактнойИнформации;
		ПараметрыПисьма.ИмяРеквизита = ДополнительныеПараметры.ИмяРеквизита;
		УправлениеКонтактнойИнформациейКлиент.СоздатьЭлектронноеПисьмо("", ПараметрыПисьма);
	КонецЕсли;

КонецПроцедуры

// Формирует письмо по переданным параметрам. По мотивам функции РаботаСПочтовымиСообщениями.ПодготовитьПисьмо
//
// Параметры:
//  ПараметрыПисьма - Структура - содержит всю необходимую информацию о письме:
//
//   * Кому - Массив
//          - Строка - интернет адреса получателей письма.
//          - Массив - коллекция структур адресов:
//              * Адрес         - Строка - почтовый адрес (должно быть обязательно заполнено).
//              * Представление - Строка - имя адресата.
//          - Строка - интернет-адреса получателей письма, разделитель - ";".
//
//   * ПолучателиСообщения - Массив - массив структур, описывающий получателей:
//      ** Адрес - Строка - почтовый адрес получателя сообщения.
//      ** Представление - Строка - представление адресата.
//
//   * Копии        - Массив
//                  - Строка - адреса получателей копий письма. См. описание поля Кому.
//
//   * СкрытыеКопии - Массив
//                  - Строка - адреса получателей скрытых копий письма. См. описание поля Кому.
//
//   * Тема       - Строка - (обязательный) тема почтового сообщения.
//   * Тело       - Строка - (обязательный) текст почтового сообщения (простой текст в кодировке win-1251).
//   * Важность   - ВажностьИнтернетПочтовогоСообщения
//
//   * Вложения - Массив - файлы, которые необходимо приложить к письму (описания в виде структур):
//     ** Представление - Строка - имя файла вложения;
//     ** АдресВоВременномХранилище - Строка - адрес двоичных данных вложения во временном хранилище.
//     ** Кодировка - Строка - кодировка вложения (используется, если отличается от кодировки письма).
//     ** Идентификатор - Строка - (необязательный) используется для отметки картинок, отображаемых в теле письма.
//
//   * АдресОтвета - Соответствие
//                 - Строка - см. описание поля Кому.
//   * ИдентификаторыОснований - Строка - идентификаторы оснований данного письма.
//   * ОбрабатыватьТексты  - Булево - необходимость обрабатывать тексты письма при отправке.
//   * УведомитьОДоставке  - Булево - необходимость запроса уведомления о доставке.
//   * УведомитьОПрочтении - Булево - необходимость запроса уведомления о прочтении.
//   * ТипТекста   - Строка
//                 - ПеречислениеСсылка.ТипыТекстовЭлектронныхПисем
//                 - ТипТекстаПочтовогоСообщения - определяет тип
//                  переданного теста допустимые значения:
//                  HTML/ТипыТекстовЭлектронныхПисем.HTML - текст почтового сообщения в формате HTML.
//                  ПростойТекст/ТипыТекстовЭлектронныхПисем.ПростойТекст - простой текст почтового сообщения.
//                                                 Отображается "как есть" (значение по
//                                                 умолчанию).
//                  РазмеченныйТекст/ТипыТекстовЭлектронныхПисем.РазмеченныйТекст - текст почтового сообщения в формате
//                                                 Rich Text.
//
// Возвращаемое значение:
//  ИнтернетПочтовоеСообщение - подготовленное письмо.
//
Функция ПодготовитьПисьмо(ПараметрыПисьма) Экспорт
	
	ТипЗнчКому = ?(ПараметрыПисьма.Свойство("Кому"), ТипЗнч(ПараметрыПисьма.Кому), Неопределено);
	ТипЗнчКопии = ?(ПараметрыПисьма.Свойство("Копии"), ТипЗнч(ПараметрыПисьма.Копии), Неопределено);
	СкрытыеКопии = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПисьма, "СкрытыеКопии");
	
	Если ТипЗнчКому = Неопределено И ТипЗнчКопии = Неопределено И СкрытыеКопии = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не указано ни одного получателя.'");
	КонецЕсли;
	
	Если ТипЗнчКому = Тип("Строка") Тогда
		ПараметрыПисьма.Кому = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(ПараметрыПисьма.Кому);
	ИначеЕсли ТипЗнчКому <> Тип("Массив") Тогда
		ПараметрыПисьма.Вставить("Кому", Новый Массив);
	КонецЕсли;
	
	Если ТипЗнчКопии = Тип("Строка") Тогда
		ПараметрыПисьма.Копии = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(ПараметрыПисьма.Копии);
	ИначеЕсли ТипЗнчКопии <> Тип("Массив") Тогда
		ПараметрыПисьма.Вставить("Копии", Новый Массив);
	КонецЕсли;
	
	Если ТипЗнч(СкрытыеКопии) = Тип("Строка") Тогда
		ПараметрыПисьма.СкрытыеКопии = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(СкрытыеКопии);
	ИначеЕсли ТипЗнч(СкрытыеКопии) <> Тип("Массив") Тогда
		ПараметрыПисьма.Вставить("СкрытыеКопии", Новый Массив);
	КонецЕсли;
	
	Если ПараметрыПисьма.Свойство("АдресОтвета") И ТипЗнч(ПараметрыПисьма.АдресОтвета) = Тип("Строка") Тогда
		ПараметрыПисьма.АдресОтвета = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(ПараметрыПисьма.АдресОтвета);
	КонецЕсли;
	
	Возврат НовоеИнтернетПочтовоеСообщение(ПараметрыПисьма);
	
КонецФункции

//Создает новое письмо и заполняет по параметрам сообщения.
//По мотивам РаботаСПочтовымиСообщениямиСлужебный.ПодготовитьПисьмо
Функция НовоеИнтернетПочтовоеСообщение(ПараметрыПисьма)
	
	Письмо = Новый ИнтернетПочтовоеСообщение;
	
	Если ПараметрыПисьма.Свойство("Тема") Тогда
		Письмо.Тема = ПараметрыПисьма.Тема;
	КонецЕсли;
	
	Кому = ПараметрыПисьма.Кому;
	Если ТипЗнч(Кому) = Тип("Строка") Тогда
		Кому = ОбщегоНазначенияКлиентСервер.РазобратьСтрокуСПочтовымиАдресами(Кому);
	КонецЕсли;
	Для Каждого ПочтовыйАдресПолучателя Из Кому Цикл
		Получатель = Письмо.Получатели.Добавить(ПочтовыйАдресПолучателя.Адрес);
		Получатель.ОтображаемоеИмя = ПочтовыйАдресПолучателя.Представление;
	КонецЦикла;
	
	Если ПараметрыПисьма.Свойство("Копии") Тогда
		Для Каждого ПочтовыйАдресПолучателяКопии Из ПараметрыПисьма.Копии Цикл
			Получатель = Письмо.Копии.Добавить(ПочтовыйАдресПолучателяКопии.Адрес);
			Получатель.ОтображаемоеИмя = ПочтовыйАдресПолучателяКопии.Представление;
		КонецЦикла;
	КонецЕсли;
	
	Если ПараметрыПисьма.Свойство("СкрытыеКопии") Тогда
		Для Каждого СведенияОПолучателе Из ПараметрыПисьма.СкрытыеКопии Цикл
			Получатель = Письмо.СлепыеКопии.Добавить(СведенияОПолучателе.Адрес);
			Получатель.ОтображаемоеИмя = СведенияОПолучателе.Представление;
		КонецЦикла;
	КонецЕсли;
	
	Если ПараметрыПисьма.Свойство("АдресОтвета") Тогда
		Для Каждого ПочтовыйАдресОтвета Из ПараметрыПисьма.АдресОтвета Цикл
			ПочтовыйАдресОбратный = Письмо.ОбратныйАдрес.Добавить(ПочтовыйАдресОтвета.Адрес);
			ПочтовыйАдресОбратный.ОтображаемоеИмя = ПочтовыйАдресОтвета.Представление;
		КонецЦикла;
	КонецЕсли;
	
	Вложения = Неопределено;
	ПараметрыПисьма.Свойство("Вложения", Вложения);
	Если Вложения <> Неопределено Тогда
		Для Каждого Вложение Из Вложения Цикл
			Если ТипЗнч(Вложение) = Тип("Структура") Тогда
				ДанныеФайла = Неопределено;
				Если ЭтоАдресВременногоХранилища(Вложение.АдресВоВременномХранилище) Тогда
					ДанныеФайла = ПолучитьИзВременногоХранилища(Вложение.АдресВоВременномХранилище);
				Иначе
					ДанныеФайла = Вложение.АдресВоВременномХранилище;
				КонецЕсли;
				НовоеВложение = Письмо.Вложения.Добавить(ДанныеФайла, Вложение.Представление);
				Если Вложение.Свойство("Кодировка") И Не ПустаяСтрока(Вложение.Кодировка) Тогда
					НовоеВложение.Кодировка = Вложение.Кодировка;
				КонецЕсли;
				Если Вложение.Свойство("Идентификатор") Тогда
					НовоеВложение.Идентификатор = Вложение.Идентификатор;
				КонецЕсли;
			Иначе 
				ВызватьИсключение "Формат 2.2.1 не поддерживается";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Вложение Из Письмо.Вложения Цикл
		Если Не ЗначениеЗаполнено(Вложение.ТипСодержимого) Тогда
			ТипСодержимого = ОпределитьТипСодержимогоПоИмениФайла(Вложение.Имя);
			Если ЗначениеЗаполнено(ТипСодержимого) Тогда
				Вложение.ТипСодержимого = ТипСодержимого;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	#Если МобильныйКлиент Тогда
	Если ПараметрыПисьма.Свойство("ИдентификаторыОснований") Тогда
		Письмо.УстановитьПолеЗаголовка("References", 
		                               ПараметрыПисьма.ИдентификаторыОснований, 
		                               СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования);
	КонецЕсли;
	
	Если ПараметрыПисьма.Свойство("ИдентификаторОснования") Тогда
		Письмо.УстановитьПолеЗаголовка("In-Reply-To", 
		                               ПараметрыПисьма.ИдентификаторОснования, 
		                               СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.БезКодирования);
	КонецЕсли;
	#КонецЕсли
	
	Тело = "";
	ПараметрыПисьма.Свойство("Тело", Тело);
	
	ТипТекста = Неопределено;
	Если ТипЗнч(Тело) = Тип("ФорматированныйДокумент") Тогда
		СодержимоеПисьма = ПолучитьHTMLФорматированногоДокументаДляПисьма(Тело);
		Тело = СодержимоеПисьма.ТекстHTML;
		Картинки = СодержимоеПисьма.Картинки;
		#Если МобильныйКлиент Тогда
		ТипТекста = ТипТекстаПочтовогоСообщения.HTML;
		#КонецЕсли
		
		Для Каждого Картинка Из Картинки Цикл
			ИмяКартинки = Картинка.Ключ;
			ДанныеКартинки = Картинка.Значение;
			Вложение = Письмо.Вложения.Добавить(ДанныеКартинки.ПолучитьДвоичныеДанные(), ИмяКартинки);
			Вложение.Идентификатор = ИмяКартинки;
		КонецЦикла;
	КонецЕсли;
	Текст = Письмо.Тексты.Добавить(Тело);
	Если ЗначениеЗаполнено(ТипТекста) Тогда
		Текст.ТипТекста = ТипТекста;
	КонецЕсли;
	
	#Если МобильныйКлиент Тогда
	Если ТипТекста = Неопределено Тогда
		Если ПараметрыПисьма.Свойство("ТипТекста", ТипТекста) Тогда
			Если ТипЗнч(ТипТекста) = Тип("Строка") Тогда
				Если      ТипТекста = "HTML" Тогда
					Текст.ТипТекста = ТипТекстаПочтовогоСообщения.HTML;
				ИначеЕсли ТипТекста = "RichText" Тогда
					Текст.ТипТекста = ТипТекстаПочтовогоСообщения.РазмеченныйТекст;
				Иначе
					Текст.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст;
				КонецЕсли;
			ИначеЕсли ТипЗнч(ТипТекста) = Тип("ПеречислениеСсылка.ТипыТекстовЭлектронныхПисем") Тогда
				Если      ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.HTML")
					ИЛИ ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.HTMLСКартинками") Тогда
					Текст.ТипТекста = ТипТекстаПочтовогоСообщения.HTML;
				ИначеЕсли ТипТекста = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.РазмеченныйТекст") Тогда
					Текст.ТипТекста = ТипТекстаПочтовогоСообщения.РазмеченныйТекст;
				Иначе
					Текст.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст;
				КонецЕсли;
			Иначе
				Текст.ТипТекста = ТипТекста;
			КонецЕсли;
		Иначе
			Текст.ТипТекста = ТипТекстаПочтовогоСообщения.ПростойТекст;
		КонецЕсли;
	КонецЕсли;
	#КонецЕсли
	
	Важность = Неопределено;
	Если ПараметрыПисьма.Свойство("Важность", Важность) Тогда
		Письмо.Важность = Важность;
	КонецЕсли;
	
	Кодировка = Неопределено;
	Если ПараметрыПисьма.Свойство("Кодировка", Кодировка) Тогда
		Письмо.Кодировка = Кодировка;
	КонецЕсли;
	
	Если ПараметрыПисьма.Свойство("УведомитьОДоставке") Тогда
		Письмо.УведомитьОДоставке = ПараметрыПисьма.УведомитьОДоставке;
	КонецЕсли;
	
	Если Не ПараметрыПисьма.Свойство("ОбрабатыватьТексты") Или ПараметрыПисьма.ОбрабатыватьТексты Тогда
		Письмо.ОбработатьТексты();
	КонецЕсли;
	
	Возврат Письмо;
	
КонецФункции

Функция ПолучитьHTMLФорматированногоДокументаДляПисьма(ФорматированныйДокумент)
	
	// Выгрузка форматированного документа в HTML текст и картинки.
	ТекстHTML = "";
	Картинки = Новый Структура;
	ФорматированныйДокумент.ПолучитьHTML(ТекстHTML, Картинки);
	
	// Конвертация HTML текста в ДокументHTML.
	Построитель = Новый ПостроительDOM;
	ЧтениеHTML = Новый ЧтениеHTML;
	ЧтениеHTML.УстановитьСтроку(ТекстHTML);
	ДокументHTML = Построитель.Прочитать(ЧтениеHTML);
	
	// Замена имен картинок в документе HTML на идентификаторы.
	Для Каждого Картинка Из ДокументHTML.Картинки Цикл
		АтрибутИсточникКартинки = Картинка.Атрибуты.ПолучитьИменованныйЭлемент("src");
		Если СтрНачинаетсяС(АтрибутИсточникКартинки.ТекстовоеСодержимое, "data:") Тогда
			Продолжить;
		КонецЕсли;
		Картинка.УстановитьАтрибут("alt", АтрибутИсточникКартинки.ТекстовоеСодержимое);
		АтрибутИсточникКартинки.ТекстовоеСодержимое = "cid:" + АтрибутИсточникКартинки.ТекстовоеСодержимое;
	КонецЦикла;
	
	// Конвертация ДокументHTML обратно в текст HTML.
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	ТекстHTML = ЗаписьHTML.Закрыть();
	
	// Подготовка результата.
	Результат = Новый Структура;
	Результат.Вставить("ТекстHTML", ТекстHTML);
	Результат.Вставить("Картинки", Картинки);
	
	Возврат Результат;
	
КонецФункции

Функция ОпределитьТипСодержимогоПоИмениФайла(ИмяФайла)
	Расширение = "";
	Позиция = СтрНайти(ИмяФайла, ".", НаправлениеПоиска.СКонца);
	Если Позиция > 0 Тогда
		Расширение = НРег(Сред(ИмяФайла, Позиция + 1));
	КонецЕсли;
	Возврат ТипыСодержимого()[Расширение];
КонецФункции

Функция ТипыСодержимого()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("json", "application/json");
	Результат.Вставить("pdf", "application/pdf");
	Результат.Вставить("xhtml", "application/xhtml+xml");
	Результат.Вставить("zip", "application/zip");
	Результат.Вставить("gzip", "application/gzip");
	
	Результат.Вставить("aac", "audio/aac");
	Результат.Вставить("ogg", "audio/ogg");
	Результат.Вставить("wma", "audio/x-ms-wma");
	Результат.Вставить("wav", "audio/vnd.wave");
	
	Результат.Вставить("gif", "image/gif");
	Результат.Вставить("jpeg", "image/jpeg");
	Результат.Вставить("png", "image/png");
	Результат.Вставить("svg", "image/svg");
	Результат.Вставить("tiff", "image/tiff");
	Результат.Вставить("ico", "image/vnd.microsoft.icon");
	
	Результат.Вставить("html", "text/html");
	Результат.Вставить("txt", "text/plain");
	Результат.Вставить("xml", "text/xml");
	
	Результат.Вставить("mpeg", "video/mpeg");
	Результат.Вставить("mp4", "video/mp4");
	Результат.Вставить("mov", "video/quicktime");
	Результат.Вставить("wmv", "video/x-ms-wmv");
	Результат.Вставить("flv", "video/x-flv");
	Результат.Вставить("3gpp", "video/3gpp");
	Результат.Вставить("3gp", "video/3gpp");
	Результат.Вставить("3gpp2", "video/3gpp2");
	Результат.Вставить("3g2", "video/3gpp2");
	
	Результат.Вставить("odt", "application/vnd.oasis.opendocument.text");
	Результат.Вставить("ods", "application/vnd.oasis.opendocument.spreadsheet");
	Результат.Вставить("odp", "application/vnd.oasis.opendocument.presentation");
	Результат.Вставить("odg", "application/vnd.oasis.opendocument.graphics");
	
	Результат.Вставить("doc", "application/msword");
	Результат.Вставить("docx", "application/vnd.openxmlformats-officedocument.wordprocessingml.document");
	Результат.Вставить("xls", "application/vnd.ms-excel");
	Результат.Вставить("xlsx", "application/vnd.openxmlformats-officedocument.spreadsheetml.sheet");
	Результат.Вставить("ppt", "application/vnd.ms-powerpoint");
	Результат.Вставить("pptx", "application/vnd.openxmlformats-officedocument.presentationml.presentation");
	
	Результат.Вставить("rar", "application/x-rar-compressed");
	
	Результат.Вставить("p7m", "application/x-pkcs7-mime");
	Результат.Вставить("p7s", "application/x-pkcs7-signature");
	
	Возврат Результат;
КонецФункции

#КонецОбласти
