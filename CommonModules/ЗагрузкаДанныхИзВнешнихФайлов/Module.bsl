
#Область ПрограммныйИнтерфейс

// Считывает файл с данными и возвращает табличный документ, содержащий считанные данные и строку для сопоставления реквизитов.
//
// Параметры:
//   ПараметрыЗагрузки - Структура - структура с ключами:
//      * ХранилищеДанныхФайла - ХранилищеЗначения - двоичные данные файла, упакованные в хранилище значений.
//      * РасширениеФайла - Строка - расширение файла.
//      * ОписаниеКолонок - ТаблицаЗначений - описание загружаемых колонок, см. НовыйОписаниеЗагружаемыхКолонок().
//      * МакетЗаголовка - ТабличныйДокумент - Макет заголовка.
//
// Возвращаемое значение:
//   ТабличныйДокумент - табличный документ, содержащий данные из файла.
//
Функция ЗагрузитьФайлВТабличныйДокумент(ПараметрыЗагрузки) Экспорт
	Перем Разделитель, Кодировка;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ДвоичныеДанныеФайла = ПараметрыЗагрузки.ХранилищеДанныхФайла.Получить();
	Если ДвоичныеДанныеФайла = Неопределено Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	МакетЗаголовка = ПараметрыЗагрузки.МакетЗаголовка;
	РасширениеФайла = ПараметрыЗагрузки.РасширениеФайла;
	ОписаниеКолонок = ПараметрыЗагрузки.ОписаниеКолонок;
	Если ПараметрыЗагрузки.Свойство("Разделитель") Тогда
		Разделитель = ПараметрыЗагрузки.Разделитель;
	КонецЕсли;
	Если ПараметрыЗагрузки.Свойство("Разделитель") Тогда
		Кодировка = ПараметрыЗагрузки.Кодировка;
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
	ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);
	
	Если РасширениеФайла = "csv" Тогда // Платформа не умеет читать файлы csv в табличный документ.
		ЗагрузитьCSVФайлВТабличныйДокумент(ТабличныйДокумент, ИмяВременногоФайла, Разделитель, Кодировка);
	Иначе
		ДанныеФайла = Новый ТабличныйДокумент;
		
		Попытка
			ДанныеФайла.Прочитать(ИмяВременногоФайла);
		Исключение
			ВызватьИсключение НСтр("ru = 'Ошибка чтения файла. Формат файла не поддерживается.'");
		КонецПопытки;
		
		Если ПараметрыЗагрузки.Свойство("ПараметрыПрикладнойЗагрузки")
			И ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.Свойство("ТолькоПоследнийЛист")
			И ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ТолькоПоследнийЛист 
			И ДанныеФайла.Области.Количество() > 1 Тогда
			ОбластиКУдалению = Новый Массив;
			// Все кроме последней области подготовим к удалению
			Для НомерОбласти = 1 По ДанныеФайла.Области.Количество() - 1 Цикл
				ОбластиКУдалению.Добавить(ДанныеФайла.Области[НомерОбласти - 1]);
			КонецЦикла;
			// Удаляем области
			Для Каждого ОбластьКУдалению Из ОбластиКУдалению Цикл
				ДанныеФайла.УдалитьОбласть(ОбластьКУдалению);
			КонецЦикла;
		КонецЕсли;
		
		Если ПараметрыЗагрузки.Свойство("ПараметрыПрикладнойЗагрузки")
			И ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.Свойство("ТолькоТаблицу")
			И ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ТолькоТаблицу Тогда
			НомерОбластиШапки = 1;
			ЕстьШапка = Не ЭтоТаблица(ДанныеФайла, НомерОбластиШапки);
			Если ЕстьШапка Тогда
				НомерОбластиШапки = 2;
				Пока Не ЭтоТаблица(ДанныеФайла, НомерОбластиШапки) Цикл
					НомерОбластиШапки = НомерОбластиШапки + 1;
					Если НомерОбластиШапки >= ДанныеФайла.ВысотаТаблицы - 1 Тогда
						// Выходим из цикла, если файл закончен
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			ЕстьПодвал = Ложь;
			Для НомерОбласти = НомерОбластиШапки По ДанныеФайла.ВысотаТаблицы Цикл
				Если Не ЭтоТаблица(ДанныеФайла, НомерОбласти) Тогда
					ЕстьПодвал = Истина;
					// Номер области сейчас указывает на последнюю строку таблицы
					// Подвал начинается со следующей строки
					НомерОбластиПодвала = НомерОбласти + 1;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			// Удаляем области снизу вверх
			Если ЕстьПодвал И НомерОбластиПодвала <= ДанныеФайла.ВысотаТаблицы Тогда
				ОбластьПодвала = ДанныеФайла.Область(НомерОбластиПодвала, , ДанныеФайла.ВысотаТаблицы);
				ДанныеФайла.УдалитьОбласть(ОбластьПодвала, ТипСмещенияТабличногоДокумента.ПоВертикали);
			КонецЕсли;
			Если ЕстьШапка Тогда
				ОбластьШапки = ДанныеФайла.Область(1, , НомерОбластиШапки - 1);
				ДанныеФайла.УдалитьОбласть(ОбластьШапки, ТипСмещенияТабличногоДокумента.ПоВертикали);
			КонецЕсли;
		КонецЕсли;
		
		// Разгруппируем все объединенные ячейки. Работа с такими ячейками не поддерживается.
		ДанныеФайла.Область().Разъединить();
		
		// Скопируем значения ячеек, чтобы избавиться от форматирования исходного файла.
		Для НомерКолонки = 1 По ДанныеФайла.ШиринаТаблицы Цикл
			Для НомерСтроки = 1 По ДанныеФайла.ВысотаТаблицы Цикл
				ТабличныйДокумент.Область(НомерСтроки, НомерКолонки).Текст = ДанныеФайла.Область(НомерСтроки, НомерКолонки).Текст;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	УдалитьВременныйФайл(ИмяВременногоФайла);
	
	УдалитьПустыеКолонки(ТабличныйДокумент);
	УдалитьПустыеСтроки(ТабличныйДокумент);
	
	ПрорисоватьГраницыЯчеек(ТабличныйДокумент);
	
	// Раздвинем первую строку табличного документа. В первой строке всегда выводим заголовки с выбором реквизитов.
	ПерваяСтрока = ТабличныйДокумент.Область(1, , 1);
	ТабличныйДокумент.ВставитьОбласть(ПерваяСтрока, , ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	ТекстНезаполненногоЗаголовка = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.ТекстЗаголовкаНесопоставленнойКолонки();
	
	// Варианты заголовков в описании колонок приведем к нижнему регистру
	// для удобного сравнения с заголовками колонок в табличном файле.
	Для Каждого ОписаниеКолонки Из ОписаниеКолонок Цикл
		Для Индекс = 0 По ОписаниеКолонки.ПодходящиеЗаголовкиФайла.ВГраница() Цикл
			Элемент = ОписаниеКолонки.ПодходящиеЗаголовкиФайла[Индекс];
			ОписаниеКолонки.ПодходящиеЗаголовкиФайла[Индекс] = НРег(Элемент);
		КонецЦикла;
	КонецЦикла;
	
	// Попытаемся сопоставить заголовки таблицы файла с колонками, обрабатываемыми при загрузке.
	Заголовок = МакетЗаголовка.ПолучитьОбласть("Заголовок");
	СопоставленныеКолонки = Новый Массив;
	Для НомерКолонки = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
		
		ТекущаяОбласть = ТабличныйДокумент.Область(2, НомерКолонки);
		ТекущаяОбласть.ПараметрРасшифровки = "НеЗагружать";
		ТекущаяОбласть.ЦветФона = Заголовок.ТекущаяОбласть.ЦветФона;
		
		ТекстЗаголовка = "";
		ЦветТекста = ЦветаСтиля.ЦветГиперссылки;
		Идентификатор = "";
		
		НайденнаяКолонка = НайтиПодходящуюКолонку(ТекущаяОбласть.Текст, ОписаниеКолонок, СопоставленныеКолонки);
		Если ПустаяСтрока(НайденнаяКолонка.Заголовок) Тогда
			ТекстЗаголовка = ТекстНезаполненногоЗаголовка;
			ЦветТекста = ЦветаСтиля.НезаполненныйРеквизит;
		Иначе
			ТекстЗаголовка = НайденнаяКолонка.Заголовок;
			Идентификатор = НайденнаяКолонка.Идентификатор;
		КонецЕсли;
		
		ИмяПараметра = "ИмяРеквизита" + НомерКолонки;
		ЯчейкаЗаголовка = ТабличныйДокумент.Область(1, НомерКолонки);
		ТабличныйДокумент.ВставитьОбласть(Заголовок.ТекущаяОбласть, ЯчейкаЗаголовка, ТипСмещенияТабличногоДокумента.БезСмещения, Истина);
		
		ЯчейкаЗаголовка.Текст = ТекстЗаголовка;
		ЯчейкаЗаголовка.ПараметрРасшифровки = Идентификатор;
		ЯчейкаЗаголовка.ЦветТекста = ЦветТекста;
		
	КонецЦикла;
	
	// Оформляем полученный табличный документ.
	Область = ТабличныйДокумент.Область(1, 1, ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы);
	Область.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Обрезать;
	Область.ШиринаКолонки    = 20;
	Область.Отступ           = 1;
	
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	Область.Обвести(Линия, Линия, Линия, Линия);
	
	ТабличныйДокумент.ФиксацияСверху = 1;
	ТабличныйДокумент.ФиксацияСлева = 0;
	
	// Особая высота для первой строки.
	ПерваяСтрока = ТабличныйДокумент.Область(1, , 1);
	ПерваяСтрока.ВысотаСтроки = Заголовок.ТекущаяОбласть.ВысотаСтроки;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Удаляет строки и колонки, не содержащие полезной информации для загрузки.
//
// Параметры:
//   ТабличныйДокумент - ТабличныйДокумент - табличный документ для обработки.
//   НайденыНенужныеСтроки - Булево - Возвращаемый. Хранит признак наличия ненужных строк.
//
Процедура УдалитьВсеНенужныеСтрокиТаблицы(ТабличныйДокумент, НайденыНенужныеСтроки) Экспорт
	
	ИнформацияОТаблице = АнализДанных(ТабличныйДокумент);
	ИнформацияОЯчейках = ИнформацияОТаблице.ИнформацияОЯчейках;
	ИнформацияОЯчейкахКЗагрузке = ИнформацияОТаблице.ИнформацияОЯчейкахКЗагрузке;
	ВГраница = ИнформацияОЯчейках.ВГраница();
	ШиринаТаблицы = ТабличныйДокумент.ШиринаТаблицы;
	Для Н = 0 По ВГраница Цикл
		
		Индекс = ВГраница - Н;
		ЭлементМассива = ИнформацияОЯчейках[Индекс];
		Если ИнформацияОЯчейкахКЗагрузке.Найти(ЭлементМассива) = Неопределено Тогда
			
			НомерСтроки = Индекс + 2;
			Область = ТабличныйДокумент.Область(НомерСтроки,, НомерСтроки);
			ТабличныйДокумент.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоВертикали);
			НайденыНенужныеСтроки = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НайденыНенужныеСтроки Тогда
		
		УдалитьПустыеКолонки(ТабличныйДокумент);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает соответствия пользовательских наименований колонок и колонок загружаемых данных из ОписаниеКолонок.
//
// Параметры:
//   ТабличныйДокумент - ТабличныйДокумент - проверяемый табличный документ.
//   ОписаниеКолонок - ДанныеФормыКоллекция - таблица значений, содержащая описание загружаемых колонок.
//                                            См. ЗагрузкаДанныхИзВнешнихФайлов.НовыйОписаниеЗагружаемыхКолонок()
//
// Возвращаемое значение:
//   Структура - Соответствие колонок загружаемых данных пользовательским наименованиям.
//
Функция СоответствиеИменКолонокЗагружаемымДанным(ТабличныйДокумент, ОписаниеКолонок) Экспорт
	
	СоответствиеИменКолонок = Новый Структура;
	
	ШиринаТаблицы = ТабличныйДокумент.ШиринаТаблицы;
	
	Для НомерКолонки = 1 по ШиринаТаблицы Цикл
		
		// Считаем, что первая строка - это отрисованная нами строка с наименованиями загружаемых данных.
		// Вторая строка - это пользовательский заголовок колонок, у нее в параметре расшифровки текст НеЗагружать.
		ЯчейкаЗаголовка = ТабличныйДокумент.Область("R1C" + НомерКолонки);
		ЯчейкаПользовательскогоЗаголовка = ТабличныйДокумент.Область("R2C" + НомерКолонки);
		Если ЯчейкаПользовательскогоЗаголовка.ПараметрРасшифровки <> "НеЗагружать" Тогда
			Продолжить;
		КонецЕсли;
		
		НайденныеКолонки = ОписаниеКолонок.НайтиСтроки(Новый Структура("Идентификатор", ЯчейкаЗаголовка.ПараметрРасшифровки));
		Если НайденныеКолонки.Количество() > 0 Тогда
			Колонка = НайденныеКолонки[0];
			СоответствиеИменКолонок.Вставить(Колонка.Идентификатор, ЯчейкаПользовательскогоЗаголовка.Текст)
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СоответствиеИменКолонок;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает таблицу для заполнения информации о загружаемых колонках.
//
// Возвращаемое значение:
//   ТаблицаЗначений - пустая таблица для описания загружаемых колонок.
Функция НовыйОписаниеЗагружаемыхКолонок() Экспорт
	
	ТипСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(0);
	
	ОписаниеКолонок = Новый ТаблицаЗначений;
	ОписаниеКолонок.Колонки.Добавить("Идентификатор", ТипСтрока);
	ОписаниеКолонок.Колонки.Добавить("ПредставлениеКолонки", ТипСтрока);
	ОписаниеКолонок.Колонки.Добавить("ПодходящиеЗаголовкиФайла", Новый ОписаниеТипов("Массив"));
	ОписаниеКолонок.Колонки.Добавить("ОбязательнаДляЗаполнения", Новый ОписаниеТипов("Булево"));
	ОписаниеКолонок.Колонки.Добавить("Родитель", ТипСтрока);
	ОписаниеКолонок.Колонки.Добавить("Тип", Неопределено);
	ОписаниеКолонок.Колонки.Добавить("ЭтоСсылочныйТип", Новый ОписаниеТипов("Булево"));
	
	Возврат ОписаниеКолонок;
	
КонецФункции

// Имя события для записи в журнал регистрации.
//
Функция СобытиеЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'Загрузка данных из файла'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Процедура ЗагрузитьCSVФайлВТабличныйДокумент(ТабличныйДокумент, ИмяФайла, СимволРазделитель, Кодировка)
	
	Если ЗначениеЗаполнено(Кодировка) Тогда
		ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, Кодировка);
	Иначе
		ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла);
	КонецЕсли;
	
	РазделительСтрок = Символы.ПС;
	Строка = ЧтениеТекста.ПрочитатьСтроку(РазделительСтрок);
	
	Если Строка = Неопределено Тогда 
		ТекстСообщения = НСтр("ru = 'Не получилось загрузить данные из этого файла. Убедитесь в корректности данных в файле.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	// В файле вместо стандартного символа переноса строки может использоваться символ возврата каретки.
	Если СтрНайти(Строка, Символы.ВК) > 0 Тогда
		Если ПустаяСтрока(Кодировка) Тогда
			ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла);
		Иначе
			ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, Кодировка);
		КонецЕсли;
		
		РазделительСтрок = Символы.ВК;
		Строка = ЧтениеТекста.ПрочитатьСтроку(РазделительСтрок);
	КонецЕсли;
	
	НомерСтроки = 1;
	Если СимволРазделитель = Неопределено Тогда
		СимволРазделитель = ПолучитьСимволРазделитель(Строка); // по строке с заголовками колонок получим символ разделитель
	КонецЕсли;
	
	Пока Строка <> Неопределено Цикл
		
		НомерКолонки = 1;
		ЗначенияКолонок = СтрРазделить(Строка, СимволРазделитель, Истина);
		
		Для Каждого ЗначениеКолонки Из ЗначенияКолонок Цикл
			Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки);
			Ячейка.Текст = ЗначениеКолонки;
			
			НомерКолонки = НомерКолонки + 1;
		КонецЦикла;
		
		Строка = ЧтениеТекста.ПрочитатьСтроку(РазделительСтрок);
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСимволРазделитель(Строка)
	
	Если СтрНайти(Строка, ";") > 0 Тогда
		Возврат ";";
	ИначеЕсли СтрНайти(Строка, ",") > 0 Тогда
		Возврат ",";
	ИначеЕсли СтрНайти(Строка, "|") > 0 Тогда
		Возврат "|";
	Иначе
		Возврат Символы.Таб;	
	КонецЕсли;
	
КонецФункции

Процедура УдалитьПустыеСтроки(ТабличныйДокумент)
	
	ВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	ШиринаТаблицы = ТабличныйДокумент.ШиринаТаблицы;
	
	ПустыеСтроки = Новый Массив;
	
	Для НомерСтроки = 1 По ВысотаТаблицы Цикл
		
		ВсеЯчейкиПустые = Истина;
		Для НомерКолонки = 1 По ШиринаТаблицы Цикл
			
			Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки);
			Если Не ПустаяСтрока(Ячейка.Текст) Тогда
				ВсеЯчейкиПустые = Ложь;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ВсеЯчейкиПустые Тогда
			ПустыеСтроки.Добавить(НомерСтроки);
		КонецЕсли;
		
	КонецЦикла;
	
	ВГраница = ПустыеСтроки.ВГраница();
	Для Индекс = 0 По ВГраница Цикл
		
		НомерСтроки = ПустыеСтроки[ВГраница - Индекс];
		Область = ТабличныйДокумент.Область(НомерСтроки, , НомерСтроки);
		ТабличныйДокумент.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрорисоватьГраницыЯчеек(ТабличныйДокумент)
	
	ВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	ШиринаТаблицы = ТабличныйДокумент.ШиринаТаблицы;
	
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	
	Для НомерСтроки = 1 По ВысотаТаблицы Цикл
		
		Для НомерКолонки = 1 По ШиринаТаблицы Цикл
			
			Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки);
			Ячейка.Обвести(Линия, Линия, Линия, Линия);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиПодходящуюКолонку(ЗаголовокКолонкиФайла, ОписаниеКолонок, СопоставленныеКолонки)
	
	ИскомыйЗаголовок = НРег(СокрЛП(ЗаголовокКолонкиФайла));
	
	Результат = Новый Структура("Заголовок, Идентификатор", "", "");
	Для Каждого Колонка Из ОписаниеКолонок Цикл
		
		Если СопоставленныеКолонки.Найти(Колонка.Идентификатор) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Колонка.ПодходящиеЗаголовкиФайла.Найти(ИскомыйЗаголовок) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Заголовок = Колонка.ПредставлениеКолонки;
		Если Не ПустаяСтрока(Колонка.Родитель) Тогда
			СтрокаРодитель = ОписаниеКолонок.Найти(Колонка.Родитель, "Идентификатор");
			Если СтрокаРодитель <> Неопределено Тогда
				Заголовок = СтрокаРодитель.ПредставлениеКолонки + ", " + Колонка.ПредставлениеКолонки;
			КонецЕсли;
		КонецЕсли;
		
		Результат.Заголовок = Заголовок;
		Результат.Идентификатор = Колонка.Идентификатор;
		
		СопоставленныеКолонки.Добавить(Колонка.Идентификатор);
		
		Прервать;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция АнализДанных(ТабличныйДокумент)
	
	ВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	ШиринаТаблицы = ТабличныйДокумент.ШиринаТаблицы;
	
	// Получаем массив информации о заполненности ячеек
	// 0 - ячейка пустая, 1 - ячейка заполнена
	ТаблицаАнализа = Новый ТаблицаЗначений;
	ТаблицаАнализа.Колонки.Добавить("СтрокаИнформации", Новый ОписаниеТипов("Строка"));
	ТаблицаАнализа.Колонки.Добавить("КоличествоПохожих", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0, ДопустимыйЗнак.Неотрицательный));
	Для НомерСтроки = 2 По ВысотаТаблицы Цикл // пропускаем первую строку (заголовки колонок)
		
		СтрокаИнформации = "";
		Для НомерКолонки = 1 По ШиринаТаблицы Цикл
			
			Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки);
			СтрокаИнформации = СтрокаИнформации + ?(ПустаяСтрока(Ячейка.Текст), "0", "1");
			
		КонецЦикла;
		СтрокаТаблицы = ТаблицаАнализа.Добавить();
		СтрокаТаблицы.СтрокаИнформации = СтрокаИнформации;
		СтрокаТаблицы.КоличествоПохожих = 1;
		
	КонецЦикла;
	ИнформацияОЯчейках = ТаблицаАнализа.ВыгрузитьКолонку("СтрокаИнформации");
	ТаблицаАнализа.Свернуть("СтрокаИнформации", "КоличествоПохожих");
	ТаблицаАнализа.Сортировать("КоличествоПохожих УБЫВ");
	
	// Получаем массив строк, содержащих информацию к загрузке
	ИнформацияОЯчейкахКЗагрузке = Новый Массив;
	МинКолОдинаковыхСтруктур = 3;
	МинЧислоЗаполненныхЯчеек = 2;
	Для Каждого СтрокаТаблицы Из ТаблицаАнализа Цикл
		
		Если СтрокаТаблицы.КоличествоПохожих < МинКолОдинаковыхСтруктур Тогда
			
			Продолжить;
			
		ИначеЕсли СтрЧислоВхождений(СтрокаТаблицы.СтрокаИнформации, "1") < МинЧислоЗаполненныхЯчеек Тогда
			
			Продолжить;
			
		КонецЕсли;
		ИнформацияОЯчейкахКЗагрузке.Добавить(СтрокаТаблицы.СтрокаИнформации);
		
	КонецЦикла;
	
	ИнформацияОТаблице = Новый Структура;
	ИнформацияОТаблице.Вставить("ИнформацияОЯчейках", ИнформацияОЯчейках);
	ИнформацияОТаблице.Вставить("ИнформацияОЯчейкахКЗагрузке", ИнформацияОЯчейкахКЗагрузке);
	
	Возврат ИнформацияОТаблице;
	
КонецФункции

// Удаляет временный файл.
// Если при попытке удаления возникает ошибка, она игнорируется - файл будет удален позднее.
//
Процедура УдалитьВременныйФайл(ПолноеИмяФайла)
	
	Если ПустаяСтрока(ПолноеИмяФайла) Тогда
		Возврат;
	КонецЕсли;
		
	Попытка
		УдалитьФайлы(ПолноеИмяФайла)
	Исключение
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось удалить временный файл
			|%1 по причине: %2'"), ПолноеИмяФайла, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
	КонецПопытки
	
КонецПроцедуры

Функция ЭтоТаблица(ДанныеФайла, НомерСтроки)
	
	// Считаем, что это таблица, если 
	//  - Четыре ячейки заполнены
	//  - В ячейках не повторяющийся текст, так как текст повторяется, если есть объединенные ячейки
	// Например, это таблица:
	// | №п/п | Фамилия |
	// |  1   | Иванов  |
	// |  2   | Петров  |
	
	ЛеваяВерхняяЯчейка = ДанныеФайла.Область(НомерСтроки, 1).Текст;
	Если ПустаяСтрока(ЛеваяВерхняяЯчейка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПраваяВерхняяЯчейка = ДанныеФайла.Область(НомерСтроки, 2).Текст;
	Если ПустаяСтрока(ПраваяВерхняяЯчейка) Или ЛеваяВерхняяЯчейка = ПраваяВерхняяЯчейка Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЛеваяНижняяЯчейка = ДанныеФайла.Область(НомерСтроки + 1, 1).Текст;
	Если ПустаяСтрока(ЛеваяНижняяЯчейка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПраваяНижняяЯчейка = ДанныеФайла.Область(НомерСтроки + 1, 2).Текст;
	Если ПустаяСтрока(ПраваяНижняяЯчейка) Или ЛеваяНижняяЯчейка = ПраваяНижняяЯчейка Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура УдалитьПустыеКолонки(ТабличныйДокумент, НачинатьСоСтроки = 1) Экспорт
	
	ВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	ШиринаТаблицы = ТабличныйДокумент.ШиринаТаблицы;
	
	ПустыеКолонки = Новый Массив;
	
	Для НомерКолонки = 1 По ШиринаТаблицы Цикл
		
		ВсеЯчейкиПустые = Истина;
		Для НомерСтроки = НачинатьСоСтроки По ВысотаТаблицы Цикл
			
			Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки);
			Если Не ПустаяСтрока(Ячейка.Текст) Тогда
				ВсеЯчейкиПустые = Ложь;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ВсеЯчейкиПустые Тогда
			ПустыеКолонки.Добавить(НомерКолонки);
		КонецЕсли;
		
	КонецЦикла;
	
	ВГраница = ПустыеКолонки.ВГраница();
	Для Индекс = 0 По ВГраница Цикл
		
		НомерКолонки = ПустыеКолонки[ВГраница - Индекс];
		Область = ТабличныйДокумент.Область(, НомерКолонки, , НомерКолонки);
		ТабличныйДокумент.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
