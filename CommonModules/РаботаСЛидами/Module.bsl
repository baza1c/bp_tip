
#Область ПрограммныйИнтерфейс

// Вызывается перед записью документа, изменяющего состояние работы с лидами
//
// Параметры:
//  ДокументОбъект  - ДокументОбъект - записывамый документ.
//  Отказ           - Булево - признак отказа от записи документа.
//  РежимЗаписи     - РежимЗаписиДокумента - режим записи документа.
//  РежимПроведения - РежимПроведенияДокумента - режим проведения документа.
//
Процедура ПередЗаписьюДокументаУсловиеИзмененияСостояния(ДокументОбъект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ДокументОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДокументОбъект.ЭтоНовый()
		И Не ДокументОбъект.ПометкаУдаления
		И ДокументОбъект.КонтактноеЛицо <> ДокументОбъект.Ссылка.КонтактноеЛицо Тогда
		ДокументОбъект.ДополнительныеСвойства.Вставить("ПредыдущийЛид", ДокументОбъект.Ссылка.КонтактноеЛицо);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при записи документа, изменяющего состояние работы с лидами
//
// Параметры:
//  ДокументОбъект  - ДокументОбъект - записывамый документ.
//  Отказ           - Булево - признак отказа от записи документа.
//
Процедура ПриЗаписиДокументаУсловиеИзмененияСостояния(ДокументОбъект, Отказ) Экспорт
	
	Если ДокументОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ФункциональностьПрограммы.РазделЛидыДоступен() Тогда
		Возврат;
	КонецЕсли;
	
	УсловиеИзменения = УсловиеИзмененияДокумента(ТипЗнч(ДокументОбъект));
	Если Не ЗначениеЗаполнено(УсловиеИзменения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДокументОбъект.ПометкаУдаления Тогда
		Если Справочники.КонтактныеЛица.ЭтоЛид(ДокументОбъект.КонтактноеЛицо, Истина) Тогда
			СостояниеЛидаИзменено = РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.ОчиститьСостояниеРаботыСЛидомПриУсловииИзменения(
				ДокументОбъект.КонтактноеЛицо,
				УсловиеИзменения);
			Если СостояниеЛидаИзменено Тогда
				ДокументОбъект.ДополнительныеСвойства.Вставить("ИзмененоСостояниеЛидов", Истина);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ПредыдущийЛид = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДокументОбъект.ДополнительныеСвойства, "ПредыдущийЛид");
		Если ЗначениеЗаполнено(ПредыдущийЛид)
			И Справочники.КонтактныеЛица.ЭтоЛид(ПредыдущийЛид, Истина) Тогда
			СостояниеЛидаИзменено = РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.ОчиститьСостояниеРаботыСЛидомПриУсловииИзменения(
				ПредыдущийЛид,
				УсловиеИзменения);
			Если СостояниеЛидаИзменено Тогда
				ДокументОбъект.ДополнительныеСвойства.Вставить("ИзмененоСостояниеЛидов", Истина);
			КонецЕсли;
		КонецЕсли;
		
		Если Справочники.КонтактныеЛица.ЭтоЛид(ДокументОбъект.КонтактноеЛицо, Истина) Тогда
			СостояниеЛидаИзменено = РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.УстановитьСостояниеРаботыСЛидомПриУсловииИзменения(
				ДокументОбъект.КонтактноеЛицо,
				УсловиеИзменения);
			Если СостояниеЛидаИзменено Тогда
				ДокументОбъект.ДополнительныеСвойства.Вставить("ИзмененоСостояниеЛидов", Истина);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УсловиеИзмененияДокумента(ТипОбъекта)
	
	Если ТипОбъекта = Тип("ДокументОбъект.КоммерческоеПредложение") Тогда
		Возврат Перечисления.УсловияИзмененияСостоянияРаботыСЛидами.ВводКоммерческогоПредложения;
	ИначеЕсли ТипОбъекта = Тип("ДокументОбъект.ПрочееВзаимодействие")
		Или ТипОбъекта = Тип("ДокументОбъект.ТелефонныйЗвонок")
		Или ТипОбъекта = Тип("ДокументОбъект.Встреча")
		Или ТипОбъекта = Тип("ДокументОбъект.ЭлектронноеПисьмоИсходящее") Тогда
		Возврат Перечисления.УсловияИзмененияСостоянияРаботыСЛидами.ВводВзаимодействия;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
