
#Область ПрограммныйИнтерфейс

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗаписатьРучнойСтатусЗадачи
//
Процедура ПриЗаписиРучногоСтатусаЗадачи(Задача, Статус, Комментарий, Пользователь, ПеренестиВАрхив) Экспорт
	
	Если Задача.Свойство("ИдентификаторЗадачи") И Задача.ИдентификаторЗадачи = "ЕдиныйНалоговыйСчет" Тогда
		ПараметрыЗадачи = Обработки.ПомощникУплатыНалога.НовыеПараметрыЗадачи();
		ЗаполнитьЗначенияСвойств(ПараметрыЗадачи, Задача);
		ТаблицаПодчиненныхЗадач = Обработки.ПомощникУплатыНалога.ПодчиненныеЗадачиПоЕдиномуНалоговомуСчету(ПараметрыЗадачи);
		Обработки.ПомощникУплатыНалога.ЗаписатьСтатусПодчиненныхЗадачПоЕдиномуНалоговомуСчету(
			ТаблицаПодчиненныхЗадач, Статус, Истина, Пользователь, Комментарий);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ВернутьАвтоматическийСтатусЗадачи
//
Процедура ПриВозвращенииАвтоматическогоСтатусаЗадачи(Задача) Экспорт
	
	Если Задача.ИдентификаторЗадачи = "ЕдиныйНалоговыйСчет" Тогда
		ПараметрыЗадачи = Обработки.ПомощникУплатыНалога.НовыеПараметрыЗадачи();
		ЗаполнитьЗначенияСвойств(ПараметрыЗадачи, Задача);
		ТаблицаПодчиненныхЗадач = Обработки.ПомощникУплатыНалога.ПодчиненныеЗадачиПоЕдиномуНалоговомуСчету(ПараметрыЗадачи);
		Обработки.ПомощникУплатыНалога.ЗаписатьСтатусПодчиненныхЗадачПоЕдиномуНалоговомуСчету(
			ТаблицаПодчиненныхЗадач, "");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействия
//
Процедура ПриОпределенииОписанияДействия(ОписаниеДействия, Событие) Экспорт
	
	Если ТипЗнч(Событие.Правило) = Тип("СправочникСсылка.ПравилаРегулярныхПлатежей") Тогда
		ОписаниеДействия = ВыполнениеЗадачБухгалтера.ОписаниеДействияРегулярныйПлатеж(Событие);
	ИначеЕсли ТипЗнч(Событие.Правило) = Тип("СправочникСсылка.Патенты") Тогда
		Если Событие.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет Тогда
			ОписаниеДействия = ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникПодготовкиЗаявленияНаПолучениеПатента(Событие);
		ИначеЕсли Событие.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога Тогда
			ОписаниеДействия = ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникОплатыПатента(Событие);
		Иначе
			ОписаниеДействия = ВыполнениеЗадачБухгалтера.ОписаниеДействияСозданиеПатента(Событие);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Событие.Правило) = Тип("СправочникСсылка.ПравилаФинОтчетности") Тогда
		ОписаниеДействия = ВыполнениеЗадачБухгалтера.ОписаниеДействияФинОтчетность(Событие);
	ИначеЕсли ТипЗнч(Событие.Правило) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
		ОписаниеДействия = ВыполнениеЗадачБухгалтера.ОписаниеДействияТребованияФНС(Событие);
	ИначеЕсли ВыполнениеЗадачБухгалтераКлиентСервер.ЭтоДокументСЭДО(Событие.Правило) Тогда
		ОписаниеДействия = ВыполнениеЗадачБухгалтера.ОписаниеДействияДокументСЭДО(Событие);
	ИначеЕсли ТипЗнч(Событие.Правило) = Тип("СправочникСсылка.ПроверкиКонтролирующимиОрганами") Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ",        Событие.Правило);
		ПараметрыФормы.Вставить("Организация", Событие.Организация);
		
		ОписаниеДействия = Новый Структура;
		ОписаниеДействия.Вставить("ИмяФормы",       "Справочник.ПроверкиКонтролирующимиОрганами.Форма.ФормаПросмотра");
		ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	Иначе
		ОписаниеДействия = ВыполнениеЗадачБухгалтера.ОписаниеДействияНалоговыйПлатеж(Событие);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.НовыйОписаниеПлатежа
//
Процедура ПриОпределенииОписанияПлатежа(ОписаниеПлатежа) Экспорт
	
	Если ОписаниеПлатежа.Свойство("Налог") Тогда
		ОписаниеПлатежа.Удалить("Налог");
	КонецЕсли;
	
	Если ОписаниеПлатежа.Свойство("ВидНалоговогоОбязательства") Тогда
		ОписаниеПлатежа.Удалить("ВидНалоговогоОбязательства");
	КонецЕсли;
	
	Если ОписаниеПлатежа.Свойство("СпособОплаты") Тогда
		ОписаниеПлатежа.Удалить("СпособОплаты");
	КонецЕсли;
	
	ОписаниеПлатежа.Вставить("Налог",                      Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка());
	ОписаниеПлатежа.Вставить("ВидНалоговогоОбязательства", Перечисления.ВидыПлатежейВГосБюджет.Налог);
	ОписаниеПлатежа.Вставить("СпособОплаты",               Перечисления.СпособыУплатыНалогов.ПустаяСсылка());
	
КонецПроцедуры 

// Подробнее
// См. ВыполнениеЗадачБухгалтера.НовыйТаблицаПлатежи
//
Процедура ПриОпределенииТаблицыПлатежи(Платежи) Экспорт
	
	Если Платежи.Колонки.Найти("КБК") <> Неопределено Тогда
		Платежи.Колонки.Удалить("КБК");
	КонецЕсли;
	
	Если Платежи.Колонки.Найти("ОКАТО") <> Неопределено Тогда
		Платежи.Колонки.Удалить("ОКАТО");
	КонецЕсли;
	
	Если Платежи.Колонки.Найти("Налог") <> Неопределено Тогда
		Платежи.Колонки.Удалить("Налог");
	КонецЕсли;
	
	Если Платежи.Колонки.Найти("ВидНалоговогоОбязательства") <> Неопределено Тогда
		Платежи.Колонки.Удалить("ВидНалоговогоОбязательства");
	КонецЕсли;
	
	// По колонкам "КБК" и "ОКАТО" предполагается индекирование, получаем тип из метаданных
	Платежи.Колонки.Добавить("КБК",   Метаданные.Документы.ПлатежноеПоручение.Реквизиты.КодБК.Тип);
	Платежи.Колонки.Добавить("ОКАТО", Метаданные.Справочники.РегистрацииВНалоговомОргане.Реквизиты.КодПоОКТМО.Тип);
	
	Платежи.Колонки.Добавить("Налог",                      Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	Платежи.Колонки.Добавить("ВидНалоговогоОбязательства", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПлатежейВГосБюджет"));
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗначенияЗаполненияПлатежногоДокумента
//
Процедура ЗаполнитьЗначенияЗаполненияПлатежногоДокумента(ЗначенияЗаполнения, ОписаниеПлатежа) Экспорт
	
	ПравилаЗаполнения = ПлатежиВБюджетНастройки.ПравилаЗаполненияРеквизитовПлатежа(ОписаниеПлатежа.Налог);
	ДатаПлатежа = ОбщегоНазначения.ТекущаяДатаПользователя();
	ДействуетПереходНаЕНП = ПлатежиВБюджетКлиентСервер.ДействуетПереходНаЕНП(ДатаПлатежа);
	ЭтоЕНП = ПлатежиВБюджетКлиентСервер.ЭтоЕдиныйНалоговыйПлатеж(ОписаниеПлатежа.КБК);
	ЭтоНалоговыйПлатеж = ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(ОписаниеПлатежа.КБК);

	Если Не ЭтоЕНП И УчетнаяПолитика.ПлательщикЕНП(ОписаниеПлатежа.Организация, ДатаПлатежа) И ДействуетПереходНаЕНП Тогда
		НалогиНеВходящиеВНЕП = ЕдиныйНалоговыйСчетПовтИсп.МассивДоступныхВидовНалогов(ДатаПлатежа);
		ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОписаниеПлатежа.Налог, "ВидНалога");
		Если НалогиНеВходящиеВНЕП.Найти(ВидНалога) = Неопределено Тогда
			ОписаниеПлатежа.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж;
			ОписаниеПлатежа.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			ОписаниеПлатежа.КБК = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(ОписаниеПлатежа.Налог);
			ЭтоЕНП = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ЭтоУплатаЕНППоНовымРеквизитам = ЭтоЕНП И ДействуетПереходНаЕНП;
	
	Если ПравилаЗаполнения.ЗаполнятьПолучателя Тогда
		Если ЭтоУплатаЕНППоНовымРеквизитам Тогда
			РеквизитыПолучателя = ЕдиныйНалоговыйСчет.РеквизитыМИДол();
			ОписаниеПлатежа.КодНалоговогоОргана = РеквизитыПолучателя.Код;
		ИначеЕсли ЭтоНалоговыйПлатеж И ДатаПлатежа >= ПлатежиВБюджетКлиентСервер.ДатаСменыНаименованияМИДол() Тогда
			ВидИКод = ДанныеГосударственныхОрганов.ВидИКодГосударственногоОрганаПоНалогу(
				ОписаниеПлатежа.Налог, ОписаниеПлатежа.Организация, ДатаПлатежа);
			ОписаниеПлатежа.КодНалоговогоОргана = ВидИКод.Код;
		ИначеЕсли Не ЗначениеЗаполнено(ОписаниеПлатежа.КодНалоговогоОргана) Тогда
			ВидИКод = ДанныеГосударственныхОрганов.ВидИКодГосударственногоОрганаПоНалогу(
			ОписаниеПлатежа.Налог, ОписаниеПлатежа.Организация, ОписаниеПлатежа.Период);
			// Если нужно заполнять получателя и не заполен Код налогового органа, попробуем получить его из сведений об организации.
			ОписаниеПлатежа.КодНалоговогоОргана = ВидИКод.Код;
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыГоловнойОрганизации = ЕдиныйНалоговыйСчет.КППОКТМООрганизацииДляУплатыВБюджет(
		ОписаниеПлатежа.Организация, ОписаниеПлатежа.Налог, ДатаПлатежа);
	
	Если ПравилаЗаполнения.ЗаполнятьКодТерриторииПоРегистрации
		И ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(ОписаниеПлатежа.КБК)
		Или Не ЗначениеЗаполнено(ОписаниеПлатежа.ОКАТО) Тогда
		Если ДействуетПереходНаЕНП Тогда
			ОписаниеПлатежа.ОКАТО = РеквизитыГоловнойОрганизации.ОКТМО;
		ИначеЕсли ПустаяСтрока(ОписаниеПлатежа.ОКАТО) Тогда
			ОписаниеПлатежа.ОКАТО = Справочники.Организации.КодТерриторииМестаРегистрации(ОписаниеПлатежа.Организация);
		КонецЕсли;
	КонецЕсли;
	
	ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(ОписаниеПлатежа.ОКАТО, ДатаПлатежа);
	
	ДанныеПлатежногоДокумента = Новый Структура;
	// Контекст
	ДанныеПлатежногоДокумента.Вставить("Организация", ОписаниеПлатежа.Организация);
	Если НЕ ПустаяСтрока(ОписаниеПлатежа.КПППлательщика) Тогда
		Если ЭтоУплатаЕНППоНовымРеквизитам Тогда
			ДанныеПлатежногоДокумента.Вставить("КПППлательщика", РеквизитыГоловнойОрганизации.КПП);
		Иначе
			ДанныеПлатежногоДокумента.Вставить("КПППлательщика", ОписаниеПлатежа.КПППлательщика);
		КонецЕсли;
	КонецЕсли;
	ДанныеПлатежногоДокумента.Вставить("Налог", ОписаниеПлатежа.Налог);
	ДанныеПлатежногоДокумента.Вставить("ВидНалоговогоОбязательства", ОписаниеПлатежа.ВидНалоговогоОбязательства);
	
	Если ОписаниеПлатежа.СпособОплаты = Перечисления.СпособыУплатыНалогов.НаличнымиПоКвитанции Тогда
	
		ДанныеПлатежногоДокумента.Вставить("ВидОперации", Перечисления.ВидыОперацийРКО.УплатаНалога);
		ДанныеПлатежногоДокумента.Вставить("СчетКасса",   ПланыСчетов.Хозрасчетный.КассаОрганизации);
		ДанныеПлатежногоДокумента.Вставить("НалоговыйПериод", НачалоМесяца(ОписаниеПлатежа.Период));
	
		Если НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(ОписаниеПлатежа.Организация) Тогда
			ПредпринимательФИО = Справочники.Организации.ФамилияИмяОтчествоПредпринимателя(ОписаниеПлатежа.Организация, ОписаниеПлатежа.Период);
			ДанныеПлатежногоДокумента.Вставить("Выдать", ПредпринимательФИО);
		КонецЕсли;
	
	Иначе
		ДанныеПлатежногоДокумента.Вставить("ПеречислениеВБюджет", Истина);
		ДанныеПлатежногоДокумента.Вставить("ВидОперации",         Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога);
	КонецЕсли;
	
	ДанныеПлатежногоДокумента.Вставить("ВалютаДокумента", ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	ДанныеПлатежногоДокумента.Вставить("ВидПеречисленияВБюджет", ПлатежиВБюджетКлиентСервер.ВидПеречисления(ОписаниеПлатежа.КБК, ОписаниеПлатежа.Период));
	
	// Известны из декларации
	ДанныеПлатежногоДокумента.Вставить("СуммаДокумента",    ОписаниеПлатежа.Сумма);
	ПоказательДаты = ПлатежиВБюджетКлиентСервер.ПреобразоватьДатуКСтроке(ОписаниеПлатежа.ДатаДекларации);
	ДанныеПлатежногоДокумента.Вставить("ПоказательДаты",
		?(ЭтоЕНП, ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение(), ПоказательДаты));
	ДанныеПлатежногоДокумента.Вставить("ДокументОснование", ОписаниеПлатежа.Декларация);
	
	// Зависят от налога, но известны из декларации
	ДанныеПлатежногоДокумента.Вставить("КодБК",             ОписаниеПлатежа.КБК);
	ДанныеПлатежногоДокумента.Вставить("КодОКАТО",          ОписаниеПлатежа.ОКАТО);
	ДанныеПлатежногоДокумента.Вставить("ПоказательПериода", ?(ЭтоЕНП,
		ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение(), ОписаниеПлатежа.ПоказательПериода));
	
	Если ЗначениеЗаполнено(ОписаниеПлатежа.Налог) И ОписаниеПлатежа.Налог <> Справочники.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы 
		И ОписаниеПлатежа.ИмяЗадачи <> "Патент_Уплата" Тогда
	
		ПредставлениеНалоговогоПериода = ПлатежиВБюджетКлиентСервер.ПредставлениеНалоговогоПериода(
			ОписаниеПлатежа.ПоказательПериода);
	
		НазначениеПлатежа = Справочники.ВидыНалоговИПлатежейВБюджет.НазначениеПлатежа(
			ОписаниеПлатежа.Налог, , ОписаниеПлатежа.Организация, ОписаниеПлатежа.Период, ПредставлениеНалоговогоПериода);
			
	Иначе
		НазначениеПлатежа = ОписаниеПлатежа.Описание;
	КонецЕсли;
	
	Если ОписаниеПлатежа.СпособОплаты = Перечисления.СпособыУплатыНалогов.НаличнымиПоКвитанции Тогда
		ДанныеПлатежногоДокумента.Вставить("Основание", НазначениеПлатежа);
	Иначе
		ДанныеПлатежногоДокумента.Вставить("НазначениеПлатежа", НазначениеПлатежа);
	КонецЕсли;
	
	Если ОписаниеПлатежа.Аванс И Не ПлатежиВБюджетКлиентСервер.ДействуетПриказ107н(ДатаПлатежа) Тогда
		ДанныеПлатежногоДокумента.Вставить("ПоказательТипа", ПлатежиВБюджетКлиентСервер.ТипПлатежаАванс());
	КонецЕсли;
	
	Если ЭтоЕНП Тогда
		ДанныеПлатежногоДокумента.Вставить("ПоказательОснования", ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение());
	ИначеЕсли ЗначениеЗаполнено(ОписаниеПлатежа.ОснованиеПлатежа) Тогда
		ДанныеПлатежногоДокумента.Вставить("ПоказательОснования", ОписаниеПлатежа.ОснованиеПлатежа);
	КонецЕсли;
	
	// Зависят от налога, но не известны из декларации
	НастройкиУплатыНалога = РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.НайтиПоКБК(
		ОписаниеПлатежа.Организация,
		ОписаниеПлатежа.КБК,
		ОписаниеПлатежа.ОКАТО);
	
	ДанныеПлатежногоДокумента.Вставить("СтатьяДвиженияДенежныхСредств",
		УчетДенежныхСредствБП.СтатьяДДСПоУмолчанию(ДанныеПлатежногоДокумента.ВидОперации, ДатаПлатежа));
	Если НастройкиУплатыНалога <> Неопределено И ОписаниеПлатежа.Налог = НастройкиУплатыНалога.Налог Тогда
		Если ЗначениеЗаполнено(НастройкиУплатыНалога.СтатьяДвиженияДенежныхСредств) Тогда
			ДанныеПлатежногоДокумента.Вставить("СтатьяДвиженияДенежныхСредств", НастройкиУплатыНалога.СтатьяДвиженияДенежныхСредств);
		КонецЕсли;
	
		Если НЕ ПустаяСтрока(НастройкиУплатыНалога.НазначениеПлатежа) Тогда
			Если НастройкиУплатыНалога.ВидПеречисленияВБюджет <> Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж Тогда
				ДанныеПлатежногоДокумента.Вставить("НазначениеПлатежа", НастройкиУплатыНалога.НазначениеПлатежа);
			Иначе
				ПредставлениеНалоговогоПериода = ПлатежиВБюджетКлиентСервер.ПредставлениеНалоговогоПериода(
					ДанныеПлатежногоДокумента.ПоказательПериода);
				Если НЕ ПустаяСтрока(ПредставлениеНалоговогоПериода) Тогда
					НазначениеПлатежа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1 %2'"),
						НастройкиУплатыНалога.НазначениеПлатежа,
						ПредставлениеНалоговогоПериода);
					ДанныеПлатежногоДокумента.Вставить("НазначениеПлатежа", НазначениеПлатежа);
				Иначе
					ДанныеПлатежногоДокумента.Вставить("НазначениеПлатежа", НастройкиУплатыНалога.НазначениеПлатежа);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
		ДанныеПлатежногоДокумента.Вставить("ВидПеречисленияВБюджет", НастройкиУплатыНалога.ВидПеречисленияВБюджет);
		Если ВыполнениеЗадачБухгалтераБП.ЗаполнитьПоДаннымНастройки(НастройкиУплатыНалога, ДанныеПлатежногоДокумента, "Получатель", "Контрагент") Тогда
			ВыполнениеЗадачБухгалтераБП.ЗаполнитьПоДаннымНастройки(НастройкиУплатыНалога, ДанныеПлатежногоДокумента, "СчетПолучателя", "СчетКонтрагента");
		КонецЕсли;
		
		ВыполнениеЗадачБухгалтераБП.ЗаполнитьПоДаннымНастройки(НастройкиУплатыНалога, ДанныеПлатежногоДокумента, "СтатусСоставителя");
		ВыполнениеЗадачБухгалтераБП.ЗаполнитьПоДаннымНастройки(НастройкиУплатыНалога, ДанныеПлатежногоДокумента, "ПоказательТипа");
		ВыполнениеЗадачБухгалтераБП.ЗаполнитьПоДаннымНастройки(НастройкиУплатыНалога, ДанныеПлатежногоДокумента, "ОчередностьПлатежа");
		ВыполнениеЗадачБухгалтераБП.ЗаполнитьПоДаннымНастройки(НастройкиУплатыНалога, ДанныеПлатежногоДокумента, "ПоказательОснования");
		
		Если НастройкиУплатыНалога.Свойство("КодОКАТО") И ЗначениеЗаполнено(НастройкиУплатыНалога.КодОКАТО) 
			И (Не ДанныеПлатежногоДокумента.Свойство("КодОКАТО") Или Не ЗначениеЗаполнено(ДанныеПлатежногоДокумента.КодОКАТО))Тогда
			ДанныеПлатежногоДокумента.Вставить("КодОКАТО", НастройкиУплатыНалога.КодОКАТО);
		КонецЕсли;
		
		// ПоказательПериода точнее определяется из декларации, чем задан в настройке
		Если НЕ ЗначениеЗаполнено(ОписаниеПлатежа.Декларация)
			И ДанныеПлатежногоДокумента.Свойство("ПоказательПериода") И НЕ ЗначениеЗаполнено(ДанныеПлатежногоДокумента.ПоказательПериода) Тогда
			ВыполнениеЗадачБухгалтераБП.ЗаполнитьПоДаннымНастройки(НастройкиУплатыНалога, ДанныеПлатежногоДокумента, "ПоказательПериода");
		КонецЕсли;
		
		Если ОписаниеПлатежа.СпособОплаты = Перечисления.СпособыУплатыНалогов.НаличнымиПоКвитанции Тогда
			АналитикаПлатежаВБюджетПриУплатеНаличными = Документы.РасходныйКассовыйОрдер.АналитикаПлатежаВБюджетПриУплатеНаличными(
				НастройкиУплатыНалога.СчетУчета,
				ОписаниеПлатежа.Организация,
				ОписаниеПлатежа.КБК,
				ОписаниеПлатежа.КодНалоговогоОргана,
				ОписаниеПлатежа.Период,
				ОписаниеПлатежа.ВидНалоговогоОбязательства);
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеПлатежногоДокумента, АналитикаПлатежаВБюджетПриУплатеНаличными);
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ОписаниеПлатежа.КодНалоговогоОргана) Тогда
			ВидГосоргана = Справочники.Контрагенты.ВидГосударственногоОрганаПоКБК(ДанныеПлатежногоДокумента.КодБК);
			ДанныеПлатежногоДокумента.Вставить("КодНалоговогоОргана", ОписаниеПлатежа.КодНалоговогоОргана);
			
			//определяем получателя по данным информационной базы
			НалоговыйОрган = Документы.ПлатежноеПоручение.ПолучательДляПлатежаГосударственномуОргануПоПравиламУплаты(ОписаниеПлатежа.Организация,
				ОписаниеПлатежа.Период,
				ВидГосоргана,
				ОписаниеПлатежа.КодНалоговогоОргана,
				ОписаниеПлатежа.Налог);
			Если ЗначениеЗаполнено(НалоговыйОрган.Контрагент) Тогда
				ДанныеПлатежногоДокумента.Вставить("Контрагент",      НалоговыйОрган.Контрагент);
				ДанныеПлатежногоДокумента.Вставить("СчетКонтрагента", НалоговыйОрган.БанковскийСчет);
			Иначе
				//Определяем данные получателя из веб-сервиса
				Если ВидГосоргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган Тогда
					Реквизиты = ДанныеГосударственныхОрганов.РеквизитыНалоговогоОрганаПоКоду(ОписаниеПлатежа.КодНалоговогоОргана);
					Если УчетнаяПолитика.ПлательщикЕНП(ОписаниеПлатежа.Организация, ОписаниеПлатежа.Период) Тогда
						ЕдиныйНалоговыйСчет.ПлатежныеРеквизитыЕдиногоНалоговогоСчета(Реквизиты.ПлатежныеРеквизиты);
						Если ЗначениеЗаполнено(Реквизиты.Ссылка) Тогда
							Реквизиты.ПлатежныеРеквизиты.БанковскийСчет = ЕдиныйНалоговыйСчет.ЕдиныйБанковскийСчетГосударственногоОргана(Реквизиты.Ссылка);
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли ВидГосоргана = Перечисления.ВидыГосударственныхОрганов.ОрганПФР Тогда
					Реквизиты = ДанныеГосударственныхОрганов.РеквизитыОтделенияПФРПоКоду(ОписаниеПлатежа.КодНалоговогоОргана);
				ИначеЕсли ВидГосоргана = Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
					Реквизиты = ДанныеГосударственныхОрганов.РеквизитыОтделенияФССПоКоду(ОписаниеПлатежа.КодНалоговогоОргана);
				Иначе
					Реквизиты = Неопределено;
				КонецЕсли;
				
				Если Реквизиты <> Неопределено
					И НЕ ЗначениеЗаполнено(Реквизиты.ОписаниеОшибки) Тогда
					
					Если ЗначениеЗаполнено(Реквизиты.Ссылка) Тогда
						ДанныеПлатежногоДокумента.Вставить("Контрагент",      Реквизиты.Ссылка);
						ДанныеПлатежногоДокумента.Вставить("СчетКонтрагента", Реквизиты.ПлатежныеРеквизиты.БанковскийСчет);
					Иначе
						ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(Реквизиты);
						ДанныеПлатежногоДокумента.Вставить("Контрагент",      Реквизиты.Ссылка);
						ДанныеПлатежногоДокумента.Вставить("СчетКонтрагента", Реквизиты.ПлатежныеРеквизиты.БанковскийСчет);
					КонецЕсли;
					
				КонецЕсли;
	
			КонецЕсли;
		КонецЕсли;
	
		Если ОписаниеПлатежа.СпособОплаты = Перечисления.СпособыУплатыНалогов.НаличнымиПоКвитанции Тогда
			Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(ОписаниеПлатежа.КБК);
			СчетУчетаРасчетовСКонтрагентом = Справочники.ВидыНалоговИПлатежейВБюджет.СчетУчета(Налог, ОписаниеПлатежа.Период);
			Если ЗначениеЗаполнено(СчетУчетаРасчетовСКонтрагентом) Тогда
				АналитикаПлатежаВБюджетПриУплатеНаличными = Документы.РасходныйКассовыйОрдер.АналитикаПлатежаВБюджетПриУплатеНаличными(
					СчетУчетаРасчетовСКонтрагентом,
					ОписаниеПлатежа.Организация,
					ОписаниеПлатежа.КБК,
					ОписаниеПлатежа.КодНалоговогоОргана,
					ОписаниеПлатежа.Период,
					ОписаниеПлатежа.ВидНалоговогоОбязательства);
				ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеПлатежногоДокумента, АналитикаПлатежаВБюджетПриУплатеНаличными);
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
	Если ОписаниеПлатежа.ОплатаЗадолженности Тогда
		ДанныеПлатежногоДокумента.Вставить("ПоказательОснования",
			ПлатежиВБюджетКлиентСервер.ОснованиеПлатежаДобровольноеПогашениеЗадолженности());
	КонецЕсли;
	
	ЗначенияЗаполнения = ДанныеПлатежногоДокумента;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами
//
Процедура ЗаполнитьМассивТиповПлатежныхДокументов(МассивТиповПлатежныхДокументов) Экспорт
	
	МассивТиповПлатежныхДокументов.Добавить(Тип("ДокументСсылка.ПлатежноеПоручение"));
	МассивТиповПлатежныхДокументов.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами
//
Процедура ДополнитьНачисленияПлатежнымиДокументами(Платежи, Правило, ПериодСобытия, Организация, СведенияПоДекларации = Неопределено, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если Платежи.Колонки.Найти("Состояние") = Неопределено Тогда
		Платежи.Колонки.Добавить("Состояние", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияБанковскихДокументов"));
	КонецЕсли;
	
	Для Каждого СтрокаПлатежа Из Платежи Цикл
		СтрокаПлатежа.Индекс = Платежи.Индекс(СтрокаПлатежа);
	КонецЦикла;
	
	ИмяЗадачи = Правило.Владелец.Код;
	ПлатежПоДекларации = ЗначениеЗаполнено(СведенияПоДекларации);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("Правило",       Правило);
	Запрос.УстановитьПараметр("Платежи",       Платежи);
	Запрос.УстановитьПараметр("ПериодСобытия", ПериодСобытия);
	Если ПлатежПоДекларации Тогда
		Запрос.УстановитьПараметр("Декларация",        СведенияПоДекларации.Ссылка);
		ПоказательПериодаОсобыйПорядок = Новый Массив;
		Если ПомощникиПоУплатеНалоговИВзносов.ПрименяетсяОсобыйПорядокУплатыНалога(Организация, ПериодСобытия, Правило.Периодичность) Тогда
			ПоказательПериодаОсобыйПорядок.Добавить(ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение());
		КонецЕсли;
		Запрос.УстановитьПараметр("ПоказательПериода", СведенияПоДекларации.ПоказательПериода);
		Запрос.УстановитьПараметр("ПоказательПериодаОсобыйПорядок", ПоказательПериодаОсобыйПорядок);
	КонецЕсли;

	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Платежи.Период КАК Период,
	|	Платежи.КБК КАК КБК,
	|	Платежи.ОКАТО КАК ОКАТО,
	|	Платежи.Индекс КАК ИндексПлатежа
	|ПОМЕСТИТЬ ОтборТекущиеПлатежи
	|ИЗ
	|	&Платежи КАК Платежи
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КБК,
	|	ОКАТО" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

	Если ПлатежПоДекларации Тогда
		ВыполнениеЗадачБухгалтераБП.ДополнитьТекстЗапросаПлатежиПоДекларации(ТекстЗапроса);
	Иначе
		ВыполнениеЗадачБухгалтераБП.ДополнитьТекстЗапросаПлатежи(ТекстЗапроса);
	КонецЕсли;

	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК Ссылка,
	|	ПлатежноеПоручение.Дата КАК Дата,
	|	ПлатежноеПоручение.КодБК КАК КодБК,
	|	ПлатежноеПоручение.КодОКАТО КАК КодОКАТО,
	|	ПлатежноеПоручение.СуммаДокумента КАК СуммаДокумента,
	|	Отбор.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТПлатежныеПоручения
	|ИЗ
	|	ОтборПравило КАК Отбор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ПО Отбор.Ссылка = ПлатежноеПоручение.Ссылка
	|ГДЕ
	|	ПлатежноеПоручение.Организация = &Организация
	|	И НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И ПлатежноеПоручение.ПеречислениеВБюджет
	|	И &УсловиеОтбораПоРегистрации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Ссылка КАК Ссылка,
	|	РасходныйКассовыйОрдер.Дата КАК Дата,
	|	РасходныйКассовыйОрдер.КодБК КАК КБК,
	|	РасходныйКассовыйОрдер.КодОКАТО КАК ОКАТО,
	|	РасходныйКассовыйОрдер.СуммаДокумента КАК СуммаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.ПустаяСсылка) КАК Состояние,
	|	ИСТИНА КАК Оплачено,
	|	Отбор.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ПлатежиПоПравилу
	|ИЗ
	|	ОтборПравило КАК Отбор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|		ПО Отбор.Ссылка = РасходныйКассовыйОрдер.Ссылка
	|ГДЕ
	|	РасходныйКассовыйОрдер.Организация = &Организация
	|	И НЕ РасходныйКассовыйОрдер.ПометкаУдаления
	|	И РасходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.УплатаНалога)
	|	И &УсловиеОтбораПоРегистрации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТПлатежныеПоручения.Ссылка,
	|	ВТПлатежныеПоручения.Дата,
	|	ВТПлатежныеПоручения.КодБК,
	|	ВТПлатежныеПоручения.КодОКАТО,
	|	ВТПлатежныеПоручения.СуммаДокумента,
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Оплачено)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВТПлатежныеПоручения.РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТПлатежныеПоручения КАК ВТПлатежныеПоручения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|		ПО ВТПлатежныеПоручения.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КБК,
	|	ОКАТО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Платежи.ИндексПлатежа КАК ИндексПлатежа,
	|	Платежи.ОКАТО КАК ОКАТОДекларации,
	|	ПлатежиПоПравилу.Ссылка КАК ПлатежноеПоручение,
	|	ПлатежиПоПравилу.ОКАТО КАК ОКАТОПлатежа,
	|	ПлатежиПоПравилу.Состояние КАК Состояние,
	|	ПлатежиПоПравилу.Оплачено КАК Оплачено,
	|	ПлатежиПоПравилу.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	ПлатежиПоПравилу КАК ПлатежиПоПравилу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборТекущиеПлатежи КАК Платежи
	|		ПО ПлатежиПоПравилу.КБК = Платежи.КБК
	|			И ПлатежиПоПравилу.ОКАТО = Платежи.ОКАТО
	|			И (&УсловиеПоПериоду)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПлатежиПоПравилу.СуммаДокумента УБЫВ
	|ИТОГИ ПО
	|	ИндексПлатежа";

	Если ПлатежПоДекларации И ИмяЗадачи = "НДС" Тогда
		// Особенность НДС в том, что он уплачивается несколькими платежами.
		// Поэтому дополнительно проверяем месяц платежного поручения.
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПериоду",
			"НАЧАЛОПЕРИОДА(Платежи.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПлатежиПоПравилу.Дата, МЕСЯЦ)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПериоду", "ИСТИНА");
	КонецЕсли;

	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) 
		И ИмяЗадачи <> "НалогНаПрибыль" Тогда
		// Для платежных поручений по налогу на прибыль регистрация в налоговом органе всегда не заполнена, поэтому отбор не накладываем.
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоРегистрации", "Отбор.РегистрацияВНалоговомОргане = (&РегистрацияВНалоговомОргане)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоРегистрации", "ИСТИНА");
	КонецЕсли;

	Запрос.Текст = ТекстЗапроса;

	ПлатежиВременный = Платежи.СкопироватьКолонки();
	ПлатежиПоИндексу = Платежи.СкопироватьКолонки();

	ВыборкаПоИндексам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоИндексам.Следующий() Цикл
		Выборка = ВыборкаПоИндексам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ПлатежиПоИндексу.Очистить();
		СуммаНалога = 0;
		Пока Выборка.Следующий() Цикл
			ТекущийПлатеж = Платежи[Выборка.ИндексПлатежа];
			СуммаНалога = ТекущийПлатеж.Сумма;
			Платеж = ПлатежиПоИндексу.Добавить();
			ЗаполнитьЗначенияСвойств(Платеж, ТекущийПлатеж);
			Платеж.ПлатежноеПоручение = Выборка.ПлатежноеПоручение;
			Платеж.СуммаОплачено      = Выборка.СуммаДокумента;
			Платеж.Оплачено           = Выборка.Оплачено;
			Платеж.Состояние          = Выборка.Состояние;
		КонецЦикла;
		Если ПлатежиПоИндексу.Количество() > 0 И СуммаНалога <> 0 Тогда
			Коэффициенты = ПлатежиПоИндексу.ВыгрузитьКолонку("СуммаОплачено");
			РаспределеннаяСумма = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаНалога, Коэффициенты, 2);
			Если РаспределеннаяСумма = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ПлатежиПоИндексу.ЗагрузитьКолонку(РаспределеннаяСумма, "Сумма");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ПлатежиПоИндексу, ПлатежиВременный);
	КонецЦикла;

	Если ЗначениеЗаполнено(ПлатежиВременный) Тогда
		Платежи = ПлатежиВременный;
	КонецЕсли;
	Платежи.Колонки.Удалить("Индекс");
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.СпособУплатыНалогаВзноса
//
Процедура ПриОпределенииСпособаУплатыНалогаВзноса(Способ, Организация) Экспорт
	
	ЭтоЮрЛицо = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	Если ЭтоЮрЛицо Тогда
		Способ = Перечисления.СпособыУплатыНалогов.БанковскийПеревод;
	Иначе
		Если Справочники.БанковскиеСчета.КоличествоБанковскихСчетовОрганизации(Организация) > 0 Тогда
			// Возможно несколько способов оплаты
			Способ = Перечисления.СпособыУплатыНалогов.ПустаяСсылка();
		Иначе
			Способ = Перечисления.СпособыУплатыНалогов.НаличнымиПоКвитанции;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ДанныеДляУплатыНалогаПоДекларации
//
Процедура ПриПолученииДанныхДляУплатыНалогаПоДекларации(ДанныеДекларации, ПараметрыПолученияДанныхПоДекларации, ДополнительныеПараметры = Неопределено) Экспорт
	
	Правило = ПараметрыПолученияДанныхПоДекларации.Правило;
	Организация = ПараметрыПолученияДанныхПоДекларации.Организация;
	РегистрацияВНалоговомОргане = ПараметрыПолученияДанныхПоДекларации.РегистрацияВНалоговомОргане;
	ПериодСобытия = ПараметрыПолученияДанныхПоДекларации.ПериодСобытия;
	Срок = ПараметрыПолученияДанныхПоДекларации.Срок;
	ПоказательПериода = ПараметрыПолученияДанныхПоДекларации.ПоказательПериода;
	
	ЭтоНДС = (Правило.Владелец.Код = "НДС");
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, ПериодСобытия);
	
	// Удалим строки, не относящиеся к заданному периоду
	КоличествоСтрок = ДанныеДекларации.Платежи.Количество();
	
	ДанныеДекларации.Платежи.Колонки.Добавить("Налог", Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	ДанныеДекларации.Платежи.Колонки.Добавить("КодНалоговогоОргана", ОбщегоНазначения.ОписаниеТипаСтрока(20));
	ДанныеДекларации.Платежи.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(0));

	// Для НДС отдельно добавим итог по декларации
	Если ЭтоНДС Тогда
		ДанныеДекларации.Вставить("СуммаНДСПоДекларации", 0);   // 1 раздел декларации
		ДанныеДекларации.Вставить("СуммаНДСНАПоДекларации", 0); // 2 раздел декларации
	КонецЕсли;
	
	Для НомерСтроки = 1 По КоличествоСтрок Цикл
		Платеж = ДанныеДекларации.Платежи[КоличествоСтрок - НомерСтроки];
		Если ЭтоНДС Тогда
			// Особенность НДС в том, что он уплачивается несколькими платежами.
			// В данных декларации такие платежи отнесены к месяцам, в которых они уплачиваются.
			ПериодПлатежа = ПериодСобытия;
			Если Платеж.Агент Тогда
				ДанныеДекларации.СуммаНДСНАПоДекларации = ДанныеДекларации.СуммаНДСНАПоДекларации + Платеж.Сумма;
				// Платежи агентского НДС не обрабатываем.
				Продолжить;
			Иначе
				ДанныеДекларации.СуммаНДСПоДекларации = ДанныеДекларации.СуммаНДСПоДекларации + Платеж.Сумма;
			КонецЕсли;
		Иначе
			ПериодПлатежа = Платеж.Период;
		КонецЕсли;
	
		КБКЗаполнен = ЗначениеЗаполнено(Платеж.КБК);
		КодЗадачи = ВыполнениеЗадачБухгалтера.КодЗадачиПоПравилу(Правило);
	
		Если ЗначениеЗаполнено(Платеж.ВидНалога) Тогда
			Платеж.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Платеж.ВидНалога);
		ИначеЕсли КБКЗаполнен И КодЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПредприниматель() Тогда
			Платеж.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(
			Платеж.КБК, ,
			ПлатежиВБюджетКлиентСервер.СтатусПлательщикаНалоговИСборовИП());
		ИначеЕсли КБКЗаполнен Тогда
			Платеж.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(Платеж.КБК);
		Иначе
			ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(КодЗадачи, Организация, ПериодСобытия);
			Платеж.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);
		КонецЕсли;
	
		Если Не КБКЗаполнен Тогда
			Платеж.КБК = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Платеж.Налог, , КонецПериода);
		КонецЕсли;
	
		Платеж.Наименование = Строка(Платеж.Налог);
		Платеж.КодНалоговогоОргана = ?(ДанныеДекларации.Свойство("КодНалоговогоОргана"),
			ДанныеДекларации.КодНалоговогоОргана, "");
	
		ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(Платеж.ОКАТО, Платеж.Период);
	
		ЧужойПериод = ПериодПлатежа < НачалоПериода Или ПериодПлатежа > КонецПериода;
		Если ЧужойПериод Тогда
			ДанныеДекларации.Платежи.Удалить(Платеж);
		КонецЕсли;
	КонецЦикла;
	
	// Возможно, платежные документы уже созданы
	Если ДанныеДекларации.Платежи.Количество() > 0 Тогда
		СведенияПоДекларации = Новый Структура;
		СведенияПоДекларации.Вставить("Ссылка", ДанныеДекларации.Ссылка);
		СведенияПоДекларации.Вставить("ПоказательПериода", ПоказательПериода);
	
		Если ЭтоНДС Тогда
			Уплаты = ДанныеДекларации.Платежи.Скопировать(Новый Структура("Агент", Ложь));
		Иначе
			Уплаты = ДанныеДекларации.Платежи.Скопировать();
		КонецЕсли;
		
		ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами(
			Уплаты, Правило, ПериодСобытия, Организация, СведенияПоДекларации, РегистрацияВНалоговомОргане);
		
		Если УчетнаяПолитика.ПлательщикЕНП(Организация, Срок) Тогда
			РеквизитыЕдиногоНалоговогоПлатежа =
				ПомощникиПоУплатеНалоговИВзносов.РеквизитыЕдиногоНалоговогоПлатежа(Организация,
					Срок);
		
			КолонкаИсключение = "";
			Для Каждого Платеж Из Уплаты Цикл
				Если ЗначениеЗаполнено(Платеж.ПлатежноеПоручение) Тогда
					КолонкаИсключение = "Наименование";
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого Платеж Из Уплаты Цикл
				Если ЗначениеЗаполнено(Платеж.ПлатежноеПоручение) Тогда
					ЗаполнитьЗначенияСвойств(Платеж, РеквизитыЕдиногоНалоговогоПлатежа);
				Иначе
					ЗаполнитьЗначенияСвойств(Платеж, РеквизитыЕдиногоНалоговогоПлатежа, , КолонкаИсключение);
				КонецЕсли;
			КонецЦикла;
			Уплаты.Свернуть("Аванс, Агент, ВидНалога, КБК, КодНалоговогоОргана, Налог, ОКАТО, Период, Наименование, ПлатежноеПоручение, Оплачено", "Сумма, СуммаОплачено");
		КонецЕсли;
		
		ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами(
			Уплаты, Правило, ПериодСобытия, Организация, СведенияПоДекларации, РегистрацияВНалоговомОргане);
		
	Иначе
		Уплаты = ДанныеДекларации.Платежи.СкопироватьКолонки();
	КонецЕсли;
	
	ДанныеДекларации.Вставить("Уплаты", Уплаты);
	
	Если ЭтоНДС Тогда
		// Особенность НДС в том, что он уплачивается несколькими платежами.
		// В данных декларации такие платежи отнесены к месяцам, в которых они уплачиваются.
		Для Каждого Платеж Из ДанныеДекларации.Платежи Цикл
			Если НачалоМесяца(Платеж.Период) = НачалоМесяца(Срок) Тогда
				Платеж.Период = ПериодСобытия;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// Удалим строки, по которым нет платежей
	Если НЕ ЭтоНДС // Для задач уплаты и отчетности по НДС всегда возвращаем список платежей
		И ДанныеДекларации.Уплаты.Количество() > 1 Тогда
		КоличествоСтрок = ДанныеДекларации.Уплаты.Количество();
		Для НомерСтроки = 1 По КоличествоСтрок Цикл
			Платеж = ДанныеДекларации.Уплаты[КоличествоСтрок - НомерСтроки];
			Если Платеж.Сумма = 0 И Не ЗначениеЗаполнено(Платеж.ПлатежноеПоручение) Тогда
				ДанныеДекларации.Уплаты.Удалить(Платеж);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ДанныеДляУплатыСтраховыхВзносов
//
Процедура ПриПолученииДанныхДляУплатыСтраховыхВзносов(Платежи, Правило, Организация, ПериодСобытия, ДанныеЕНП, ВариантВыборкиСтраховыхВзносов) Экспорт
	
	ВариантыВыборкиСтраховыхВзносов = ВыполнениеЗадачБухгалтера.ВариантыВыборкиСтраховыхВзносов();
	
	НачалоПериода               = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Перечисления.Периодичность.Месяц, КонецМесяца(ПериодСобытия)+1);
	НачалоПредыдущегоПериода    = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Перечисления.Периодичность.Месяц, ПериодСобытия);
	
	ПараметрыСтраховыхВзносов = ВыполнениеЗадачБухгалтера.ПараметрыСтраховыхВзносов(ПериодСобытия);
	СчетаСтраховыхВзносов = УчетЗарплаты.СчетаУчетаСтраховыхВзносов(ПериодСобытия, ВариантВыборкиСтраховыхВзносов, ПараметрыСтраховыхВзносов);
	
	ЗначенияСубконто = Новый Массив;
	ЗначенияСубконто.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Налог);
	Если ВариантВыборкиСтраховыхВзносов <> ВариантыВыборкиСтраховыхВзносов.Травматизм
		И ВариантВыборкиСтраховыхВзносов <> ВариантыВыборкиСтраховыхВзносов.ДобровольныеВзносы Тогда
		ЗначенияСубконто.Добавить(Перечисления.ВидыПлатежейВГосБюджет.ВзносыБезСпецоценки);
	КонецЕсли;
	
	ВзносыВнеЕНП =
		ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.Травматизм
		Или ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.ДопВзносыЛетчикиШахтеры
		Или ВариантВыборкиСтраховыхВзносов = ВариантыВыборкиСтраховыхВзносов.ДобровольныеВзносы;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",              Организация);
	Запрос.УстановитьПараметр("НачалоПериода",            НачалоПериода);
	Запрос.УстановитьПараметр("НачалоПредыдущегоПериода", НачалоПредыдущегоПериода);
	Запрос.УстановитьПараметр("СчетаСтраховыхВзносов",    СчетаСтраховыхВзносов);
	Запрос.УстановитьПараметр("ВидСубконто",              ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет);
	Запрос.УстановитьПараметр("ЗначенияСубконто",         ЗначенияСубконто);
	Запрос.УстановитьПараметр("НачалоПростогоУчета",      ЕдиныйНалоговыйСчет.НачалоПростогоУчета());
	Запрос.УстановитьПараметр("ВзносыВнеЕНП",             ВзносыВнеЕНП);

	// Данные, уже переданные в уведомлении (Дт 68.90 - Кт 69.хх)
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.СчетУчета КАК СчетУчета,
	|	СУММА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.Сумма) КАК Сумма,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог КАК Налог,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.КодБК КАК КБК,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.КодПоОКТМО КАК ОКАТО,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Организация КАК Организация,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Дата КАК Дата
	|ИЗ
	|	Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомлениеОбИсчисленныхСуммахНалоговНалоги
	|ГДЕ
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.СчетУчета В(&СчетаСтраховыхВзносов)
	|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Проведен
	|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Организация = &Организация
	|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Дата < НАЧАЛОПЕРИОДА(&НачалоПростогоУчета, МЕСЯЦ)
	|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Дата МЕЖДУ &НачалоПредыдущегоПериода И КОНЕЦПЕРИОДА(&НачалоПериода, МЕСЯЦ)
	|	И НАЧАЛОПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.СрокУплаты, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&НачалоПериода, МЕСЯЦ)
	|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.СрокУплаты < НАЧАЛОПЕРИОДА(&НачалоПростогоУчета, МЕСЯЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.СчетУчета,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.КодБК,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.КодПоОКТМО,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Организация,
	|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Дата";

	ТаблицаПоУведомлениям = Запрос.Выполнить().Выгрузить();
	ТаблицаПоУведомлениям.Колонки.Добавить("ВидНалоговогоОбязательства");

	ВидыНалогов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПоУведомлениям, "Налог", Истина), "ВидНалога");

	Для Каждого СтрокаТаблицы Из ТаблицаПоУведомлениям Цикл
		СтрокаТаблицы.ВидНалоговогоОбязательства =
			Справочники.ВидыНалоговИПлатежейВБюджет.ВидНалоговогоОбязательстваПоКБК(СтрокаТаблицы.КБК,
				ПериодСобытия,
				ВидыНалогов[СтрокаТаблицы.Налог]);
	КонецЦикла;
	
	// Остатки расчетов по взносам
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии") Тогда
	
		ТаблицаВзносов = УчетЗарплаты.ИсчисленныеСтраховыеВзносыЗаПериод(Организация, НачалоПредыдущегоПериода, ВариантВыборкиСтраховыхВзносов);
		Запрос.УстановитьПараметр("ТаблицаВзносов", ТаблицаВзносов);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаВзносов.СчетУчета КАК СчетУчета,
		|	ТаблицаВзносов.ВидНалоговогоОбязательства КАК ВидНалоговогоОбязательства,
		|	ТаблицаВзносов.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВТ_ТаблицаВзносов
		|ИЗ
		|	&ТаблицаВзносов КАК ТаблицаВзносов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТаблицаВзносов.СчетУчета КАК СчетУчета,
		|	ВТ_ТаблицаВзносов.ВидНалоговогоОбязательства КАК ВидНалоговогоОбязательства,
		|	ВТ_ТаблицаВзносов.Сумма КАК Сумма
		|ИЗ
		|	ВТ_ТаблицаВзносов КАК ВТ_ТаблицаВзносов
		|ГДЕ
		|	ВТ_ТаблицаВзносов.СчетУчета В(&СчетаСтраховыхВзносов)
		|	И ВТ_ТаблицаВзносов.ВидНалоговогоОбязательства В(&ЗначенияСубконто)";
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОтражениеЗарплатыВБухучете.Организация КАК Организация,
		|	ОтражениеЗарплатыВБухучете.ПериодРегистрации КАК ПериодРегистрации,
		|	ОтражениеЗарплатыВБухучете.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Регистраторы
		|ИЗ
		|	Документ.ОтражениеЗарплатыВБухучете КАК ОтражениеЗарплатыВБухучете
		|ГДЕ
		|	ОтражениеЗарплатыВБухучете.Организация = &Организация
		|	И ОтражениеЗарплатыВБухучете.ПериодРегистрации = &НачалоПредыдущегоПериода
		|	И НЕ ОтражениеЗарплатыВБухучете.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтражениеЗарплатыВБухучете.Ссылка,
		|	ОтражениеЗарплатыВБухучете.Организация,
		|	ОтражениеЗарплатыВБухучете.ПериодРегистрации
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОтражениеЗарплатыВУчете.Организация,
		|	ОтражениеЗарплатыВУчете.ПериодРегистрации,
		|	ОтражениеЗарплатыВУчете.Ссылка
		|ИЗ
		|	Документ.ОтражениеЗарплатыВУчете КАК ОтражениеЗарплатыВУчете
		|ГДЕ
		|	ОтражениеЗарплатыВУчете.Организация = &Организация
		|	И ОтражениеЗарплатыВУчете.ПериодРегистрации = &НачалоПредыдущегоПериода
		|	И НЕ ОтражениеЗарплатыВУчете.ПометкаУдаления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Счет КАК СчетУчета,
		|	ХозрасчетныйОбороты.Субконто1 КАК ВидНалоговогоОбязательства,
		|	СУММА(ВЫБОР
		|			КОГДА &ВзносыВнеЕНП
		|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт
		|			ИНАЧЕ ХозрасчетныйОбороты.СуммаОборотДт
		|		КОНЕЦ) КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПредыдущегоПериода,
		|			,
		|			Регистратор,
		|			Счет В (&СчетаСтраховыхВзносов),
		|			&ВидСубконто,
		|			Организация = &Организация
		|				И Субконто1 В (&ЗначенияСубконто),
		|			ВЫБОР
		|				КОГДА &ВзносыВнеЕНП
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет)
		|			КОНЕЦ,
		|			) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	ХозрасчетныйОбороты.Регистратор В
		|			(ВЫБРАТЬ
		|				ВТ_Регистраторы.Ссылка
		|			ИЗ
		|				ВТ_Регистраторы)
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Счет,
		|	ХозрасчетныйОбороты.Субконто1";
	КонецЕсли;

	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();

	КодТерритории = Справочники.Организации.КодТерриторииМестаРегистрации(Организация);
	ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(КодТерритории, ПериодСобытия);
	КодыГосударственныхОрганов = Новый Соответствие; // Кешируем в функции

	СтрокиКУдалению = Новый Массив;

	Платежи = ВыполнениеЗадачБухгалтера.НовыйТаблицаПлатежи(); 
	Пока Выборка.Следующий() Цикл
		Платеж = Платежи.Добавить();
		ЗаполнитьЗначенияСвойств(Платеж, Выборка);
		Платеж.Организация = Организация;
		Платеж.ОКАТО       = КодТерритории;
		СтруктураПоиска = Новый Структура("СчетУчета, ВидНалоговогоОбязательства");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);

		СтрокиПоУведомлениям = ТаблицаПоУведомлениям.НайтиСтроки(СтруктураПоиска);
		СуммаПоУведомлениям = 0;
		Для Каждого СтрокаПоУведомлению Из СтрокиПоУведомлениям Цикл
			Если НачалоМесяца(СтрокаПоУведомлению.Дата) = НачалоПредыдущегоПериода Тогда
				СуммаПоУведомлениям = СуммаПоУведомлениям + СтрокаПоУведомлению.Сумма;
			КонецЕсли;
			СтрокиКУдалению.Добавить(СтрокаПоУведомлению);
		КонецЦикла;
		
		Платеж.Сумма = Платеж.Сумма + СуммаПоУведомлениям;
		
		РеквизитыПоСчету = ПараметрыСтраховыхВзносов.НайтиСтроки(СтруктураПоиска);
		Если РеквизитыПоСчету.Количество() <> 0 Тогда
			ЗаполнитьЗначенияСвойств(Платеж, РеквизитыПоСчету[0], "КБК, Налог");
			
			ПравилаЗаполнения = ПлатежиВБюджетНастройки.ПравилаЗаполненияРеквизитовПлатежа(Платеж.Налог);
			Если ПравилаЗаполнения.СохранятьКодТерриторииПриЗаписи Тогда
				КлючНастройкиУплатыНалога = РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.КлючНастройкиУплатыНалога(
					Платеж.Налог, Платеж.Организация, Неопределено);
				Если ЗначениеЗаполнено(КлючНастройкиУплатыНалога) Тогда
				ДанныеЗаполнения = РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.ДанныеЗаполнения(
					КлючНастройкиУплатыНалога, ПериодСобытия, Платеж.Организация);
					Если ЗначениеЗаполнено(ДанныеЗаполнения)
						И ДанныеЗаполнения.Свойство("КодОКАТО") И ЗначениеЗаполнено(ДанныеЗаполнения.КодОКАТО) Тогда
						ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(ДанныеЗаполнения.КодОКАТО, ПериодСобытия);
						Платеж.ОКАТО = ДанныеЗаполнения.КодОКАТО;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ТаблицаПоУведомлениям.Удалить(СтрокаКУдалению);
	КонецЦикла;

	Для Каждого СтрокаТаблицы Из ТаблицаПоУведомлениям Цикл
		Платеж = Платежи.Добавить();
		ЗаполнитьЗначенияСвойств(Платеж, СтрокаТаблицы);
	КонецЦикла;
	
	Для Каждого Платеж Из Платежи Цикл
		ШаблонНаименования    = НСтр("ru = '%1%2'");
		НаименованиеНалога    = Строка(Платеж.Налог);
		УточнениеНаименования = "";
		Если Платеж.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыБезСпецоценки Тогда
			УточнениеНаименования = НСтр("ru = ' (без спецоценки)'");
		КонецЕсли;
		Платеж.Наименование = СтрШаблон(ШаблонНаименования, НаименованиеНалога, УточнениеНаименования);

		ВидГосударственногоОргана = ПлатежиВБюджетПереопределяемый.ВидГосударственногоОргана(Платеж.КБК);
		КодГосударственногоОргана = КодыГосударственныхОрганов[ВидГосударственногоОргана];
		Если КодГосударственногоОргана = Неопределено Тогда
			КодГосударственногоОргана =
				ДанныеГосударственныхОрганов.КодГосударственногоОрганаОрганизации(Организация, ВидГосударственногоОргана);
			КодыГосударственныхОрганов.Вставить(ВидГосударственногоОргана, КодГосударственногоОргана);
		КонецЕсли;
		Платеж.КодНалоговогоОргана = КодГосударственногоОргана;
	КонецЦикла;
	
	Если Платежи.Количество() > 0 Тогда
	
		ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами(Платежи, Правило, ПериодСобытия, Организация);
	
		Если ДанныеЕНП И Не ВзносыВнеЕНП Тогда
	
			Срок = ВыполнениеЗадачБухгалтера.СрокВыполненияЗадачи(Организация, Правило, ПериодСобытия);
			НалогиУплачиваемыеОтдельно = ЕдиныйНалоговыйСчетПовтИсп.НалогиУплачиваемыеОтдельно(Срок);
			КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Перечисления.Периодичность.Месяц, Срок);

			ГоловнаяОрганизация = БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(Организация);

			РеквизитыЕдиногоНалоговогоПлатежа =
				ПомощникиПоУплатеНалоговИВзносов.РеквизитыЕдиногоНалоговогоПлатежа(ГоловнаяОрганизация,
					Срок);

			Для Каждого Платеж Из Платежи Цикл
				Если НалогиУплачиваемыеОтдельно.Найти(Платеж.Налог) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Если Не Платеж.Оплачено Тогда
					ЗаполнитьЗначенияСвойств(Платеж, РеквизитыЕдиногоНалоговогоПлатежа);
					Платеж.ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
				КонецЕсли;
			КонецЦикла;
			Платежи.Свернуть("Аванс, ВидНалоговогоОбязательства, КБК, КодНалоговогоОргана, Налог, ОКАТО, Период, Наименование, ПлатежноеПоручение, Оплачено", "Сумма, СуммаОплачено");
			
			ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами(Платежи, Правило, ПериодСобытия, Организация);
			
			КоличествоСтрок = Платежи.Количество();
			Для НомерСтроки = 1 По КоличествоСтрок Цикл
				Платеж = Платежи[КоличествоСтрок - НомерСтроки];
				Если Платеж.Сумма = 0 И Не ЗначениеЗаполнено(Платеж.ПлатежноеПоручение) Тогда
					Платежи.Удалить(Платеж);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли
	КонецЕсли;
	
	Если Платежи.Колонки.Найти("НалоговыйПериод") = Неопределено Тогда
		Платежи.Колонки.Добавить("НалоговыйПериод", Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ДанныеДляУплатыИмущественныхНалогов
//
Процедура ПриПолученииДанныхДляУплатыИмущественныхНалогов(Платежи, ПараметрыЗадачи, ДанныеЕНП, ГруппироватьПоНалоговойБазе) Экспорт
	
	ИмяЗадачи     = ПараметрыЗадачи.ИдентификаторЗадачи;
	// Актуальный расчет налога всегда находится в интервале последнего квартала
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
		Перечисления.Периодичность.Квартал, ПараметрыЗадачи.ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
		Перечисления.Периодичность.Квартал, ПараметрыЗадачи.ПериодСобытия);
	ВидНалога     = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(ИмяЗадачи, ПараметрыЗадачи.Организация, КонецПериода);
	Налог         = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);
	КБК           = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог, , КонецПериода);
	
	КодНалоговогоОргана = ДанныеГосударственныхОрганов.КодГосударственногоОрганаОрганизации(
		ПараметрыЗадачи.Организация,
		Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",    ПараметрыЗадачи.Организация);
	Запрос.УстановитьПараметр("НачалоПериода",  НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",   КонецПериода);
	Запрос.УстановитьПараметр("НалоговыйОрган", ПараметрыЗадачи.РегистрацияВНалоговомОргане);
	
	Если ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество()
		И (ПараметрыЗадачи.ПериодСобытия < РасчетИмущественныхНалогов.НачалоПримененияНовогоРасчетаНалогаНаИмущество()
			Или РасчетИмущественныхНалоговПереопределяемый.ИспользоватьПрежнийРасчетНалогаНаИмуществоВПереходныйПериод(
				ПараметрыЗадачи.Организация, КонецПериода)) Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетНалогаНаИмущество.ПериодРасчета КАК Период,
		|	РасчетНалогаНаИмущество.КодПоОКТМО КАК ОКАТО,
		|	СУММА(РасчетНалогаНаИмущество.СуммаНалогаКУплате) КАК Сумма,
		|	ЕСТЬNULL(РасчетНалогаНаИмущество.ИФНС.Код, """") КАК КодНалоговогоОргана,
		|	РасчетНалогаНаИмущество.КБК КАК КБК
		|ИЗ
		|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
		|ГДЕ
		|	РасчетНалогаНаИмущество.Организация = &Организация
		|	И РасчетНалогаНаИмущество.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РасчетНалогаНаИмущество.ИФНС = &НалоговыйОрган
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетНалогаНаИмущество.ПериодРасчета,
		|	РасчетНалогаНаИмущество.КодПоОКТМО,
		|	РасчетНалогаНаИмущество.КБК,
		|	ЕСТЬNULL(РасчетНалогаНаИмущество.ИФНС.Код, """")
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетНалогаНаИмущество.СуммаНалогаКУплате) > 0";
		
	ИначеЕсли ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаИмущество() Тогда

		// В некоторых случаях (это определяется вне данного метода) в сумму уведомления
		// включается только налог по кадастровой стоимости (см. параметр ГруппироватьПоНалоговойБазе).
		// Но даже если требуется группировка по налоговой базе, то данные нужно выбрать только по тем
		// получателям платежа (подразумевается сочетание кода налогового органа, ОКТМО и КБК),
		// где не возникло переплаты с учетом авансов (общая сумма налога к уплате больше 0).
		// Для этого используем временную таблицу ФильтрПоПолучателямПлатежа - ее группировки не зависят
		// от группировок результирующей таблицы.
		// Не индексируем ФильтрПоПолучателямПлатежа, т.к. не ожидаем в ней большое количество записей.
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ПериодРасчета КАК ПериодРасчета,
		|	ВЫБОР
		|		КОГДА &ГруппироватьПоНалоговойБазе
		|			ТОГДА ""СреднегодоваяСтоимость""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НалоговаяБаза,
		|	РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.КодПоОКТМО КАК КодПоОКТМО,
		|	РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.СуммаНалогаКУплате КАК СуммаНалогаКУплате,
		|	ВЫБОР
		|		КОГДА &ГруппироватьПоНалоговойБазе
		|			ТОГДА 0
		|		ИНАЧЕ РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.СуммаНалогаКУплате
		|	КОНЕЦ КАК СуммаУведомления,
		|	ЕСТЬNULL(РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ИФНС.Код, """") КАК КодНалоговогоОргана,
		|	ВЫБОР
		|		КОГДА РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.КБК = """"
		|			ТОГДА &КБК
		|		ИНАЧЕ РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.КБК
		|	КОНЕЦ КАК КБК
		|ПОМЕСТИТЬ ДанныеРасчета
		|ИЗ
		|	РегистрСведений.РасчетНалогаНаИмуществоПоСреднегодовойСтоимости КАК РасчетНалогаНаИмуществоПоСреднегодовойСтоимости
		|ГДЕ
		|	РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.Организация = &Организация
		|	И РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ИФНС = &НалоговыйОрган
		|	И (НЕ &ГруппироватьПоНалоговойБазе
		|			ИЛИ &Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ПериодРасчета,
		|	ВЫБОР
		|		КОГДА &ГруппироватьПоНалоговойБазе
		|			ТОГДА ""КадастроваяСтоимость""
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.КодПоОКТМО,
		|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.СуммаНалогаКУплате,
		|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.СуммаНалогаКУплате,
		|	ЕСТЬNULL(РасчетНалогаНаИмуществоПоКадастровойСтоимости.ИФНС.Код, """"),
		|	&КБК
		|ИЗ
		|	РегистрСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости КАК РасчетНалогаНаИмуществоПоКадастровойСтоимости
		|ГДЕ
		|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.Организация = &Организация
		|	И РасчетНалогаНаИмуществоПоКадастровойСтоимости.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РасчетНалогаНаИмуществоПоКадастровойСтоимости.ИФНС = &НалоговыйОрган
		|	И (НЕ &ГруппироватьПоНалоговойБазе
		|			ИЛИ &Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРасчета.ПериодРасчета КАК ПериодРасчета,
		|	ДанныеРасчета.КодПоОКТМО КАК КодПоОКТМО,
		|	ДанныеРасчета.КодНалоговогоОргана КАК КодНалоговогоОргана,
		|	ДанныеРасчета.КБК КАК КБК
		|ПОМЕСТИТЬ ФильтрПоПолучателямПлатежа
		|ИЗ
		|	ДанныеРасчета КАК ДанныеРасчета
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРасчета.ПериодРасчета,
		|	ДанныеРасчета.КодПоОКТМО,
		|	ДанныеРасчета.КБК,
		|	ДанныеРасчета.КодНалоговогоОргана
		|
		|ИМЕЮЩИЕ
		|	СУММА(ДанныеРасчета.СуммаНалогаКУплате) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРасчета.ПериодРасчета КАК Период,
		|	ДанныеРасчета.НалоговаяБаза КАК НалоговаяБаза,
		|	ДанныеРасчета.КодПоОКТМО КАК ОКАТО,
		|	СУММА(ДанныеРасчета.СуммаНалогаКУплате) КАК Сумма,
		|	СУММА(ДанныеРасчета.СуммаУведомления) КАК СуммаУведомления,
		|	ДанныеРасчета.КодНалоговогоОргана КАК КодНалоговогоОргана,
		|	ДанныеРасчета.КБК КАК КБК
		|ИЗ
		|	ДанныеРасчета КАК ДанныеРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФильтрПоПолучателямПлатежа КАК Фильтр
		|		ПО ДанныеРасчета.ПериодРасчета = Фильтр.ПериодРасчета
		|			И ДанныеРасчета.КодПоОКТМО = Фильтр.КодПоОКТМО
		|			И ДанныеРасчета.КодНалоговогоОргана = Фильтр.КодНалоговогоОргана
		|			И ДанныеРасчета.КБК = Фильтр.КБК
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРасчета.ПериодРасчета,
		|	ДанныеРасчета.НалоговаяБаза,
		|	ДанныеРасчета.КодПоОКТМО,
		|	ДанныеРасчета.КБК,
		|	ДанныеРасчета.КодНалоговогоОргана";

		// Если КБК не указан явно в расчете налога (в настройках объектов), то берем КБК по имуществу,
		// не входящему в ЕСГС
		КБК = ?(
			ЗначениеЗаполнено(КБК),
			КБК,
			РегистрыСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КБКПоИмуществуНеВходящемуВЕСГС()
			);
		Запрос.УстановитьПараметр("КБК", КБК);
		Запрос.УстановитьПараметр("ГруппироватьПоНалоговойБазе", ГруппироватьПоНалоговойБазе);
		Запрос.УстановитьПараметр("Действие", ПараметрыЗадачи.Действие);
		
	ИначеЕсли ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог()
		И ПараметрыЗадачи.ПериодСобытия < РасчетИмущественныхНалоговКлиентСервер.НачалоПримененияНовогоРасчетаТранспортногоНалога() Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетТранспортногоНалога.ПериодРасчета КАК Период,
		|	РасчетТранспортногоНалога.КодПоОКТМО КАК ОКАТО,
		|	СУММА(РасчетТранспортногоНалога.СуммаНалогаКУплате) КАК Сумма,
		|	ЕСТЬNULL(РасчетТранспортногоНалога.ИФНС.Код, """") КАК КодНалоговогоОргана,
		|	"""" КАК КБК
		|ИЗ
		|	РегистрСведений.РасчетТранспортногоНалогаДо2020 КАК РасчетТранспортногоНалога
		|ГДЕ
		|	РасчетТранспортногоНалога.Организация = &Организация
		|	И РасчетТранспортногоНалога.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РасчетТранспортногоНалога.ИФНС = &НалоговыйОрган
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетТранспортногоНалога.ПериодРасчета,
		|	РасчетТранспортногоНалога.КодПоОКТМО,
		|	ЕСТЬNULL(РасчетТранспортногоНалога.ИФНС.Код, """")
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетТранспортногоНалога.СуммаНалогаКУплате) > 0";
		
	ИначеЕсли ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиТранспортныйНалог() Тогда

		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетТранспортногоНалога.ПериодРасчета КАК Период,
		|	РасчетТранспортногоНалога.КодПоОКТМО КАК ОКАТО,
		|	СУММА(РасчетТранспортногоНалога.СуммаНалогаКУплате) КАК Сумма,
		|	ЕСТЬNULL(РасчетТранспортногоНалога.ИФНС.Код, """") КАК КодНалоговогоОргана,
		|	"""" КАК КБК
		|ИЗ
		|	РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
		|ГДЕ
		|	РасчетТранспортногоНалога.Организация = &Организация
		|	И РасчетТранспортногоНалога.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РасчетТранспортногоНалога.ИФНС = &НалоговыйОрган
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетТранспортногоНалога.ПериодРасчета,
		|	РасчетТранспортногоНалога.КодПоОКТМО,
		|	ЕСТЬNULL(РасчетТранспортногоНалога.ИФНС.Код, """")
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетТранспортногоНалога.СуммаНалогаКУплате) > 0";

	ИначеЕсли ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог()
		И ПараметрыЗадачи.ПериодСобытия < РасчетИмущественныхНалоговКлиентСервер.НачалоПримененияНовогоРасчетаЗемельногоНалога() Тогда

		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетЗемельногоНалога.ПериодРасчета КАК Период,
		|	РасчетЗемельногоНалога.КодПоОКТМО КАК ОКАТО,
		|	СУММА(РасчетЗемельногоНалога.СуммаНалогаКУплате) КАК Сумма,
		|	РасчетЗемельногоНалога.КБК КАК КБК,
		|	ЕСТЬNULL(РасчетЗемельногоНалога.ИФНС.Код, """") КАК КодНалоговогоОргана
		|ИЗ
		|	РегистрСведений.РасчетЗемельногоНалогаДо2020 КАК РасчетЗемельногоНалога
		|ГДЕ
		|	РасчетЗемельногоНалога.Организация = &Организация
		|	И РасчетЗемельногоНалога.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РасчетЗемельногоНалога.ИФНС = &НалоговыйОрган
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетЗемельногоНалога.ПериодРасчета,
		|	РасчетЗемельногоНалога.КодПоОКТМО,
		|	РасчетЗемельногоНалога.КБК,
		|	ЕСТЬNULL(РасчетЗемельногоНалога.ИФНС.Код, """")
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетЗемельногоНалога.СуммаНалогаКУплате) > 0";

	ИначеЕсли ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиЗемельныйНалог() Тогда

		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетЗемельногоНалога.ПериодРасчета КАК Период,
		|	РасчетЗемельногоНалога.КодПоОКТМО КАК ОКАТО,
		|	СУММА(РасчетЗемельногоНалога.СуммаНалогаКУплате) КАК Сумма,
		|	РасчетЗемельногоНалога.КБК КАК КБК,
		|	ЕСТЬNULL(РасчетЗемельногоНалога.ИФНС.Код, """") КАК КодНалоговогоОргана
		|ИЗ
		|	РегистрСведений.РасчетЗемельногоНалога КАК РасчетЗемельногоНалога
		|ГДЕ
		|	РасчетЗемельногоНалога.Организация = &Организация
		|	И РасчетЗемельногоНалога.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РасчетЗемельногоНалога.ИФНС = &НалоговыйОрган
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетЗемельногоНалога.ПериодРасчета,
		|	РасчетЗемельногоНалога.КодПоОКТМО,
		|	РасчетЗемельногоНалога.КБК,
		|	ЕСТЬNULL(РасчетЗемельногоНалога.ИФНС.Код, """")
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетЗемельногоНалога.СуммаНалогаКУплате) > 0";

	КонецЕсли;

	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();

	Платежи = ВыполнениеЗадачБухгалтера.НовыйТаблицаПлатежи();

	Пока Выборка.Следующий() Цикл

		Платеж = Платежи.Добавить();
		ЗаполнитьЗначенияСвойств(Платеж, Выборка);

		Если Не ГруппироватьПоНалоговойБазе Тогда
			Платеж.СуммаУведомления = Платеж.Сумма;
		КонецЕсли;

		Платеж.КБК = ?(ЗначениеЗаполнено(Выборка.КБК), Выборка.КБК, КБК);

		Если ЗначениеЗаполнено(Налог) Тогда
			Платеж.Налог = Налог;
		ИначеЕсли ЗначениеЗаполнено(Платеж.КБК) Тогда
			Платеж.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(Платеж.КБК);
		КонецЕсли;

		Платеж.Наименование = Строка(Платеж.Налог);
		Если ГруппироватьПоНалоговойБазе Тогда
			ТекстШаблона = НСтр("ru='%1 %2'");
			Если Выборка.НалоговаяБаза = "СреднегодоваяСтоимость" Тогда
				Платеж.Наименование = СтрШаблон(
					ТекстШаблона,
					Платеж.Наименование,
					НСтр("ru='по среднегодовой стоимости'"));
			ИначеЕсли Выборка.НалоговаяБаза = "КадастроваяСтоимость" Тогда
				Платеж.Наименование = СтрШаблон(
					ТекстШаблона,
					Платеж.Наименование,
					НСтр("ru='по кадастровой стоимости'"));
			КонецЕсли;
		КонецЕсли;
		
		Платеж.КодНалоговогоОргана = ?(ЗначениеЗаполнено(Выборка.КодНалоговогоОргана),
			Выборка.КодНалоговогоОргана, КодНалоговогоОргана);
		Платеж.Аванс = Истина;
		
		ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(Платеж.ОКАТО, Платеж.Период);

	КонецЦикла;

	Если Платежи.Количество() > 0 Тогда

		ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами(
			Платежи,
			ПараметрыЗадачи.Правило,
			ПараметрыЗадачи.ПериодСобытия,
			ПараметрыЗадачи.Организация,
			,
			ПараметрыЗадачи.РегистрацияВНалоговомОргане);

		Если ДанныеЕНП Тогда

			ГоловнаяОрганизация = БухгалтерскийУчетПереопределяемый.ГоловнаяОрганизация(ПараметрыЗадачи.Организация);

			РеквизитыЕдиногоНалоговогоПлатежа =
				ПомощникиПоУплатеНалоговИВзносов.РеквизитыЕдиногоНалоговогоПлатежа(ГоловнаяОрганизация,
					ПараметрыЗадачи.Срок);

			Для Каждого Платеж Из Платежи Цикл
				Если Не Платеж.Оплачено Тогда
					ЗаполнитьЗначенияСвойств(Платеж, РеквизитыЕдиногоНалоговогоПлатежа);
				КонецЕсли;
			КонецЦикла;
			Платежи.Свернуть("Аванс, ВидНалоговогоОбязательства, КБК, КодНалоговогоОргана, Налог, ОКАТО, Период, Наименование, ПлатежноеПоручение, Оплачено", "Сумма, СуммаОплачено");

			ВыполнениеЗадачБухгалтера.ДополнитьНачисленияПлатежнымиДокументами(
				Платежи,
				ПараметрыЗадачи.Правило,
				ПараметрыЗадачи.ПериодСобытия,
				ПараметрыЗадачи.Организация,
				,
				ПараметрыЗадачи.РегистрацияВНалоговомОргане);

			КоличествоСтрок = Платежи.Количество();
			Для НомерСтроки = 1 По КоличествоСтрок Цикл
				Платеж = Платежи[КоличествоСтрок - НомерСтроки];
				Если Платеж.Сумма = 0 И Не ЗначениеЗаполнено(Платеж.ПлатежноеПоручение) Тогда
					Платежи.Удалить(Платеж);
				КонецЕсли;
			КонецЦикла;

		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ДанныеДляУведомлений
//
Процедура ПриПолученииДанныхДляУведомлений(ТаблицаУведомлений, Правила, Организация, ПериодСобытия, РегистрацияВНалоговомОргане = Неопределено, ЭтоПериодПоНДФЛКонецМесяца = Ложь) Экспорт
	
	ВидыКодыНалогов = РасчетыСБюджетом.ТаблицаСоответствияЗадачБухгалтераДляУведомлений();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                 Организация);
	Запрос.УстановитьПараметр("Правила",                     Правила);
	Запрос.УстановитьПараметр("ПериодСобытия",               ПериодСобытия);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	Запрос.УстановитьПараметр("ВидыКодыНалогов",             ВидыКодыНалогов);
	Запрос.УстановитьПараметр("ЭтоПериодПоНДФЛКонецМесяца",  ЭтоПериодПоНДФЛКонецМесяца);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыКодыНалогов.ВидНалога КАК ВидНалога,
	|	ВидыКодыНалогов.КодЗадачи КАК КодНалога
	|ПОМЕСТИТЬ ВидыКодыНалогов
	|ИЗ
	|	&ВидыКодыНалогов КАК ВидыКодыНалогов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидНалога
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Налог
	|ПОМЕСТИТЬ НалогиУведомления
	|ИЗ
	|	ВидыКодыНалогов КАК ВидыКодыНалогов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО ВидыКодыНалогов.КодНалога = ЗадачиБухгалтера.Код
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО (ПравилаПредставленияОтчетовУплатыНалогов.Владелец = ЗадачиБухгалтера.Ссылка)
	|			И (ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление))
	|			И (ПравилаПредставленияОтчетовУплатыНалогов.Ссылка В (&Правила))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|		ПО (ВидыНалоговИПлатежейВБюджет.ВидНалога = ВидыКодыНалогов.ВидНалога)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УведомлениеОбИсчисленныхСуммахНалогов.Ссылка КАК Уведомление,
	|	УведомлениеОбИсчисленныхСуммахНалогов.Дата КАК Дата,
	|	СУММА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.Сумма) КАК Сумма
	|ИЗ
	|	Документ.УведомлениеОбИсчисленныхСуммахНалогов КАК УведомлениеОбИсчисленныхСуммахНалогов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомлениеОбИсчисленныхСуммахНалоговНалоги
	|		ПО УведомлениеОбИсчисленныхСуммахНалогов.Ссылка = УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка
	|			И (ВЫБОР
	|				КОГДА &ЭтоПериодПоНДФЛКонецМесяца
	|					ТОГДА НАЧАЛОПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода, ДЕНЬ)
	|							И УведомлениеОбИсчисленныхСуммахНалоговНалоги.КонецПериода = &ПериодСобытия
	|				ИНАЧЕ НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.КонецПериода, МЕСЯЦ), ДЕНЬ) = &ПериодСобытия
	|						И НАЧАЛОПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода, МЕСЯЦ) = НАЧАЛОПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода, ДЕНЬ)
	|			КОНЕЦ)
	|			И (&УсловиеРегистрацияВНалоговомОрганеУведомления)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НалогиУведомления КАК НалогиУведомления
	|		ПО (НалогиУведомления.Налог = УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог)
	|ГДЕ
	|	УведомлениеОбИсчисленныхСуммахНалогов.Организация = &Организация
	|	И НЕ УведомлениеОбИсчисленныхСуммахНалогов.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	УведомлениеОбИсчисленныхСуммахНалогов.Ссылка,
	|	УведомлениеОбИсчисленныхСуммахНалогов.Дата
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераУведомления.Уведомление,
	|	ОперацияПоЕдиномуНалоговомуСчету.Дата,
	|	ОперацияПоЕдиномуНалоговомуСчету.СуммаДокумента
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераУведомления КАК ЗадачиБухгалтераУведомления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОперацияПоЕдиномуНалоговомуСчету КАК ОперацияПоЕдиномуНалоговомуСчету
	|		ПО ЗадачиБухгалтераУведомления.Уведомление = ОперацияПоЕдиномуНалоговомуСчету.Ссылка
	|ГДЕ
	|	ЗадачиБухгалтераУведомления.Организация = &Организация
	|	И ЗадачиБухгалтераУведомления.Правило В(&Правила)
	|	И ЗадачиБухгалтераУведомления.ПериодСобытия = &ПериодСобытия
	|	И &УсловиеРегистрацияВНалоговомОргане
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераУведомления.Уведомление,
	|	ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.Дата,
	|	ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.СуммаДокумента
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераУведомления КАК ЗадачиБухгалтераУведомления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности КАК ЗаявлениеОЗачетеВСчетПредстоящейОбязанности
	|		ПО ЗадачиБухгалтераУведомления.Уведомление = ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.Ссылка
	|ГДЕ
	|	ЗадачиБухгалтераУведомления.Организация = &Организация
	|	И ЗадачиБухгалтераУведомления.Правило В(&Правила)
	|	И ЗадачиБухгалтераУведомления.ПериодСобытия = &ПериодСобытия
	|	И &УсловиеРегистрацияВНалоговомОргане
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераУведомления.Уведомление,
	|	УведомлениеОбИсчисленныхСуммахНалогов.Дата,
	|	УведомлениеОбИсчисленныхСуммахНалогов.СуммаДокумента
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераУведомления КАК ЗадачиБухгалтераУведомления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбИсчисленныхСуммахНалогов КАК УведомлениеОбИсчисленныхСуммахНалогов
	|		ПО ЗадачиБухгалтераУведомления.Уведомление = УведомлениеОбИсчисленныхСуммахНалогов.Ссылка
	|ГДЕ
	|	ЗадачиБухгалтераУведомления.Организация = &Организация
	|	И ЗадачиБухгалтераУведомления.Правило В(&Правила)
	|	И ЗадачиБухгалтераУведомления.ПериодСобытия = &ПериодСобытия
	|	И &УсловиеРегистрацияВНалоговомОргане
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОрганеУведомления", "УведомлениеОбИсчисленныхСуммахНалоговНалоги.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "ЗадачиБухгалтераУведомления.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОрганеУведомления", "Истина");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРегистрацияВНалоговомОргане", "Истина");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаУведомлений.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПоказательПериодаПлатежаПоДекларации
//
Процедура ПриПолученииПоказателяПериодаПлатежаПоДекларации(ПоказательПериода, Правило, ПериодСобытия, Срок, НалоговыйПериодДляПлатежейВБюджет) Экспорт
	
	ПериодичностьПоКлассификатору = ПлатежиВБюджетПереопределяемый.ПериодичностьПоКлассификатору(НалоговыйПериодДляПлатежейВБюджет);
	Если ПериодичностьПоКлассификатору = ПлатежиВБюджетКлиентСервер.ПлатежПоКонкретнойДате() 
		Или НалоговыйПериодДляПлатежейВБюджет <> Правило.Периодичность Тогда
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(Срок, ПериодичностьПоКлассификатору);
	Иначе
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(ПериодСобытия, ПериодичностьПоКлассификатору);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПравилаПредоставленияОтчетаУплатыНДФЛПредпринимателя
//
Процедура ПриПолученииПравилПредоставленияОтчетаУплатыНДФЛПредпринимателя(Правила, Организация, Период) Экспорт
	
	Если ЗначениеЗаполнено(ОбщегоНазначенияБПВызовСервераПовтИсп.ДатаЗакрытияОрганизации(Организация)) Тогда
		Правила.Вставить("Отчет", "2013_ИП_Декларация_СнятиеСУчета");
		Правила.Вставить("Уплата", "2013_ИП_Налог_СнятиеСУчета");
	Иначе
		Правила.Вставить("Отчет", "2013_ИП_Декларация");
		Правила.Вставить("Уплата", "2013_ИП_Налог");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникОплатыНДФЛПрошлыхПериодов
//
Процедура ПриОпределенииОписанияДействияПомощникОплатыНДФЛПрошлыхПериодов(ОписаниеДействия, Событие) Экспорт
	
	ОписаниеДействия.Вставить("ИмяФормы", "Обработка.ПомощникЗаполнения3НДФЛ.Форма.УплатаНалогаПрошлыхЛет");
	
КонецПроцедуры

// Определяет набор связанных правил. См. ВыполнениеЗадачБухгалтера.ОпределитьСвязанныеПравила
//
// Параметры:
//  Параметры       - Структура
//
Процедура ПриОпределенииСвязанныхПравил(ПараметрыНабора) Экспорт
	
	Если ТипЗнч(ПараметрыНабора.Правило) = Тип("СправочникСсылка.Патенты") Тогда
		ПараметрыНабора.ПравилоУплаты      = ПараметрыНабора.Правило;
		ПараметрыНабора.ПравилоУведомления = ПараметрыНабора.Правило;
		ПараметрыНабора.ПравилоОтчета      = ПараметрыНабора.Правило;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.НовыйТаблицаУведомлений
//
Процедура ПриОпределенииТаблицыУведомлений(ТаблицаУведомлений) Экспорт
	
	МассивТиповДокументов = Новый Массив;
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.УведомлениеОбИсчисленныхСуммахНалогов"));
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.ОперацияПоЕдиномуНалоговомуСчету"));
	МассивТиповДокументов.Добавить(Тип("ДокументСсылка.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности"));
	
	Если ТаблицаУведомлений.Колонки.Найти("Уведомление") <> Неопределено Тогда
		ТаблицаУведомлений.Колонки.Удалить("Уведомление");
	КонецЕсли;
	
	ТаблицаУведомлений.Колонки.Добавить("Уведомление", Новый ОписаниеТипов(МассивТиповДокументов));
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ИдентификаторыПравилВыполняющихсяНеРеглОтчетами
//
Процедура ПриОпределенииИдентификаторовПравилВыполняющихсяНеРеглОтчетами(Идентификаторы) Экспорт
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтераВызовСервера.ИмяФормыНалогиОтчеты
//
Процедура ПриОпределенииИмениФормыНалогиОтчеты(ИмяФормы) Экспорт
	
	ИмяФормы = "ОбщаяФорма.НалогиИОтчеты";
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтераВызовСервера.ИмяФормыВводНачальныхОстатков
//
Процедура ПриОпределенииИмениФормыВводНачальныхОстатков(ИмяФормы) Экспорт
	
	ИмяФормы = "Обработка.ВводНачальныхОстатков.Форма";
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗавершитьПроверкуБанковскихОперацийАУСН
//
Процедура ПриЗавершенииПроверкиБанковскихОперацийАУСН(Результат) Экспорт
	
	ВыполнениеЗадачБухгалтераБП.ЗавершитьПроверкуБанковскихОперацийАУСН(Результат);
	
КонецПроцедуры

#Область ВыполнениеДействий

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяУниверсальнымПомощником
//
Процедура ПриОпределенииПравилоВыполняетсяУниверсальнымПомощником(Признак, Правило, ИмяЗадачи, ПолноеИмяПравила) Экспорт
	
	Признак = Ложь;
	Если ИмяЗадачи = "СтраховыеВзносы"
		Или ИмяЗадачи = "НДФЛ_Агент_Уплата"
		Или ПолноеИмяПравила = "НДФЛ_Агент_Уведомление.2022_ЕНП_УведомлениеНДФЛ"
		Или ВыполнениеЗадачБухгалтера.ЭтоПравилоУплатыИмущественногоНалога(ПолноеИмяПравила)
		Или ПолноеИмяПравила = "ТорговыйСбор.2015_Уплата"
		Или ПолноеИмяПравила = "ТорговыйСбор.2022_ЕНП_УведомлениеУплата"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_Декабрь"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2023_НДФЛ_Уплата_ЕдиныйПомощник"
		Или ПолноеИмяПравила = "НДФЛ_Агент.2024_НДФЛ_Уплата_КонецМесяца"
		Или ИнтерфейсыВзаимодействияБРО.ТребованиеОбрабатывается(ПолноеИмяПравила)
		И Не ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомРасчетаНалогаУСН(Правило) 
		Или ИмяЗадачи = "ЕдиныйНалоговыйСчет" Тогда
		
		Признак = Истина;
		
	ИначеЕсли ИмяЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиНалогНаПрофессиональныйДоход()
		И Не ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомУплатыНПД(Правило, ИмяЗадачи) Тогда
		Признак = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияНалоговыйПлатеж
//
Процедура ПриОпределенииПризнакаУчетаЗарплатыИКадровВЭтойПрограмме(Признак) Экспорт
	
	Признак = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии");
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияНалоговыйПлатеж
//
Процедура ПриОпределенииПравилаСтраховыеВзносыЕдиныйПомощник(СтраховыеВзносыЕдиныйПомощник, Правило, ПолноеИмяПравила) Экспорт
	
	СтраховыеВзносыЕдиныйПомощник = Не ПолноеИмяПравила = "СтраховыеВзносы.2023_УплатаСтраховыеВзносы"
		И Не ПолноеИмяПравила = "СтраховыеВзносы.2023_УплатаСтраховыеВзносы_ЕдиныйПомощник"
		И Не ВыполнениеЗадачБухгалтера.ЭтоСтраховыеВзносыВнеЕНП(Правило);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияЗаявленияОВвозе
//
Процедура ПриОпределенииОписанияДействияЗаявленияОВвозе(Событие, ОписаниеДействия) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Событие.Организация);

	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.ЗаявлениеОВвозеТоваров.ФормаСписка");
	ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияОткрытьДокументыРасчетаВнешняяЗарплата
//
Процедура ПриОпределенииОписанияДействияОткрытьДокументыРасчетаВнешняяЗарплата(Событие, ОписаниеДействия) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно") Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("Организация", Событие.Организация);
		
		ОписаниеДействия.Вставить("ИмяФормы",       "Документ.ВедомостьНаВыплатуЗарплаты.ФормаСписка");
		ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
	Иначе
		ПриОпределенииОписанияДействияОткрытьОтражениеВУчетеВнешняяЗарплата(Событие, ОписаниеДействия);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияОткрытьОтражениеВУчетеВнешняяЗарплата
//
Процедура ПриОпределенииОписанияДействияОткрытьОтражениеВУчетеВнешняяЗарплата(Событие, ОписаниеДействия) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Событие.Организация);
	
	ИмяДокумента = ЗакрытиеМесяца.ВидДокументаНачисленияЗарплаты(Событие.Организация);
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Документ." + ИмяДокумента + ".ФормаСписка");
	ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияРегулярныйПлатеж
//
Процедура ПриОпределенииОписанияДействияРегулярныйПлатеж(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	// В качестве шаблона для ввода новой платежки ищем последний введенный по текущему правилу документ.
	ШаблонПравила = РегистрыСведений.РегулярныеПлатежи.ШаблонПравила(
		Событие.Организация, Событие.Правило, Событие.ПериодСобытия);
	Если ШаблонПравила = Неопределено Тогда
	
	ИначеЕсли ШаблонПравила.ПериодСобытия = Событие.ПериодСобытия Тогда
	
		// Если по правилу вводился документ в ПериодСобытия, то открываем его
		ПараметрыФормы.Вставить("Ключ", ШаблонПравила.ПлатежноеПоручение);
	
	Иначе
		
		// Если в периоде документ не вводился, вводим новый копированием последнего введенного по правилу платежного поручения
		ПараметрыФормы.Вставить("ЗначениеКопирования", ШаблонПравила.ПлатежноеПоручение);
		ПараметрыФормы.Вставить("Правило",       Событие.Правило);
		ПараметрыФормы.Вставить("ПериодСобытия", Событие.ПериодСобытия);
	
	КонецЕсли;
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.ПлатежноеПоручение.ФормаОбъекта");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияФинОтчетность
//
Процедура ПриОпределенииОписанияДействияФинОтчетность(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;

	Правило = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Событие.Правило,
		"Организация, Получатель, Периодичность, ИдентификаторКомплекта, КраткоеПредставлениеКомплекта");

	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.Периодичность, Событие.ПериодСобытия-1);
	КонецПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, Событие.ПериодСобытия-1);

	СуществующийДокумент = Документы.ФинОтчетВБанк.НайтиПоРеквизитам(Правило.Организация,
		Правило.Получатель, Правило.ИдентификаторКомплекта, КонецПериода);

	Если СуществующийДокумент <> Неопределено Тогда
		ПараметрыФормы.Вставить("Ключ", СуществующийДокумент);
	Иначе
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Организация",                   Правило.Организация);
		ЗначенияЗаполнения.Вставить("Банк",                          Правило.Получатель);
		ЗначенияЗаполнения.Вставить("НачалоПериода",                 НачалоПериода);
		ЗначенияЗаполнения.Вставить("КонецПериода",                  КонецПериода);
		ЗначенияЗаполнения.Вставить("ИдентификаторКомплекта",        Правило.ИдентификаторКомплекта);
		ЗначенияЗаполнения.Вставить("КраткоеПредставлениеКомплекта", Правило.КраткоеПредставлениеКомплекта);

		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("Правило", Событие.Правило);
	КонецЕсли;

	ОписаниеДействия.Вставить("ФинОтчетность", Истина);
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПлатежПоДаннымДекларации
//
Процедура ПриОпределенииОписанияДействияПлатежПоДаннымДекларации(Организация, РегистрацияВНалоговомОргане, СпособОплаты, Срок, Правило, ПериодСобытия, Описание, ОписаниеДействия) Экспорт
	
	ИмяФормыПлатежногоДокумента = Перечисления.СпособыУплатыНалогов.ИмяФормыПлатежногоДокумента(СпособОплаты);
	НачалоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.Периодичность, ПериодСобытия);
	КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Правило.Периодичность, ПериодСобытия);

	Показатели                  = ВыполнениеЗадачБухгалтера.ПоказателиБазовогоПериода(НачалоПериода, КонецПериода, Правило);
	ДлительностьБазовогоПериода = Показатели.ДлительностьБазовогоПериода;
	КонецБазовогоПериода        = Показатели.КонецБазовогоПериода;

	ИдентификаторЗадачи  = "";
	ИдентификаторПравила = "";
	ПолноеИмяПравила     = "";

	Идентификаторы = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ИдентификаторыПравилаИЗадачи(Правило);

	Если Идентификаторы <> Неопределено Тогда
		ИдентификаторЗадачи  = Идентификаторы.ИдентификаторЗадачи;
		ИдентификаторПравила = Идентификаторы.ИдентификаторПравила;
		ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.СкомпоноватьПолноеИмяПравила(
			ИдентификаторЗадачи, ИдентификаторПравила);
	КонецЕсли;

	ОКАТО = Справочники.Организации.КодТерриторииМестаРегистрации(Организация);
	ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(ОКАТО, ПериодСобытия);

	ОписаниеПлатежа = ВыполнениеЗадачБухгалтера.НовыйОписаниеПлатежа();
	ОписаниеПлатежа.Организация  = Организация;
	ОписаниеПлатежа.СпособОплаты = СпособОплаты;
	ОписаниеПлатежа.ИмяЗадачи    = Правило.Владелец.Код;
	ОписаниеПлатежа.ОКАТО        = ОКАТО;

	ПоказательПериода = ВыполнениеЗадачБухгалтера.ПоказательПериодаПлатежаПоДекларации(Правило, ПериодСобытия, Срок);
	ДанныеДекларации  = ВыполнениеЗадачБухгалтера.ДанныеДляУплатыНалогаПоДекларации(
		Правило,
		Организация,
		РегистрацияВНалоговомОргане,
		ПериодСобытия,
		Срок,
		ПоказательПериода);

	Если ДанныеДекларации = Неопределено Тогда

		// Декларации нет.
		// Надо предложить пользователю заполнить декларацию
		// Но быть готовым, что он не согласится и придется заполнять платежку без декларации.

		ШаблонТекста = НСтр("ru = 'Перед оформлением платежного документа рекомендуется подготовить отчет ""[Декларация] за [Период]"".
			|В этом случае сумма и реквизиты платежа будут взяты из отчета.'");
		ПараметрыТекста = Новый Структура;
		ПараметрыТекста.Вставить("Декларация", ИнтерфейсыВзаимодействияБРО.ПредставлениеФормыРегламентированногоОтчета(ПолноеИмяПравила, КонецБазовогоПериода));
		НачалоНалоговогоПериода = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(Правило.ФинансовыйПериод, КонецБазовогоПериода);
		ПараметрыТекста.Вставить("Период",     КалендарьБухгалтера.ПредставлениеПериодаСобытия(НачалоНалоговогоПериода, КонецБазовогоПериода));

		ОписаниеДействия.Вставить("Вопрос",      СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекста));
		ОписаниеДействия.Вставить("ВариантДа",   НСтр("ru = 'Подготовить отчет'"));
		ОписаниеДействия.Вставить("ВариантНет",  НСтр("ru = 'Оформить платежный документ'"));
		ОписаниеДействия.Вставить("ДействиеДа",  Новый Структура);
		ОписаниеДействия.Вставить("ДействиеНет", Новый Структура);

		// Откроем форму отчета
		ОписаниеФормы = ИнтерфейсыВзаимодействияБРО.ОписаниеФормыРеглОтчета(
			ПолноеИмяПравила,
			Организация,
			РегистрацияВНалоговомОргане,
			НачалоНалоговогоПериода,
			КонецБазовогоПериода,
			ДлительностьБазовогоПериода);
		ОписаниеДействия.ДействиеДа.Вставить("ИмяФормы",       ОписаниеФормы.Имя);
		ОписаниеДействия.ДействиеДа.Вставить("ПараметрыФормы", ОписаниеФормы.Параметры);

		// Откроем пустой документ

		ОписаниеПлатежа.Период            = ПериодСобытия;
		ОписаниеПлатежа.ПоказательПериода = ПоказательПериода;

		ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(ОписаниеПлатежа.ИмяЗадачи, ОписаниеПлатежа.Организация, ОписаниеПлатежа.Период);

		ОписаниеПлатежа.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);

		ОписаниеПлатежа.КБК = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(ОписаниеПлатежа.Налог, , ОписаниеПлатежа.Период);

		ЗначенияЗаполнения = ВыполнениеЗадачБухгалтера.ЗначенияЗаполненияПлатежногоДокумента(ОписаниеПлатежа);

		ОписаниеДействия.ДействиеНет.Вставить("ИмяФормы", ИмяФормыПлатежногоДокумента);
			ОписаниеДействия.ДействиеНет.Вставить("ПараметрыФормы",
				Новый Структура("ЗначенияЗаполнения, ПериодСобытия, Правило",
					ЗначенияЗаполнения,
					ПериодСобытия,
					Правило));
	КонецЕсли;
	
	Если ДанныеДекларации.Платежи.Количество() = 0 Тогда

		ОписаниеДействия.Вставить("ИмяФормы", ИмяФормыПлатежногоДокумента);
		// По декларации ничего не надо платить.
		// Откроем пустую платежку

		ОписаниеПлатежа.Период              = ПериодСобытия;
		ОписаниеПлатежа.Декларация          = ДанныеДекларации.Ссылка;
		ОписаниеПлатежа.ДатаДекларации      = ДанныеДекларации.ДатаПодписи;
		ОписаниеПлатежа.КодНалоговогоОргана = ДанныеДекларации.КодНалоговогоОргана;
		ОписаниеПлатежа.ПоказательПериода   = ПоказательПериода;
		ОписаниеПлатежа.Описание            = Описание;

		ВидНалога = РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(ОписаниеПлатежа.ИмяЗадачи, ОписаниеПлатежа.Организация, ОписаниеПлатежа.Период);

		ОписаниеПлатежа.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);

		ОписаниеПлатежа.КБК = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(ОписаниеПлатежа.Налог, , ОписаниеПлатежа.Период);

		ЗначенияЗаполнения = ВыполнениеЗадачБухгалтера.ЗначенияЗаполненияПлатежногоДокумента(ОписаниеПлатежа);

		ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("ЗначенияЗаполнения, ПериодСобытия, Правило", ЗначенияЗаполнения, ПериодСобытия, Правило));

	ИначеЕсли ДанныеДекларации.Платежи.Количество() = 1 Тогда

		ОписаниеДействия.Вставить("ИмяФормы", ИмяФормыПлатежногоДокумента);

		Платеж = ДанныеДекларации.Платежи[0];
		Если ЗначениеЗаполнено(Платеж.ПлатежноеПоручение) Тогда
			// Откроем существующую платежку
			ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Ключ", Платеж.ПлатежноеПоручение));
		Иначе

			// Создадим платежку по данным декларации

			ОписаниеПлатежа.Период              = ПериодСобытия;
			ОписаниеПлатежа.ПоказательПериода   = ПоказательПериода;
			ОписаниеПлатежа.Аванс               = Платеж.Аванс;
			ОписаниеПлатежа.ОКАТО               = Платеж.ОКАТО;
			ОписаниеПлатежа.Сумма               = Платеж.Сумма;
			ОписаниеПлатежа.Описание            = Описание;
			ОписаниеПлатежа.Декларация          = ДанныеДекларации.Ссылка;
			ОписаниеПлатежа.ДатаДекларации      = ДанныеДекларации.ДатаПодписи;
			ОписаниеПлатежа.КодНалоговогоОргана = ДанныеДекларации.КодНалоговогоОргана;
			ОписаниеПлатежа.Налог               = Платеж.Налог;
			ОписаниеПлатежа.КБК                 = Платеж.КБК;

			ЗначенияЗаполнения = ВыполнениеЗадачБухгалтера.ЗначенияЗаполненияПлатежногоДокумента(ОписаниеПлатежа);

			ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("ЗначенияЗаполнения, ПериодСобытия, Правило", ЗначенияЗаполнения, ПериодСобытия, Правило));

		КонецЕсли;
	
	Иначе

		// Откроем форму списка платежей

		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",       Организация);
		ПараметрыФормы.Вставить("ПериодСобытия",     ПериодСобытия);
		ПараметрыФормы.Вставить("Правило",           Правило);
		ПараметрыФормы.Вставить("СпособОплаты",      СпособОплаты);
		ПараметрыФормы.Вставить("Декларация",        ДанныеДекларации.Ссылка);
		ПараметрыФормы.Вставить("ДатаДекларации",    ДанныеДекларации.ДатаПодписи);
		ПараметрыФормы.Вставить("ОписаниеДействия",  Описание);
		ПараметрыФормы.Вставить("ПоказательПериода", ПоказательПериода);
		ПараметрыФормы.Вставить("ВидНалога",         РасчетыСБюджетом.ВидНалогаПоКодуЗадачи(ОписаниеПлатежа.ИмяЗадачи, ОписаниеПлатежа.Организация, ОписаниеПлатежа.Период));
		ПараметрыФормы.Вставить("Платежи",           ПоместитьВоВременноеХранилище(ДанныеДекларации.Платежи));

		ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ФормированиеПлатежныхПорученийНаУплатуНалогов.Форма.УплатаПоДекларации");
		ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияФормированиеПлатежныхПорученийНаУплатуНалогов
//
Процедура ПриОпределенииОписанияДействияФормированиеПлатежныхПорученийНаУплатуНалогов(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Событие.Организация);
	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ФормированиеПлатежныхПорученийНаУплатуНалогов.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияРасчетСтраховыхВзносовИП
//
Процедура ПриОпределенииОписанияДействияРасчетСтраховыхВзносовИП(Событие, ОписаниеДействия) Экспорт
	
	ПомощникПубликуется = Ложь;

	Если Событие.ИдентификаторПравила <> ЗадачиБухгалтераКлиентСервер.КодЗадачиУплатаПенсионныеДобровольныеВзносы()
		И УчетСтраховыхВзносовИПКлиентСервер.ЕдиныйТарифВключенВЕдиныйНалоговыйПлатеж(Событие.ПериодСобытия)
		И Не УчетСтраховыхВзносовИП.ЭтоПравилоУплатыВзносовСДоходов(Событие.Правило) Тогда

		ПараметрыФормы = ВыполнениеЗадачБухгалтераКлиентСервер.НовыйПараметрыФормыПомощника();
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, Событие);
		КонецГода = КонецГода(Событие.ПериодСобытия);

		ПорядокЕНП = ВыполнениеЗадачБухгалтера.ПорядокУплатыНалога(Событие.Организация,
			КонецГода,
			Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж);

		ПредставлениеПериода = КалендарьБухгалтера.ПредставлениеПериодаСобытия(
			НачалоМесяца(Событие.ПериодСобытия),
			КонецМесяца(Событие.ПериодСобытия));
		ПериодВПредложномПадеже = ПолучитьСклоненияСтроки(ПредставлениеПериода, , "ПД=Предложный")[0];

		// ЕНП платится за прошлый месяц: для того, чтобы взнос по единому тарифу попал в декабрьский платеж,
		// смещаем период события на конец прошлого месяца
		ПараметрыФормы.ПериодСобытия = НачалоМесяца(КонецГода) - 1;
		ПараметрыФормы.Правило = ПорядокЕНП.Правило;
		ПараметрыФормы.Описание = СтрШаблон(НСтр("ru = 'Единый налоговый платеж в %1'"), ПериодВПредложномПадеже);
		ПараметрыФормы.ИдентификаторЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиЕдиныйНалоговыйСчет();

		ИмяФормыПомощника = "Обработка.ПомощникУплатыНалога.Форма.ФормаЕдиная";
		ПериодСобытия = КонецКвартала(Событие.ПериодСобытия);
		ПомощникПубликуется = Не (ВыполнениеЗадачБухгалтера.ЭтоОплатаНалогаЗаПрошлыйПериод(Событие)
			И УчетнаяПолитика.ПрименяетсяУСН(Событие.Организация, ТекущаяДатаСеанса()));

	ИначеЕсли Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Событие.Организация)
		И Не ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности() Тогда

		ПериодСобытия = ТекущаяДатаСеанса();
		СведенияОбОрганизации = Обработки.РасчетСтраховыхВзносовИП.СведенияОбОрганизации(Событие.Организация, ПериодСобытия);
		Если Событие.ПериодСобытия >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета() Тогда
			ИмяФормыПомощника = "Обработка.РасчетСтраховыхВзносовИП.Форма";
		Иначе
			ИмяФормыПомощника = "Обработка.РасчетСтраховыхВзносовИП.Форма.ФормаВзносыИПНулевка";
		КонецЕсли;

		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Правило", Событие.Правило);
		ПараметрыФормы.Вставить("Организация", Событие.Организация);
		ПараметрыФормы.Вставить("ПериодСобытия", ПериодСобытия);
		ПараметрыФормы.Вставить("Срок", Событие.Срок);
		ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
		ПараметрыФормы.Вставить("АдресСведенийОбОрганизации",
			ПоместитьВоВременноеХранилище(СведенияОбОрганизации, Новый УникальныйИдентификатор));

	Иначе

		ПериодСобытия = КонецКвартала(Событие.ПериодСобытия);
		Если ВыполнениеЗадачБухгалтера.ЭтоОплатаНалогаЗаПрошлыйПериод(Событие)
			И УчетнаяПолитика.ПрименяетсяУСН(Событие.Организация, ТекущаяДатаСеанса()) Тогда
		
			ИмяФормыПомощника = "Обработка.ПомощникУплатыНалоговВзносовПрошлыхЛет.Форма.ФормаОплатыЗаПрошлыеПериоды";
		Иначе
			// Конечное имя формы определяетя видом страховых взносов
			ИмяФормыПомощника = "Обработка.РасчетСтраховыхВзносовИП.Форма";
			ПомощникПубликуется = Истина;
		КонецЕсли;

		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",      Событие.Организация);
		ПараметрыФормы.Вставить("ПериодСобытия",    ПериодСобытия);
		ПараметрыФормы.Вставить("Правило",          Событие.Правило);
		ПараметрыФормы.Вставить("Срок",             Событие.Срок);
		ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
		ПараметрыФормы.Вставить("Заголовок",        Событие.Наименование);
		ПараметрыФормы.Вставить("Периодичность",    Событие.Периодичность);

	КонецЕсли;

	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("ИмяФормы",       ИмяФормыПомощника);
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);

	ИдентификаторФормы = ВыполнениеЗадачБухгалтера.ИдентификаторФормыПоПериодуСобытияОрганизации(ПараметрыФормы);
	Если ЗначениеЗаполнено(ИдентификаторФормы) Тогда
		ОписаниеДействия.Вставить("ИдентификаторФормы", ИдентификаторФормы);
	КонецЕсли;

	Если ПомощникПубликуется Тогда
		ОписаниеДействия.Вставить("ПубликуемыйИдентификатор", Обработки.РасчетСтраховыхВзносовИП.ПубликуемыйИдентификатор());
		
		ВходящиеНастройкиПубликуемыхДанных = Обработки.РасчетСтраховыхВзносовИП.ВходящиеНастройкиПубликуемыхДанных(
			Событие.Организация, ПериодСобытия);
		ОписаниеДействия.Вставить("ВходящиеНастройкиПубликуемыхДанных", ВходящиеНастройкиПубликуемыхДанных);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПараметрыСтраховыхВзносов
//
Процедура ПриОпределенииПараметровСтраховыхВзносов(ТаблицаСчетовСтраховыхВзносов, ПериодСобытия, ЗаполнятьКБК) Экспорт
	
	Если ТаблицаСчетовСтраховыхВзносов.Колонки.Найти("Налог") <> Неопределено Тогда
		ТаблицаСчетовСтраховыхВзносов.Колонки.Удалить("Налог");
	КонецЕсли;
	
	Если ТаблицаСчетовСтраховыхВзносов.Колонки.Найти("ВидНалоговогоОбязательства") <> Неопределено Тогда
		ТаблицаСчетовСтраховыхВзносов.Колонки.Удалить("ВидНалоговогоОбязательства");
	КонецЕсли;
	
	Если ТаблицаСчетовСтраховыхВзносов.Колонки.Найти("СчетУчета") <> Неопределено Тогда
		ТаблицаСчетовСтраховыхВзносов.Колонки.Удалить("СчетУчета");
	КонецЕсли;
	
	ТаблицаСчетовСтраховыхВзносов.Колонки.Добавить("Налог",                      Новый ОписаниеТипов("СправочникСсылка.ВидыНалоговИПлатежейВБюджет"));
	ТаблицаСчетовСтраховыхВзносов.Колонки.Добавить("ВидНалоговогоОбязательства", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПлатежейВГосБюджет"));
	ТаблицаСчетовСтраховыхВзносов.Колонки.Добавить("СчетУчета",                  Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	Если ПериодСобытия < УчетЗарплаты.ДатаПереходаНаЕдиныйТарифСтраховыхВзносов() Тогда
		
		// 69.01
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
			
		// 69.02.7
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_СтраховаяЧасть),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
			
		
		// 69.03.1
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ФФОМС),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
			
	КонецЕсли;
		
	// 69.11
	ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
		Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ),
		,
		ПериодСобытия,
		ЗаполнятьКБК);
				
	// 69.09
	ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
		Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносыЕдиныйТариф),
		Перечисления.ВидыПлатежейВГосБюджет.Налог,
		ПериодСобытия,
		ЗаполнятьКБК);
		
	УчетЗарплатыИКадровВоВнешнейПрограмме = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	
	Если УчетЗарплаты.ИспользуетсяТрудЧленовЛетныхЭкипажей() Или УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		// 69.02.3
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ЛетныеЭкипажи),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
	КонецЕсли;

	Если УчетЗарплаты.ИспользуетсяТрудШахтеров() Или УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		// 69.02.4
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_Шахтеры),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
	КонецЕсли;

	Если УчетЗарплаты.ИспользуютсяРаботыСДосрочнойПенсией() Или УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		// 69.02.5
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ВредныеУсловия),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
		
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ВредныеУсловия),
			Перечисления.ВидыПлатежейВГосБюджет.ВзносыБезСпецоценки,
			ПериодСобытия,
			ЗаполнятьКБК);
	
		// 69.02.6
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ТяжелыеУсловия),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
		
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.ДополнительныеВзносы_ПФР_ТяжелыеУсловия),
			Перечисления.ВидыПлатежейВГосБюджет.ВзносыБезСпецоценки,
			ПериодСобытия,
			ЗаполнятьКБК);
	КонецЕсли;

	Если УчетЗарплаты.УдерживаютсяДобровольныеВзносыВПФР() Или УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		// 69.05.2
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ПФР_Добровольные),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
	КонецЕсли;

	Если ПериодСобытия >= УчетЗарплаты.ДатаПереходаНаЕдиныйТарифСтраховыхВзносов()
		И (УчетЗарплаты.ИспользуетсяТрудИностранцевСОсобымиВзносами() Или УчетЗарплатыИКадровВоВнешнейПрограмме) Тогда
		// 69.02.7
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ОПС_ИностранныеРаботники, Истина, ПериодСобытия),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);

		// 69.03.1
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ОМС_ИностранныеРаботники, Истина, ПериодСобытия),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
		
		// 69.01
		ВыполнениеЗадачБухгалтераБП.ДополнитьМассивСчетовУчета(ТаблицаСчетовСтраховыхВзносов,
			Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(Перечисления.ВидыНалогов.СтраховыеВзносы_ОСС_ИностранныеРаботники, Истина, ПериодСобытия),
			Перечисления.ВидыПлатежейВГосБюджет.Налог,
			ПериодСобытия,
			ЗаполнятьКБК);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПараметрыНДФЛ
//
Процедура ПриОпределенииПараметровНДФЛ(ТаблицаСчетовСтраховыхВзносов, ПериодСобытия) Экспорт
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияВыплатаЗарплатыИУплатаНДФЛ
//
Процедура ПриОпределенииОписанияДействияВыплатаЗарплатыИУплатаНДФЛ(Событие, Аванс, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",      Событие.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия",    Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("ОписаниеДействия", НСтр("ru = 'Выплата зарплаты и уплата НДФЛ'"));
	ПараметрыФормы.Вставить("Правило",          Событие.Правило);
	ПараметрыФормы.Вставить("Описание",         Событие.Наименование);
	ПараметрыФормы.Вставить("Аванс",            Аванс);
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникУчетаЗарплаты.Форма.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияНалоговыйПлатеж
//
Процедура ПриОпределенииОписанияДействияНалоговыйПлатежОтчетУведомление(ОписаниеДействия, Правило, Событие, ПолноеИмяПравила) Экспорт
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияНалоговыйПлатеж
//
Процедура ПриОпределенииОписанияДействияНалоговыйПлатежПроверка(ОписаниеДействия, Правило, Событие) Экспорт
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияНалоговыйПлатеж
//
Процедура ПриОпределенииОписанияДействияНалоговыйПлатежРегулярныйПлатеж(ОписаниеДействия, Правило, Событие) Экспорт
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияСверкаНДС
//
Процедура ПриОпределенииОписанияДействияСверкаНДС(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",     Событие.Организация);
	ПараметрыФормы.Вставить("НалоговыйПериод", Событие.ПериодСобытия);
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.СверкаДанныхУчетаНДС.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПлатежПоПатенту
//
Процедура ПриОпределенииОписанияДействияПлатежПоПатенту(Организация, ДокументПатент, Сумма, СпособОплаты, ПериодСобытия, Срок, ОписаниеДействия) Экспорт
	
	Если ПомощникиПоУплатеНалоговИВзносов.ПрименяетсяОсобыйПорядокУплатыНалога(Организация, Срок) Тогда
		РеквизитыПлатежа =
			ПомощникиПоУплатеНалоговИВзносов.РеквизитыЕдиногоНалоговогоПлатежа(Организация,
			КонецКвартала(Срок));
		ПоказательПериода   = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();
		ОснованиеПлатежа    = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();
		КодПоОКТМО = РеквизитыПлатежа.ОКТМО;
		
		НазначениеПлатежа = Справочники.ВидыНалоговИПлатежейВБюджет.НазначениеПлатежа(
			РеквизитыПлатежа.Налог, РеквизитыПлатежа.ВидНалога, Организация);
	
	Иначе
		РеквизитыПлатежа = Документы.ОперацияСПатентом.РеквизитыНалоговогоПлатежа(ДокументПатент);
		ПериодичностьПоКлассификатору = ПлатежиВБюджетКлиентСервер.ПериодичностьГод();
		ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(ПериодСобытия, ПериодичностьПоКлассификатору);
		ОснованиеПлатежа  = "";
		КодПоОКТМО = РеквизитыПлатежа.ОКАТО;
	
		НазначениеПлатежа = Справочники.ВидыНалоговИПлатежейВБюджет.НазначениеПлатежа(
			РеквизитыПлатежа.Налог, РеквизитыПлатежа.ВидНалога, Организация)
			+ РеквизитыПлатежа.Описание;
	
	КонецЕсли;

	ПериодСобытияУплаты = ПериодСобытия;
	Налог               = РеквизитыПлатежа.Налог;
	КодНалоговогоОргана = РеквизитыПлатежа.КодНалоговогоОргана;
	ОКТМО               = КодПоОКТМО;
	КБК                 = РеквизитыПлатежа.КБК;

	ОписаниеПлатежа = ВыполнениеЗадачБухгалтера.НовыйОписаниеПлатежа();

	ОписаниеПлатежа.Организация         = Организация;
	ОписаниеПлатежа.СпособОплаты        = СпособОплаты;
	ОписаниеПлатежа.ИмяЗадачи           = "Патент_Уплата";
	ОписаниеПлатежа.Описание            = НазначениеПлатежа;
	ОписаниеПлатежа.Период              = ПериодСобытияУплаты;
	ОписаниеПлатежа.ПоказательПериода   = ПоказательПериода;
	ОписаниеПлатежа.КБК                 = КБК;
	ОписаниеПлатежа.Налог               = Налог;
	ОписаниеПлатежа.ОКАТО               = ОКТМО;
	ОписаниеПлатежа.Сумма               = Сумма;
	ОписаниеПлатежа.КодНалоговогоОргана = КодНалоговогоОргана;
	ОписаниеПлатежа.ОснованиеПлатежа    = ОснованиеПлатежа;

	ИмяФормыПлатежногоДокумента = ?(СпособОплаты = Перечисления.СпособыУплатыНалогов.НаличнымиПоКвитанции,
		"Документ.РасходныйКассовыйОрдер.ФормаОбъекта", "Документ.ПлатежноеПоручение.Форма.ФормаДокументаНалоговая");

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ВыполнениеЗадачБухгалтера.ЗначенияЗаполненияПлатежногоДокумента(ОписаниеПлатежа));
	ПараметрыФормы.Вставить("ПериодСобытия",      ПериодСобытияУплаты);
	ПараметрыФормы.Вставить("Правило",            ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументПатент, "Патент"));

	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("ИмяФормы", ИмяФормыПлатежногоДокумента);
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникРасчетаУСН
//
Процедура ПриОпределенииОписанияДействияПомощникРасчетаУСН(Событие, ОписаниеДействия) Экспорт
	
	ОписаниеДействия.Вставить("ИмяФормы", "Обработка.ПомощникРасчетаНалогаУСН.Форма.Форма");

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
	ПараметрыФормы.Вставить("КонецПериода",                КонецКвартала(Событие.ПериодСобытия));
	ПараметрыФормы.Вставить("КонтекстныйВызов",            Истина);
	ПараметрыФормы.Вставить("Заголовок",                   Событие.Наименование);
	ПараметрыФормы.Вставить("Правило",                     Событие.Правило);

	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	ОписаниеДействия.Вставить("ИдентификаторФормы", Событие.Организация.УникальныйИдентификатор());

	ГраницаОтчетностиПрошлыхПериодов = ПомощникиПоУплатеНалоговИВзносов.ГраницаОтчетностиПрошлыхПериодов(Событие.Организация);

	Если Не ЗначениеЗаполнено(ГраницаОтчетностиПрошлыхПериодов)
		Или Событие.ПериодСобытия > ГраницаОтчетностиПрошлыхПериодов Тогда

		// В том случае, если период события находится в периоде, когда пользователь уже работал в программе,
		// добавляем данные публикации ссылки на программный интерфейс помощника расчета налога УСН.

		ОписаниеДействия.Вставить("ПубликуемыйИдентификатор", Обработки.ПомощникРасчетаНалогаУСН.ПубликуемыйИдентификатор());

		ВходящиеНастройкиПубликуемыхДанных = Обработки.ПомощникРасчетаНалогаУСН.ВходящиеНастройкиПубликуемыхДанных(
			Событие.Организация, КонецКвартала(Событие.ПериодСобытия));
		ОписаниеДействия.Вставить("ВходящиеНастройкиПубликуемыхДанных", ВходящиеНастройкиПубликуемыхДанных);

	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникОплатыНалогаПрошлыхПериодов
//
Процедура ПриОпределенииОписанияДействияПомощникОплатыНалогаПрошлыхПериодов(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",      Событие.Организация);
	ПараметрыФормы.Вставить("Правило",          Событие.Правило);
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникУплатыНалоговВзносовПрошлыхЛет.Форма.ФормаОплатыЗаПрошлыеПериоды");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПлатежУСН
//
Процедура ПриОпределенииОписанияДействияПлатежУСН(СтруктураРеквизитовУплатыНалога, ОписаниеДействия) Экспорт
	
	Организация         = СтруктураРеквизитовУплатыНалога.Организация;
	ПлательщикЕНП       = СтруктураРеквизитовУплатыНалога.ПлательщикЕНП;
	Сумма               = СтруктураРеквизитовУплатыНалога.Сумма;
	СпособОплаты        = СтруктураРеквизитовУплатыНалога.СпособУплатыНалога;
	ВидНалога           = СтруктураРеквизитовУплатыНалога.ВидНалога;
	ПериодСобытия       = СтруктураРеквизитовУплатыНалога.ПериодСобытия;
	ОплатаЗадолженности = СтруктураРеквизитовУплатыНалога.ОплатаЗадолженности;
	Правило             = СтруктураРеквизитовУплатыНалога.Правило;

	ПорядокУплаты = РегистрыСведений.ЗадачиБухгалтера.ПорядокУплатыНалогаЗаПериод(
	        Организация, ВидНалога, КонецКвартала(ПериодСобытия));

	Если ПорядокУплаты = Неопределено Тогда
		ОписаниеДействия = Неопределено;
		Возврат;
	КонецЕсли;

	Если ПлательщикЕНП Тогда
		ОплатаЗадолженности = Ложь;
	КонецЕсли;

	Если ПлательщикЕНП Тогда
		РеквизитыЕдиногоНалоговогоПлатежа =
			ПомощникиПоУплатеНалоговИВзносов.РеквизитыЕдиногоНалоговогоПлатежа(Организация,
				КонецКвартала(ПорядокУплаты.Срок));
		Налог               = РеквизитыЕдиногоНалоговогоПлатежа.Налог;
		КодНалоговогоОргана = РеквизитыЕдиногоНалоговогоПлатежа.КодНалоговогоОргана;
		ОКТМО               = РеквизитыЕдиногоНалоговогоПлатежа.ОКТМО;
		КБК                 = РеквизитыЕдиногоНалоговогоПлатежа.КБК;
		ПоказательПериода   = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();
	Иначе
		Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);
		КодНалоговогоОргана = ДанныеГосударственныхОрганов.КодГосударственногоОрганаОрганизации(
			Организация,
			Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган);
		ОКТМО = Справочники.Организации.КодТерриторииМестаРегистрации(Организация);
		ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(ОКТМО, ПериодСобытия);
		КБК = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог, , КонецКвартала(ПериодСобытия));
		ПоказательПериода = ВыполнениеЗадачБухгалтераБП.ПоказательПериодаПлатежаПоПравилу(ПорядокУплаты.Периодичность, ПорядокУплаты.Срок, ПериодСобытия);
	КонецЕсли;

	Аванс = КонецГода(ПериодСобытия) <> КонецКвартала(ПериодСобытия);

	ОписаниеПлатежа = ВыполнениеЗадачБухгалтера.НовыйОписаниеПлатежа();
	ОписаниеПлатежа.СпособОплаты = СпособОплаты;

	ОписаниеПлатежа.Организация = Организация;
	ОписаниеДействия.Вставить("ИмяФормы", Перечисления.СпособыУплатыНалогов.ИмяФормыПлатежногоДокумента(СпособОплаты));

	// Создадим платежку по данным операции
	ОписаниеПлатежа.Налог               = Налог;
	ОписаниеПлатежа.Период              = ПериодСобытия;
	ОписаниеПлатежа.ПоказательПериода   = ПоказательПериода;
	ОписаниеПлатежа.Аванс               = Аванс;
	ОписаниеПлатежа.КБК                 = КБК;
	ОписаниеПлатежа.ОКАТО               = ОКТМО;
	ОписаниеПлатежа.Сумма               = Сумма;
	ОписаниеПлатежа.Описание            = ПорядокУплаты.Наименование;
	ОписаниеПлатежа.КодНалоговогоОргана = КодНалоговогоОргана;
	ОписаниеПлатежа.ОплатаЗадолженности = ОплатаЗадолженности;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ВыполнениеЗадачБухгалтера.ЗначенияЗаполненияПлатежногоДокумента(ОписаниеПлатежа));
	Если Правило <> Неопределено Тогда
		ПараметрыФормы.Вставить("ПериодСобытия", ПериодСобытия);
		ПараметрыФормы.Вставить("Правило",       Правило);
	КонецЕсли;

	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияЖурналСчетовФактур
//
Процедура ПриОпределенииОписанияДействияЖурналСчетовФактур(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",     Событие.Организация);
	ПараметрыФормы.Вставить("НалоговыйПериод", Событие.ПериодСобытия);
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.ЖурналУчетаСчетовФактурДляПередачиВЭлектронномВиде.Форма.ФормаДокумента");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникПодготовкиОтчетностиПрошлыхПериодов
//
Процедура ПриОпределенииОписанияДействияПомощникПодготовкиОтчетностиПрошлыхПериодов(Событие, ОписаниеДействия) Экспорт
	
	ОписаниеДействия.Вставить("ИмяФормы", "Обработка.ПодготовкаОтчетностиПрошлыхПериодов.Форма.Форма");

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",      Событие.Организация);
	ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);

	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	ОписаниеДействия.Вставить("ИдентификаторФормы", Событие.Организация.УникальныйИдентификатор());
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияРасчетЕНВД
//
Процедура ПриОпределенииОписанияДействияРасчетЕНВД(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Событие.Организация);
	ПараметрыФормы.Вставить("Правило",     Событие.Правило);
	ПараметрыФормы.Вставить("Период",      Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("ВызовИзСпискаЗадач", Истина);

	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникРасчетаЕНВД.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЭтоОтчетПрошлогоПериода
//
Процедура ПриОпределенииПризнакаОтчетаПрошлогоПериода(ЭтоОтчетПрошлогоПериода, Событие) Экспорт
	
	ГраницаОтчетностиПрошлыхПериодов = ПомощникиПоУплатеНалоговИВзносов.ГраницаОтчетностиПрошлыхПериодов(
		Событие.Организация);
		
	Если ГраницаОтчетностиПрошлыхПериодов = Неопределено Тогда
		ЭтоОтчетПрошлогоПериода = Ложь;
		Возврат;
	КонецЕсли;

	НачалоПериодаСобытия = ИнтерфейсыВзаимодействияБРОКлиентСервер.НачалоПериода(
		Событие.Периодичность, Событие.ПериодСобытия);

	// Задача по событию, которое началось уже точно после начала ведения учета.
	Если НачалоПериодаСобытия > ГраницаОтчетностиПрошлыхПериодов Тогда
		ЭтоОтчетПрошлогоПериода = Ложь;
		Возврат;
	КонецЕсли;

	// Срок события наступил до того, как пользователь начал вести учет.
	Если КонецДня(Событие.Срок) <= ГраницаОтчетностиПрошлыхПериодов Тогда
		ЭтоОтчетПрошлогоПериода = Истина;
		Возврат;
	КонецЕсли;

	// Событие относится к прошлому периоду (НачалоПериодаСобытия > КонецПрошлогоПериода),
	// но его срок наступил после начала ведения учета.
	// Возможно, эта задача была создана помощником подготовки отчетности прошлых периодов.

	ЭтоОтчетПрошлогоПериода = РегистрыСведений.РезультатыПроверкиНалоговОтчетовПрошлыхПериодов.ЗадачаВведенаПомощникомПрошлыхПериодов(
		Событие.Организация, Событие.Правило, Событие.ПериодСобытия);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЭтоОплатаНалогаЗаПрошлыйПериод
//
Процедура ПриОпределенииПризнакаОплатыНалогаЗаПрошлыйПериод(ЭтоОплатаНалогаЗаПрошлыйПериод, Событие) Экспорт
	
	ГраницаОтчетностиПрошлыхПериодов = ПомощникиПоУплатеНалоговИВзносов.ГраницаОтчетностиПрошлыхПериодов(
		Событие.Организация);
	
	Если ГраницаОтчетностиПрошлыхПериодов = Неопределено Тогда
		ЭтоОплатаНалогаЗаПрошлыйПериод = Ложь;
		Возврат;
	КонецЕсли;

	КонецПериодаСобытия = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(
		Событие.Периодичность, Событие.ПериодСобытия);
	
	// Событие завершилось до начала ведения учета
	Если КонецПериодаСобытия <= КонецДня(ГраницаОтчетностиПрошлыхПериодов) Тогда
		ЭтоОплатаНалогаЗаПрошлыйПериод = Истина;
	Иначе
		ЭтоОплатаНалогаЗаПрошлыйПериод = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникЗаполнения3НДФЛ
//
Процедура ПриОпределенииОписанияДействияПомощникЗаполнения3НДФЛ(Событие, ОписаниеДействия) Экспорт
	
	Если Событие.ПериодСобытия < Обработки.ПомощникЗаполнения3НДФЛ.ДатаНачалаПрименения()
		Или СтрНайти(НРег(Событие.ИдентификаторПравила), "_аванс_") <> 0 Тогда
		ПриОпределенииОписанияДействияПомощникУплатыНалога(Событие, ОписаниеДействия);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Событие.Организация);
		ПараметрыФормы.Вставить("Период", НачалоГода(Событие.ПериодСобытия));
		ПараметрыФормы.Вставить("ДействиеКалендаряБухгалтера", Событие.Правило.Действие);
		ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);

		ОписаниеДействия = Новый Структура;
		ОписаниеДействия.Вставить("ИмяФормы", "Обработка.ПомощникЗаполнения3НДФЛ.Форма");
		ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
		ОписаниеДействия.Вставить("ИдентификаторФормы", Неопределено); // Не создавать уникальный идентификатор для корректной работы навигации помошника.
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникУплатыАвансовыхПлатежейНДФЛ
//
Процедура ПриОпределенииОписанияДействияПомощникУплатыАвансовыхПлатежейНДФЛ(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
	ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", Событие.РегистрацияВНалоговомОргане);
	ПараметрыФормы.Вставить("ПериодСобытия",               Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",                     Событие.Правило);
	ПараметрыФормы.Вставить("Срок",                        Событие.Срок);
	ПараметрыФормы.Вставить("Описание",                    Событие.Наименование);
	ПараметрыФормы.Вставить("ЕстьИнформацияНаИТС",         Событие.ЕстьИнформацияНаИТС);
	ПараметрыФормы.Вставить("ИдентификаторЗадачи",         Событие.ИдентификаторЗадачи);

	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникЗаполнения3НДФЛ.Форма.УплатаАвансовыхПлатежейНДФЛ");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникЗаявленияНаПолучениеПатента
//
Процедура ПриОпределенииОписанияДействияПомощникЗаявленияНаПолучениеПатента(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
	ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", Событие.РегистрацияВНалоговомОргане);
	ПараметрыФормы.Вставить("ПериодСобытия",               Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",                     Событие.Правило);
	ПараметрыФормы.Вставить("Срок",                        Событие.Срок);
	ПараметрыФормы.Вставить("Описание",                    Событие.Наименование);
	ПараметрыФормы.Вставить("ЕстьИнформацияНаИТС",         Событие.ЕстьИнформацияНаИТС);
	ПараметрыФормы.Вставить("ИдентификаторЗадачи",         Событие.ИдентификаторЗадачи);

	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Событие.Организация);
	Отбор.Вставить("ВидУведомления",
		Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеНаПолучениеПатентаРекомендованнаяФорма);

	Уведомления = УведомлениеОСпецрежимахНалогообложения.НайтиУведомления(Отбор);
	Уведомления.Сортировать("Дата Убыв, Ссылка Убыв", Новый СравнениеЗначений);

	Для Индекс = 0 По Уведомления.Количество() - 1 Цикл

		СтрокаУведомление = Уведомления[Индекс];
		Если СтрокаУведомление.Дата >= НачалоГода(Событие.ПериодСобытия) 
			И СтрокаУведомление.Дата <= КонецМесяца(Событие.Срок) Тогда
		
			СсылкаНаУведомление = СтрокаУведомление.Ссылка;
		КонецЕсли;
	КонецЦикла;

	ПараметрыФормы.Вставить("Ключ", СсылкаНаУведомление);
	ПараметрыФормы.Вставить("СоздатьНовоеЗаявление", Не ЗначениеЗаполнено(СсылкаНаУведомление));

	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникЗаполненияЗаявленияНаПолучениеПатента.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникУплатыНалога
//
Процедура ПриОпределенииОписанияДействияПомощникУплатыНалога(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
	ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", Событие.РегистрацияВНалоговомОргане);
	ПараметрыФормы.Вставить("ПериодСобытия",               Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",                     Событие.Правило);
	ПараметрыФормы.Вставить("Срок",                        Событие.Срок);
	ПараметрыФормы.Вставить("Описание",                    Событие.Наименование);
	ПараметрыФормы.Вставить("ЕстьИнформацияНаИТС",         Событие.ЕстьИнформацияНаИТС);
	ПараметрыФормы.Вставить("ИдентификаторЗадачи",         Событие.ИдентификаторЗадачи);
	ПараметрыФормы.Вставить("НачальныйСрок",               Событие.НачальныйСрок);

	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникУплатыНалога.Форма.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияСверкаИмущественныхНалогов
//
Процедура ПриОпределенииОписанияДействияСверкаИмущественныхНалогов(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",     Событие.Организация);
	Если ЗначениеЗаполнено(Событие.Задача) Тогда
		ПараметрыФормы.Вставить("Налог", Перечисления.ВидыИмущественныхНалогов.НалогПоЗадачеБухгалтера(Событие.Задача));
	Иначе
		Задача = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Событие.Правило, "Владелец");
		ПараметрыФормы.Вставить("Налог", Перечисления.ВидыИмущественныхНалогов.НалогПоЗадачеБухгалтера(Задача));
	КонецЕсли;
	ПараметрыФормы.Вставить("НалоговыйПериод", НачалоГода(Событие.ПериодСобытия));
	ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", Событие.РегистрацияВНалоговомОргане);
	ПараметрыФормы.Вставить("Правило",         Событие.Правило);
	ПараметрыФормы.Вставить("Описание",        Событие.Наименование);

	ОписаниеДействия.Вставить("ИмяФормы",           "Документ.СверкаСФНСПоИмущественнымНалогам.Форма.ФормаЗадачи");
	ОписаниеДействия.Вставить("ИдентификаторФормы", Ложь); // уникальность - искать по ключевым параметрам
	ОписаниеДействия.Вставить("ПараметрыФормы",     ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияВводОстатковПриСменеОбъектаУСН
//
Процедура ПриОпределенииОписанияДействияВводОстатковПриСменеОбъектаУСН(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Событие.Организация);
	ОписаниеДействия.Вставить("ИмяФормы", "Обработка.ВводОстатковПриИзмененииОбъектаУСН.Форма");
	ОписаниеДействия.Вставить("ИдентификаторФормы", Ложь); // уникальность - искать по ключевым параметрам
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияУведомлениеОбИсчисленныхСуммахНалогов
//
Процедура ПриОпределенииОписанияДействияУведомлениеОбИсчисленныхСуммахНалогов(Событие, ОписаниеДействия) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Событие.Организация);

	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.УведомлениеОбИсчисленныхСуммахНалогов.ФормаСписка");
	ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияЗаявлениеОЗачетеВзносовИП
//
Процедура ПриОпределенииОписанияДействияЗаявлениеОЗачетеВзносовИП(Событие, ОписаниеДействия) Экспорт
	
	ПериодСобытия = КонецКвартала(Событие.ПериодСобытия);

	ИмяФормыПомощника = "Обработка.РасчетСтраховыхВзносовИП.Форма";

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",      Событие.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия",    ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",          Событие.Правило);
	ПараметрыФормы.Вставить("Срок",             Событие.Срок);
	ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
	ПараметрыФормы.Вставить("Заголовок",        Событие.Наименование);
	
	ОписаниеДействия.Вставить("ИмяФормы",       ИмяФормыПомощника);
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомЗаполнения3НДФЛ
//
Процедура ПриОпределенииПравилоВыполняетсяПомощникомЗаполнения3НДФЛ(Событие, Выполняется) Экспорт
	
	Выполняется = Истина;
	
	Если Событие.ИдентификаторЗадачи <> "НДФЛ_Предприниматель" Тогда
		Выполняется = Ложь;
		
	ИначеЕсли Событие.ПериодСобытия < Обработки.ПомощникЗаполнения3НДФЛ.ДатаНачалаПрименения()
		Или СтрНайти(НРег(Событие.ИдентификаторПравила), "_аванс_") <> 0 Тогда
		Выполняется = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомУплатыАвансовыхПлатежейНДФЛ
//
Процедура ПриОпределенииПравилоВыполняетсяПомощникомУплатыАвансовыхПлатежейНДФЛ(Событие, Выполняется) Экспорт
	
	Если Событие.ПериодСобытия < УчетНДФЛПредпринимателя.ДатаНачалаРасчетаАвансовыхПлатежейНДФЛ() Тогда
		Выполняется = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникОплатыПатента
//
Процедура ПриОпределенииОписанияДействияПомощникОплатыПатента(Событие, ОписаниеДействия) Экспорт
	
	ОписаниеДействия = Новый Структура;
	
	Если Событие.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Отчет Тогда
		РанееПодготовленноеЗаявление = ВыполнениеЗадачБухгалтераБП.ПодготовленноеЗаявлениеНаПолучениеПатентаИзЗадачи(Событие.Правило, Событие.ПериодСобытия);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
		ПараметрыФормы.Вставить("ПериодСобытия",               Событие.ПериодСобытия);
		ПараметрыФормы.Вставить("Правило",                     Событие.Правило);
		ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", Событие.РегистрацияВНалоговомОргане);
		Если ЗначениеЗаполнено(РанееПодготовленноеЗаявление) Тогда
			СтатусЗаявления = РегламентированнаяОтчетность.СохраненныйСтатусОтправкиУведомления(РанееПодготовленноеЗаявление);
			ЗаявлениеСдано = СтатусЗаявления = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой();
			ПараметрыФормы.Вставить("Ключ",                  РанееПодготовленноеЗаявление);
			ПараметрыФормы.Вставить("ЗаявлениеСдано",        ЗаявлениеСдано);
			ПараметрыФормы.Вставить("СоздатьНовоеЗаявление", Ложь);
		Иначе
			ПараметрыФормы.Вставить("СоздатьНовоеЗаявление", Истина);
		КонецЕсли;
		ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникЗаполненияЗаявленияНаПолучениеПатента.Форма");
		ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",        Событие.Организация);
		ПараметрыФормы.Вставить("ПериодСобытия",      Событие.ПериодСобытия);
		ПараметрыФормы.Вставить("Патент",             Событие.Правило);
		ПараметрыФормы.Вставить("Действие",           Событие.Действие);
		ПараметрыФормы.Вставить("Срок",               Событие.Срок);
		ПараметрыФормы.Вставить("ДокументПатент", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Событие, "ДокументПатент", УчетПСН.ДокументПатентПоДеятельностиИСроку(Событие.Правило, Событие.Срок)));
			
		ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникОплатыПатента.Форма.Форма");
		ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникПодготовкиЗаявленияНаПолучениеПатента
//
Процедура ПриОпределенииОписанияДействияПомощникПодготовкиЗаявленияНаПолучениеПатента(Событие, ОписаниеДействия) Экспорт
	
	РанееПодготовленноеЗаявление = ВыполнениеЗадачБухгалтераБП.ПодготовленноеЗаявлениеНаПолучениеПатентаИзЗадачи(Событие.Правило, Событие.ПериодСобытия);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия",               Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",                     Событие.Правило);
	ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", Событие.РегистрацияВНалоговомОргане);
	
	Если ЗначениеЗаполнено(РанееПодготовленноеЗаявление) Тогда
		СтатусЗаявления = РегламентированнаяОтчетность.СохраненныйСтатусОтправкиУведомления(РанееПодготовленноеЗаявление);
		ЗаявлениеСдано = СтатусЗаявления = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой();
		ПараметрыФормы.Вставить("Ключ",                         РанееПодготовленноеЗаявление);
		ПараметрыФормы.Вставить("ЗаявлениеСдано",               ЗаявлениеСдано);
		ПараметрыФормы.Вставить("СоздатьНовоеЗаявление",        Ложь);
	Иначе
		ПараметрыФормы.Вставить("СоздатьНовоеЗаявление",        Истина);
	КонецЕсли;
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникЗаполненияЗаявленияНаПолучениеПатента.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
		
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияСозданиеПатента
//
Процедура ПриОпределенииОписанияДействияСозданиеПатента(Событие, ОписаниеДействия) Экспорт
	
	РанееСозданныйПатент = УчетПСН.ПатентПоВидуДеятельностиНаДату(Событие.Правило, Событие.ПериодСобытия);
	
	ПараметрыФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(РанееСозданныйПатент) Тогда
		ПараметрыФормы.Вставить("Ключ", РанееСозданныйПатент);
	Иначе
		ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
		ПараметрыФормы.Вставить("Патент",                      Событие.Правило);
		ПараметрыФормы.Вставить("РегистрацияВНалоговомОргане", Событие.РегистрацияВНалоговомОргане);
		ПараметрыФормы.Вставить("ЗаявлениеНаПолучениеПатента",
			ВыполнениеЗадачБухгалтераБП.ПодготовленноеЗаявлениеНаПолучениеПатентаИзЗадачи(Событие.Правило, Событие.ПериодСобытия, Истина));
	КонецЕсли;
	
	ОписаниеДействия.Вставить("ИмяФормы",       "Документ.ОперацияСПатентом.ФормаОбъекта");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникРасчетаНДС
//
Процедура ПриОпределенииОписанияДействияПомощникРасчетаНДС(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",      Событие.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия",    Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",          Событие.Правило);
	ПараметрыФормы.Вставить("Срок",             Событие.Срок);
	ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
	ПараметрыФормы.Вставить("ИсточникВызова",   "ОткрытиеПомощникаНДССписокЗадач");

	ОписаниеДействия.Вставить("ИмяФормы", "Обработка.ПомощникРасчетаНДС.Форма.Форма");

	Если (Событие.Правило.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога 
		Или Событие.Правило.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление)
		И Не Событие.Правило.ВыполняетсяЕдинымПомощником Тогда
		ОписаниеДействия.Вставить("ИмяФормы", "Обработка.ПомощникРасчетаНДС.Форма.ФормаОплата");
	КонецЕсли;

	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПомощникУплатыНПД
//
Процедура ПриОпределенииОписанияДействияПомощникУплатыНПД(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",                 Событие.Организация);
	ПараметрыФормы.Вставить("ПериодСобытия",               Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",                     Событие.Правило);
	ПараметрыФормы.Вставить("КонтекстныйВызов",            Истина);
	ПараметрыФормы.Вставить("Срок",                        Событие.Срок);
	ПараметрыФормы.Вставить("Описание",                    Событие.Наименование);
	ПараметрыФормы.Вставить("ЕстьИнформацияНаИТС",         Событие.ЕстьИнформацияНаИТС);
	ПараметрыФормы.Вставить("ИдентификаторЗадачи",         Событие.ИдентификаторЗадачи);

	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникУплатыНПД.Форма.Форма");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомУплатыНПД
//
Процедура ПриОпределенииПризнакаДоступнаИнтеграцияСПлатформойСамозанятые(ДоступнаИнтеграцияСПлатформойСамозанятые) Экспорт
	
	ДоступнаИнтеграцияСПлатформойСамозанятые = ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые();
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияОплатаУставногоКапитала
//
Процедура ПриОпределенииОписанияДействияОплатаУставногоКапитала(Организация, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);

	ОписаниеДействия.Вставить("ИмяФормы", "ОбщаяФорма.ОплатаУставногоКапитала");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	ОписаниеДействия.Вставить("ИдентификаторФормы", Организация.УникальныйИдентификатор());
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомУплатыАУСН
//
Процедура ПриОпределенииПравилоВыполняетсяПомощникомУплатыАУСН(Событие, Выполняется) Экспорт
	
	Выполняется = РегистрыСведений.СостоянияИнтеграцииАУСН.СервисЛКАУСНПодключен() 
		Или ЗначениеЗаполнено(РегистрыСведений.СостоянияИнтеграцииАУСН.БанкИнтеграции(Событие.Организация));
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПлатежЕНС
//
Процедура ПриОпределенииОписанияДействияПлатежЕНС(СтруктураРеквизитовУплатыНалога, ОписаниеДействия) Экспорт
	
	Организация         = СтруктураРеквизитовУплатыНалога.Организация;
	Сумма               = СтруктураРеквизитовУплатыНалога.Сумма;
	СпособОплаты        = СтруктураРеквизитовУплатыНалога.СпособУплатыНалога;
	ПериодСобытия       = СтруктураРеквизитовУплатыНалога.ПериодСобытия;
	ОплатаЗадолженности = Ложь;

	РеквизитыЕдиногоНалоговогоПлатежа =
		ПомощникиПоУплатеНалоговИВзносов.РеквизитыЕдиногоНалоговогоПлатежа(Организация,
			КонецКвартала(ПериодСобытия));
	Налог               = РеквизитыЕдиногоНалоговогоПлатежа.Налог;
	КодНалоговогоОргана = РеквизитыЕдиногоНалоговогоПлатежа.КодНалоговогоОргана;
	ОКТМО               = РеквизитыЕдиногоНалоговогоПлатежа.ОКТМО;
	КБК                 = РеквизитыЕдиногоНалоговогоПлатежа.КБК;
	ПоказательПериода   = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();

	ОписаниеПлатежа = ВыполнениеЗадачБухгалтера.НовыйОписаниеПлатежа();
	ОписаниеПлатежа.СпособОплаты = СпособОплаты;

	ОписаниеПлатежа.Организация = Организация;

	ОписаниеДействия.Вставить("ИмяФормы", Перечисления.СпособыУплатыНалогов.ИмяФормыПлатежногоДокумента(СпособОплаты));

	// Создадим платежку по данным операции
	ОписаниеПлатежа.Налог               = Налог;
	ОписаниеПлатежа.Период              = ПериодСобытия;
	ОписаниеПлатежа.ПоказательПериода   = ПоказательПериода;
	ОписаниеПлатежа.КБК                 = КБК;
	ОписаниеПлатежа.ОКАТО               = ОКТМО;
	ОписаниеПлатежа.Сумма               = Сумма;
	ОписаниеПлатежа.КодНалоговогоОргана = КодНалоговогоОргана;
	ОписаниеПлатежа.ОплатаЗадолженности = ОплатаЗадолженности;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ВыполнениеЗадачБухгалтера.ЗначенияЗаполненияПлатежногоДокумента(ОписаниеПлатежа));

	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияВзносыНаСоциальноеСтрахованиеИП
//
Процедура ПриОпределенииОписанияДействияВзносыНаСоциальноеСтрахованиеИП(Событие, ОписаниеДействия) Экспорт
	
	ОсталосьТекущихВзносовИП = УчетСтраховыхВзносовИП.СуммаФиксированныхВзносовНаВНиМ(
		Событие.Организация,
		Событие.ПериодСобытия,
		Событие.Правило);
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПериодСобытия", Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("Правило",       Событие.Правило);
	
	ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФСС;
	Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалога);
	
	Если ОсталосьТекущихВзносовИП > 0 Тогда
		
		ОписаниеПлатежа = ВыполнениеЗадачБухгалтера.НовыйОписаниеПлатежа();
		
		ОписаниеПлатежа.Организация       = Событие.Организация;
		ОписаниеПлатежа.СпособОплаты      = Перечисления.СпособыУплатыНалогов.БанковскийПеревод;
		ОписаниеПлатежа.ИмяЗадачи         = Событие.ИдентификаторЗадачи;
		ОписаниеПлатежа.Период            = Событие.ПериодСобытия;
		ОписаниеПлатежа.КБК               = Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Налог);
		ОписаниеПлатежа.Налог             = Налог;
		ОписаниеПлатежа.Сумма             = ОсталосьТекущихВзносовИП;
		ОписаниеПлатежа.ПоказательПериода = "0";
		
		ИмяФормыПлатежногоДокумента = "Документ.ПлатежноеПоручение.Форма.ФормаДокументаНалоговая";
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ВыполнениеЗадачБухгалтера.ЗначенияЗаполненияПлатежногоДокумента(ОписаниеПлатежа));
		
	Иначе
		ИмяФормыПлатежногоДокумента = "Документ.ПлатежноеПоручение.ФормаСписка";
		ПараметрыФормы.Вставить("Отбор", Новый Структура);
		ПараметрыФормы.Отбор.Вставить("Организация", Событие.Организация);
		ПараметрыФормы.Отбор.Вставить("Налог", Налог);
		
	КонецЕсли;
	
	ОписаниеДействия.Вставить("ИмяФормы", ИмяФормыПлатежногоДокумента);
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияЕдиныйПомощникУплатыНалога
//
Процедура ПриОпределенииОписанияДействияЕдиныйПомощникУплатыНалога(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = ВыполнениеЗадачБухгалтераКлиентСервер.НовыйПараметрыФормыПомощника();
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Событие);
	ПараметрыФормы.Описание = Событие.Наименование;

	ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ПомощникУплатыНалога.Форма.ФормаЕдиная");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);

	ИдентификаторФормы = ВыполнениеЗадачБухгалтера.ИдентификаторФормыПоПериодуСобытияОрганизации(Событие);
	Если ЗначениеЗаполнено(ИдентификаторФормы) Тогда
		ОписаниеДействия.Вставить("ИдентификаторФормы", ИдентификаторФормы);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияНапоминаниеОПроверкеНалогаАУСН
//
Процедура ПриОпределенииОписанияДействияНапоминаниеОПроверкеНалогаАУСН(Событие, ОписаниеДействия) Экспорт
	
	Перем БанковскиеСчетаДляСверки;
	
	Если Не СтрНачинаетсяС(Событие.ИдентификаторПравила, "2021") Тогда
		
		ЗапросСчетов = Новый Запрос;
		ЗапросСчетов.УстановитьПараметр("Организация", Событие.Организация);
		ЗапросСчетов.Текст =
		"ВЫБРАТЬ
		|	КлассификаторБанков.Ссылка КАК Счет,
		|	БанковскиеСчета.Представление КАК СчетПредставление,
		|	ВЫБОР
		|		КОГДА СостоянияИнтеграцииАУСН.Организация ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ПодключенаИнтеграцияСБанком
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК КлассификаторБанков
		|		ПО БанковскиеСчета.Банк.Код = КлассификаторБанков.Код
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияИнтеграцииАУСН КАК СостоянияИнтеграцииАУСН
		|		ПО БанковскиеСчета.Владелец = СостоянияИнтеграцииАУСН.Организация
		|			И (КлассификаторБанков.Ссылка = СостоянияИнтеграцииАУСН.Банк)
		|			И (СостоянияИнтеграцииАУСН.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияИнтеграцииАУСН.Подключено))
		|ГДЕ
		|	БанковскиеСчета.Владелец = &Организация
		|	И НЕ БанковскиеСчета.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодключенаИнтеграцияСБанком";
		ВыборкаСчетов = ЗапросСчетов.Выполнить().Выбрать();
		
		БанковскиеСчетаДляСверки = Новый СписокЗначений;
		Пока ВыборкаСчетов.Следующий() Цикл
			БанковскиеСчетаДляСверки.Добавить(ВыборкаСчетов.Счет, ВыборкаСчетов.СчетПредставление, ВыборкаСчетов.ПодключенаИнтеграцияСБанком);
		КонецЦикла;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",              Событие.Организация);
	ПараметрыФормы.Вставить("Период",                   Событие.ПериодСобытия);
	ПараметрыФормы.Вставить("СрокЗадачи",               Событие.Срок);
	Если БанковскиеСчетаДляСверки <> Неопределено Тогда
		ПараметрыФормы.Вставить("БанковскиеСчетаДляСверки", БанковскиеСчетаДляСверки);
	КонецЕсли;
	ПараметрыФормы.Вставить("КонтекстныйВызов",         Истина);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии",  Истина);
	
	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("ИмяФормы", "Отчет.БанковскиеОперацииАУСН.ФормаОбъекта");
	ОписаниеДействия.Вставить("ИдентификаторФормы", Событие.Организация.УникальныйИдентификатор());
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияУплатаНалогаАУСН
//
Процедура ПриОпределенииОписанияДействияУплатаНалогаАУСН(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Срок", Событие.Срок);
	ПараметрыФормы.Вставить("Заголовок", Событие.Наименование);
	ПараметрыФормы.Вставить("ДатаНачала", Событие.НачальныйСрок);
	
	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("ИмяФормы", "ОбщаяФорма.НапоминаниеОплатитьАУСН");
	ОписаниеДействия.Вставить("ИдентификаторФормы", Событие.Организация.УникальныйИдентификатор());
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияУплатаФСС_НС_АУСН
//
Процедура ПриОпределенииОписанияДействияУплатаФСС_НС_АУСН(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыФормы = Новый Структура;
	
	ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	СтраховыеВзносы_ФСС_НСиПЗ = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(
		Перечисления.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ,
		Истина);
		
	СуществующийДокумент = Неопределено;
	
	Запрос =Новый Запрос;
	Запрос.УстановитьПараметр("Организация",      Событие.Организация);
	Запрос.УстановитьПараметр("Правило",          Событие.Правило);
	Запрос.УстановитьПараметр("ПериодСобытия",    Событие.ПериодСобытия);
	Запрос.УстановитьПараметр("Налог",            СтраховыеВзносы_ФСС_НСиПЗ);
	Запрос.УстановитьПараметр("ВидОперации",      Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Платежи
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Проведен
	|	И НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И ПлатежноеПоручение.Организация = &Организация
	|	И ПлатежноеПоручение.ВидОперации = &ВидОперации
	|	И ПлатежноеПоручение.Налог = &Налог
	|	И ПлатежноеПоручение.СуммаДокумента > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ИЗ
	|	ВТ_Платежи КАК ПлатежныйДокумент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
	|		ПО ПлатежныйДокумент.Ссылка = ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение
	|ГДЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация = &Организация
	|	И ЗадачиБухгалтераНалоговыеПлатежи.ПериодСобытия = &ПериодСобытия
	|	И ЗадачиБухгалтераНалоговыеПлатежи.Правило = &Правило";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ПлатежноеПоручение) Тогда
			СуществующийДокумент = Выборка.ПлатежноеПоручение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если СуществующийДокумент <> Неопределено Тогда
		ПараметрыФормы.Вставить("Ключ", СуществующийДокумент);
		ПараметрыФормы.Вставить("Правило",              Событие.Правило);
		ПараметрыФормы.Вставить("ПериодСобытия",        Событие.ПериодСобытия);
	Иначе
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Дата", ОбщегоНазначения.ТекущаяДатаПользователя());
		ЗначенияЗаполнения.Вставить("Организация", Событие.Организация);
		ЗначенияЗаполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога);
		ЗначенияЗаполнения.Вставить("Налог", СтраховыеВзносы_ФСС_НСиПЗ);
		ЗначенияЗаполнения.Вставить("ВидНалоговогоОбязательства", Перечисления.ВидыПлатежейВГосБюджет.Налог);
		ЗначенияЗаполнения.Вставить("СуммаДокумента", УчетЗарплаты.СтраховыеВзносы_ФСС_НСиПЗ(Событие.Организация, Событие.ПериодСобытия));
		ЗначенияЗаполнения.Вставить("НазначениеПлатежа",
			СтрШаблон(НСтр("ru = 'Взносы на страхование от несчастных случаев при Автоматизированной упрощенной системе налогообложения за %1'"),
				Формат(Событие.ПериодСобытия, "ДФ='MMMM yyyy ''года'''")));
		
		ПараметрыФормы.Вставить("ЗначенияЗаполнения",   ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("Правило",              Событие.Правило);
		ПараметрыФормы.Вставить("ПериодСобытия",        Событие.ПериодСобытия);
	КонецЕсли;
	
	ОписаниеДействия.Вставить("ИмяФормы", "Документ.ПлатежноеПоручение.Форма.ФормаДокумента");
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияПередачаСведенийОСотрудникахАУСН
//
Процедура ПриОпределенииОписанияДействияПередачаСведенийОСотрудникахАУСН(Событие, ОписаниеДействия) Экспорт
	
	ПараметрыОткрытия = Обработки.ПомощникУчетаЗарплаты.ПараметрыОтчетаВыплатыСотрудникамАУСН(
		Событие.Организация,
		Событие.ПериодСобытия,
		Событие);
	
	ОписаниеДействия.Вставить("ИмяФормы", "Справочник.ВариантыОтчетов.ФормаОбъекта");
	ОписаниеДействия.Вставить("ИдентификаторФормы", Событие.Организация.УникальныйИдентификатор());
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыОткрытия);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияСведенияЭТКЕжемесячные
//
Процедура ПриОпределенииОписанияДействияСведенияЭТКЕжемесячные(Событие, ОписаниеДействия) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация",       Событие.Организация);
	Отбор.Вставить("ПериодСобытия",     Событие.ПериодСобытия);
	Отбор.Вставить("ПометкаУдаления",   Ложь);
	Отбор.Вставить("ПериодичностьДень", Событие.Периодичность = Перечисления.Периодичность.День);
	
	Если Событие.ИдентификаторЗадачи = "ТрудовыеКнижки" Тогда
		ЗаписиТрудовыхКнижек = УчетЗарплаты.НайтиЗаписиТрудовыхКнижек(Отбор);
		Если ЗаписиТрудовыхКнижек.Количество() = 0 Или УчетЗарплаты.ЕстьНедооформленныеЗаписиТрудовыхКнижек(Отбор) Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация",                Событие.Организация);
			ПараметрыФормы.Вставить("ОкончаниеПериодаЗаполнения", Событие.Срок);
			ПараметрыФормы.Вставить("ДатаСобытия",                Событие.Срок);
			ОписаниеДействия.Вставить("ИмяФормы",       "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.ФормаОбъекта");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
		ИначеЕсли ЗаписиТрудовыхКнижек.Количество() > 1 Тогда
			ОписаниеДействия.Вставить("ИмяФормы",       "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.ФормаСписка");
			ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
		ИначеЕсли ЗаписиТрудовыхКнижек.Количество() = 1 Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", ЗаписиТрудовыхКнижек[0].Ссылка);
			ОписаниеДействия.Вставить("ИмяФормы",       "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.ФормаОбъекта");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
		КонецЕсли;
	Иначе
		ОписаниеДействия.Вставить("ИмяФормы",       "Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.ФормаСписка");
		ОписаниеДействия.Вставить("ПараметрыФормы", Новый Структура("Отбор", Отбор));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Статусы

Процедура ПриОпределенииВыполненияЗадачПоПродлениюПатента(ТаблицаЗадач, ВыполнениеЗадач) Экспорт
	
	// Индекс нужен для группировки нескольких документов по одному набору параметров.
	Если ВыполнениеЗадач.Колонки.Найти("Индекс") = Неопределено Тогда
		ВыполнениеЗадач.Колонки.Добавить("Индекс", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	Для Каждого Задача Из ВыполнениеЗадач Цикл
		Задача.Индекс = ВыполнениеЗадач.Индекс(Задача);
	КонецЦикла;
	
	// Описание таблицы документов, заполняемой для каждой задачи.
	ТаблицаДокументы = Новый ТаблицаЗначений;
	ТаблицаДокументы.Колонки.Добавить("Ссылка", Метаданные.РегистрыСведений.ЖурналОтправокВКонтролирующиеОрганы.Измерения.Ссылка.Тип);
	ТаблицаДокументы.Колонки.Добавить("Дата",   ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаДокументы.Колонки.Добавить("Статус", Новый ОписаниеТипов("Строка"));
	ТаблицаДокументы.Колонки.Добавить("СостояниеСдачиОтчетности",
		Новый ОписаниеТипов("ПеречислениеСсылка.СостояниеСдачиОтчетности"));

	ВыполнениеЗадач.Колонки.Добавить("Документы", Новый ОписаниеТипов("ТаблицаЗначений"));

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задачи", ВыполнениеЗадач);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задачи.Индекс КАК Индекс,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус
	|ПОМЕСТИТЬ ВТ_Задачи
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Задачи.Индекс КАК Индекс,
	|	ВТ_Задачи.Организация КАК Организация,
	|	ВТ_Задачи.ПериодСобытия КАК ПериодСобытия,
	|	ВТ_Задачи.Правило КАК Правило,
	|	ВТ_Задачи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтераПродлениеПатента.Заявление КАК Ссылка,
	|	ВТ_Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ВТ_Задачи.РучнойСтатус КАК РучнойСтатус,
	|	УведомлениеОСпецрежимахНалогообложения.ДатаПодписи КАК Дата
	|ПОМЕСТИТЬ ВТ_ЗадачиБухгалтераПродленияПатентов
	|ИЗ
	|	ВТ_Задачи КАК ВТ_Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтераПродлениеПатента КАК ЗадачиБухгалтераПродлениеПатента
	|		ПО ВТ_Задачи.Организация = ЗадачиБухгалтераПродлениеПатента.Организация
	|			И ВТ_Задачи.Правило = ЗадачиБухгалтераПродлениеПатента.Правило
	|			И ВТ_Задачи.ПериодСобытия = ЗадачиБухгалтераПродлениеПатента.ПериодСобытия
	|			И ВТ_Задачи.РегистрацияВНалоговомОргане = ЗадачиБухгалтераПродлениеПатента.РегистрацияВНалоговомОргане
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОСпецрежимахНалогообложения КАК УведомлениеОСпецрежимахНалогообложения
	|		ПО ВТ_Задачи.Организация = УведомлениеОСпецрежимахНалогообложения.Организация
	|			И (ЗадачиБухгалтераПродлениеПатента.Заявление = УведомлениеОСпецрежимахНалогообложения.Ссылка)
	|			И (НЕ УведомлениеОСпецрежимахНалогообложения.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.Индекс КАК Индекс,
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.Организация КАК Организация,
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.ПериодСобытия КАК ПериодСобытия,
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.Правило КАК Правило,
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.Ссылка КАК Ссылка,
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.РучнойСтатус КАК РучнойСтатус,
	|	ВТ_ЗадачиБухгалтераПродленияПатентов.Дата КАК Дата,
	|	ЕСТЬNULL(ЖурналОтправокВКонтролирующиеОрганы.СтатусОтправки, """") КАК Статус,
	|	ЕСТЬNULL(ЖурналОтправокВКонтролирующиеОрганы.СостояниеСдачиОтчетности, ЗНАЧЕНИЕ(Перечисление.СостояниеСдачиОтчетности.ПустаяСсылка)) КАК СостояниеСдачиОтчетности
	|ИЗ
	|	ВТ_ЗадачиБухгалтераПродленияПатентов КАК ВТ_ЗадачиБухгалтераПродленияПатентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
	|		ПО ВТ_ЗадачиБухгалтераПродленияПатентов.Организация = ЖурналОтправокВКонтролирующиеОрганы.Организация
	|			И ВТ_ЗадачиБухгалтераПродленияПатентов.Ссылка = ЖурналОтправокВКонтролирующиеОрганы.Ссылка
	|			И (ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала = ЗНАЧЕНИЕ(Перечисление.СтраницыЖурналаОтчетность.Уведомления))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ
	|ИТОГИ ПО
	|	Индекс";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаИндекс = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИндекс.Следующий() Цикл

		Задача = ВыполнениеЗадач[ВыборкаИндекс.Индекс];
		Задача.Документы = ТаблицаДокументы.СкопироватьКолонки();

		Выборка = ВыборкаИндекс.Выбрать();

		Пока Выборка.Следующий() Цикл
			Если Не ОбщегоНазначения.СсылкаСуществует(Выборка.Ссылка) Тогда
				Продолжить;
			КонецЕсли;
		
			ЗаполнитьЗначенияСвойств(Задача.Документы.Добавить(), Выборка);
		КонецЦикла;

	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗарегистрироватьИзменениеСтатусаЗадачиПодготовкиОтчета
//
Процедура ПриОпределенииПризнакаПеренестиВАрхив(Документ, ПеренестиВАрхив) Экспорт
	
	Если ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВИнформационнойБазеВключена()
		И ЗначениеЗаполнено(Документ)
		И ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", Документ.Метаданные()) Тогда

		// При интеграции с банками переносим задачи в архив автоматически
		ОрганизацияДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Организация");
		ПеренестиВАрхив = Справочники.НастройкиИнтеграцииСБанками.ИнтеграцияОрганизацииВключена(ОрганизацияДокумента);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗарегистрироватьИзменениеСтатусаЗадачиФинОтчетности
//
Процедура ОпределитьСтатусОтправкиФинОтчетностиВБанкиСдано(ФинОтчетностьСдана, Статус) Экспорт
	ФинОтчетностьСдана = (Статус = Перечисления.СтатусыОтправкиФинОтчетностиВБанки.Сдано);
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗарегистрироватьИзменениеСтатусаЗадачиФинОтчетности
//
Процедура ПриОпределенииПравилаФинОтчетности(РеквизитыДокумента, Правило) Экспорт
	
	Правило = Справочники.ПравилаФинОтчетности.НайтиПоРеквизитам(РеквизитыДокумента.Организация,
		РеквизитыДокумента.Банк, РеквизитыДокумента.ИдентификаторКомплекта, РеквизитыДокумента.Дата);
		
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач
//
Процедура ПриДобавленииСтатусовЗадач(ТаблицаЗадач, ЗадачиПодготовкаОтчета, КлючиЗадачи) Экспорт
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиСвязанныеСОтчетом
//
Процедура ПриОпределенииЗадачСвязанныхСОтчетом(ЗадачиСвязанныеСОтчетом, Документ, СвойстваОтчета, ИсточникОтчета) Экспорт
	
	Если ЕдиныйНалоговыйСчет.ЭтоУведомлениеПоЕдиномуНалоговомуПлатежу(ИсточникОтчета) Тогда
		
		ЗадачиСвязанныеСОтчетом = ВыполнениеЗадачБухгалтера.ЗадачиДляУведомления(Документ, СвойстваОтчета); 
		
	ИначеЕсли ЕдиныйНалоговыйСчет.ЭтоЗаявлениеОЗачетеНалогаПоЕдиномуНалоговомуПлатежу(ИсточникОтчета) Тогда
		
		ЗадачиСвязанныеСОтчетом = ВыполнениеЗадачБухгалтера.ЗадачиДляЗаявленияОЗачетеНалога(Документ, СвойстваОтчета);
		
	ИначеЕсли УчетПСНКлиентСервер.ЭтоЗаявлениеНаПолучениеПатента(ИсточникОтчета) Тогда
		
		ЗадачиСвязанныеСОтчетом = ВыполнениеЗадачБухгалтера.ЗадачиДляЗаявленияНаПолучениеПатента(Документ, СвойстваОтчета);
		
	Иначе
		
		ЗадачиСвязанныеСОтчетом = ВыполнениеЗадачБухгалтера.ЗадачиДляОтчета(Документ, СвойстваОтчета);
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиДляОтчета
//
Процедура ПриПолученииОтбораНалоговыеОрганы(ОтборНалоговыеОрганы, Организация, КодНалоговогоОргана) Экспорт
	
	РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(Организация, "", КодНалоговогоОргана);
	
	ОтборНалоговыеОрганы.Добавить(Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	ОтборНалоговыеОрганы.Добавить(РегистрацияВНалоговомОргане);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиДляЗаявленияОЗачетеНалога
//
Процедура ПриПолученииДанныхЗадачиПоЗаявлению(ДанныеЗадачиПоЗаявлению, ЗаявлениеОЗачетеНалога) Экспорт
	
	ДанныеЗадачиПоЗаявлению = РегистрыСведений.ЗадачиБухгалтераУведомления.ДанныеЗадачиПоУведомлению(ЗаявлениеОЗачетеНалога);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиДляУведомления
//
Процедура ПриПолученииЗадачДляУведомления(ЗадачиДляУведомления, Организация, ВидыКодыНалогов, УведомлениеОбИсчисленныхСуммахНалога, ПравилаИсключения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидыКодыНалогов", ВидыКодыНалогов);
	Запрос.УстановитьПараметр("УведомлениеОбИсчисленныхСуммахНалога", УведомлениеОбИсчисленныхСуммахНалога);
	Запрос.УстановитьПараметр("ДатаНачалаЕНС", НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж());
	Запрос.УстановитьПараметр("ПравилаИсключения",    ПравилаИсключения);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыКодыНалогов.ВидНалога КАК ВидНалога,
	|	ВидыКодыНалогов.КодЗадачи КАК КодНалога
	|ПОМЕСТИТЬ ВидыКодыНалогов
	|ИЗ
	|	&ВидыКодыНалогов КАК ВидыКодыНалогов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидНалога
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Уведомления.Организация КАК Организация,
	|	УведомленияНалоги.Налог.ВидНалога КАК ВидНалога,
	|	УведомленияНалоги.НачалоПериода КАК НачалоПериода,
	|	УведомленияНалоги.КонецПериода КАК КонецПериода,
	|	УведомленияНалоги.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ УведомленияБезКодов
	|ИЗ
	|	Документ.УведомлениеОбИсчисленныхСуммахНалогов КАК Уведомления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомленияНалоги
	|		ПО Уведомления.Ссылка = УведомленияНалоги.Ссылка
	|ГДЕ
	|	Уведомления.Ссылка = &УведомлениеОбИсчисленныхСуммахНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидНалога
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УведомленияБезКодов.Организация КАК Организация,
	|	ПравилаПредставленияОтчетовУплатыНалогов.Ссылка КАК Правило,
	|	ЗадачиБухгалтера.Код КАК КодЗадачи,
	|	УведомленияБезКодов.НачалоПериода КАК НачалоПериода,
	|	УведомленияБезКодов.КонецПериода КАК КонецПериода,
	|	УведомленияБезКодов.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ Уведомления
	|ИЗ
	|	УведомленияБезКодов КАК УведомленияБезКодов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыКодыНалогов КАК ВидыКодыНалогов
	|		ПО УведомленияБезКодов.ВидНалога = ВидыКодыНалогов.ВидНалога
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ПО (ВидыКодыНалогов.КодНалога = ЗадачиБухгалтера.Код)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО (ПравилаПредставленияОтчетовУплатыНалогов.Владелец = ЗадачиБухгалтера.Ссылка)
	|			И (ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление))
	|			И (ПравилаПредставленияОтчетовУплатыНалогов.КонецДействия >= &ДатаНачалаЕНС
	|				ИЛИ ПравилаПредставленияОтчетовУплатыНалогов.КонецДействия = ДАТАВРЕМЯ(1, 1, 1))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераУведомления.Организация,
	|	ЗадачиБухгалтераУведомления.Правило,
	|	ЗадачиБухгалтераУведомления.Правило.Владелец.Код,
	|	ЗадачиБухгалтераУведомления.ПериодСобытия,
	|	ЗадачиБухгалтераУведомления.ПериодСобытия,
	|	ЗадачиБухгалтераУведомления.РегистрацияВНалоговомОргане
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераУведомления КАК ЗадачиБухгалтераУведомления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбИсчисленныхСуммахНалогов КАК УведомлениеОбИсчисленныхСуммахНалогов
	|		ПО ЗадачиБухгалтераУведомления.Уведомление = УведомлениеОбИсчисленныхСуммахНалогов.Ссылка
	|ГДЕ
	|	ЗадачиБухгалтераУведомления.Уведомление = &УведомлениеОбИсчисленныхСуммахНалога
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус КАК Статус,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗадачиБухгалтера.Действие КАК Действие,
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Уведомления КАК Уведомления
	|		ПО ЗадачиБухгалтера.Организация = Уведомления.Организация
	|			И ЗадачиБухгалтера.Правило = Уведомления.Правило
	|			И ЗадачиБухгалтера.ПериодСобытия = Уведомления.КонецПериода
	|			И ЗадачиБухгалтера.РегистрацияВНалоговомОргане = Уведомления.РегистрацияВНалоговомОргане
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И НЕ ЗадачиБухгалтера.Правило В (&ПравилаИсключения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиБухгалтера.Организация,
	|	ЗадачиБухгалтера.Правило,
	|	ЗадачиБухгалтера.ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус,
	|	ЗадачиБухгалтера.Наименование,
	|	ЗадачиБухгалтера.Периодичность,
	|	ЗадачиБухгалтера.Действие,
	|	ЗадачиБухгалтера.Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Уведомления КАК Уведомления
	|		ПО ЗадачиБухгалтера.Организация = Уведомления.Организация
	|			И ЗадачиБухгалтера.Правило = Уведомления.Правило
	|			И ЗадачиБухгалтера.ПериодСобытия = Уведомления.КонецПериода
	|			И (ЗадачиБухгалтера.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление)
	|	И НЕ ЗадачиБухгалтера.Правило В (&ПравилаИсключения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация,
	|	ЗадачиБухгалтера.Правило,
	|	ЗадачиБухгалтера.ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус,
	|	ЗадачиБухгалтера.Наименование,
	|	ЗадачиБухгалтера.Периодичность,
	|	ЗадачиБухгалтера.Действие,
	|	ЗадачиБухгалтера.Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Уведомления КАК Уведомления
	|		ПО ЗадачиБухгалтера.Организация = Уведомления.Организация
	|			И ЗадачиБухгалтера.Правило = Уведомления.Правило
	|			И (ЗадачиБухгалтера.ПериодСобытия = Уведомления.КонецПериода
	|				И НАЧАЛОПЕРИОДА(Уведомления.НачалоПериода, МЕСЯЦ) <> Уведомления.НачалоПериода)
	|			И ЗадачиБухгалтера.РегистрацияВНалоговомОргане = Уведомления.РегистрацияВНалоговомОргане
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Правило В(&ПравилаИсключения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗадачиБухгалтера.Организация,
	|	ЗадачиБухгалтера.Правило,
	|	ЗадачиБухгалтера.ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус,
	|	ЗадачиБухгалтера.Наименование,
	|	ЗадачиБухгалтера.Периодичность,
	|	ЗадачиБухгалтера.Действие,
	|	ЗадачиБухгалтера.Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Уведомления КАК Уведомления
	|		ПО ЗадачиБухгалтера.Организация = Уведомления.Организация
	|			И ЗадачиБухгалтера.Правило = Уведомления.Правило
	|			И (ЗадачиБухгалтера.ПериодСобытия = Уведомления.КонецПериода
	|				И НАЧАЛОПЕРИОДА(Уведомления.НачалоПериода, МЕСЯЦ) <> Уведомления.НачалоПериода)
	|			И (ЗадачиБухгалтера.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление)
	|	И ЗадачиБухгалтера.Правило В(&ПравилаИсключения)";
	
	ЗадачиДляУведомления = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиДляЗаявленияНаПолучениеПатента
//
Процедура ПриПолученииЗадачДляЗаявленияНаПолучениеПатента(ЗадачиДляЗаявленияНаПолучениеПатента, Документ, СвойстваОтчета) Экспорт
	
	Организация = СвойстваОтчета.Организация;
	
	ДанныеЗадачиПоЗаявлению = РегистрыСведений.ЗадачиБухгалтераПродлениеПатента.ДанныеЗадачиПоЗаявлению(Организация, Документ);
	ПериодСобытия = ДанныеЗадачиПоЗаявлению.ПериодСобытия;
	ОтборПоПравилу = ДанныеЗадачиПоЗаявлению.Правило;
	
	Если Не ЗначениеЗаполнено(ОтборПоПравилу) Тогда
		Возврат;
	КонецЕсли;
	
	Если СвойстваОтчета.ВидКонтролирующегоОргана = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
		КодНалоговогоОргана = СвойстваОтчета.КодКонтролирующегоОргана;
	Иначе
		КодНалоговогоОргана = "";
	КонецЕсли;
	
	РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(
		Организация, "", КодНалоговогоОргана);
	
	ОтборНалоговыеОрганы = Новый Массив;
	ОтборНалоговыеОрганы.Добавить(Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	ОтборНалоговыеОрганы.Добавить(РегистрацияВНалоговомОргане);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",          Организация);
	Запрос.УстановитьПараметр("ОтборНалоговыеОрганы", ОтборНалоговыеОрганы);
	Запрос.УстановитьПараметр("ОтборПоПравилу",       ОтборПоПравилу);
	Запрос.УстановитьПараметр("ПериодСобытия",        ПериодСобытия);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Статус КАК Статус,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗадачиБухгалтера.Действие КАК Действие,
	|	ЗадачиБухгалтера.Срок КАК Срок
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.РегистрацияВНалоговомОргане В (&ОтборНалоговыеОрганы)
	|	И ЗадачиБухгалтера.Правило = &ОтборПоПравилу
	|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытия
	|	И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Отчет)";
	
	ЗадачиДляЗаявленияНаПолучениеПатента = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиСвязанныеСПлатежом
//
Процедура ПриОпределенииТаблицыЗадачСвязанныхСПлатежами(ТаблицаЗадач, РеквизитыПлатежа, ПараметрыРасчетаСтатусов) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(
		ВыполнениеЗадачБухгалтераБП.СвязанныеЗадачиПодготовкиРегулярныхПлатежей(РеквизитыПлатежа),
		ТаблицаЗадач);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(
		ВыполнениеЗадачБухгалтераБП.СвязанныеЗадачиУплатыНалога(РеквизитыПлатежа, ПараметрыРасчетаСтатусов),
		ТаблицаЗадач);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.СвязанныеЗадачиВыплатыЗарплаты
//
Процедура ПолучитьСвязанныеЗадачиВыплатыЗарплаты(ТаблицаЗадач, РеквизитыДокумента, ПравилоВыплатыЗарплатыАванс) Экспорт
	
	ДокументОбрабатывается = ВыполнениеЗадачБухгалтераБП.ЭтоВыплатаЗарплаты(РеквизитыДокумента.ВидОперации)
		ИЛИ (ВыполнениеЗадачБухгалтераБП.ЭтоУплатаНалога(РеквизитыДокумента.ВидОперации)
			И РеквизитыДокумента.ВидНалога = Перечисления.ВидыНалогов.НДФЛ);
	
	Если Не ДокументОбрабатывается Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",        РеквизитыДокумента.Организация);
	Запрос.УстановитьПараметр("ПравилоАванс",       ПравилоВыплатыЗарплатыАванс);
	Запрос.УстановитьПараметр("СпособВыплатыАванс", Справочники.СпособыВыплатыЗарплаты.Аванс());
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.ПлатежнаяВедомость) Тогда
		
		Запрос.УстановитьПараметр("Ведомость", РеквизитыДокумента.ПлатежнаяВедомость);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗадачиБухгалтера.Организация КАК Организация,
		|	ЗадачиБухгалтера.Правило КАК Правило,
		|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
		|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	ЗадачиБухгалтера.Правило.Действие КАК Действие
		|ИЗ
		|	Документ.%Ведомость% КАК ВедомостьНаВыплатуЗарплаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|		ПО ВедомостьНаВыплатуЗарплаты.Организация = ЗадачиБухгалтера.Организация
		|			И КОНЕЦПЕРИОДА(ВедомостьНаВыплатуЗарплаты.ПериодРегистрации, МЕСЯЦ) = КОНЕЦПЕРИОДА(ЗадачиБухгалтера.ПериодСобытия, МЕСЯЦ)
		|			И &УсловиеАванс
		|			И (ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПравилаПредставленияОтчетовУплатыНалогов)
		|			И (ЗадачиБухгалтера.Правило.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата))
		|ГДЕ
		|	ВедомостьНаВыплатуЗарплаты.Ссылка = &Ведомость
		|	И ЗадачиБухгалтера.Организация = &Организация
		|	И ЗадачиБухгалтера.Правило ССЫЛКА Справочник.ПравилаПредставленияОтчетовУплатыНалогов
		|	И ЗадачиБухгалтера.Правило.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата)
		|	И &УсловиеАванс";
		
		ВедомостьМетаданные = РеквизитыДокумента.ПлатежнаяВедомость.Метаданные();
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Ведомость%", ВедомостьМетаданные.Имя);
		Если ВедомостьМетаданные.Реквизиты.Найти("СпособВыплаты") <> Неопределено Тогда
			
			ТекстЗамены = "(ВЫБОР
			|				КОГДА ВедомостьНаВыплатуЗарплаты.СпособВыплаты = &СпособВыплатыАванс
			|					ТОГДА ЗадачиБухгалтера.Правило = &ПравилоАванс
			|				ИНАЧЕ ЗадачиБухгалтера.Правило <> &ПравилоАванс
			|			КОНЕЦ)";
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеАванс", ТекстЗамены);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеАванс", "ИСТИНА");
		КонецЕсли;
		
		ТаблицаЗадач = Запрос.Выполнить().Выгрузить();
		
	Иначе
		// Если ведомость не заполнена, то это уплата НДФЛ.
		
		ОписаниеПериода = ПлатежиВБюджетКлиентСервер.РазобратьНалоговыйПериод(РеквизитыДокумента.ПоказательПериода);
		Периодичность   = ВыполнениеЗадачБухгалтераБП.ПериодичностьПоПоказателюПериода(ОписаниеПериода.Периодичность);
		ПериодСобытия   = НачалоДня(ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(Периодичность, ОписаниеПериода.Дата));
		
		Запрос.УстановитьПараметр("ПериодСобытияЗарплата", ПериодСобытия);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗадачиБухгалтера.Организация КАК Организация,
		|	ЗадачиБухгалтера.Правило КАК Правило,
		|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
		|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата) КАК Действие
		|ИЗ
		|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
		|ГДЕ
		|	ЗадачиБухгалтера.Правило.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата)
		|	И ЗадачиБухгалтера.ПериодСобытия = &ПериодСобытияЗарплата
		|	И ЗадачиБухгалтера.Организация = &Организация
		|	И ЗадачиБухгалтера.Правило <> &ПравилоАванс";
		
		ТаблицаЗадач = Запрос.Выполнить().Выгрузить();
		
		// Убедимся, что документ уплаты НДФЛ добавлен в служебный регистр "ЗадачиБухгалтераНалоговыеПлатежи"
		Для Каждого Задача Из ТаблицаЗадач Цикл
			РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи.ДобавитьЗаписьПравила(
				Задача.Организация, РеквизитыДокумента.Ссылка, Задача.Правило, Задача.ПериодСобытия);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиПродлениеПатента
//
Процедура ПриОпределенииТаблицыЗадачПродленияПатента(ЗадачиПродлениеПатента, ТаблицаЗадач) Экспорт
	
	Для Каждого СтрокаЗадача Из ТаблицаЗадач Цикл
		Если ТипЗнч(СтрокаЗадача.Правило) <> Тип("СправочникСсылка.Патенты") Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЗадачиПродлениеПатента.Добавить(), СтрокаЗадача);
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.СтатусыЗадачПоФинОтчетности
//
Процедура ПриОпределенииФинОтчетаВБанк(Отчет, Правило, Период, Статус) Экспорт
	
	Отчет = Документы.ФинОтчетВБанк.НайтиПоРеквизитам(Правило.Организация,
		Правило.Получатель, Правило.ИдентификаторКомплекта, Период);
	
	Если ЗначениеЗаполнено(Отчет)
		И ФинОтчетностьВБанки.СтатусОтчета(Отчет) = Перечисления.СтатусыОтправкиФинОтчетностиВБанки.Сдано Тогда
		Статус = НСтр("ru = 'Сдано'");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗаполнитьВыполнениеЗадачНеОбрабатываемыхБРО
//
// Удаление из массива пустых элементов
//
// Параметры:
//   МассивЭлементов - Массив
//
Процедура УдалитьНеЗаполненныеЭлементыМассива(МассивЭлементов) Экспорт
	
	ОбщегоНазначенияБПКлиентСервер.УдалитьНеЗаполненныеЭлементыМассива(МассивЭлементов);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.СтатусыЗадачПоУплатеНалогов
//
Процедура ПриОпределенииСтатусовЗадачПоУплатеНалогов(ЗадачиСоСтатусами, Выборка) Экспорт
	
	ПараметрыЗадач = Новый Структура;
	
	ПараметрыЗадач.Вставить("СтраховыеВзносыИП",     Обработки.РасчетСтраховыхВзносовИП.НовыеПараметрыЗадач());
	ПараметрыЗадач.Вставить("ЕНВД",                  Обработки.ПомощникРасчетаЕНВД.НовыеПараметрыЗадач());
	ПараметрыЗадач.Вставить("УСН",                   Обработки.ПомощникРасчетаНалогаУСН.НовыеПараметрыЗадач());
	ПараметрыЗадач.Вставить("НДФЛ_ИП",               Обработки.ПомощникЗаполнения3НДФЛ.НовыеПараметрыЗадач());
	ПараметрыЗадач.Вставить("Патент",                Обработки.ПомощникОплатыПатента.НовыеПараметрыЗадач());
	ПараметрыЗадач.Вставить("УчетЗарплаты",          Обработки.ПомощникУчетаЗарплаты.НовыеПараметрыЗадач());
	ПараметрыЗадач.Вставить("НДС",                   Обработки.ПомощникРасчетаНДС.НовыеПараметрыЗадач());
	ПараметрыЗадач.Вставить("УниверсальныйПомощник", Обработки.ПомощникУплатыНалога.НовыеПараметрыЗадач());
	ПараметрыЗадач.Вставить("АУСН",                  ВыполнениеЗадачБухгалтера.НовыеПараметрыЗадачБухгалтера());
	ПараметрыЗадач.Вставить("ДобровольныеВзносыИП",  ВыполнениеЗадачБухгалтера.НовыеПараметрыЗадачБухгалтера());
	
	Пока Выборка.Следующий() Цикл
		
		Правило             = Выборка.Правило;
		ИдентификаторЗадачи = Выборка.ИдентификаторЗадачи;
		ПолноеИмяПравила    = Выборка.ПолноеИмяПравила;
		
		ГруппаПараметров = Неопределено;
		
		Если ТипЗнч(Правило) = Тип("СправочникСсылка.Патенты") Тогда
			
			ГруппаПараметров = "Патент";
			
		ИначеЕсли ЗадачиБухгалтераКлиентСервер.ЭтоУплатаДобровольныхВзносовИП(Выборка.ИдентификаторПравила) Тогда
			
			ГруппаПараметров = "ДобровольныеВзносыИП";
			
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомРасчетСтраховыхВзносовИП(Правило, ИдентификаторЗадачи) Тогда
			
			ГруппаПараметров = "СтраховыеВзносыИП";
			
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомЕНВД(Правило, ИдентификаторЗадачи) Тогда
			
			ГруппаПараметров = "ЕНВД";
			
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомРасчетаНалогаУСН(Правило, ПолноеИмяПравила) Тогда
			
			ГруппаПараметров = "УСН";
		
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомУплатыНалогаАУСН(Правило, ПолноеИмяПравила) Тогда
			
			ГруппаПараметров = "АУСН";
		
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомУплатыАвансовыхПлатежейНДФЛ(Выборка)
			Или ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомЗаполнения3НДФЛ(Выборка) Тогда
			
			ГруппаПараметров = "НДФЛ_ИП";
			
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомУчетаЗарплаты(Правило, ИдентификаторЗадачи) 
			И Не ВыполнениеЗадачБухгалтера.ПравилоМожетВыполнятьсяУниверсальнымПомощником(Правило, ИдентификаторЗадачи, ПолноеИмяПравила) Тогда
			
			ГруппаПараметров = "УчетЗарплаты";
			
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяПомощникомРасчетаНДС(Правило, ПолноеИмяПравила) Тогда
			
			ГруппаПараметров = "НДС";
			
		ИначеЕсли ВыполнениеЗадачБухгалтера.ПравилоВыполняетсяУниверсальнымПомощником(Правило, ИдентификаторЗадачи, ПолноеИмяПравила)
			Или ВыполнениеЗадачБухгалтера.ПравилоМожетВыполнятьсяУниверсальнымПомощником(Правило, ИдентификаторЗадачи, ПолноеИмяПравила) Тогда
			
			ГруппаПараметров = "УниверсальныйПомощник";
			
		КонецЕсли;
		
		Если ГруппаПараметров <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыЗадач[ГруппаПараметров].Добавить(), Выборка);
		КонецЕсли;
		
	КонецЦикла;
	
	Обработки.РасчетСтраховыхВзносовИП.ЗаполнитьСтатусыЗадач(ПараметрыЗадач.СтраховыеВзносыИП);
	Обработки.ПомощникРасчетаНалогаУСН.ЗаполнитьСтатусыЗадач(ПараметрыЗадач.УСН);
	Обработки.ПомощникЗаполнения3НДФЛ.ЗаполнитьСтатусыЗадач(ПараметрыЗадач.НДФЛ_ИП);
	Обработки.ПомощникРасчетаЕНВД.ЗаполнитьСтатусыЗадач(ПараметрыЗадач.ЕНВД);
	Обработки.ПомощникОплатыПатента.ЗаполнитьСтатусыЗадач(ПараметрыЗадач.Патент);
	Обработки.ПомощникУплатыНалога.ЗаполнитьСтатусыЗадач(ПараметрыЗадач.УниверсальныйПомощник);
	Обработки.ПомощникУчетаЗарплаты.ЗаполнитьСтатусыЗадачУплатыВБюджет(ПараметрыЗадач.УчетЗарплаты);
	Обработки.ПомощникРасчетаНДС.ЗаполнитьСтатусыЗадач(ПараметрыЗадач.НДС);
	Если Не РегистрыСведений.СостоянияИнтеграцииАУСН.СервисЛКАУСНПодключен()
		И Не РегистрыСведений.СостоянияИнтеграцииАУСН.СервисПодключен() Тогда
		ВыполнениеЗадачБухгалтераБП.ЗаполнитьСтатусыЗадачАУСН(ПараметрыЗадач.АУСН);
	КонецЕсли;
	ЗаполнитьСтатусыЗадачДобровольныеВзносыИП(ПараметрыЗадач.ДобровольныеВзносыИП);
	
	// Для определения статусов в алгоритмах универсального помощника используется имя ПравилоУплаты.
	// В остальных помощниках используется имя Правило.
	// При объединении всех задач необходимо переименовать колонку таблицы параметров помощника.
	ПараметрыЗадач.УниверсальныйПомощник.Колонки.ПравилоУплаты.Имя = "Правило";
	
	ЗадачиСоСтатусами = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадач();
	ЗадачиСоСтатусами.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	
	Для Каждого ГруппаПараметровЗадач Из ПараметрыЗадач Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ГруппаПараметровЗадач.Значение, ЗадачиСоСтатусами);
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.СтатусыЗадачПоПодготовкеРегПлатежей
//
Процедура ПриОпределенииСтатусовЗадачПоПодготовкеРегПлатежей(ЗадачиСоСтатусами, ТаблицаЗадач) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задачи", ТаблицаЗадач);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	Задачи.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Задачи.РучнойСтатус КАК РучнойСтатус
	|ПОМЕСТИТЬ Задачи
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка) КАК РегистрацияВНалоговомОргане,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило,
	|	ВЫБОР
	|		КОГДА Задачи.СтатусУстановленВручную
	|			ТОГДА Задачи.РучнойСтатус
	|		ИНАЧЕ ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, """")
	|	КОНЕЦ КАК Статус
	|ИЗ
	|	Задачи КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегулярныеПлатежи КАК РегулярныеПлатежи
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|			ПО РегулярныеПлатежи.ПлатежноеПоручение = СостоянияБанковскихДокументов.СсылкаНаОбъект
	|				И РегулярныеПлатежи.Организация = СостоянияБанковскихДокументов.Организация
	|		ПО Задачи.Организация = РегулярныеПлатежи.Организация
	|			И Задачи.Правило = РегулярныеПлатежи.Правило
	|			И Задачи.ПериодСобытия = РегулярныеПлатежи.ПериодСобытия";
	
	ЗадачиСоСтатусами = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.СтатусыЗадачПоВыплатеЗарплаты
//
Процедура ПриОпределенииСтатусовЗадачПоВыплатеЗарплаты(ЗадачиСоСтатусами, ТаблицаЗадач) Экспорт
	
	Обработки.ПомощникУчетаЗарплаты.ЗаполнитьСтатусыЗадачВыплатыЗарплаты(ЗадачиСоСтатусами);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗаполнитьСсылкиНаРеглОтчеты
//
Процедура ЗаполнитьСсылкиНаРеглОтчеты(ПараметрыРеглОтчетов) Экспорт
	
	ВидыКодыНалогов = РасчетыСБюджетом.ТаблицаСоответствияЗадачБухгалтераДляУведомлений();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПараметрыРеглОтчетов", ПараметрыРеглОтчетов);
	Запрос.УстановитьПараметр("ВидыКодыНалогов", ВидыКодыНалогов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыРеглОтчетов.Индекс КАК Индекс,
	|	ПараметрыРеглОтчетов.ИсточникОтчета КАК ИсточникОтчета,
	|	ПараметрыРеглОтчетов.Организация КАК Организация,
	|	ПараметрыРеглОтчетов.ДатаОкончания КАК ДатаОкончания,
	|	ПараметрыРеглОтчетов.ПериодСобытия КАК ПериодСобытия,
	|	ВЫРАЗИТЬ(ПараметрыРеглОтчетов.РегистрацияВНалоговомОргане КАК Справочник.РегистрацииВНалоговомОргане) КАК РегистрацияВНалоговомОргане,
	|	ПараметрыРеглОтчетов.Правило КАК Правило
	|ПОМЕСТИТЬ ПараметрыРеглОтчетов
	|ИЗ
	|	&ПараметрыРеглОтчетов КАК ПараметрыРеглОтчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ИсточникОтчета,
	|	ПериодСобытия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыКодыНалогов.ВидНалога КАК ВидНалога,
	|	ВидыКодыНалогов.КодЗадачи КАК КодЗадачи
	|ПОМЕСТИТЬ ВТ_ВидыКодыНалогов
	|ИЗ
	|	&ВидыКодыНалогов КАК ВидыКодыНалогов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидНалога
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПараметрыРеглОтчетов.Индекс КАК Индекс,
	|	УведомлениеОСпецрежимахНО.Ссылка КАК Ссылка,
	|	0 КАК Вид,
	|	УведомлениеОСпецрежимахНО.Дата КАК ДатаОтчета,
	|	УведомлениеОСпецрежимахНО.ДатаПодписи КАК Дата,
	|	ПараметрыРеглОтчетов.ИсточникОтчета КАК ИсточникОтчета,
	|	ПараметрыРеглОтчетов.Организация КАК Организация,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
	|	ПараметрыРеглОтчетов.ПериодСобытия КАК ПериодСобытия,
	|	ПараметрыРеглОтчетов.ДатаОкончания КАК ДатаОкончания,
	|	ПараметрыРеглОтчетов.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	УведомлениеОСпецрежимахНО.ИмяФормы КАК ВыбраннаяФорма,
	|	ПараметрыРеглОтчетов.Правило КАК Правило,
	|	ЕСТЬNULL(ЖурналУведомленийСтатусы.СтатусОтправки, """""""") КАК Статус,
	|	ЕСТЬNULL(ЖурналУведомленийСтатусы.СостояниеСдачиОтчетности, ЗНАЧЕНИЕ(Перечисление.СостояниеСдачиОтчетности.ПустаяСсылка)) КАК СостояниеСдачиОтчетности,
	|	ЕСТЬNULL(ЖурналУведомленийСтатусы.Наименование, """") КАК НаименованиеОтчета,
	|	"""" КАК ФинансовыйПериод,
	|	0 КАК ВариантОтчета,
	|	ЕСТЬNULL(ЖурналУведомленийСтатусы.ПредставлениеКонтролирующегоОргана, """") КАК ПредставлениеКонтролирующегоОргана,
	|	ЕСТЬNULL(ЖурналУведомленийСтатусы.Комментарий, """") КАК Комментарий
	|ПОМЕСТИТЬ ВТ_Ссылки
	|ИЗ
	|	Документ.УведомлениеОСпецрежимахНалогообложения КАК УведомлениеОСпецрежимахНО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПараметрыРеглОтчетов КАК ПараметрыРеглОтчетов
	|		ПО УведомлениеОСпецрежимахНО.Организация = ПараметрыРеглОтчетов.Организация
	|			И УведомлениеОСпецрежимахНО.ИмяОтчета = ПараметрыРеглОтчетов.ИсточникОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналУведомленийСтатусы
	|		ПО УведомлениеОСпецрежимахНО.Организация = ЖурналУведомленийСтатусы.Организация
	|			И УведомлениеОСпецрежимахНО.Ссылка = ЖурналУведомленийСтатусы.Ссылка
	|			И (ЖурналУведомленийСтатусы.СтраницаЖурнала = ЗНАЧЕНИЕ(Перечисление.СтраницыЖурналаОтчетность.Уведомления))
	|ГДЕ
	|	НЕ УведомлениеОСпецрежимахНО.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыНалоговИПлатежейВБюджет.Ссылка КАК Налог,
	|	ПараметрыРеглОтчетов.Организация КАК Организация,
	|	ПараметрыРеглОтчетов.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПараметрыРеглОтчетов.Правило КАК Правило,
	|	ПараметрыРеглОтчетов.ПериодСобытия КАК ПериодСобытия
	|ПОМЕСТИТЬ ВТ_ЗадачиБухгалтераВидыНалогов
	|ИЗ
	|	ПараметрыРеглОтчетов КАК ПараметрыРеглОтчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК ПравилаПредставленияОтчетовУплатыНалогов
	|		ПО (ПравилаПредставленияОтчетовУплатыНалогов.Ссылка = ПараметрыРеглОтчетов.Правило)
	|			И (ПравилаПредставленияОтчетовУплатыНалогов.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЗадачиБухгалтера КАК СправочникЗадачиБухгалтера
	|		ПО (СправочникЗадачиБухгалтера.Ссылка = ПравилаПредставленияОтчетовУплатыНалогов.Владелец)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВидыКодыНалогов КАК ВТ_ВидыКодыНалогов
	|		ПО (ВТ_ВидыКодыНалогов.КодЗадачи = СправочникЗадачиБухгалтера.Код)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыНалоговИПлатежейВБюджет
	|		ПО (ВидыНалоговИПлатежейВБюджет.ВидНалога = ВТ_ВидыКодыНалогов.ВидНалога)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Налог,
	|	ПериодСобытия,
	|	РегистрацияВНалоговомОргане,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Уведомления.Ссылка КАК Уведомление,
	|	Уведомления.Уведомление КАК УведомлениеБРО,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.Организация КАК Организация,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.Правило КАК Правило,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.ПериодСобытия КАК ПериодСобытия
	|ПОМЕСТИТЬ ЗадачиБухгалтераУведомления
	|ИЗ
	|	ВТ_ЗадачиБухгалтераВидыНалогов КАК ВТ_ЗадачиБухгалтераВидыНалогов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомленияНалоги
	|		ПО (УведомленияНалоги.Налог = ВТ_ЗадачиБухгалтераВидыНалогов.Налог)
	|			И (УведомленияНалоги.РегистрацияВНалоговомОргане = ВТ_ЗадачиБухгалтераВидыНалогов.РегистрацияВНалоговомОргане)
	|			И (УведомленияНалоги.КонецПериода = ВТ_ЗадачиБухгалтераВидыНалогов.ПериодСобытия)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбИсчисленныхСуммахНалогов КАК Уведомления
	|		ПО (Уведомления.Ссылка = УведомленияНалоги.Ссылка)
	|			И (Уведомления.Организация = ВТ_ЗадачиБухгалтераВидыНалогов.Организация)
	|			И (НЕ Уведомления.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Уведомления.Ссылка,
	|	Уведомления.Уведомление,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.Организация,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.РегистрацияВНалоговомОргане,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.Правило,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.ПериодСобытия
	|ИЗ
	|	ВТ_ЗадачиБухгалтераВидыНалогов КАК ВТ_ЗадачиБухгалтераВидыНалогов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомленияНалоги
	|		ПО (УведомленияНалоги.Налог = ВТ_ЗадачиБухгалтераВидыНалогов.Налог)
	|			И (ВТ_ЗадачиБухгалтераВидыНалогов.РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|			И (УведомленияНалоги.КонецПериода = ВТ_ЗадачиБухгалтераВидыНалогов.ПериодСобытия)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОбИсчисленныхСуммахНалогов КАК Уведомления
	|		ПО (Уведомления.Ссылка = УведомленияНалоги.Ссылка)
	|			И (Уведомления.Организация = ВТ_ЗадачиБухгалтераВидыНалогов.Организация)
	|			И (НЕ Уведомления.ПометкаУдаления)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Уведомления.Уведомление,
	|	Уведомления.Уведомление.Уведомление,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.Организация,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.РегистрацияВНалоговомОргане,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.Правило,
	|	ВТ_ЗадачиБухгалтераВидыНалогов.ПериодСобытия
	|ИЗ
	|	ВТ_ЗадачиБухгалтераВидыНалогов КАК ВТ_ЗадачиБухгалтераВидыНалогов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтераУведомления КАК Уведомления
	|		ПО (Уведомления.Организация = ВТ_ЗадачиБухгалтераВидыНалогов.Организация)
	|			И (Уведомления.РегистрацияВНалоговомОргане = ВТ_ЗадачиБухгалтераВидыНалогов.РегистрацияВНалоговомОргане)
	|			И (Уведомления.Правило = ВТ_ЗадачиБухгалтераВидыНалогов.Правило)
	|			И (Уведомления.ПериодСобытия = ВТ_ЗадачиБухгалтераВидыНалогов.ПериодСобытия)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераУведомления.Правило КАК Правило,
	|	ЗадачиБухгалтераУведомления.Уведомление КАК Уведомление,
	|	ЗадачиБухгалтераУведомления.ПериодСобытия КАК ПериодСобытия,
	|	ВТ_Ссылки.Индекс КАК Индекс,
	|	ВТ_Ссылки.Ссылка КАК Ссылка,
	|	ВТ_Ссылки.Вид КАК Вид,
	|	ВТ_Ссылки.ДатаОтчета КАК ДатаОтчета,
	|	ВТ_Ссылки.Дата КАК Дата,
	|	ВТ_Ссылки.ИсточникОтчета КАК ИсточникОтчета,
	|	ВТ_Ссылки.Организация КАК Организация,
	|	ВТ_Ссылки.ДатаНачала КАК ДатаНачала,
	|	ВТ_Ссылки.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_Ссылки.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТ_Ссылки.ВыбраннаяФорма КАК ВыбраннаяФорма,
	|	ВТ_Ссылки.Статус КАК Статус,
	|	ВТ_Ссылки.СостояниеСдачиОтчетности КАК СостояниеСдачиОтчетности,
	|	ВТ_Ссылки.НаименованиеОтчета КАК НаименованиеОтчета,
	|	ВТ_Ссылки.ФинансовыйПериод КАК ФинансовыйПериод,
	|	ВТ_Ссылки.ВариантОтчета КАК ВариантОтчета,
	|	ВТ_Ссылки.ПредставлениеКонтролирующегоОргана КАК ПредставлениеКонтролирующегоОргана,
	|	ВТ_Ссылки.Комментарий КАК Комментарий
	|ИЗ
	|	ЗадачиБухгалтераУведомления КАК ЗадачиБухгалтераУведомления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Ссылки КАК ВТ_Ссылки
	|		ПО ЗадачиБухгалтераУведомления.Правило = ВТ_Ссылки.Правило
	|			И ЗадачиБухгалтераУведомления.Организация = ВТ_Ссылки.Организация
	|			И ЗадачиБухгалтераУведомления.УведомлениеБРО = ВТ_Ссылки.Ссылка
	|			И ЗадачиБухгалтераУведомления.ПериодСобытия = ВТ_Ссылки.ПериодСобытия
	|			И ЗадачиБухгалтераУведомления.РегистрацияВНалоговомОргане = ВТ_Ссылки.РегистрацияВНалоговомОргане
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадачиБухгалтераУведомления.Правило,
	|	ЗадачиБухгалтераУведомления.Уведомление,
	|	ЗадачиБухгалтераУведомления.ПериодСобытия,
	|	ВТ_Ссылки.Индекс,
	|	ВТ_Ссылки.Ссылка,
	|	ВТ_Ссылки.Вид,
	|	ВТ_Ссылки.ДатаОтчета,
	|	ВТ_Ссылки.Дата,
	|	ВТ_Ссылки.ИсточникОтчета,
	|	ВТ_Ссылки.Организация,
	|	ВТ_Ссылки.ДатаНачала,
	|	ВТ_Ссылки.ДатаОкончания,
	|	ВТ_Ссылки.РегистрацияВНалоговомОргане,
	|	ВТ_Ссылки.ВыбраннаяФорма,
	|	ВТ_Ссылки.Статус,
	|	ВТ_Ссылки.СостояниеСдачиОтчетности,
	|	ВТ_Ссылки.НаименованиеОтчета,
	|	ВТ_Ссылки.ФинансовыйПериод,
	|	ВТ_Ссылки.ВариантОтчета,
	|	ВТ_Ссылки.ПредставлениеКонтролирующегоОргана,
	|	ВТ_Ссылки.Комментарий
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераУведомления КАК ЗадачиБухгалтераУведомления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОЗачетеВСчетПредстоящейОбязанности КАК ЗаявлениеОЗачетеВСчетПредстоящейОбязанности
	|		ПО ЗадачиБухгалтераУведомления.Организация = ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.Организация
	|			И ЗадачиБухгалтераУведомления.Уведомление = ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Ссылки КАК ВТ_Ссылки
	|		ПО ЗадачиБухгалтераУведомления.Правило = ВТ_Ссылки.Правило
	|			И ЗадачиБухгалтераУведомления.Организация = ВТ_Ссылки.Организация
	|			И (ЗаявлениеОЗачетеВСчетПредстоящейОбязанности.Уведомление = ВТ_Ссылки.Ссылка)
	|			И ЗадачиБухгалтераУведомления.ПериодСобытия = ВТ_Ссылки.ПериодСобытия
	|ГДЕ
	|	ЗадачиБухгалтераУведомления.Организация В
	|			(ВЫБРАТЬ
	|				ВТ_Ссылки.Организация
	|			ИЗ
	|				ВТ_Ссылки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Индекс,
	|	ДатаОтчета УБЫВ,
	|	Вид УБЫВ";
	
	ПараметрыРеглОтчетов.Колонки.Добавить("Документы", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	МетаданныеЖурналОтчетовСтатусы = Метаданные.РегистрыСведений.ЖурналОтчетовСтатусы;
	
	ТаблицаДокументы = Новый ТаблицаЗначений;
	ТаблицаДокументы.Колонки.Добавить("Ссылка", МетаданныеЖурналОтчетовСтатусы.Измерения.Ссылка.Тип);
	ТаблицаДокументы.Колонки.Добавить("Дата",   ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаДокументы.Колонки.Добавить("Статус", Новый ОписаниеТипов("Строка"));
	ТаблицаДокументы.Колонки.Добавить("СостояниеСдачиОтчетности",
		Новый ОписаниеТипов("ПеречислениеСсылка.СостояниеСдачиОтчетности"));
	ТаблицаДокументы.Колонки.Добавить("ДатаНачала",         ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаДокументы.Колонки.Добавить("ДатаОкончания",      ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаДокументы.Колонки.Добавить("НаименованиеОтчета", МетаданныеЖурналОтчетовСтатусы.Реквизиты.НаименованиеОтчета.Тип);
	ТаблицаДокументы.Колонки.Добавить("ФинансовыйПериод",   МетаданныеЖурналОтчетовСтатусы.Реквизиты.ФинансовыйПериод.Тип);
	ТаблицаДокументы.Колонки.Добавить("ВариантОтчета",      МетаданныеЖурналОтчетовСтатусы.Реквизиты.ВариантОтчета.Тип);
	ТаблицаДокументы.Колонки.Добавить("РегистрацияВНалоговомОргане",        Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	ТаблицаДокументы.Колонки.Добавить("ПредставлениеКонтролирующегоОргана", МетаданныеЖурналОтчетовСтатусы.Реквизиты.ПредставлениеКонтролирующегоОргана.Тип);
	ТаблицаДокументы.Колонки.Добавить("Комментарий",                        МетаданныеЖурналОтчетовСтатусы.Реквизиты.Комментарий.Тип);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Поиск = Новый Структура("Индекс");
	
	Для каждого ПараметрыРеглОтчета Из ПараметрыРеглОтчетов Цикл
		
		ПолноеИмяПравила = СтрШаблон("%1.%2", ПараметрыРеглОтчета.ИдентификаторЗадачи, ПараметрыРеглОтчета.ИдентификаторПравила);
		ЭтоПравилоТринадцатогоНДФЛ = ВыполнениеЗадачБухгалтера.ЭтоПравилоТринадцатогоНДФЛ(ПолноеИмяПравила);
		
		ПараметрыРеглОтчета.Документы = ТаблицаДокументы.СкопироватьКолонки();
		Поиск.Индекс = ПараметрыРеглОтчета.Индекс;
		
		Выборка.Сбросить();
		Пока Выборка.НайтиСледующий(Поиск) Цикл
			УведомлениеСодержитНДФЛЗаТринадцатыйПериод = ВыполнениеЗадачБухгалтера.УведомлениеСодержитНДФЛЗаТринадцатыйПериод(Выборка.Уведомление);
			Если (ЭтоПравилоТринадцатогоНДФЛ И Не УведомлениеСодержитНДФЛЗаТринадцатыйПериод)
				Или (Не ЭтоПравилоТринадцатогоНДФЛ И УведомлениеСодержитНДФЛЗаТринадцатыйПериод) Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ПараметрыРеглОтчета.Документы.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.РелевантныйПериодСобытияДляОтчета
//
Процедура ПриОпределенииПризнакаРазрешенУчетРегулярнойДеятельности(РазрешенУчетРегулярнойДеятельности) Экспорт
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПодготовитьТаблицыПоЕжедневнымЗадачам
//
Процедура ВТЗадачиПоКадровымМероприятиям(Запрос, ИмяТаблицы) Экспорт
	
	УчетЗарплаты.ВТЗадачиПоКадровымМероприятиям(Запрос, ИмяТаблицы);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ТекущаяЗадачаНалоговогоПлатежа
//
Процедура ПриПолученииТекущейЗадачиНалоговогоПлатежа(Задача, РеквизитыДокумента) Экспорт
	
	// Задача для документа уже может присутствовать в служебном регистре ЗадачиБухгалтераНалоговыеПлатежи.
	// Записи добавляются в регистр, когда платежные документы создаются из помощников
	// или регистрируются автоматически в качестве исполнителей налоговых платежей.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДокумента.Ссылка);
	Запрос.УстановитьПараметр("Организация", РеквизитыДокумента.Организация);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация КАК Организация,
	|	ЗадачиБухгалтераНалоговыеПлатежи.Правило КАК Правило,
	|	ЗадачиБухгалтераНалоговыеПлатежи.ПериодСобытия КАК ПериодСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога) КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтераНалоговыеПлатежи КАК ЗадачиБухгалтераНалоговыеПлатежи
	|ГДЕ
	|	ЗадачиБухгалтераНалоговыеПлатежи.Организация = &Организация
	|	И ЗадачиБухгалтераНалоговыеПлатежи.ПлатежноеПоручение = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Задача = ВыполнениеЗадачБухгалтераБП.НоваяЗадачаСвязаннаяСПлатежом();
		ЗаполнитьЗначенияСвойств(Задача, Выборка);
	Иначе
		Задача = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.НоваяЗадачаНалоговогоПлатежа
//
Процедура ПриПолученииНовойЗадачиНалоговогоПлатежа(Задача, РеквизитыДокумента, ПредыдущаяЗадача, ПараметрыРасчетаСтатусов) Экспорт
	
	// Если платеж еще не зарегистрирован в регистре ЗадачиБухгалтераНалоговыеПлатежи,
	// или для данного налога поддерживается обновление задач при любом изменении платежа -
	// определим свойства новой задачи, которую исполнит документ.
	
	ТребуетсяОбновитьЗадачу = (ПредыдущаяЗадача = Неопределено)
		Или (ВыполнениеЗадачБухгалтераБП.ПлатежиПоНалогуВсегдаОбновляютСвязанныеЗадачи(РеквизитыДокумента.ВидНалога)
			И Не ВыполнениеЗадачБухгалтераБП.ЭтоПлатежноеПоручениеНаОснованииСписанияСоСчета(РеквизитыДокумента));
	
	// Если задача выполняется из помощника, то она уже зарегистрирована в регистре ЗадачиБухгалтераНалоговыеПлатежи.
	// В таком случае не нужно искать новую задачу и регистрировать новую связь с документом.
	// Кроме того, если не изменились ключевые реквизиты, влияющие на связь между документом и задачей,
	// в таком случае тоже не нужно искать связанную с документом задачу.
	
	ПропуститьОбновлениеЗадачи = ПредыдущаяЗадача <> Неопределено
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыРасчетаСтатусов, "ПропуститьОбновлениеСвязиДокументЗадача", Ложь);
	
	Если Не ТребуетсяОбновитьЗадачу Или ПропуститьОбновлениеЗадачи Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущееПравило       = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПредыдущаяЗадача, "Правило", Неопределено);
	ПредыдущийПериодСобытия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПредыдущаяЗадача, "ПериодСобытия", Неопределено);
	
	НовыйПериодСобытия = ВыполнениеЗадачБухгалтераБП.ПериодСобытияПлатежа(РеквизитыДокумента, ПредыдущееПравило, ПредыдущийПериодСобытия);
	
	ПорядокУплаты = РегистрыСведений.ЗадачиБухгалтера.ПорядокУплатыНалогаЗаПериод(
		РеквизитыДокумента.Организация, РеквизитыДокумента.ВидНалога, НовыйПериодСобытия);
	
	Если ПорядокУплаты = Неопределено Тогда
		// Для платежа с данными реквизитами исполняемая задача отсутствует.
		Возврат;
	КонецЕсли;
	
	Задача = ВыполнениеЗадачБухгалтераБП.НоваяЗадачаСвязаннаяСПлатежом();
	
	Задача.Организация   = РеквизитыДокумента.Организация;
	Задача.Правило       = ПорядокУплаты.Правило;
	Задача.ПериодСобытия = ПорядокУплаты.ПериодСобытия;
	Задача.Действие      = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.УведомлениеСодержитПромежуточныйНДФЛ
//
Процедура ПриОпределенииПризнакаУведомлениеСодержитПромежуточныйНДФЛ(Содержит, УведомлениеОбИсчисленныхСуммахНалога) Экспорт
	
	ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УведомлениеОбИсчисленныхСуммахНалога, "Дата");
	Если ДатаДокумента >= УчетЗарплаты.ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ() Тогда
		Содержит = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", УведомлениеОбИсчисленныхСуммахНалога);
	Запрос.УстановитьПараметр("ВидНалога", Перечисления.ВидыНалогов.НДФЛ);
	
	// Федеральный закон от 31.07.2023 № 389-ФЗ:
	// 12.2. В течение 2023 года налоговые агенты могут подавать уведомления об исчисленных суммах по налогу на доходы физических лиц, 
	// подлежащему перечислению в сроки, установленные пунктом 6 статьи 226, пунктом 9 статьи 226.1, пунктом 7 статьи 226.2 Налогового кодекса Российской Федерации, 
	// до истечения 12-го числа текущего месяца, указав в таком уведомлении сумму налога, 
	// удержанную в период с 23-го числа предыдущего месяца до 9-го числа текущего месяца. 
	Запрос.УстановитьПараметр("ДеньПодачиПромежуточногоУведомления", 12);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог КАК Налог
		|ИЗ
		|	Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомлениеОбИсчисленныхСуммахНалоговНалоги
		|ГДЕ
		|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка = &Ссылка
		|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог.ВидНалога = &ВидНалога
		|	И ДЕНЬ(УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка.Дата) <= &ДеньПодачиПромежуточногоУведомления";
	
	Содержит = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.УведомлениеСодержитНДФЛЗаТринадцатыйПериод
//
Процедура ПриОпределенииПризнакаУведомлениеСодержитНДФЛЗаТринадцатыйПериод(Содержит, УведомлениеОбИсчисленныхСуммахНалога) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", УведомлениеОбИсчисленныхСуммахНалога);
	Запрос.УстановитьПараметр("ВидНалога", Перечисления.ВидыНалогов.НДФЛ);
	Запрос.УстановитьПараметр("НачалоПериода", 23);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог КАК Налог
		|ИЗ
		|	Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомлениеОбИсчисленныхСуммахНалоговНалоги
		|ГДЕ
		|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка = &Ссылка
		|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог.ВидНалога = &ВидНалога
		|	И ДЕНЬ(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода) = &НачалоПериода
		|	И КОНЕЦПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода, МЕСЯЦ) = КОНЕЦПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.КонецПериода, ГОД)";
	
	Содержит = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.УведомлениеСодержитНДФЛЗаКонецМесяца
//
Процедура ПриОпределенииПризнакаУведомлениеСодержитНДФЛЗаКонецМесяца(Содержит, УведомлениеОбИсчисленныхСуммахНалога, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",        УведомлениеОбИсчисленныхСуммахНалога);
	Запрос.УстановитьПараметр("ВидНалога",     Перечисления.ВидыНалогов.НДФЛ);
	Запрос.УстановитьПараметр("НачалоПериода", 23);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог КАК Налог
		|ИЗ
		|	Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомлениеОбИсчисленныхСуммахНалоговНалоги
		|ГДЕ
		|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка = &Ссылка
		|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог.ВидНалога = &ВидНалога
		|	И ДЕНЬ(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода) = &НачалоПериода
		|	И КОНЕЦПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода, МЕСЯЦ) = КОНЕЦПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.КонецПериода, ДЕНЬ)
		|	И &УсловиеПериод";
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПериод", "Истина");
	Иначе
		Запрос.УстановитьПараметр("Период", Период);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПериод", "КОНЕЦПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода, МЕСЯЦ) = КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)");
	КонецЕсли;
	
	Содержит = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.УведомлениеСодержитНДФЛЗаНачалоМесяца
//
Процедура ПриОпределенииПризнакаУведомлениеСодержитНДФЛЗаНачалоМесяца(Содержит, УведомлениеОбИсчисленныхСуммахНалога, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",        УведомлениеОбИсчисленныхСуммахНалога);
	Запрос.УстановитьПараметр("ВидНалога",     Перечисления.ВидыНалогов.НДФЛ);
	Запрос.УстановитьПараметр("НачалоПериода", 1);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог КАК Налог
		|ИЗ
		|	Документ.УведомлениеОбИсчисленныхСуммахНалогов.Налоги КАК УведомлениеОбИсчисленныхСуммахНалоговНалоги
		|ГДЕ
		|	УведомлениеОбИсчисленныхСуммахНалоговНалоги.Ссылка = &Ссылка
		|	И УведомлениеОбИсчисленныхСуммахНалоговНалоги.Налог.ВидНалога = &ВидНалога
		|	И ДЕНЬ(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода) = &НачалоПериода
		|	И &УсловиеПериод";
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПериод", "Истина");
	Иначе
		Запрос.УстановитьПараметр("Период", Период);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПериод", "КОНЕЦПЕРИОДА(УведомлениеОбИсчисленныхСуммахНалоговНалоги.НачалоПериода, МЕСЯЦ) = КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)");
	КонецЕсли;
	
	Содержит = Не Запрос.Выполнить().Пустой();
	
КонецПроцедуры

// Подробнее
// См. ОбщаяФорма.СписокЗадач.ЗавершитьЗадачу
//
Процедура ЗаписатьСтатусПодчиненныхЗадач(ИсходнаяЗадача, Статус, СтатусУстановленВручную = Ложь, ПользовательУстановившийСтатус = Неопределено, Комментарий = "") Экспорт
	
	Если ИсходнаяЗадача.ИдентификаторЗадачи = "ЕдиныйНалоговыйСчет" Тогда
		ВыполнениеЗадачБухгалтераБП.ЗавершитьПодчиненныеЗадачиЕдиногоНалоговогоСчета(ИсходнаяЗадача);
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.СтатусыЗадачПоДокументамСЭДО
//
Процедура ПриОпределенииСтатусовЗадачПоДокументамСЭДО(ТаблицаЗадач, ЗадачиСоСтатусами, Запрос) Экспорт
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодСобытия КАК ПериодСобытия,
	|	Задачи.Правило КАК Правило
	|ПОМЕСТИТЬ ЗадачиДокументыСЭДО
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиДокументыСЭДО.Организация КАК Организация,
	|	ЗадачиДокументыСЭДО.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиДокументыСЭДО.Правило КАК Правило,
	|	ВЫБОР
	|		КОГДА ВходящийЗапросФССДляРасчетаПособия.Обработан
	|			ТОГДА &СтатусОтветОтправлен
	|		КОГДА НЕ ВходящийЗапросФССДляРасчетаПособия.Загружен
	|			ТОГДА &СтатусРасшифровать
	|		КОГДА ВходящийЗапросФССДляРасчетаПособия.Загружен
	|			ТОГДА &СтатусОтправьтеОтвет
	|		КОГДА ИзвещениеФСС.Обработано
	|			ТОГДА &СтатусОтветОтправлен
	|		КОГДА НЕ ИзвещениеФСС.Загружено
	|			ТОГДА &СтатусРасшифровать
	|		КОГДА ИзвещениеФСС.Загружено
	|				И (НЕ ИзвещениеФСС.ПодтверждениеПолученоФСС
	|					ИЛИ ИзвещениеФСС.ДатаОтправкиПодтверждения = ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА &СтатусПодтвердитеПрием
	|		КОГДА ИзвещениеФСС.Загружено
	|			ТОГДА &СтатусОтправьтеОтвет
	|		КОГДА НЕ ОтказВВозмещенииВыплат.Загружен
	|			ТОГДА &СтатусРасшифровать
	|		КОГДА ОтказВВозмещенииВыплат.Загружен
	|				И (НЕ ОтказВВозмещенииВыплат.ПодтверждениеПолученоСФР
	|					ИЛИ ОтказВВозмещенииВыплат.ДатаОтправкиПодтверждения = ДАТАВРЕМЯ(1, 1, 1))
	|			ТОГДА &СтатусПодтвердитеПрием
	|		КОГДА ОтказВВозмещенииВыплат.Обработан
	|				ИЛИ ОтказВВозмещенииВыплат.Загружен
	|			ТОГДА &СтатусПриемПодтвержден
	|		КОГДА ОтказВВозмещенииВыплат.Загружен
	|			ТОГДА &СтатусОтправьтеОтвет
	|		КОГДА НЕ УведомлениеОСтатусеВыплатыПособия.Загружен
	|			ТОГДА &СтатусРасшифровать
	|		КОГДА НЕ УведомлениеОСтатусеВыплатыПособия.Обработан
	|			И УведомлениеОСтатусеВыплатыПособия.ТребуетсяОбработать
	|			И УведомлениеОСтатусеВыплатыПособия.ТребуетсяПодтверждение
	|			И УведомлениеОСтатусеВыплатыПособия.ДатаОтправкиПодтверждения = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &СтатусПодтвердитеПрием
	|		КОГДА УведомлениеОСтатусеВыплатыПособия.Загружен
	|			ТОГДА &СтатусПриемПодтвержден
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Статус
	|ИЗ
	|	ЗадачиДокументыСЭДО КАК ЗадачиДокументыСЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВходящийЗапросФССДляРасчетаПособия КАК ВходящийЗапросФССДляРасчетаПособия
	|		ПО ЗадачиДокументыСЭДО.Правило = ВходящийЗапросФССДляРасчетаПособия.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзвещениеФСС КАК ИзвещениеФСС
	|		ПО ЗадачиДокументыСЭДО.Правило = ИзвещениеФСС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтказВВозмещенииВыплатРодителямДетейИнвалидов КАК ОтказВВозмещенииВыплат
	|		ПО ЗадачиДокументыСЭДО.Правило = ОтказВВозмещенииВыплат.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОСтатусеВыплатыПособия КАК УведомлениеОСтатусеВыплатыПособия
	|		ПО ЗадачиДокументыСЭДО.Правило = УведомлениеОСтатусеВыплатыПособия.Ссылка";
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПроверитьАктуальностьСтатуса
//
Процедура ОпределитьЭтоЗадачаСПустымКодом(ЭтоЗадачаСПустымКодом, ПараметрыЗадачи) Экспорт
	
	ЭтоЗадачаСПустымКодом = ТипЗнч(ПараметрыЗадачи.Правило) = Тип("СправочникСсылка.Патенты")
		Или ТипЗнч(ПараметрыЗадачи.Правило) = Тип("СправочникСсылка.ПравилаРегулярныхПлатежей");
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиДляОтчета
//
Процедура ОпределитьЭтоДокументСЗВ_ТД(ЭтоДокументСЗВ_ТД, Документ) Экспорт
	ЭтоДокументСЗВ_ТД = (ТипЗнч(Документ) = Тип("ДокументСсылка.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД"));
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ВключитьАвтоматическоеОбновлениеЗадачБухгалтера
//
Процедура ПриВключенииАвтоматическогоОбновленияЗадачБухгалтера() Экспорт
	
	// С задачами бухгалтера также связан показ проверок контролирующими органами,
	// который мог быть отключен в форме панели администрирования "Производительность".
	// Включим его тоже.
	РегламентныеЗаданияСервер.УстановитьИспользованиеРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбновлениеПроверокКонтролирующимиОрганами, Истина);
		
КонецПроцедуры

#КонецОбласти

#Область ПлатежныйКалендарь

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиДляКалендаря
//
Процедура ЗаполнитьДоступныеОрганизацииДляОтбора(ДоступныеОрганизации, Организация, ПравоНаИзменение = Ложь) Экспорт
	
	ДоступныеОрганизации = Справочники.Организации.ДоступныеОрганизацииДляОтбора(Организация);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗадачиДляКалендаря
//
Процедура ПолучитьЗадачиДляКалендаря(ЗадачиБухгалтера, ПараметрыПолученияЗадач) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокОрганизаций",           ПараметрыПолученияЗадач.СписокОрганизаций);
	Запрос.УстановитьПараметр("РабочаяДата",                 ПараметрыПолученияЗадач.РабочаяДата);
	Запрос.УстановитьПараметр("ГоризонтПланирования",        ПараметрыПолученияЗадач.ГоризонтПланирования);
	Запрос.УстановитьПараметр("СтатусыВыполненныхЗадач",     ПараметрыПолученияЗадач.СтатусыВыполненныхЗадач);
	Запрос.УстановитьПараметр("ИдентификаторыЗадачЗарплаты", ПараметрыПолученияЗадач.ИдентификаторыЗадачЗарплаты);
	Запрос.УстановитьПараметр("ИдентификаторыЗадачЗарплатыАванс", ПараметрыПолученияЗадач.ИдентификаторыЗадачЗарплатыАванс);
	Запрос.УстановитьПараметр("УчетЗарплатыСредствамиБухгалтерии", ПараметрыПолученияЗадач.УчетЗарплатыСредствамиБухгалтерии);
	Запрос.УстановитьПараметр("ПредыдущийМесяц", ПараметрыПолученияЗадач.ПредыдущийМесяц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Срок КАК Срок,
	|	ЗадачиБухгалтера.Наименование КАК Наименование,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.Периодичность КАК Периодичность,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие КАК Действие,
	|	ЗадачиБухгалтера.НачалоВыполнения КАК НачалоВыполнения
	|ПОМЕСТИТЬ ЗадачиИзСписка
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация В(&СписокОрганизаций)
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И ЗадачиБухгалтера.Срок МЕЖДУ &РабочаяДата И &ГоризонтПланирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация,
	|	ЗадачиБухгалтера.Срок,
	|	ЗадачиБухгалтера.Наименование,
	|	ЗадачиБухгалтера.Правило,
	|	ЗадачиБухгалтера.Периодичность,
	|	ЗадачиБухгалтера.ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|	ЗадачиБухгалтера.Действие,
	|	ЗадачиБухгалтера.НачалоВыполнения
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация В(&СписокОрганизаций)
	|	И НЕ ЗадачиБухгалтера.ВАрхиве
	|	И НЕ ЗадачиБухгалтера.Статус В (&СтатусыВыполненныхЗадач)
	|	И ЗадачиБухгалтера.Срок < &РабочаяДата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Правило,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Правила.Ссылка КАК Ссылка,
	|	Правила.Владелец КАК Задача,
	|	Правила.Владелец.Наименование КАК НаименованиеЗадачиБухгалтера,
	|	Правила.Код КАК ИдентификаторПравила,
	|	Правила.Описание КАК Описание,
	|	Правила.ЕстьИнформацияНаИТС КАК ЕстьИнформацияНаИТС
	|ПОМЕСТИТЬ ПравилаУплатыНалогов
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Правила
	|ГДЕ
	|	НЕ Правила.ПометкаУдаления
	|	И Правила.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|	И НЕ Правила.Код В (&ИдентификаторыЗадачЗарплаты)
	|	И НЕ Правила.ВыполняетсяЕдинымПомощником
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Правила.Ссылка КАК Ссылка,
	|	Правила.Владелец КАК Задача,
	|	Правила.Владелец.Наименование КАК НаименованиеЗадачиБухгалтера,
	|	Правила.Код КАК ИдентификаторПравила,
	|	Правила.Описание КАК Описание,
	|	Правила.ЕстьИнформацияНаИТС КАК ЕстьИнформацияНаИТС,
	|	ЛОЖЬ КАК ЭтоАванс
	|ПОМЕСТИТЬ ПравилаВыплатыЗарплаты
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Правила
	|ГДЕ
	|	НЕ Правила.ПометкаУдаления
	|	И Правила.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата)
	|	И Правила.Код В(&ИдентификаторыЗадачЗарплаты)
	|	И НЕ Правила.ВыполняетсяЕдинымПомощником
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Правила.Ссылка,
	|	Правила.Владелец,
	|	Правила.Владелец.Наименование,
	|	Правила.Код,
	|	Правила.Описание,
	|	Правила.ЕстьИнформацияНаИТС,
	|	ИСТИНА
	|ИЗ
	|	Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Правила
	|ГДЕ
	|	НЕ Правила.ПометкаУдаления
	|	И Правила.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата)
	|	И Правила.Код В(&ИдентификаторыЗадачЗарплатыАванс)
	|	И НЕ Правила.ВыполняетсяЕдинымПомощником
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Патенты.Владелец КАК Организация,
	|	Патенты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Патенты
	|ИЗ
	|	Справочник.Патенты КАК Патенты
	|ГДЕ
	|	НЕ Патенты.ПометкаУдаления
	|	И Патенты.Владелец В(&СписокОрганизаций)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Правила.Организация КАК Организация,
	|	Правила.Ссылка КАК Правило,
	|	Правила.Периодичность КАК Периодичность,
	|	РегулярныеПлатежи.ПериодСобытия КАК ПериодСобытия,
	|	РегулярныеПлатежи.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ПОМЕСТИТЬ РегулярныеПлатежи
	|ИЗ
	|	Справочник.ПравилаРегулярныхПлатежей КАК Правила
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегулярныеПлатежи КАК РегулярныеПлатежи
	|		ПО Правила.Ссылка = РегулярныеПлатежи.Правило
	|			И Правила.Организация = РегулярныеПлатежи.Организация
	|ГДЕ
	|	НЕ Правила.ПометкаУдаления
	|	И Правила.Выполняется
	|	И Правила.Организация В(&СписокОрганизаций)
	|	И РегулярныеПлатежи.ПериодСобытия <= &ГоризонтПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗадачиИзСписка.Организация КАК Организация,
	|	ЗадачиИзСписка.Срок КАК ДатаПлатежа,
	|	ЗадачиИзСписка.Срок < &РабочаяДата КАК Просрочен,
	|	ЗадачиИзСписка.Правило КАК Правило,
	|	ЗадачиИзСписка.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиИзСписка.Периодичность КАК Периодичность,
	|	ВЫБОР
	|		КОГДА ЗадачиИзСписка.ПериодСобытия = РегулярныеПлатежиПодходитПериод.ПериодСобытия
	|			ТОГДА РегулярныеПлатежиПодходитПериод.ПлатежноеПоручение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПлатежныйДокумент,
	|	ВЫБОР
	|		КОГДА ЗадачиИзСписка.ПериодСобытия = РегулярныеПлатежиПодходитПериод.ПериодСобытия
	|			ТОГДА РегулярныеПлатежиПодходитПериод.ПлатежноеПоручение
	|		ИНАЧЕ РегулярныеПлатежи.ПлатежноеПоручение
	|	КОНЕЦ КАК ПлатежноеПоручение
	|ПОМЕСТИТЬ РегулярныеПлатежиСПлатежкой
	|ИЗ
	|	ЗадачиИзСписка КАК ЗадачиИзСписка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегулярныеПлатежи КАК РегулярныеПлатежи
	|		ПО (РегулярныеПлатежи.Правило = ЗадачиИзСписка.Правило)
	|			И (РегулярныеПлатежи.Организация = ЗадачиИзСписка.Организация)
	|			И (РегулярныеПлатежи.ПериодСобытия = ДАТАВРЕМЯ(1, 1, 1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегулярныеПлатежи КАК РегулярныеПлатежиПодходитПериод
	|		ПО (РегулярныеПлатежиПодходитПериод.Правило = ЗадачиИзСписка.Правило)
	|			И (РегулярныеПлатежиПодходитПериод.Организация = ЗадачиИзСписка.Организация)
	|			И (РегулярныеПлатежиПодходитПериод.ПериодСобытия = ЗадачиИзСписка.ПериодСобытия)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегулярныеПлатежи.Организация КАК Организация,
	|	РегулярныеПлатежи.ДатаПлатежа КАК ДатаПлатежа,
	|	РегулярныеПлатежи.Просрочен КАК Просрочен,
	|	РегулярныеПлатежи.Правило КАК Правило,
	|	РегулярныеПлатежи.ПериодСобытия КАК ПериодСобытия,
	|	РегулярныеПлатежи.Периодичность КАК Периодичность,
	|	РегулярныеПлатежи.ПлатежныйДокумент КАК ПлатежныйДокумент,
	|	РегулярныеПлатежи.ПлатежноеПоручение КАК ПлатежноеПоручение,
	|	РегулярныеПлатежи.ПлатежноеПоручение.Контрагент КАК Расшифровка,
	|	РегулярныеПлатежи.ПлатежноеПоручение.Контрагент КАК Контрагент,
	|	РегулярныеПлатежи.ПлатежноеПоручение.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РегулярныеПлатежи.ПлатежноеПоручение.СуммаДокумента КАК Сумма,
	|	""Регулярный платеж"" КАК Описание,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУплатыНалогов.БанковскийПеревод) КАК СпособОплаты,
	|	ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка) КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	РегулярныеПлатежиСПлатежкой КАК РегулярныеПлатежи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога) КАК Действие,
	|	ЗадачиИзСписка.Организация КАК Организация,
	|	ЗадачиИзСписка.Срок КАК Срок,
	|	ЗадачиИзСписка.Срок КАК ДатаПлатежа,
	|	ЗадачиИзСписка.Срок < &РабочаяДата КАК Просрочен,
	|	ЗадачиИзСписка.Наименование КАК Наименование,
	|	ЗадачиИзСписка.Наименование КАК НаименованиеЗадачиИзСписка,
	|	ЗадачиИзСписка.Правило КАК Правило,
	|	ЗадачиИзСписка.Периодичность КАК Периодичность,
	|	ЗадачиИзСписка.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиИзСписка.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Правила.Задача КАК Задача,
	|	Правила.НаименованиеЗадачиБухгалтера КАК Расшифровка,
	|	Правила.НаименованиеЗадачиБухгалтера КАК НаименованиеЗадачиБухгалтера,
	|	Правила.ИдентификаторПравила КАК ИдентификаторПравила,
	|	Правила.Задача.Код КАК ИдентификаторЗадачи,
	|	Правила.Описание КАК ОписаниеПравила,
	|	Правила.ЕстьИнформацияНаИТС КАК ЕстьИнформацияНаИТС,
	|	ЗадачиИзСписка.НачалоВыполнения КАК НачалоВыполнения
	|ИЗ
	|	ЗадачиИзСписка КАК ЗадачиИзСписка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПравилаУплатыНалогов КАК Правила
	|		ПО ЗадачиИзСписка.Правило = Правила.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога),
	|	ЗадачиИзСписка.Организация,
	|	ЗадачиИзСписка.Срок,
	|	ЗадачиИзСписка.Срок,
	|	ЗадачиИзСписка.Срок < &РабочаяДата,
	|	ЗадачиИзСписка.Наименование,
	|	ЗадачиИзСписка.Наименование,
	|	ЗадачиИзСписка.Правило,
	|	ЗадачиИзСписка.Периодичность,
	|	ЗадачиИзСписка.ПериодСобытия,
	|	ЗадачиИзСписка.РегистрацияВНалоговомОргане,
	|	NULL,
	|	""Патент"",
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	ЗадачиИзСписка.НачалоВыполнения
	|ИЗ
	|	ЗадачиИзСписка КАК ЗадачиИзСписка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Патенты КАК Патенты
	|		ПО ЗадачиИзСписка.Организация = Патенты.Организация
	|			И ЗадачиИзСписка.Правило = Патенты.Ссылка
	|			И (ЗадачиИзСписка.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога) КАК Действие,
	|	ВЫБОР
	|		КОГДА Правила.ЭтоАванс
	|			ТОГДА ЕСТЬNULL(ВыплаченныеАвансы.СуммаКВыплатеРасход, 0)
	|		ИНАЧЕ ЕСТЬNULL(НачисленныеСуммы.СуммаОборотКт, 0) - ЕСТЬNULL(ВыплаченныеАвансы.СуммаКВыплатеРасход, 0)
	|	КОНЕЦ КАК Сумма,
	|	ЗадачиИзСписка.Организация КАК Организация,
	|	ЗадачиИзСписка.Срок КАК ДатаПлатежа,
	|	ЗадачиИзСписка.Срок < &РабочаяДата КАК Просрочен,
	|	ЗадачиИзСписка.Наименование КАК НаименованиеЗадачиИзСписка,
	|	ЗадачиИзСписка.Правило КАК Правило,
	|	ЗадачиИзСписка.Периодичность КАК Периодичность,
	|	ЗадачиИзСписка.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиИзСписка.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗадачиИзСписка.НачалоВыполнения КАК НачалоВыполнения,
	|	Правила.ИдентификаторПравила КАК ИдентификаторПравила,
	|	Правила.Задача.Код КАК ИдентификаторЗадачи,
	|	Правила.Задача КАК Задача,
	|	ВЫБОР
	|		КОГДА Правила.ЭтоАванс
	|			ТОГДА ""Зарплата, аванс""
	|		ИНАЧЕ ""Зарплата""
	|	КОНЕЦ КАК Расшифровка,
	|	Правила.НаименованиеЗадачиБухгалтера КАК НаименованиеЗадачиБухгалтера,
	|	Правила.Описание КАК ОписаниеПравила,
	|	Правила.ЕстьИнформацияНаИТС КАК ЕстьИнформацияНаИТС,
	|	Правила.ЭтоАванс КАК ЭтоАванс
	|ИЗ
	|	ЗадачиИзСписка КАК ЗадачиИзСписка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПравилаВыплатыЗарплаты КАК Правила
	|		ПО ЗадачиИзСписка.Правило = Правила.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(&ПредыдущийМесяц, , Месяц, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда), , Организация В (&СписокОрганизаций), , ) КАК НачисленныеСуммы
	|		ПО ЗадачиИзСписка.Организация = НачисленныеСуммы.Организация
	|			И (НАЧАЛОПЕРИОДА(ЗадачиИзСписка.ПериодСобытия, МЕСЯЦ) = НачисленныеСуммы.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗарплатаКВыплатеАвансом.Обороты(&ПредыдущийМесяц, , , Организация В (&СписокОрганизаций)) КАК ВыплаченныеАвансы
	|		ПО ЗадачиИзСписка.Организация = ВыплаченныеАвансы.Организация
	|			И (НАЧАЛОПЕРИОДА(ЗадачиИзСписка.ПериодСобытия, МЕСЯЦ) = ВыплаченныеАвансы.ПериодВзаиморасчетов)
	|			И (&УчетЗарплатыСредствамиБухгалтерии)";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗадачиБухгалтера = Новый Структура;
	
	ЗадачиБухгалтера.Вставить("РегулярныеПлатежи", РезультатЗапроса[6].Выгрузить());
	ЗадачиБухгалтера.Вставить("УплатаНалогов",     РезультатЗапроса[7].Выгрузить());
	ЗадачиБухгалтера.Вставить("Зарплата",          РезультатЗапроса[8].Выгрузить());
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.РегулярныеПлатежи
//
Процедура ПриОпределенииРегулярныхПлатежей(РегулярныеПлатежи, ТаблицаЗадач) Экспорт
	
	РегулярныеПлатежи = Обработки.ПлатежныйКалендарь.НоваяТаблицаПлатежи();
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		
		Платеж = РегулярныеПлатежи.Добавить();
		
		ЗаполнитьЗначенияСвойств(Платеж, Задача);
		
		Платеж.ПараметрыКоманды = ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи();
		
		ЗаполнитьЗначенияСвойств(Платеж.ПараметрыКоманды, Задача);
		
	КонецЦикла;
	
КонецПроцедуры 

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗарплатныеПлатежи
//
Процедура ПриОпределенииЗарплатныхПлатежей(ЗарплатныеПлатежи, ТаблицаЗадач) Экспорт
	
	УчетЗарплатыИКадровСредствамиБухгалтерии = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Задачи", ТаблицаЗадач);
	Запрос.УстановитьПараметр("СпособВыплатыЗарплата", Справочники.СпособыВыплатыЗарплаты.ПоУмолчанию());
	Запрос.УстановитьПараметр("СпособВыплатыАванс", Справочники.СпособыВыплатыЗарплаты.Аванс());
	Запрос.УстановитьПараметр("УчетЗарплатыСредствамиБухгалтерии", УчетЗарплатыИКадровСредствамиБухгалтерии);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Задачи.Правило КАК Правило,
	|	Задачи.Организация КАК Организация,
	|	Задачи.НачалоВыполнения КАК НачалоВыполнения,
	|	Задачи.ДатаПлатежа КАК ДатаПлатежа,
	|	НАЧАЛОПЕРИОДА(Задачи.ПериодСобытия, МЕСЯЦ) КАК ПериодРегистрации,
	|	Задачи.ЭтоАванс КАК ЭтоАванс
	|ПОМЕСТИТЬ Задачи
	|ИЗ
	|	&Задачи КАК Задачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задачи.Правило КАК Правило,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка КАК Ведомость,
	|	Задачи.Организация КАК Организация,
	|	Задачи.ПериодРегистрации КАК ПериодРегистрации,
	|	0 КАК СуммаВедомости
	|ПОМЕСТИТЬ Ведомости
	|ИЗ
	|	Задачи КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|		ПО Задачи.Организация = ВедомостьНаВыплатуЗарплатыВБанк.Организация
	|			И (Задачи.ПериодРегистрации = НАЧАЛОПЕРИОДА(ВедомостьНаВыплатуЗарплатыВБанк.ПериодРегистрации, МЕСЯЦ))
	|			И (ВедомостьНаВыплатуЗарплатыВБанк.СпособВыплаты = &СпособВыплатыЗарплата)
	|ГДЕ
	|	НЕ Задачи.ЭтоАванс
	|	И ВедомостьНаВыплатуЗарплатыВБанк.Проведен
	|	И ВедомостьНаВыплатуЗарплатыВБанк.СуммаПоДокументу <> 0
	|	И &УчетЗарплатыСредствамиБухгалтерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Задачи.Правило,
	|	ВедомостьНаВыплатуЗарплатыВБанк.Ссылка,
	|	Задачи.Организация,
	|	Задачи.ПериодРегистрации,
	|	0
	|ИЗ
	|	Задачи КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыВБанк КАК ВедомостьНаВыплатуЗарплатыВБанк
	|		ПО Задачи.Организация = ВедомостьНаВыплатуЗарплатыВБанк.Организация
	|			И (Задачи.ПериодРегистрации = НАЧАЛОПЕРИОДА(ВедомостьНаВыплатуЗарплатыВБанк.ПериодРегистрации, МЕСЯЦ))
	|			И (ВедомостьНаВыплатуЗарплатыВБанк.СпособВыплаты = &СпособВыплатыАванс)
	|ГДЕ
	|	Задачи.ЭтоАванс
	|	И ВедомостьНаВыплатуЗарплатыВБанк.Проведен
	|	И ВедомостьНаВыплатуЗарплатыВБанк.СуммаПоДокументу <> 0
	|	И &УчетЗарплатыСредствамиБухгалтерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Задачи.Правило,
	|	ВедомостьНаВыплатуЗарплаты.Ссылка,
	|	Задачи.Организация,
	|	Задачи.ПериодРегистрации,
	|	ВедомостьНаВыплатуЗарплаты.СуммаДокумента
	|ИЗ
	|	Задачи КАК Задачи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплаты КАК ВедомостьНаВыплатуЗарплаты
	|		ПО Задачи.Организация = ВедомостьНаВыплатуЗарплаты.Организация
	|			И (Задачи.ПериодРегистрации = НАЧАЛОПЕРИОДА(ВедомостьНаВыплатуЗарплаты.ПериодРегистрации, МЕСЯЦ))
	|			И (ВедомостьНаВыплатуЗарплаты.Дата МЕЖДУ Задачи.НачалоВыполнения И КОНЕЦПЕРИОДА(Задачи.ДатаПлатежа, ДЕНЬ))
	|			И (ВедомостьНаВыплатуЗарплаты.ВидМестаВыплаты = ЗНАЧЕНИЕ(Перечисление.ВидыМестВыплатыЗарплаты.БанковскийСчет)
	|				ИЛИ ВедомостьНаВыплатуЗарплаты.ВидМестаВыплаты = ЗНАЧЕНИЕ(Перечисление.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект))
	|ГДЕ
	|	НЕ ВедомостьНаВыплатуЗарплаты.ПометкаУдаления
	|	И ВедомостьНаВыплатуЗарплаты.СуммаДокумента <> 0
	|	И НЕ &УчетЗарплатыСредствамиБухгалтерии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ведомости.Ведомость КАК Ведомость,
	|	Ведомости.Правило КАК Правило,
	|	Ведомости.Организация КАК Организация,
	|	Ведомости.ПериодРегистрации КАК Период,
	|	СУММА(Ведомости.СуммаВедомости) КАК СуммаВедомости
	|ИЗ
	|	Ведомости КАК Ведомости
	|
	|СГРУППИРОВАТЬ ПО
	|	Ведомости.Ведомость,
	|	Ведомости.Правило,
	|	Ведомости.Организация,
	|	Ведомости.ПериодРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ведомости.Правило КАК Правило,
	|	Ведомости.Организация КАК Организация,
	|	Ведомости.ПериодРегистрации КАК Период
	|ИЗ
	|	Ведомости КАК Ведомости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ПО Ведомости.Ведомость = ПлатежноеПоручение.ДокументОснование
	|ГДЕ
	|	НЕ ПлатежноеПоручение.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Ведомости.Правило,
	|	Ведомости.Организация,
	|	Ведомости.ПериодРегистрации
	|ИЗ
	|	Ведомости КАК Ведомости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ПО Ведомости.Ведомость = ПлатежноеПоручение.ПлатежнаяВедомость
	|ГДЕ
	|	НЕ ПлатежноеПоручение.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	Ведомости.Правило,
	|	Ведомости.Организация,
	|	Ведомости.ПериодРегистрации";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет(); 
	
	ПериодыВедомости = РезультатЗапроса[2].Выгрузить();
	ПериодыЕстьПлатежка = РезультатЗапроса[3].Выгрузить();
	
	ЗарплатныеПлатежи = Обработки.ПлатежныйКалендарь.НоваяТаблицаПлатежи();
	
	Для Каждого Задача Из ТаблицаЗадач Цикл
		
		Платеж = ЗарплатныеПлатежи.Добавить();
		
		ЗаполнитьЗначенияСвойств(Платеж, Задача);
		
		Платеж.ПараметрыКоманды = ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи();
		
		ЗаполнитьЗначенияСвойств(Платеж.ПараметрыКоманды, Задача);
		
		ПараметрыОтбора = Новый Структура("Правило, Организация, Период",
				Задача.Правило, Задача.Организация, НачалоМесяца(Задача.ПериодСобытия));

		Если Не УчетЗарплатыИКадровСредствамиБухгалтерии  Тогда
					
			Ведомости = ПериодыВедомости.Скопировать(ПараметрыОтбора);
			Платеж.Сумма = Ведомости.Итог("СуммаВедомости");
			Если Платеж.Сумма <> 0 Тогда 
				СписокВедомостей = Новый СписокЗначений;
				СписокВедомостей.ЗагрузитьЗначения(Ведомости.ВыгрузитьКолонку("Ведомость"));
				Платеж.ПараметрыКоманды.Вставить("СписокВедомостей", СписокВедомостей);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Платеж.Сумма = 0 И НЕ ЗначениеЗаполнено(Платеж.Примечание) Тогда
			Платеж.Примечание = ВыполнениеЗадачБухгалтераБП.РекомендацияПоНачислениюЗарплаты(Задача.Организация, Задача.ПериодСобытия, Задача.ЭтоАванс);
		КонецЕсли;
		
		Если Платеж.Сумма <> 0 И ПериодыЕстьПлатежка.Количество() <> 0 Тогда	
						
			Платеж.ЕстьПлатежныйДокумент = ПериодыЕстьПлатежка.НайтиСтроки(ПараметрыОтбора).Количество() <> 0;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.НалоговыеПлатежи
//
Процедура ПриОпределенииНалоговыхПлатежей(НалоговыеПлатежи, ТаблицаЗадач) Экспорт
	
	ВыполнениеЗадачБухгалтераБП.ПриОпределенииНалоговыхПлатежей(НалоговыеПлатежи, ТаблицаЗадач);
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеВспомогательныеПроцедуры

// Возвращает признак того, что правило является правилом уплаты патента
Процедура ОпределитьЭтоПравилоУплатыПатента(ЭтоПравилоУплатыПатента, Правило) Экспорт
	
	ЭтоПравилоУплатыПатента = ТипЗнч(Правило) = Тип("СправочникСсылка.Патенты");
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ОперацияНачисленияВзносовВыполнена
//
Процедура ПриОпределенииПризнакаОперацияНачисленияВзносовВыполнена(ОперацияВыполнена, Организация, ПериодСобытия, ДокументЗарплаты, НесколькоДокументов, ИмяДокумента) Экспорт
	
	ОперацияВыполнена = Ложь;
	
	Если ИмяДокумента = Неопределено Тогда
		ИмяДокумента = ЗакрытиеМесяца.ВидДокументаНачисленияЗарплаты(Организация);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период",      ПериодСобытия);
	Запрос.УстановитьПараметр("Документ",    ИмяДокумента);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	НачислениеЗарплаты.Ссылка КАК Ссылка,
	|	НачислениеЗарплаты.Проведен КАК Проведен
	|ИЗ
	|	Документ.НачислениеЗарплаты КАК НачислениеЗарплаты
	|ГДЕ
	|	&Документ = ""НачислениеЗарплаты""
	|	И НЕ НачислениеЗарплаты.ПометкаУдаления
	|	И НачислениеЗарплаты.Организация = &Организация
	|	И НачислениеЗарплаты.МесяцНачисления МЕЖДУ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	ОтражениеЗарплатыВБухучете.Ссылка,
	|	ОтражениеЗарплатыВБухучете.Проведен
	|ИЗ
	|	Документ.ОтражениеЗарплатыВБухучете КАК ОтражениеЗарплатыВБухучете
	|ГДЕ
	|	&Документ = ""ОтражениеЗарплатыВБухучете""
	|	И НЕ ОтражениеЗарплатыВБухучете.ПометкаУдаления
	|	И ОтражениеЗарплатыВБухучете.Организация = &Организация
	|	И ОтражениеЗарплатыВБухучете.ПериодРегистрации МЕЖДУ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	ОтражениеЗарплатыВУчете.Ссылка,
	|	ОтражениеЗарплатыВУчете.Проведен
	|ИЗ
	|	Документ.ОтражениеЗарплатыВУчете КАК ОтражениеЗарплатыВУчете
	|ГДЕ
	|	&Документ = ""ОтражениеЗарплатыВУчете""
	|	И НЕ ОтражениеЗарплатыВУчете.ПометкаУдаления
	|	И ОтражениеЗарплатыВУчете.Организация = &Организация
	|	И ОтражениеЗарплатыВУчете.ПериодРегистрации МЕЖДУ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РегламентнаяОперация.Ссылка,
	|	РегламентнаяОперация.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийРегламентныхОпераций.Выполнено)
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	&Документ = ""РегламентнаяОперация""
	|	И НЕ РегламентнаяОперация.ПометкаУдаления
	|	И РегламентнаяОперация.Организация = &Организация
	|	И РегламентнаяОперация.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|	И РегламентнаяОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыРегламентныхОпераций.НачислениеВзносовОтНесчастныхСлучаев)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Проведен УБЫВ";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Если Выборка.Проведен Тогда
			ОперацияВыполнена = Истина;
		Иначе
			Если Выборка.Количество() = 1 Тогда
				ДокументЗарплаты = Выборка.Ссылка;
			ИначеЕсли Выборка.Количество() = 2 Тогда
				НесколькоДокументов = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗанестиСведенияОНарушенииСрокаПодачиРегламентированногоОтчета
//
Процедура ЗанестиСведенияОНарушенииСрокаПодачиРегламентированногоОтчета(Документ, ТекущийДень, Организация) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.ОтчетностьСНарушеннымСрокомПодачи.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.РегламентированныйОтчет = Документ;
	МенеджерЗаписи.Дата = ТекущийДень;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.УстановитьДополнительныеСвойстваОплатыАУСН
//
Процедура УстановитьДополнительныеСвойстваОплатыАУСН(ДокументОбъект) Экспорт
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СписаниеСРасчетногоСчета")
		И УчетнаяПолитика.ПрименяетсяАУСН(ДокументОбъект.Организация, ДокументОбъект.НалоговыйПериод) Тогда
		
		ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Налог, "ВидНалога");
		Если ВидНалога = Перечисления.ВидыНалогов.АУСН
			Или ВидНалога = Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж Тогда
			ПравилоАУСН = ВыполнениеЗадачБухгалтераБП.ПравилоДействияКалендаряБухгалтера_АУСН(ДокументОбъект.НалоговыйПериод,
				Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога);
			Если ЗначениеЗаполнено(ПравилоАУСН) Тогда
				ДокументОбъект.ДополнительныеСвойства.Вставить("ПравилоУплатыНалогов", ПравилоАУСН);
				ДокументОбъект.ДополнительныеСвойства.Вставить("ПериодСобытияКалендаря", КонецМесяца(ДокументОбъект.НалоговыйПериод));
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗаполнитьСтатусыЗадачДобровольныеВзносыИП
//
Процедура ЗаполнитьСтатусыЗадачДобровольныеВзносыИП(ПараметрыЗадач) Экспорт
	
	Для Каждого ПараметрыЗадачи Из ПараметрыЗадач Цикл
		
		Если ПараметрыЗадачи.СтатусУстановленВручную Тогда
			ПараметрыЗадачи.Статус = ПараметрыЗадачи.РучнойСтатус;
			Продолжить;
		КонецЕсли;
		
		СуммаКУплате = УчетСтраховыхВзносовИП.СуммаФиксированныхВзносовНаВНиМ(
			ПараметрыЗадачи.Организация,
			ПараметрыЗадачи.ПериодСобытия,
			ПараметрыЗадачи.Правило);
		
		Если СуммаКУплате = 0 Тогда
			ПараметрыЗадачи.Статус = "Оплачено";
		Иначе
			ПараметрыЗадачи.Статус = "";
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьЗадачиУплатыПатента(ЗадачиУплатыПатента, Организация, ОтборПатент) Экспорт
	
	Запрос = Новый Запрос;

	Если ТипЗнч(ОтборПатент) = Тип("СправочникСсылка.Патенты") Тогда
		Патенты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОтборПатент);
	ИначеЕсли ТипЗнч(ОтборПатент) = Тип("Массив") Тогда
		Патенты = ОтборПатент;
	Иначе
		ЗадачиУплатыПатента = Новый ТаблицаЗначений;
		Возврат;
	КонецЕсли;

	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Патенты",     Патенты);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗадачиБухгалтера.Организация КАК Организация,
	|	ЗадачиБухгалтера.Правило КАК Правило,
	|	ЗадачиБухгалтера.ПериодСобытия КАК ПериодСобытия,
	|	ЗадачиБухгалтера.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога) КАК Действие
	|ИЗ
	|	РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|ГДЕ
	|	ЗадачиБухгалтера.Организация = &Организация
	|	И ЗадачиБухгалтера.Правило В(&Патенты)
	|	И НЕ ЗадачиБухгалтера.ВАрхиве";

	ЗадачиУплатыПатента = Запрос.Выполнить().Выгрузить();

КонецПроцедуры

#КонецОбласти

#Область РегистрацияПлатежей

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЗарегистрироватьИсполнениеЗадачиПриЗаписиПлатежа
//
Процедура ЗарегистрироватьИсполнениеЗадачиПриЗаписиПлатежа(РеквизитыПлатежа,
		ДополнительныеСвойства,
		ПараметрыРасчетаСтатусов,
		ПравилоУплатыНалогов,
		ПериодСобытияКалендаря,
		ОплатаЗадолженности,
		РегистрацияВНалоговомОргане) Экспорт
		
	РегистрыСведений.ЗадачиБухгалтераНалоговыеПлатежи.ДобавитьЗаписьПравила(
			РеквизитыПлатежа.Организация,
			РеквизитыПлатежа.Ссылка,
			ПравилоУплатыНалогов,
			ПериодСобытияКалендаря,
			ОплатаЗадолженности,
			РегистрацияВНалоговомОргане);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ИзменятьСтатусыЗадачПриЗаписиПлатежа
//
Процедура УточнитьИзменятьСтатусыЗадачПриЗаписиПлатежа(ИзменятьСтатусы, РеквизитыПлатежа, ДополнительныеСвойства) Экспорт
	
	ИзменятьСтатусы = Ложь;
	
	Если РеквизитыПлатежа.ВидДокумента = "РасходныйКассовыйОрдер" Тогда
		
		ИзменятьСтатусы = Истина;
		
	ИначеЕсли РеквизитыПлатежа.ВидДокумента = "ПлатежноеПоручение" Тогда
		
		ОбновлятьСвязанныеЗадачи = ВыполнениеЗадачБухгалтераБП.ПлатежиПоНалогуВсегдаОбновляютСвязанныеЗадачи(РеквизитыПлатежа.ВидНалога);
		
		Если ОбновлятьСвязанныеЗадачи Тогда
			ТекущееСостояние = РегистрыСведений.СостоянияБанковскихДокументов.ТекущееСостояниеДокумента(РеквизитыПлатежа.Ссылка);
			НовоеСостояние   = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "Состояние");
			
			// Обновление задач имеет смысл только при изменении оплаченного документа без изменения его состояния.
			// Если состояние изменилось - обновление задач вызывается при записи нового состояния.
			ИзменяетсяОплаченныйДокумент = (НовоеСостояние = ТекущееСостояние)
				И ТекущееСостояние = Перечисления.СостоянияБанковскихДокументов.Оплачено;
		Иначе
			ИзменяетсяОплаченныйДокумент = Ложь;
		КонецЕсли;
		
		ПлатежноеПоручениеВведеноНаОснованииСписания = ВыполнениеЗадачБухгалтераБП.ЭтоПлатежноеПоручениеНаОснованииСписанияСоСчета(РеквизитыПлатежа);
		
		ИзменятьСтатусы = ОбновлятьСвязанныеЗадачи И ИзменяетсяОплаченныйДокумент И НЕ ПлатежноеПоручениеВведеноНаОснованииСписания;
		
	ИначеЕсли РеквизитыПлатежа.ВидДокумента = "СписаниеСРасчетногоСчета" Тогда
		
		// Обновлять статусы требуется, если платится налог, для которого все платежи влияют на состояние задач,
		// и уплата произведена непосредственно списанием с расчетного счета без платежного поручения.
		
		ОбновлятьСвязанныеЗадачи = ВыполнениеЗадачБухгалтераБП.ПлатежиПоНалогуВсегдаОбновляютСвязанныеЗадачи(РеквизитыПлатежа.ВидНалога);
		
		ЭтоСписаниеБезПлатежногоПоручения =
			(НЕ ЗначениеЗаполнено(РеквизитыПлатежа.ДокументОснование)
				ИЛИ ТипЗнч(РеквизитыПлатежа.ДокументОснование) <> Тип("ДокументСсылка.ПлатежноеПоручение"));
				
		Если ВыполнениеЗадачБухгалтераБП.ЭтоВыплатаЗарплаты(РеквизитыПлатежа.ВидОперации) Тогда
			ОбновлятьСвязанныеЗадачи = Истина;
		КонецЕсли;
		
		Если УчетнаяПолитика.ПрименяетсяАУСН(РеквизитыПлатежа.Организация, РеквизитыПлатежа.Дата) Тогда 
			Если ВыполнениеЗадачБухгалтераБП.ЭтоУплатаНалога(РеквизитыПлатежа.ВидОперации)
				И РеквизитыПлатежа.ВидНалога = Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж Тогда
				ИзменятьСтатусы = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ИзменятьСтатусы = ОбновлятьСвязанныеЗадачи И ЭтоСписаниеБезПлатежногоПоручения;
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ДокументВыполняетЗадачу
//
Процедура ПриОпределенииПризнакаДокументВыполняетЗадачу(ДокументВыполняетЗадачу, ДокументОбъект) Экспорт
	
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДокументОбъект, "ВидОперации") Тогда
		ДокументВыполняетЗадачу = Ложь;
		Возврат;
	КонецЕсли;
	
	ДокументВыполняетЗадачу = ВыполнениеЗадачБухгалтераБП.ЭтоУплатаНалога(ДокументОбъект.ВидОперации)
		ИЛИ ВыполнениеЗадачБухгалтераБП.ЭтоВыплатаЗарплаты(ДокументОбъект.ВидОперации);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЭтоПлатежноеПоручениеНаОснованииСписанияСоСчета
//
Процедура ОпределитьЭтоПлатежноеПоручениеНаОснованииСписанияСоСчета(ЭтоПлатежноеПоручение, РеквизитыПлатежа) Экспорт
	
	ЭтоПлатежноеПоручение = РеквизитыПлатежа.ВидДокумента = "ПлатежноеПоручение"
		И ЗначениеЗаполнено(РеквизитыПлатежа.ДокументОснование)
		И ТипЗнч(РеквизитыПлатежа.ДокументОснование) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета");
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ЭтоПогашениеНалоговойЗадолженности
//
Процедура ОпределитьЭтоПогашениеНалоговойЗадолженности(ЭтоПогашение, РеквизитыПлатежа) Экспорт
	
	ПоказательОснования = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РеквизитыПлатежа, "ПоказательОснования", "");
	
	ЭтоПогашение = ПлатежиВБюджетКлиентСервер.ЭтоДобровольноеПогашениеЗадолженности(ПоказательОснования);
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.РеквизитыПлатежаПоДаннымДокумента
//
Процедура ОпределитьРеквизитыПлатежаПоДаннымДокумента(РеквизитыПлатежа, ДокументСсылка) Экспорт
	
	РеквизитыПлатежа = Неопределено;
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
		
		РеквизитыПлатежа = ВыполнениеЗадачБухгалтераБП.РеквизитыПлатежногоПоручения(ДокументСсылка);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		
		РеквизитыПлатежа = ВыполнениеЗадачБухгалтераБП.РеквизитыРасходногоКассовогоОрдера(ДокументСсылка);
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		
		РеквизитыПлатежа = ВыполнениеЗадачБухгалтераБП.РеквизитыСписанияСРасчетногоСчета(ДокументСсылка);
		
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПриПолученииРеквизитовПлатежаВлияющихНаСтатусЗадачи
//
Процедура ПриПолученииРеквизитовПлатежаВлияющихНаСтатусЗадачи(ОписаниеРеквизитов) Экспорт
	
	ОписаниеРеквизитов.Вставить("Налог", Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка());
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияУведомлений

// Подробнее
// См. ВыполнениеЗадачБухгалтера.УстановитьСвойстваУведомленияПриРегистрации
//
Процедура УстановитьСвойстваУведомленияПриРегистрации(ДокументОбъект, Правило, ПериодСобытия, ПропуститьОбновлениеСтатуса = Ложь, РегистрацияВНалоговомОргане = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Правило) ИЛИ НЕ ЗначениеЗаполнено(ПериодСобытия) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПравилоУведомления",          Правило);
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПериодСобытияКалендаря",      ПериодСобытия);
	
	Если Не ВыполнениеЗадачБухгалтера.ЭтоПравилоЕдиногоНалоговогоСчета(Правило) Тогда
		ДокументОбъект.ДополнительныеСвойства.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	КонецЕсли;
	
	Если ПропуститьОбновлениеСтатуса Тогда
		ДокументОбъект.ДополнительныеСвойства.Вставить("ПропуститьОбновлениеСтатусаЗадачи");
	КонецЕсли;
	
КонецПроцедуры

// Подробнее
// См. ВыполнениеЗадачБухгалтера.ПриЗаписиУведомления
//
Процедура ПриЗаписиУведомления(ДокументОбъект) Экспорт
	
	РеквизитыУведомления = ВыполнениеЗадачБухгалтераБП.РеквизитыУведомленияПоДаннымОбъекта(ДокументОбъект);
	РеквизитыУведомленияДоИзменения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДокументОбъект.ДополнительныеСвойства, "РеквизитыУведомленияДоИзменения");
	
	ПараметрыРасчетаСтатусов = Новый Структура;
	
	ВыполнениеЗадачБухгалтераБП.ЗарегистрироватьИсполнениеЗадачиПриЗаписиУведомления(
		РеквизитыУведомления, ДокументОбъект.ДополнительныеСвойства, ПараметрыРасчетаСтатусов);
	
КонецПроцедуры

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать ВыполнениеЗадачБухгалтераПереопределяемый.ПриОпределенииОписанияДействияВзносыНаСоциальноеСтрахованиеИП.
// См. ВыполнениеЗадачБухгалтера.ОписаниеДействияСтраховыеВзносыИПНаВНиМ
//
Процедура ПриОпределенииОписанияДействияСтраховыеВзносыИПНаВНиМ(Событие, ОписаниеДействия) Экспорт
КонецПроцедуры 

#КонецОбласти

#КонецОбласти
