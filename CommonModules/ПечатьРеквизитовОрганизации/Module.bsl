////////////////////////////////////////////////////////////////////////////////
// ПечатьРеквизитовОрганизации: процедуры и функции для печати карточки организации и настройки ее вывода
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает табличный документ - карточки реквизитов организации.
// Карточки формируются по сохраненным настройкам и сведениям.
//
// Параметры:
//  МассивОбъектов - Массив из СправочникСсылка.Организации
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
// 
// Возвращаемое значение:
//  ТабличныйДокумент - карточка реквизитов организации
//
Функция Печать(МассивОбъектов, ОбъектыПечати) Экспорт
	
	НаборНастроекИСведений = Новый Массив;
	
	Для Каждого Организация Из МассивОбъектов Цикл
		
		Настройки = НастройкиПечати(Организация);
		Сведения = СведенияДляПечати(Организация);
		ЭлементНабора = НовыйЭлементНабораНастроекИСведений(Организация, Настройки, Сведения);
		НаборНастроекИСведений.Добавить(ЭлементНабора);
		
	КонецЦикла;
	
	ТабличныйДокумент = РеквизитыОрганизации(НаборНастроекИСведений, ОбъектыПечати);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Возвращает табличный документ - карточку реквизитов организации, построенную по переданным настройкам и сведениям.
// Используется для формирования карточки по уже полученным ранее настройкам и сведениям.
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  НастройкиРеквизитов - ДеревоЗначений: см. НовоеДеревоНастроекПечати
//  Сведения - Структура: см. НовыйСведенияОбОрганизации
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
// 
// Возвращаемое значение:
//  ТабличныйДокумент - карточка реквизитов организации
//
Функция ПечатьПоНастройкамИСведениям(Организация, НастройкиРеквизитов, Сведения, ОбъектыПечати) Экспорт
	
	НаборНастроекИСведений = Новый Массив;
	
	ЭлементНабора = НовыйЭлементНабораНастроекИСведений(Организация, НастройкиРеквизитов, Сведения);
	НаборНастроекИСведений.Добавить(ЭлементНабора);
		
	ТабличныйДокумент = РеквизитыОрганизации(НаборНастроекИСведений, ОбъектыПечати);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Возвращает массив реквизитов и их значений по организации, согласно настройкам печати реквизитов организации
//
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Массив из Структура: см. РеквизитСтруктурой
//
Функция РеквизитыОрганизацииДляТелаПисьма(Организация) Экспорт
	
	Результат = Новый Массив;
	
	Настройки = НастройкиПечати(Организация);
	Сведения = СведенияДляПечати(Организация);
	
	Для Каждого СтрокаГруппы Из Настройки.Строки Цикл
		
		Если Не СтрокаГруппы.Печатать Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоГруппаРеквизитов(СтрокаГруппы) Тогда
			
			Для Каждого СтрокаРеквизита Из СтрокаГруппы.Строки Цикл
				ДанныеРеквизита = ДанныеРеквизитаПоСведениямИСтрокеНастроек(Сведения, СтрокаРеквизита);
				Если ДанныеРеквизита <> Неопределено Тогда
					Результат.Добавить(ДанныеРеквизита);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает дерево настроек для вывода печатной формы реквизитов организации,
// заполненное согласно настройкам текущего пользователя или по-умолчанию.
// Если у текущего пользователя нет своих настроек, вернутся настройки по-умолчанию.
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ПоУмолчанию - Булево - заполнить настройками по-умолчанию
//
// Возвращаемое значение:
//  ДеревоЗначений: см. НовоеДеревоНастроекПечатиРеквизитовОрганизации
//
Функция НастройкиПечати(Организация, ПоУмолчанию = Ложь) Экспорт
	
	СтандартныеНастройки = СтандартныеНастройкиПечати(Организация);
	
	Если ПоУмолчанию Тогда
		Возврат СтандартныеНастройки;
	КонецЕсли;
	
	ИдентификаторОрганизации = Строка(Организация.УникальныйИдентификатор());
	ПользовательскиеНастройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъектаНастроек(),
		ИдентификаторОрганизации);
	
	Настройки = СтандартныеНастройки;
	
	Если ТипЗнч(ПользовательскиеНастройки) = Тип("ДеревоЗначений") Тогда
		АктуализироватьПользовательскиеНастройкиПечати(ПользовательскиеНастройки, СтандартныеНастройки);
		Настройки = ПользовательскиеНастройки;
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

// Записывает дерево настроек для вывода печатной формы реквизитов организации.
//
// Параметры:
//  Настройки - ДеревоЗначений: см. НовоеДеревоНастроекПечатиРеквизитовОрганизации
//  Организация - СправочникСсылка.Организации
//
Процедура ЗаписатьНастройкиПечати(Настройки, Организация) Экспорт
	
	ИдентификаторОрганизации = Строка(Организация.УникальныйИдентификатор());
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъектаНастроек(),
		ИдентификаторОрганизации,
		Настройки);
	
КонецПроцедуры

// Данные для заполнения печатной формы реквизитов организации
//
// Параметры:
//  Организация - СправочникСсылка.Организации
//
// Возвращаемое значение:
//  Структура: см. НовыйСведенияОбОрганизации
//
Функция СведенияДляПечати(Организация) Экспорт
	
	СведенияОбОрганизации = НовыйСведенияОбОрганизации();
	
	ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
	СведенияОбОрганизации.Дата = ТекущаяДата;
	
	БанковскийСчет = Неопределено;
	ОсновнойБанковскийСчет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ОсновнойБанковскийСчет");
	Если Не ЗначениеЗаполнено(ОсновнойБанковскийСчет) Тогда
		ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		БанковскиеСчетаОрганизации = Справочники.БанковскиеСчета.БанковскиеСчетаОрганизации(Организация, ВалютаРегУчета);
		Если БанковскиеСчетаОрганизации.Количество() = 1 Тогда
			БанковскийСчет = БанковскиеСчетаОрганизации[0].Ссылка;
		КонецЕсли;
	КонецЕсли;
		
	СведенияОЮрФизЛице = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, ТекущаяДата, БанковскийСчет);
		
	ЗаполнитьЗначенияСвойств(СведенияОбОрганизации, СведенияОЮрФизЛице);
		
	ЭтоФизЛицо = СведенияОЮрФизЛице.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
	ФайлЛоготип = Справочники.Организации.ДвоичныеДанныеКартинкиОрганизации(Организация, "ФайлЛоготип");
	ФайлПечать = Справочники.Организации.ДвоичныеДанныеКартинкиОрганизации(Организация, "ФайлПечать");
	Если ЭтоФизЛицо Тогда
		ВладелецПодписи = СведенияОбОрганизации.ИндивидуальныйПредприниматель;
	Иначе
		ВладелецПодписи = СведенияОбОрганизации.Руководитель;
	КонецЕсли;
	ФайлПодпись = УправлениеПечатьюБП.ДвоичныеДанныеФаксимилеФизическогоЛица(ВладелецПодписи);
	СведенияОбОрганизации.Логотип = ФайлЛоготип;
	СведенияОбОрганизации.Печать = ФайлПечать;
	СведенияОбОрганизации.Подпись = ФайлПодпись;
	
	ОтветственныеЛица = ОтветственныеЛицаБППовтИсп.ОтветственныеЛица(Организация, ТекущаяДата);
	Если ЗначениеЗаполнено(ОтветственныеЛица.Руководитель) Тогда
		ФИО = ОтветственныеЛица.РуководительФИО;
		ФИОПолные = СокрЛП(СтрШаблон(НСтр("ru = '%1 %2 %3'"), ФИО.Фамилия, ФИО.Имя, ФИО.Отчество));
		СведенияОбОрганизации.РуководительФамилияИО = ОтветственныеЛица.РуководительПредставление;
		Должность = ОтветственныеЛица.РуководительДолжность;
		Если Не ЗначениеЗаполнено(Должность) Тогда
			Должность = "Руководитель";
		КонецЕсли;
		РуководительДанные = РеквизитСтруктурой(Должность, ФИОПолные);
		СведенияОбОрганизации.РуководительДанные = РуководительДанные;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтветственныеЛица.ГлавныйБухгалтер) Тогда
		ФИО = ОтветственныеЛица.ГлавныйБухгалтерФИО;
		ФИОПолные = СокрЛП(СтрШаблон(НСтр("ru = '%1 %2 %3'"), ФИО.Фамилия, ФИО.Имя, ФИО.Отчество));
		СведенияОбОрганизации.ГлавныйБухгалтерФамилияИО = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		Должность = ОтветственныеЛица.ГлавныйБухгалтерДолжность;
		Если Не ЗначениеЗаполнено(Должность) Тогда
			Должность = "Главный бухгалтер";
		КонецЕсли;
		ГлавныйБухгалтерДанные = РеквизитСтруктурой(Должность, ФИОПолные);
		СведенияОбОрганизации.ГлавныйБухгалтерДанные = ГлавныйБухгалтерДанные;
	КонецЕсли;
	
	ПоляСведений = СтрРазделить("ОКТМО, ОКПО, ОКВЭД2, НаимГоловнОрг, СистемыНалогообложения", ", ", Ложь);
	Сведения = РегламентированнаяОтчетностьБП.ПолучитьСведенияОбОрганизации(Организация, ТекущаяДата, ПоляСведений);
	
	СведенияОбОрганизации.ОКТМО = Сведения.ОКТМО;
	СведенияОбОрганизации.ОКПО = Сведения.ОКПО;
	СведенияОбОрганизации.ОКВЭД = Сведения.ОКВЭД2;
	СведенияОбОрганизации.ГоловнаяОрганизация = Сведения.НаимГоловнОрг;
	
	ПредставлениеНалогообложения = Сведения.СистемыНалогообложения;
	Если УчетнаяПолитика.ПрименяетсяУСН(Организация, ТекущаяДата) Тогда
		НДС = НСтр("ru = ', Без НДС'");
		Если УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Организация, ТекущаяДата) Тогда
			СтавкаНДС = УчетНДСВызовСервера.СтавкаНДСПоОрганизации(Организация, ТекущаяДата);
			НДС = СтрШаблон(НСтр("ru = ', НДС %1'"), СтавкаНДС);
		КонецЕсли;
		ПредставлениеНалогообложения = ПредставлениеНалогообложения + НДС;
	КонецЕсли;
	Если УчетнаяПолитика.ПлательщикНДС(Организация, ТекущаяДата)
		И УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(Организация, ТекущаяДата) Тогда
		ПредставлениеНалогообложения = СтрШаблон(НСтр("ru = '%1 (освобождение от НДС по ст. 145 НК РФ)'"),
			ПредставлениеНалогообложения);
	КонецЕсли;
	СведенияОбОрганизации.Налогообложение = ПредставлениеНалогообложения;
	
	ДопРеквизитыИСведения = Новый Соответствие;
	ДанныеЗначенийДопСвойств = УправлениеСвойствами.ЗначенияСвойств(Организация);
	Для Каждого ЗначениеДопСвойства Из ДанныеЗначенийДопСвойств Цикл
		ДопРеквизитыИСведения.Вставить(ЗначениеДопСвойства.ИмяСвойства, ЗначениеДопСвойства.Представление);
	КонецЦикла;
	СведенияОбОрганизации.ДопРеквизитыИСведения = ДопРеквизитыИСведения;
	
	СведенияОбОрганизации.ДатаРегистрации = Формат(СведенияОбОрганизации.ДатаРегистрации, "ДЛФ=D");
	
	ИдентификаторыЭДО = Новый Соответствие;
	ДанныеУчетныхЗаписейЭДО = ОбменСКонтрагентами.ДанныеУчетныхЗаписейЭДООрганизации(Организация);
	Для Каждого ДанныеЭДО Из ДанныеУчетныхЗаписейЭДО Цикл
		ЗаписьДанныхЭДО = РеквизитСтруктурой(ДанныеЭДО.ПредставлениеОператора, ДанныеЭДО.ИдентификаторЭДО);
		ИдентификаторыЭДО.Вставить(ДанныеЭДО.ИдентификаторЭДО, ЗаписьДанныхЭДО);
	КонецЦикла;
	СведенияОбОрганизации.Вставить("ИдентификаторыЭДО", ИдентификаторыЭДО);
	
	//@skip-check constructor-function-return-section
	Возврат СведенияОбОрганизации;
	
КонецФункции

// Конструктор формирования дерева настроек печати реквизитов организации
//
// Возвращаемое значение:
//   ДеревоЗначений:
//      * Группа - Строка - имя группы реквизитов, определяет область макета
//      * Представление - Строка - определяет представление группы или реквизита в настройках и печатной форме
//      * Реквизит - Строка - имя реквизита
//      * ИмяНабораСведений - Строка - имя поля в СведенияОбОрганизации, в котором хранятся динамические данные
//          , такие как дополнительные реквизиты и сведения подсистемы Свойства
//      * Печатать - Булево - выводить реквизит на печать
//
Функция НовоеДеревоНастроекПечати() Экспорт
	
	Результат = Новый ДеревоЗначений;
	ТипСтрока100 = ОбщегоНазначения.ОписаниеТипаСтрока(100);
	ТипСтрока30 = ОбщегоНазначения.ОписаниеТипаСтрока(30);
	ТипБулево = Новый ОписаниеТипов("Булево");
	
	Результат.Колонки.Добавить("Группа", ТипСтрока30); 
	Результат.Колонки.Добавить("Представление", ТипСтрока100);
	Результат.Колонки.Добавить("Реквизит", ТипСтрока100);
	Результат.Колонки.Добавить("ИмяНабораСведений", ТипСтрока100);
	Результат.Колонки.Добавить("Печатать", ТипБулево);
	
	Возврат Результат;
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

Процедура ОбновитьСтруктуруНастроек_ИмяНабораСведенийИНалогообложение(ПараметрыОбновления = Неопределено) Экспорт
	
	// вместо признака ДопРеквизитСведение используется указание на набор сведений
	ТаблицаКлючейНастроек = ТаблицаКлючейНастроекВсехПользователей();
	Для Каждого СтрокаКлючей Из ТаблицаКлючейНастроек Цикл
		ПользовательскиеНастройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(СтрокаКлючей.КлючОбъектаНастроек,
				СтрокаКлючей.КлючНастроек,
				,
				,
				СтрокаКлючей.ИмяПользователя);
				
		Если ТипЗнч(ПользовательскиеНастройки) = Тип("ДеревоЗначений") Тогда
			
			КолонкаДопРеквизитСведение = ПользовательскиеНастройки.Колонки.Найти("ДопРеквизитСведение");
			Если КолонкаДопРеквизитСведение <> Неопределено Тогда
				ПользовательскиеНастройки.Колонки.Добавить("ИмяНабораСведений", ОбщегоНазначения.ОписаниеТипаСтрока(100));
				ПараметрыОтбора = Новый Структура("ДопРеквизитСведение", Истина);
				СтрокиДопРеквизитовИСведений = ПользовательскиеНастройки.Строки.НайтиСтроки(ПараметрыОтбора, Истина);
				Для Каждого СтрокаДопРеквизита Из СтрокиДопРеквизитовИСведений Цикл
					СтрокаДопРеквизита.ИмяНабораСведений = "ДопРеквизитыИСведения";
				КонецЦикла;
				ПользовательскиеНастройки.Колонки.Удалить(КолонкаДопРеквизитСведение);
			КонецЕсли;
			
			ГруппаДополнительно = ПользовательскиеНастройки.Строки.Найти("Дополнительно", "Группа");
			НалогообложениеСтароеПоле = ГруппаДополнительно.Строки.Найти("СистемаНалогообложения", "Реквизит");
			Если НалогообложениеСтароеПоле <> Неопределено Тогда
				Печатать = НалогообложениеСтароеПоле.Печатать;
				ГруппаОсновное = ПользовательскиеНастройки.Строки.Найти("Основное", "Группа");
				НалогообложениеНовоеПоле = ГруппаОсновное.Строки.Найти("Налогообложение", "Реквизит");
				Если НалогообложениеНовоеПоле <> Неопределено Тогда
					НалогообложениеНовоеПоле.Печатать = Печатать;
				Иначе
					ДобавитьСтрокуНастроек(ГруппаОсновное, "Налогообложение", "Налогообложение", , Печатать);
				КонецЕсли;
			КонецЕсли;
			
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(СтрокаКлючей.КлючОбъектаНастроек,
					СтрокаКлючей.КлючНастроек,
					ПользовательскиеНастройки,
					,
					СтрокаКлючей.ИмяПользователя);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РеквизитыОрганизации(НаборНастроекИСведений, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.Организации.ПФ_MXL_Реквизиты");
	
	ПервыйДокумент = Истина;
	
	Для Каждого ЭлементНабора Из НаборНастроекИСведений Цикл
	
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		РеквизитыОрганизации = РеквизитыОрганизацииПоНастройкамИСведениям(ЭлементНабора.Настройки,
			ЭлементНабора.Сведения,
			Макет);
		
		ТабличныйДокумент.Вывести(РеквизитыОрганизации);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			ЭлементНабора.Организация);
		
	КонецЦикла;
	
	ТабличныйДокумент.ПолеСверху = 20;
	ТабличныйДокумент.ПолеСнизу = 20;
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСправа = 20;
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Организация_КарточкаОрганизации";
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция РеквизитыОрганизацииПоНастройкамИСведениям(НастройкиРеквизитов, Сведения, Макет)
	
	РеквизитыОрганизации = Новый ТабличныйДокумент;
	
	Если Не ЗначениеЗаполнено(НастройкиРеквизитов) Или Не ЗначениеЗаполнено(Сведения) Тогда
		Возврат РеквизитыОрганизации;
	КонецЕсли;
	
	Для Каждого СтрокаГруппы Из НастройкиРеквизитов.Строки Цикл
		
		Если Не СтрокаГруппы.Печатать Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаГруппы.Группа = "Заголовок" Тогда
			Заголовок = СформироватьЗаголовок(СтрокаГруппы, Макет, Сведения);
			РеквизитыОрганизации.Вывести(Заголовок);
		КонецЕсли;
		
		НаименованиеГруппы = Новый ТабличныйДокумент;
		Если ВыводитьНаименованиеГруппы(СтрокаГруппы) Тогда
			НаименованиеГруппы = СформироватьНаименованиеГруппы(СтрокаГруппы, Макет);
		КонецЕсли;
		
		Если ЭтоГруппаРеквизитов(СтрокаГруппы) Тогда
			
			Реквизиты = СформироватьРеквизиты(СтрокаГруппы, Макет, Сведения);
			Если Реквизиты.ВысотаТаблицы > 0 Тогда
				РеквизитыОрганизации.Вывести(НаименованиеГруппы);
				РеквизитыОрганизации.Вывести(Реквизиты);
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтрокаГруппы.Группа = "ПечатьИПодпись" Тогда
			ПечатьИПодпись = СформироватьПечатьИПодпись(СтрокаГруппы, Макет, Сведения);
			РеквизитыОрганизации.Вывести(ПечатьИПодпись);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РеквизитыОрганизации;
	
КонецФункции

Функция ВыводитьНаименованиеГруппы(СтрокаГруппы)
	
	ИменаГрупп = Новый Массив;
	ИменаГрупп.Добавить("РасчетныйСчет");
	ИменаГрупп.Добавить("Контакты");
	ИменаГрупп.Добавить("Дополнительно");
	ИменаГрупп.Добавить("ИдентификаторыЭДО");
	
	Возврат ИменаГрупп.Найти(СтрокаГруппы.Группа) <> Неопределено;
	
КонецФункции

Функция ЭтоГруппаРеквизитов(СтрокаГруппы)
	
	ИменаГрупп = Новый Массив;
	ИменаГрупп.Добавить("Основное");
	ИменаГрупп.Добавить("РасчетныйСчет");
	ИменаГрупп.Добавить("Контакты");
	ИменаГрупп.Добавить("Дополнительно");
	ИменаГрупп.Добавить("ИдентификаторыЭДО");
	
	Возврат ИменаГрупп.Найти(СтрокаГруппы.Группа) <> Неопределено;
	
КонецФункции

Функция СтандартныеНастройкиПечати(Организация)
	
	Результат = НовоеДеревоНастроекПечати();
	
	ВидОрганизации = ВидОрганизации(Организация);
	
	Самозанятый = ВидОрганизацииСамозанятый();
	ИП = ВидОрганизацииИндивидуальныйПредприниматель();
	ЮрЛицо = ВидОрганизацииЮридическоеЛицо();
	ОбособленноеПодразделение = ВидОрганизацииОбособленноеПодразделение();
	
	ГруппаЗаголовок = ДобавитьГруппуНастроек(Результат, "Заголовок", "Заголовок", Истина);
	ДобавитьСтрокуНастроек(ГруппаЗаголовок, "Дата", "Дата", , Истина);
	ДобавитьСтрокуНастроек(ГруппаЗаголовок, "Логотип", "Логотип", , Истина);
	ДобавитьСтрокуНастроек(ГруппаЗаголовок, "Наименование", "НаименованиеДляПечатныхФорм", , Истина);
	
	ГруппаОсновное = ДобавитьГруппуНастроек(Результат, "Основное", "Основное", Истина);
	Если ВидОрганизации = Самозанятый Тогда
		ДобавитьСтрокуНастроек(ГруппаОсновное, "ФИО", "ПолноеНаименование", , Истина);
	Иначе
		ДобавитьСтрокуНастроек(ГруппаОсновное, "Полное наименование", "ПолноеНаименование", , Истина);
		ДобавитьСтрокуНастроек(ГруппаОсновное, "Сокращенное наименование", "СокращенноеНаименование", , Истина);
	КонецЕсли;
	ДобавитьСтрокуНастроек(ГруппаОсновное, "ИНН", "ИНН", , Истина);
	Если ВидОрганизации = ЮрЛицо Или ВидОрганизации = ОбособленноеПодразделение Тогда
		ДобавитьСтрокуНастроек(ГруппаОсновное, "КПП", "КПП", , Истина);
		ДобавитьСтрокуНастроек(ГруппаОсновное, "ОГРН", "ОГРН", , Истина);
		ДобавитьСтрокуНастроек(ГруппаОсновное, "Юридический адрес", "ЮридическийАдрес", , Истина);
		ДобавитьСтрокуНастроек(ГруппаОсновное, "Руководитель", "РуководительДанные", , Истина);
		ДобавитьСтрокуНастроек(ГруппаОсновное, "Главный бухгалтер", "ГлавныйБухгалтерДанные", , Истина);
	КонецЕсли;
	Если ВидОрганизации = ИП Тогда
		ДобавитьСтрокуНастроек(ГруппаОсновное, "ОГРНИП", "ОГРН", , Истина);
		ДобавитьСтрокуНастроек(ГруппаОсновное, "Свидетельство", "Свидетельство", , Истина);
	КонецЕсли;
	Если ВидОрганизации = ИП Или ВидОрганизации = Самозанятый Тогда
		ДобавитьСтрокуНастроек(ГруппаОсновное, "Адрес", "ЮридическийАдрес", , Истина);
	КонецЕсли;
	Если ВидОрганизации = ОбособленноеПодразделение Тогда
		ДобавитьСтрокуНастроек(ГруппаОсновное, "Головная организация", "ГоловнаяОрганизация", , Истина);
	КонецЕсли;
	ДобавитьСтрокуНастроек(ГруппаОсновное, "Налогообложение", "Налогообложение", , Истина);
	
	ГруппаРасчетныйСчет = ДобавитьГруппуНастроек(Результат, "РасчетныйСчет", "Расчетный счет", Истина);
	ДобавитьСтрокуНастроек(ГруппаРасчетныйСчет, "Номер счета", "НомерСчета", , Истина);
	ДобавитьСтрокуНастроек(ГруппаРасчетныйСчет, "Банк", "Банк", , Истина);
	ДобавитьСтрокуНастроек(ГруппаРасчетныйСчет, "БИК", "БИК", , Истина);
	ДобавитьСтрокуНастроек(ГруппаРасчетныйСчет, "Корр. счет", "КоррСчет", , Истина);
	
	ГруппаКонтакты = ДобавитьГруппуНастроек(Результат, "Контакты", "Контакты", Истина);
	ДобавитьСтрокуНастроек(ГруппаКонтакты, "Телефон", "Телефоны", , Истина);
	ДобавитьСтрокуНастроек(ГруппаКонтакты, "Электронная почта", "Email", , Истина);
	Если ВидОрганизации = ЮрЛицо Или ВидОрганизации = ОбособленноеПодразделение Тогда
		ДобавитьСтрокуНастроек(ГруппаКонтакты, "Фактический адрес", "ФактическийАдрес");
	КонецЕсли;
	ДобавитьСтрокуНастроек(ГруппаКонтакты, "Почтовый адрес", "ПочтовыйАдрес");
	
	ГруппаИдентификаторыЭДО = ДобавитьГруппуНастроек(Результат, "ИдентификаторыЭДО", "Идентификаторы ЭДО");
	ДанныеУчетныхЗаписейЭДО = ОбменСКонтрагентами.ДанныеУчетныхЗаписейЭДООрганизации(Организация);
	Для Каждого ДанныеЭДО Из ДанныеУчетныхЗаписейЭДО Цикл
		Представление = ДанныеЭДО.НаименованиеУчетнойЗаписи;
		Реквизит = ДанныеЭДО.ИдентификаторЭДО;
		ДобавитьСтрокуНастроек(ГруппаИдентификаторыЭДО, Представление, Реквизит, "ИдентификаторыЭДО");
	КонецЦикла;
	
	ГруппаДополнительно = ДобавитьГруппуНастроек(Результат, "Дополнительно", "Дополнительно");
	ДобавитьСтрокуНастроек(ГруппаДополнительно, "ОКТМО", "ОКТМО");
	Если Не ВидОрганизации = Самозанятый Тогда
		ДобавитьСтрокуНастроек(ГруппаДополнительно, "ОКВЭД", "ОКВЭД");
		ДобавитьСтрокуНастроек(ГруппаДополнительно, "ОКПО", "ОКПО");
		ДобавитьСтрокуНастроек(ГруппаДополнительно, "Дата регистрации", "ДатаРегистрации");
	КонецЕсли;
	
	ДопРеквизитыИСведения = УправлениеСвойствами.СвойстваОбъекта(Справочники.Организации.ПустаяСсылка());
	Для Каждого Свойство Из ДопРеквизитыИСведения Цикл
		ДобавитьСтрокуНастроек(ГруппаДополнительно, Свойство.Наименование, Свойство.Имя, "ДопРеквизитыИСведения");
	КонецЦикла;
	
	ДобавитьГруппуНастроек(Результат, "ПечатьИПодпись", "Печать и подпись");
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьГруппуНастроек(Дерево, Группа, Представление, Печатать = Ложь)
	
	НоваяГруппа = Дерево.Строки.Добавить();
	НоваяГруппа.Группа = Группа;
	НоваяГруппа.Представление = Представление;
	НоваяГруппа.Печатать = Печатать;
	
	Возврат НоваяГруппа;
	
КонецФункции

Функция ДобавитьСтрокуНастроек(СтрокаГруппы, Представление, Реквизит = "", ИмяНабораСведений = "", Печатать = Ложь)
	
	НоваяСтрока = СтрокаГруппы.Строки.Добавить();
	НоваяСтрока.Группа = СтрокаГруппы.Группа;
	НоваяСтрока.Представление = Представление;
	НоваяСтрока.Реквизит = Реквизит;
	НоваяСтрока.ИмяНабораСведений = ИмяНабораСведений;
	НоваяСтрока.Печатать = Печатать;
	
	Возврат НоваяСтрока;
	
КонецФункции

Процедура АктуализироватьПользовательскиеНастройкиПечати(ПользовательскиеНастройки, СтандартныеНастройки)
	
	// Актуализируем пользовательские настройки по стандартным, т.к. с момента сохранения могла поменяться логика вывода
	
	// Удаляем из пользовательских настроек строки, которых больше нет в стандартных настройках
	// и обновляем представления пользовательских настроек по стандартным настройкам
	НеактуальныеГруппы = Новый Массив;
	ПараметрыОтбора = Новый Структура("Реквизит, ИмяНабораСведений");
	Для Каждого ГруппаПользовательских Из ПользовательскиеНастройки.Строки Цикл
		НеактуальныеСтроки = Новый Массив;
		ГруппаСтандартных = СтандартныеНастройки.Строки.Найти(ГруппаПользовательских.Группа, "Группа");
		Если ГруппаСтандартных = Неопределено Тогда
			НеактуальныеГруппы.Добавить(ГруппаПользовательских);
			Продолжить;
		Иначе
			ГруппаПользовательских.Представление = ГруппаСтандартных.Представление;
		КонецЕсли;
		Для Каждого СтрокаПользовательских Из ГруппаПользовательских.Строки Цикл
			ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаПользовательских);
			СтрокиСтандартных = ГруппаСтандартных.Строки.НайтиСтроки(ПараметрыОтбора);
			Если СтрокиСтандартных.Количество() Тогда
				СтрокаПользовательских.Представление = СтрокиСтандартных[0].Представление;
			Иначе
				НеактуальныеСтроки.Добавить(СтрокаПользовательских);
			КонецЕсли;
		КонецЦикла;
		Для Каждого НеактуальнаяСтрока Из НеактуальныеСтроки Цикл
			ГруппаПользовательских.Строки.Удалить(НеактуальнаяСтрока);
		КонецЦикла;
	КонецЦикла;
	Для Каждого НеактуальнаяГруппа Из НеактуальныеГруппы Цикл
		ПользовательскиеНастройки.Строки.Удалить(НеактуальнаяГруппа);
	КонецЦикла;
	
	// добавляем новые строки
	Для Каждого ГруппаСтандартных Из СтандартныеНастройки.Строки Цикл
		ГруппаПользовательских = ПользовательскиеНастройки.Строки.Найти(ГруппаСтандартных.Группа, "Группа");
		Если ГруппаПользовательских = Неопределено Тогда
			ИндексСтандартных = СтандартныеНастройки.Строки.Индекс(ГруппаСтандартных);
			ГруппаПользовательских = ПользовательскиеНастройки.Строки.Вставить(ИндексСтандартных);
			ЗаполнитьЗначенияСвойств(ГруппаПользовательских, ГруппаСтандартных);
		КонецЕсли;
		Для Каждого СтрокаСтандартных Из ГруппаСтандартных.Строки Цикл
			СтрокаПользовательских = ГруппаПользовательских.Строки.Найти(СтрокаСтандартных.Реквизит, "Реквизит");
			Если СтрокаПользовательских = Неопределено Тогда
				СтрокаПользовательских = ГруппаПользовательских.Строки.Вставить(ГруппаСтандартных.Строки.Индекс(СтрокаСтандартных));
				ЗаполнитьЗначенияСвойств(СтрокаПользовательских, СтрокаСтандартных);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ВидОрганизации(Организация)
	
	Результат = Неопределено;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация,
		"ЮридическоеФизическоеЛицо, ОбособленноеПодразделение, СтатусФизическогоЛица");
	ЭтоФизЛицо = (ЗначенияРеквизитов.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	ЭтоСамозанятый = ЗначенияРеквизитов.СтатусФизическогоЛица = Перечисления.СтатусыФизическихЛиц.Самозанятый;
	ЭтоОбособленноеПодразделение = Не ЭтоФизЛицо И ЗначенияРеквизитов.ОбособленноеПодразделение;
	
	Если ЭтоФизЛицо Тогда
		Если ЭтоСамозанятый Тогда
			Результат = ВидОрганизацииСамозанятый();
		Иначе
			Результат = ВидОрганизацииИндивидуальныйПредприниматель();
		КонецЕсли;
	Иначе
		Если ЭтоОбособленноеПодразделение Тогда
			Результат = ВидОрганизацииОбособленноеПодразделение();
		Иначе
			Результат = ВидОрганизацииЮридическоеЛицо();
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВидОрганизацииСамозанятый()
	Возврат "Самозанятый";
КонецФункции

Функция ВидОрганизацииИндивидуальныйПредприниматель()
	Возврат "ИндивидуальныйПредприниматель";
КонецФункции

Функция ВидОрганизацииОбособленноеПодразделение()
	Возврат "ОбособленноеПодразделение";
КонецФункции

Функция ВидОрганизацииЮридическоеЛицо()
	Возврат "ЮридическоеЛицо";
КонецФункции

Функция ОбластьМакетаСуществует(Макет, ИмяОбласти)
	
	Область = Макет.Области.Найти(ИмяОбласти);
	Возврат Область <> Неопределено;
	
КонецФункции

Процедура ВывестиОтступ(ТабличныйДокумент, Макет)
	
	ИмяОбластиМакета = "Отступ";
	
	Если ОбластьМакетаСуществует(Макет, ИмяОбластиМакета) Тогда
		ОбластьОтступ = Макет.ПолучитьОбласть(ИмяОбластиМакета);
		ТабличныйДокумент.Вывести(ОбластьОтступ);
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьЗаголовок(СтрокаЗаголовок, Макет, СведенияОбОрганизации)
	
	Результат = Новый ТабличныйДокумент;
	
	ВыводитьДату = Ложь;
	ВыводитьЛоготип = Ложь;
	ВыводитьНаименование = Ложь;
	Для Каждого РеквизитЗаголовка Из СтрокаЗаголовок.Строки Цикл
		Если РеквизитЗаголовка.Печатать Тогда
			Если РеквизитЗаголовка.Реквизит = "Дата" И ЗначениеЗаполнено(СведенияОбОрганизации.Дата) Тогда
				ВыводитьДату = Истина;
			КонецЕсли;
			Если РеквизитЗаголовка.Реквизит = "Логотип" И ЗначениеЗаполнено(СведенияОбОрганизации.Логотип) Тогда
				ВыводитьЛоготип = Истина;
			КонецЕсли;
			Если РеквизитЗаголовка.Реквизит = "НаименованиеДляПечатныхФорм"
				И ЗначениеЗаполнено(СведенияОбОрганизации.НаименованиеДляПечатныхФорм) Тогда
				ВыводитьНаименование = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ВыводитьДату И ОбластьМакетаСуществует(Макет, "Дата") Тогда
		ОбластьДата = Макет.ПолучитьОбласть("Дата");
		ОбластьДата.Параметры.Дата = Формат(СведенияОбОрганизации.Дата, "ДЛФ=DD");
		Результат.Вывести(ОбластьДата);
	КонецЕсли;
	Если ВыводитьЛоготип И ВыводитьНаименование И ОбластьМакетаСуществует(Макет, "ЗаголовокЛоготипНаименование") Тогда
		ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокЛоготипНаименование");
		ОбластьЗаголовок.Рисунки.Логотип.Картинка = Новый Картинка(СведенияОбОрганизации.Логотип);
	ИначеЕсли ВыводитьНаименование И ОбластьМакетаСуществует(Макет, "ЗаголовокНаименование") Тогда
		ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокНаименование");
	ИначеЕсли ВыводитьЛоготип И ОбластьМакетаСуществует(Макет, "ЗаголовокЛоготип") Тогда
		ОбластьЗаголовок = Макет.ПолучитьОбласть("ЗаголовокЛоготип");
		ОбластьЗаголовок.Рисунки.Логотип2.Картинка = Новый Картинка(СведенияОбОрганизации.Логотип);
	Иначе
		ОбластьЗаголовок = Неопределено;
	КонецЕсли;
	
	Если ОбластьЗаголовок <> Неопределено Тогда
		ОбластьЗаголовок.Параметры.Заполнить(СведенияОбОрганизации);
		УбратьЛинииУРисунков(ОбластьЗаголовок);
		Результат.Вывести(ОбластьЗаголовок);
		ВывестиОтступ(Результат, Макет);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьНаименованиеГруппы(СтрокаГруппы, Макет)
	
	Результат = Новый ТабличныйДокумент;
	ИмяОбластиМакета = "Группа";
	
	Если Не ОбластьМакетаСуществует(Макет, ИмяОбластиМакета) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОбластьГруппа = Макет.ПолучитьОбласть(ИмяОбластиМакета);
	ОбластьГруппа.Параметры.Заполнить(СтрокаГруппы);
	Результат.Вывести(ОбластьГруппа);
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьРеквизиты(СтрокаГруппы, Макет, СведенияОбОрганизации)
	
	Результат = Новый ТабличныйДокумент;
	ИмяОбластиМакета = "Реквизит";
	
	Если Не ОбластьМакетаСуществует(Макет, ИмяОбластиМакета) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОбластьРеквизит = Макет.ПолучитьОбласть(ИмяОбластиМакета);
	ЕстьСтроки = Ложь;
	Для Каждого СтрокаРеквизита Из СтрокаГруппы.Строки Цикл
		ДанныеРеквизита = ДанныеРеквизитаПоСведениямИСтрокеНастроек(СведенияОбОрганизации, СтрокаРеквизита);
		Если ДанныеРеквизита <> Неопределено Тогда
			ОбластьРеквизит.Параметры.Заполнить(ДанныеРеквизита);
			Результат.Вывести(ОбластьРеквизит);
			ЕстьСтроки = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьСтроки Тогда
		ВывестиОтступ(Результат, Макет);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеРеквизитаПоСведениямИСтрокеНастроек(СведенияОбОрганизации, СтрокаНастроек)
	
	ДанныеРеквизита = Неопределено;
	
	Если Не СтрокаНастроек.Печатать Тогда
		Возврат ДанныеРеквизита;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаНастроек.ИмяНабораСведений) Тогда
		НаборСведений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации,
			СтрокаНастроек.ИмяНабораСведений,
			Новый Соответствие());
		ЗначениеРеквизита = НаборСведений.Получить(СтрокаНастроек.Реквизит);
	Иначе
		СведенияОбОрганизации.Свойство(СтрокаНастроек.Реквизит, ЗначениеРеквизита);
	КонецЕсли;
	Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		ДанныеРеквизита = РеквизитСтруктурой(СтрокаНастроек.Представление, ЗначениеРеквизита);
		Если ТипЗнч(ЗначениеРеквизита) = Тип("Структура") Тогда
			ДанныеРеквизита = РеквизитСтруктурой(ЗначениеРеквизита.НаименованиеРеквизита, ЗначениеРеквизита.ЗначениеРеквизита);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеРеквизита;
	
КонецФункции

Функция СформироватьПечатьИПодпись(СтрокаПечатьИПодпись, Макет, СведенияОбОрганизации)
	
	Результат = Новый ТабличныйДокумент;
	ИмяОбластиМакета = "ПечатьИПодпись";
	
	Если Не ОбластьМакетаСуществует(Макет, ИмяОбластиМакета) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОбластьПечатьИПодпись = Макет.ПолучитьОбласть(ИмяОбластиМакета);
	ОбластьПечатьИПодпись.Рисунки.Печать.Картинка = Новый Картинка(СведенияОбОрганизации.Печать);
	ОбластьПечатьИПодпись.Рисунки.Подпись.Картинка = Новый Картинка(СведенияОбОрганизации.Подпись);
	ОбластьПечатьИПодпись.Параметры.Заполнить(СведенияОбОрганизации);
	УбратьЛинииУРисунков(ОбластьПечатьИПодпись);
	Результат.Вывести(ОбластьПечатьИПодпись);
	ВывестиОтступ(Результат, Макет);
	
	Возврат Результат;
	
КонецФункции

Процедура УбратьЛинииУРисунков(Область)
	
	ЛинияСТипомНетЛинии = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
	Для Каждого Рисунок Из Область.Рисунки Цикл
		Рисунок.Линия = ЛинияСТипомНетЛинии;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает структуру с данными реквизита
//
// Параметры:
//  НаименованиеРеквизита - Строка
//  ЗначениеРеквизита - Произвольный
//
// Возвращаемое значение:
//   Структура
//      * НаименованиеРеквизита
//      * ЗначениеРеквизита
//
Функция РеквизитСтруктурой(НаименованиеРеквизита, ЗначениеРеквизита)
	
	Результат = Новый Структура;
	Результат.Вставить("НаименованиеРеквизита", НаименованиеРеквизита);
	Результат.Вставить("ЗначениеРеквизита", ЗначениеРеквизита);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйЭлементНабораНастроекИСведений(Организация, Настройки, Сведения)
	
	ЭлементНабора = Новый Структура("Организация, Настройки, Сведения");
	ЭлементНабора.Организация = Организация;
	ЭлементНабора.Настройки = Настройки;
	ЭлементНабора.Сведения = Сведения;
	
	Возврат ЭлементНабора;
	
КонецФункции

Функция НовыйСведенияОбОрганизации()
	
	СведенияОбОрганизации = Новый Структура;
	СведенияОбОрганизации.Вставить("Дата", Дата(1, 1, 1));
	СведенияОбОрганизации.Вставить("Логотип", Неопределено); // Двоичные данные файла с логотипом
	СведенияОбОрганизации.Вставить("НаименованиеДляПечатныхФорм", "");
	СведенияОбОрганизации.Вставить("ИндивидуальныйПредприниматель", Справочники.ФизическиеЛица.ПустаяСсылка());
	СведенияОбОрганизации.Вставить("ПолноеНаименование", "");
	СведенияОбОрганизации.Вставить("СокращенноеНаименование", "");
	СведенияОбОрганизации.Вставить("ИНН", "");
	СведенияОбОрганизации.Вставить("КПП", "");
	СведенияОбОрганизации.Вставить("ОГРН", "");
	СведенияОбОрганизации.Вставить("ЮридическийАдрес", "");
	
	// см. РеквизитСтруктурой, НаименованиеРеквизита - Должность руководителя, ЗначениеРеквизита - ФИО руководителя
	СведенияОбОрганизации.Вставить("РуководительДанные", Новый Структура);
	СведенияОбОрганизации.Вставить("РуководительФамилияИО", "");
	СведенияОбОрганизации.Вставить("Руководитель", Справочники.ФизическиеЛица.ПустаяСсылка());
	
	// см. РеквизитСтруктурой, НаименованиеРеквизита - Должность главбуха, ЗначениеРеквизита - ФИО главбуха
	СведенияОбОрганизации.Вставить("ГлавныйБухгалтерДанные", Новый Структура);
	СведенияОбОрганизации.Вставить("ГлавныйБухгалтерФамилияИО", "");
	СведенияОбОрганизации.Вставить("Свидетельство", "");
	СведенияОбОрганизации.Вставить("ГоловнаяОрганизация", ""); // Наименование головной организации
	СведенияОбОрганизации.Вставить("НомерСчета", "");
	СведенияОбОрганизации.Вставить("Банк", "");
	СведенияОбОрганизации.Вставить("БИК", "");
	СведенияОбОрганизации.Вставить("КоррСчет", "");
	СведенияОбОрганизации.Вставить("Телефоны", "");
	СведенияОбОрганизации.Вставить("Email", "");
	СведенияОбОрганизации.Вставить("ФактическийАдрес", "");
	СведенияОбОрганизации.Вставить("ПочтовыйАдрес", "");
	СведенияОбОрганизации.Вставить("Налогообложение", "");
	СведенияОбОрганизации.Вставить("ОКТМО", "");
	СведенияОбОрганизации.Вставить("ОКВЭД", "");
	СведенияОбОрганизации.Вставить("ОКПО", "");
	СведенияОбОрганизации.Вставить("ДатаРегистрации", "");
	СведенияОбОрганизации.Вставить("Печать", Неопределено); // Двоичные данные файла с печатью
	СведенияОбОрганизации.Вставить("Подпись", Неопределено); // Двоичные данные файла с подписью
		
	// Дополнительные реквизиты и сведения, Ключ - имя свойства, Значение - представление
	СведенияОбОрганизации.Вставить("ДопРеквизитыИСведения", Новый Соответствие);
	
	// Массив структур, см. РеквизитСтруктурой, НаименованиеРеквизита - Представление оператора, ЗначениеРеквизита - ИД ЭДО
	СведенияОбОрганизации.Вставить("ИдентификаторыЭДО", Новый Массив);
	
	Возврат СведенияОбОрганизации;
	
КонецФункции

Функция КлючОбъектаНастроек()

	Возврат "НастройкиПечатиРеквизитовОрганизации";

КонецФункции

Функция ТаблицаКлючейНастроекВсехПользователей()
	
	ТаблицаКлючейНастроек = Новый ТаблицаЗначений;
	ОписаниеТипаСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(150);
	ТаблицаКлючейНастроек.Колонки.Добавить("ИмяПользователя", ОписаниеТипаСтрока);
	ТаблицаКлючейНастроек.Колонки.Добавить("КлючОбъектаНастроек", ОписаниеТипаСтрока);
	ТаблицаКлючейНастроек.Колонки.Добавить("КлючНастроек", ОписаниеТипаСтрока);
	
	ИменаПользователей = Новый Массив;
	СписокПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Если СписокПользователей.Количество() Тогда
		Для Каждого ПользовательИБ Из СписокПользователей Цикл
			ИменаПользователей.Добавить(ПользовательИБ.Имя);
		КонецЦикла;
	Иначе
		ИменаПользователей.Добавить("");
	КонецЕсли;
	
	КлючОбъектаНастроек = КлючОбъектаНастроек();
	
	Для Каждого ИмяПользователя Из ИменаПользователей Цикл
		СписокНастроек = ХранилищеОбщихНастроек.ПолучитьСписок(КлючОбъектаНастроек, ИмяПользователя);
		Для Каждого Настройка Из СписокНастроек Цикл
			КлючНастроек = Настройка.Значение;
			СтрокаКлючей = ТаблицаКлючейНастроек.Добавить();
			СтрокаКлючей.ИмяПользователя = ИмяПользователя;
			СтрокаКлючей.КлючОбъектаНастроек = КлючОбъектаНастроек;
			СтрокаКлючей.КлючНастроек = КлючНастроек;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаКлючейНастроек;
	
КонецФункции

#КонецОбласти
