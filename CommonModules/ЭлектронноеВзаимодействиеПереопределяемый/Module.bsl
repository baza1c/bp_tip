

////////////////////////////////////////////////////////////////////////////////
// ЭлектронноеВзаимодействиеПереопределяемый: общий механизм обмена электронными документами.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет соответствие функциональных опций библиотеки и прикладного решения,
// в случае различий в наименовании.
//
// Параметры:
//  СоответствиеФО - Соответствие - список функциональных опций. Возможные ключи:
//   - БазоваяВерсия
//   - ИспользоватьПартнеровИКонтрагентов
//
Процедура ПолучитьСоответствиеФункциональныхОпций(СоответствиеФО) Экспорт
	
	ЭлектронноеВзаимодействиеБЗК.ПолучитьСоответствиеФункциональныхОпций(СоответствиеФО);
	
КонецПроцедуры

// Определяет соответствие справочников библиотеки и прикладного решения.
//
// Параметры:
//  СоответствиеСправочников - Соответствие - список справочников. Возможные ключи:
//   - Организации
//   - Контрагенты
//   - ДоговорыКонтрагентов
//   - Номенклатура
//   - ЕдиницыИзмерения
//   - Валюты
//   - Банки
//   - БанковскиеСчетаОрганизаций
//   - БанковскиеСчетаКонтрагентов
//   - УпаковкиНоменклатуры
//   - ФизическиеЛица
//   - Партнеры
//   - ХарактеристикиНоменклатуры
//   - ГосударственныеКонтрактыЕИС
//
Процедура ПолучитьСоответствиеСправочников(СоответствиеСправочников) Экспорт
		
	СоответствиеСправочников.Вставить("Организации", "Организации");
	СоответствиеСправочников.Вставить("Контрагенты", "Контрагенты");
	СоответствиеСправочников.Вставить("Партнеры",    "");
	СоответствиеСправочников.Вставить("ДоговорыКонтрагентов", "ДоговорыКонтрагентов");
	СоответствиеСправочников.Вставить("Номенклатура", "Номенклатура");
	СоответствиеСправочников.Вставить("ЕдиницыИзмерения", "КлассификаторЕдиницИзмерения");
	СоответствиеСправочников.Вставить("УпаковкиНоменклатуры", "КлассификаторЕдиницИзмерения");
	СоответствиеСправочников.Вставить("НоменклатураПоставщиков", "НоменклатураПоставщиков");
	СоответствиеСправочников.Вставить("Валюты", "Валюты");
	СоответствиеСправочников.Вставить("Банки", "Банки");
	СоответствиеСправочников.Вставить("БанковскийСчет", "БанковскиеСчета");
	СоответствиеСправочников.Вставить("БанковскиеСчетаОрганизаций", "БанковскиеСчета");
	СоответствиеСправочников.Вставить("БанковскиеСчетаКонтрагентов", "БанковскиеСчета");
	СоответствиеСправочников.Вставить("БанковскиеСчетаКонтрагентов", "БанковскиеСчета");
	
	ЭлектронноеАктированиеБП.ПолучитьСоответствиеСправочников(СоответствиеСправочников);
	
	ЭлектронноеВзаимодействиеБЗК.ПолучитьСоответствиеСправочников(СоответствиеСправочников);
	
КонецПроцедуры

// В процедуре формируется соответствие для сопоставления имен переменных библиотеки,
// наименованиям объектов и реквизитов метаданных прикладного решения.
// Если в прикладном решении есть документы, на основании которых формируется ЭД,
// причем названия реквизитов данных документов отличаются от общепринятых "Организация", "Контрагент", "СуммаДокумента", "Номер", "Дата",
// то для этих реквизитов необходимо добавить в соответствие записи виде:
// Ключ = "ДокументВМетаданных.ОбщепринятоеНазваниеРеквизита", Значение - "ДокументВМетаданных.ДругоеНазваниеРеквизита".
// Например:
//  СоответствиеРеквизитовОбъекта.Вставить("МЗ_Покупка.Организация", "МЗ_Покупка.Учреждение");
//  СоответствиеРеквизитовОбъекта.Вставить("МЗ_Покупка.Контрагент",  "МЗ_Покупка.Грузоотправитель");
//  СоответствиеРеквизитовОбъекта.Вставить("СчетФактураВыданный.СуммаДокумента",  "СчетФактураВыданный.Основание.СуммаДокумента");
// 
// Для подсистемы БизнесСеть обязательно определение следующих полей:
//   "ИННКонтрагента"
//   "КППКонтрагента"
//   "НаименованиеКонтрагента"
//   "НаименованиеОрганизации"
//   "ИННОрганизации"
//   "КППОрганизации"
//   "СокращенноеНаименованиеОрганизации"
// Для подсистемы ОбменСКонтрагентами обязательно определение следующих полей: 
//   "НаименованиеКонтрагентаДляСообщенияПользователю"
//   "НаименованиеКонтрагента"
//   "ВнешнийКодКонтрагента"
//   "ВладелецДоговораКонтрагента"
//   "ПартнерКонтрагента"
//   "ИННКонтрагента"
//   "КППКонтрагента"
//   "НаименованиеОрганизации"
//   "СокращенноеНаименованиеОрганизации"
//   "ИННОрганизации"
//   "КППОрганизации"
//   "ОГРНОрганизации"
//   "НомерДоговораКонтрагента"
//   "ДатаДоговораКонтрагента"
// Для подсистемы ОбменССайтами обязательно определение следующих полей:
// 	 "ИННОрганизации"
//   "КППОрганизации"
//   "НаименованиеОрганизации"
//   "ПолноеНаименованиеОрганизации"
//   "ЮридическоеФизическоеЛицо"
// Для подсистемы ОбменСБанками требуется определение следующих полей:
//   "ИННОрганизации" (обязательное)
//   "Банк.БИК" (обязательное)
//   "Банк.Наименование" (обязательное)
//   "Банк.Город" (обязательное)
//   "БанковскийСчетОрганизации.Организация" (обязательное, если есть в метаданных)
//   "БанковскийСчетОрганизации.Банк" (обязательное, если есть в метаданных)
//   "БанковскийСчетОрганизации.НомерСчета" (обязательное, если есть в метаданных)
//   "ПлатежноеПоручениеВМетаданных" (необязательное)
//   "БанковскийСчетОрганизации.Закрыт" (необязательное)
//   "СокращенноеНаименованиеОрганизации" (необязательное)
//   "ПлатежноеПоручение.СчетОрганизации" (обязательное для писем)
//   "ПлатежноеПоручение.Организация" (обязательное для писем)
//
// Параметры:
//  СоответствиеРеквизитовОбъекта - Соответствие из КлючИЗначение:
//    * Ключ - Строка - имя переменной, используемой в коде библиотеки;
//    * Значение - Строка - наименование объекта метаданных или реквизита объекта в прикладном решении.
//
Процедура ПолучитьСоответствиеНаименованийОбъектовМДИРеквизитов(СоответствиеРеквизитовОбъекта) Экспорт
	
	// Обмен с контрагентами
	СоответствиеРеквизитовОбъекта.Вставить("ДатаВыставленияВСчетеФактуреВыданном"           , "ДатаВыставления");
	СоответствиеРеквизитовОбъекта.Вставить("ДатаПолученияВСчетеФактуреПолученном"           , "Дата");
	
	СоответствиеРеквизитовОбъекта.Вставить("НомерСчета"                                     , "НомерСчета");
	СоответствиеРеквизитовОбъекта.Вставить("ИННКонтрагента"                                 , "ИНН");
	СоответствиеРеквизитовОбъекта.Вставить("КППКонтрагента"                                 , "КПП");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеКонтрагента"                        , "НаименованиеПолное");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеКонтрагентаДляСообщенияПользователю", "Наименование");
	СоответствиеРеквизитовОбъекта.Вставить("ВнешнийКодКонтрагента"                          , "Код");
	СоответствиеРеквизитовОбъекта.Вставить("ПартнерКонтрагента"                             , Неопределено);
	
	СоответствиеРеквизитовОбъекта.Вставить("ИННОрганизации"                                 , "ИНН");
	СоответствиеРеквизитовОбъекта.Вставить("КППОрганизации"                                 , "КПП");
	СоответствиеРеквизитовОбъекта.Вставить("ВнешнийКодОрганизации"                          , "Код");
	СоответствиеРеквизитовОбъекта.Вставить("НаименованиеОрганизации"                        , "НаименованиеПолное");
	СоответствиеРеквизитовОбъекта.Вставить("ПолноеНаименованиеОрганизации"                  , "НаименованиеПолное");
	СоответствиеРеквизитовОбъекта.Вставить("СокращенноеНаименованиеОрганизации"             , "НаименованиеСокращенное");
	СоответствиеРеквизитовОбъекта.Вставить("ОГРНОрганизации"                                , "ОГРН");
	
	СоответствиеРеквизитовОбъекта.Вставить("РеализацияТоваровУслугВМетаданных"              , "РеализацияТоваровУслуг");
	СоответствиеРеквизитовОбъекта.Вставить("ПоступлениеТоваровУслугВМетаданных"             , "ПоступлениеТоваровУслуг");
	
	СоответствиеРеквизитовОбъекта.Вставить("НомерДоговораКонтрагента"                       , "Номер");
	СоответствиеРеквизитовОбъекта.Вставить("ДатаДоговораКонтрагента"                        , "Дата");
	СоответствиеРеквизитовОбъекта.Вставить("ВладелецДоговораКонтрагента"                    , "Владелец");
	
	// ДиректБанк
	СоответствиеРеквизитовОбъекта.Вставить("ПлатежноеПоручениеВМетаданных"                  , "ПлатежноеПоручение");
	СоответствиеРеквизитовОбъекта.Вставить("ПлатежноеТребованиеВМетаданных"                 , "ПлатежноеТребование");
	СоответствиеРеквизитовОбъекта.Вставить("БанковскийСчетОрганизации"                      , "БанковскиеСчета");
	
	СоответствиеРеквизитовОбъекта.Вставить("Банк.Наименование"                              , "Наименование");
	СоответствиеРеквизитовОбъекта.Вставить("Банк.Город"                                     , "Город");
	СоответствиеРеквизитовОбъекта.Вставить("ПлатежноеПоручение.СчетОрганизации"             , "СчетОрганизации");
	СоответствиеРеквизитовОбъекта.Вставить("ПлатежноеПоручение.Организация"                 , "Организация");
	СоответствиеРеквизитовОбъекта.Вставить("БанковскийСчетОрганизации.НомерСчета"           , "НомерСчета");
	СоответствиеРеквизитовОбъекта.Вставить("БанковскийСчетОрганизации.Банк"                 , "Банк");
	СоответствиеРеквизитовОбъекта.Вставить("Банк.БИК"                                       , "Код");
	СоответствиеРеквизитовОбъекта.Вставить("БанковскийСчетОрганизации.Организация"          , "Владелец");
	
КонецПроцедуры

// Поиск ссылки на объект информационной базы по типу, идентификатору и дополнительным реквизитам.
//
// Параметры:
//  ТипОбъекта - Строка	 - идентификатор типа объекта, который необходимо найти. Поддерживаемые типы:
//    - Контрагенты
//    - Организации
//    - Банки
//    - БанковскиеСчетаОрганизаций
//    - БанковскиеСчетаКонтрагентов
//    - Валюты
//    - ЕдиницыИзмерения
//    - ВидыКонтактнойИнформации
//    - Номенклатура
//    - Партнеры
//    - ДоговорыКонтрагентов
//  Результат - ЛюбаяСсылка - ссылка на найденный объект. Выходной параметр.
//  ИдОбъекта			 - Строка	 - идентификатор объекта заданного типа. Поддерживаемые идентификаторы в разрезе типов:
//    - Контрагенты: отсутствует
//    - Организации: отсутствует
//    - Банки: отсутствует
//    - БанковскиеСчетаОрганизаций: номер счета
//    - БанковскиеСчетаКонтрагентов: номер счета
//    - Валюты: код валюты
//    - ЕдиницыИзмерения: код единицы измерения
//    - ВидыКонтактнойИнформации: "EmailКонтрагента", "ТелефонКонтрагента", "ФаксКонтрагента", "EmailОрганизации", "ТелефонОрганизации", "ФаксОрганизации".
//    - Номенклатура: отсутствует
//    - Партнеры: отсутствуют
//    - ДоговорыКонтрагентов: отсутствует
//  ДополнительныеРеквизиты	 - Структура - набор дополнительных полей объекта для поиска. Поддерживаемые ключи в разрезе типов:
//    - Контрагенты: ИНН, КПП, Наименование, GLN
//    - Организации: ИНН, КПП, Наименование, GLN
//    - Банки: Код (БИК)
//    - БанковскиеСчетаОрганизаций: Владелец (организация)
//    - БанковскиеСчетаКонтрагентов: Владелец (контрагент)
//    - Валюты: отсутствуют
//    - ЕдиницыИзмерения: наименование единицы
//    - ВидыКонтактнойИнформации: отсутствуют
//    - Номенклатура: Идентификатор (идентификатор номенклатуры поставщика), Артикул
//    - Партнеры: Контрагент
//    - ДоговорыКонтрагентов: НомерДоговора, ДатаДоговора, Организация, Владелец (контрагент)
// Возвращаемое значение:
//  Ссылка - ссылка на найденный объект.
//
Процедура НайтиСсылкуНаОбъект(ТипОбъекта, Результат, ИдОбъекта = "", ДополнительныеРеквизиты = Неопределено) Экспорт
	
	Результат = ОбменСКонтрагентамиБП.НайтиСсылкуНаОбъект(ТипОбъекта, ИдОбъекта, ДополнительныеРеквизиты);
	
КонецПроцедуры

// Получает печатный номер документа, служащего основанием для формирования электронных документов.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - ссылка на документ информационной базы.
//  Результат - Строка - номер документа.
//
Процедура ПолучитьПечатныйНомерДокумента(СсылкаНаОбъект, Результат) Экспорт
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ПлатежноеПоручение") Тогда
		Номер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "Номер");
		Результат = УчетДенежныхСредствКлиентСервер.НомерОбъектаБезПрефикса(Номер);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") ИЛИ ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "Номер, ДоговорКонтрагента.ГосударственныйКонтракт.Код");
		
		Результат = 
			?(ЗначениеЗаполнено(РеквизитыОбъекта.ДоговорКонтрагентаГосударственныйКонтрактКод), РеквизитыОбъекта.ДоговорКонтрагентаГосударственныйКонтрактКод+"/", "")
			+ ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыОбъекта.Номер, Истина, Ложь);
		
	Иначе
		Результат = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "Номер"), Истина, Ложь);
	КонецЕсли;

КонецПроцедуры

// Проверяет, готовность документов ИБ для формирования электронных документов, и удаляет из массива неготовые документы.
//
// Параметры:
//  ДокументыМассив - Массив Из ДокументСсылка - ссылки на документы-основания электронных документов.
//  БезЭлектроннойПодписи - Булево - обозначает использование электронной подписи при обмене документами.
//                          Истина - обмен происходит в рамках подсистемы ЭлектронноеВзаимодействие.БизнесСеть
//                          Ложь - обмен происходит в рамках подсистем ЭлектронноеВзаимодействие.ОбменСКонтрагентами,
//                          ЭлектронноеВзаимодействие.ОбменСБанками.
//
Процедура ПроверитьГотовностьИсточников(ДокументыМассив, БезЭлектроннойПодписи = Ложь) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияТипаИзМассива(ДокументыМассив, Тип("СтрокаГруппировкиДинамическогоСписка"));
	
	МассивСчетовФактур        = Новый Массив;
	МассивПлатежныхДокументов = Новый Массив;
	МассивНаУдаление          = Новый Массив;
	Для Каждого Элемент из ДокументыМассив Цикл
		Если ТипЗнч(Элемент) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			МассивСчетовФактур.Добавить(Элемент);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ДокументСсылка.ПлатежноеПоручение")
				ИЛИ ТипЗнч(Элемент) = Тип("ДокументСсылка.ПлатежноеТребование") Тогда
			МассивПлатежныхДокументов.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСчетовФактур.Количество() > 0 Тогда
		Запрос = Новый Запрос();
		Запрос.Параметры.Вставить("СписокСчетовФактур", МассивСчетовФактур);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыданный.Представление КАК Представление,
		|	СчетФактураВыданный.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА СчетФактураВыданный.СчетФактураНеВыставляется
		|			ТОГДА ""Не выставляется""
		|	КОНЕЦ КАК Флаг
		|ИЗ
		|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|ГДЕ
		|	СчетФактураВыданный.СчетФактураНеВыставляется
		|	И СчетФактураВыданный.Ссылка В(&СписокСчетовФактур)";
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		ШаблонСообщения = НСтр("ru = 'У %1 установлен флаг ""%2"". Для таких документов формировать электронный счет-фактуру нельзя.'");
		Пока Выборка.Следующий() Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(ШаблонСообщения, Выборка.Представление, Выборка.Флаг), Выборка.Ссылка);
			МассивНаУдаление.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	// Проверка банковских платежек. Не формируем ЭД для валютных банковских счетов.
	Для Каждого Элемент Из МассивПлатежныхДокументов Цикл
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Элемент, "СчетОрганизации.Валютный") Тогда
			
			ШаблонСообщения = НСтр("ru = 'Электронный документ для документа ""%1"" не сформирован. 1С:ДиректБанк не поддерживает обмен по валютным банковским счетам.
				|Используйте команду ""Отправить в банк"" в списке документов для выгрузки данных в клиент-банк.'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Строка(Элемент));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			МассивНаУдаление.Добавить(Элемент);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Элемент Из МассивНаУдаление Цикл
		
		ЭлементНаУдаление = ДокументыМассив.Найти(Элемент);
		Если ЭлементНаУдаление <> Неопределено Тогда
			
			ДокументыМассив.Удалить(ДокументыМассив.Найти(Элемент));
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЭлектронноеВзаимодействиеБЗК.ПроверитьГотовностьИсточников(ДокументыМассив, БезЭлектроннойПодписи);
	
КонецПроцедуры

// Получает данные о юридическом (физическом) лице по ссылке.
//
// Параметры:
//  ЮрФизЛицо - СправочникСсылка - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - см. ЭлектронноеВзаимодействие.СтруктураДанныхЮрФизЛица
//
Процедура ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, Сведения) Экспорт
	
	СведенияЮрФизЛица = ОбменСКонтрагентамиБП.ПолучитьДанныеЮрФизЛица(ЮрФизЛицо, Неопределено, Неопределено);
	ЗаполнитьЗначенияСвойств(Сведения, СведенияЮрФизЛица);
	Сведения.ЮрФизЛицо = ?(СведенияЮрФизЛица.ЭтоИндивидуальныйПредприниматель, "Индивидуальный предприниматель", СведенияЮрФизЛица.ЮридическоеФизическоеЛицо);
	Сведения.ЮридическийАдресXML = СведенияЮрФизЛица.ЗначенияПолейЮридическийАдрес;
	Сведения.ДатаОГРН = СведенияЮрФизЛица.ДатаРегистрации;
	
КонецПроцедуры

// Получает ссылку на физическое лицо по сведениям.
// 
// Параметры:
//  СведенияФизЛица - структура:
//  	* ИНН - строка
// 
// Возвращаемое значение:
//  - СправочникСсылка.ФизическиеЛица
//  - Неопределено - если не элемент не найден в ИБ.
//
Функция ПолучитьФизЛицоМЧД(СведенияФизЛица) Экспорт
	
	ФизическоеЛицо = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИНН", СведенияФизЛица.ИНН);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизическиеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.ИНН = &ИНН";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ФизическоеЛицо = Выборка.Ссылка;
	КонецЕсли;
	 
	Возврат ФизическоеЛицо;
	
КонецФункции

// Получить типы организации представителя МЧД.
// 
// Параметры:
//  Типы - массив
Процедура ПолучитьТипыОрганизацииПредставителяМЧД(Типы) Экспорт
	
	Типы.Добавить(Тип("СправочникСсылка.Организации"));
	Типы.Добавить(Тип("СправочникСсылка.Контрагенты"));
	
КонецПроцедуры

// Получить ссылку на организацию-представителя МЧД.
// 
// Параметры:
//  СведенияЮрЛица - структура:
// 		* ИНН 	- строка
// 		* КПП 	- строка
// 		* ОГРН 	- строка
// 
// Возвращаемое значение:
// 	СправочникСсылка - ссылка на элемент справочника, Неопределено
//  
Функция ПолучитьОрганизациюМЧД(СведенияЮрЛица) Экспорт
	
	Контрагент = РаботаСКонтрагентамиБП.НайтиКонтрагентаПоИНН_КПП(СведенияЮрЛица);
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Контрагент;
	КонецЕсли;
	
	Организация = Справочники.Организации.НайтиОрганизацию(СведенияЮрЛица.ИНН, СведенияЮрЛица.КПП, Истина);
	Если ЗначениеЗаполнено(Организация) Тогда
		Возврат Организация;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает текстовое описание организации по параметрам.
//
// Параметры:
//  СведенияОрганизации - Структура - См. ПолучитьДанныеЮрФизЛица 
//  Результат           - Строка - описание организации.
//  Список              - Строка - список параметров организации, которые нужно включить в описание. Если пустой, должно 
//                        формироваться наиболее полное представление.
//
Процедура ОписаниеОрганизации(СведенияОрганизации, Результат, Список = "") Экспорт
	
	СписокПолей = ?(Список = "", "ПолноеНаименование,ИНН,Свидетельство,ЮридическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет", Список);
	МассивПолей = СтрРазделить(СписокПолей, ",", Ложь);
	МассивПолейПослеПроверки = Новый Массив;
	Для Каждого ЭлементМассива Из МассивПолей Цикл
		
		Если СведенияОрганизации.Свойство(ЭлементМассива) Тогда
			
			МассивПолейПослеПроверки.Добавить(ЭлементМассива);
			
		КонецЕсли;
		
	КонецЦикла;
	СписокПолей = СтрСоединить(МассивПолейПослеПроверки, ",");
	
	Результат = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОрганизации, СписокПолей);

КонецПроцедуры

// Проверяет наличие прав на открытие журнала регистрации.
//
// Параметры:
//  Результат - Булево - если пользователь имеет право на открытие журнала регистрации,
//                       в этой переменной должна быть установлена Истина.
//
Процедура ЕстьПравоОткрытияЖурналаРегистрации(Результат) Экспорт
	
	Результат = Пользователи.ЭтоПолноправныйПользователь(, , Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа с электронными документами

// Выполняется перед записью учетного объекта - владельца электронного документа, который может служить основанием для 
// исходящего электронного документа в случае, если существует действующая настройка отправки, соответствующая параметрам,
// указанным в объекте учета.
//
// Параметры:
//  Объект - ДокументОбъект - прикладной объект, запись которого инициировала вызов метода. Входной параметр.
//  ИзменилисьКлючевыеРеквизиты - Булево - признак изменения данных, влияющих на формирование электронного документа. Выходной параметр.
//                                         Если Истина, то текущая версия электронного документа становится неактуальной. 
//                                         По умолчанию для нового документа Истина, иначе Ложь.
//                                         Не используется для внутренних электронных документов
//  СостояниеЭлектронногоДокумента - ПеречислениеСсылка - состояние текущей версии электронного документа.
//                                   Входной параметр. Может быть использован для анализа текущего этапа обработки электронного документа. 
//                                   Позволяет описать зависимости заполнения выходных параметров от факта создания, подписания или отправки ЭД контрагенту.
//                                   Не передается для внутренних электронных документов
//  ПодлежитОбменуЭД - Булево - признак участия документа в ЭДО. Выходной параметр. По умолчанию Истина.
//                              При установке в Ложь прикладной объект не будет отображаться как требующий создания электронного документа (например, раздел "Создать" в текущих делах ЭДО). 
//                              Если ЭД уже был создан, то он становиться неактуальным.
//                              Не используется для внутренних электронных документов
//  ТребуетсяКонтрольАктуальности - Булево - Необходимо указать, требуется ли запустить встроенную проверку актуальности 
//                                           сформированных электронных документов. Проверка может быть ресурсозатратной.
//                                           Рекомендуется ее отключать, если проводятся операции, заведомо не приводящие
//                                           к потере актуальности электронных документов. По умолчанию Истина.
//                                           Только для внутренних электронных документов. Выходной параметр.
//  Отказ - Булево - если установить Истина, то владелец электронного документа записан не будет. Выходной параметр. По умолчанию Ложь.
//
// Пример:
//  1. Необходимо сделать существующий ЭД неактуальным, чтобы пользователь создал новый. Для этого:
//   * Присвоить параметру  ИзменилисьКлючевыеРеквизиты значение Истина.
//  2. Необходимо отказать пользователю во внесении изменений в документ, если уже есть существующий ЭД. Для этого:
//   * Проверить параметр СостояниеЭлектронногоДокумента на неравенство значению НеСформирован.
//   * Присвоить параметру  Отказ значение Истина.
//   * (необязательно) Присвоить параметру  ИзменилисьКлючевыеРеквизиты значение Истина. 
//     В этом случае пользователь дополнительно получит сообщение: "Существует электронный документ. Изменение ключевых реквизитов документа запрещено.".
//  3. Необходимо исключить прикладной объект из возможных оснований для ЭД. Например, если известно, что он выставлен в бумажном виде, и ЭД не требуется. 
//     Существующий ЭД сделать неактуальным и не отображать прикладной документ в разделе "Создать" обработки "Текущие дела ЭДО". Для этого:
//   * Присвоить параметру  ПодлежитОбменуЭД значение Ложь.
//
Процедура ПередЗаписьюВладельцаЭлектронногоДокумента(Объект, ИзменилисьКлючевыеРеквизиты, Знач СостояниеЭлектронногоДокумента, 
	ПодлежитОбменуЭД, ТребуетсяКонтрольАктуальности, Отказ) Экспорт
	
	ЭлектронноеВзаимодействиеБП.ПередЗаписьюВладельцаЭлектронногоДокумента(Объект, ИзменилисьКлючевыеРеквизиты, СостояниеЭлектронногоДокумента, ПодлежитОбменуЭД, ТребуетсяКонтрольАктуальности, Отказ);
	
КонецПроцедуры

// Выполняется перед записью учетного объекта (элемента справочника) - владельца электронного документа, который может
// служить основанием для исходящего электронного документа в случае, если существует действующая настройка отправки,
// соответствующая параметрам, указанным в объекте учета.
//
// Параметры:
//  Объект - СправочникОбъект - прикладной объект, запись которого инициировала вызов метода. Входной параметр.
//  ИзменилисьКлючевыеРеквизиты - Булево - Признак изменения данных, влияющих на формирование электронного документа.
//                                         Выходной параметр.
//                                         Если Истина, то текущая версия электронного документа становится
//                                         неактуальной.
//                                         По умолчанию для нового элемента справочника Истина, иначе Ложь.
//                                         Не используется для внутренних электронных документов.
//  СостояниеЭлектронногоДокумента - ПеречислениеСсылка - Состояние текущей версии электронного документа.
//                                   Входной параметр.
//                                   Может быть использован для анализа текущего этапа обработки электронного документа.
//                                   Позволяет описать зависимости заполнения выходных параметров от факта создания,
//                                   подписания или отправки ЭД контрагенту.
//                                   Не передается для внутренних электронных документов
//  ПодлежитОбменуЭД - Булево - Признак участия справочника в ЭДО. Выходной параметр. По умолчанию Истина.
//                              При установке в Ложь прикладной объект не будет отображаться как требующий создания
//                              электронного документа (например, раздел "Создать" в текущих делах ЭДО).
//                              Если ЭД уже был создан, то он становиться неактуальным.
//                              Не используется для внутренних электронных документов
//  ТребуетсяКонтрольАктуальности - Булево - Необходимо указать, требуется ли запустить встроенную проверку актуальности
//                                           сформированных электронных документов. Проверка может быть
//                                           ресурсозатратной.
//                                           Рекомендуется ее отключать, если проводятся операции, заведомо не
//                                           приводящие к потере актуальности электронных документов.
//                                           По умолчанию Истина.
//                                           Только для внутренних электронных документов. Выходной параметр.
//  Отказ - Булево - Если установить Истина, то владелец электронного документа записан не будет. Выходной параметр.
//                   По умолчанию Ложь.
//
// Пример:
//  1. Необходимо сделать существующий ЭД неактуальным, чтобы пользователь создал новый. Для этого:
//   * Присвоить параметру  ИзменилисьКлючевыеРеквизиты значение Истина.
//  2. Необходимо отказать пользователю во внесении изменений в элемент справочника, если уже есть существующий ЭД.
//     Для этого:
//   * Проверить параметр СостояниеЭлектронногоДокумента на неравенство значению НеСформирован.
//   * Присвоить параметру  Отказ значение Истина.
//   * (необязательно) Присвоить параметру  ИзменилисьКлючевыеРеквизиты значение Истина. 
//     В этом случае пользователь дополнительно получит сообщение: "Существует электронный документ. Изменение ключевых
//     реквизитов запрещено.".
//  3. Необходимо исключить прикладной объект из возможных оснований для ЭД. Например, если известно, что он выставлен в
//     бумажном виде, и ЭД не требуется.
//     Существующий ЭД сделать неактуальным и не отображать элемент справочника в разделе "Создать" обработки
//     "Текущие дела ЭДО". Для этого:
//   * Присвоить параметру  ПодлежитОбменуЭД значение Ложь.
//
Процедура ПередЗаписьюВладельцаЭлектронногоДокументаЭлементСправочника(Объект, ИзменилисьКлючевыеРеквизиты,
	Знач СостояниеЭлектронногоДокумента, ПодлежитОбменуЭД, ТребуетсяКонтрольАктуальности, Отказ) Экспорт
	
КонецПроцедуры

// Получает данные о физическом лице по ссылке.
// Для использования в МЧД следует вызывать МашиночитаемыеДоверенностиПереопределяемый.ПриИзмененииДанныеФизЛица.
//
// Параметры:
//  ФизЛицо - СправочникСсылка - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - см. ЭлектронноеВзаимодействие.СтруктураДанныхФизЛица
//
Процедура ПолучитьДанныеФизЛица(ФизЛицо, Сведения, Организация = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ФизЛицо) Тогда
		Возврат
	КонецЕсли;

	Сведения.Ссылка = ФизЛицо;

	СтруктураДанных = УчетЗарплаты.ДанныеФизическихЛиц(Неопределено, ФизЛицо, ТекущаяДатаСеанса());
	СтруктураДанных.Свойство("ДатаРождения",      Сведения.ДатаРождения);
	СтруктураДанных.Свойство("ИНН",               Сведения.ИНН);
	СтруктураДанных.Свойство("СтраховойНомерПФР", Сведения.СтраховойНомерПФР);
	СтруктураДанных.Свойство("Фамилия",           Сведения.Фамилия);
	СтруктураДанных.Свойство("Имя",               Сведения.Имя);
	СтруктураДанных.Свойство("Отчество",          Сведения.Отчество);
	СтруктураДанных.Свойство("Страна",            Сведения.Гражданство);
	СтруктураДанных.Свойство("Пол",               Сведения.Пол);
	СтруктураДанных.Свойство("МестоРождения",     Сведения.МестоРождения);

	ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ФизЛицо,,,Ложь);

	Для Каждого СтрокаТаблицыКИ Из ТаблицаКИ Цикл
		Если СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица Тогда
			Сведения.АдресМестаПроживанияXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.АдресМестаПроживания = СтрокаТаблицыКИ.Представление;
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица Тогда
			Сведения.АдресПоПропискеXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.АдресПоПрописке = СтрокаТаблицыКИ.Представление;
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица
			ИЛИ СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонДомашнийФизическиеЛица Тогда
			Сведения.ТелефоныXML = СтрокаТаблицыКИ.ЗначенияПолей;
			Сведения.Телефоны = СтрокаТаблицыКИ.Представление;
		ИначеЕсли СтрокаТаблицыКИ.Вид = Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица Тогда
			Сведения.ЭлектроннаяПочта = СтрокаТаблицыКИ.Представление;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Область СобытияПодсистемы

// Выполняется при создании формы подсистемы, допускающей изменение.
// Позволяет изменить реквизиты, команды и элементы формы.
// Вызывается для форм со следующим назначением (см. параметр Контекст.Назначение):
// "СопоставлениеНоменклатуры"
// Для добавленных элементов возможно подключение обработчиков событий методом УстановитьДействие.
// Список подключаемых действий в формате <Событие>-<Имя подключаемого метода>-<Имя метода с реализацией>:
// ПриИзменении                  - Подключаемый_ЭлементПриИзменении                  - ЭлементФормыПодсистемыПриИзменении
// НачалоВыбора                  - Подключаемый_ЭлементНачалоВыбора                  - ЭлементФормыПодсистемыНачалоВыбора
// НачалоВыбораИзСписка          - Подключаемый_ЭлементНачалоВыбораИзСписка          - ЭлементФормыПодсистемыНачалоВыбораИзСписка
// Очистка                       - Подключаемый_ЭлементОчистка                       - ЭлементФормыПодсистемыОчистка
// Создание                      - Подключаемый_ЭлементСоздание                      - ЭлементФормыПодсистемыСоздание
// ОбработкаВыбора               - Подключаемый_ЭлементОбработкаВыбора               - ЭлементФормыПодсистемыОбработкаВыбора
// ИзменениеТекстаРедактирования - Подключаемый_ЭлементИзменениеТекстаРедактирования - ЭлементФормыПодсистемыИзменениеТекстаРедактирования
// АвтоПодбор                    - Подключаемый_ЭлементАвтоПодбор                    - ЭлементФормыПодсистемыАвтоПодбор
// ОкончаниеВводаТекста          - Подключаемый_ЭлементОкончаниеВводаТекста          - ЭлементФормыПодсистемыОкончаниеВводаТекста
// Нажатие                       - Подключаемый_ЭлементНажатие                       - ЭлементФормыПодсистемыНажатие
// ОбработкаНавигационнойСсылки  - Подключаемый_ЭлементОбработкаНавигационнойСсылки  - ЭлементФормыПодсистемыОбработкаНавигационнойСсылки
// ДействиеКоманды               - Подключаемый_КомандаДействие                      - КомандаФормыПодсистемыДействие
// Методы с реализацией находятся в модуле ОбменСКонтрагентамиКлиентПереопределяемый.
//
// Параметры:
//  Контекст - ФиксированнаяСтруктура - контекст создания формы:
//   * Назначение - Строка - назначение формы.
//   * Форма - ФормаКлиентскогоПриложения - форма для изменения.
//   * Префикс - Строка - префикс имен для новых реквизитов, команд и элементов формы.
//  Отказ - Булево - аналогичен параметру обработчика события "ПриСозданииНаСервер" управляемой формы.
//  СтандартнаяОбработка - Булево - аналогичен параметру обработчика события "ПриСозданииНаСервер" управляемой формы.
//
// Пример:
//  Если Контекст.Назначение = "СопоставлениеНоменклатуры" Тогда
//  	Контекст.Форма.Элементы.Добавить(Префикс + "ИмяНовогоЭлемент",...);
//  	Контекст.Форма.Команды.Добавить(Префикс + "ИмяНовойКоманды");
//  	....
//  КонецЕсли;
//
//@skip-warning
Процедура ПриСозданииФормыПодсистемы(Контекст, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Контекст.Назначение = "СопоставлениеНоменклатуры" Тогда
		Контекст.Форма.ИспользоватьСервисИнтерактивно = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать См. МашиночитаемыеДоверенностиПереопределяемый.ПриИзмененииДанныеФизЛица
// Получает удостоверения личности физического лица.
// 
// Параметры:
//  ФизЛицо - СправочникСсылка.ФизическиеЛица - ссылка на элемент справочника, по которому получаются данные.
//  Сведения - См. ЭлектронноеВзаимодействие.НоваяСтруктураДанныхДокументаФизЛица
//
Процедура ПолучитьДокументыФизЛица(ФизЛицо, Сведения) Экспорт
	
	ДанныеФизЛица = УчетЗарплаты.ДанныеФизическихЛиц(Неопределено, ФизЛицо, ТекущаяДатаСеанса());
	
	Сведения = Новый Структура;
	Сведения.Вставить("ВидДокумента"    , ДанныеФизЛица.ВидДокумента);
	Сведения.Вставить("Серия"           , ДанныеФизЛица.Серия);
	Сведения.Вставить("Номер"           , ДанныеФизЛица.Номер);
	Сведения.Вставить("ДатаВыдачи"      , ДанныеФизЛица.ДатаВыдачи);
	Сведения.Вставить("КемВыдан"        , ДанныеФизЛица.КемВыдан);
	Сведения.Вставить("КодПодразделения", ДанныеФизЛица.КодПодразделения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
