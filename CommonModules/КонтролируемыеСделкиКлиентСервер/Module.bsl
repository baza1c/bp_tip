#Область ПрограммныйИнтерфейс

Функция ВерсияУведомления_2012() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 27 июля 2012 г. N ММВ-7-13/524
	
	Возврат 1;
	
КонецФункции

Функция ВерсияУведомления_2017() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 27 июля 2012 г. N ММВ-7-13/524
	// с дополнениями по Письму ФНС от 22 марта 2018 г. N ЕД-4-13/5367@
	
	Возврат 2;
	
КонецФункции

Функция ВерсияУведомления_2018() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 07 мая 2018 №ММВ-7-13/249@
	
	Возврат 3;
	
КонецФункции

Функция ВерсияУведомления_2019() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 26.07.2019 № ММВ-7-13/380@
	
	Возврат 4;
	
КонецФункции

Функция ВерсияУведомления_2021() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 26.07.2019 № ММВ-7-13/380@
	// с дополнениями по границам уведомления, внесенными законом от 17 февраля 2021 года №6-ФЗ
	
	Возврат 5;
	
КонецФункции

Функция ВерсияУведомления_2024() Экспорт
	
	// Формат уведомления описывается Приказом ФНС от 02.12.2024 № ЕД-7-13/1088@
	
	Возврат 6;
	
КонецФункции

Функция ОписаниеОснованийКонтролируемости(ВерсияУведомления) Экспорт
	
	Описание = НовыйОписаниеОснованийКонтролируемости();
	Если ВерсияУведомления <= ВерсияУведомления_2017() Тогда
		ЗаполнитьОснованияКонтролируемости2012(Описание);
	ИначеЕсли ВерсияУведомления <= ВерсияУведомления_2018() Тогда
		ЗаполнитьОснованияКонтролируемости2018(Описание);
	ИначеЕсли ВерсияУведомления <= ВерсияУведомления_2021() Тогда
		ЗаполнитьОснованияКонтролируемости2019(Описание);
	Иначе
		ЗаполнитьОснованияКонтролируемости2024(Описание);
	КонецЕсли;
	Возврат Описание;
	
КонецФункции

Функция НужноЗаполнятьСведенияОСделкеЛиста1В(ИспользованияПроисхожденияТовара) Экспорт
	
	Возврат ЗначениеЗаполнено(ИспользованияПроисхожденияТовара)
		И ИспользованияПроисхожденияТовара <> ПредопределенноеЗначение("Перечисление.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.Переработка")
		И ИспользованияПроисхожденияТовара <> ПредопределенноеЗначение("Перечисление.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.Производство")
		И ИспользованияПроисхожденияТовара <> ПредопределенноеЗначение("Перечисление.ТипыИспользованияПроисхожденияТовараКонтролируемыхСделок.Иное");
	
КонецФункции

Функция МаксимальноеКоличествоСделокВЦепочке() Экспорт
	Возврат 4;
КонецФункции

Функция ЭтоРозничнаяПродажаЦепочкиСделок(ПризнакУчастникаСделки) Экспорт
	
	Возврат ЗначениеЗаполнено(ПризнакУчастникаСделки)
		И ПризнакУчастникаСделки = ПредопределенноеЗначение("Перечисление.ТипыУчастниковЦепочкиКонтролируемыхСделок.КонечныйРозничныйПокупатель");
	
КонецФункции

Функция НовыйОписаниеОснованийКонтролируемости()
	
	Описание = Новый Структура;
	Описание.Вставить("ОснованиеКонтролируемости131", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости132", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости133", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости134", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости135", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости136", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости137", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости138", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости139", Неопределено);
	Описание.Вставить("ОснованиеКонтролируемости140", Неопределено);
	Возврат Описание;
	
КонецФункции

Функция НовыйОписаниеОснования()
	
	Описание = Новый Структура();
	Описание.Вставить("Наименование", "");
	Описание.Вставить("Подсказка", "");
	Возврат Описание;
	
КонецФункции

Процедура ЗаполнитьОснованияКонтролируемости2012(Описание)
	
	ДобавитьОснованиеОбщийПределРФ(Описание, "131");
	ДобавитьОснованиеНДПИ(Описание, "132");
	ДобавитьОснованиеСпецрежим(Описание, "133");
	ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, "134");
	ДобавитьОснованиеРезидентОЭЗ(Описание, "135");
	
КонецПроцедуры

Процедура ЗаполнитьОснованияКонтролируемости2018(Описание)
	
	ДобавитьОснованиеОбщийПределРФ(Описание, "131");
	ДобавитьОснованиеНДПИ(Описание, "132");
	ДобавитьОснованиеСпецрежим(Описание, "133");
	ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, "134");
	ДобавитьОснованиеРезидентОЭЗ(Описание, "135");
	ДобавитьОснованиеОператорНовогоМорскогоМесторождения(Описание, "136");
	ДобавитьОснованиеУчастникРегиональногоИнвестиционногоПроекта(Описание, "137");
	ДобавитьОснованиеИсследовательскийЦентрСколково(Описание, "138");
	ДобавитьОснованиеИнвестиционныйНалоговыйВычет(Описание, "139");
	ДобавитьОснованиеНалогНаДополнительныйДоходОтУглеводородов(Описание, "140");
	
КонецПроцедуры

Процедура ЗаполнитьОснованияКонтролируемости2019(Описание)
	
	ДобавитьОснованиеРазныеСтавкиНалогаНаПрибыль(Описание, "131");
	ДобавитьОснованиеНДПИ(Описание, "132");
	ДобавитьОснованиеСпецрежим(Описание, "133");
	ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, "134");
	ДобавитьОснованиеОператорНовогоМорскогоМесторождения(Описание, "135");
	ДобавитьОснованиеИсследовательскийЦентрСколково(Описание, "136");
	ДобавитьОснованиеИнвестиционныйНалоговыйВычет(Описание, "137");
	ДобавитьОснованиеНалогНаДополнительныйДоходОтУглеводородов(Описание, "138");
	
КонецПроцедуры

Процедура ЗаполнитьОснованияКонтролируемости2024(Описание)
	
	ДобавитьОснованиеРазныеСтавкиНалогаНаПрибыль(Описание, "131");
	ДобавитьОснованиеНДПИ(Описание, "132");
	ДобавитьОснованиеСпецрежим(Описание, "133");
	ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, "134");
	ДобавитьОснованиеОператорНовогоМорскогоМесторождения(Описание, "135");
	ДобавитьОснованиеИсследовательскийЦентрСколковоИЭра(Описание, "136");
	ДобавитьОснованиеИнвестиционныйНалоговыйВычет(Описание, "137");
	ДобавитьОснованиеНалогНаДополнительныйДоходОтУглеводородов(Описание, "138");
	
КонецПроцедуры

Процедура ДобавитьОснованиеОбщийПределРФ(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - сумма доходов по сделкам превышает предел'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Сумма доходов по сделкам (сумма цен сделок) между взаимозависимыми лицами за соответствующий календарный год превышает 1 млрд. рублей (пп. 1 п. 2 статьи 105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеНДПИ(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - уплата НДПИ по процентной ставке'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Одна из сторон сделки является налогоплательщиком налога на добычу полезных ископаемых, исчисляемого по налоговой ставке  в процентах, и предметом сделки является добытое полезное ископаемое (пп.2 п.2 статьи 105.14 НК РФ).'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеСпецрежим(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - применение спец.режимов налогообложения'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки применяет специальный режим налогообложения ЕСХН или ЕНВД, а другая сторона - не применяет. При этом сделка заключена в рамках этого спецрежима (пп.3.п2. ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеОсвобождениеОтУплатыНалогаНаПрибыль(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - освобождение от уплаты налога на прибыль'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки освобождена от уплаты налога на прибыль или применяет налоговую ставку 0% в соответствии с п. 5.1 статьи 284 НК РФ, а другая сторона - не освобождена и не применяет ставку 0% (пп.4 п.2 ст. 105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеРезидентОЭЗ(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - резидент особой экономической зоны'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки является резидентом особой экономической зоны, в которой предусматриваются льготы по налогу на прибыль, а другая сторона не является таким резидентом (пп.5 п.2 ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеОператорНовогоМорскогоМесторождения(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - оператор нового морского месторождения'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Одна из сторон сделки учитывает доходы (расходы) при расчете базы по налогу на прибыль в соответствии со ст.275.2 НК РФ, а вторая не применяет ст.275.2 НК РФ (пп.6.п.2 ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеУчастникРегиональногоИнвестиционногоПроекта(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - участник регионального инвестиционного проекта'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон является участником регионального инвестиционного проекта, применяющим пониженную налоговую ставку по налогу на прибыль в соответствии со ст. 284.3 и 284.3-1 НК РФ (пп.7.п.2 ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеИсследовательскийЦентрСколково(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - исследовательский корпоративный центр ""Сколково""'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки является исследовательским корпоративным центром в ""Сколково"", применяющим освобождение от НДС в соответствии со ст.145.1 НК РФ (пп.8.п.2 ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеИсследовательскийЦентрСколковоИЭра(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - корпоративный центр ""Сколково"" или технополис ""Эра""'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки является либо исследовательским корпоративным центром в ""Сколково"", применяющим освобождение от НДС в соответствии со ст.145.1 НК РФ,
			|либо участником Военного инновационного технополиса ""Эра"" Министерства обороны, применяющим освобождение от НДС в соответствии со ст.145.2 НК РФ (пп.8.п.2 ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеИнвестиционныйНалоговыйВычет(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - применение инвестиционного налогового вычета'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки применяет в течение налогового периода инвестиционный налоговый вычет по налогу на прибыль, предусмотренный ст. 286.1 НК РФ (пп. 9 п. 2 с. 105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеНалогНаДополнительныйДоходОтУглеводородов(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - применение налога на дополнительный доход от добычи углеводородов'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Хотя бы одна из сторон сделки является плательщиком налога на дополнительный доход от добычи углеводородного сырья и доходы (расходы) по такой сделке учитываются при определении налоговой базы по этому налогу (пп.10 п.2. ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры

Процедура ДобавитьОснованиеРазныеСтавкиНалогаНаПрибыль(Описание, НомерПункта)
	
	ОписаниеОснования = НовыйОписаниеОснования();
	ОписаниеОснования.Наименование = 
		СтрШаблон(НСтр("ru = '%1 - применяются разные ставки налога на прибыль'"), НомерПункта);
	ОписаниеОснования.Подсказка = 
		НСтр("ru = 'Стороны сделки применяют разные ставки по налогу на прибыль по деятельности, в рамках которой заключена сделка (пп.1 п.2. ст.105.14 НК РФ)'");
	Описание["ОснованиеКонтролируемости" + НомерПункта] = ОписаниеОснования;
	
КонецПроцедуры


#КонецОбласти