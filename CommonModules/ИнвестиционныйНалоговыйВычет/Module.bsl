
#Область ПрограммныйИнтерфейс

// Дата начала применения инвестиционного вычета.
// 
// Возвращаемое значение:
//  Дата
//
Функция ДатаНачалаПрименения() Экспорт
	Возврат Дата(2025, 1, 1);
КонецФункции

// Проверяет корректность периода применения инвестиционного налогового вычета.
// 
// Параметры:
//  Запись - РегистрСведенийМенеджерЗаписи.НастройкиУчетаНалогаНаПрибыль
//         - РегистрСведенийЗапись.НастройкиУчетаНалогаНаПрибыль
//  Период - Дата
//  Отказ  - Булево - Возвращаемый параметр, признак отказа от записи
//
Процедура ПроверитьКорректностьПериода(Запись, Период, Отказ) Экспорт
	
	Если Период >= ДатаНачалаПрименения() Тогда
		Возврат;
	КонецЕсли;

	Если Не Запись.ИспользуетсяПередачаИнвестиционногоВычета И Не Запись.ПрименяетсяИнвестиционныйВычет Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоля = "";
	
	ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Федеральный инвестиционный налоговый не может применяться до %1 года'"),
		Формат(Год(ДатаНачалаПрименения()), "ЧГ="));
		
	СообщениеПользователю = ТекстОшибки;
	
	Если ТипЗнч(Запись) = Тип("РегистрСведенийЗапись.НастройкиУчетаНалогаНаПрибыль") Тогда
		
		ИмяПоля = "Период";
		
		СообщениеПользователю = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле",
			"Корректность",
			Метаданные.РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.СтандартныеРеквизиты.Период.Представление(),
			, // НомерСтроки
			, // ИмяСписка
			ТекстОшибки);

	КонецЕсли;
	
	ОбщегоНазначения.СообщитьПользователю(СообщениеПользователю, , ИмяПоля, , Отказ);

КонецПроцедуры

// Конструктор коллекции реквизитов, необходимых для определения возможности применения вычета.
// 
// Возвращаемое значение:
//  Структура:
// * Дата - Дата
// * Организация - СправочникСсылка.Организации
// * ВидОперации - ПеречислениеСсылка.ВидыОперацийПоступлениеТоваровУслуг
//               - ПеречислениеСсылка.ВидыОперацийПринятияКУчетуОсновныхСредств
//               - ПеречислениеСсылка.ВидыОперацийПринятияКУчетуНМА
// * ВидОбъектаУчета - ПеречислениеСсылка.ВидыОбъектовУчетаНМА
// * ПорядокВключенияСтоимостиВСоставРасходовНУ - ПеречислениеСсылка.ПорядокВключенияСтоимостиОСВСоставРасходовНУ
// * МенеджерДокумента - Неопределено,
//                     - ДокументМенеджер - менеджер документа применения инвестиционного вычета, возможные значения:
//                       - ДокументМенеджер.ПринятиеКУчетуОС,
//                       - ДокументМенеджер.ПоступлениеТоваровУслуг
//                       - ДокументМенеджер.ПринятиеКУчетуНМА
//                       - ДокументМенеджер.МодернизацияОС
//
Функция НовыйКритерииПримененияВычета() Экспорт
	
	КритерииПримененияВычета = Новый Структура;
	КритерииПримененияВычета.Вставить("Дата", Дата(1, 1, 1));
	КритерииПримененияВычета.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	КритерииПримененияВычета.Вставить("ВидОперации", Перечисления.ВидыОперацийПринятияКУчетуНМА.ПустаяСсылка());
	КритерииПримененияВычета.Вставить("ВидОбъектаУчета", Перечисления.ВидыОбъектовУчетаНМА.ПустаяСсылка());
	КритерииПримененияВычета.Вставить("ПорядокВключенияСтоимостиВСоставРасходовНУ",
		Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ПустаяСсылка());
	КритерииПримененияВычета.Вставить("МенеджерДокумента", Неопределено);
	
	Возврат КритерииПримененияВычета;
	
КонецФункции

// Возвращает признак, что в документе может использоваться инвестиционный вычет.
// 
// Параметры:
//  КритерииПримененияВычета - см. НовыйКритерииПримененияВычета
// 
// Возвращаемое значение:
//  Булево - Можно применять налоговый вычет
//
Функция МожноПрименятьНалоговыйВычет(КритерииПримененияВычета) Экспорт
	
	Если ДокументыПримененияИнвестиционногоВычета().Найти(КритерииПримененияВычета.МенеджерДокумента) = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КритерииПримененияВычета.Дата) И КритерииПримененияВычета.Дата < ДатаНачалаПрименения() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПлательщикНалогаПрибыль =
		УчетнаяПолитика.ПлательщикНалогаНаПрибыль(КритерииПримененияВычета.Организация, КритерииПримененияВычета.Дата);
	
	Если Не ПлательщикНалогаПрибыль Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыФункциональнойОпции = Новый Структура;
	ПараметрыФункциональнойОпции.Вставить("Организация", КритерииПримененияВычета.Организация);
	Если ЗначениеЗаполнено(КритерииПримененияВычета.Дата) Тогда
		ПериодКеша = Мин(НачалоМесяца(КритерииПримененияВычета.Дата), '39991201');
		ПараметрыФункциональнойОпции.Вставить("Период", ПериодКеша);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ПрименяетсяИнвестиционныйВычет", ПараметрыФункциональнойОпции) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Не КритерииПримененияВычета.МенеджерДокумента.ЗапрещеноПрименятьНалоговыйВычет(КритерииПримененияВычета);

КонецФункции

// Определяет возможность использовать документ "Операция с инвестиционным вычетом" в зависимости от настроек учетной
// политики по налогу на прибыль.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Период - Дата
//  СсылкаНаДокумент - Неопределено
//                   - ДокументСсылка.ОперацияСИнвестиционнымВычетом
//  ВыводитьСообщения - Булево
// 
// Возвращаемое значение:
//  Булево
//
Функция МожноИспользоватьОперациюСИнвестиционнымВычетом(Организация, Период, СсылкаНаДокумент = Неопределено, ВыводитьСообщения = Ложь) Экспорт
	
	ПлательщикНалогаПрибыль =
		УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Период);
	
	Если Не ПлательщикНалогаПрибыль Тогда
		Если ВыводитьСообщения Тогда
			ШаблонТекстаОшибки = НСтр("ru = 'Организация %1 на %2 не является плательщиком налога на прибыль.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонТекстаОшибки,
				Организация,
				Формат(НачалоМесяца(Период), "ДЛФ=DD;"));
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, СсылкаНаДокумент);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.ИспользуетсяПередачаИнвестиционногоВычета КАК ИспользуетсяПередачаИнвестиционногоВычета
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&Период, Организация = &Организация) КАК Настройки";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Или Не Выборка.ИспользуетсяПередачаИнвестиционногоВычета Тогда
		Если ВыводитьСообщения Тогда
			ШаблонТекстаОшибки = НСтр("ru = 'Для организация %1 на %2 в настройке учетной политики по налогу на прибыль установлено, что налоговый вычет не применяется.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонТекстаОшибки,
				Организация,
				Формат(НачалоМесяца(Период), "ДЛФ=DD;"));
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, СсылкаНаДокумент);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Возвращает процент вычета в зависимости от вида документа, значений его реквизитов, а также учетной политики по налогу на прибыль
// на дату документа.
// 
// Параметры:
//  КритерииПрименения - см. НовыйКритерииПримененияВычета
// 
// Возвращаемое значение:
//  Число
//
Функция ПроцентИнвестиционногоВычета(КритерииПрименения) Экспорт
	
	Если Не МожноПрименятьНалоговыйВычет(КритерииПрименения) Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат СтандартныйПроцентВычета();
	
КонецФункции

// Процент от суммы расходов - величина федерального инвестиционного вычета.
// Постановление Правительства Российской Федерации от 28 ноября 2024 г. N 1638 "О параметрах применения
// федерального инвестиционного налогового вычета".
// п. 3. Установить, что величина федерального инвестиционного налогового вычета текущего налогового (отчетного) периода
// определяется в размере 3 процентов суммы расходов, указанных в пункте 4 статьи 286 2 Налогового кодекса Российской Федерации.
//
// Возвращаемое значение:
//  Число
//
Функция СтандартныйПроцентВычета() Экспорт
	Возврат 3;
КонецФункции

// Возвращает таблицу для формирования движений по инвестиционному вычету в документе "Принятие к учету ОС".
// 
// Параметры:
//  ТаблицаОбъектов - ТаблицаЗначений
//  ТаблицаРеквизиты - ТаблицаЗначений
// 
// Возвращаемое значение:
//  см. НовыйТаблицаИнвестиционныйВычет
//
Функция ПодготовитьТаблицуИнвестиционныйВычетПринятиеКУчетуОС(ТаблицаОбъектов, ТаблицаРеквизиты) Экспорт
	
	ТаблицаИнвестиционныйВычет = НовыйТаблицаИнвестиционныйВычет();
	
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат ТаблицаИнвестиционныйВычет;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыТаблицаИнвестиционныйВычетПринятиеКУчетуОС(ТаблицаРеквизиты, ТаблицаОбъектов);

	Реквизиты = Параметры.Реквизиты[0];
	ОбъектыИнвестиционногоВычета = Параметры.ОбъектыИнвестиционногоВычета;

	Для Каждого СтрокаСведений Из ОбъектыИнвестиционногоВычета Цикл
		
		СуммаВычета = СуммаИнвестиционногоВычета(СтрокаСведений.СуммаНУ, Реквизиты.ПроцентИнвестиционногоВычета);
		
		Если СуммаВычета = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВычета = ТаблицаИнвестиционныйВычет.Добавить();
		
		СтрокаВычета.СчетУчета                = Реквизиты.СчетУчета;
		СтрокаВычета.ПодразделениеОрганизации = Реквизиты.ПодразделениеОрганизации;
		СтрокаВычета.АмортизируемоеИмущество  = СтрокаСведений.ОсновноеСредство;
		СтрокаВычета.Сумма                    = СуммаВычета;

	КонецЦикла;

	Возврат ТаблицаИнвестиционныйВычет;

КонецФункции

// Возвращает таблицу для формирования движений по инвестиционному вычету в документе "Поступление товаров и услуг".
// 
// Параметры:
//  ТаблицаОбъектов - ТаблицаЗначений
//  ТаблицаРеквизиты - ТаблицаЗначений
// 
// Возвращаемое значение:
//  см. НовыйТаблицаИнвестиционныйВычет
//
Функция ПодготовитьТаблицуИнвестиционныйВычетПоступлениеТоваровУслуг(ТаблицаОбъектов, ТаблицаРеквизиты) Экспорт
	
	ТаблицаИнвестиционныйВычет = НовыйТаблицаИнвестиционныйВычет();
	
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат ТаблицаИнвестиционныйВычет;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыТаблицаИнвестиционныйВычетПоступлениеТоваровУслуг(ТаблицаРеквизиты, ТаблицаОбъектов);

	Реквизиты = Параметры.Реквизиты[0];
	
	ОбъектыИнвестиционногоВычета = Параметры.ОбъектыИнвестиционногоВычета;
	
	Для Каждого СтрокаСведений Из ОбъектыИнвестиционногоВычета Цикл

		СуммаИнвестиционногоВычета = СуммаИнвестиционногоВычета(СтрокаСведений.СуммаНУ, СтрокаСведений.ПроцентИнвестиционногоВычета);
		
		Если СуммаИнвестиционногоВычета = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВычета = ТаблицаИнвестиционныйВычет.Добавить();
		
		СтрокаВычета.СчетУчета                = СтрокаСведений.СчетУчета;
		СтрокаВычета.ПодразделениеОрганизации = Реквизиты.Подразделение;
		СтрокаВычета.АмортизируемоеИмущество  = СтрокаСведений.ОсновноеСредство;
		СтрокаВычета.Сумма                    = СуммаИнвестиционногоВычета;
		
	КонецЦикла;

	Возврат ТаблицаИнвестиционныйВычет;

КонецФункции

// Возвращает таблицу для формирования движений по инвестиционному вычету в документе "Принятие к учету НМА".
// 
// Параметры:
//  ТаблицаОбъектов - ТаблицаЗначений
//  ТаблицаРеквизиты - ТаблицаЗначений
// 
// Возвращаемое значение:
//  см. НовыйТаблицаИнвестиционныйВычет
//
Функция ПодготовитьТаблицуИнвестиционныйВычетПринятиеКУчетуНМА(ТаблицаРеквизиты) Экспорт
	
	ТаблицаИнвестиционныйВычет = НовыйТаблицаИнвестиционныйВычет();
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат ТаблицаИнвестиционныйВычет;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыТаблицаИнвестиционныйВычетПринятиеКУчетуНМА(ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];
	
	СуммаВычета = СуммаИнвестиционногоВычета(Реквизиты.СтоимостьНУ, Реквизиты.ПроцентИнвестиционногоВычета);
	
	Если СуммаВычета = 0 Тогда
		Возврат ТаблицаИнвестиционныйВычет;
	КонецЕсли;
	
	СтрокаВычета = ТаблицаИнвестиционныйВычет.Добавить();
	
	СтрокаВычета.СчетУчета                = Реквизиты.СчетУчета;
	СтрокаВычета.ПодразделениеОрганизации = Реквизиты.ПодразделениеДт;
	СтрокаВычета.АмортизируемоеИмущество  = Реквизиты.НематериальныйАктив;
	СтрокаВычета.Сумма                    = СуммаВычета;

	Возврат ТаблицаИнвестиционныйВычет;

КонецФункции

// Возвращает таблицу для формирования движений по инвестиционному вычету в документе "Модернизация ОС".
// 
// Параметры:
//  ТаблицаОбъектов - ТаблицаЗначений
//  ТаблицаРеквизиты - ТаблицаЗначений
// 
// Возвращаемое значение:
//  см. НовыйТаблицаИнвестиционныйВычет
//
Функция ПодготовитьТаблицуИнвестиционныйВычетМодернизацияОС(ТаблицаОбъектов, ТаблицаРеквизиты) Экспорт
	
	ТаблицаИнвестиционныйВычет = НовыйТаблицаИнвестиционныйВычет();
	
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат ТаблицаИнвестиционныйВычет;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыТаблицаИнвестиционныйВычетМодернизацияОС(ТаблицаРеквизиты, ТаблицаОбъектов);

	Реквизиты = Параметры.Реквизиты[0];
	
	Если Реквизиты.ПроцентИнвестиционногоВычета = 0 Тогда
		Возврат ТаблицаИнвестиционныйВычет;
	КонецЕсли;
	
	ОбъектыИнвестиционногоВычета = Параметры.ОбъектыИнвестиционногоВычета;

	Для Каждого СтрокаСведений Из ОбъектыИнвестиционногоВычета Цикл
		
		СуммаВычета = СуммаИнвестиционногоВычета(СтрокаСведений.СуммаМодернизацииНУ, Реквизиты.ПроцентИнвестиционногоВычета);
		
		Если Не СтрокаСведений.ПрименяетсяИнвестиционныйВычет
			Или СуммаВычета = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВычета = ТаблицаИнвестиционныйВычет.Добавить();
		
		СтрокаВычета.СчетУчета                = СтрокаСведений.СчетУчета;
		СтрокаВычета.ПодразделениеОрганизации = Реквизиты.ПодразделениеОрганизации;
		СтрокаВычета.АмортизируемоеИмущество  = СтрокаСведений.ОсновноеСредство;
		СтрокаВычета.Сумма                    = СуммаВычета;
		
	КонецЦикла;

	Возврат ТаблицаИнвестиционныйВычет;

КонецФункции

// Возвращает таблицу для формирования движений по инвестиционному вычету в документе "ОперацияСИнвестиционнымВычетом"
// с видом операции "Передача".
// Предполагается вызов из обработки проведения документа (или в ранее открытой транзакции с обработкой исключения).
// 
// Параметры:
//  ПередачаИнвестиционногоВычета - ТаблицаЗначений
//  ТаблицаРеквизиты - ТаблицаЗначений
// 
// Возвращаемое значение:
//  см. НовыйТаблицаПередачаПолучениеИнвестиционногоВычета
//
Функция ПодготовитьТаблицуПередачаИнвестиционногоВычета(ПередачаИнвестиционногоВычета, ТаблицаРеквизиты) Экспорт

	ТаблицаПередачаВычета = НовыйТаблицаПередачаПолучениеИнвестиционногоВычета();

	Если Не ЗначениеЗаполнено(ПередачаИнвестиционногоВычета) Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат ТаблицаПередачаВычета;
	КонецЕсли;

	Параметры = ПодготовитьПараметрыПередачаИнвестиционногоВычета(ПередачаИнвестиционногоВычета, ТаблицаРеквизиты);

	Реквизиты = Параметры.Реквизиты[0];
	ПередачаВычета = Параметры.ТаблицаПередачаИнвестиционногоВычета[0];

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Хозрасчетный");
	ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(, КонецМесяца(Реквизиты.Период)));
	ЭлементБлокировки.УстановитьЗначение("Счет", ЗабалансовыйСчетУчетаИнвестиционногоВычета());
	ЭлементБлокировки.УстановитьЗначение(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.АмортизируемоеИмущество,
		ПередачаВычета.АмортизируемоеИмущество);
	ЭлементБлокировки.УстановитьЗначение(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыИнвестиционногоВычета,
		ПередачаВычета.ДокументВычета);

	//@skip-check lock-out-of-try
	Блокировка.Заблокировать();

	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("Период", Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор));

	Запрос.УстановитьПараметр("СчетИнвестиционногоВычета", ЗабалансовыйСчетУчетаИнвестиционногоВычета());

	СубконтоСчета = СубконтоСчетаИнвестиционныйВычет();

	Запрос.УстановитьПараметр("СубконтоСчета", СубконтоСчета);
	Запрос.УстановитьПараметр("ОбъектВычета", ПередачаВычета.АмортизируемоеИмущество);
	Запрос.УстановитьПараметр("ДокументВычета", ПередачаВычета.ДокументВычета);
	Запрос.УстановитьПараметр("ЭтоОбособленноеПодразделение", ПередачаВычета.ЭтоОбособленноеПодразделение);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК ВидРасходов,
	|	ХозрасчетныйОстатки.СуммаНУОстатокДт КАК СуммаВычета
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет = &СчетИнвестиционногоВычета,
	|			&СубконтоСчета,
	|			Организация = &Организация
	|				И Субконто2 = &ОбъектВычета
	|				И Субконто3 = &ДокументВычета) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	НЕ &ЭтоОбособленноеПодразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.СуммаНУОборотДт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоМесяца,
	|			&Период,
	|			,
	|			Счет = &СчетИнвестиционногоВычета,
	|			&СубконтоСчета,
	|			Организация = &Организация
	|				И Субконто2 = &ОбъектВычета
	|				И Субконто3 = &ДокументВычета,
	|			,
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	&ЭтоОбособленноеПодразделение";

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		СтрокаВычета = ТаблицаПередачаВычета.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВычета, ПередачаВычета);
		СтрокаВычета.Сумма = - Выборка.СуммаВычета;
		СтрокаВычета.ВидРасходов = Выборка.ВидРасходов;
	КонецЦикла;

	Возврат ТаблицаПередачаВычета;
	
КонецФункции

// Возвращает список субконто счета инвестиционного вычета
// 
// Возвращаемое значение:
//  Массив из ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
//
Функция СубконтоСчетаИнвестиционныйВычет() Экспорт

	СубконтоСчета = Новый Массив;

	СубконтоСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыРасходовНаАмортизируемоеИмущество);
	СубконтоСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.АмортизируемоеИмущество);
	СубконтоСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыИнвестиционногоВычета);

	Возврат СубконтоСчета;
	
КонецФункции

// Формирует движения по инвестиционному вычету в документах, увеличивающих стоимость объектов.
// 
// Параметры:
//  ТаблицаИнвестиционныйВычет - см. НовыйТаблицаИнвестиционныйВычет
//  ТаблицаРеквизиты - ТаблицаЗначений
//  Движения - КоллекцияДвижений
//
Процедура СформироватьДвиженияИнвестиционныйВычетСтоимостьОбъектов(ТаблицаИнвестиционныйВычет, ТаблицаРеквизиты, Движения) Экспорт

	Если Не ЗначениеЗаполнено(ТаблицаИнвестиционныйВычет) Или Не ЗначениеЗаполнено(ТаблицаРеквизиты)Тогда
		Возврат;
	КонецЕсли;

	Параметры = ПараметрыПроведенияИнвестиционныйВычетСтоимостьОбъектов(ТаблицаРеквизиты, ТаблицаИнвестиционныйВычет);
	Реквизиты = Параметры.Реквизиты[0];

	СчетВычета = ЗабалансовыйСчетУчетаИнвестиционногоВычета();
	ОбъектыИнвестицинногоВычета = Параметры.ТаблицаИнвестиционныйВычет; // см. НовыйТаблицаИнвестиционныйВычет
	ЭтоОбособленноеПодразделение = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоОбособленноеПодразделение(Реквизиты.Организация);
	ВидРасходов = ВидРасходовПоТипуРегистратора(Реквизиты.Регистратор);

	Для Каждого СтрокаТаблицы Из ОбъектыИнвестицинногоВычета Цикл

		Если СтрокаТаблицы.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;

		// Уменьшение стоимости амортизируемого имущества в НУ
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;

		Проводка.Содержание = НСтр("ru = 'Федеральный инвестиционный вычет'", ОбщегоНазначения.КодОсновногоЯзыка());

		Проводка.СчетДт = СтрокаТаблицы.СчетУчета;
		Проводка.СчетКт = СтрокаТаблицы.СчетУчета;

		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);

		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = СтрокаТаблицы.ПодразделениеОрганизации;
		КонецЕсли;
		
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства",
			СтрокаТаблицы.АмортизируемоеИмущество);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НематериальныеАктивы",
			СтрокаТаблицы.АмортизируемоеИмущество);

		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);

		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = СтрокаТаблицы.ПодразделениеОрганизации;
		КонецЕсли;

		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ОсновныеСредства",
			СтрокаТаблицы.АмортизируемоеИмущество);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "НематериальныеАктивы",
			СтрокаТаблицы.АмортизируемоеИмущество);

		Проводка.СуммаПРДт = СтрокаТаблицы.Сумма;
		Проводка.СуммаВРДт = - СтрокаТаблицы.Сумма;

		Проводка.СуммаНУКт = СтрокаТаблицы.Сумма;
		Проводка.СуммаВРКт = - СтрокаТаблицы.Сумма;

		// Забалансовый счет
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;

		Проводка.Содержание = НСтр("ru = 'Федеральный инвестиционный вычет'", ОбщегоНазначения.КодОсновногоЯзыка());

		Проводка.СчетДт = СчетВычета;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "АмортизируемоеИмущество",
			СтрокаТаблицы.АмортизируемоеИмущество);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ДокументыИнвестиционногоВычета",
			Реквизиты.Регистратор);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ВидыРасходовНаАмортизируемоеИмущество",
			ВидРасходов);

		Проводка.СуммаНУДт = СтрокаТаблицы.Сумма;

		// Передача головной организации
		Если ЭтоОбособленноеПодразделение Тогда

			Проводка = Движения.Хозрасчетный.Добавить();

			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;

			Проводка.Содержание = НСтр("ru = 'Передача вычета головной организации'", ОбщегоНазначения.КодОсновногоЯзыка());

			Проводка.СчетКт = СчетВычета;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "АмортизируемоеИмущество",
				СтрокаТаблицы.АмортизируемоеИмущество);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ДокументыИнвестиционногоВычета",
				Реквизиты.Регистратор);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыРасходовНаАмортизируемоеИмущество",
				ВидРасходов);

			Проводка.СуммаНУКт = СтрокаТаблицы.Сумма;

		КонецЕсли;

	КонецЦикла;

	Движения.Хозрасчетный.Записывать = Истина;

КонецПроцедуры

// Формирует движения по инвестиционному вычету в документе ОперацияСИнвестиционнымВычетом.
// 
// Параметры:
//  ТаблицаИнвестиционныйВычет - см. НовыйТаблицаИнвестиционныйВычет
//  ТаблицаРеквизиты - ТаблицаЗначений
//  Движения - КоллекцияДвижений
//
Процедура СформироватьДвиженияПередачаПолучение(ТаблицаИнвестиционныйВычет, ТаблицаРеквизиты, Движения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаИнвестиционныйВычет) Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПараметрыПроведенияПередачаПолучение(ТаблицаРеквизиты, ТаблицаИнвестиционныйВычет);
	
	Реквизиты = Параметры.Реквизиты[0];
	
	ТаблицаИнвестиционныйВычет = Параметры.ТаблицаИнвестиционныйВычет; // см.НовыйТаблицаПередачаПолучениеИнвестиционногоВычета
	
	СчетВычета = ЗабалансовыйСчетУчетаИнвестиционногоВычета();
	
	Для Каждого СтрокаТаблицы Из ТаблицаИнвестиционныйВычет Цикл

		Если СтрокаТаблицы.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;

		// Забалансовый счет
		Проводка = Движения.Хозрасчетный.Добавить();

		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;

		Проводка.Содержание = СтрокаТаблицы.СодержаниеПроводки;

		Проводка.СчетДт = СчетВычета;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "АмортизируемоеИмущество",
			СтрокаТаблицы.АмортизируемоеИмущество);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ДокументыИнвестиционногоВычета",
			СтрокаТаблицы.ДокументВычета);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ВидыРасходовНаАмортизируемоеИмущество",
			СтрокаТаблицы.ВидРасходов);

		Проводка.СуммаНУДт = СтрокаТаблицы.Сумма;

		// Передача головной организации
		Если СтрокаТаблицы.ЭтоОбособленноеПодразделение Тогда

			Проводка = Движения.Хозрасчетный.Добавить();

			Проводка.Период      = Реквизиты.Период;
			Проводка.Организация = Реквизиты.Организация;
	
			Проводка.Содержание = СтрокаТаблицы.СодержаниеПроводкиПередачаГоловнойОрганизации;
	
			Проводка.СчетКт = СчетВычета;
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "АмортизируемоеИмущество",
				СтрокаТаблицы.АмортизируемоеИмущество);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ДокументыИнвестиционногоВычета",
				СтрокаТаблицы.ДокументВычета);
			БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ВидыРасходовНаАмортизируемоеИмущество",
				СтрокаТаблицы.ВидРасходов);
	
			Проводка.СуммаНУКт = СтрокаТаблицы.Сумма;

		КонецЕсли;

	КонецЦикла;

	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  ТаблицаЗначений:
//  * СчетУчета                - ПланСчетовСсылка.Хозрасчетный
//  * ПодразделениеОрганизации - СправочникСсылка.ПодразделенияОрганизаций
//  * АмортизируемоеИмущество  - СправочникСсылка.ОсновныеСредства
//                             - СправочникСсылка.НематериальныеАктивы
//  * Сумма                    - Число
//
Функция НовыйТаблицаИнвестиционныйВычет()
	ТаблицаИнвестиционныйВычет = Новый ТаблицаЗначений;
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("ПодразделениеОрганизации", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("АмортизируемоеИмущество",
		Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства, СправочникСсылка.НематериальныеАктивы"));
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("Сумма", Метаданные.Документы.ОперацияСИнвестиционнымВычетом.Реквизиты.СуммаВычета.Тип);
	Возврат ТаблицаИнвестиционныйВычет;
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений:
// * АмортизируемоеИмущество - Неопределено
//                           - СправочникСсылка.НематериальныеАктивы
//                           - СправочникСсылка.ОсновныеСредства
// * ДокументВычета                                - ДокументСсылка
// * ВидРасходов                                   - ПеречислениеСсылка.ВидыРасходовПоИнвестиционномуВычету
// * ЭтоОбособленноеПодразделение                  - Булево
// * СодержаниеПроводки                            - Строка
// * СодержаниеПроводкиПередачаГоловнойОрганизации - Строка
// * Сумма                                         - Число
//
Функция НовыйТаблицаПередачаПолучениеИнвестиционногоВычета()
	ТаблицаИнвестиционныйВычет = Новый ТаблицаЗначений;
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("АмортизируемоеИмущество");
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("ДокументВычета");
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("ВидРасходов");
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("ЭтоОбособленноеПодразделение");
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("СодержаниеПроводки");
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("СодержаниеПроводкиПередачаГоловнойОрганизации");
	ТаблицаИнвестиционныйВычет.Колонки.Добавить("Сумма");
	Возврат ТаблицаИнвестиционныйВычет;
КонецФункции

Функция ПараметрыПроведенияИнвестиционныйВычетСтоимостьОбъектов(ТаблицаРеквизиты, ТаблицаИнвестиционныйВычет)

	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	
	// Реквизиты
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты", 
		ТаблицаРеквизиты);

	Описание.Колонки.Добавить("Период"); // Дата
	Описание.Колонки.Добавить("Организация"); // СправочникСсылка.Организации
	Описание.Колонки.Добавить("Регистратор"); // ДокументСсылка
	
	// ТаблицаИнвестиционныйВычет
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"ТаблицаИнвестиционныйВычет", 
		ТаблицаИнвестиционныйВычет);
	
	Для Каждого Колонка Из НовыйТаблицаИнвестиционныйВычет().Колонки Цикл
		Описание.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Параметры = ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
	
	Возврат Параметры;
	
КонецФункции

Функция ПараметрыПроведенияПередачаПолучение(ТаблицаРеквизиты, ТаблицаИнвестиционныйВычет)
	
	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	
	// Реквизиты
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты", 
		ТаблицаРеквизиты);

	Описание.Колонки.Добавить("Период"); // Дата
	Описание.Колонки.Добавить("Организация"); // СправочникСсылка.Организации
	Описание.Колонки.Добавить("Регистратор"); // ДокументСсылка.ОперацияСИнвестиционнымВычетом
	
	// ТаблицаИнвестиционныйВычет
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"ТаблицаИнвестиционныйВычет", 
		ТаблицаИнвестиционныйВычет);
		
	Для Каждого Колонка Из НовыйТаблицаПередачаПолучениеИнвестиционногоВычета().Колонки Цикл
		Описание.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Параметры = ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
	
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыТаблицаИнвестиционныйВычетПринятиеКУчетуОС(ТаблицаРеквизиты,ТаблицаОбъектов)

	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	
	// Реквизиты
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты", 
		ТаблицаРеквизиты);

	Описание.Колонки.Добавить("Период"); // Дата
	Описание.Колонки.Добавить("Организация"); // СправочникСсылка.Организации
	Описание.Колонки.Добавить("СчетУчета"); // ПланСчетовСсылка.Хозрасчетный
	Описание.Колонки.Добавить("ПодразделениеОрганизации"); // СправочникСсылка.ПодразделенияОрганизаций
	Описание.Колонки.Добавить("ПроцентИнвестиционногоВычета"); // Число
	
	// ОбъектыИнвестиционногоВычета
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"ОбъектыИнвестиционногоВычета", 
		ТаблицаОбъектов);
		
	Описание.Колонки.Добавить("ОсновноеСредство");
	Описание.Колонки.Добавить("СуммаНУ");

	Параметры = ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыТаблицаИнвестиционныйВычетМодернизацияОС(ТаблицаРеквизиты,ТаблицаОбъектов)

	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	
	// Реквизиты
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты", 
		ТаблицаРеквизиты);

	Описание.Колонки.Добавить("Период"); // Дата
	Описание.Колонки.Добавить("Организация"); // СправочникСсылка.Организации
	Описание.Колонки.Добавить("ПодразделениеОрганизации"); // СправочникСсылка.ПодразделенияОрганизаций
	Описание.Колонки.Добавить("ПроцентИнвестиционногоВычета"); // Число
	
	// ОбъектыИнвестиционногоВычета
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"ОбъектыИнвестиционногоВычета", 
		ТаблицаОбъектов);
	
	Описание.Колонки.Добавить("ОсновноеСредство");
	Описание.Колонки.Добавить("СуммаМодернизацииНУ");
	Описание.Колонки.Добавить("СчетУчета");
	Описание.Колонки.Добавить("ПрименяетсяИнвестиционныйВычет");

	Параметры = ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
	
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыТаблицаИнвестиционныйВычетПринятиеКУчетуНМА(ТаблицаРеквизиты)

	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	
	// Реквизиты
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты", 
		ТаблицаРеквизиты);

	Описание.Колонки.Добавить("Период"); // Дата
	Описание.Колонки.Добавить("Организация"); // СправочникСсылка.Организации
	Описание.Колонки.Добавить("СчетУчета"); // ПланСчетовСсылка.Хозрасчетный
	Описание.Колонки.Добавить("ПодразделениеДт"); // СправочникСсылка.ПодразделенияОрганизаций
	Описание.Колонки.Добавить("НематериальныйАктив"); // СправочникСсылка.НематериальныеАктивы
	Описание.Колонки.Добавить("СтоимостьНУ"); // Число
	Описание.Колонки.Добавить("ПроцентИнвестиционногоВычета"); // Число
	
	Параметры = ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
	
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыТаблицаИнвестиционныйВычетПоступлениеТоваровУслуг(ТаблицаРеквизиты,ТаблицаОбъектов)

	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	
	// Реквизиты
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты", 
		ТаблицаРеквизиты);

	Описание.Колонки.Добавить("Период"); // Дата
	Описание.Колонки.Добавить("Организация"); // СправочникСсылка.Организации
	Описание.Колонки.Добавить("Подразделение"); // СправочникСсылка.ПодразделенияОрганизаций
	
	// ОбъектыИнвестиционногоВычета
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"ОбъектыИнвестиционногоВычета", 
		ТаблицаОбъектов);
	
	Описание.Колонки.Добавить("ОсновноеСредство"); // СправочникСсылка.ОсновныеСредства
	Описание.Колонки.Добавить("СчетУчета"); // ПланСчетовСсылка.Хозрасчетный
	Описание.Колонки.Добавить("СуммаНУ"); // Число
	Описание.Колонки.Добавить("ПроцентИнвестиционногоВычета"); // Число
	
	Параметры = ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
	
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыПередачаИнвестиционногоВычета(ПередачаИнвестиционногоВычета, ТаблицаРеквизиты)
	ОписаниеТаблиц = ОбщегоНазначенияБПВызовСервера.НовыйОписаниеТаблицПараметровПроведения();
	
	// Реквизиты
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"Реквизиты", 
		ТаблицаРеквизиты);

	Описание.Колонки.Добавить("Период"); // Дата
	Описание.Колонки.Добавить("Организация"); // СправочникСсылка.Организации
	Описание.Колонки.Добавить("Регистратор"); // СправочникСсылка.ПодразделенияОрганизаций
	
	// ОбъектыИнвестиционногоВычета
	Описание = ОбщегоНазначенияБПВызовСервера.ДобавитьОписаниеТаблицыПараметровПроведения(
		ОписаниеТаблиц,
		"ТаблицаПередачаИнвестиционногоВычета", 
		ПередачаИнвестиционногоВычета);
	
	Описание.Колонки.Добавить("АмортизируемоеИмущество"); // СправочникСсылка.ОсновныеСредства, СправочникСсылка.НематериальныеАктивы
	Описание.Колонки.Добавить("ДокументВычета"); // ДокументСсылка
	Описание.Колонки.Добавить("ЭтоОбособленноеПодразделение"); // Булево
	Описание.Колонки.Добавить("СодержаниеПроводки"); // Строка
	Описание.Колонки.Добавить("СодержаниеПроводкиПередачаГоловнойОрганизации"); // Строка
	
	Параметры = ОбщегоНазначенияБПВызовСервера.ПодготовитьТаблицыПараметрыПроведения(ОписаниеТаблиц);
	
	Возврат Параметры;
	
КонецФункции

Функция ВидРасходовПоТипуРегистратора(Регистратор)
	
	ПеречислениеМенеджер = Перечисления.ВидыРасходовПоИнвестиционномуВычету;
	
	ВидыРасходовПоТипам = Новый Соответствие;
	ВидыРасходовПоТипам.Вставить(Тип("ДокументСсылка.ПоступлениеТоваровУслуг"), ПеречислениеМенеджер["ПервоначальнаяСтоимостьОС"]);
	ВидыРасходовПоТипам.Вставить(Тип("ДокументСсылка.ПринятиеКУчетуОС"), ПеречислениеМенеджер["ПервоначальнаяСтоимостьОС"]);
	ВидыРасходовПоТипам.Вставить(Тип("ДокументСсылка.МодернизацияОС"), ПеречислениеМенеджер["МодернизацияОС"]);
	ВидыРасходовПоТипам.Вставить(Тип("ДокументСсылка.ПринятиеКУчетуНМА"), ПеречислениеМенеджер["ПервоначальнаяСтоимостьНМА"]);
	
	ВидРасходов = ВидыРасходовПоТипам[ТипЗнч(Регистратор)];
	
	Если Не ЗначениеЗаполнено(ВидРасходов) Тогда
		Возврат ПеречислениеМенеджер.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ВидРасходов;
	
КонецФункции

Функция ДокументыПримененияИнвестиционногоВычета()
	
	ДокументыПримененияИнвестиционногоВычета = Новый Массив;
	ДокументыПримененияИнвестиционногоВычета.Добавить(Документы.МодернизацияОС);
	ДокументыПримененияИнвестиционногоВычета.Добавить(Документы.ПоступлениеТоваровУслуг);
	ДокументыПримененияИнвестиционногоВычета.Добавить(Документы.ПринятиеКУчетуНМА);
	ДокументыПримененияИнвестиционногоВычета.Добавить(Документы.ПринятиеКУчетуОС);
	
	Возврат ДокументыПримененияИнвестиционногоВычета;
	
КонецФункции

Функция ЗабалансовыйСчетУчетаИнвестиционногоВычета()
	Возврат ПланыСчетов.Хозрасчетный.ФедеральныйИнвестиционныйНалоговыйВычет;
КонецФункции

Функция СуммаИнвестиционногоВычета(СуммаНУ, ПроцентИнвестиционногоВычета)
	Возврат Цел(СуммаНУ * (ПроцентИнвестиционногоВычета / 100));
КонецФункции

#КонецОбласти

