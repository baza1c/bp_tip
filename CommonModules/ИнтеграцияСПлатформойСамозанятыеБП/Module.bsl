#Область ПрограммныйИнтерфейс

// Функция возвращает номер чека из ссылки на чек на сайте ФНС
//
// Параметры:
//  СсылкаНаЧек - Строка - URL на чек самозанятого на сайте ФНС
// 
// Возвращаемое значение:
//  Строка - Номер чека
//
Функция НомерЧекаИзСсылки(СсылкаНаЧек) Экспорт
	
	ЧастьСсылкиНаЧек = СтрЗаменить(СсылкаНаЧек,
		ИнтеграцияСПлатформойСамозанятыеБПВызовСервераПовтИсп.АдресСервисаФНС(), "");
	ЧастиСсылки = СтрРазделить(ЧастьСсылкиНаЧек, "/");

	ИНН = ИННИзСсылкиНаЧек(ЧастиСсылки);
	
	НомерЧека = "";
	ПредыдущееЗначение = "";
	СоответствуетУсловиям = Ложь;
	// Номер чека идет после ИНН и перед "print"
	Для Каждого ЧастьСсылки Из ЧастиСсылки Цикл
		Если ЗначениеЗаполнено(НомерЧека) И ЧастьСсылки = "print" Тогда
			СоответствуетУсловиям = Истина;
		КонецЕсли;
		Если ПредыдущееЗначение = ИНН Тогда
			НомерЧека = ЧастьСсылки;
		КонецЕсли;
		ПредыдущееЗначение = ЧастьСсылки;
	КонецЦикла;
	Если Не СоответствуетУсловиям Тогда
		НомерЧека = "";
	КонецЕсли;
	
	Возврат НомерЧека;
	
КонецФункции

// Получает ИНН контрагента из ссылки на чек самозанятого.
//
// Параметры:
//   ЧастиСсылки - Массив из Строка - Массив, полученный в результате разделения ссылки на чек по символу "/".
//
// Возвращаемое значение:
//  Строка
//
Функция ИННИзСсылкиНаЧек(ЧастиСсылки) Экспорт

	ИНН = "";
	Для Каждого ЧастьСсылки Из ЧастиСсылки Цикл
		Результат = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ЧастьСсылки, Ложь);
		Если Результат.СоответствуетТребованиям Тогда
			ИНН = ЧастьСсылки;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИНН;

КонецФункции

// Функция возвращает ссылку на скаченный файл чека самозанятого по переданной ссылке
//
// Параметры:
//  СсылкаНаЧек - Строка - URL на чек самозанятого на сайте ФНС
//  ЧекСамозанятого - СправочникСсылка.ВыплатыСамозанятымПрисоединенныеФайлы - Ссылка на присоединенный файл,
//                    чтобы можно было его обновить
//  Документ - ДокументСсылка.ВыплатыСамозанятым, ДокументСсылка.ПоступлениеТоваровУслуг, Документ.ПоступлениеДопРасходов
//             - Ссылка на документ, к которому присоединяется файл
// 
// Возвращаемое значение:
//  СправочникСсылка.ВыплатыСамозанятымПрисоединенныеФайлы, СправочникСсылка.ПоступлениеТоваровУслугПрисоединенныеФайлы,
//  Неопределено - Ссылка на присоединенный файл, в случае возникновения ошибки возвращается Неопределено
//
Функция ЧекСамозанятогоИзИнтернета(СсылкаНаЧек, ЧекСамозанятого, Документ) Экспорт

	URL = СсылкаНаЧек;
	
	ПараметрыПолучения = ПолучениеФайловИзИнтернетаКлиентСервер.ПараметрыПолученияФайла();
	ПараметрыПолучения.Таймаут = 1260;
	
	Результат = ПолучениеФайловИзИнтернета.СкачатьФайлВоВременноеХранилище(URL, ПараметрыПолучения);
	
	Если Результат.Статус Тогда
		ИмяФайла = СтрШаблон(НСтр("ru='№%1'"), НомерЧекаИзСсылки(СсылкаНаЧек));
		Если ЗначениеЗаполнено(ЧекСамозанятого) Тогда
			// Обновим существующий файл
			ИнформацияОФайле = Новый Структура;
			ИнформацияОФайле.Вставить("АдресФайлаВоВременномХранилище", Результат.Путь);
			ИнформацияОФайле.Вставить("АдресВременногоХранилищаТекста", "");
			ИнформацияОФайле.Вставить("ИмяБезРасширения", ИмяФайла);
			ИнформацияОФайле.Вставить("Расширение", "jpg");
			ИнформацияОФайле.Вставить("ДатаМодификацииУниверсальная", ТекущаяУниверсальнаяДата());
			РаботаСФайлами.ОбновитьФайл(ЧекСамозанятого, ИнформацияОФайле);
		Иначе
			// Добавим новый файл
			ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла("Описание");
			ПараметрыФайла.ВладелецФайлов = Документ;
			ПараметрыФайла.ИмяБезРасширения = ИмяФайла;
			ПараметрыФайла.РасширениеБезТочки = "jpg";
			ПараметрыФайла.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
			ПараметрыФайла.Служебный = Ложь;
			ЧекСамозанятого = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, Результат.Путь); 
		КонецЕсли;
		
		Возврат ЧекСамозанятого;
	Иначе
		ТекстОшибки = НСтр("ru = 'Не удалось скачать чек самозанятого.
			|Описание: %1
			|URL: %2'");
		Если Результат.Свойство("КодСостояния") Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = 'Код ошибки: %3'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Результат.СообщениеОбОшибке, URL, Результат.КодСостояния);
		Иначе
			ТекстОшибки = СтрШаблон(ТекстОшибки, Результат.СообщениеОбОшибке, URL);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Загрузка чеков самозанятых из файла'"), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	КонецЕсли;

КонецФункции

#КонецОбласти