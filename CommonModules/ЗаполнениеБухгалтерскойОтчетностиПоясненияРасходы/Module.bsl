#Область ПрограммныйИнтерфейс

// Описывает состав прикладных данных таблицы отчета.
// Под таблицей понимается совокупность строк с одинаковым составом колонок.
// Обычно каждая таблица в отчете имеет отдельное название, номер и шапку с описанием колонок.
//
// Параметры:
//  Описание - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйОписаниеТаблицыОтчета - заполняемая коллекция
//  ИмяТаблицыОтчета - Строка - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета
//
Процедура ОписатьТаблицуОтчета(Описание, ИмяТаблицыОтчета) Экспорт
	
	Если ИмяТаблицыОтчета = "Расходы" Тогда
		ОписатьТаблицуОтчетаРасходы(Описание);
	КонецЕсли;
	
КонецПроцедуры

// Определяет состав заполняемых пояснений конкретного отчета (за конкретный период, по конкретной организации)
//
// Параметры:
//  ТаблицыОтчета - Структура - Ключ - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета; Значение - Булево.
//                  Для заполняемых пояснений следует установить значение Истина, состав коллекции изменять не следует.
//  КонтекстОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстОтчета
//
Процедура ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчета, КонтекстОтчета) Экспорт
	
	ТаблицыОтчета.Расходы = Истина;
	
КонецПроцедуры

// Заполняет таблицы отчета за один период.
//
// Параметры:
//  ПолучениеДанныхТаблицОтчета - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйПолучениеДанныхТаблицОтчета -
//     процессор, который содержит исходные параметры для получения данных,
//     и в который нужно разместить полученные данные.
//  Классификаторы - Структура - Ключ: имя таблицы отчета; Значение: Структура - Ключ и Значение: имя колонки, содержащей значения по классификатору
//  КонтекстЗаполненияОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
//
Процедура ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета) Экспорт
	
	ЗаполнитьПояснениеРасходы(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОписатьТаблицуОтчетаРасходы(Описание)
	
	Описание.Представление = НСтр("ru = 'Расходы по обычным видам деятельности'");
	
	Описание.Структура.Добавить("Показатель");
	// С учетом проекта рекомендации в будущих версиях возможно изменение структуры отчета с тем,
	// чтобы перечень элементов затрат не был закрытым.
	// В результате структура отчета должна получиться переменной:
	//    - По элементам затрат - вписываемые строки
	//    - Итого по элементам затрат - в авторасчете
	//    - Сверочные показатели - вписываемые строки
	//    - Итого - в авторасчете
	
	Описание.Классификаторы.Вставить("Показатель", Новый СписокЗначений);
	Для Каждого ЭлементЗатрат Из ЭлементыЗатрат() Цикл
		Описание.Классификаторы.Показатель.Добавить(ОбщегоНазначения.ИмяЗначенияПеречисления(ЭлементЗатрат.Значение), ЭлементЗатрат.Представление);
	КонецЦикла;
	
	Описание.Классификаторы.Показатель.Добавить(
		"ИтогоЭлементыЗатрат",
		НСтр("ru = 'Итого по элементам затрат'"));
	Описание.Классификаторы.Показатель.Добавить(
		"ИзменениеСтоимостиЗапасов",
		НСтр("ru = 'Изменение остатков незавершенного производства, готовой продукции'"));
	Описание.Классификаторы.Показатель.Добавить(
		"Итого",
		НСтр("ru = 'Итого расходы по обычным видам деятельности'"));
	
	Описание.ЧисловыеЗначения.Добавить("Сумма");
	
КонецПроцедуры

Функция ЭлементыЗатрат()
	
	ЭлементыИзФормы = Новый СписокЗначений; // пока в форме отчета фиксированные строки - соответствует фиксированным строкам
	
	ЭлементыИзФормы.Добавить(Перечисления.ЭлементыЗатрат.МатериальныеЗатраты,         НСтр("ru = 'Материальные затраты'"));
	ЭлементыИзФормы.Добавить(Перечисления.ЭлементыЗатрат.ОплатаТруда,                 НСтр("ru = 'Затраты на оплату труда'"));
	ЭлементыИзФормы.Добавить(Перечисления.ЭлементыЗатрат.ОтчисленияНаСоциальныеНужды, НСтр("ru = 'Отчисления на социальные нужды'"));
	ЭлементыИзФормы.Добавить(Перечисления.ЭлементыЗатрат.Амортизация,                 НСтр("ru = 'Амортизация'"));
	ЭлементыИзФормы.Добавить(Перечисления.ЭлементыЗатрат.ПрочиеЗатраты,               НСтр("ru = 'Прочие затраты'"));
	
	Возврат ЭлементыИзФормы;
	
КонецФункции

Процедура ЗаполнитьПредопределенныеСчетаРасходов(СчетаРасходов)
	
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.СебестоимостьПродаж);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажу);
	СчетаРасходов.Добавить(ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходы);
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеРасходы(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета)
	
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ПолучениеДанныхТаблицОтчета, "Расходы") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// См. проект Рекомендации БМЦ "Элементы затрат" (используются формулировки текста, опубликованного 10 марта 2025)
	// http://bmcenter.ru/Files/R-KpR_Elementi_zatrat
	
	Расчет = НовыйРасчетРасходы();
	НачатьРасчет(Расчет, Классификаторы.Расходы.Показатель);
	
	Если Расчет.Показатели.Основной = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыТекстовРасшифровки = Новый Структура;
	УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстовРасшифровки, КонтекстЗаполненияОтчета, Расчет);
	
	Расходы = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ПолучениеДанныхТаблицОтчета,
		"Расходы",
		Новый Структура,
		ПараметрыТекстовРасшифровки);
		
	РасходыУчитываютсяПоЭлементамЗатрат = ЭлементыЗатратНастройкаПараметровУчета.РасходыУчитываютсяПоЭлементамЗатрат(
		КонтекстЗаполненияОтчета.Период.Начало,
		КонтекстЗаполненияОтчета.Период.КонецДата,
		КонтекстЗаполненияОтчета.Организации[0]);
		
	Если РасходыУчитываютсяПоЭлементамЗатрат Тогда
		// Соответствует подходу, изложенному в варианте "а" проекта Рекомендации
		ЗаполнитьЭлементыЗатратПоДаннымСчетовРасходов(
			Расходы,
			Расчет,
			КонтекстЗаполненияОтчета,
			ПараметрыТекстовРасшифровки);
	Иначе
		// Соответствует подходу, изложенному в варианте "б" проекта Рекомендации
		ЗаполнитьЭлементыЗатратПоДаннымСчетовЗатрат(
			Расходы,
			Расчет,
			КонтекстЗаполненияОтчета,
			ПараметрыТекстовРасшифровки);
	КонецЕсли;
	
	ЗаполнитьИтогоЭлементыЗатрат(Расходы, Расчет, Классификаторы.Расходы.Показатель);
	Если Не РасходыУчитываютсяПоЭлементамЗатрат Тогда
		ЗаполнитьСверочныеПоказатели(Расходы, Расчет, Классификаторы.Расходы.Показатель, КонтекстЗаполненияОтчета, ПараметрыТекстовРасшифровки);
	КонецЕсли;
	
	ДобавитьИтого(Расходы, Расчет, Классификаторы.Расходы.Показатель);
	
КонецПроцедуры

Процедура ЗаполнитьЭлементыЗатратПоДаннымСчетовРасходов(Расходы, Расчет, КонтекстЗаполненияОтчета, ПараметрыТекстовРасшифровки)
	
	ВидСубконтоЭлементыЗатрат = Неопределено;
	ЭлементыЗатрат.ОпределитьВидСубконтоЭлементыЗатрат(ВидСубконтоЭлементыЗатрат);
	Если ВидСубконтоЭлементыЗатрат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Расчет.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода",                   КонтекстЗаполненияОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",                    КонтекстЗаполненияОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",                     КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаРасходов",                   Расчет.Субсчета.РасходыПоЭлементамЗатрат);
	Запрос.УстановитьПараметр("ВидСубконтоЭлементыЗатрат",       ВидСубконтоЭлементыЗатрат);
	Запрос.УстановитьПараметр("ОсновнойПоказатель",              Расчет.Показатели.Основной.Имя);
	Запрос.УстановитьПараметр("ОсновнойПоказательПорядок",       Расчет.Показатели.Основной.Порядок);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Расходы.Субконто1 КАК ЭлементЗатрат,
	|	Расходы.Организация КАК Организация,
	|	Расходы.Счет КАК Счет,
	|	Расходы.СуммаОборотДт КАК Сумма
	|ПОМЕСТИТЬ ВТ_ВсеРасходы
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаРасходов), &ВидСубконтоЭлементыЗатрат, Организация В (&Организации), , ) КАК Расходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расходы.ЭлементЗатрат КАК ЗначениеАналитики
	|ПОМЕСТИТЬ ВТ_АналитикаЭлементЗатратНеопределен
	|ИЗ
	|	ВТ_ВсеРасходы КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.ЭлементыЗатрат КАК ЭлементыЗатрат
	|		ПО Расходы.ЭлементЗатрат = ЭлементыЗатрат.Ссылка
	|ГДЕ
	|	ЭлементыЗатрат.Ссылка ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗначениеАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыНеопределено.Субконто1 КАК ЗначениеАналитики,
	|	РасходыНеопределено.Организация КАК Организация,
	|	РасходыНеопределено.Счет КАК Счет,
	|	РасходыНеопределено.КорСчет КАК КорСчет,
	|	РасходыНеопределено.СуммаОборотДт КАК Сумма
	|ПОМЕСТИТЬ ВТ_РасходыЭлементЗатратНеопределен
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет В (&СчетаРасходов),
	|			&ВидСубконтоЭлементыЗатрат,
	|			Организация В (&Организации)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ЭлементЗатратНеопределен.ЗначениеАналитики
	|					ИЗ
	|						ВТ_АналитикаЭлементЗатратНеопределен КАК ЭлементЗатратНеопределен),
	|			,
	|			) КАК РасходыНеопределено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_АналитикаЭлементЗатратНеопределен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расходы.ЭлементЗатратОпределенПоСубконто КАК ЭлементЗатратОпределенПоСубконто,
	|	Расходы.ЗначениеАналитики КАК ЗначениеАналитики,
	|	Расходы.ЭлементЗатрат КАК ЭлементЗатрат,
	|	Расходы.Организация КАК Организация,
	|	Расходы.Счет КАК Счет,
	|	Расходы.КорСчет КАК КорСчет,
	|	СУММА(Расходы.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_РасходыПоЭлементамЗатрат
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИСТИНА КАК ЭлементЗатратОпределенПоСубконто,
	|		ВсеРасходы.ЭлементЗатрат КАК ЗначениеАналитики,
	|		ВсеРасходы.ЭлементЗатрат КАК ЭлементЗатрат,
	|		ВсеРасходы.Организация КАК Организация,
	|		ВсеРасходы.Счет КАК Счет,
	|		NULL КАК КорСчет,
	|		ВсеРасходы.Сумма КАК Сумма
	|	ИЗ
	|		ВТ_ВсеРасходы КАК ВсеРасходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИСТИНА,
	|		РасходыНеопределеноМинус.ЗначениеАналитики,
	|		РасходыНеопределеноМинус.ЗначениеАналитики,
	|		РасходыНеопределеноМинус.Организация,
	|		РасходыНеопределеноМинус.Счет,
	|		NULL,
	|		-РасходыНеопределеноМинус.Сумма
	|	ИЗ
	|		ВТ_РасходыЭлементЗатратНеопределен КАК РасходыНеопределеноМинус
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЛОЖЬ,
	|		РасходыНеопределеноПлюс.ЗначениеАналитики,
	|		ЕСТЬNULL(КорСчетаЭлементыЗатрат.ЭлементЗатрат, ЗНАЧЕНИЕ(Перечисление.ЭлементыЗатрат.ПрочиеЗатраты)),
	|		РасходыНеопределеноПлюс.Организация,
	|		РасходыНеопределеноПлюс.Счет,
	|		РасходыНеопределеноПлюс.КорСчет,
	|		РасходыНеопределеноПлюс.Сумма
	|	ИЗ
	|		ВТ_РасходыЭлементЗатратНеопределен КАК РасходыНеопределеноПлюс
	|			ЛЕВОЕ СОЕДИНЕНИЕ КорСчетаЭлементыЗатрат КАК КорСчетаЭлементыЗатрат
	|			ПО РасходыНеопределеноПлюс.КорСчет = КорСчетаЭлементыЗатрат.КорСчет) КАК Расходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Расходы.ЭлементЗатратОпределенПоСубконто,
	|	Расходы.ЗначениеАналитики,
	|	Расходы.ЭлементЗатрат,
	|	Расходы.Организация,
	|	Расходы.Счет,
	|	Расходы.КорСчет
	|
	|ИМЕЮЩИЕ
	|	СУММА(Расходы.Сумма) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_РасходыЭлементЗатратНеопределен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПоказателиЭлементыЗатрат.Имя, &ОсновнойПоказатель) КАК Показатель,
	|	ЕСТЬNULL(ПоказателиЭлементыЗатрат.Порядок, &ОсновнойПоказательПорядок) КАК ПоказательПорядок,
	|	Расходы.Организация КАК Организация,
	|	Расходы.Организация.Наименование КАК ОрганизацияПорядок,
	|	Расходы.Организация.Представление КАК ОрганизацияПредставление,
	|	Расходы.Счет КАК Счет,
	|	Расходы.Счет.Порядок КАК СчетПорядок,
	|	Расходы.Счет.Представление КАК СчетПредставление,
	|	Расходы.ЗначениеАналитики КАК ЗначениеАналитики,
	|	Расходы.ЗначениеАналитики.Порядок КАК ЗначениеАналитикиПорядок,
	|	ПРЕДСТАВЛЕНИЕ(Расходы.ЗначениеАналитики) КАК ЗначениеАналитикиПредставление,
	|	Расходы.ЭлементЗатрат КАК ЭлементЗатрат,
	|	Расходы.ЭлементЗатрат.Порядок КАК ЭлементЗатратПорядок,
	|	Расходы.Сумма КАК Сумма,
	|	Расходы.ЭлементЗатратОпределенПоСубконто КАК ЭлементЗатратОпределенПоСубконто,
	|	Расходы.КорСчет КАК КорСчет,
	|	Расходы.КорСчет.Порядок КАК КорСчетПорядок,
	|	Расходы.КорСчет.Представление КАК КорСчетПредставление
	|ИЗ
	|	ВТ_РасходыПоЭлементамЗатрат КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоказателиЭлементыЗатрат КАК ПоказателиЭлементыЗатрат
	|		ПО Расходы.ЭлементЗатрат = ПоказателиЭлементыЗатрат.ЭлементЗатрат
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоказательПорядок,
	|	ОрганизацияПорядок,
	|	СчетПорядок,
	|	ЭлементЗатратОпределенПоСубконто УБЫВ,
	|	ЗначениеАналитикиПорядок,
	|	КорСчетПорядок
	|ИТОГИ
	|	МАКСИМУМ(ПоказательПорядок),
	|	СУММА(Сумма),
	|	МАКСИМУМ(ЭлементЗатратОпределенПоСубконто)
	|ПО
	|	Показатель,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ВсеРасходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_РасходыПоЭлементамЗатрат";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПоказатель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ШаблонЗаголовковРасшифровки = ШаблонЗаголовковРасшифровки();
	ШаблонЗаголовковРасшифровки.ПорядокРасчета  = НСтр("ru = 'Оборот по [СчетамРасходов] с указанием элемента затрат [ЭлементЗатрат], а также без указания элемента затрат в корреспонденции со [СчетамиПоЭлементуЗатрат]'");
	
	ШаблоныЗаголовковРасшифровки = Новый Соответствие;
	ШаблоныЗаголовковРасшифровки.Вставить("Сумма", ШаблонЗаголовковРасшифровки);
	
	Для Каждого Показатель Из Расчет.Показатели.Все Цикл
		
		ЕстьДанныеПоказателя = ВыборкаПоказатель.НайтиСледующий(Показатель.Имя, "Показатель");
		
		ПараметрыТекстовРасшифровки.Показатель = Показатель.Представление;
		ПараметрыТекстовРасшифровки.Вставить("ЭлементЗатрат", Строка(Показатель.ЭлементЗатрат));
		
		УстановитьПараметрТекстаРасшифровкиСчетамиПоЭлементуЗатрат(ПараметрыТекстовРасшифровки, Показатель, Расчет);
		
		ДобавитьСтрокуРасходы(Расходы, Показатель.Имя, ШаблоныЗаголовковРасшифровки, ПараметрыТекстовРасшифровки);
		
		Если ЕстьДанныеПоказателя Тогда
			
			ВыборкаОрганизация = ВыборкаПоказатель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаОрганизация.Следующий() Цикл
				
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
					Расходы,
					ШаблоныЗаголовковРасшифровки,
					ВыборкаОрганизация.Организация,
					ВыборкаОрганизация.ОрганизацияПредставление);
					
				Выборка = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока Выборка.Следующий() Цикл
					
					ОтборОтчета = Новый Структура;
					ОтборОтчета.Вставить("Счет",          Выборка.Счет);
					ОтборОтчета.Вставить("ЭлементЗатрат", Выборка.ЭлементЗатрат);
					
					Если Выборка.ЭлементЗатратОпределенПоСубконто Тогда
						Описание = СтрШаблон(НСтр("ru = '%1 - элемент затрат %2'"), Выборка.СчетПредставление, Выборка.ЗначениеАналитикиПредставление);
						ПредметЗаписи = Выборка.Счет;
					Иначе
						Описание = СтрШаблон(НСтр("ru = '%1 - без указания элемента затрат, в корреспонденции со счетом %2'"), Выборка.СчетПредставление, Выборка.КорСчетПредставление);
						ПредметЗаписи = Выборка.КорСчет;
						ОтборОтчета.Вставить("КорСчет", Выборка.КорСчет);
					КонецЕсли;
					
					Отчет = НастройкиОтчетаРасходы(ОтборОтчета);
					ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(Расходы, "Сумма", Описание, Выборка.Сумма, ПредметЗаписи, Отчет);
					
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
		
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(Расходы, ШаблоныЗаголовковРасшифровки);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьЭлементыЗатратПоДаннымСчетовЗатрат(Расходы, Расчет, КонтекстЗаполненияОтчета, ПараметрыТекстовРасшифровки)
	
	// Способ основан на предположении, что основная часть расходов, связанных с получением выручки, отражается первично
	// на счетах затрат на производство и продажу, где для большинства затат можно определить элемент затрат по корсчету.
	// Затем эти затраты формируют себестоимость запасов (продукции, незавершенного производства - "вторичные затраты"),
	// которая по мере реализации включается в себестоимость продаж и иные показатели расходов, формирующие прибыль от продаж.
	// Таким образом, основную часть расходов можно объяснить через первичные затраты и изменение запасов, связанных с результатами производства.
	// Часть затрат, такие как стоимость товаров или обесценение запасов, включаются в расходы в обход счетов затрат.
	// Кроме того, часть затрат могут быть включены в стоимость внеоборотных активов (то есть, формировать прибыль от продаж опосредованно,
	// в течение многих периодов - через амортизацию внеоборотных активов) или расходов, не связываемых с получением выручки.
	// Могут быть и иные эффекты, описывающие связь между затратами, учтенными на счетах затрат на производство и продажу,
	// и расходами, формирующими прибыль от продаж, однако их влияние предполагается незначительным.
	
	// Таким образом, основные данные об элементах затрат получаем по счетам первичных затрат,
	// а в той части, когда расходы заведомо не проходят через счета затрат (первичные и/или вторичные) - по счетам расходов.
	
	ЗаполнитьСчетаЗатрат(Расчет);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Расчет.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода",             КонтекстЗаполненияОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",              КонтекстЗаполненияОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",               КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаЗатратПервичные",      Расчет.Субсчета.ЗатратыПервичные);
	Запрос.УстановитьПараметр("СчетаЗатратВсе",            Расчет.Субсчета.ЗатратыВсе);
	Запрос.УстановитьПараметр("СчетаРасходов",             Расчет.Субсчета.Расходы);
	Запрос.УстановитьПараметр("ОсновнойПоказатель",        Расчет.Показатели.Основной.Имя);
	Запрос.УстановитьПараметр("ОсновнойПоказательПорядок", Расчет.Показатели.Основной.Порядок);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Затраты.Организация КАК Организация,
	|	Затраты.Счет КАК Счет,
	|	Затраты.КорСчет КАК КорСчет,
	|	Затраты.СуммаОборотДт КАК Сумма
	|ПОМЕСТИТЬ Затраты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаЗатратПервичные), , Организация В (&Организации), НЕ КорСчет В (&СчетаЗатратПервичные), ) КАК Затраты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КорСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расходы.Организация КАК Организация,
	|	Расходы.Счет КАК Счет,
	|	Расходы.КорСчет КАК КорСчет,
	|	Расходы.СуммаОборотДт КАК Сумма,
	|	Расходы.КорСчет В (&СчетаЗатратВсе) КАК ЧерезСчетаЗатрат
	|ПОМЕСТИТЬ Расходы
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаРасходов), , Организация В (&Организации), , ) КАК Расходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК ПоСчетуЗатрат,
	|	ЕСТЬNULL(КорСчетаЭлементыЗатрат.ЭлементЗатрат, ЗНАЧЕНИЕ(Перечисление.ЭлементыЗатрат.ПрочиеЗатраты)) КАК ЭлементЗатрат,
	|	Затраты.Организация КАК Организация,
	|	Затраты.Счет КАК Счет,
	|	Затраты.КорСчет КАК КорСчет,
	|	Затраты.Сумма КАК Сумма
	|ПОМЕСТИТЬ РасходыЭлементыЗатрат
	|ИЗ
	|	Затраты КАК Затраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорСчетаЭлементыЗатрат КАК КорСчетаЭлементыЗатрат
	|		ПО Затраты.КорСчет = КорСчетаЭлементыЗатрат.КорСчет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЛОЖЬ,
	|	ЕСТЬNULL(КорСчетаЭлементыЗатрат.ЭлементЗатрат, ЗНАЧЕНИЕ(Перечисление.ЭлементыЗатрат.ПрочиеЗатраты)),
	|	Расходы.Организация,
	|	Расходы.Счет,
	|	Расходы.КорСчет,
	|	Расходы.Сумма
	|ИЗ
	|	Расходы КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ КорСчетаЭлементыЗатрат КАК КорСчетаЭлементыЗатрат
	|		ПО Расходы.КорСчет = КорСчетаЭлементыЗатрат.КорСчет
	|ГДЕ
	|	НЕ Расходы.ЧерезСчетаЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПоказателиЭлементыЗатрат.Имя, &ОсновнойПоказатель) КАК Показатель,
	|	ЕСТЬNULL(ПоказателиЭлементыЗатрат.Порядок, &ОсновнойПоказательПорядок) КАК ПоказательПорядок,
	|	Расходы.Организация КАК Организация,
	|	Расходы.Организация.Наименование КАК ОрганизацияПорядок,
	|	Расходы.Организация.Представление КАК ОрганизацияПредставление,
	|	Расходы.ПоСчетуЗатрат КАК ПоСчетуЗатрат,
	|	Расходы.Счет КАК Счет,
	|	Расходы.Счет.Порядок КАК СчетПорядок,
	|	Расходы.Счет.Представление КАК СчетПредставление,
	|	Расходы.КорСчет КАК КорСчет,
	|	Расходы.КорСчет.Порядок КАК КорСчетПорядок,
	|	Расходы.КорСчет.Представление КАК КорСчетПредставление,
	|	Расходы.Сумма КАК Сумма
	|ИЗ
	|	РасходыЭлементыЗатрат КАК Расходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоказателиЭлементыЗатрат КАК ПоказателиЭлементыЗатрат
	|		ПО Расходы.ЭлементЗатрат = ПоказателиЭлементыЗатрат.ЭлементЗатрат
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоказательПорядок,
	|	ОрганизацияПорядок,
	|	ПоСчетуЗатрат УБЫВ,
	|	СчетПорядок,
	|	КорСчетПорядок
	|ИТОГИ
	|	МАКСИМУМ(ПоказательПорядок),
	|	МАКСИМУМ(ПоСчетуЗатрат),
	|	СУММА(Сумма)
	|ПО
	|	Показатель,
	|	Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПоказатель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ШаблонЗаголовковРасшифровки = ШаблонЗаголовковРасшифровки();
	ШаблонЗаголовковРасшифровки.ПорядокРасчета = НСтр("ru = 'Обороты по [СчетамПервичныхЗатрат] в корреспонденции со [СчетамиПоЭлементуЗатрат]'");
	
	ПараметрыТекстовРасшифровки.Вставить("СчетамПервичныхЗатрат", СтрШаблон(НСтр("ru = 'счетам %1'"), Расчет.ПредставленияСчетов.ЗатратыПервичные));
	
	ШаблоныЗаголовковРасшифровки = Новый Соответствие;
	ШаблоныЗаголовковРасшифровки.Вставить("Сумма", ШаблонЗаголовковРасшифровки);
	
	Для Каждого Показатель Из Расчет.Показатели.Все Цикл
		
		ЕстьДанныеПоказателя = ВыборкаПоказатель.НайтиСледующий(Показатель.Имя, "Показатель");
		
		ПараметрыТекстовРасшифровки.Показатель = Показатель.Представление;
		
		УстановитьПараметрТекстаРасшифровкиСчетамиПоЭлементуЗатрат(ПараметрыТекстовРасшифровки, Показатель, Расчет);
		
		ДобавитьСтрокуРасходы(Расходы, Показатель.Имя, ШаблоныЗаголовковРасшифровки, ПараметрыТекстовРасшифровки);
		
		Если ЕстьДанныеПоказателя Тогда
			
			ВыборкаОрганизация = ВыборкаПоказатель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаОрганизация.Следующий() Цикл
				
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
					Расходы,
					ШаблоныЗаголовковРасшифровки,
					ВыборкаОрганизация.Организация,
					ВыборкаОрганизация.ОрганизацияПредставление);
					
				Выборка = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока Выборка.Следующий() Цикл
					
					ОтборОтчета = Новый Структура;
					ОтборОтчета.Вставить("Счет",    Выборка.Счет);
					ОтборОтчета.Вставить("КорСчет", Выборка.КорСчет);
					
					Описание = СтрШаблон(НСтр("ru = 'Дт %1 Кт %2'"), Выборка.СчетПредставление, Выборка.КорСчетПредставление);
					ПредметЗаписи = Выборка.КорСчет;
					
					Отчет = НастройкиОтчетаРасходы(ОтборОтчета);
					ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(Расходы, "Сумма", Описание, Выборка.Сумма, ПредметЗаписи, Отчет);
					
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
		
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(Расходы, ШаблоныЗаголовковРасшифровки);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ШаблонЗаголовковРасшифровки()
	
	Шаблон = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	Шаблон.ЗаголовокГруппы = ШаблонЗаголовкаРасшифровки();
	Возврат Шаблон;
	
КонецФункции

Функция ШаблонЗаголовкаРасшифровки()
	
	Возврат НСтр("ru = 'Расходы по обычным видам деятельности за [Период]: [Показатель]'");
	
КонецФункции

Процедура СоздатьПоказателиЭлементыЗатрат(МенеджерВременныхТаблиц, ПоказателиЭлементыЗатрат)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПоказателиЭлементыЗатрат", ПоказателиЭлементыЗатрат.Все);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Показатели.Имя КАК Имя,
	|	Показатели.ЭлементЗатрат КАК ЭлементЗатрат,
	|	Показатели.Порядок КАК Порядок
	|ПОМЕСТИТЬ ПоказателиЭлементыЗатрат
	|ИЗ
	|	&ПоказателиЭлементыЗатрат КАК Показатели
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Показатели.ЭлементЗатрат";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьКорСчетаЭлементыЗатрат(МенеджерВременныхТаблиц, КорСчетаЭлементыЗатрат)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КорСчетаЭлементыЗатрат", КорСчетаЭлементыЗатрат);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорСчетаЭлементыЗатрат.Элемент КАК ЭлементЗатрат,
	|	КорСчетаЭлементыЗатрат.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ КорСчетаЭлементыЗатрат
	|ИЗ
	|	&КорСчетаЭлементыЗатрат КАК КорСчетаЭлементыЗатрат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КорСчет";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ПоказателиЭлементыЗатрат(Показатели, КорСчета)
	
	ПоказателиЭлементыЗатрат = Новый Структура;
	ПоказателиЭлементыЗатрат.Вставить("Все", Новый ТаблицаЗначений);
	ПоказателиЭлементыЗатрат.Все.Колонки.Добавить("Имя",           ОбщегоНазначения.ОписаниеТипаСтрока(255));
	ПоказателиЭлементыЗатрат.Все.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	ПоказателиЭлементыЗатрат.Все.Колонки.Добавить("ЭлементЗатрат", Новый ОписаниеТипов("ПеречислениеСсылка.ЭлементыЗатрат"));
	ПоказателиЭлементыЗатрат.Все.Колонки.Добавить("КорСчета",      Новый ОписаниеТипов("Массив")); // Из ПланСчетовСсылка.Хозрасчетный
	ПоказателиЭлементыЗатрат.Все.Колонки.Добавить("Порядок",       Новый ОписаниеТипов("Число"));
	ПоказателиЭлементыЗатрат.Вставить("Основной", Неопределено);
	
	Для Каждого ЭлементЗатрат Из ЭлементыЗатрат() Цикл
		
		ИмяПоказателя = ОбщегоНазначения.ИмяЗначенияПеречисления(ЭлементЗатрат.Значение);
		Если Не Показатели.Свойство(ИмяПоказателя) Тогда
			// Невозможно вывести в отчет
			Продолжить;
		КонецЕсли;
		
		Показатель = ПоказателиЭлементыЗатрат.Все.Добавить();
		Показатель.Имя           = Показатели[ИмяПоказателя];
		Показатель.Представление = ЭлементЗатрат.Представление;
		Показатель.ЭлементЗатрат = ЭлементЗатрат.Значение;
		Показатель.Порядок       = ПоказателиЭлементыЗатрат.Все.Индекс(Показатель);
		
		ПоказателиЭлементыЗатрат.Основной = Показатель;
		
	КонецЦикла;
	
	ПоказателиЭлементыЗатрат.Все.Индексы.Добавить("ЭлементЗатрат");
	
	Для Каждого ОписаниеКорСчета Из КорСчета Цикл
		Показатель = ПоказателиЭлементыЗатрат.Все.Найти(ОписаниеКорСчета.Элемент, "ЭлементЗатрат");
		Показатель.КорСчета.Добавить(ОписаниеКорСчета.КорСчет);
	КонецЦикла;
	
	Возврат ПоказателиЭлементыЗатрат;
	
КонецФункции

Процедура УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета, Расчет)
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета);
	
	ПараметрыТекстов.Вставить("СчетамРасходов", СтрШаблон(НСтр("ru = 'счетам %1'"), Расчет.ПредставленияСчетов.Расходы));
	
	ПараметрыТекстов.Вставить("Показатель", ""); // Устанавливается при заполнении по элементам затрат
	
КонецПроцедуры

Процедура УстановитьПараметрТекстаРасшифровкиСчетамиПоЭлементуЗатрат(ПараметрыТекстовРасшифровки, Показатель, Расчет)
	
	ЭтоОсновнойПоказатель = (Показатель = Расчет.Показатели.Основной);
	
	Если ЭтоОсновнойПоказатель Тогда
		ПараметрыТекстовРасшифровки.Вставить(
			"СчетамиПоЭлементуЗатрат",
			НСтр("ru = 'всеми другими счетами, кроме указанных в других элементах затрат'"));
	Иначе
		СчетаПоЭлементуЗатрат = Расчет.ПредставленияСчетов[ИмяКорСчетаЭлементаЗатрат(Показатель.Имя)];
		ПараметрыТекстовРасшифровки.Вставить(
			"СчетамиПоЭлементуЗатрат",
			СтрШаблон(НСтр("ru = 'счетами %1'"), СчетаПоЭлементуЗатрат));
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьСтрокуРасходы(Расходы, Показатель, ШаблоныЗаголовковКолонок, ПараметрыТекстаРасшифровки)
	
	СтрокаТаблицы = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		Расходы,
		ШаблоныЗаголовковКолонок,
		ПараметрыТекстаРасшифровки);
	
	СтрокаТаблицы.Показатель = Показатель;
	
КонецПроцедуры

Функция НастройкиОтчетаРасходы(ОтборОтчета)
	// Планируется к реализации в будущих версиях
	Возврат Неопределено;
КонецФункции

Функция ЗаполнитьИтогоЭлементыЗатрат(Расходы, Расчет, Показатели)
	
	ЭлементыЗатратДляВывода = ЭлементыЗатрат();
	КоличествоВыведенныхЭлементовЗатрат = Расходы.Данные.ТаблицаОтчета.Количество();
	
	ШаблонЗаголовковРасшифровки = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	ШаблонЗаголовковРасшифровки.ЗаголовокГруппы = НСтр("ru = 'Итого по элементам'");
	
	ШаблоныЗаголовковКолонок = Новый Соответствие;
	ШаблоныЗаголовковКолонок.Вставить("Сумма", ШаблонЗаголовковРасшифровки);
	
	ПараметрыТекстаРасшифровки = Новый Структура;
	ДобавитьСтрокуРасходы(Расходы, Показатели.ИтогоЭлементыЗатрат, ШаблоныЗаголовковКолонок, ПараметрыТекстаРасшифровки);
	
	Для ИндексСтрокиЭлементЗатрат = 0 По КоличествоВыведенныхЭлементовЗатрат - 1 Цикл
		
		СтрокаЭлементЗатрат = Расходы.Данные.ТаблицаОтчета[ИндексСтрокиЭлементЗатрат];
		
		Если Метаданные.Перечисления.ЭлементыЗатрат.ЗначенияПеречисления.Найти(СтрокаЭлементЗатрат.Показатель) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементЗатрат = Перечисления.ЭлементыЗатрат[СтрокаЭлементЗатрат.Показатель];
		ОписаниеЭлементЗатрат = ЭлементыЗатратДляВывода.НайтиПоЗначению(ЭлементЗатрат);
		Если ОписаниеЭлементЗатрат = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(Расходы, "Сумма", ОписаниеЭлементЗатрат.Представление, СтрокаЭлементЗатрат.Сумма);
		
		Расчет.Сумма = Расчет.Сумма + СтрокаЭлементЗатрат.Сумма;
		
	КонецЦикла;
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(Расходы, ШаблоныЗаголовковКолонок);
	
КонецФункции

Процедура ЗаполнитьСверочныеПоказатели(Расходы, Расчет, Показатели, КонтекстЗаполненияОтчета, ПараметрыТекстовРасшифровки)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Расчет.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода",  КонтекстЗаполненияОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",   Новый Граница(КонтекстЗаполненияОтчета.Период.Конец, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организации",    КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаЗатратВсе", Расчет.Субсчета.ЗатратыВсе);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начало.Счет КАК Счет,
	|	Начало.Организация КАК Организация,
	|	Начало.СуммаОстаток КАК Сумма,
	|	""Начало"" КАК Слагаемое,
	|	0 КАК СлагаемоеПорядок
	|ПОМЕСТИТЬ ИзменениеОстатков
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетаЗатратВсе), , Организация В (&Организации)) КАК Начало
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Конец.Счет,
	|	Конец.Организация,
	|	-Конец.СуммаОстаток,
	|	""Конец"",
	|	1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет В (&СчетаЗатратВсе), , Организация В (&Организации)) КАК Конец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзменениеОстатков.Организация КАК Организация,
	|	ИзменениеОстатков.Счет КАК Счет,
	|	ИзменениеОстатков.Сумма КАК Сумма,
	|	ИзменениеОстатков.Слагаемое КАК Слагаемое,
	|	ИзменениеОстатков.СлагаемоеПорядок КАК СлагаемоеПорядок,
	|	""0:ИзменениеОстатковЗапасов"" КАК ШагРасчета
	|ПОМЕСТИТЬ ВсеДанные
	|ИЗ
	|	ИзменениеОстатков КАК ИзменениеОстатков
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Расходы.Организация,
	|	Расходы.Счет,
	|	СУММА(Расходы.Сумма),
	|	""РасходыВсего"",
	|	0,
	|	""9:ИныеФакторы""
	|ИЗ
	|	Расходы КАК Расходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Расходы.Организация,
	|	Расходы.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходыЭлементыЗатрат.Организация,
	|	NULL,
	|	-СУММА(РасходыЭлементыЗатрат.Сумма),
	|	""ЗатратыПоЭлементам"",
	|	1,
	|	""9:ИныеФакторы""
	|ИЗ
	|	РасходыЭлементыЗатрат КАК РасходыЭлементыЗатрат
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходыЭлементыЗатрат.Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИзменениеОстатков.Организация,
	|	NULL,
	|	-СУММА(ИзменениеОстатков.Сумма),
	|	""ИзменениеОстатковЗапасов"",
	|	2,
	|	""9:ИныеФакторы""
	|ИЗ
	|	ИзменениеОстатков КАК ИзменениеОстатков
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзменениеОстатков.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчет.Счет КАК Счет,
	|	Расчет.Счет.Порядок КАК СчетПорядок,
	|	Расчет.Счет.Представление КАК СчетПредставление,
	|	Расчет.Организация КАК Организация,
	|	Расчет.Организация.Наименование КАК ОрганизацияПорядок,
	|	Расчет.Организация.Представление КАК ОрганизацияПредставление,
	|	Расчет.Слагаемое КАК Слагаемое,
	|	Расчет.СлагаемоеПорядок КАК СлагаемоеПорядок,
	|	Расчет.Сумма КАК Сумма,
	|	Расчет.ШагРасчета КАК ШагРасчета
	|ИЗ
	|	ВсеДанные КАК Расчет
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОрганизацияПорядок,
	|	ШагРасчета,
	|	СлагаемоеПорядок,
	|	СчетПорядок
	|ИТОГИ
	|	МАКСИМУМ(СлагаемоеПорядок),
	|	СУММА(Сумма)
	|ПО
	|	Организация,
	|	ШагРасчета";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОрганизация = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ШаблонЗаголовковРасшифровки = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	ШаблонЗаголовковРасшифровки.ЗаголовокГруппы = НСтр("ru = 'Изменение остатков запасов'");
	ШаблонЗаголовковРасшифровки.ПорядокРасчета  = НСтр("ru = 'Этот показатель обеспечивает сверку (увязку) сумм понесенных затрат и признанных расходов.
                                                        |Основной учитываемый фактор - изменение в течение периода уровня запасов (незавершенного производства, готовой продукции). Например, остатки готовой продукции, израсходованные в течение периода, увеличивают расходы за период. Он определяется как разность сальдо на [НачалоПериода] и [КонецПериода] по счетам [СчетаЗапасов].
                                                        |Дополнительно рассчитывается совокупное влияние на расходы иных факторов, не учтенных ранее: из общей суммы оборотов по [СчетамРасходов] вычитаются результаты предыдущих расчетов'");
	
	ПараметрыТекстовРасшифровки.Вставить("СчетаЗапасов", Расчет.ПредставленияСчетов.ЗатратыЗапасы);
	
	ОписаниеШаговРасчета = Новый Соответствие;
	ОписаниеШаговРасчета.Вставить("0:ИзменениеОстатковЗапасов", НСтр("ru = 'Изменение остатков запасов'"));
	ОписаниеШаговРасчета.Вставить("9:ИныеФакторы",              НСтр("ru = 'Иные факторы'"));
	
	ОписаниеСлагаемых = Новый Соответствие;
	ОписаниеСлагаемых.Вставить("Начало",                   НСтр("ru = 'На начало периода - [Счет]'"));
	ОписаниеСлагаемых.Вставить("Конец",                    НСтр("ru = 'На конец периода  - [Счет]'"));
	ОписаниеСлагаемых.Вставить("РасходыВсего",             НСтр("ru = 'Всего расходы - [Счет]'"));
	ОписаниеСлагаемых.Вставить("ЗатратыПоЭлементам",       НСтр("ru = 'За вычетом: затраты по элементам'"));
	ОписаниеСлагаемых.Вставить("ИзменениеОстатковЗапасов", НСтр("ru = 'За вычетом: изменение остатков запасов'"));
	
	ШаблоныЗаголовковРасшифровки = Новый Соответствие;
	ШаблоныЗаголовковРасшифровки.Вставить("Сумма", ШаблонЗаголовковРасшифровки);
	
	ДобавитьСтрокуРасходы(Расходы, Показатели.ИзменениеСтоимостиЗапасов, ШаблоныЗаголовковРасшифровки, ПараметрыТекстовРасшифровки);
	
	Пока ВыборкаОрганизация.Следующий() Цикл
		
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
			Расходы,
			ШаблоныЗаголовковРасшифровки,
			ВыборкаОрганизация.Организация,
			ВыборкаОрганизация.ОрганизацияПредставление);
			
		ВыборкаШагРасчета = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаШагРасчета.Следующий() Цикл
			
			Описание = ОписаниеШаговРасчета[ВыборкаШагРасчета.ШагРасчета];
			
			ДобавитьПромежуточныйИтогРасшифровки(Расходы, "Сумма", Описание, ВыборкаШагРасчета.Сумма);
			Уровень = 1;
			
			Расчет.Сумма = Расчет.Сумма + ВыборкаШагРасчета.Сумма;
			
			Выборка = ВыборкаШагРасчета.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока Выборка.Следующий() Цикл
				
				ПараметрыТекстовРасшифровки.Вставить("Счет", Выборка.Счет);
				Описание = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ОписаниеСлагаемых[Выборка.Слагаемое], ПараметрыТекстовРасшифровки);
				ПредметЗаписи = Выборка.Счет;
				
				Отчет = Неопределено;
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(Расходы, "Сумма", Описание, Выборка.Сумма, ПредметЗаписи, Отчет, Уровень);
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(Расходы, ШаблоныЗаголовковРасшифровки);
	
КонецПроцедуры

Процедура ДобавитьИтого(Расходы, Расчет, Показатели)
	ДобавитьСтрокуРасходы(Расходы, Показатели.Итого, Новый Структура, Новый Структура);
	ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(Расходы, "Сумма", НСтр("ru = 'Итого'"), Расчет.Сумма);
КонецПроцедуры

Процедура ДобавитьПромежуточныйИтогРасшифровки(ВыводТаблицы, ИмяКолонки, Описание, Сумма, Уровень = 0)
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьЗаписьРасшифровкиСтроки(
		ВыводТаблицы,
		ИмяКолонки,
		"ИерархияСлагаемых",
		Описание,
		Сумма,
		Неопределено,
		Неопределено,
		Уровень);
	
КонецПроцедуры

Функция НовыйРасчетРасходы()
	
	Расчет = Новый Структура;
	
	Расчет.Вставить("Сумма", 0); // Для расчета общего итога
	
	Расчет.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	
	Расчет.Вставить("Показатели"); // См. ПоказателиЭлементыЗатрат
	
	Расчет.Вставить("Счета", Новый Структура); // Ключ - имя набора счетов; Значение - массив из ПланСчетовСсылка.Хозрасчетный
	Расчет.Вставить("Субсчета", Новый Структура); // Ключ - имя набора счетов; Значение - массив из ПланСчетовСсылка.Хозрасчетный
	Расчет.Вставить("ПредставленияСчетов", Новый Структура); // Ключ - имя набора счетов; Значение - Строка
	
	Возврат Расчет;
	
КонецФункции

Процедура НачатьРасчет(Расчет, КлассификаторПоказатели)
	
	КорСчета = Перечисления.ЭлементыЗатрат.КорСчета();
	СоздатьКорСчетаЭлементыЗатрат(Расчет.МенеджерВременныхТаблиц, КорСчета);
	
	Расчет.Показатели = ПоказателиЭлементыЗатрат(КлассификаторПоказатели, КорСчета);
	СоздатьПоказателиЭлементыЗатрат(Расчет.МенеджерВременныхТаблиц, Расчет.Показатели);
	
	Расчет.Счета = ПредопределенныеСчета(Расчет.Показатели);
	Расчет.ПредставленияСчетов = ПредставленияСчетов(Расчет.Счета);
	Расчет.Субсчета = Субсчета(Расчет.Счета);
	
КонецПроцедуры

Функция ПредопределенныеСчета(ПоказателиЭлементыЗатрат)
	
	Счета = Новый Структура;
	
	Счета.Вставить("Расходы",                  Новый Массив);
	Счета.Вставить("РасходыПоЭлементамЗатрат", Новый Массив);

	ЗаполнитьПредопределенныеСчетаРасходов(Счета.Расходы);
	ЭлементыЗатрат.ЗаполнитьПредопределенныеСчетаРасходов(Счета.РасходыПоЭлементамЗатрат);
	
	Для Каждого ЭлементЗатрат Из ПоказателиЭлементыЗатрат.Все Цикл
		Счета.Вставить(ИмяКорСчетаЭлементаЗатрат(ЭлементЗатрат.Имя), ЭлементЗатрат.КорСчета);
	КонецЦикла;
	
	// Может быть дополнена - см. ЗаполнитьСчетаЗатрат
	
	Возврат Счета;
	
КонецФункции

Функция ИмяКорСчетаЭлементаЗатрат(ИмяЭлементаЗатрат)
	
	Возврат СтрШаблон("КорСчета%1", ИмяЭлементаЗатрат);
	
КонецФункции

Процедура ЗаполнитьСчетаЗатрат(Расчет)
	
	// Здесь под счетами затрат и запасов понимаются только счета "производственного контура" -
	// из раздела Затраты на производство плана счетов, а также Расходы на продажу.
	// Счета запасов также включают те, на которых отражаются результаты производства.
	
	// Предопределенные
	СчетаЗатрат = Новый Структура;
	СчетаЗатрат.Вставить("ЗатратыПервичные",           Новый Массив);
	СчетаЗатрат.Вставить("ЗатратыЗапасы",              Новый Массив);
	СчетаЗатрат.Вставить("ЗатратыКапитальныеВложения", Новый Массив);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Расчет.Счета, СчетаЗатрат);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗатрат.ЗатратыПервичные, УчетЗатрат.ПредопределенныеСчетаРасходов());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗатрат.ЗатратыПервичные, УчетЗатрат.ПредопределенныеСчетаРасходовНаПродажу());
	
	СчетаЗатрат.ЗатратыЗапасы.Добавить(ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья);
	СчетаЗатрат.ЗатратыЗапасы.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);
	СчетаЗатрат.ЗатратыЗапасы.Добавить(ПланыСчетов.Хозрасчетный.ВыпускПродукции);
	СчетаЗатрат.ЗатратыЗапасы.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);
	СчетаЗатрат.ЗатратыЗапасы.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная);
	
	СчетаЗатрат.ЗатратыКапитальныеВложения.Добавить(ПланыСчетов.Хозрасчетный.ВыполнениеРемонтовОС);
	
	// Субсчета
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Расчет.Субсчета, Субсчета(СчетаЗатрат));
	
	ЗатратыБезЗапасов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(Расчет.Субсчета.ЗатратыПервичные, Расчет.Субсчета.ЗатратыЗапасы);
	ИсключенияЗапасыНаСчетахЗатрат = ОбщегоНазначенияКлиентСервер.РазностьМассивов(Расчет.Субсчета.ЗатратыПервичные, ЗатратыБезЗапасов);
	
	Расчет.Субсчета.ЗатратыПервичные = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ЗатратыБезЗапасов,
		Расчет.Субсчета.ЗатратыКапитальныеВложения);
	
	Расчет.Субсчета.Вставить("ЗатратыВсе", Новый Массив);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Расчет.Субсчета.ЗатратыВсе, Расчет.Субсчета.ЗатратыПервичные);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Расчет.Субсчета.ЗатратыВсе, Расчет.Субсчета.ЗатратыЗапасы);
	
	// Представления
	СчетаЗатрат.Вставить("ИсключенияЗапасыНаСчетахЗатрат", ИсключенияЗапасыНаСчетахЗатрат);
	СчетаЗатрат.Вставить(
		"ЗапасыБезСчетовЗатрат",
		ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаЗатрат.ЗатратыЗапасы, СчетаЗатрат.ИсключенияЗапасыНаСчетахЗатрат));
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Расчет.ПредставленияСчетов, ПредставленияСчетов(СчетаЗатрат));
	
	ПредставлениеЗатратыВсе = СтрШаблон(
		НСтр("ru = '%1 (кроме подчиненных счетов капитальных вложений - %2), а также %3'"),
		Расчет.ПредставленияСчетов.ЗатратыПервичные,
		Расчет.ПредставленияСчетов.ЗатратыКапитальныеВложения,
		Расчет.ПредставленияСчетов.ЗапасыБезСчетовЗатрат);
	Расчет.ПредставленияСчетов.Вставить("ЗатратыВсе", ПредставлениеЗатратыВсе);
	
	Расчет.ПредставленияСчетов.ЗатратыПервичные = СтрШаблон(
		НСтр("ru = '%1 (кроме подчиненных счетов запасов %2 и капитальных вложений %3)'"),
		Расчет.ПредставленияСчетов.ЗатратыПервичные,
		Расчет.ПредставленияСчетов.ИсключенияЗапасыНаСчетахЗатрат,
		Расчет.ПредставленияСчетов.ЗатратыКапитальныеВложения);
		
КонецПроцедуры

Функция ПредставленияСчетов(ПредопределенныеСчета)
	
	ПредставленияСчетов = Новый Структура;
	
	ВсеСчета = Новый Массив;
	Для Каждого НаборСчетов Из ПредопределенныеСчета Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеСчета, НаборСчетов.Значение);
	КонецЦикла;
	
	ВсеПредставления = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВсеСчета, "Код");
	Для Каждого НаборСчетов Из ПредопределенныеСчета Цикл
		ПредставленияНабораСчетов = Новый Массив;
		Для Каждого Счет Из НаборСчетов.Значение Цикл
			ПредставленияНабораСчетов.Добавить(ВсеПредставления[Счет]);
		КонецЦикла;
		ПредставленияСчетов.Вставить(НаборСчетов.Ключ, ОбщегоНазначенияБПКлиентСервер.ПредставлениеСписка(ПредставленияНабораСчетов));
	КонецЦикла;
	
	Возврат ПредставленияСчетов;
	
КонецФункции

Функция Субсчета(ПредопределенныеСчета)
	
	Субсчета = Новый Структура;
	
	Для Каждого НаборСчетов Из ПредопределенныеСчета Цикл
		Субсчета.Вставить(НаборСчетов.Ключ, БухгалтерскийУчет.СформироватьМассивСубсчетов(НаборСчетов.Значение));
	КонецЦикла;
	
	Возврат Субсчета;
	
КонецФункции


#КонецОбласти
