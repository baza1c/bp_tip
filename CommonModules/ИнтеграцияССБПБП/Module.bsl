
#Область ПрограммныйИнтерфейс

#Область НастройкиИнтеграции

// Определяет прикладные настройки интеграции с Системой быстрых платежей.
// См. СистемаБыстрыхПлатежейПереопределяемый.ПриОпределенииНастроекПодключения
//
Процедура ПриОпределенииНастроекПодключения(Настройки) Экспорт
	
	Настройки.c2b.ОбъектМетаданных = Метаданные.РегистрыСведений.СоответствиеНастроекИнтеграции;
	Настройки.b2b.ОбъектМетаданных = Метаданные.РегистрыСведений.СоответствиеНастроекИнтеграцииСБПb2b;
	
	Настройки.c2b.ИспользоватьНастройкуКассовыхСсылок = Истина;
	
	// Шаблон c2b заполнения документа РозничнаяПродажа
	НовыйШаблонc2b = Настройки.c2b.ШаблоныНазначений.Добавить();
	НовыйШаблонc2b.ОбъектМетаданных = Метаданные.Документы.РозничнаяПродажа.Имя;
	НовыйШаблонc2b.Идентификатор    = Метаданные.Документы.РозничнаяПродажа.Имя;
	НовыйШаблонc2b.Наименование     = Метаданные.Документы.РозничнаяПродажа.Синоним;
	
	НовыйШаблонc2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Наименование организации'"),
			"[НаименованиеОрганизации]"));
	НовыйШаблонc2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Дата чека'"),
			"[ДатаЧека]"));
	НовыйШаблонc2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Сумма чека'"),
			"[СуммаЧека]"));
	
	// Шаблон c2b заполнения документа СчетНаОплатуПокупателю
	НовыйШаблонc2b = Настройки.c2b.ШаблоныНазначений.Добавить();
	НовыйШаблонc2b.ОбъектМетаданных = Метаданные.Документы.СчетНаОплатуПокупателю.Имя;
	НовыйШаблонc2b.Идентификатор    = Метаданные.Документы.СчетНаОплатуПокупателю.Имя;
	НовыйШаблонc2b.Наименование     = Метаданные.Документы.СчетНаОплатуПокупателю.Синоним;
	
	НовыйШаблонc2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Наименование организации'"),
			"[НаименованиеОрганизации]"));
	НовыйШаблонc2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Номер счета'"),
			"[НомерСчета]"));
	НовыйШаблонc2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Дата счета'"),
			"[ДатаСчета]"));
	НовыйШаблонc2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Сумма счета'"),
			"[СуммаСчета]"));
	
	// Шаблон b2b заполнения документа СчетНаОплатуПокупателю
	НовыйШаблонb2b = Настройки.b2b.ШаблоныНазначений.Добавить();
	НовыйШаблонb2b.ОбъектМетаданных = Метаданные.Документы.СчетНаОплатуПокупателю.Имя;
	НовыйШаблонb2b.Идентификатор    = Метаданные.Документы.СчетНаОплатуПокупателю.Имя;
	НовыйШаблонb2b.Наименование     = Метаданные.Документы.СчетНаОплатуПокупателю.Синоним;
	
	НовыйШаблонb2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Наименование организации'"),
			"[НаименованиеОрганизации]"));
	НовыйШаблонb2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Номер счета'"),
			"[НомерСчета]"));
	НовыйШаблонb2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Дата счета'"),
			"[ДатаСчета]"));
	НовыйШаблонb2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'Сумма счета'"),
			"[СуммаСчета]"));
	НовыйШаблонb2b.Параметры.Добавить(
		Новый Структура(
			"Наименование, Идентификатор",
			НСтр("ru = 'НДС'"),
			"[НДС]"));
	
КонецПроцедуры

// Определяет алгоритм записи настроек оплат в регистр сведений указанный в методе
// СистемаБыстрыхПлатежейПереопределяемый.ПриОпределенииНастроекПодключения.
// См. СистемаБыстрыхПлатежейПереопределяемый.ПриЗаписиНастроекПодключения
//
Процедура ПриЗаписиНастроекПодключения(ПараметрыОплаты, Отказ, СообщениеОбОшибке) Экспорт
	
	c2b = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОплаты, "c2b");
	b2b = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОплаты, "b2b");
	Вариант = ?(ЗначениеЗаполнено(c2b), "c2b", "b2b");
	
	Организация = ПараметрыОплаты[Вариант].Получить("Организация");
	ВидОплаты   = ПараметрыОплаты[Вариант].Получить("ВидОплаты");
	Банк        = ПараметрыОплаты[Вариант].Получить("Банк");
	Если Не ЗначениеЗаполнено(Организация) Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не заполнена Организация.'");
	ИначеЕсли Не ЗначениеЗаполнено(ВидОплаты) Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не заполнено поле Вид оплаты.'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоответствиеНастроекИнтеграции");
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.УстановитьЗначение("ВидОплаты",   ВидОплаты);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Если ЗначениеЗаполнено(ПараметрыОплаты[Вариант].Получить("ТорговаяТочка")) Тогда
			Запрос = Новый Запрос;
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	СоответствиеНастроекИнтеграции.Организация КАК Организация,
			|	СоответствиеНастроекИнтеграции.ВидОплаты КАК ВидОплаты
			|ИЗ
			|	РегистрСведений.СоответствиеНастроекИнтеграции КАК СоответствиеНастроекИнтеграции
			|ГДЕ
			|	СоответствиеНастроекИнтеграции.ТорговаяТочка = &ТорговаяТочка";
			
			Если Вариант = "b2b" Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СоответствиеНастроекИнтеграции", "СоответствиеНастроекИнтеграцииСБПb2b");
			КонецЕсли;
			
			Запрос.Текст = ТекстЗапроса;
			
			Запрос.УстановитьПараметр("ТорговаяТочка", ПараметрыОплаты[Вариант].Получить("ТорговаяТочка"));
			
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Если Вариант = "b2b" Тогда
					Набор = РегистрыСведений.СоответствиеНастроекИнтеграцииСБПb2b.СоздатьНаборЗаписей();
				Иначе
					Набор = РегистрыСведений.СоответствиеНастроекИнтеграции.СоздатьНаборЗаписей();
				КонецЕсли;
				Набор.Отбор.Организация.Установить(ВыборкаДетальныеЗаписи.Организация);
				Набор.Отбор.ВидОплаты.Установить(ВыборкаДетальныеЗаписи.ВидОплаты);
				Набор.Записать();
			КонецЦикла;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	СоответствиеНастроекИнтеграции.ТорговаяТочка КАК НастройкаИнтеграции,
		|	СоответствиеНастроекИнтеграции.ТорговаяТочка.Используется КАК Используется
		|ИЗ
		|	РегистрСведений.СоответствиеНастроекИнтеграции КАК СоответствиеНастроекИнтеграции
		|ГДЕ
		|	СоответствиеНастроекИнтеграции.Организация = &Организация
		|	И СоответствиеНастроекИнтеграции.ВидОплаты = &ВидОплаты
		|	И СоответствиеНастроекИнтеграции.ТорговаяТочка <> &ТорговаяТочка";
		
		Если Вариант = "b2b" Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СоответствиеНастроекИнтеграции", "СоответствиеНастроекИнтеграцииСБПb2b");
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		
		Запрос.УстановитьПараметр("Организация",   Организация);
		Запрос.УстановитьПараметр("ВидОплаты",     ВидОплаты);
		Запрос.УстановитьПараметр("ТорговаяТочка", ПараметрыОплаты[Вариант].Получить("ТорговаяТочка"));
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			Если ЗначениеЗаполнено(Выборка.НастройкаИнтеграции) Тогда
				Если Выборка.Используется Тогда
					Отказ             = Истина;
					СообщениеОбОшибке = НСтр("ru = 'Для организации и вида оплаты уже задана настройка интеграции.'");
					ОтменитьТранзакцию();
					Возврат;
				Иначе
					НастройкаИнтеграцииОбъект = Выборка.НастройкаИнтеграции.ПолучитьОбъект();
					НастройкаИнтеграцииОбъект.Используется = Истина;
					НастройкаИнтеграцииОбъект.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Вариант = "b2b" Тогда
			Запись = РегистрыСведений.СоответствиеНастроекИнтеграцииСБПb2b.СоздатьМенеджерЗаписи();
			ВидПоступления = ПараметрыОплаты[Вариант].Получить("ВидПоступленияОплатыВБанковскойВыписке");
			Если ЗначениеЗаполнено(ВидПоступления) Тогда
				Запись.ВидПоступленияОплатыВБанковскойВыписке = ВидПоступления;
			Иначе
				Запись.ВидПоступленияОплатыВБанковскойВыписке = Перечисления.ВидПоступленияОплатыСБПb2b.ОплатаОтПокупателя;
			КонецЕсли;
		Иначе
			Запись = РегистрыСведений.СоответствиеНастроекИнтеграции.СоздатьМенеджерЗаписи();
		КонецЕсли;
		
		Запись.ТорговаяТочка = ПараметрыОплаты[Вариант].Получить("ТорговаяТочка");
		Запись.Организация   = Организация;
		Запись.ВидОплаты     = ВидОплаты;
		Запись.Банк          = Банк;
		
		Запись.Записать();
		
		УстановитьПривилегированныйРежим(Истина);
		Если Не Константы.ИспользоватьОплатуПоПлатежнымКартам.Получить() Тогда
			Константы.ИспользоватьОплатуПоПлатежнымКартам.Установить(Истина);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
	Исключение
		Если Вариант = "b2b" Тогда
			МетаданныеСБП = Метаданные.РегистрыСведений.СоответствиеНастроекИнтеграцииСБПb2b;
		Иначе
			МетаданныеСБП = Метаданные.РегистрыСведений.СоответствиеНастроекИнтеграции;
		КонецЕсли;
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Настройка интеграции с платежными системами'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			МетаданныеСБП,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Отказ = Истина;
		СообщениеОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

// Позволяет настроить элементы настройки приема оплат на формах подключения к Системе быстрых платежей.
// См. СистемаБыстрыхПлатежейПереопределяемый.ПриНастройкеЭлементовФормыПодключения
//
Процедура ПриНастройкеЭлементовФормыПодключения(НастройкиФормы, ДополнительныеПараметры) Экспорт
	
	НастройкаИзПомощника = ЗначениеЗаполнено(ДополнительныеПараметры);
	НастройкаПодключения = НастройкиФормы.НастройкаПодключения;
	
	Если НастройкаИзПомощника Тогда
		ВариантНастройки = ДополнительныеПараметры.ВариантНастройки;
	Иначе
		c2b = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиФормы, "c2b");
		b2b = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиФормы, "b2b");
		ВариантНастройки = ?(ЗначениеЗаполнено(c2b),
			Перечисления.ВариантыНастройкиСБП.c2b, Перечисления.ВариантыНастройкиСБП.b2b);
	КонецЕсли;
	
	НастройкиПодключения = Неопределено;
	Если ЗначениеЗаполнено(НастройкаПодключения) Тогда
		НастройкиПодключения = СистемаБыстрыхПлатежей.НастройкиПодключения(НастройкаПодключения);
		ВариантНастройки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаПодключения, "ВариантНастройки");
	КонецЕсли;
	
	Вариант = ?(ВариантНастройки = Перечисления.ВариантыНастройкиСБП.b2b, "b2b", "c2b");
	
	НастройкиФормы[Вариант].ЭлементыНастроекОплаты.ВидОплаты.Подсказка
		= НСтр("ru = 'Соглашение с банком, участником Системы быстрых платежей.
	                 |В нем можно указать процент комиссии, взимаемой банком за совершение операций по СБП.'");
	НастройкиФормы[Вариант].ЭлементыНастроекОплаты.ВидОплаты.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	
	ПлатежныйАгрегатор = Ложь;
	Если Вариант = "b2b" Тогда
		НастройкиФормы.b2b.ЭлементыНастроекОплаты.ВидПоступленияОплатыВБанковскойВыписке.Подсказка
			= НСтр("ru = 'Если в выписке банка, в операциях оплаты через СБП, отражаются реквизиты покупателя - укажите вид операции ""Оплата от покупателя"".
		                 |Если отражаются реквизиты банка - укажите вид операции ""Поступление по платежным картам"".'");
		НастройкиФормы[Вариант].ЭлементыНастроекОплаты.ВидПоступленияОплатыВБанковскойВыписке.ОтображениеПодсказки
			= ОтображениеПодсказки.Кнопка;
		Если ЗначениеЗаполнено(НастройкаПодключения) Тогда
			ИдентификаторУчастника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				НастройкаПодключения, "ИдентификаторУчастника");
		Иначе
			ИдентификаторУчастника = НастройкиФормы.ИдентификаторУчастника;
		КонецЕсли;
		
		НастройкиРегистр  = Неопределено;
		ВидПоступленияb2b = Неопределено;
		Если ЗначениеЗаполнено(ИдентификаторУчастника) Тогда
			НастройкиРегистр = СистемаБыстрыхПлатежейСлужебный.НастройкиУчастникаСБП(ИдентификаторУчастника);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НастройкиРегистр) Тогда
			ВидПоступленияb2b = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиРегистр, "ВидПоступленияb2b");
		КонецЕсли;
		
		НастройкиФормы[Вариант].ЭлементыНастроекОплаты.ВидПоступленияОплатыВБанковскойВыписке.Видимость =
			Не НастройкаИзПомощника Или Не ЗначениеЗаполнено(ВидПоступленияb2b);
	Иначе // Вариант c2b
		Если ЗначениеЗаполнено(НастройкиПодключения) Тогда
			ПлатежныйАгрегатор = НастройкиПодключения.НастройкиСБПc2b.ПлатежныйАгрегатор;
		Иначе
			БИК = "";
			ИдентификаторУчастника = "";
			Если НастройкаИзПомощника
				И ДополнительныеПараметры.Свойство("Банк") И ЗначениеЗаполнено(ДополнительныеПараметры.Банк) Тогда
				БИК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеПараметры.Банк, "Код");
			КонецЕсли;
			
			Если ПустаяСтрока(БИК) Тогда
				Если ЗначениеЗаполнено(НастройкиФормы[Вариант].ЗначенияНастроекОплаты.Банк) Тогда
					БИК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкиФормы.c2b.ЗначенияНастроекОплаты.Банк, "Код");
				ИначеЕсли ЗначениеЗаполнено(НастройкаПодключения) Тогда
					Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
						НастройкиФормы[Вариант].ЗначенияНастроекОплаты.ВидОплаты, "Контрагент");
					Банк = РаботаСбанкамиБП.КлассификаторБанкаПоКонтрагенту(Контрагент);
					БИК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Код");
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(БИК) Тогда
				ИдентификаторУчастника = СистемаБыстрыхПлатежейСлужебный.ИнтегрированныйУчастникСБППоБИК(БИК, ВариантНастройки);
			КонецЕсли;
			
			ПлатежныйАгрегатор = Не ЗначениеЗаполнено(ИдентификаторУчастника);
		КонецЕсли;
	КонецЕсли;
	
	НастройкиФормы[Вариант].ЭлементыНастроекОплаты.Банк.Видимость = Не НастройкаИзПомощника И ПлатежныйАгрегатор;
	Если ПлатежныйАгрегатор Тогда
		НастройкиФормы[Вариант].ЭлементыНастроекОплаты.Банк.Подсказка
			= НСтр("ru = 'Укажите банк, если хотите ограничить выставление счета покупателю по счету в конкретном банке.'");
		НастройкиФормы[Вариант].ЭлементыНастроекОплаты.Банк.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	КонецЕсли;
	
	ПараметрыБезналичныхОплат = Новый Массив;
	ТипыОплатБезналичныеОплаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ТипыОплат.СБП);
	ПараметрыБезналичныхОплат.Добавить(Новый ПараметрВыбора("Отбор.ТипОплаты",
		Новый ФиксированныйМассив(ТипыОплатБезналичныеОплаты)));
	
	Если НастройкаИзПомощника Тогда
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Организация");
	Иначе
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			НастройкиФормы[Вариант].ЗначенияНастроекОплаты, "Организация");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Организации = Справочники.ВидыОплатОрганизаций.ОрганизацииДляОтбораВидовОплат(Организация);
		ПараметрыБезналичныхОплат.Добавить(Новый ПараметрВыбора("Отбор.Организация",
			Новый ФиксированныйМассив(Организации)));
	КонецЕсли;
	
	НастройкиФормы[Вариант].ЭлементыНастроекОплаты.ВидОплаты.ПараметрыВыбора =
		Новый ФиксированныйМассив(ПараметрыБезналичныхОплат);
	
	Если НастройкаИзПомощника Тогда
		НастройкиФормы[Вариант].ЭлементыНастроекОплаты.Организация.ТолькоПросмотр = Истина;
		Наименование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Наименование");
		Если ЗначениеЗаполнено(Наименование) Тогда
			НастройкиФормы.ОбщиеЭлементы.Наименование.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	НастройкиФормы.ОбщиеЭлементы.Наименование.Подсказка
		= НСтр("ru = 'Наименование настройки для отображения в списках.'");
	НастройкиФормы.ОбщиеЭлементы.Наименование.ОтображениеПодсказки
		= ОтображениеПодсказки.ОтображатьСнизу;
	
КонецПроцедуры

// Позволяет предзаполнить настройки приема платежей на формах подключения к Системе быстрых платежей.
// См. СистемаБыстрыхПлатежейПереопределяемый.ПриЗаполненииФормыНастройкиПодключения
//
Процедура ПриЗаполненииФормыНастройкиПодключения(Настройки, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры) Тогда
		Возврат;
	КонецЕсли;
	
	Вариант = ?(ДополнительныеПараметры.ВариантНастройки = Перечисления.ВариантыНастройкиСБП.b2b, "b2b", "c2b");
	
	Если Вариант = "b2b"
		И Не ЗначениеЗаполнено(Настройки.НастройкиОплаты[Вариант].ВидПоступленияОплатыВБанковскойВыписке) Тогда
		Если Настройки.ДополнительныеНастройки[Вариант].ВидПоступления = "ACQUIRING" Тогда
			Настройки.НастройкиОплаты[Вариант].ВидПоступленияОплатыВБанковскойВыписке =
				Перечисления.ВидПоступленияОплатыСБПb2b.Эквайринг;
		Иначе
			Настройки.НастройкиОплаты[Вариант].ВидПоступленияОплатыВБанковскойВыписке =
				Перечисления.ВидПоступленияОплатыСБПb2b.ОплатаОтПокупателя;
		КонецЕсли;
	КонецЕсли;
	
	Настройки.НастройкиОплаты[Вариант].Организация = ДополнительныеПараметры.Организация;
	Банк = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Банк");
	
	Наименование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Наименование");
	Если ЗначениеЗаполнено(Наименование) Тогда
		Настройки.ОбщиеНастройки.Наименование = Наименование;
	КонецЕсли;
	
	ПлатежныйАгент = Вариант <> "b2b" И Настройки.НастройкиУчастникаСБП.ИНН = ИНН1ССБП();
	
	Шаблон = НСтр("ru = 'в %1 '");
	Если Настройки.НастройкиУчастникаСБП.ПлатежныйАгрегатор И Не ПлатежныйАгент Тогда
		РеквизитыБанка = БанковскиеСчетаВызовСервера.ПолучитьРеквизитыБанкаИзСправочника(Банк);
		ТекстЗаменыЧто = СтрШаблон(Шаблон, РеквизитыБанка.Наименование);
		ТекстЗаменыНа  = СтрШаблон(Шаблон, Настройки.НастройкиУчастникаСБП.Наименование);
		Настройки.ОбщиеНастройки.Наименование =
			СтрЗаменить(Настройки.ОбщиеНастройки.Наименование, ТекстЗаменыЧто, ТекстЗаменыНа);
	Иначе
		РеквизитыБанка = БанковскиеСчетаВызовСервера.ПолучитьРеквизитыБанкаИзСправочника(Банк);
		НастройкиСБП = Неопределено;
		Если Настройки.НастройкиУчастникаСБП.БИК.Найти(РеквизитыБанка.Код) <> Неопределено Тогда
			НастройкиСБП = СистемаБыстрыхПлатежей.ПараметрыПодключенияПоБИК(РеквизитыБанка.Код);
		КонецЕсли;
		
		Если НастройкиСБП = Неопределено Или Не НастройкиСБП.ИнтеграцияДоступнаСБПc2b Тогда
			ТекстЗамены = СтрШаблон(Шаблон, Настройки.НастройкиУчастникаСБП.Наименование);
			Настройки.ОбщиеНастройки.Наименование = СтрЗаменить(Настройки.ОбщиеНастройки.Наименование, ТекстЗамены, "");
			
			Если Не ПлатежныйАгент Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Настройки.НастройкиУчастникаСБП.ПлатежныйАгрегатор И ПлатежныйАгент Тогда // платежный агент также агрегатор
		Настройки.НастройкиОплаты[Вариант].Банк = Банк;
		Настройки.НастройкиОплаты[Вариант].ВидОплаты = ПолучитьВидОплаты(ДополнительныеПараметры.Организация, Банк, Истина);
	ИначеЕсли ЗначениеЗаполнено(Настройки.НастройкиУчастникаСБП.ИНН) Тогда
		Настройки.НастройкиОплаты[Вариант].ВидОплаты =
			ПолучитьВидОплаты(ДополнительныеПараметры.Организация, Настройки.НастройкиУчастникаСБП, Истина);
	ИначеЕсли ЗначениеЗаполнено(Банк) Тогда
		Настройки.НастройкиОплаты[Вариант].ВидОплаты = ПолучитьВидОплаты(ДополнительныеПараметры.Организация, Банк, Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ИНН1ССБП()
	
	ИНН = "7706249306";
	
	Возврат ИНН;
	
КонецФункции

// Определяет доступность использования функциональности СБП
// на основании прав доступа пользователя.
//
// Возвращаемое значение:
//  Булево - если Истина, Система быстрых платежей доступна.
//
Функция ДоступнаИнтеграцияССБП() Экспорт
	
	Возврат ИнтеграцияССБПБППовтИсп.ДоступнаИнтеграцияССБП();
	
КонецФункции

#КонецОбласти

#Область ПрикладныеОперации

// Загружает статусы оплаты СБП.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - параметры выполнения длительной операции;
//  АдресРезультата - Строка - адрес результата операции во временном хранилище.
//
Процедура ПолучитьСтатусыОперация(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	Результат = Неопределено;
	Настройки = РегистрыСведений.СоответствиеНастроекИнтеграции.ВсеНастройкиИнтеграции();
	НастройкиСБПb2b = РегистрыСведений.СоответствиеНастроекИнтеграцииСБПb2b.ВсеНастройкиИнтеграции();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Настройки, НастройкиСБПb2b, Истина);
	Если ЗаблокироватьПолучениеСтатусов(Настройки) Тогда
		Результат = СистемаБыстрыхПлатежей.СтатусыОпераций();
		РазблокироватьПолучениеСтатусов(Настройки)
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

// Определяются данные для формирования запроса на оплату в платежную систему СБП.
// Все поля переменной ЗаказНаОплату обязательны для заполнения.
// См. ПереводыСБПc2bПереопределяемый.ПриФормированииЗаказаНаОплатуСБП
//
Процедура ПриФормированииСчетаНаОплатуСБП(Основание,
										  РеквизитыСчета,
										  НастройкаПодключения,
										  ДополнительныеПараметры,
										  СтатическаяСсылка = Ложь) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Идентификатор", Основание.Метаданные().Имя);
	НайденныеСтроки = РеквизитыСчета.ШаблоныНазначений.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() <> 0 Тогда
		ШаблонНазначения = НайденныеСтроки[0];
	Иначе
		ШаблонНазначения = Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		РеквизитыСчетаНаОплатуПоСчетуПокупателю(Основание, РеквизитыСчета, ШаблонНазначения);
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РозничнаяПродажа") Тогда
		РеквизитыСчетаНаОплатуПоРозничнойПродаже(Основание, РеквизитыСчета, ДополнительныеПараметры, ШаблонНазначения);
	КонецЕсли;
	
КонецПроцедуры

// Определяет алгоритм обработки операции, статус которых был получен регламентным заданием.
// См. ПереводыСБПc2bПереопределяемый.ПриЗагрузкеСтатусаОперации
// и   ПереводыСБПb2bПереопределяемый.ПриЗагрузкеСтатусаОперации
//
Процедура ПриЗагрузкеСтатусаОперации(ДокументОперации,
									 НастройкаПодключения,
									 РезультатОбработки,
									 Обработан) Экспорт
	
	Если РезультатОбработки.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена() Тогда
		СоздатьОперациюПоПлатежнойКарте(ДокументОперации, НастройкаПодключения, РезультатОбработки);
	КонецЕсли;
	
	Обработан = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ФормаПодготовкиПлатежнойСсылки

// Определяет объекты, которые могут выступать в качестве оснований платежа через СБП.
// См. СистемаБыстрыхПлатежейПереопределяемый.ПриОпределенииОбъектовСКомандамиСБП
//
Процедура ПриОпределенииОбъектовСКомандамиСБП(ИменаОснованийПлатежа) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИменаОснованийПлатежа, ОснованияПлатежа(), Истина);
	
КонецПроцедуры

// Определяет возможность формирования платежной ссылки на основании данных документа операции.
// См. СистемаБыстрыхПлатежейПереопределяемый.ПриОпределенииДоступностиПодключенияПоДокументуОперации
//
Процедура ПриОпределенииДоступностиИнтеграцииПоДокументуОперации(ДокументОперации, Результат) Экспорт
	
	ТекстОшибки = "";
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ДокументОперации);
	
	Результат.ИнтеграцияДоступна = Истина;
	
	ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОперации,
		"Организация, ВалютаДокумента, Контрагент, Контрагент.ЮридическоеФизическоеЛицо, Контрагент.ИндивидуальныйПредприниматель,
		|ДоговорКонтрагента, СтруктурнаяЕдиница");
	
	Если Не ЗначениеЗаполнено(ДанныеОснования.СтруктурнаяЕдиница) Тогда
		ТекстОшибки = НСтр("ru = 'Не заполнен банковский счет.'");
	КонецЕсли;
	
	ДанныеБанкСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеОснования.СтруктурнаяЕдиница,
		"Банк, ВалютаДенежныхСредств, ЦифровойСчет");
	
	СчетВВалюте = ДанныеОснования.ВалютаДокумента
		<> ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Если СчетВВалюте И ЗначениеЗаполнено(ДанныеОснования.ДоговорКонтрагента) Тогда
		ВидРасчетовПоДоговору = БухгалтерскийУчетПереопределяемый.ОпределениеВидаРасчетовПоПараметрамДоговора(
			ДанныеОснования.ДоговорКонтрагента);
		СчетВВалюте = ВидРасчетовПоДоговору = Перечисления.ВидыРасчетовПоДоговорам.РасчетыВИностраннойВалюте;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеБанкСчета.ВалютаДенежныхСредств) И ДанныеБанкСчета.ВалютаДенежныхСредств
		<> ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета() Тогда
		// Банковский счет должен быть однозначно рублевым, вне зависимости от вида договора.
		СчетВВалюте = Истина;
	КонецЕсли;
	
	Если СчетВВалюте Тогда
		ТекстОшибки = НСтр("ru = 'Валютные счета не оплачиваются через СБП.'");
	ИначеЕсли ДанныеБанкСчета.ЦифровойСчет Тогда
		ТекстОшибки = НСтр("ru = 'Оплата через СБП с кошелька цифрового рубля пока не поддерживается.
			|Используйте расчетный банковский счет.'");
	КонецЕсли;
	
	ОблачнаяКасса = МенеджерОБорудованияБП.ОблачнаяКассаДляАвтоматическогоПробитияЧеков(ДанныеОснования.Организация);
	Если ЗначениеЗаполнено(ОблачнаяКасса) Тогда
		ПечатьФискальныхДокументов.ПроверитьКонтактнуюИнформациюДляОтправкиЧека(ДанныеОснования.Контрагент, ОблачнаяКасса, ТекстОшибки)
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		Результат.СообщениеОбОшибке = ТекстОшибки;
		Результат.ИнтеграцияДоступна = Ложь;
		Возврат;
	КонецЕсли;
	
	// Проверим возможность получения настройки подключения к СБП по данным основания платежа
	ПолучитьНастройки(Результат, ДанныеОснования, ДанныеБанкСчета);
	
КонецПроцедуры

// Определяет перечень возможных настроек интеграции на основании данных документа операции.
// См. СистемаБыстрыхПлатежейПереопределяемый.ПриОпределенииПараметровПодключенияДокументаОперации
//
Процедура ПриОпределенииПараметровИнтеграцииДокументаОперации(ДокументОперации,
															  НастройкиПодключения,
															  ДополнительныеНастройки,
															  ПараметрыВопроса) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ДокументОперации);
	
	ДанныеОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОперации,
		"Организация, ВалютаДокумента, Контрагент.ЮридическоеФизическоеЛицо, Контрагент.ИндивидуальныйПредприниматель,
		|ДоговорКонтрагента, СтруктурнаяЕдиница");
	
	ДанныеБанкСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеОснования.СтруктурнаяЕдиница,
		"Банк, ВалютаДенежныхСредств");
	
	// Требуется только в качестве параметра,
	// сама проверка выполнена ранее, в ПриОпределенииДоступностиИнтеграцииПоДокументуОперации()
	РезультатПроверки = Новый Структура("СообщениеОбОшибке, ИнтеграцияДоступна", "", Ложь);
	
	// Получаем настройки подключения к СБП по данным основания платежа
	НастройкиСБП = ПолучитьНастройки(РезультатПроверки, ДанныеОснования, ДанныеБанкСчета, Истина);
	Если ЗначениеЗаполнено(НастройкиСБП) Тогда
		Если ТипЗнч(НастройкиСБП) = Тип("Массив") Тогда
			Если ДанныеОснования.КонтрагентЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НастройкиПодключения.b2b, НастройкиСБП);
			Иначе
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НастройкиПодключения.c2b, НастройкиСБП);
			КонецЕсли;
		Иначе
			Если ДанныеОснования.КонтрагентЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
				НастройкиПодключения.b2b.Добавить(НастройкиСБП);
			Иначе
				НастройкиПодключения.c2b.Добавить(НастройкиСБП);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаССообщениями

// Заполняет параметры сообщения электронной почты, отправляемого без шаблона.
// См. СистемаБыстрыхПлатежейКлиентПереопределяемый.ПриЗаполненииПараметровСообщенияБезШаблонаСБП
//
Процедура ПриЗаполненииПараметровСообщенияБезШаблонаСБП(ПараметрыСообщения, ПараметрыОперации) Экспорт
	
	СведенияОСчете = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыСообщения.Предмет,
		"Номер, Организация, Контрагент, СуммаДокумента, ВалютаДокумента");
	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(СведенияОСчете.Организация);
	СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(СведенияОСчете.Контрагент);
	Подпись = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("Подпись");
	
	ФорматнаяСтрока = ?(СведенияОСчете.СуммаДокумента - Цел(СведенияОСчете.СуммаДокумента) > 0,
		"ЧЦ=15; ЧДЦ=2",
		"ЧЦ=15");
	
	ПредставлениеСуммы = СтрШаблон("%1 %2",
		Формат(СведенияОСчете.СуммаДокумента, ФорматнаяСтрока),
		СведенияОСчете.ВалютаДокумента);
	
	ПараметрыСообщения.Тема = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Оплата счета на сумму %1 (%2)'"),
		ПредставлениеСуммы,
		СведенияОбОрганизации.НаименованиеДляПечатныхФорм);
	
	ДанныеКартинки = Base64Строка(СистемаБыстрыхПлатежей.ИзображениеQRКодаСБП(
		ПараметрыОперации.ПлатежнаяСсылка, 200));
	
	ПараметрыСообщения.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '<html><body>
			|<p>Добрый день!</p>
			|<p>Вы сделали заказ на сумму %1 у %2.</p>
			|<p>Для оплаты счета № %3 перейдите по ссылке:</p>
			|<p><a href=""%4"">%4</a></p>
			|<p>Или отсканируйте QR-код</p>
			|<br>
			|<img src=""data:image/png;base64,%5""/>
			|<br>
			|<p>%6</p>
			|</body></html>'"),
		НормализоватьСтрокуДляHTML(ПредставлениеСуммы),
		НормализоватьСтрокуДляHTML(СведенияОбОрганизации.НаименованиеДляПечатныхФорм),
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СведенияОСчете.Номер, Истина, Ложь),
		ПараметрыОперации.ПлатежнаяСсылка,
		СтрЗаменить(ДанныеКартинки, Символы.ВК + Символы.ПС, ""),
		Подпись);
	
КонецПроцедуры

// Заполняет список получателей сообщения с платежной ссылкой.
// См. СистемаБыстрыхПлатежейПереопределяемый.ПриФормированииСпискаПолучателейСообщенияСБП
//
Процедура ПриФормированииСпискаПолучателейСообщенияСБП(ДокументОперации, ВариантОтправки, Получатели) Экспорт
	
	Если ВариантОтправки = "ЭлектроннаяПочта" Тогда
		Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОперации, "Контрагент");
		СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Контрагент);
		Получатели.Добавить(СведенияОКонтрагенте.Email);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Возвраты

// Определяются данные для формирования запроса на возврат в платежную систему СБП.
// Все поля переменной ЗаказНаВозврат обязательны для заполнения.
// См. ПереводыСБПc2bПереопределяемый.ПриФормированииЗаказаНаВозвратСБП
//
Процедура ПриФормированииДокументаНаВозвратСБП(ДокументВозврата,
											   ДокументНаВозврат,
											   НастройкаИнтеграции,
											   ДополнительныеПараметры) Экспорт
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ДокументНаВозврат, ДополнительныеПараметры, "СуммаВозврата, ДатаВозврата");
		Возврат;
		
	КонецЕсли;
	
	Если ТипЗнч(ДокументВозврата) <> Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументВозврата, "СуммаДокумента, Дата");
		ДокументНаВозврат.СуммаВозврата = РеквизитыДокумента.СуммаДокумента;
		ДокументНаВозврат.ДатаВозврата  = РеквизитыДокумента.Дата;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ФункционалСБПДоступен() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ФОДоступна = Константы.ИспользоватьОплатуПоПлатежнымКартам.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ФОДоступна Тогда
		// Механизм СБП использует в работе функционал эквайринга.
		// Если он ещё не включен, то будет включен принудительно, во время настройки подключения СБП.
		// Но включения функционала эквайринга может быть недоступно из-за тарифных ограничений, поэтому проверим
		// возможность записи заранее, чтобы пользователь не столкнулся с этим после выполнения всех шагов настройки.
		
		НачатьТранзакцию();
		
		Попытка
			УстановитьПривилегированныйРежим(Истина);
			Константы.ИспользоватьОплатуПоПлатежнымКартам.Установить(Истина);
			УстановитьПривилегированныйРежим(Ложь);
			ФОДоступна = Истина;
		Исключение
			ФОДоступна = Ложь;
		КонецПопытки;
		
		ОтменитьТранзакцию();
		
	КонецЕсли;
	
	Возврат ФОДоступна;
	
КонецФункции

Функция НастройкаИнтеграцииСБППоБанковскомуСчету(Организация, БанковскийСчет, Вид = "c2b") Экспорт
	
	НастройкаИнтеграции = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат НастройкаИнтеграции;
	КонецЕсли;
	
	Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Банк");
	
	НастройкаИнтеграции = НастройкаИнтеграцииСБППоБанку(Организация, Банк, Вид);
	
	Возврат НастройкаИнтеграции;
	
КонецФункции

Функция НастройкаИнтеграцииСБППоБанку(Организация, Банк, Вид = "c2b") Экспорт
	
	НастройкаИнтеграции = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(Банк) Тогда
		Возврат НастройкаИнтеграции;
	КонецЕсли;
	
	БИКБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Код");
	
	НастройкаИнтеграции = НастройкаИнтеграцииСБП(Организация, БИКБанка, Вид);
	
	Возврат НастройкаИнтеграции;
	
КонецФункции

Функция НастройкаИнтеграцииСБП(Организация, БИКБанка, Вид = "c2b") Экспорт
	
	ВключаяАгрегаторы   = Вид = "c2b";
	НастройкиИнтеграции = НастройкиИнтеграцииСБП(Организация, БИКБанка, ВключаяАгрегаторы);
	
	Если Вид = "c2b"
		И НастройкиИнтеграции.ИнтеграцияДоступнаСБПc2b И ЗначениеЗаполнено(НастройкиИнтеграции.Настройка) Тогда
		НастройкаИнтеграции = НастройкиИнтеграции.Настройка;
	ИначеЕсли Вид = "b2b"
		И НастройкиИнтеграции.ИнтеграцияДоступнаСБПb2b И ЗначениеЗаполнено(НастройкиИнтеграции.Настройкаb2b) Тогда
		НастройкаИнтеграции = НастройкиИнтеграции.Настройкаb2b;
	Иначе
		НастройкаИнтеграции = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ПустаяСсылка();
	КонецЕсли;
	
	Возврат НастройкаИнтеграции;
	
КонецФункции

Функция НастройкиИнтеграцииСБП(Организация, БИКБанка, ВключаяАгрегаторы = Ложь, ИзФормыНастройки = Ложь, ВсеНастройки = Ложь) Экспорт
	
	НастройкиИнтеграции = Новый Структура;
	НастройкиИнтеграции.Вставить("ИнтеграцияДоступнаСБПc2b", Ложь);
	НастройкиИнтеграции.Вставить("ИнтеграцияДоступнаСБПb2b", Ложь);
	НастройкиИнтеграции.Вставить("Настройка");
	НастройкиИнтеграции.Вставить("Настройкаb2b");
	
	Если Не ЗначениеЗаполнено(БИКБанка) Тогда
		Возврат НастройкиИнтеграции;
	КонецЕсли;
	
	Если ВсеНастройки Тогда
		НастройкиИнтеграции.Настройка    = Новый Массив;
		НастройкиИнтеграции.Настройкаb2b = Новый Массив;
	КонецЕсли;
	
	ПолучитьНастройкиСБПсБанком(НастройкиИнтеграции, Организация, БИКБанка);
	
	Если (Не ЗначениеЗаполнено(НастройкиИнтеграции.Настройка) Или ВсеНастройки) И ВключаяАгрегаторы Тогда
		// По выбранному счету к СБП настройки подключения c2b нет.
		// Поищем настройку подключения с платежными агрегаторами, к которым подключена организация.
		ЗаполнитьНастройкуСБПсАгрегатором(НастройкиИнтеграции, Организация, БИКБанка);
		
		Если Не ЗначениеЗаполнено(НастройкиИнтеграции.Настройка) И Не ИзФормыНастройки Тогда
			// Банк интеграцию c2b не поддерживает и нет настройки с агрегатором.
			// Сообщим об этом и предложим использовать агрегатор.
			НастройкиИнтеграции.ИнтеграцияДоступнаСБПc2b = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкиИнтеграции;
	
КонецФункции

Функция QRКодСБП(ДанныеДокумента) Экспорт
	
	ВариантНастройки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ДанныеДокумента.НастройкаИнтеграцииСБП, "ВариантНастройки");
	
	Если ВариантНастройки = Перечисления.ВариантыНастройкиСБП.b2b Тогда
		РезультатОперации = ПереводыСБПb2b.ДинамическаяСсылка(
			ДанныеДокумента.Документ,
			ДанныеДокумента.НастройкаИнтеграцииСБП);
	Иначе
		РезультатОперации = ПереводыСБПc2b.ДинамическаяСсылка(
			ДанныеДокумента.Документ,
			ДанныеДокумента.НастройкаИнтеграцииСБП);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатОперации.ПлатежнаяСсылка) Тогда
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			СообщениеОбОшибке = ?( ЗначениеЗаполнено(РезультатОперации.СообщениеОбОшибке),
				РезультатОперации.СообщениеОбОшибке, РезультатОперации.ИнформацияОбОшибке);
			ЗаписьЖурналаРегистрации(НСтр("ru = 'ИнтеграцияССБПБП.QRКодСБП'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.СчетНаОплатуПокупателю,
				ДанныеДокумента.Документ,
				СообщениеОбОшибке);
			ТекстОшибки = НСтр("ru = 'Не удалось сформировать QR-код для СБП. Подробности в Журнале регистрации'",
				ОбщегоНазначения.КодОсновногоЯзыка());
		Иначе
			ТекстОшибки = НСтр("ru = 'Не удалось сформировать QR-код для СБП.'",
				ОбщегоНазначения.КодОсновногоЯзыка());
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		
		Возврат "";
	КонецЕсли;
	
	ДанныеQRКода = СистемаБыстрыхПлатежей.ИзображениеQRКодаСБП(
		РезультатОперации.ПлатежнаяСсылка,
		150,
		2);
	
	Возврат ДанныеQRКода;
	
КонецФункции

#Область ПрикладныеОперации

#Область ВозвратыОтмены

// Производит возврат продажи платежной системе на основании чека продажи или идентификатора оплаты.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - параметры выполнения длительной операции;
//  АдресРезультата - Строка - адрес результата операции во временном хранилище.
//
Процедура ВозвратОплаты(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ПараметрыПроцедуры.ДокументВозврата);
	
	ДокументНаВозврат = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыПроцедуры.ДокументВозврата) Тогда
		ДокументНаВозврат = Новый Структура("ДатаВозврата, СуммаВозврата");
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПараметрыПроцедуры.ДокументВозврата, "СуммаДокумента, Дата");
		ДокументНаВозврат.СуммаВозврата = РеквизитыДокумента.СуммаДокумента;
		ДокументНаВозврат.ДатаВозврата  = РеквизитыДокумента.Дата;
	КонецЕсли;
	
	РезультатОперации = ПереводыСБПc2b.ВозвратОплаты(
		ПараметрыПроцедуры.ДокументВозврата,
		?(ЗначениеЗаполнено(ПараметрыПроцедуры.ДокументОплаты),
			ПараметрыПроцедуры.ДокументОплаты,
			ПараметрыПроцедуры.ИдентификаторОплаты),
		ПараметрыПроцедуры.НастройкаИнтеграции,
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыПроцедуры, "УчастникСБП", Неопределено), ДокументНаВозврат);
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Производит получение статуса возврата по документу.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - параметры выполнения длительной операции;
//  АдресРезультата - Строка - адрес результата операции во временном хранилище.
//  ДлительныйВызов - Булево - если Истина, получение статуса оплаты будет выполнятся
//    в цикле. Длительность выполнения операции определяется на основании
//    данных константы ДлительностьОперацииСистемыБыстрыхПлатежей.
//
Процедура СтатусВозвратОплаты(ПараметрыПроцедуры, АдресРезультата, ДлительныйВызов = Истина) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ПараметрыПроцедуры.ДокументВозврата);
	
	РезультатОперации = ПереводыСБПc2b.СтатусВозврата(
		ПараметрыПроцедуры.ДокументВозврата,
		ПараметрыПроцедуры.НастройкаПодключения,
		ДлительныйВызов);
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Подтверждает возврат в платежной системе.
//
// Параметры:
//  ДокументОплаты - ОпределяемыйТип.ДокументОперацииБИП - документ, который отражает
//                   оплату в информационной базе;
//  НастройкаИнтеграции - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//                  настройка выполнения операции платежной системы.
//
// Возвращаемое значение:
//  Структура - см. ПереводыСБПc2b.ПодтвердитьВозврат.
//
Функция ПодтвердитьВозврат(ДокументВозврата, НастройкаИнтеграции) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ДокументВозврата);
	
	Возврат ПереводыСБПc2b.ПодтвердитьВозврат(
		ДокументВозврата,
		НастройкаИнтеграции);
	
КонецФункции

Функция ДанныеВозвратОплаты(ДанныеДокумента) Экспорт
	
	Результат = НовыйДанныеВозвратОплаты();
	
	Если Не ДанныеДокумента.ЭтоВозвратПоСБП Тогда
		Возврат Результат;
	КонецЕсли;
	
	НастройкаПодключения = НастройкаИнтеграцииСБППоБанковскомуСчету(
		ДанныеДокумента.Организация, ДанныеДокумента.БанковскийСчет);
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.Доступен = Истина;
	
	Если Не ЗначениеЗаполнено(ДанныеДокумента.Ссылка) Тогда
		// Это новый документ возврата, а значит сам возврат еще не выполнен. Поэтому команда возврата должна быть доступна.
		Возврат Результат;
	КонецЕсли;
	
	Операции = ПереводыСБПc2b.ОперацииПоДокументу(ДанныеДокумента.Ссылка);
	Если ЗначениеЗаполнено(Операции.ДанныеОпераций) Тогда
		ОперацияСБП = Операции.ДанныеОпераций[0];
		Результат.Доступен = Не ОперацияСБП.Оплата;
		Если Результат.Доступен Тогда
			Результат.Исполнен    = ОперацияСБП.СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииВыполнена();
			Результат.Отклонен    = ОперацияСБП.СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОтклонена();
			Результат.Отменена    = ОперацияСБП.СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОтменена();
			Результат.Ошибка      = ОперацияСБП.СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииОшибка();
			Результат.Выполняется = ОперацияСБП.СтатусОперации = СистемаБыстрыхПлатежейКлиентСервер.СтатусОперацииВыполняется();
			Результат.ТекстПредупреждения = ТекстПредупрежденияВозвратаСБП(Результат);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СчетОплачен(СчетНаОплату, СсылочныйНомер) Экспорт
	
	Результат = Ложь;
	
	Операции = ПереводыСБПc2b.ОперацииПоДокументу(СчетНаОплату);
	Если ЗначениеЗаполнено(Операции.ДанныеОпераций) Тогда
		Для Каждого Операция Из Операции.ДанныеОпераций Цикл
			Если Операция.ИдентификаторОперации = СсылочныйНомер Тогда
				Результат = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РеквизитыСчетаНаОплатуПоРозничнойПродаже(РозничнаяПродажа, РеквизитыСчета, ДанныеДокумента, ШаблонНазначения)
	
	РеквизитыСчета.СуммаОплаты = ДанныеДокумента.СуммаДокумента;
	РеквизитыСчета.ДатаОплаты  = ДанныеДокумента.Дата;
	
	ОрганизацияНаименование = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
		ДанныеДокумента.Организация).НаименованиеДляПечатныхФорм;
	
	Если ШаблонНазначения = Неопределено Тогда
		РеквизитыСчета.НазначениеПлатежа = СтрШаблон(НСтр("ru = 'Оплата %1 по чеку от %2. Сумма %3.'"),
			ОрганизацияНаименование,
			Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yy"),
			Формат(ДанныеДокумента.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ="));
	Иначе
		РеквизитыСчета.НазначениеПлатежа = СтрЗаменить(
			ШаблонНазначения.НазначениеПлатежа,
			"[НаименованиеОрганизации]",
			ОрганизацияНаименование);
		РеквизитыСчета.НазначениеПлатежа = СтрЗаменить(
			РеквизитыСчета.НазначениеПлатежа,
			"[ДатаЧека]",
			Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yy"));
		РеквизитыСчета.НазначениеПлатежа = СтрЗаменить(
			РеквизитыСчета.НазначениеПлатежа,
			"[СуммаЧека]",
			Формат(ДанныеДокумента.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ="));
	КонецЕсли;
	
	РеквизитыСчета.ОтложенноеПолучениеСтатуса = Ложь;
	
КонецПроцедуры

Процедура РеквизитыСчетаНаОплатуПоСчетуПокупателю(СчетНаОплату, РеквизитыСчета, ШаблонНазначения)
	
	Если Не ЗначениеЗаполнено(СчетНаОплату) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СчетНаОплату,
		"Организация, СуммаДокумента, Дата, Номер, ДокументБезНДС, СтруктурнаяЕдиница");
	
	МаксимальныйСрокЖизниQRКода = 90 * 24* 60;
	СрокОплаты = СрокиОплатыДокументов.УстановленныйСрокОплаты(СчетНаОплату, ДанныеДокумента.Организация);
	ТекДата = ТекущаяДатаСеанса();
	Если ЗначениеЗаполнено(СрокОплаты) Тогда
		СрокОплаты = КонецДня(СрокОплаты);
		Если ТекДата < СрокОплаты Тогда
			РеквизитыСчета.СрокЖизниQRКода = Окр((СрокОплаты - ТекДата) / 60); // в целых минутах
		Иначе
			РеквизитыСчета.СрокЖизниQRКода = Окр((СрокОплаты - ДанныеДокумента.Дата) / 60);
		КонецЕсли;
		
		Если РеквизитыСчета.СрокЖизниQRКода > МаксимальныйСрокЖизниQRКода Тогда
			РеквизитыСчета.СрокЖизниQRКода = МаксимальныйСрокЖизниQRКода;
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыСчета.СуммаОплаты = ДанныеДокумента.СуммаДокумента;
	РеквизитыСчета.ДатаОплаты  = ДанныеДокумента.Дата;
	
	Если РеквизитыСчета.Свойство("РасчетныйСчет") Тогда
		РеквизитыСчета.РасчетныйСчет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ДанныеДокумента.СтруктурнаяЕдиница, "НомерСчета");
	КонецЕсли;
	
	Если РеквизитыСчета.Свойство("ОблагаетсяНДС") И РеквизитыСчета.Свойство("СуммаНДС") Тогда
		РеквизитыСчета.ОблагаетсяНДС = Не ДанныеДокумента.ДокументБезНДС;
		Если РеквизитыСчета.ОблагаетсяНДС Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", СчетНаОплату);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СУММА(ЕСТЬNULL(СчетНаОплатуПокупателюТовары.СуммаНДС, 0)) КАК СуммаНДС
			|ИЗ
			|	Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателюТовары
			|ГДЕ
			|	СчетНаОплатуПокупателюТовары.Ссылка = &Ссылка";
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				РеквизитыСчета.СуммаНДС = Выборка.СуммаНДС;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОрганизацияНаименование = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
		ДанныеДокумента.Организация).НаименованиеДляПечатныхФорм;
	
	Если ШаблонНазначения = Неопределено Тогда
		Если РеквизитыСчета.Свойство("СуммаНДС") И ЗначениеЗаполнено(РеквизитыСчета.СуммаНДС) Тогда
			РеквизитыСчета.НазначениеПлатежа = СтрШаблон(НСтр("ru = 'Оплата через СБП по счету № %1 от %2. Сумма %3, в т.ч. НДС %4.'"),
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер, Истина, Ложь),
				Формат(РеквизитыСчета.ДатаОплаты, "ДФ=dd.MM.yyyy"),
				Формат(РеквизитыСчета.СуммаОплаты, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ="),
				Формат(РеквизитыСчета.СуммаНДС, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ="));
		Иначе
			РеквизитыСчета.НазначениеПлатежа = СтрШаблон(НСтр("ru = 'Оплата через СБП по счету № %1 от %2. Сумма %3.'"),
				ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер, Истина, Ложь),
				Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy"),
				Формат(ДанныеДокумента.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ="));
		КонецЕсли;
	Иначе
		РеквизитыСчета.НазначениеПлатежа = СтрЗаменить(
			ШаблонНазначения.НазначениеПлатежа,
			"[НаименованиеОрганизации]",
			ОрганизацияНаименование);
		РеквизитыСчета.НазначениеПлатежа = СтрЗаменить(
			РеквизитыСчета.НазначениеПлатежа,
			"[НомерСчета]",
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер, Истина, Ложь));
		РеквизитыСчета.НазначениеПлатежа = СтрЗаменить(
			РеквизитыСчета.НазначениеПлатежа,
			"[ДатаСчета]",
			Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy"));
		РеквизитыСчета.НазначениеПлатежа = СтрЗаменить(
			РеквизитыСчета.НазначениеПлатежа,
			"[СуммаСчета]",
			Формат(ДанныеДокумента.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ="));
		Если РеквизитыСчета.Свойство("СуммаНДС") И ЗначениеЗаполнено(РеквизитыСчета.СуммаНДС) Тогда
			РеквизитыСчета.НазначениеПлатежа = СтрЗаменить(
				РеквизитыСчета.НазначениеПлатежа,
				"[НДС]",
				Формат(РеквизитыСчета.СуммаНДС, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ="));
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыСчета.ОтложенноеПолучениеСтатуса = Истина;
	
КонецПроцедуры

// Возвращает полные имена объектов метаданных, которые могут выступать в качестве оснований платежа через СБП.
//
// Возвращаемое значение:
//  Массив - имена (Строка) объектов метаданных оснований платежа.
//
Функция ОснованияПлатежа()
	
	ИменаОснованийПлатежа = Новый Массив;
	ИменаОснованийПлатежа.Добавить("Документ.СчетНаОплатуПокупателю");
	
	Возврат ИменаОснованийПлатежа;
	
КонецФункции

Функция ПолучитьНастройки(Результат, ДанныеОснования, ДанныеБанкСчета, ВсеНастройки = Ложь)
	
	НастройкиСБП = Неопределено;
	
	Организация = ДанныеОснования.Организация;
	Банк = ДанныеБанкСчета.Банк;
	ДанныеБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Банк, "Код, Наименование");
	БИКБанка = ДанныеБанка.Код;
	НаименованиеБанка = ДанныеБанка.Наименование;
	
	ВключаяАгрегаторы = ДанныеОснования.КонтрагентЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
		И Не ДанныеОснования.КонтрагентИндивидуальныйПредприниматель;
	НастройкиИнтеграции = НастройкиИнтеграцииСБП(Организация, БИКБанка, ВключаяАгрегаторы,, ВсеНастройки);
	
	Если ДанныеОснования.КонтрагентЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо
		Или ДанныеОснования.КонтрагентИндивидуальныйПредприниматель Тогда
		Если Не НастройкиИнтеграции.ИнтеграцияДоступнаСБПb2b Тогда
			ШаблонСообщения = НСтр("ru = 'Оплата через СБП (b2b) для %1 пока не поддерживается.'");
			Результат.СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, НаименованиеБанка);
			Результат.ИнтеграцияДоступна = Ложь;
		ИначеЕсли ЗначениеЗаполнено(НастройкиИнтеграции.Настройкаb2b) Тогда
			НастройкиСБП = НастройкиИнтеграции.Настройкаb2b;
		ИначеЕсли Не СистемаБыстрыхПлатежей.НастройкаПодключенияДоступна() Тогда
			ТекстОшибки = НСтр("ru = 'Не настроено подключение к СБП (b2b). Обратитесь к адмнистратору.'");
			Результат.СообщениеОбОшибке = ТекстОшибки;
			Результат.ИнтеграцияДоступна = Ложь;
		КонецЕсли;
	Иначе
		Если Не НастройкиИнтеграции.ИнтеграцияДоступнаСБПc2b Тогда
			ШаблонСообщения = НСтр("ru = 'Оплата через СБП для %1 пока не поддерживается.
				|Рекомендуем использовать СБП через платежный агрегатор.'");
			Результат.СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, НаименованиеБанка);
			Результат.ИнтеграцияДоступна = Ложь;
		ИначеЕсли ЗначениеЗаполнено(НастройкиИнтеграции.Настройка) Тогда
			НастройкиСБП = НастройкиИнтеграции.Настройка;
		ИначеЕсли Не СистемаБыстрыхПлатежей.НастройкаПодключенияДоступна() Тогда
			ТекстОшибки = НСтр("ru = 'Не настроено подключение к СБП. Обратитесь к адмнистратору.'");
			Результат.СообщениеОбОшибке = ТекстОшибки;
			Результат.ИнтеграцияДоступна = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкиСБП;
	
КонецФункции

Процедура ЗаполнитьНастройкуСБПсАгрегатором(НастройкиИнтеграции, Организация, БИКБанка)
	
	// Получим все доступные, для текущего пользователя настройки, до установки привилегированного режима.
	ВсеНастройки = РегистрыСведений.СоответствиеНастроекИнтеграции.ТаблицаНастроекИнтеграцииПоОрганизации(Организация);
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Сначала найдем все существующие настройки интеграции с платежными агрегаторами - у таких систем нет БИК.
	НастройкиПлатежныхАгрегаторов = Новый Массив;
	Для Каждого Настройка Из ВсеНастройки Цикл
		НастройкиПодключения = СистемаБыстрыхПлатежей.НастройкиПодключения(Настройка.НастройкаИнтеграции);
		Если НастройкиПодключения.НастройкиСБПc2b.ПлатежныйАгрегатор
			И (Не ЗначениеЗаполнено(Настройка.БИКБанка) Или Настройка.БИКБанка = БИКБанка) Тогда
			НастройкиПлатежныхАгрегаторов.Добавить(Настройка.НастройкаИнтеграции);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого НастройкаПлатежнойСистемы Из НастройкиПлатежныхАгрегаторов Цикл
		Настройка = РегистрыСведений.СоответствиеНастроекИнтеграции.НастройкиТорговойТочки(НастройкаПлатежнойСистемы);
		Если Настройка.Организация = Организация Тогда
			НастройкиИнтеграции.ИнтеграцияДоступнаСБПc2b = Истина;
			Если ТипЗнч(НастройкиИнтеграции.Настройка) = Тип("Массив") Тогда
				НастройкиИнтеграции.Настройка.Добавить(НастройкаПлатежнойСистемы);
			Иначе
				НастройкиИнтеграции.Настройка = НастройкаПлатежнойСистемы;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НастройкиИнтеграции.ИнтеграцияДоступнаСБПc2b Тогда
		Возврат;
	КонецЕсли;
	
	// Банк не поддерживает интеграцию и нет ни одной настройки, но возможно подключение через платежных агрегаторов.
	НастройкиИнтеграции.ИнтеграцияДоступнаСБПc2b = Истина;
	
КонецПроцедуры

Функция ПолучитьНастройкиСБПсБанком(НастройкиИнтеграции, Организация, БИКБанка)
	
	УстановитьПривилегированныйРежим(Истина);
	НастройкиСБП = СистемаБыстрыхПлатежей.ПараметрыПодключенияПоБИК(БИКБанка);
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗаполнитьЗначенияСвойств(НастройкиИнтеграции, НастройкиСБП);
	
	Если НастройкиСБП.ИнтеграцияДоступнаСБПc2b Тогда
		НастройкиИнтеграции.ИнтеграцияДоступнаСБПb2b =
			НастройкиСБП.ПараметрыУчастникаСБП.НастройкаПодключения.Количество() > 0;
		
		Для Каждого Настройка Из НастройкиСБП.ПараметрыУчастникаСБП.НастройкаПодключения Цикл
			Если ПодключениеКСБПВыполнено(Организация, Настройка, "c2b") Тогда
				Если ТипЗнч(НастройкиИнтеграции.Настройка) = Тип("Массив") Тогда
					НастройкиИнтеграции.Настройка.Добавить(Настройка);
				Иначе
					НастройкиИнтеграции.Настройка = Настройка;
				КонецЕсли;
			КонецЕсли;
			
			Если ПодключениеКСБПВыполнено(Организация, Настройка, "b2b") Тогда
				Если ТипЗнч(НастройкиИнтеграции.Настройкаb2b) = Тип("Массив") Тогда
					НастройкиИнтеграции.Настройкаb2b.Добавить(Настройка);
				Иначе
					НастройкиИнтеграции.Настройкаb2b = Настройка;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецФункции

// Заменяет спецсимволы HTML на их ссылки-мнемоники.
//
// Параметры:
// Текст - Строка - Текст, который необходимо преобразовать для вставки в HTML
//
// Возвращаемое значение:
// Строка - текст, в котором спецсимволы HTML заменены на свои ссылки-мненмоники.
//
Функция НормализоватьСтрокуДляHTML(Значение)
	
	Значение = СтрЗаменить(Значение, "&", "&amp;");
	Значение = СтрЗаменить(Значение, "<", "&lt;");
	Значение = СтрЗаменить(Значение, ">", "&gt;");
	Значение = СтрЗаменить(Значение, """", "&quot;");
	Значение = СтрЗаменить(Значение, "'", "&apos;");
	
	Возврат Значение;
	
КонецФункции

// Проверяет документ операции, в случае ошибки вызывает исключение.
//
// Параметры:
//  ДокументОплаты - ОпределяемыйТип.ДокументОперацииБИП - документ, который отражает
//                   оплату в информационной базе;
//
Процедура ПередВыполнениемОперацииВПлатежнойСистеме(ДокументОперации)
	
	Если ТипЗнч(ДокументОперации) <> Тип("ДокументСсылка.СчетНаОплатуПокупателю")
		И ТипЗнч(ДокументОперации) <> Тип("ДокументСсылка.ОплатаПлатежнойКартой") Тогда
		ВызватьИсключение НСтр("ru = 'Оплата в платежной системе для документа не поддерживается.'");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДокументОперации) Тогда
		ВызватьИсключение НСтр("ru = 'Перед выполнением оплаты запишите документ.'")
	КонецЕсли;
	
КонецПроцедуры

// Создает документ оплаты по счету, по которому пришло потверждение оплаты по СБП.
//
// Параметры:
//  Счет - ДокументыСсылка.СчетНаОплатуПокупателю - документ, по которому пришло подтверждение оплаты по СБП
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//    настройка выполнения операций.
//  РезультатОбработки - Структура - результат загрузки статуса операции:
//    * СтатусОперации - Строка - текущее состояние операции операции. Для проверки статуса
//      операции, необходимо функции программного интерфейса общего модуля
//      СистемаБыстрыхПлатежейКлиентСервер. Возможные значения:
//        - "Отменена" - по ранее сформированная операция отменена НСПК;
//        - "Выполнена" - участник СБП подтвердил выполнение операции;
//        - "Ошибка" - не удалось выполнить проверку статуса операции из-за ошибки
//           или участник СБП вернул ошибку;
//    * ПараметрыОперации - Структура - дополнительные данные по оплате:
//        * ДатаОперации - Дата - фактическая дата оплаты в UTC;
//        * СуммаОперации - Число - фактическая суммы операции по документу;
///        * ИдентификаторОперации - Строка - идентификатор сформированной операции;
//    * СообщениеОбОшибке - Строка - сообщение пользователю. Заполняется в случае ошибки.
//
Процедура СоздатьОперациюПоПлатежнойКарте(Счет, НастройкаПодключения, РезультатОбработки)
	
	Если Не ЗначениеЗаполнено(Счет) Тогда
		Возврат; // Загружаются платежи только по имеющимся в базе счетам.
	КонецЕсли;
	
	// БИП всегда приводит даты операций к UTC. Приведем дату обратно к локальному часовому поясу.
	ДатаОперацииВUTC = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		РезультатОбработки.ПараметрыОперации, "ДатаОперации");
	ДатаОперации  = ЛокальнаяДатаСоСмещениемПоЧасовомуПоясу(ДатаОперацииВUTC);
	СуммаОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатОбработки.ПараметрыОперации, "СуммаОперации");
	ИдентификаторПлатежа = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		РезультатОбработки.ПараметрыОперации, "ИдентификаторОперации");
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		НастройкиПодключения = ПереводыСБПc2b.НастройкиПодключенияОперации(Счет);
		Если НастройкиПодключения.Количество() > 0 Тогда
			НастройкаПодключения = НастройкиПодключения[0];
		КонецЕсли;
	КонецЕсли;
	
	ЭтоСБПb2b = НастройкаПодключения.ВариантНастройки = Перечисления.ВариантыНастройкиСБП.b2b;
	
	ВыпискаЗагружаетсяКакЭквайринг = Истина;
	Если ЭтоСБПb2b Тогда
		Настройки = РегистрыСведений.СоответствиеНастроекИнтеграцииСБПb2b.НастройкиТорговойТочки(НастройкаПодключения);
		ВидПоступленияОплатыВБанковскойВыписке = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			Настройки, "ВидПоступленияОплатыВБанковскойВыписке");
		ВыпискаЗагружаетсяКакЭквайринг =
			ВидПоступленияОплатыВБанковскойВыписке = Перечисления.ВидПоступленияОплатыСБПb2b.Эквайринг;
	Иначе
		Настройки = РегистрыСведений.СоответствиеНастроекИнтеграции.НастройкиТорговойТочки(НастройкаПодключения);
	КонецЕсли;
	
	ВидОплаты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Настройки, "ВидОплаты");
	
	Если Не ЗначениеЗаполнено(ИдентификаторПлатежа)
		И Не РезультатОбработки.ПараметрыОперации.Свойство("ИдентификаторОперации") Тогда
		Если ЭтоСБПb2b Тогда
			Операции = ПереводыСБПb2b.ОперацииПоДокументу(Счет);
		Иначе
			Операции = ПереводыСБПc2b.ОперацииПоДокументу(Счет);
		КонецЕсли;
		Если ЗначениеЗаполнено(Операции.ДанныеОпераций) Тогда
			ОперацияСБП = Операции.ДанныеОпераций[0];
			ИдентификаторПлатежа = ОперацияСБП.ИдентификаторОперации;
		КонецЕсли;
	КонецЕсли;
	
	ЭтоНовый = Ложь;
	ДокументОплатыСсылка = ДокументОплатыПоИдентификатору(ИдентификаторПлатежа);
	Если ЗначениеЗаполнено(ДокументОплатыСсылка) Тогда
		ДокументОплаты = ДокументОплатыСсылка.ПолучитьОбъект();
	Иначе
		ДокументОплаты = Документы.ОплатаПлатежнойКартой.СоздатьДокумент();
		ЭтоНовый = Истина;
	КонецЕсли;
	
	ДанныеСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Счет,
		"СуммаДокумента, ВалютаДокумента, Дата, Номер");
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ПроверитьЗаполнениеСуммы = ДанныеСчета.СуммаДокумента <> СуммаОперации;
	Если ЗначениеЗаполнено(СуммаОперации) И ПроверитьЗаполнениеСуммы
		И ДанныеСчета.ВалютаДокумента = ВалютаРеглУчета Тогда
		ДанныеОплаты = Новый Структура;
		ДанныеОплаты.Вставить("Основание",     Счет);
		ДанныеОплаты.Вставить("СуммаОперации", СуммаОперации);
		ДокументОплаты.Заполнить(ДанныеОплаты);
	Иначе
		ДокументОплаты.Заполнить(Счет);
	КонецЕсли;
	
	Если ВыпискаЗагружаетсяКакЭквайринг Тогда
		ДокументОплаты.ВидОперации = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя;
	Иначе
		ДокументОплаты.ВидОперации = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаСчетаЧерезСБП;
	КонецЕсли;
	
	Если ЭтоНовый Тогда
		Если ЗначениеЗаполнено(ДатаОперации) Тогда
			ДокументОплаты.Дата = ДатаОперации;
		Иначе
			ДокументОплаты.УстановитьВремя(РежимАвтоВремя.ТекущееИлиПоследним, Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидОплаты) Тогда
		БанковскийСчет = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Счет, "СтруктурнаяЕдиница");
		Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Банк");
		ВидОплаты = ПолучитьВидОплаты(ДокументОплаты.Организация, Банк);
	КонецЕсли;
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ВидОплаты",      ВидОплаты);
	ДанныеЗаполнения.Вставить("СсылочныйНомер", ИдентификаторПлатежа);
	
	ЗаполнитьЗначенияСвойств(ДокументОплаты, ДанныеЗаполнения);
	
	ВидОплатыРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДокументОплаты.ВидОплаты, "Контрагент, ДоговорКонтрагента, СчетУчетаРасчетов");
	ДокументОплаты.СчетКасса         = ВидОплатыРеквизиты.СчетУчетаРасчетов;
	ДокументОплаты.Эквайер           = ВидОплатыРеквизиты.Контрагент;
	ДокументОплаты.ДоговорЭквайринга = ВидОплатыРеквизиты.ДоговорКонтрагента;
	
	УстановитьДоговорКонтрагента(ДокументОплаты);
	
	Попытка
		ДокументОплаты.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ИнтеграцияССБПБП.СоздатьОперациюПоПлатежнойКарте'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.ОбщиеМодули.ИнтеграцияССБПБП,
			,
			НСтр("ru = 'Ошибка проведения документа ""Оплата платежной картой"" при загрузке операций из СБП'",
				ОбщегоНазначения.КодОсновногоЯзыка()));
			
		ДокументОплаты.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения", Истина);
		ДокументОплаты.Записать();
	КонецПопытки; 
	
	ИдентификаторУстройства = Неопределено;
	ОснованиеПечатиЧека = ДокументОплаты.Ссылка;
	
	Если ОблачныеКассы.ПробитиеЧековДоступно()
		И НЕ ПечатьФискальныхДокументовВызовСервера.ЕстьФискальныеОперацииПоДокументу(ОснованиеПечатиЧека) Тогда
	
		ИдентификаторУстройства = МенеджерОборудованияБП.ОблачнаяКассаДляАвтоматическогоПробитияЧеков(ДокументОплаты.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
	
		ПараметрыУстройства = МенеджерОборудованияВызовСервера.ДанныеУстройства(ИдентификаторУстройства);
		
		МассивЧеков = ПечатьФискальныхДокументов.СобратьДанныеЧеков(ОснованиеПечатиЧека);
		Если МассивЧеков.Количество() = 1 Тогда  
			ФискальнаяОперация = МассивЧеков[0];
			ФискальнаяОперация.ОблачнаяККТ = Истина;
			ПечатьФискальныхДокументовВызовСервера.ДобавитьЧекВОчередьЧековККТ(ФискальнаяОперация, ИдентификаторУстройства);
		КонецЕсли;
	КонецЕсли;
	
	Если ПроверитьЗаполнениеСуммы И ДанныеСчета.СуммаДокумента = ДокументОплаты.СуммаДокумента Тогда
		// Покупатель оплатил сумму, отличную от суммы счета, но заполнить документ оплаты этой суммой не вышло
		// и сейчас документ оплаты заполнен суммой счета, а не оплаченной суммой.
		// Это могло произойти по разным причинам, например:
		//  - не совпадение валюты оплаты и валюты счета на оплату
		//  - разными ставками НДС в счете на оплату
		МассивВалют = Новый Массив;
		МассивВалют.Добавить(ДанныеСчета.ВалютаДокумента);
		МассивВалют.Добавить(ДокументОплаты.ВалютаДокумента);
		НаименованияВалют = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивВалют, "Наименование");
		СуммаСчетаПрописью =
			Формат(ДанныеСчета.СуммаДокумента, "ЧДЦ=2") + " " + НаименованияВалют[ДанныеСчета.ВалютаДокумента].Наименование;
		СуммаОперацииПрописью =
			Формат(СуммаОперации, "ЧДЦ=2") + " " + НаименованияВалют[ДокументОплаты.ВалютаДокумента].Наименование;
		ШаблонОшибки =
			НСтр("ru = 'Сумма оплаты %1 по операции СБП не совпадает с суммой %2 по счету №%3 от %4.
				|В документ установлена сумма из счета'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки,
			СуммаОперацииПрописью,
			СуммаСчетаПрописью,
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеСчета.Номер, Истина, Ложь),
			Формат(ДанныеСчета.Дата, "ДЛФ=D"));
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'ИнтеграцияССБПБП.СоздатьОперациюПоПлатежнойКарте'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.Документы.ОплатаПлатежнойКартой,
			ДокументОплаты.Ссылка,
			ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДоговорКонтрагента(Объект)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаРасшифровки = Объект.РасшифровкаПлатежа[0];
	
	ПараметрыПлатежа = Новый Структура;
	ПараметрыПлатежа.Вставить("ОплатаВВалюте", Новый Структура("ЗначениеОтбора", Ложь));
	ВидыДоговоров = РаботаСДоговорамиКонтрагентовБПВызовСервера.ВидыДоговоровДокумента(
		Объект.ВидОперации, ТипЗнч(Объект.Ссылка));
	
	Если ЗначениеЗаполнено(СтрокаРасшифровки.ДоговорКонтрагента) Тогда
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаРасшифровки.ДоговорКонтрагента, "ВидДоговора");
		Если ВидыДоговоров.Найти(ВидДоговора) = Неопределено Тогда
			СтрокаРасшифровки.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		СтрокаРасшифровки.ДоговорКонтрагента, Объект.Контрагент, Объект.Организация, ВидыДоговоров, ПараметрыПлатежа);
	
	Если Не ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		СтрокаРасшифровки.ДоговорКонтрагента = РаботаСБанкамиБП.ДоговорКонтрагентБанка(Объект.Организация, Объект.Контрагент, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает ссылку на вид оплаты по расчетам с банком.
// Выбирается вид оплаты с контрагентом, ИНН которого соответствует ИНН банка-эквайера.
// Если подоходящий вид оплаты не найден, он создается с данными банка-эквайера,
// если включена соответствующая настройка.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация для поиска (создания) вида оплаты.
//  УчастникСБП - Структура, СправочникСсылка.Банки - Банк-эквайер или данные платежного агрегатора
//                                                    для поиска (создания) вида оплаты.
//  СоздаватьНовыеЭлементы - Булево - создавать Вид оплаты (и контрагента), если не найдены. По умолчанию Ложь.
//
// Возвращаемое значение:
//   СправочникСсылка.ВидыОплатОрганизаций - Вид оплаты.
//
Функция ПолучитьВидОплаты(Организация, УчастникСБП, СоздаватьНовыеЭлементы = Ложь)
	
	ДанныеБанка = РаботаСБанкамиБП.ПолучитьДанныеКонтрагентаБанка(УчастникСБП, СоздаватьНовыеЭлементы);
	Контрагент  = ДанныеБанка.Ссылка;
	Договор     = РаботаСБанкамиБП.ДоговорКонтрагентБанка(Организация, Контрагент, СоздаватьНовыеЭлементы);
	ВидОплаты   = НайтиВидОплаты(Организация, Контрагент);
	Если СоздаватьНовыеЭлементы И Не ЗначениеЗаполнено(ВидОплаты) Тогда
		ВидОплаты = СоздатьВидОплаты(Организация, Контрагент, Договор, ДанныеБанка.Наименование);
	КонецЕсли;
	
	Возврат ВидОплаты;
	
КонецФункции

Функция ДокументОплатыПоИдентификатору(ИдентификаторПлатежа)
	
	Результат = Неопределено;
	Если Не ЗначениеЗаполнено(ИдентификаторПлатежа) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылочныйНомер", ИдентификаторПлатежа);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОплатаПлатежнойКартой.Ссылка КАК Ссылка,
	|	ОплатаПлатежнойКартой.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.ОплатаПлатежнойКартой КАК ОплатаПлатежнойКартой
	|ГДЕ
	|	ОплатаПлатежнойКартой.СсылочныйНомер = &СсылочныйНомер
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПодключениеКСБПВыполнено(Организация, Настройка, Вариант)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Настройка",   Настройка);
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле
	|ИЗ
	|	РегистрСведений.СоответствиеНастроекИнтеграции КАК СоответствиеНастроекИнтеграции
	|ГДЕ
	|	СоответствиеНастроекИнтеграции.Организация = &Организация
	|	И СоответствиеНастроекИнтеграции.ТорговаяТочка = &Настройка
	|	И СоответствиеНастроекИнтеграции.ТорговаяТочка.Используется";
	
	Если Вариант = "b2b" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СоответствиеНастроекИнтеграции", "СоответствиеНастроекИнтеграцииСБПb2b");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Не Запрос.Выполнить().Пустой();
	
	Возврат Результат;
	
КонецФункции

Функция НайтиВидОплаты(Организация, Контрагент)
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Справочники.ВидыОплатОрганизаций.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент",  Контрагент);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	ВидыОплатОрганизаций.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыОплатОрганизаций КАК ВидыОплатОрганизаций
	|ГДЕ
	|	ВидыОплатОрганизаций.Контрагент = &Контрагент
	|	И ВидыОплатОрганизаций.Организация = &Организация
	|	И НЕ ВидыОплатОрганизаций.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Возвращаем найденный вид оплаты, только если он один.
	Если Выборка.Количество() > 1 Тогда
		Возврат Справочники.ВидыОплатОрганизаций.ПустаяСсылка();
	КонецЕсли;
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

Функция СоздатьВидОплаты(Организация, Контрагент, Договор, Наименование)
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Справочники.ВидыОплатОрганизаций.ПустаяСсылка();
	КонецЕсли;
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ТипОплаты",   Перечисления.ТипыОплат.СБП);
	ДанныеЗаполнения.Вставить("Организация", Организация);
	ДанныеЗаполнения.Вставить("Контрагент",  Контрагент);
	
	ВидОплаты = Справочники.ВидыОплатОрганизаций.СоздатьЭлемент();
	ВидОплаты.Заполнить(ДанныеЗаполнения);
	ВидОплаты.ДоговорКонтрагента = Договор;
	ВидОплаты.Наименование = СтрШаблон(НСтр("ru = 'СБП с %1'"), Наименование);
	
	Попытка
		ВидОплаты.Записать();
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'ИнтеграцияССБПБП.НайтиИлиСоздатьВидОплаты'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.ОбщиеМодули.ИнтеграцияССБПБП,
			,
			НСтр("ru = 'Ошибка создания вида оплаты при настройке интеграции с СБП'",
				ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат Справочники.ВидыОплатОрганизаций.ПустаяСсылка();
	КонецПопытки;
	
	Возврат ВидОплаты.Ссылка;
	
КонецФункции

Функция ЗаблокироватьПолучениеСтатусов(Настройки)
	
	НастройкиЗаблокированы = Ложь;
	
	Попытка
		Для Каждого Настройка Из Настройки Цикл
			ЗаблокироватьДанныеДляРедактирования(Настройка);
			НастройкиЗаблокированы = Истина;
		КонецЦикла;
	Исключение
		НастройкиЗаблокированы = Ложь;
	КонецПопытки;
	
	Возврат НастройкиЗаблокированы;
	
КонецФункции

Процедура РазблокироватьПолучениеСтатусов(Настройки)
	
	Для Каждого Настройка Из Настройки Цикл
		РазблокироватьДанныеДляРедактирования(Настройка);
	КонецЦикла;
	
КонецПроцедуры

// Преобразует дату в UTC в дату локальную дату со смещением по часовому поясу.
//
// Параметры:
//  Значение - Дата - исходная дата.
//
// Возвращаемое значение:
//  Дата - результат преобразования.
//
Функция ЛокальнаяДатаСоСмещениемПоЧасовомуПоясу(ДатаВUTC) Экспорт
	
	Возврат ДатаВUTC + СмещениеСтандартногоВремени(ЧасовойПоясСеанса(), ДатаВUTC);
	
КонецФункции

Функция НовыйДанныеВозвратОплаты()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Исполнен",    Ложь);
	Результат.Вставить("Доступен",    Ложь);
	Результат.Вставить("Ошибка",      Ложь);
	Результат.Вставить("Отклонен",    Ложь);
	Результат.Вставить("Отменена",    Ложь);
	Результат.Вставить("Выполняется", Ложь);
	Результат.Вставить("ТекстПредупреждения", "");
	
	Возврат Результат;
	
КонецФункции

Функция ТекстПредупрежденияВозвратаСБП(СостояниеВозврата)
	
	ТекстПредупреждения = "";
	
	Если СостояниеВозврата.Отклонен Тогда
		ТекстПредупреждения = НСтр("ru = 'Возврат средств по СБП отклонен.'");
	ИначеЕсли СостояниеВозврата.Ошибка Тогда
		ТекстПредупреждения = НСтр("ru = 'При возврате средств по СБП произошла ошибка. Подробности в Журнале регистрации.'");
	ИначеЕсли СостояниеВозврата.Отменена Тогда
		ТекстПредупреждения = НСтр("ru = 'Операция по возврату средств по СБП не выполнена (отменена банком), подробности в Журнале регистрации. Повторите операцию.'");
	ИначеЕсли СостояниеВозврата.Выполняется Тогда
		ТекстПредупреждения = НСтр("ru = 'Возврат средств по СБП не завершен. Повторите операцию.'");
	КонецЕсли;
	
	Возврат ТекстПредупреждения;
	
КонецФункции

#КонецОбласти
