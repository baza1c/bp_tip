////////////////////////////////////////////////////////////////////////////////
// УчетЗарплатыКлиент: клиентские методы, используемые:
//               - при настройке зарплаты и кадров;
//               - при отражении зарплаты и налогов/взносов с ФОТ в учете;
//               - при получении сведений о документах подсистем учета зарплаты и кадров;
//               - ...
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПоказатьВопросПриОтменеПроведенияПлатежногоДокумента(Форма) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ВопросПередОтменойПроведенияЗавершение", Форма);
	ТекстВопроса = ТекстВопросаПриОтменеПроведенияПлатежногоДокумента();
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

Процедура ДолжностьОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			Должность = УчетЗарплатыВызовСервера.СоздатьДолжностьПоПараметрам(ВыбранноеЗначение);
			Если Не ЗначениеЗаполнено(Должность) Тогда
				Оповещение = Новый ОписаниеОповещения("ЗаписьДолжностиЗавершение", Форма);
				ОткрытьФорму("Справочник.Должности.Форма.ФормаЭлемента",
					Новый Структура("ТекстЗаполнения, РежимВыбора, КонтекстноеСоздание", ВыбранноеЗначение.Наименование, Истина, Истина),
					Форма,
					,
					,
					,
					Оповещение,
					РежимОткрытияОкнаФормы.Независимый);
			Иначе
				ВыбранноеЗначение = Должность;
			КонецЕсли;
		Иначе
			Оповещение = Новый ОписаниеОповещения("ПослеВыбораДолжностиИзКлассификатора", Форма, ВыбранноеЗначение);
			ОткрытьФорму("Справочник.Должности.Форма.ВыборДолжности",
				Новый Структура("ЗначениеПоиска", Элемент.ТекстРедактирования),
				Форма,
				,
				,
				,
				Оповещение,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗакрытиемКадровогоДокумента(Форма, ОтразитьВТрудовойКнижке, ЗакрытьПослеЗаписи, Отказ, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	Если Форма.МожноРедактировать И Не Форма.ТолькоПросмотр И Не Форма.Модифицированность
		И Объект.Проведен И ОтразитьВТрудовойКнижке
		И НачалоДня(Форма.ДатаПредставленияЗаписиТрудовойКнижки) >= НачалоДня(ТекущаяДата())
		И Форма.ЗаписиТрудовойКнижки.Количество() = 0 И Не Форма.ЗапущенаРегистрацияЗаписиЭТК Тогда
		
		Отказ                              = Истина;
		СтандартнаяОбработка               = Ложь;
		Форма.ЗапущенаРегистрацияЗаписиЭТК = Истина;
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("КадровыйДокумент", Объект.Ссылка);
		ПараметрыОткрытия.Вставить("Организация",      Объект.Организация);
		ПараметрыОткрытия.Вставить("Сотрудник",        Объект.Сотрудник);
		ПараметрыОткрытия.Вставить("ФизическоеЛицо",   Объект.ФизическоеЛицо);
		ПараметрыОткрытия.Вставить("ДатаСобытия",      Форма.ДатаПредставленияЗаписиТрудовойКнижки);
		Оповещение = Новый ОписаниеОповещения("РегистрацияЗаписиЭТКЗавершение", Форма);
		ОткрытьФорму("ОбщаяФорма.ПодсказкаРегистрацииЗаписиЭТК",
			ПараметрыОткрытия,
			Форма,
			Форма.УникальныйИдентификатор,
			,
			,
			Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ЗакрытьПослеЗаписи = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПереместитьСтроку(Форма, Направление) Экспорт
	
	// Направление:
	//	1 вниз
	// -1 вверх
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ИсходнаяСтрока = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ИсходнаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НомерИсходнойСтроки = ИсходнаяСтрока.НомерСтроки;
	
	Если НомерИсходнойСтроки = 1 И Направление = - 1 Тогда
		// Первая строка
		Возврат;
	ИначеЕсли Объект.Сотрудники.Количество() = НомерИсходнойСтроки И Направление = 1 Тогда
		// Последняя строка
		Возврат;
	КонецЕсли;
	
	Если НЕ Форма.ПоддержкаСовместительства Тогда
		// Не ведется учет совместителей, простой сдвиг
		ИндексСтрокиОтсчета = НомерИсходнойСтроки - 1;
		Объект.Сотрудники.Сдвинуть(ИндексСтрокиОтсчета, Направление);
	Иначе
		// Проверка на наличие совместителей
		ФизическоеЛицо        = ИсходнаяСтрока.ФизическоеЛицо;
		МассивСотрудников     = Объект.Сотрудники.НайтиСтроки(Новый Структура("ФизическоеЛицо", ФизическоеЛицо));
		КоличествоСотрудников = МассивСотрудников.Количество();
		Если КоличествоСотрудников = 1 Тогда
			// Нет совместителей
			ИндексСтрокиОтсчета = НомерИсходнойСтроки - 1;
			// Но соседняя строка может оказаться совместителем
			ИндексСоседнейСтроки          = ИндексСтрокиОтсчета + Направление;
			ДанныеСоседнейСтроки          = Объект.Сотрудники.Получить(ИндексСоседнейСтроки);
			ФизическоеЛицо                = ДанныеСоседнейСтроки.ФизическоеЛицо;
			МассивСоседнихСотрудников     = Объект.Сотрудники.НайтиСтроки(Новый Структура("ФизическоеЛицо", ФизическоеЛицо));
			КоличествоСоседнихСотрудников = МассивСоседнихСотрудников.Количество();
			Объект.Сотрудники.Сдвинуть(ИндексСтрокиОтсчета, КоличествоСоседнихСотрудников * Направление);
		Иначе
			// Есть совместители - сдвиг всей группы
			ПервыйНомерИсходнойСтроки = 99999;
			ПоследнийНомерИсходнойСтроки = 0;
			Для Каждого ЗначениеМассива ИЗ МассивСотрудников Цикл
				ПервыйНомерИсходнойСтроки    = Мин(ПервыйНомерИсходнойСтроки, ЗначениеМассива.НомерСтроки);
				ПоследнийНомерИсходнойСтроки = Макс(ПоследнийНомерИсходнойСтроки, ЗначениеМассива.НомерСтроки);
			КонецЦикла;
			Если ПервыйНомерИсходнойСтроки = 1 И Направление = - 1 Тогда
				// Первая строка
				Возврат;
			ИначеЕсли Объект.Сотрудники.Количество() = ПоследнийНомерИсходнойСтроки И Направление = 1 Тогда
				// Последняя строка
				Возврат;
			КонецЕсли;
			// Сдвигаем все записи
			// Определяем первую или последнюю запись - от них будем двигать строки
			ИндексСтрокиОтсчета = ?(Направление = -1, ПервыйНомерИсходнойСтроки, ПоследнийНомерИсходнойСтроки)- 1;
			// Но соседняя строка может оказаться совместителем - двигаем группы
			ИндексСоседнейСтроки          = ИндексСтрокиОтсчета + Направление;
			ДанныеСоседнейСтроки          = Объект.Сотрудники.Получить(ИндексСоседнейСтроки);
			ФизическоеЛицо                = ДанныеСоседнейСтроки.ФизическоеЛицо;
			МассивСоседнихСотрудников     = Объект.Сотрудники.НайтиСтроки(Новый Структура("ФизическоеЛицо", ФизическоеЛицо));
			КоличествоСоседнихСотрудников = МассивСоседнихСотрудников.Количество();
			Для НомерСтроки = 0 По КоличествоСоседнихСотрудников - 1 Цикл
				Объект.Сотрудники.Сдвинуть(ИндексСоседнейСтроки + НомерСтроки * Направление, - КоличествоСотрудников * Направление);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстВопросаПриОтменеПроведенияПлатежногоДокумента()
	
	ТекстВопроса = НСтр("ru = 'Отмена проведения платежных документов может:
						| - привести к ошибкам в расчетах с сотрудниками по зарплате;
						| - изменить состав признанных расходов;
						| - повлиять на корректность учета денежных средств;
						| - потребовать корректировки налоговой отчетности.
						|Продолжить?'");
	
	Возврат ТекстВопроса;
	
КонецФункции

#КонецОбласти
