
#Область ПрограммныйИнтерфейс

//Создает реквизит формы и флажок
//Параметры: Форма-Тип("ФормаКлиентскогоПриложения")
//
//Возвращаемое значение: нет
//
Процедура ДобавитьУправлениеРазрешениемРедактироватьКомментарий(Форма) Экспорт
	
	ЭлементыФормы 			= Форма.Элементы;
	ГруппаРазмещения 		= ЭлементыФормы["ГруппаДатыЗапретаИзменения"];
	ГруппаРазмещения.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Заголовок 				= "Разрешить редактирование комментария документов в закрытом периоде";
	ТекстПодсказки 			= "Разрешает редактирование комментария в документах закрытого периода для всех 
					 	  	  |пользователей, имеющих право на редактирование документов в открытом периоде.
					 	  	  |Для сохранения изменения комментариев используйте историю изменения объектов.";
	//Добавляем реквизит формы
	ДобавляемыеРеквизиты 	= Новый Массив;
	ТипРеквизита 			= Новый ОписаниеТипов("Булево");
	РеквизитФормы 			= Новый РеквизитФормы("РазрешитьРедактированияКомментарияДокументовЗакрытогоПериода", ТипРеквизита);
	ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	Форма.РазрешитьРедактированияКомментарияДокументовЗакрытогоПериода = ПолучитьФункциональнуюОпцию("РазрешитьРедактированияКомментарияДокументовЗакрытогоПериода");
	//Добавляем элемент формы(флажок)
	ПолеФлажка 						= ЭлементыФормы.Добавить("ФормаРазрешитьРедактироватьКомментарий", Тип("ПолеФормы"), ГруппаРазмещения);
	ПолеФлажка.Вид 					= ВидПоляФормы.ПолеПереключателя;
	ПолеФлажка.ПутьКДанным 			= "РазрешитьРедактированияКомментарияДокументовЗакрытогоПериода";
	ПолеФлажка.Заголовок 			= Заголовок;
	ПолеФлажка.ПоложениеЗаголовка 	= ПоложениеЗаголовкаЭлементаФормы.Право;
	ПолеФлажка.Подсказка 			= ТекстПодсказки;
	ПолеФлажка.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	ПолеФлажка.УстановитьДействие("ПриИзменении", "Подключаемый_РазрешитьРедактированияКомментарияПриИзменении");
	ПолеФлажка.Доступность			= ПолучитьФункциональнуюОпцию("ИспользоватьДатыЗапретаИзменения");
	
КонецПроцедуры

//Создает команды в дополнительном подменю  и в контекстном меню списка
//Параметры:
//			- Форма Тип("ФормаКлиентскогоПриложения")
//			- ИмяГруппы Тип("Строка")-необяз. Группа в подмен. куда будет добавлена команда
//			- ИмяЭлемента Тип("Строка")- необяз. Команда вставляется перед элементом
//			- ДобавитьВКонтекстноеМеню Тип("Булево") -необяз. Если Истина то помещаем команду в контекстное меню списка
//			- ИмяЭлементаКонтекстногоМеню Тип("Строка")- необяз. Команда вставляется в контекстное меню перед элементом
//
//Возвращаемое значение: нет
//
Процедура ПриСозданииНаСервере(Форма, ИмяГруппы = "", ИмяЭлемента = "", ДобавитьВКонтекстноеМеню = Ложь, ИмяЭлементаКонтекстногоМеню = "") Экспорт
	
	Если Не РазрешитьВыводНаФорму(Форма) Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы 			= Форма.Элементы;
	Группа 					= ЭлементыФормы[ИмяГруппы];
	Команда 				= Форма.Команды.Добавить("РедактироватьКомментарий");
	Команда.Заголовок 		= "Редактировать комментарий";
	Команда.Подсказка 		= "Изменить комментарий документа";
	Команда.Отображение 	= ОтображениеКнопки.Авто;
	Команда.Действие 		= "Подключаемый_РедактироватьКомментарий";
	
	//Добавляем в подменю "Еще" 
	Если ЗначениеЗаполнено(ИмяЭлемента) Тогда
		КнопкаФормы 		= ЭлементыФормы.Вставить("РедактироватьКомментарий", Тип("КнопкаФормы"), Группа, ЭлементыФормы[ИмяЭлемента]);
	Иначе
		КнопкаФормы 		= ЭлементыФормы.Добавить("РедактироватьКомментарий", Тип("КнопкаФормы"), Группа);
	КонецЕсли;
	КнопкаФормы.ИмяКоманды 	= Команда.Имя;
	КнопкаФормы.Вид 		= ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаФормы.Заголовок	= "Редактировать комментарий";
	КнопкаФормы.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	//Добавляем в контекстное меню
	Если ДобавитьВКонтекстноеМеню Тогда
		Если ЗначениеЗаполнено(ИмяЭлементаКонтекстногоМеню) Тогда
			НовыйЭлементКонтекстногоМеню = ЭлементыФормы.Вставить("КонтекстноеМенюРедактироватьКомментарий", Тип("КнопкаФормы"), 
												ЭлементыФормы.Список.КонтекстноеМеню, ЭлементыФормы[ИмяЭлементаКонтекстногоМеню]);	
		Иначе
			НовыйЭлементКонтекстногоМеню = ЭлементыФормы.Добавить("КонтекстноеМенюРедактироватьКомментарий", Тип("КнопкаФормы"), 
												ЭлементыФормы.Список.КонтекстноеМеню);
		КонецЕсли;
		НовыйЭлементКонтекстногоМеню.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		НовыйЭлементКонтекстногоМеню.ИмяКоманды = Команда.Имя;
	КонецЕсли;
	
КонецПроцедуры

//Добавляети на форму документа кнопку редактирования комментария в закрытом периоде
//Параметры:
//			- Форма -Тип("ФормаКлиентскогоПриложения")
//			- ТекущийОбъект Тип("ДокументОбъект.*")
//			- ИмяГруппы Тип("Строка") - группа расположения комментария
//			- ИмяЭлемента Тип("Строка") - имя элемента перед которым нужно вставить кнопку
//
//Возвращаемое значение: нет
//
Процедура ПриЧтенииНаСервере(Форма, ТекущийОбъект, ИмяГруппы = "", ИмяЭлемента = "") Экспорт
	
	ЭлементыФормы = Форма.Элементы;
	
	Если Не ЭлементыФормы.Найти("РедактироватьКомментарий") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДатыЗапретаИзменения.ИзменениеЗапрещено(ТекущийОбъект) 
		Или Не РазрешитьВыводНаФорму(Форма) Тогда
		Возврат; 
	КонецЕсли;	

	Если ЗначениеЗаполнено(ИмяГруппы) Тогда
		ГруппаКомментарий = ЭлементыФормы[ИмяГруппы];
	Иначе
		ГруппаКомментарий = ЭлементыФормы.Добавить("ГруппаКомментарий", Тип("ГруппаФормы"), Форма);
		ГруппаКомментарий.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаКомментарий.РазрешитьИзменениеСостава = Истина;
		ПолеКомментарий = ЭлементыФормы.Найти("Комментарий"); 
		Если ПолеКомментарий = Неопределено Тогда
			Возврат;
		Иначе
			ЭлементыФормы.Переместить(ПолеКомментарий, ГруппаКомментарий);
		КонецЕсли;
	КонецЕсли;
	Элемент = ?(ЗначениеЗаполнено(ИмяЭлемента), ЭлементыФормы[ИмяЭлемента], Неопределено);
	
	Команда 				= Форма.Команды.Добавить("РедактироватьКомментарий");
	Команда.Заголовок 		= "Редактировать комментарий";
	Команда.Подсказка 		= "Редактировать комментарий без перепроведения документа";
	Команда.Картинка 		= БиблиотекаКартинок.Изменить;
	Команда.Действие 		= "Подключаемый_РедактироватьКомментарий";
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Команда.Отображение 	= ОтображениеКнопки.Авто;
	Иначе
		Команда.Отображение 	= ОтображениеКнопки.Картинка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяЭлемента) Тогда
		КнопкаФормы 		= ЭлементыФормы.Вставить("РедактироватьКомментарий", Тип("КнопкаФормы"), ГруппаКомментарий, Элемент);
	Иначе
		КнопкаФормы 		= ЭлементыФормы.Добавить("РедактироватьКомментарий", Тип("КнопкаФормы"), ГруппаКомментарий);
	КонецЕсли;
	КнопкаФормы.ИмяКоманды 	= "РедактироватьКомментарий";
	КнопкаФормы.Вид 		= ВидКнопкиФормы.ОбычнаяКнопка;
	
КонецПроцедуры

//Получает текущий комментарий документа в закрытом периоде
//Параметры:  ДокументСсылка Тип("ДокументСсылка")
//
//Возвращаемое значение: комментарий Тип("Строка")
//
Функция ТекущийКомментарийДокумента(ДокументСсылка) Экспорт
	
	Возврат ДокументСсылка.Комментарий;
	
КонецФункции

//Записывет документ без перепроведения и с обходом бизнес-логики в закрытом периоде
//Параметры:
//			- СсылкаНаДокумент Тип("ДокументСсылка.*")
//			- НовыйКомментарий тип("Строка")
//			- Отказ Тип("Булево")
//			- ТекстОшибки Тип("Строка")
//
//Возвращаемое значение: нет
//
Процедура ЗаписатьКомментарий(СсылкаНаДокумент, НовыйКомментарий, Отказ, ТекстОшибки) Экспорт
	
	ДокОбъект = СсылкаНаДокумент.ПолучитьОбъект();
	
	ДокОбъект.Комментарий = НовыйКомментарий;
	ДокОбъект.ОбменДанными.Загрузка = Истина;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		Отказ = Истина; 
		ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры   

//Получает полное имя формы списка документа
//Параметры: ДокументСсылка Тип("ДокументСсылка.*")
//
//Возвращаемое значение: ИмяФормыСписка Тип("Строка")
//
Функция ПолучитьИмяФормыСпискаДокумента(ДокументСсылка) Экспорт
	
	ИмяФормыСписка = "";
	
	МетаданныеДокумента = ДокументСсылка.Метаданные();
	ФормаСписка = МетаданныеДокумента.Формы.Найти("ФормаСписка");
	Если Не ФормаСписка = Неопределено Тогда
		ИмяФормыСписка = МетаданныеДокумента.ПолноеИмя() + ".Форма." + ФормаСписка.Имя;
	КонецЕсли;
	
	Возврат ИмяФормыСписка;
	
КонецФункции

//Устанавливает значение константы разрешения редактирования комментария
//Параметры: Разрешить -Тип("Булево")
//
//Возвращаемое значение: нет
//
Процедура РазрешитьРедактированиеКомментариевПользователям(Разрешить) Экспорт
	
	Константы.РазрешитьРедактированиеКомментарияДокументовЗакрытогоПериода.Установить(Разрешить);
	
КонецПроцедуры

//Определяет возможность изменения пользователем комментария документа в закрытом периоде
//Параметры: СсылкаНаОбъект Тип("ДокументСсылка.*")
//
//Возвращаемое значение: РазрешитьИзменениеКомментария Тип("Булево")
//
Функция РазрешитьИзменениеКомментарияВСписке(СсылкаНаОбъект) Экспорт
	
	РазрешитьИзменениеКомментария = Ложь;
	
	ДокументОбъект = СсылкаНаОбъект.ПолучитьОбъект();
	Если Не ДатыЗапретаИзменения.ИзменениеЗапрещено(ДокументОбъект) Тогда
		РазрешитьИзменениеКомментария = Истина;
	Иначе
		Если ДоступностьПоРолям("ПолныеПрава, ДобавлениеИзменениеДатЗапретаИзменения") Или  
			ПолучитьФункциональнуюОпцию("РазрешитьРедактированияКомментарияДокументовЗакрытогоПериода") Тогда
			РазрешитьИзменениеКомментария = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РазрешитьИзменениеКомментария;
	
КонецФункции

//Определяет возможность изменения комментария если объект не заблокирован другим пользователем
//Параметры: СсылкаНаОбъект ТИп("ДокументССылка.***")
//
//Возвращаемое значение : БлокировкаВозможна Тип("Булево") 
//
Функция РазрешенаБлокировкаОбъектаДляРедактирования(СсылкаНаОбъект) Экспорт
	
	БлокировкаВозможна = Ложь;
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(СсылкаНаОбъект);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	БлокировкаВозможна = Истина;
	РазблокироватьДанныеДляРедактирования(СсылкаНаОбъект);
	
	Возврат БлокировкаВозможна; 
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

//Проверяет разрешения для вывода на форму 
//Параметры: -Форма Тип("ФормаКлиентскогоПриложения")
//
//Возвращаемое значение: РазрешитьВывод Тип("Булево")
//
Функция РазрешитьВыводНаФорму(Форма)
	
	РазрешитьВывод 		= Ложь;
	
	ИмяФормы 			= Форма.ИмяФормы;
	ИмяДокумента 		= СтрРазделить(ИмяФормы,".")[1];
	МетаданныеОбъекта 	= Метаданные.Документы[ИмяДокумента];
	Если СтрНайти(ИмяФормы, "ФормаСписка") Тогда
		Если ПравоДоступа("Редактирование", МетаданныеОбъекта) Тогда
			РазрешитьВывод = Истина;
		КонецЕсли;
	Иначе
		Если ДоступностьПоРолям("ПолныеПрава, ДобавлениеИзменениеДатЗапретаИзменения") Тогда
			РазрешитьВывод = Истина;
		Иначе
			Если ПолучитьФункциональнуюОпцию("РазрешитьРедактированияКомментарияДокументовЗакрытогоПериода")
				И ПравоДоступа("Редактирование", МетаданныеОбъекта) Тогда
				РазрешитьВывод = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РазрешитьВывод;
	
КонецФункции

//Определяет доступность функционала по конкретным ролям
//Параметры: Роли - Тип("Строка")- имена ролей через запятую
//				  - Тип("Массив")- массив имен ролей
//
//Возвращаемое значение: Доступность Тип("Булево")"Истина" - доступно
//
Функция ДоступностьПоРолям(Роли)
	
	Доступность = Ложь;
	
	Если ТипЗнч(Роли) = Тип("Строка") Тогда
		МассивРолей = СтрРазделить(Роли, ",");
	Иначе
		МассивРолей = Роли;
	КонецЕсли;
	
	Для Каждого Роль Из МассивРолей Цикл
		СтрокаРоль = СокрЛП(Роль);
		Если РольДоступна(СтрокаРоль) Тогда
			Доступность = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Доступность; 
	
КонецФункции

#КонецОбласти
