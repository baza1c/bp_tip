#Область СлужебныйПрограммныйИнтерфейс

// Возвращает параметры обсуждений
// 
// Возвращаемое значение:
//   - Неопределено
//   - Соответствие из Структура
//
Функция ПараметрыЧатов() Экспорт
	
	Возврат ОповещенияПлатформыСамозанятые.ПараметрыЧатов();
	
КонецФункции

// Запускает длительную операцию обновления обсуждений Системы взаимодействия.
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
//  Быстро - Булево - Истина, если нужно проверять актальность данных перед обновлением
// 
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьФункцию()
//
Функция ДлительнаяОперация_ОбновитьОбсуждения(Знач ОтборОрганизация, Знач Быстро = Истина) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление обсуждений сервиса Мой налог'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОповещенияПлатформыСамозанятые.ОбновитьОбсуждения",
		ОтборОрганизация,
		Быстро);
	
	//@skip-check constructor-function-return-section
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию получения количества непрочитанных оповещений.
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
// 
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьФункцию()
//
Функция ДлительнаяОперация_ПолучитьКоличествоНепрочитанных(Знач ОтборОрганизация) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение количества непрочитанных оповещений'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОповещенияПлатформыСамозанятые.ПолучитьКоличествоНепрочитанных",
		ОтборОрганизация);
	
	//@skip-check constructor-function-return-section
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию обновления количества непрочитанных оповещений в Системе взаимодействия.
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
// 
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьПроцедуру()
//
Функция ДлительнаяОперация_ОбновитьКоличествоНепрочитанных(Знач ОтборОрганизация) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление количества непрочитанных оповещений'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ОповещенияПлатформыСамозанятые.ОбновитьКоличествоНепрочитанных",
		ОтборОрганизация);
	
	//@skip-check constructor-function-return-section
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию получения оповещений.
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
//  ПолучатьПрочитанные - Неопределено, Булево - признак необходимости получения прочитанных
// 
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьФункцию()
//
Функция ДлительнаяОперация_ПолучитьОповещения(Знач ОтборОрганизация, Знач ПолучатьПрочитанные = Неопределено) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение оповещений'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОповещенияПлатформыСамозанятые.ПолучитьОповещения",
		ОтборОрганизация,
		ПолучатьПрочитанные);
	
	//@skip-check constructor-function-return-section
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию обновления списка оповещений.
// 
// Параметры:
//  ОтборОрганизация - СправочникСсылка.Организации, Неопределено - отбор по организации
//  ОчиститьПередЗаполнением - Булево - признак необходимости очистки обсуждения перед заполнением
// 
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьПроцедуру()
//
Функция ДлительнаяОперация_ОбновитьОповещения(Знач ОтборОрганизация, Знач ОчиститьПередЗаполнением = Ложь) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление оповещений'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ОповещенияПлатформыСамозанятые.ОбновитьОповещения",
		ОтборОрганизация,
		ОчиститьПередЗаполнением);
	
	//@skip-check constructor-function-return-section
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию отправки уведомлений о прочтении всех оповещений в выбранном обсуждении.
// 
// Параметры:
//  ПараметрыЧата - Структура:
//    * Организация- СправочникСсылка.Организации
//    * ИдентификаторЧата - Строка
//    * ИдентификаторПомощника - Строка
// 
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьФункцию()
//
Функция ДлительнаяОперация_УведомитьОПрочтенииВсех(Знач ПараметрыЧата) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Уведомление об изменении статусов всех оповещений'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОповещенияПлатформыСамозанятые.УведомитьОбИзмененииСтатусаВсех",
		ПараметрыЧата,
		Перечисления.СтатусыОповещенийПлатформыСамозанятые.НеПрочитано,
		Перечисления.СтатусыОповещенийПлатформыСамозанятые.Прочитано);
	
	//@skip-check constructor-function-return-section
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию отправки уведомлений о прочтении оповещения.
// 
// Параметры:
//  ПараметрыЧата - Структура:
//    * Организация- СправочникСсылка.Организации
//    * ИдентификаторЧата - Строка
//    * ИдентификаторПомощника - Строка
//  Идентификатор - Строка - код оповещения
// 
// Возвращаемое значение:
//  - Структура - см. ДлительныеОперации.ВыполнитьФункцию()
//  - Неопределено
//
Функция ДлительнаяОперация_УведомитьОПрочтении(Знач ПараметрыЧата, Знач Идентификатор) Экспорт
	
	ДлительнаяОперация = ДлительнаяОперация_УведомитьОбИзмененииСтатуса(
		ПараметрыЧата, Идентификатор, Перечисления.СтатусыОповещенийПлатформыСамозанятые.Прочитано);
	
	//@skip-check constructor-function-return-section
	Возврат ДлительнаяОперация;
	
КонецФункции

// Запускает длительную операцию перемещения оповещения в архив.
// 
// Параметры:
//  ПараметрыЧата - Структура:
//    * Организация- СправочникСсылка.Организации
//    * ИдентификаторЧата - Строка
//    * ИдентификаторПомощника - Строка
//  Идентификатор - Строка - код оповещения
// 
// Возвращаемое значение:
//  - Структура - см. ДлительныеОперации.ВыполнитьФункцию()
//  - Неопределено
//
Функция ДлительнаяОперация_ПоместитьВАрхив(Знач ПараметрыЧата, Знач Идентификатор) Экспорт
	
	ДлительнаяОперация = ДлительнаяОперация_УведомитьОбИзмененииСтатуса(
		ПараметрыЧата, Идентификатор, Перечисления.СтатусыОповещенийПлатформыСамозанятые.Удалено);
	
	//@skip-check constructor-function-return-section
	Возврат ДлительнаяОперация;
	
КонецФункции

// Определяет является ли сообщение оповещением сервиса Мой налог.
// Признаком является возможность найти элемент справочника по идентификатору из реквизита Данные сообщения.
// 
// Параметры:
//  ДанныеСообщения - Произвольный - данные сообщения
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Булево - Истина, если сообщение является оповещением сервиса Мой налог.
//
Функция ЭтоСообщениеПлатформыСамозанятые(Знач ДанныеСообщения, Знач Организация) Экспорт
	
	ИдентификаторОповещения = ОповещенияПлатформыСамозанятые.ИдентификаторОповещения(ДанныеСообщения);
	Если ИдентификаторОповещения = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Ссылка = Справочники.ОповещенияПлатформыСамозанятые.НайтиПоКоду(ИдентификаторОповещения, , , Организация);
	
	Возврат ЗначениеЗаполнено(Ссылка);
	
КонецФункции

// Удаляет сообщение из обсуждения.
// 
// Параметры:
//  ИдентификаторСообщения - ИдентификаторСообщенияСистемыВзаимодействия
//
Процедура УдалитьСообщение(Знач ИдентификаторСообщения) Экспорт
	
	ОповещенияПлатформыСамозанятые.УдалитьСообщение(ИдентификаторСообщения);
	
КонецПроцедуры

// Находит элемент справочника ОповещенияПлатформыСамозанятые, соответствующий выбранному сообщению.
// 
// Параметры:
//  ДанныеСообщения - Произвольный
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  - СправочникСсылка.ОповещенияПлатформыСамозанятые
//  - Неопределено
//
Функция НайтиОповещение(Знач ДанныеСообщения, Знач Организация) Экспорт
	
	Возврат ОповещенияПлатформыСамозанятые.НайтиОповещение(ДанныеСообщения, Организация);
	
КонецФункции

// Возвращает статус оповещения.
// 
// Параметры:
//  ДанныеСообщения - Произвольный
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  - ПеречислениеСсылка.СтатусыОповещенийПлатформыСамозанятые
//  - Неопределено
//
Функция СтатусОповещения(Знач ДанныеСообщения, Знач Организация) Экспорт
	
	Ссылка = ОповещенияПлатформыСамозанятые.НайтиОповещение(ДанныеСообщения, Организация);
	Если ЗначениеЗаполнено(Ссылка) Тогда
		РеквизитыСтатуса = РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.ПрочитатьСтатус(Ссылка, Организация);
		Если РеквизитыСтатуса <> Неопределено Тогда
			Возврат РеквизитыСтатуса.Статус;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает ключ записи регистра СостоянияОповещенийПлатформыСамозанятые по значениям измерений.
// 
// Параметры:
//  ЗначенияКлюча - Структура:
//   * Организация - СправочникСсылка.Организации
//   * Оповещение - СправочникСсылка.ОповещенияПлатформыСамозанятые
// 
// Возвращаемое значение:
//  РегистрСведенийКлючЗаписи.СостоянияОповещенийПлатформыСамозанятые - Ключ записи состояния оповещений платформы самозанятые
//
Функция КлючЗаписиСостоянияОповещенийПлатформыСамозанятые(ЗначенияКлюча) Экспорт
	
	Возврат РегистрыСведений.СостоянияОповещенийПлатформыСамозанятые.СоздатьКлючЗаписи(ЗначенияКлюча);
	
КонецФункции

Функция ЕстьПравоПолучатьОповещения() Экспорт
	
	Возврат ОповещенияПлатформыСамозанятые.ЕстьПравоПолучатьОповещения();
	
КонецФункции

Функция ЕстьПравоИзменятьСостоянияОповещений() Экспорт
	
	Возврат ОповещенияПлатформыСамозанятые.ЕстьПравоИзменятьСостоянияОповещений();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДлительнаяОперация_УведомитьОбИзмененииСтатуса(ПараметрыЧата, Идентификатор, Статус)
	
	Оповещение = Справочники.ОповещенияПлатформыСамозанятые.НайтиПоКоду(Идентификатор, , , ПараметрыЧата.Организация);
	Если Не ЗначениеЗаполнено(Оповещение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ПараметрыЧата", ПараметрыЧата);
	ПараметрыМетода.Вставить("СписокОповещений", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Оповещение));
	ПараметрыМетода.Вставить("Статус", Статус);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Уведомление об изменении статуса оповещения'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОповещенияПлатформыСамозанятые.УведомитьОбИзмененииСтатуса",
		ПараметрыЧата,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Оповещение),
		Статус);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

#КонецОбласти
