
#Область ПрограммныйИнтерфейс

// Возвращает набор свойств по имени владельца.
//
// Параметры:
//  ИмяВладельца - Строка - имя владельца свойств
//
// Возвращаемое значение:
//  СправочникСсылка.НаборыДополнительныхРеквизитовИСведений - ссылка на набор свойств.
//  Неопределено - если предопределенный набор не существует.
Функция НаборСвойствВладельца(ИмяВладельца) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НаборыСвойств.Ссылка КАК Ссылка,
		|	НаборыСвойств.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыСвойств
		|ГДЕ
		|	НаборыСвойств.Предопределенный";
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИмяПредопределенныхДанных = СтрЗаменить(ИмяВладельца, ".", "_");
	НаборСсылка = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		Если СтрНачинаетсяС(Выборка.ИмяПредопределенныхДанных, "Удалить") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.ИмяПредопределенныхДанных = ИмяПредопределенныхДанных Тогда
			НаборСсылка = Выборка.Ссылка;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НаборСсылка = Неопределено Тогда
		НаборСсылка = УправлениеСвойствами.НаборСвойствПоИмени(ИмяПредопределенныхДанных);
	КонецЕсли;
	
	Возврат НаборСсылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеВыбораМеток(Параметры, ОкончаниеВводаТекста = Ложь) Экспорт
	
	ДанныеВыбора = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПолучитьДанныеВыбора(Параметры);
	Если ДанныеВыбора.Количество() = 0
		И ПравоДоступа("ИнтерактивноеДобавление", Метаданные.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения) Тогда
		СтрокаПоиска = СокрЛП(Параметры.СтрокаПоиска);
		ПредставлениеНового = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru='Создать: ""%1""'"), СтрокаПоиска);
		ДанныеВыбора.Добавить(СтрокаПоиска, ПредставлениеНового);
		
		Если ОкончаниеВводаТекста Тогда
			// Добавляем вторую строку, чтобы в этом режиме не срабатывал автовыбор единственной строки.
			ДанныеВыбора.Добавить("");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеВыбора;
КонецФункции

#КонецОбласти
