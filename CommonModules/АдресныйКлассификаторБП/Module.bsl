
#Область ПрограммныйИнтерфейс

// Возвращает информацию о субъектах РФ определенных в федеральной информационной адресной системе.
// Также добавляет служебные коды регионов (в частности, федеральную территорию Сириус).
//
// Возвращаемое значение:
//     ТаблицаЗначений - см. АдресныйКлассификатор.СубъектыРФ()
//
Функция РегионыРФ() Экспорт
	
	Регионы = АдресныйКлассификатор.СубъектыРФ();
	
	СтрокаФТСириус = Регионы.Добавить();
	СтрокаФТСириус.КодСубъектаРФ = ДанныеГосударственныхОрганов.КодРегионаФедеральнойТерриторииСириусЧислом();
	СтрокаФТСириус.Наименование = ДанныеГосударственныхОрганов.НаименованиеФедеральнойТерриторииСириус();
	СтрокаФТСириус.ПолноеНаименование = ДанныеГосударственныхОрганов.НаименованиеФедеральнойТерриторииСириус();
	
	Возврат Регионы;
	
КонецФункции

// Возвращает полное наименование региона по его коду.
// Также возвращает наименования федеральной территории "Сириус".
// 
// Параметры:
//  КодРегиона - Число
//             - Строка - код региона. Например, 77 или 2378.
//  РегионыРФ - ТаблицаЗначений, ДанныеФормыКоллекция - см. РегионыРФ()
//  ДобавитьКодВПредставление - Булево - если Истина, то в начало наименования добавляется код региона,
//                              например "77, Москва г" 
// 
// Возвращаемое значение:
//  Строка - наименование региона в виде "Москва г" или "77, Москва г" или "Федеральная территория "Сириус".
//  Неопределено - если регион не существует.
//
Функция ПредставлениеРегионаПоКоду(Знач КодРегиона, РегионыРФ = Неопределено, ДобавитьКодВПредставление = Ложь) Экспорт
	
	// В классификаторе субъектов РФ код региона - это число(2).
	// Поэтому при необходимости приведем здесь к нужному типу.
	Если ТипЗнч(КодРегиона) = Тип("Строка") Тогда
		КодРегионаСтрока = КодРегиона;
		ТипЧисло = Новый ОписаниеТипов("Число");
		КодРегиона = ТипЧисло.ПривестиЗначение(КодРегиона);
	Иначе
		КодРегионаСтрока = XMLСтрока(КодРегиона);
	КонецЕсли;
	
	Если КодРегиона = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если РегионыРФ = Неопределено Тогда
		РегионыРФ = РегионыРФ();
	КонецЕсли;
	
	СтрокиКлассификатора = РегионыРФ.НайтиСтроки(Новый Структура("КодСубъектаРФ", КодРегиона));
	
	Если СтрокиКлассификатора.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПредставлениеРегиона(
		КодРегионаСтрока,
		СтрокиКлассификатора[0].Наименование,
		СтрокиКлассификатора[0].ТипОбъекта,
		ДобавитьКодВПредставление);
	
КонецФункции

// Формирует представление региона (федеральной территории). Например, "Москва г"
// 
// Параметры:
//  КодРегиона - Число
//             - Строка - код региона. Например, 77 или 2378.
//  Наименование - Строка - Наименование региона из классификатора или наименование федеральной территории
//  ТипРегиона - Строка - тип региона из классификатора: "г", "обл", "респ"  и т.п.
//  ДобавитьКодВПредставление - Булево - если Истина, то в начало наименования добавляется код региона,
//                              например "77, Москва г" 
// 
// Возвращаемое значение:
//  Строка - представление региона
Функция ПредставлениеРегиона(Знач КодРегиона, Наименование = "", ТипРегиона = "", ДобавитьКодВПредставление = Ложь) Экспорт
	
	ПредставлениеРегиона = Новый Массив;
	
	Если ТипЗнч(КодРегиона) = Тип("Число") Тогда
		КодРегиона = XMLСтрока(КодРегиона);
	КонецЕсли;
	
	КодРегиона = СокрЛП(КодРегиона);
	
	Если ДобавитьКодВПредставление Тогда
		// Представление номера региона всегда 2 знака. Например, "01, Адыгея"
		КодРегионаПредставление = Лев(КодРегиона, 2); // для ф.т. Сириус код региона имеет сокращенное представление
		КодРегионаПредставление = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(КодРегионаПредставление, 2);
		ПредставлениеРегиона.Добавить(КодРегионаПредставление);
	КонецЕсли;
	
	// Тип региона может быть уже указан в наименовании (например, "федеральная территория Сириус") -
	// тогда сюда уже не передается
	Если ЗначениеЗаполнено(ТипРегиона) Тогда
		ПредставлениеРегиона.Добавить(СтрШаблон(НСтр("ru='%1 %2'"), Наименование, ТипРегиона));
	Иначе
		ПредставлениеРегиона.Добавить(Наименование);
	КонецЕсли;
	
	Возврат СтрСоединить(ПредставлениеРегиона, ", ");
	
КонецФункции

// Возвращает источник для получения данных об адресных сведениях
// В обычной ситуации возрващает "Сервис1С", но если он отключен в настройках,
// то возвращает Неопределено и БСП самостоятельно определяет, откуда брать данные.
// 
// Возвращаемое значение:
//   - Строка, Неопределено
//
Функция ИсточникПолученияСведенийОбАдресныхСведениях() Экспорт
	
	Если АдресныйКлассификатор.РазрешенДоступВИнтернет() Тогда
		Возврат "Сервис1С";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Устанавливает адресу идентификаторы из адресного классификатора БСП.
// 
// Параметры:
//  Адрес - Строка - адрес строкой JSON
//
Процедура УстановитьИдентификаторыАдреса(Адрес) Экспорт
	
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		СтруктураАдреса = ЗапросыREST.СтруктураИзСтрокиJSON(Адрес);
	Исключение
		Возврат;
	КонецПопытки;
	
	Если СтруктураАдреса.Свойство("streetId") И ЗначениеЗаполнено(СтруктураАдреса.streetId)
		Или СтруктураАдреса.Свойство("houseId") И ЗначениеЗаполнено(СтруктураАдреса.houseId) Тогда
		// Идентификаторы объектов адреса необходимы для корректной работы с адресом средствами адресного классификатора БСП.
		// Улица и дом - самые глубокие уровни детализации адреса. Если идентификаторы заполнены, то это указывает
		// на готовность работы средствами БСП с этим адресом. Ничего не требуется делать.
		Возврат;
	КонецЕсли;
	
	ОписаниеАдреса = ОписаниеАдреса(Адрес);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КодыАдреса", Истина);
	СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(ОписаниеАдреса, ДополнительныеПараметры);
	Идентификаторы = СведенияОбАдресе.Идентификаторы;
	
	Идентификатор = Неопределено;
	Если Не СтруктураАдреса.Свойство("areaId") И Идентификаторы.Свойство("РегионИдентификатор")
		И ЗначениеЗаполнено(Идентификаторы.РегионИдентификатор) Тогда
		СтруктураАдреса.Вставить("areaId", Идентификаторы.РегионИдентификатор);
		Идентификатор = Идентификаторы.РегионИдентификатор;
	КонецЕсли;
	
	Если Не СтруктураАдреса.Свойство("munDistrictId") И Идентификаторы.Свойство("МуниципальныйРайонИдентификатор")
		И ЗначениеЗаполнено(Идентификаторы.МуниципальныйРайонИдентификатор) Тогда
		СтруктураАдреса.Вставить("munDistrictId", Идентификаторы.МуниципальныйРайонИдентификатор);
		Идентификатор = Идентификаторы.МуниципальныйРайонИдентификатор;
	КонецЕсли;
	
	Если Не СтруктураАдреса.Свойство("localityId") И Идентификаторы.Свойство("НаселенныйПунктИдентификатор")
		И ЗначениеЗаполнено(Идентификаторы.НаселенныйПунктИдентификатор) Тогда
		СтруктураАдреса.Вставить("localityId", Идентификаторы.НаселенныйПунктИдентификатор);
		Идентификатор = Идентификаторы.НаселенныйПунктИдентификатор;
	КонецЕсли;
	
	Если Не СтруктураАдреса.Свойство("streetId") И Идентификаторы.Свойство("УлицаИдентификатор")
		И ЗначениеЗаполнено(Идентификаторы.УлицаИдентификатор) Тогда
		СтруктураАдреса.Вставить("streetId", Идентификаторы.УлицаИдентификатор);
		Идентификатор = Идентификаторы.УлицаИдентификатор;
	КонецЕсли;
	
	Если Не СтруктураАдреса.Свойство("houseId") И СведенияОбАдресе.Свойство("ИдентификаторДома")
		И ЗначениеЗаполнено(СведенияОбАдресе.ИдентификаторДома) Тогда
		СтруктураАдреса.Вставить("houseId", СведенияОбАдресе.ИдентификаторДома);
		Идентификатор = СведенияОбАдресе.ИдентификаторДома;
	КонецЕсли;
	
	// id - идентификатор элемента адреса самого "глубокого уровня".
	// Как правило это - идентификатор дома (houseId). Реже - идентификатор улицы (streetId).
	Если Идентификатор <> Неопределено Тогда
		СтруктураАдреса.Вставить("id", Идентификатор);
	КонецЕсли;
	
	Адрес = ЗапросыRest.ЗначениеВСтрокуJSON(СтруктураАдреса);
	
КонецПроцедуры

// Возвращает коды адреса (ОКТМО, ОКАТО, налоговых инспекций ФНС и др.)
//
// Параметры:
//  Адрес    - Строка - адрес строкой JSON
//  Источник - Строка, Неопределено - источник получения кодов адреса: "Сервис1С", "ЗагруженныеДанные" или не указано.
//
// Возвращаемое значение:
//  Структура - см. возвращаемое значение метода АдресныйКлассификатор.КодыАдреса()
//
Функция КодыАдреса(Адрес, Источник = Неопределено) Экспорт
	
	ОписаниеАдреса = ОписаниеАдреса(Адрес);
	Возврат АдресныйКлассификатор.КодыАдреса(ОписаниеАдреса, Источник);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеАдреса(Адрес)
	
	ТипИнформации = Перечисления.ТипыКонтактнойИнформации.Адрес;
	ОписаниеАдреса = РаботаСАдресамиКлиентСервер.ОписаниеНовойКонтактнойИнформации(ТипИнформации);
	
	Попытка
		СтруктураАдреса = ЗапросыREST.СтруктураИзСтрокиJSON(Адрес);
	Исключение
		Возврат ЗапросыREST.ЗначениеВСтрокуJSON(ОписаниеАдреса);
	КонецПопытки;
	
	ЗаполнитьЗначенияСвойств(ОписаниеАдреса, СтруктураАдреса);
	ЗаполнитьУровниДетализацииАдреса(ОписаниеАдреса);
	
	Возврат ЗапросыREST.ЗначениеВСтрокуJSON(ОписаниеАдреса);
	
КонецФункции

// Заполняет уровни детализации муниципального (munLevels) и административно-территориального (admLevels) адреса.
// По ним адресный классификатор сможет "понять" вложенность элементов адреса.
// Например: верхний уровень - регион (area), глубже - район (district), ещё глубже - город (city), далее - улица(street),
// и самый глубокий уровень - дом (house).
// 
// Параметры:
//  ОписаниеАдреса - Структура - адрес, см. РаботаСАдресамиКлиентСервер.ОписаниеНовойКонтактнойИнформации()
//
Процедура ЗаполнитьУровниДетализацииАдреса(ОписаниеАдреса)
	
	Если ОписаниеАдреса.munLevels.Количество() = 0 Тогда
		УровниАдреса = РаботаСАдресамиКлиентСервер.ИменаУровнейАдреса(РаботаСАдресамиКлиентСервер.МуниципальныйАдрес(), Истина);
		Для Каждого УровеньАдреса Из УровниАдреса Цикл
			Если ЗначениеЗаполнено(ОписаниеАдреса[УровеньАдреса]) Тогда
				ОписаниеАдреса.munLevels.Добавить(УровеньАдреса);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ОписаниеАдреса.houseNumber) Или ОписаниеАдреса.buildings.Количество() > 0 Тогда
			ОписаниеАдреса.munLevels.Добавить("house");
		КонецЕсли;
	КонецЕсли;
	
	Если ОписаниеАдреса.admLevels.Количество() = 0 Тогда
		УровниАдреса = РаботаСАдресамиКлиентСервер.ИменаУровнейАдреса(РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес(), Истина);
		Для Каждого УровеньАдреса Из УровниАдреса Цикл
			Если ЗначениеЗаполнено(ОписаниеАдреса[УровеньАдреса]) Тогда
				ОписаниеАдреса.admLevels.Добавить(УровеньАдреса);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ОписаниеАдреса.houseNumber) Или ОписаниеАдреса.buildings.Количество() > 0 Тогда
			ОписаниеАдреса.admLevels.Добавить("house");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
