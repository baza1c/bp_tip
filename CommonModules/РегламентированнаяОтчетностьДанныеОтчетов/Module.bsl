////////////////////////////////////////////////////////////////////////////////
// Модуль содержит процедуры и функции для получения данных из форм
// регламентированной отчетности. Сбор данных выполняется для проверки
// контрольных соотношений в уведомлениях об исчисленных суммах налогов
// (КНД 1110355) и для обработок обслуживания единого налогового счета.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Получает регламентированные отчеты по заданному критерию, формирует таблицы данных отобранных отчетов
// в разрезе КБК, ОКТМО, сроков уплаты налогов. Выполняет отбор данных из таблиц по заданным условиям.
//
// Параметры:
//   Параметры - Структура - для передачи параметров отбора и возврата отобранных даных. 
//
Процедура ПолучитьДанныеОтчетов(Параметры) Экспорт
	
	СвойстваДокументовПоКНД = Новый ТаблицаЗначений;
	СвойстваДокументовПоКНД.Колонки.Добавить("КНД");
	Параметры.Вставить("СвойстваДокументовПоКНД", СвойстваДокументовПоКНД);
	
	КодыОП = КодыОтчетныхПериодов();
	
	ВидыОтправляемыхДокументовСКНД(Параметры);
	СвойстваДокументовПоКНД = Параметры.СвойстваДокументовПоКНД;
	
	ТаблицаОтчеты      = Параметры.СтруктураДанных.ТаблицаОтчеты.Скопировать();
	ТаблицаОтчетыНовая = Параметры.СтруктураДанных.ТаблицаОтчеты.СкопироватьКолонки();
	
	Параметры.СтруктураДанных.ТаблицаОтчеты = ТаблицаОтчетыНовая;
	
	Для Каждого СтрокаТаблицы Из ТаблицаОтчеты Цикл
		
		Если СтрокаТаблицы.СрокУпл = Дата(1, 1, 1) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивКНД = СтрРазделить(СтрокаТаблицы.КНД, ",");
		СвойстваДокументовПоКНДОтбор = СвойстваДокументовПоКНД.СкопироватьКолонки();
		Для Каждого КНД Из МассивКНД Цикл
			СвойстваДокументовОтбор = СвойстваДокументовПоКНД.НайтиСтроки(Новый Структура("КНД", КНД));
			Для Каждого СтрокаСвойств Из СвойстваДокументовОтбор Цикл
				ЗаполнитьЗначенияСвойств(СвойстваДокументовПоКНДОтбор.Добавить(), СтрокаСвойств);
			КонецЦикла;
		КонецЦикла;
		
		Параметры.Вставить("Организация", СтрокаТаблицы.Организация);
		
		ПараметрыОтбораПоКритерию = Новый Структура("СвойстваДокументовОтбор, ДатаДокумента, СвойстваОтчетногоПериода, Год",
			СвойстваДокументовПоКНДОтбор, СтрокаТаблицы.ДатаДок, КодыОП[СокрЛП(СтрокаТаблицы.Период)], СтрокаТаблицы.ГодОтч);
		Параметры.Вставить("ПараметрыОтбораПоКритерию", ПараметрыОтбораПоКритерию);
		
		Параметры.Вставить("ДокументыДляСверки", Неопределено);
		
		ДокументыДляСверкиПоКритерию(Параметры);
		
		ДокументДляСверки = ДокументДляСверки(Параметры.ДокументыДляСверки, СтрокаТаблицы.КБК);
		
		Если ЗначениеЗаполнено(ДокументДляСверки) Тогда
			
			ТаблицаСумм = ДокументДляСверки.ТаблицаСумм;
			НоваяТаблицаСумм = ТаблицаСумм.СкопироватьКолонки();
			
			СрокУплаты1 = НачалоМесяца(СтрокаТаблицы.СрокУпл);
			СрокУплаты2 = КонецМесяца(СтрокаТаблицы.СрокУпл); // расширяем диапазон для сроков платежей
			Если СтрокаТаблицы.КНД = "1151100" Тогда // НДФЛ
				СрокУплаты1 = НачалоДня(СтрокаТаблицы.СрокУпл - 7 * (24 * 3600));
				СрокУплаты2 = КонецДня(СтрокаТаблицы.СрокУпл + 2 * (24 * 3600)); // расширяем диапазон для сроков платежей
			КонецЕсли;
			
			Для Каждого СтрокаТС Из ТаблицаСумм Цикл
				
				Если СтрокаТаблицы.КБК <> СтрокаТС.КБК ИЛИ СтрокаТаблицы.ОКТМО <> СтрокаТС.ОКТМО Тогда
					Продолжить;
				КонецЕсли;
				
				Если СтрокаТС.Период > СрокУплаты1 И СтрокаТС.Период <= СрокУплаты2 Тогда
					НовСтрокаТС = НоваяТаблицаСумм.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтрокаТС, СтрокаТС);
				КонецЕсли;
				
			КонецЦикла;
			
			Если НоваяТаблицаСумм.Количество() > 0 Тогда
				ДокументДляСверки.ТаблицаСумм = НоваяТаблицаСумм;
				
				НоваяСтрокаТаблицаОтчеты = ТаблицаОтчетыНовая.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаОтчеты, СтрокаТаблицы);
				НоваяСтрокаТаблицаОтчеты.Данные = ДокументДляСверки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ВидыОтправляемыхДокументовСКНД(Параметры)
	
	Перем ТаблицаВидовОтправляемыхДокументовСКНД;
	
	КонтекстЭДОСервер = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "КонтекстЭДОСервер", Неопределено);
	Если КонтекстЭДОСервер = Неопределено Тогда
		ТекстСообщения = "";
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО(ТекстСообщения);
		Если КонтекстЭДОСервер = Неопределено Тогда 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		Параметры.Вставить("КонтекстЭДОСервер", КонтекстЭДОСервер);
	КонецЕсли;
	
	МакетКНД = КонтекстЭДОСервер.ПолучитьМакет("ОписаниеФормДляИмпорта");
	
	ТаблицаКНД = КонтекстЭДОСервер.ДанныеМакетаЧерезПостроительЗапроса(МакетКНД);
	ТаблицаКНД.Колонки[1].Имя = "ИмяПредопределенного";
	ТаблицаКНД.Колонки[2].Имя = "НарастающимИтогом_";
	
	ТаблицаВидовОтправляемыхДокументов = ДокументооборотСКОПовтИсп.ТаблицаВидовОтправляемыхДокументов();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаКНД.КНД КАК КНД,
		|	ВЫРАЗИТЬ(ТаблицаКНД.ИмяПредопределенного КАК СТРОКА(200)) КАК ИмяПредопределенного,
		|	ВЫБОР
		|		КОГДА СОКРЛП(ТаблицаКНД.НарастающимИтогом_) = ""X""
		|			ТОГДА ЛОЖЬ
		|		КОГДА СОКРЛП(ТаблицаКНД.НарастающимИтогом_) = ""x""
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК НарастающимИтогом
		|ПОМЕСТИТЬ ВТ_ТаблицаКНД
		|ИЗ
		|	&ТаблицаКНД КАК ТаблицаКНД
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИмяПредопределенного
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫРАЗИТЬ(ВидыОтправляемыхДокументов.ИмяПредопределенныхДанных КАК СТРОКА(200)) КАК ИмяПредопределенного,
		|	ВЫРАЗИТЬ(ВидыОтправляемыхДокументов.Ссылка КАК Справочник.ВидыОтправляемыхДокументов) КАК СсылкаЭлемент
		|ПОМЕСТИТЬ ВТ_ВидыОтправляемыхДокументов
		|ИЗ
		|	&ТаблицаВидовОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИмяПредопределенного
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_ТаблицаКНД.КНД КАК КНД,
		|	ВТ_ТаблицаКНД.НарастающимИтогом КАК НарастающимИтогом,
		|	ВТ_ВидыОтправляемыхДокументов.СсылкаЭлемент.Наименование КАК Наименование,
		|	ВТ_ВидыОтправляемыхДокументов.СсылкаЭлемент.Источник КАК Источник,
		|	ВТ_ВидыОтправляемыхДокументов.СсылкаЭлемент.ТипДокумента КАК ТипДокумента
		|ИЗ
		|	ВТ_ТаблицаКНД КАК ВТ_ТаблицаКНД
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВидыОтправляемыхДокументов КАК ВТ_ВидыОтправляемыхДокументов
		|		ПО ВТ_ТаблицаКНД.ИмяПредопределенного = ВТ_ВидыОтправляемыхДокументов.ИмяПредопределенного
		|ГДЕ
		|	ВТ_ВидыОтправляемыхДокументов.СсылкаЭлемент.Предопределенный
		|	И НЕ ВТ_ВидыОтправляемыхДокументов.СсылкаЭлемент.ЭтоГруппа
		|	И НЕ ВТ_ВидыОтправляемыхДокументов.СсылкаЭлемент.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ТаблицаКНД", ТаблицаКНД);
	Запрос.УстановитьПараметр("ТаблицаВидовОтправляемыхДокументов", ТаблицаВидовОтправляемыхДокументов);
	
	ТаблицаВидовОтправляемыхДокументовСКНД = Запрос.Выполнить().Выгрузить();
	
	ТаблицаВидовОтправляемыхДокументовСКНД.Индексы.Добавить("КНД");
	
	Параметры.Вставить("СвойстваДокументовПоКНД", ТаблицаВидовОтправляемыхДокументовСКНД);
	
КонецПроцедуры

Процедура ДокументыДляСверкиПоКритерию(Параметры)
	
	Перем ТаблицаРезультатаРасш;
	
	Организация              = Параметры.Организация;
	СвойстваДокументовОтбор  = Параметры.ПараметрыОтбораПоКритерию.СвойстваДокументовОтбор;
	ДатаДокумента            = Параметры.ПараметрыОтбораПоКритерию.ДатаДокумента;
	СвойстваОтчетногоПериода = Параметры.ПараметрыОтбораПоКритерию.СвойстваОтчетногоПериода;
	Год                      = ЧислоИзСтроки(Параметры.ПараметрыОтбораПоКритерию.Год);
	
	Если НЕ ЗначениеЗаполнено(СвойстваДокументовОтбор) Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОСервер = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "КонтекстЭДОСервер", Неопределено);
	
	МассивНаименованийОтчетов = СвойстваДокументовОтбор.ВыгрузитьКолонку("Наименование");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СвойстваДокументовОтбор.Наименование КАК Наименование,
		|	СвойстваДокументовОтбор.НарастающимИтогом КАК НарастающимИтогом
		|ПОМЕСТИТЬ ВТ_СвойстваДокументовОтбор
		|ИЗ
		|	&СвойстваДокументовОтбор КАК СвойстваДокументовОтбор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка КАК Ссылка,
		|	ЖурналОтправокВКонтролирующиеОрганы.Наименование КАК Наименование,
		|	ЖурналОтправокВКонтролирующиеОрганы.ДатаНачала КАК ДатаНачала,
		|	ЖурналОтправокВКонтролирующиеОрганы.ДатаОкончания КАК ДатаОкончания,
		|	ЖурналОтправокВКонтролирующиеОрганы.ПредставлениеПериода КАК ПредставлениеПериода,
		|	"""" КАК ПредставлениеВида,
		|	РАЗНОСТЬДАТ(ЖурналОтправокВКонтролирующиеОрганы.ДатаНачала, ЖурналОтправокВКонтролирующиеОрганы.ДатаОкончания, МЕСЯЦ) + 1 КАК ПМ,
		|	МЕСЯЦ(ЖурналОтправокВКонтролирующиеОрганы.ДатаОкончания) КАК НМ,
		|	ЕСТЬNULL(ВТ_СвойстваДокументовОтбор1.НарастающимИтогом, ЛОЖЬ) КАК НарастающимИтогом,
		|	0 КАК КодЖурнала
		|ИЗ
		|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СвойстваДокументовОтбор КАК ВТ_СвойстваДокументовОтбор1
		|		ПО ЖурналОтправокВКонтролирующиеОрганы.Наименование = ВТ_СвойстваДокументовОтбор1.Наименование
		|ГДЕ
		|	ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала = ЗНАЧЕНИЕ(Перечисление.СтраницыЖурналаОтчетность.Уведомления)
		|	И ЖурналОтправокВКонтролирующиеОрганы.Организация = &Организация
		|	И ЖурналОтправокВКонтролирующиеОрганы.Наименование В(&НаименованиеОтчета)
		|	И ЖурналОтправокВКонтролирующиеОрганы.ВидКонтролирующегоОргана = &ВидКонтролирующегоОргана
		|	И ЖурналОтправокВКонтролирующиеОрганы.СостояниеСдачиОтчетности = &СостояниеСдачиОтчетности
		|	И ЖурналОтправокВКонтролирующиеОрганы.ДатаОтправки МЕЖДУ &ДатаОтправки1 И &ДатаОтправки2
		|	И (ВЫРАЗИТЬ(ЖурналОтправокВКонтролирующиеОрганы.СтатусОтправки КАК СТРОКА(5))) = ""Сдано""
		|	И НЕ ЖурналОтправокВКонтролирующиеОрганы.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЖурналОтчетовСтатусы.Ссылка,
		|	ЖурналОтчетовСтатусы.НаименованиеОтчета,
		|	ЖурналОтчетовСтатусы.ДатаНачала,
		|	ЖурналОтчетовСтатусы.ДатаОкончания,
		|	ЖурналОтчетовСтатусы.ФинансовыйПериод,
		|	ЖурналОтчетовСтатусы.ВариантОтчета,
		|	РАЗНОСТЬДАТ(ЖурналОтчетовСтатусы.ДатаНачала, ЖурналОтчетовСтатусы.ДатаОкончания, МЕСЯЦ) + 1,
		|	МЕСЯЦ(ЖурналОтчетовСтатусы.ДатаОкончания),
		|	ЕСТЬNULL(ВТ_СвойстваДокументовОтбор2.НарастающимИтогом, ЛОЖЬ),
		|	1
		|ИЗ
		|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СвойстваДокументовОтбор КАК ВТ_СвойстваДокументовОтбор2
		|		ПО ЖурналОтчетовСтатусы.НаименованиеОтчета = ВТ_СвойстваДокументовОтбор2.Наименование
		|ГДЕ
		|	ЖурналОтчетовСтатусы.Организация  = &Организация
		|	И ЖурналОтчетовСтатусы.НаименованиеОтчета В(&НаименованиеОтчета)
		|	И ЖурналОтчетовСтатусы.ВидКонтролирующегоОргана = &ВидКонтролирующегоОргана
		|	И ЖурналОтчетовСтатусы.СостояниеСдачиОтчетности = &СостояниеСдачиОтчетности
		|	И ЖурналОтчетовСтатусы.ДатаОтправки МЕЖДУ &ДатаОтправки1 И &ДатаОтправки2
		|	И (ВЫРАЗИТЬ(ЖурналОтчетовСтатусы.Статус КАК СТРОКА(5))) = ""Сдано""
		|	И НЕ ЖурналОтчетовСтатусы.ПометкаУдаления
		|";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НаименованиеОтчета", МассивНаименованийОтчетов);
	Запрос.УстановитьПараметр("ВидКонтролирующегоОргана",
		Перечисления.ТипыКонтролирующихОрганов.ФНС);
	Запрос.УстановитьПараметр("СостояниеСдачиОтчетности",
		Перечисления.СостояниеСдачиОтчетности.ПоложительныйРезультатДокументооборота);
	Запрос.УстановитьПараметр("ДатаОтправки1", НачалоГода(НачалоГода(ДатаДокумента) - 1));
	Запрос.УстановитьПараметр("ДатаОтправки2", КонецДня(ДатаДокумента) - (24 * 3600));
	
	Запрос.УстановитьПараметр("СвойстваДокументовОтбор", СвойстваДокументовОтбор);
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	
	ТаблицаРезультатаЗапроса.Колонки.Вставить(6, "ПредставлениеДокумента", Новый ОписаниеТипов("Строка"));
	ТаблицаРезультатаЗапроса.Колонки.Вставить(0, "КБК", Новый ОписаниеТипов("Строка"));
	ТаблицаРезультатаЗапроса.Колонки.Добавить("ТаблицаСумм");
	
	Если ТипЗнч(СвойстваОтчетногоПериода) = Тип("Структура") Тогда
		КоличествоСтрок = ТаблицаРезультатаЗапроса.Количество();
		Для СмещСтроки = 1 По КоличествоСтрок Цикл
			СтрокаТаблицы = ТаблицаРезультатаЗапроса[КоличествоСтрок - СмещСтроки];
			Если СтрокаТаблицы.КодЖурнала = 0 Тогда
				Продолжить;
			КонецЕсли;
			// Проверяем год и номер последнего месяца отчетного периода.
			Если Год(СтрокаТаблицы.ДатаОкончания) = Год
				И (СвойстваОтчетногоПериода.НМ = 0 ИЛИ СвойстваОтчетногоПериода.НМ = СтрокаТаблицы.НМ) Тогда
				// Количество месяцев в отчетном периоде.
				ПМ = ?(СтрокаТаблицы.НарастающимИтогом, СвойстваОтчетногоПериода.НМ, СвойстваОтчетногоПериода.ПМ);
				Если ПМ = 0 ИЛИ ПМ = СтрокаТаблицы.ПМ Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			ТаблицаРезультатаЗапроса.Удалить(СтрокаТаблицы);
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаРезультатаРасш = ТаблицаРезультатаЗапроса.СкопироватьКолонки();
	
	Для Каждого СтрокаТаблицы Из ТаблицаРезультатаЗапроса Цикл
		// Сформировать представления документов.
		СтрокаТаблицы.ПредставлениеДокумента = ПредставлениеДокумента(
			СтрокаТаблицы.Ссылка,
			СтрокаТаблицы.Наименование,
			СтрокаТаблицы.ПредставлениеПериода,
			СтрокаТаблицы.ПредставлениеВида);
		
		// Получить таблицу сумм для ссылки.
		СтрокаТаблицы.ТаблицаСумм = ДанныеДокументаДляСверки(СтрокаТаблицы.Ссылка, КонтекстЭДОСервер);
		
		ТаблицаКБК = СтрокаТаблицы.ТаблицаСумм.Скопировать(, "КБК");
		ТаблицаКБК.Свернуть("КБК");
		Если ТаблицаКБК.Количество() > 0 Тогда
			Для Каждого СтрокаКБК Из ТаблицаКБК Цикл
				НоваяСтрока = ТаблицаРезультатаРасш.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.КБК = СтрокаКБК.КБК;
			КонецЦикла;
		Иначе
			НоваяСтрока = ТаблицаРезультатаРасш.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		КонецЕсли;
		
	КонецЦикла;
	
	Параметры.Вставить("ДокументыДляСверки", ТаблицаРезультатаРасш);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КодыОтчетныхПериодов()
	
	// <Код отчетного периода>;<Период в месяцах>;<Месяц конца периода>.
	СтрКоды =
	"01;1;1"   //январь
	"02;1;2"   //февраль
	"03;1;3"   //март
	"04;1;4"   //апрель
	"05;1;5"   //май
	"06;1;6"   //июнь
	"07;1;7"   //июль
	"08;1;8"   //август
	"09;1;9"   //сентябрь
	"10;1;10"  //октябрь
	"11;1;11"  //ноябрь
	"12;1;12"  //декабрь
	"13;3;3"   //первый квартал по консолидированной группе налогоплательщиков
	"14;6;6"   //полугодие по консолидированной группе налогоплательщиков
	"15;9;9"   //девять месяцев по консолидированной группе налогоплательщиков
	"16;12;12" //год по консолидированной группе налогоплательщиков
	"17;3;6"   //полугодие (2 квартал)
	"18;3;9"   //девять месяцев (3 квартал)
	"20;3;0"   //квартал
	"21;3;3"   //1 квартал
	"22;3;6"   //2 квартал
	"23;3;9"   //3 квартал
	"24;3;12"  //4 квартал
	"25;3;3"   //1 квартал в случае представления налоговой декларации управляющим товарищ.
	"26;3;6"   //2 квартал в случае представления налоговой декларации управляющим товарищ.
	"27;3;9"   //3 квартал в случае представления налоговой декларации управляющим товарищ.
	"28;3;12"  //4 квартал в случае представления налоговой декларации управляющим товарищ.
	"31;6;6"   //полугодие
	"33;9;9"   //девять месяцев
	"34;12;12" //год
	"35;1;1"   //один месяц*
	"36;2;2"   //два месяца*
	"37;3;3"   //три месяца*
	"38;4;4"   //четыре месяца*
	"39;5;5"   //пять месяцев*
	"40;6;6"   //шесть месяцев*
	"41;7;7"   //семь месяцев*
	"42;8;8"   //восемь месяцев*
	"43;9;9"   //девять месяцев*
	"44;10;10" //десять месяцев*
	"45;11;11" //одиннадцать месяцев*
	"46;12;12" //год*
	"47;3;6"   //полугодие (2 квартал) при реорганизации (ликвидации) организации
	"48;3;9"   //девять месяцев (3 квартал) при реорганизации (ликвидации) организации
	"50;0;0"   //последний налоговый период при реорганизации (ликвидации) организации
	"51;3;3"   //1 квартал при реорганизации (ликвидации) организации
	"52;6;6"   //полугодие при реорганизации (ликвидации) организации
	"53;9;9"   //9 месяцев при реорганизации (ликвидации) организации
	"54;3;6"   //2 квартал при реорганизации (ликвидации) организации
	"55;3;9"   //3 квартал при реорганизации (ликвидации) организации
	"56;3;12"  //4 квартал при реорганизации (ликвидации) организации
	"57;1;1"   //один месяц по консолидированной группе налогоплательщиков
	"58;2;2"   //два месяца по консолидированной группе налогоплательщиков
	"59;3;3"   //три месяца по консолидированной группе налогоплательщиков
	"60;4;4"   //четыре месяца по консолидированной группе налогоплательщиков
	"61;5;5"   //пять месяцев по консолидированной группе налогоплательщиков
	"62;6;6"   //шесть месяцев по консолидированной группе налогоплательщиков
	"63;7;7"   //семь месяцев по консолидированной группе налогоплательщиков
	"64;8;8"   //восемь месяцев по консолидированной группе налогоплательщиков
	"65;9;9"   //девять месяцев по консолидированной группе налогоплательщиков
	"66;10;10" //десять месяцев по консолидированной группе налогоплательщиков
	"67;11;11" //одиннадцать месяцев по консолидированной группе налогоплательщиков
	"68;12;12" //год по консолидированной группе налогоплательщиков
	"71;1;1"   //за январь при реорганизации (ликвидации) организации
	"72;1;2"   //за февраль при реорганизации (ликвидации) организации
	"73;1;3"   //за март при реорганизации (ликвидации) организации
	"74;1;4"   //за апрель при реорганизации (ликвидации) организации
	"75;1;5"   //за май при реорганизации (ликвидации) организации
	"76;1;6"   //за июнь при реорганизации (ликвидации) организации
	"77;1;7"   //за июль при реорганизации (ликвидации) организации
	"78;1;8"   //за август при реорганизации (ликвидации) организации
	"79;1;9"   //за сентябрь при реорганизации (ликвидации) организации
	"80;1;10"  //за октябрь при реорганизации (ликвидации) организации
	"81;1;11"  //за ноябрь при реорганизации (ликвидации) организации
	"82;1;12"  //за декабрь при реорганизации (ликвидации) организации
	"83;3;3"   //1 квартал при снятии с учета, в качестве индивидуального предпринимателя ((главы) крестьянского (фермерского) хозяйства)
	"84;6;6"   //полугодие при снятии с учета, в качестве индивидуального предпринимателя ((главы) крестьянского (фермерского) хозяйства)
	"85;9;9"   //9 месяцев при снятии с учета, в качестве индивидуального предпринимателя ((главы) крестьянского (фермерского) хозяйства)
	"86;12;12" //год при снятии с учета, в качестве индивидуального предпринимателя ((главы) крестьянского (фермерского) хозяйства)
	"90;12;12" //год при реорганизации (ликвидации) организации
	"91;3;3"   //I квартал при реорганизации (ликвидации) организации (промежуточная отчетность)
	"92;6;6"   //полугодие при реорганизации (ликвидации) организации (промежуточная отчетность)
	"93;9;9"   //9 месяцев при реорганизации (ликвидации) организации (промежуточная отчетность)
	"94;12;12" //год при реорганизации (ликвидации) организации (промежуточная отчетность)
	"95;0;0"   //последний налоговый период при переходе на иной режим налогообложения
	"96;0;0"   //последний налоговый период при прекращении предпринимательской деятельности,
		       //в отношении которой налогоплательщиком применялась упрощенная система налогообложения
	"99;0;0"   //иной отчетный период
	"00;13;13";//несуществующий
	
	СвойстваКодов = Новый Соответствие;
	
	Для НомСтр = 1 По СтрЧислоСтрок(СтрКоды) Цикл
		ЭлементыСтроки = СтрРазделить(СтрПолучитьСтроку(СтрКоды, НомСтр), ";", Истина);
		СвойстваКодов.Вставить(ЭлементыСтроки[0],
			Новый Структура("ПМ,НМ", Число(ЭлементыСтроки[1]), Число(ЭлементыСтроки[2])));
	КонецЦикла;
	
	Возврат СвойстваКодов;
	
КонецФункции

Функция НоваяТаблицаСумм()
	
	ТаблицаСумм = Новый ТаблицаЗначений;
	ТаблицаСумм.Колонки.Добавить("КБК",    Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
	ТаблицаСумм.Колонки.Добавить("ОКТМО",  Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(11)));
	ТаблицаСумм.Колонки.Добавить("Сумма",  Новый ОписаниеТипов("Число"));
	ТаблицаСумм.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТаблицаСумм.Колонки.Добавить("Дополнительно"); // для расшифровки содержания суммы в отдельных отчетах
	
	Возврат ТаблицаСумм;
	
КонецФункции

Функция ДокументДляСверки(ДокументыДляСверки, КБК)
	
	Если НЕ ЗначениеЗаполнено(ДокументыДляСверки) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДокументыДляСверкиПоКБК = ДокументыДляСверки.НайтиСтроки(Новый Структура("КБК", КБК));
	Если НЕ ЗначениеЗаполнено(ДокументыДляСверкиПоКБК) Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Возврат ДокументыДляСверкиПоКБК[0];
	
КонецФункции

Функция ДанныеДокументаДляСверки(Ссылка, КонтекстЭДОСервер = Неопределено)
	
	ТаблицаСумм = НоваяТаблицаСумм();
	
	ТипДокумента = ТипЗнч(Ссылка);
	
	Если ТипДокумента = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		ОбработатьДокументРегламентированныйОтчет(Ссылка, ТаблицаСумм);
		
	ИначеЕсли ТипДокумента = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
		
		ОбработатьЭлектронноеПредставлениеРегламентированногоОтчета(Ссылка, ТаблицаСумм, КонтекстЭДОСервер);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
		
		ОбработатьУведомлениеОСпецрежимахНалогообложения(Ссылка, ТаблицаСумм);
		
	КонецЕсли;
	
	Возврат ТаблицаСумм;
	
КонецФункции

Процедура ОбработатьДокументРегламентированныйОтчет(РегламентированныйОтчет, ТаблицаСумм)
	
	РеквизитыРеглОтчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РегламентированныйОтчет,
		"Ссылка, ИсточникОтчета, ВыбраннаяФорма, ДанныеОтчета, ДатаОкончания");
	
	ИдОтчета = РеквизитыРеглОтчета.ИсточникОтчета;
	
	Если ИдОтчета = "РегламентированныйОтчетНДС" Тогда // КНД 1151001
		ВыбратьДанные_РегламентированныйОтчетНДС(РеквизитыРеглОтчета, ТаблицаСумм);
	ИначеЕсли ИдОтчета = "РегламентированныйОтчет6_НДФЛ" Тогда // КНД 1151100
		ВыбратьДанные_РегламентированныйОтчет6_НДФЛ(РеквизитыРеглОтчета, ТаблицаСумм);
	ИначеЕсли ИдОтчета = "РегламентированныйОтчетРасчетПоСтраховымВзносам" Тогда // КНД 1151111
		ВыбратьДанные_РегламентированныйОтчетРасчетПоСтраховымВзносам(РеквизитыРеглОтчета, ТаблицаСумм);
	ИначеЕсли ИдОтчета = "РегламентированныйОтчетДоходыИностраннымОрганизациям" Тогда // КНД 1151056
		ВыбратьДанные_РегламентированныйОтчетДоходыИностраннымОрганизациям(РеквизитыРеглОтчета, ТаблицаСумм);
	ИначеЕсли ИдОтчета = "РегламентированныйОтчетНДПИ" Тогда // КНД 1151054
		ВыбратьДанные_РегламентированныйОтчетНДПИ(РеквизитыРеглОтчета, ТаблицаСумм);
	ИначеЕсли ИдОтчета = "РегламентированныйОтчетУСН" Тогда  // КНД 1152017
		ВыбратьДанные_РегламентированныйОтчетУСН(РеквизитыРеглОтчета, ТаблицаСумм);
	КонецЕсли;
	
	Если ТаблицаСумм.Количество() = 0 Тогда
		
		Попытка
			
			МодульМенеджераРегламентированногоОтчета = ОбщегоНазначения.ОбщийМодуль("Отчеты." + ИдОтчета);
			Если МодульМенеджераРегламентированногоОтчета = Неопределено Тогда
				Возврат;
			КонецЕсли;
			ТабСумм = МодульМенеджераРегламентированногоОтчета.ДанныеРеглОтчета(РегламентированныйОтчет);
			Если ТипЗнч(ТабСумм) <> Тип("ТаблицаЗначений") 
				Или ТабСумм.Количество() = 0 Тогда
				Возврат;
			КонецЕсли;
			Если ИдОтчета = "РегламентированныйОтчетПрибыль" Тогда 
				// Для цели сверки по ЕНС авансы - только ежемесячные авансовые платежи следующих периодов
				Для каждого СтрокаСуммы Из ТабСумм Цикл
					СтрокаСуммы.Аванс = СтрокаСуммы.Период > РеквизитыРеглОтчета.ДатаОкончания;
				КонецЦикла;
			КонецЕсли;
			ТаблицаСумм = ТабСумм;
			
		Исключение
			
			Возврат;
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьЭлектронноеПредставлениеРегламентированногоОтчета(
				ЭлектронноеПредставление, ТаблицаСумм, КонтекстЭДОСервер = Неопределено)
	
	Если КонтекстЭДОСервер = Неопределено Тогда
		ТекстСообщения = "";
		КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО(ТекстСообщения);
		Если КонтекстЭДОСервер = Неопределено Тогда 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ИзвлеченныеФайлы = КонтекстЭДОСервер.ИзвлечьЭлектронныеПредставленияВХранилище(ЭлектронноеПредставление, Неопределено);
	
	ОсновнойФайл = Неопределено;
	
	Если ИзвлеченныеФайлы <> Неопределено Тогда
		Если ИзвлеченныеФайлы.Количество() = 1 И НЕ ИзвлеченныеФайлы[0].ВАрхиве Тогда
			ОсновнойФайл = ИзвлеченныеФайлы[0];
		Иначе
			Для Каждого СвойстваФайла Из ИзвлеченныеФайлы Цикл
				Если СвойстваФайла.ВАрхиве Тогда 
					Продолжить;
				КонецЕсли;
				Если СвойстваФайла.ТипФайлаОтчетности = Перечисления.ТипыФайловБухОтчетности.ОсновнойФайлОтчетности Тогда
					ОсновнойФайл = СвойстваФайла;
					Прервать;
				КонецЕсли;
				НРегРасширениеФайла = НРег(Прав(СвойстваФайла.ИмяФайла, 3));
				Если НРегРасширениеФайла = "txt" ИЛИ НРегРасширениеФайла = "xml" Тогда
					ОсновнойФайл = СвойстваФайла;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ОсновнойФайл <> Неопределено Тогда
		
		РеквизитыЭлектронногоПредставленияОтчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭлектронноеПредставление,
			"Ссылка, ВидОтчета, ТипПолучателя");
		РеквизитыЭлектронногоПредставленияОтчета.Вставить("ИмяОсновногоФайла", ОсновнойФайл.ИмяФайла);
		
		ДанныеФайла = ОсновнойФайл.Данные;
		
		ЭтоВыгрузкаВXML = (НРег(Прав(ОсновнойФайл.ИмяФайла, 3)) = "xml");
		РеквизитыЭлектронногоПредставленияОтчета.Вставить("ЭтоВыгрузкаВXML", ЭтоВыгрузкаВXML);
		
		ФайлОсновногоПредставления = ПолучитьИмяВременногоФайла(?(ЭтоВыгрузкаВXML, "xml", "txt"));
		ДанныеФайла.Записать(ФайлОсновногоПредставления);
		
		Если ЭтоВыгрузкаВXML Тогда
			
			ОбъектЧтениеXML = Новый ЧтениеXML;
			Попытка
				ОбъектЧтениеXML.ОткрытьФайл(ФайлОсновногоПредставления);
				ОбъектЧтениеXML.ИгнорироватьПробелы = Ложь;
				
				РеквизитыЭлектронногоПредставленияОтчета.Вставить("ОбъектЧтение", ОбъектЧтениеXML);
				ВыбратьДанныеЭП(РеквизитыЭлектронногоПредставленияОтчета, ТаблицаСумм);
				
				ОбъектЧтениеXML.Закрыть();
			Исключение
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Файл ""%1"" не является файлом выгрузки в формате XML.'"), ОсновнойФайл.ИмяФайла);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				
				ОписаниеОшибки = ИнформацияОбОшибке().Описание;
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Сверки данных учета с данными налогового органа по ЕНС'"),
					УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения + Символы.ПС + ОписаниеОшибки);
				
				Если ОбъектЧтениеXML <> Неопределено Тогда
					ОбъектЧтениеXML.Закрыть();
				КонецЕсли;
			КонецПопытки;
			
		Иначе
			
			КодировкаФайла = "windows-1251";
			Попытка
				ОбъектЧтениеТекста =  Новый ЧтениеТекста(ФайлОсновногоПредставления, КодировкаФайла);
				
				РеквизитыЭлектронногоПредставленияОтчета.Вставить("ОбъектЧтение", ОбъектЧтениеТекста);
				ВыбратьДанныеЭП(РеквизитыЭлектронногоПредставленияОтчета, ТаблицаСумм);
				
				ОбъектЧтениеТекста.Закрыть();
			Исключение
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Файл ""%1"" не является файлом выгрузки в текстовом формате.'"), ОсновнойФайл.ИмяФайла);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				
				ОписаниеОшибки = ИнформацияОбОшибке().Описание;
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Сверки данных учета с данными налогового органа по ЕНС'"),
					УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения + Символы.ПС + ОписаниеОшибки);
					
				Если ОбъектЧтениеТекста <> Неопределено Тогда
					ОбъектЧтениеТекста.Закрыть();
				КонецЕсли;
			КонецПопытки;
			
		КонецЕсли;
		
		УдалитьФайлы(ФайлОсновногоПредставления);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьУведомлениеОСпецрежимахНалогообложения(РегламентированноеУведомление, ТаблицаСумм)
	
	Данные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РегламентированноеУведомление, "ДанныеУведомления,ИмяФормы");
	ДанныеОтчета = Данные.ДанныеУведомления.Получить();
	Если ТипЗнч(ДанныеОтчета) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ДанныеОтчета.Свойство("ДанныеМногостраничныхРазделов") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ДанныеОтчета.ДанныеМногостраничныхРазделов.Свойство("СумИсчНалог") Тогда
		Возврат;
	КонецЕсли;
	Если Данные.ИмяФормы = "Форма2023_1" Тогда
		Для Каждого Стр Из ДанныеОтчета.ДанныеМногостраничныхРазделов.СумИсчНалог Цикл 
			НовСтр = ТаблицаСумм.Добавить();
			НовСтр.КБК = Стр.Значение.КБК;
			НовСтр.ОКТМО = Стр.Значение.ОКТМО;
			НовСтр.Сумма = Стр.Значение.СумНалогАванс;
			НовСтр.Дополнительно = Новый Структура("Период,НомерМесКварт", Стр.Значение.Период, Стр.Значение.НомерМесКварт);
		КонецЦикла;
	ИначеЕсли Данные.ИмяФормы = "Форма2022_1" Тогда 
		Для Каждого Стр Из ДанныеОтчета.ДанныеМногостраничныхРазделов.СумИсчНалог Цикл 
			НовСтр = ТаблицаСумм.Добавить();
			НовСтр.КБК = Стр.Значение.КБК;
			НовСтр.ОКТМО = Стр.Значение.ОКТМО;
			НовСтр.Сумма = Стр.Значение.СумНалогАванс;
			НовСтр.Дополнительно = Новый Структура("СрокУпл", Стр.Значение.СрокУпл);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработкаНалоговойОтчетности

Процедура ВыбратьДанные_РегламентированныйОтчетНДС(РеквизитыРеглОтчета, ТаблицаСумм)
	
	ТаблицаДанныхРеглОтчета = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	
	Если РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2025Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2023Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2022Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2021Кв3"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2020Кв4"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2019Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2017Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2015Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2009Кв4" Тогда
		
		ДанныеРеглОтчета = РеквизитыРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеРеглОтчета) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		Если НЕ ДанныеРеглОтчета.Свойство("ПоказателиОтчета") Тогда
			Возврат;
		КонецЕсли;
		Если НЕ ДанныеРеглОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел1") Тогда
			Возврат;
		КонецЕсли;
		
		НалогКУплате = ДанныеРеглОтчета.ПоказателиОтчета.ПолеТабличногоДокументаРаздел1;
		
		Период = РеквизитыРеглОтчета.Ссылка.ДатаОкончания;
		
		КодСтрокиОКАТО        = "П000100001003";
		КодСтрокиКБК          = "П000100002003";
		КодСтрокиСуммаСт173п5 = "П000100003003";
		КодСтрокиСуммаСт173п1 = "П000100004003";
		
		ОКАТО              = НалогКУплате[КодСтрокиОКАТО];
		КБК                = НалогКУплате[КодСтрокиКБК];
		СуммаЕдиновременно = НалогКУплате[КодСтрокиСуммаСт173п5];
		СуммаРассрочка     = НалогКУплате[КодСтрокиСуммаСт173п1];
		
		СуммаПлатежа = Цел(СуммаРассрочка / 3);
		
		Платежи = Новый Массив;
		Платежи.Добавить(СуммаПлатежа + СуммаЕдиновременно);
		Платежи.Добавить(СуммаПлатежа);
		Платежи.Добавить(СуммаРассрочка - 2 * СуммаПлатежа);
		
		Для НомерМесяца = 1 По 3 Цикл
			
			Платеж = ТаблицаДанныхРеглОтчета.Добавить();
			Платеж.Период = ДобавитьМесяц(Период, НомерМесяца);
			Платеж.ОКАТО  = ОКАТО;
			Платеж.КБК    = КБК;
			Платеж.Сумма  = Платежи[НомерМесяца - 1];
			
		КонецЦикла;
		
		Если НЕ ДанныеРеглОтчета.Свойство("ДанныеМногостраничныхРазделов") Тогда
			Возврат;
		КонецЕсли;
		Если НЕ ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Свойство("Раздел2") Тогда
			Возврат;
		КонецЕсли;
		СтраницыРаздела2 = ДанныеРеглОтчета.ДанныеМногостраничныхРазделов.Раздел2;
		
		СуммаЗаНалоговыхАгентов = Новый ТаблицаЗначений;
		СуммаЗаНалоговыхАгентов.Колонки.Добавить("Наим",  Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
		СуммаЗаНалоговыхАгентов.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
		
		КодСтрокиНаим  = "П000200002003"; // Наименование налогоплательщика - продавца
		КодСтрокиСумма = "П000200006003"; // Сумма налога, подлежащая уплате в бюджет
		
		Для Каждого СтраницаРаздела2 Из СтраницыРаздела2 Цикл
			
			ДанныеСтраницы = СтраницаРаздела2.Данные;
			
			Наименование = СокрЛП(ЗначениеПоказателя(ДанныеСтраницы, КодСтрокиНаим));
			СуммаНалога  = ЗначениеПоказателя(ДанныеСтраницы, КодСтрокиСумма, 0);
			
			СтрокаСуммаЗаНалоговыхАгентов = СуммаЗаНалоговыхАгентов.Добавить();
			СтрокаСуммаЗаНалоговыхАгентов.Наим  = Наименование;
			СтрокаСуммаЗаНалоговыхАгентов.Сумма = СуммаНалога;
			
		КонецЦикла;
		
		СуммаЗаНалоговыхАгентов.Свернуть("Наим", "Сумма");
		
		ОбщаяСумма = 0;
		СуммаЗаПервыеДваМесяца = 0;
		Для Каждого СтрокаСуммаЗаНалоговыхАгентов Из СуммаЗаНалоговыхАгентов Цикл
			СуммаЗаПервыеДваМесяца = СуммаЗаПервыеДваМесяца + Окр(СтрокаСуммаЗаНалоговыхАгентов.Сумма / 3);
			ОбщаяСумма = ОбщаяСумма + СтрокаСуммаЗаНалоговыхАгентов.Сумма;
		КонецЦикла;
		
		Для НомерМесяца = 1 По 2 Цикл
			
			Платеж = ТаблицаДанныхРеглОтчета.Добавить();
			Платеж.Период = ДобавитьМесяц(Период, НомерМесяца);
			Платеж.ОКАТО  = ОКАТО;
			Платеж.КБК    = КБК;
			Платеж.Сумма  = СуммаЗаПервыеДваМесяца;
			Платеж.Агент  = Истина;
			
		КонецЦикла;
		
		Платеж = ТаблицаДанныхРеглОтчета.Добавить();
		Платеж.Период = ДобавитьМесяц(Период, 3);
		Платеж.ОКАТО  = ОКАТО;
		Платеж.КБК    = КБК;
		Платеж.Сумма  = Окр(ОбщаяСумма - 2 * СуммаЗаПервыеДваМесяца);
		Платеж.Агент  = Истина;
		
	КонецЕсли;
	
	ТаблицаСумм = ТаблицаДанныхРеглОтчета;
	
КонецПроцедуры

Процедура ВыбратьДанные_РегламентированныйОтчет6_НДФЛ(РеквизитыРеглОтчета, ТаблицаСумм)
	
	ИменаАтрибутовСумм1 = СтрРазделить("СрокНал,СумНал", ",");
	ИменаАтрибутовСумм2 = СтрРазделить("СумНал1Срок,СумНал2Срок,СумНал3Срок,СумНал4Срок", ",");
	ИменаАтрибутовСумм3 = СтрРазделить("СумНал1Срок,СумНал2Срок,СумНал3Срок,СумНал4Срок,СумНал5Срок,СумНал6Срок", ",");
	
	ТаблицаПоказателей = Новый ТаблицаЗначений;
	ТаблицаПоказателей.Колонки.Добавить("МнЧ",     Новый ОписаниеТипов("Булево"));
	ТаблицаПоказателей.Колонки.Добавить("ИдФормы", Новый ОписаниеТипов("Строка"));
	ТаблицаПоказателей.Колонки.Добавить("П_КБК",   Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(13)));
	ТаблицаПоказателей.Колонки.Добавить("П_Суммы", Новый ОписаниеТипов("СписокЗначений"));
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.МнЧ     = Истина;
	НовСтрока.ИдФормы = "ФормаОтчета2021Кв1,ФормаОтчета2021Кв4";
	НовСтрока.П_КБК = "П000010001001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П00001М102101";
	НовСтрока.П_Суммы[1].Представление = "П00001М102201";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.ИдФормы = "ФормаОтчета2023Кв1,ФормаОтчета2024Кв1";
	НовСтрока.П_КБК   = "П000010001001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм2);
	НовСтрока.П_Суммы[0].Представление = "П000010002101";
	НовСтрока.П_Суммы[1].Представление = "П000010002201";
	НовСтрока.П_Суммы[2].Представление = "П000010002301";
	НовСтрока.П_Суммы[3].Представление = "П000010002401";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.ИдФормы = "ФормаОтчета2024Кв2+";
	НовСтрока.П_КБК   = "П000010001001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм3);
	НовСтрока.П_Суммы[0].Представление = "П000010002101";
	НовСтрока.П_Суммы[1].Представление = "П000010002201";
	НовСтрока.П_Суммы[2].Представление = "П000010002301";
	НовСтрока.П_Суммы[3].Представление = "П000010002401";
	НовСтрока.П_Суммы[4].Представление = "П000010002501";
	НовСтрока.П_Суммы[5].Представление = "П000010002601";
	
	КодыОП = КодыОтчетныхПериодов();
	Период = РеквизитыРеглОтчета.Ссылка.ДатаОкончания;
	
	СохраненныеДанныеОтчета = РеквизитыРеглОтчета.ДанныеОтчета.Получить();
	Если НЕ СохраненныеДанныеОтчета.Свойство("ПоказателиОтчета") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ СохраненныеДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаТитульный") Тогда
		Возврат;
	КонецЕсли;
	
	ПоказателиТитульный = СохраненныеДанныеОтчета.ПоказателиОтчета.ПолеТабличногоДокументаТитульный;
	
	СвойстваОП = КодыОП[ЗначениеПоказателя(ПоказателиТитульный, "Период")];
	ОтчетГод   = ЧислоИзСтроки(ЗначениеПоказателя(ПоказателиТитульный, "ОтчетГод", Год(Период)));
	ОКТМО      = СокрЛП(ЗначениеПоказателя(ПоказателиТитульный, "ОКТМО"));
	
	Если НЕ СохраненныеДанныеОтчета.Свойство("ДанныеМногоуровневыхРазделов") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ СохраненныеДанныеОтчета.ДанныеМногоуровневыхРазделов.Свойство("Раздел1") Тогда
		Возврат;
	КонецЕсли;
	СтраницыРаздела1 = СохраненныеДанныеОтчета.ДанныеМногоуровневыхРазделов.Раздел1;
	
	Для Каждого СтраницаРаздела1 Из СтраницыРаздела1.Строки Цикл
		
		ДанныеРаздела1 = СтраницаРаздела1.Данные;
		
		Для Каждого СтрокаТаблицы Из ТаблицаПоказателей Цикл
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.ИдФормы)
			   И СтрНайти(ВРег(СтрокаТаблицы.ИдФормы), ВРег(РеквизитыРеглОтчета.ВыбраннаяФорма)) = 0 Тогда
				Если Прав(СтрокаТаблицы.ИдФормы, 1) = "+" Тогда
					ИдФорм = СтрРазделить(СтрокаТаблицы.ИдФормы, ",");
					Если СтрСравнить(ИдФорм[ИдФорм.ВГраница()], РеквизитыРеглОтчета.ВыбраннаяФорма + "+") > 0 Тогда
						Продолжить;
					КонецЕсли;
				Иначе
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			КБК = СокрЛП(ЗначениеПоказателя(ДанныеРаздела1, СтрокаТаблицы.П_КБК));
			
			Если СтрокаТаблицы.МнЧ Тогда
				
				МногострочнаяЧасть1Раздела1 = СтраницаРаздела1.ДанныеМногострочныхЧастей[Лев(СтрокаТаблицы.П_Суммы[0].Представление, 8)];
				Для Каждого СтрокаМнЧ Из МногострочнаяЧасть1Раздела1.Строки Цикл
					
					ДанныеМнЧ1Раздела1 = СтрокаМнЧ.Данные;
					
					СрокУплаты  = ЗначениеПоказателя(ДанныеМнЧ1Раздела1, СтрокаТаблицы.П_Суммы[0].Представление, '00010101');
					СуммаНалога = ЗначениеПоказателя(ДанныеМнЧ1Раздела1, СтрокаТаблицы.П_Суммы[1].Представление, 0);
					
					Если СуммаНалога <> 0 И (ЗначениеЗаполнено(КБК) ИЛИ ЗначениеЗаполнено(ОКТМО)) Тогда
						ЗаписьПоНалогу = ТаблицаСумм.Добавить();
						ЗаписьПоНалогу.КБК           = КБК;
						ЗаписьПоНалогу.ОКТМО         = ОКТМО;
						ЗаписьПоНалогу.Сумма         = СуммаНалога;
						ЗаписьПоНалогу.Период        = СрокУплаты;
						
						ДополнительнаяИнформация = Новый Структура;
						ДополнительнаяИнформация.Вставить("СрокУплаты", СрокУплаты);
						ДополнительнаяИнформация.Вставить("ИменаАтрибутовСумм", ВРег(СтрокаТаблицы.П_Суммы[1].Значение));
						
						ЗаписьПоНалогу.Дополнительно = ДополнительнаяИнформация;
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				
				Для Каждого ЭлемСумма Из СтрокаТаблицы.П_Суммы Цикл
					
					СуммаНалога = ЗначениеПоказателя(ДанныеРаздела1, ЭлемСумма.Представление, 0);
					
					Если СуммаНалога <> 0 И (ЗначениеЗаполнено(КБК) ИЛИ ЗначениеЗаполнено(ОКТМО)) Тогда
						ЗаписьПоНалогу = ТаблицаСумм.Добавить();
						ЗаписьПоНалогу.КБК           = КБК;
						ЗаписьПоНалогу.ОКТМО         = ОКТМО;
						ЗаписьПоНалогу.Сумма         = СуммаНалога;
						
						СрокУплаты = '00010101';
						
						ОкончаниеИмени = СтрЗаменить(ЭлемСумма.Значение, "СумНал", "");
						Если ЗначениеЗаполнено(ОкончаниеИмени) Тогда
							НомерСрокаУплаты = Лев(ОкончаниеИмени, 1);
							Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерСрокаУплаты) Тогда
								Если ТипЗнч(СвойстваОП) = Тип("Структура") Тогда
									Если ОтчетГод > 2023 Тогда
										НомСрокаУплаты = Число(НомерСрокаУплаты);
										НомМесОП = СвойстваОП.НМ + Окр(НомСрокаУплаты / 2) - 3;
										Если НомСрокаУплаты % 2 = 0 Тогда // четный номер срока уплаты
											СрокУплаты = КонецМесяца(Дата(ОтчетГод, НомМесОП, 1));
										ИначеЕсли НомМесОП < 1 Тогда
											СрокУплаты = Дата(ОтчетГод, 12, 31, 23, 59, 59);
										Иначе
											СрокУплаты = Дата(ОтчетГод, НомМесОП, 22, 23, 59, 59);
										КонецЕсли;
									Иначе
										Если НомерСрокаУплаты = "4" И СвойстваОП.НМ <> 12 Тогда
											ТаблицаСумм.Удалить(ЗаписьПоНалогу);
											Продолжить;
										КонецЕсли;
										НомМесОП = СвойстваОП.НМ + Число(НомерСрокаУплаты) - 3;
										Если НомМесОП < 1 ИЛИ НомМесОП > 12 Тогда
											СрокУплаты = Дата(ОтчетГод, 12, 31, 23, 59, 59);
										Иначе
											СрокУплаты = Дата(ОтчетГод, НомМесОП, 22, 23, 59, 59);
										КонецЕсли;
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						
						ЗаписьПоНалогу.Период = СрокУплаты;
						
						ДополнительнаяИнформация = Новый Структура;
						ДополнительнаяИнформация.Вставить("СрокУплаты", СрокУплаты);
						ДополнительнаяИнформация.Вставить("ИменаАтрибутовСумм", ВРег(ЭлемСумма.Значение));
						
						ЗаписьПоНалогу.Дополнительно = ДополнительнаяИнформация;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыбратьДанные_РегламентированныйОтчетРасчетПоСтраховымВзносам(РеквизитыРеглОтчета, ТаблицаСумм)
	
	ИменаАтрибутовСумм1 = СтрРазделить("СумСВУпл1М,СумСВУпл2М,СумСВУпл3М,СумСВУплПер", ",");
	ИменаАтрибутовСумм2 = СтрРазделить("ПревРасхСВ1М,ПревРасхСВ2М,ПревРасхСВ3М,ПревРасхСВПер", ",");
	
	ТаблицаПоказателей = Новый ТаблицаЗначений;
	ТаблицаПоказателей.Колонки.Добавить("МнЧ",     Новый ОписаниеТипов("Булево"));
	ТаблицаПоказателей.Колонки.Добавить("ИдФормы", Новый ОписаниеТипов("Строка"));
	ТаблицаПоказателей.Колонки.Добавить("П_КБК",   Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(13)));
	ТаблицаПоказателей.Колонки.Добавить("П_Суммы", Новый ОписаниеТипов("СписокЗначений"));
	
	П_ОКТМО = "П000010001001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.П_КБК = "П000010002001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П000010003101";
	НовСтрока.П_Суммы[1].Представление = "П000010003201";
	НовСтрока.П_Суммы[2].Представление = "П000010003301";
	НовСтрока.П_Суммы[3].Представление = "П000010003001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.ИдФормы = "ФормаОтчета2017Кв1,ФормаОтчета2020Кв1,ФормаОтчета2020Кв4,ФормаОтчета2022Кв1";
	НовСтрока.П_КБК   = "П000010004001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П000010005101";
	НовСтрока.П_Суммы[1].Представление = "П000010005201";
	НовСтрока.П_Суммы[2].Представление = "П000010005301";
	НовСтрока.П_Суммы[3].Представление = "П000010005001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.МнЧ     = Истина;
	НовСтрока.ИдФормы = "ФормаОтчета2023Кв1,ФормаОтчета2023Кв3,ФормаОтчета2025Кв1+";
	НовСтрока.П_КБК   = "П00001М104001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П00001М105101";
	НовСтрока.П_Суммы[1].Представление = "П00001М105201";
	НовСтрока.П_Суммы[2].Представление = "П00001М105301";
	НовСтрока.П_Суммы[3].Представление = "П00001М105001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.МнЧ     = Истина;
	НовСтрока.ИдФормы = "ФормаОтчета2023Кв1,ФормаОтчета2023Кв3,ФормаОтчета2025Кв1+";
	НовСтрока.П_КБК   = "П00001М206001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П00001М207101";
	НовСтрока.П_Суммы[1].Представление = "П00001М207201";
	НовСтрока.П_Суммы[2].Представление = "П00001М207301";
	НовСтрока.П_Суммы[3].Представление = "П00001М207001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.МнЧ     = Истина;
	НовСтрока.ИдФормы = "ФормаОтчета2017Кв1,ФормаОтчета2020Кв1,ФормаОтчета2020Кв4,ФормаОтчета2022Кв1";
	НовСтрока.П_КБК   = "П00001М106001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П00001М107101";
	НовСтрока.П_Суммы[1].Представление = "П00001М107201";
	НовСтрока.П_Суммы[2].Представление = "П00001М107301";
	НовСтрока.П_Суммы[3].Представление = "П00001М107001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.МнЧ     = Истина;
	НовСтрока.ИдФормы = "ФормаОтчета2017Кв1,ФормаОтчета2020Кв1,ФормаОтчета2020Кв4,ФормаОтчета2022Кв1";
	НовСтрока.П_КБК   = "П00001М208001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П00001М209101";
	НовСтрока.П_Суммы[1].Представление = "П00001М209201";
	НовСтрока.П_Суммы[2].Представление = "П00001М209301";
	НовСтрока.П_Суммы[3].Представление = "П00001М209001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.ИдФормы = "ФормаОтчета2023Кв3,ФормаОтчета2025Кв1+";
	НовСтрока.П_КБК   = "П000010008001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П000010009101";
	НовСтрока.П_Суммы[1].Представление = "П000010009201";
	НовСтрока.П_Суммы[2].Представление = "П000010009301";
	НовСтрока.П_Суммы[3].Представление = "П000010009001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.ИдФормы = "ФормаОтчета2017Кв1,ФормаОтчета2020Кв1,ФормаОтчета2020Кв4,ФормаОтчета2022Кв1,ФормаОтчета2023Кв3,ФормаОтчета2025Кв1+";
	НовСтрока.П_КБК   = "П000010010001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П000010011101";
	НовСтрока.П_Суммы[1].Представление = "П000010011201";
	НовСтрока.П_Суммы[2].Представление = "П000010011301";
	НовСтрока.П_Суммы[3].Представление = "П000010011001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.ИдФормы = "ФормаОтчета2017Кв1,ФормаОтчета2020Кв1,ФормаОтчета2020Кв4";
	НовСтрока.П_КБК   = "П000010010001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм2);
	НовСтрока.П_Суммы[0].Представление = "П000010012101";
	НовСтрока.П_Суммы[1].Представление = "П000010012201";
	НовСтрока.П_Суммы[2].Представление = "П000010012301";
	НовСтрока.П_Суммы[3].Представление = "П000010012001";
	
	НовСтрока = ТаблицаПоказателей.Добавить();
	НовСтрока.ИдФормы = "ФормаОтчета2023Кв3,ФормаОтчета2025Кв1+";
	НовСтрока.П_КБК   = "П000010012001";
	НовСтрока.П_Суммы.ЗагрузитьЗначения(ИменаАтрибутовСумм1);
	НовСтрока.П_Суммы[0].Представление = "П000010013101";
	НовСтрока.П_Суммы[1].Представление = "П000010013201";
	НовСтрока.П_Суммы[2].Представление = "П000010013301";
	НовСтрока.П_Суммы[3].Представление = "П000010013001";
	
	СохраненныеДанныеОтчета = РеквизитыРеглОтчета.ДанныеОтчета.Получить();
	Если НЕ СохраненныеДанныеОтчета.Свойство("ДанныеМногоуровневыхРазделов") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ СохраненныеДанныеОтчета.ДанныеМногоуровневыхРазделов.Свойство("Раздел1") Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(СохраненныеДанныеОтчета.ДанныеМногоуровневыхРазделов.Раздел1) <> Тип("ДеревоЗначений")
		Или СохраненныеДанныеОтчета.ДанныеМногоуровневыхРазделов.Раздел1.Строки.Количество() = 0
		Или СохраненныеДанныеОтчета.ДанныеМногоуровневыхРазделов.Раздел1.Колонки.Найти("Данные") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Попытка
		СтраницаРаздела1 = СохраненныеДанныеОтчета.ДанныеМногоуровневыхРазделов.Раздел1.Строки[0];
		ДанныеРаздела1 = СтраницаРаздела1.Данные;
		ДанныеМнЧРаздела1 = СтраницаРаздела1.ДанныеМногострочныхЧастей.П00001М1.Строки[0].Данные;
	Исключение
		Возврат;
	КонецПопытки;
	
	ОКТМО = СокрЛП(ЗначениеПоказателя(ДанныеРаздела1, П_ОКТМО));
	Если НЕ ЗначениеЗаполнено(ОКТМО) Тогда
		Возврат;
	КонецЕсли;
	
	Период = РеквизитыРеглОтчета.Ссылка.ДатаОкончания;
	
	Для Каждого СтрокаТаблицы Из ТаблицаПоказателей Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ИдФормы)
		   И СтрНайти(ВРег(СтрокаТаблицы.ИдФормы), ВРег(РеквизитыРеглОтчета.ВыбраннаяФорма)) = 0 Тогда
			Если Прав(СтрокаТаблицы.ИдФормы, 1) = "+" Тогда
				ИдФорм = СтрРазделить(СтрокаТаблицы.ИдФормы, ",");
				Если СтрСравнить(ИдФорм[ИдФорм.ВГраница()], РеквизитыРеглОтчета.ВыбраннаяФорма + "+") > 0 Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ДанныеРаздела = ?(СтрокаТаблицы.МнЧ, ДанныеМнЧРаздела1, ДанныеРаздела1);
		ЕстьСуммы3М = Ложь;
		КБК = СокрЛП(ЗначениеПоказателя(ДанныеРаздела, СтрокаТаблицы.П_КБК));
		
		Для Каждого ЭлемСумма Из СтрокаТаблицы.П_Суммы Цикл
			
			СуммаНалога = ЗначениеПоказателя(ДанныеРаздела, ЭлемСумма.Представление, 0);
			
			Инд = СтрокаТаблицы.П_Суммы.Индекс(ЭлемСумма);
			Если СуммаНалога <> Неопределено И СуммаНалога > 0 Тогда
				Если Инд < 3 Тогда
					ЕстьСуммы3М = Истина;
				ИначеЕсли ЕстьСуммы3М Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(КБК) ИЛИ ЗначениеЗаполнено(ОКТМО) Тогда
				ЗаписьПоНалогу = ТаблицаСумм.Добавить();
				ЗаписьПоНалогу.КБК           = КБК;
				ЗаписьПоНалогу.ОКТМО         = ОКТМО;
				ЗаписьПоНалогу.Сумма         = СуммаНалога;
				ЗаписьПоНалогу.Период        = ДобавитьМесяц(Период, Мин(Инд, 2) - 1);
				ЗаписьПоНалогу.Дополнительно = ЭлемСумма.Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыбратьДанные_РегламентированныйОтчетДоходыИностраннымОрганизациям(РеквизитыРеглОтчета, ТаблицаСумм)
	
	ТаблицаДанныхРеглОтчета = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	
	Если РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2007Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2014Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2016Кв1"
	 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2020Кв1" Тогда
		
		ДанныеОтчета = РеквизитыРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеОтчета) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		Если РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2007Кв1"
		 ИЛИ РеквизитыРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2014Кв1" Тогда
			Если НЕ ДанныеОтчета.Свойство("ДанныеМногостраничныхРазделов") Тогда
				Возврат;
			КонецЕсли;
			Если НЕ ДанныеОтчета.ДанныеМногостраничныхРазделов.Свойство("Раздел1") Тогда
				Возврат;
			КонецЕсли;
			СтраницыРаздела1 = ДанныеОтчета.ДанныеМногостраничныхРазделов.Раздел1;
			
			Для Каждого СтраницаРаздела1 Из СтраницыРаздела1 Цикл
				
				ДанныеСтраницы = СтраницаРаздела1.Данные;
				
				КБК = СокрЛП(ЗначениеПоказателя(ДанныеСтраницы, "КБК"));
				ОКАТО = СокрЛП(ЗначениеПоказателя(ДанныеСтраницы, "ОКАТО"));
				
				ИмяПоказателяСуммаНалога = "П000010006009";
				ИмяПоказателяДатаУплаты  = "П000010006011";
				
				Инд = 1;
				Пока ДанныеСтраницы.Свойство(ИмяПоказателяСуммаНалога + "_" + СтрокаЧГ0(Инд))
				 ИЛИ ДанныеСтраницы.Свойство(ИмяПоказателяДатаУплаты + "_" + СтрокаЧГ0(Инд)) Цикл
					ДатаУплаты  = ЗначениеПоказателя(ДанныеСтраницы, ИмяПоказателяСуммаНалога + "_" + СтрокаЧГ0(Инд), '00010101');
					СуммаНалога = ЗначениеПоказателя(ДанныеСтраницы, ИмяПоказателяСуммаНалога + "_" + СтрокаЧГ0(Инд));
					
					Если ЗначениеЗаполнено(КБК) Тогда
						ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
						ЗаписьПоНалогу.Период = ДатаУплаты;
						ЗаписьПоНалогу.КБК    = КБК;
						ЗаписьПоНалогу.ОКАТО  = ОКАТО;
						ЗаписьПоНалогу.Сумма  = СуммаНалога;
					КонецЕсли;
					
					Инд = Инд + 1;
				КонецЦикла;
				
			КонецЦикла;
			
		Иначе
			Если НЕ ДанныеОтчета.Свойство("ДанныеМногоуровневыхРазделов") Тогда
				Возврат;
			КонецЕсли;
			Если НЕ ДанныеОтчета.ДанныеМногоуровневыхРазделов.Свойство("Раздел1") Тогда
				Возврат;
			КонецЕсли;
			СтраницаРаздела1 = ДанныеОтчета.ДанныеМногоуровневыхРазделов.Раздел1.Строки[0];
			МногострочнаяЧастьРаздела1 = СтраницаРаздела1.ДанныеМногострочныхЧастей["П0001000"];
			
			ОКАТО = СокрЛП(ЗначениеПоказателя(СтраницаРаздела1.Данные, "П000100001003")); // ОКТМО
			
			Для Каждого МнСтрокаРаздела Из МногострочнаяЧастьРаздела1.Строки Цикл
				
				ДанныеСтроки = МнСтрокаРаздела.Данные;
				
				КБК         = СокрЛП(ЗначениеПоказателя(ДанныеСтроки, "П000100002003"));
				Период      = ЗначениеПоказателя(ДанныеСтроки, "П000100003003", '00010101');
				СуммаНалога = ЗначениеПоказателя(ДанныеСтроки, "П000100004003", 0);
				
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.Период = Период;
				ЗаписьПоНалогу.КБК    = КБК;
				ЗаписьПоНалогу.ОКАТО  = ОКАТО;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
				
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаСумм = ТаблицаДанныхРеглОтчета;
	
КонецПроцедуры

Процедура ВыбратьДанные_РегламентированныйОтчетНДПИ(РеквизитыРеглОтчета, ТаблицаСумм)
	
	ИменаПоказателей = Новый Структура("КБК, ОКТМО, СуммаНалога",
		"П000100000101_", "П000100000102_", "П000100000103_");
	
	ИдФормы = РеквизитыРеглОтчета.ВыбраннаяФорма;
	Если ИдФормы = "ФормаОтчета2011Кв2" ИЛИ ИдФормы = "ФормаОтчета2012Кв1"
	 ИЛИ ИдФормы = "ФормаОтчета2013Кв4" Тогда
		ИменаПоказателей = Новый Структура("КБК, ОКТМО, СуммаНалога",
			"П000100001001_", "П000100001002_", "П000100001003_");
	КонецЕсли;
	
	СохраненныеДанныеОтчета = РеквизитыРеглОтчета.ДанныеОтчета.Получить();
	Если ТипЗнч(СохраненныеДанныеОтчета) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ СохраненныеДанныеОтчета.Свойство("ПоказателиОтчета") Тогда
		Возврат;
	КонецЕсли;
	Если НЕ СохраненныеДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел1") Тогда
		Возврат;
	КонецЕсли;
	ПоказателиРаздела = СохраненныеДанныеОтчета.ПоказателиОтчета.ПолеТабличногоДокументаРаздел1;
	
	Инд = 1;
	Пока ПоказателиРаздела.Свойство(ИменаПоказателей.КБК + СтрокаЧГ0(Инд)) Цикл
		КБК   = СокрЛП(ЗначениеПоказателя(ПоказателиРаздела, ИменаПоказателей.КБК + СтрокаЧГ0(Инд)));
		ОКТМО = СокрЛП(ЗначениеПоказателя(ПоказателиРаздела, ИменаПоказателей.ОКТМО + СтрокаЧГ0(Инд)));
		СуммаНалога = ЗначениеПоказателя(ПоказателиРаздела, ИменаПоказателей.СуммаНалога + СтрокаЧГ0(Инд));
		
		Если ЗначениеЗаполнено(КБК) ИЛИ ЗначениеЗаполнено(ОКТМО) Тогда
			ЗаписьПоНалогу = ТаблицаСумм.Добавить();
			ЗаписьПоНалогу.КБК   = КБК;
			ЗаписьПоНалогу.ОКТМО = ОКТМО;
			ЗаписьПоНалогу.Сумма = СуммаНалога;
		КонецЕсли;
		
		Инд = Инд + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыбратьДанные_РегламентированныйОтчетУСН(РеквизитыРеглОтчета, ТаблицаСумм)
	
	ТаблицаДанныхРеглОтчета = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	
	Период = РеквизитыРеглОтчета.Ссылка.ДатаОкончания;
	
	ЭтоЮрЛицо = РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(РеквизитыРеглОтчета.Ссылка.Организация);
	
	ИдФормы = РеквизитыРеглОтчета.ВыбраннаяФорма;
	
	Если ИдФормы = "ФормаОтчета2024Кв1"
	 ИЛИ ИдФормы = "ФормаОтчета2023Кв1"
	 ИЛИ ИдФормы = "ФормаОтчета2021Кв1"
	 ИЛИ ИдФормы = "ФормаОтчета2015Кв1"
	 ИЛИ ИдФормы = "ФормаОтчета2014Кв1" Тогда
		
		СохраненныеДанныеОтчета = РеквизитыРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(СохраненныеДанныеОтчета) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		Если НЕ СохраненныеДанныеОтчета.Свойство("ПоказателиОтчета") Тогда
			Возврат;
		КонецЕсли;
		
		ДеньПлатежа = 25;
		ДеньПлатежаГод = ?(ЭтоЮрЛицо, 31, 30);
		Если ИдФормы = "ФормаОтчета2024Кв1"
		 ИЛИ ИдФормы = "ФормаОтчета2023Кв1" Тогда
			ДеньПлатежа = 28;
			ДеньПлатежаГод = 28;
		КонецЕсли;
		
		// Раздел 1.1.
		НалогКУплате11 = Неопределено;
		Если СохраненныеДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел11", НалогКУплате11) Тогда
			
			// Коды ОКТМО.
			КодСтрокиОКТМО11_010 = "П000110001003";
			КодСтрокиОКТМО11_030 = "П000110003003";
			КодСтрокиОКТМО11_060 = "П000110006003";
			КодСтрокиОКТМО11_090 = "П000110009003";
			
			Если ЗначениеЗаполнено(НалогКУплате11[КодСтрокиОКТМО11_090]) Тогда
				ОКТМО11 = НалогКУплате11[КодСтрокиОКТМО11_090];
			ИначеЕсли ЗначениеЗаполнено(НалогКУплате11[КодСтрокиОКТМО11_060]) Тогда
				ОКТМО11 = НалогКУплате11[КодСтрокиОКТМО11_060];
			ИначеЕсли ЗначениеЗаполнено(НалогКУплате11[КодСтрокиОКТМО11_030]) Тогда
				ОКТМО11 = НалогКУплате11[КодСтрокиОКТМО11_030];
			Иначе
				ОКТМО11 = НалогКУплате11[КодСтрокиОКТМО11_010];
			КонецЕсли;
			
			КБК11 = "18210501011011000110";
			
			КодСтрокиСуммаАвансовогоПлатежа3_11  = "П000110002003"; //сумма авансового платежа к уплате по сроку 1 кв.
			КодСтрокиСуммаАвансовогоПлатежа6_11  = "П000110004003"; //сумма авансового платежа к уплате по сроку за полугодие
			КодСтрокиСуммаАвансовогоПлатежа9_11  = "П000110007003"; //сумма авансового платежа к уплате по сроку за 9 мес.
			КодСтрокиСуммаПодлежащаяДоплате12_11 = "П000110010003"; //сумма налога, подлежащая доплате за год.
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате11, КодСтрокиСуммаАвансовогоПлатежа3_11);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_Доходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период), 04, ДеньПлатежа, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО11;
				ЗаписьПоНалогу.КБК    = КБК11;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате11, КодСтрокиСуммаАвансовогоПлатежа6_11);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_Доходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период), 07, ДеньПлатежа, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО11;
				ЗаписьПоНалогу.КБК    = КБК11;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате11, КодСтрокиСуммаАвансовогоПлатежа9_11);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_Доходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период), 10, ДеньПлатежа, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО11;
				ЗаписьПоНалогу.КБК    = КБК11;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате11, КодСтрокиСуммаПодлежащаяДоплате12_11);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_Доходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период) + 1, 3 + ?(ЭтоЮрЛицо, 0, 1), ДеньПлатежаГод, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО11;
				ЗаписьПоНалогу.КБК    = КБК11;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
		КонецЕсли;
		
		// Раздел 1.2.
		НалогКУплате12 = Неопределено;
		Если СохраненныеДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел12", НалогКУплате12) Тогда
			
			// Коды ОКТМО.
			КодСтрокиОКТМО12_010 = "П000120001003";
			КодСтрокиОКТМО12_030 = "П000120003003";
			КодСтрокиОКТМО12_060 = "П000120006003";
			КодСтрокиОКТМО12_090 = "П000120009003";
			
			Если ЗначениеЗаполнено(НалогКУплате12[КодСтрокиОКТМО12_090]) Тогда
				ОКТМО12 = НалогКУплате12[КодСтрокиОКТМО12_090];
			ИначеЕсли ЗначениеЗаполнено(НалогКУплате12[КодСтрокиОКТМО12_060]) Тогда
				ОКТМО12 = НалогКУплате12[КодСтрокиОКТМО12_060];
			ИначеЕсли ЗначениеЗаполнено(НалогКУплате12[КодСтрокиОКТМО12_030]) Тогда
				ОКТМО12 = НалогКУплате12[КодСтрокиОКТМО12_030];
			Иначе
				ОКТМО12 = НалогКУплате12[КодСтрокиОКТМО12_010];
			КонецЕсли;
			
			КБК12 = "18210501021011000110";
			
			КодСтрокиСуммаАвансовогоПлатежа3_12  = "П000120002003"; //сумма авансового платежа к уплате по сроку 1 кв.
			КодСтрокиСуммаАвансовогоПлатежа6_12  = "П000120004003"; //сумма авансового платежа к уплате по сроку за полугодие
			КодСтрокиСуммаАвансовогоПлатежа9_12  = "П000120007003"; //сумма авансового платежа к уплате по сроку за 9 мес.
			КодСтрокиСуммаПодлежащаяДоплате12_12 = "П000120010003"; //сумма налога, подлежащая доплате за год.
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате12, КодСтрокиСуммаАвансовогоПлатежа3_12);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_ДоходыМинусРасходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период), 04, ДеньПлатежа, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО12;
				ЗаписьПоНалогу.КБК    = КБК12;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате12, КодСтрокиСуммаАвансовогоПлатежа6_12);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_ДоходыМинусРасходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период), 07, ДеньПлатежа, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО12;
				ЗаписьПоНалогу.КБК    = КБК12;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате12, КодСтрокиСуммаАвансовогоПлатежа9_12);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_ДоходыМинусРасходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период), 10, ДеньПлатежа, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО12;
				ЗаписьПоНалогу.КБК    = КБК12;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате12, КодСтрокиСуммаПодлежащаяДоплате12_12);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_ДоходыМинусРасходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период) + 1, 3 + ?(ЭтоЮрЛицо, 0, 1), ДеньПлатежаГод, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО12;
				ЗаписьПоНалогу.КБК    = КБК12;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
			КодСтрокиСуммаМинНалога12 = "П000120012003"; //сумма мин. налога, подлежащая уплате
			
			СуммаНалога = ЗначениеПоказателя(НалогКУплате12, КодСтрокиСуммаМинНалога12);
			Если ЗначениеЗаполнено(СуммаНалога) Тогда
				ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
				ЗаписьПоНалогу.ВидНалога = Перечисления.ВидыНалогов.УСН_ДоходыМинусРасходы;
				ЗаписьПоНалогу.Период = Дата(Год(Период) + 1, 3 + ?(ЭтоЮрЛицо, 0, 1), ДеньПлатежаГод, 23, 59, 59);
				ЗаписьПоНалогу.ОКАТО  = ОКТМО12;
				ЗаписьПоНалогу.КБК    = КБК12;
				ЗаписьПоНалогу.Сумма  = СуммаНалога;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИдФормы = "ФормаОтчета2009Кв1" Тогда
		
		СохраненныеДанныеОтчета = РеквизитыРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(СохраненныеДанныеОтчета) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		Если НЕ СохраненныеДанныеОтчета.Свойство("ПоказателиОтчета") Тогда
			Возврат;
		КонецЕсли;
		// Раздел 1.
		// 010 - код ОКАТО
		// 020 - КБК налога
		// 060 - сумма
		// 080 - КБК минимального налога
		// 090 - сумма минимального налога
		
		НалогКУплате = Неопределено;
		Если СохраненныеДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел1", НалогКУплате) Тогда
			
			КодСтрокиОКАТО                   = "П000010002003";
			КодСтрокиКБК                     = "П000010002003";
			КодСтрокиСумма                   = "П000010006003";
			КодСтрокиКБКМинимальногоНалога   = "П000010008003";
			КодСтрокиСуммаМинимальногоНалога = "П000010009003";
			
			// Налог.
			ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
			ЗаписьПоНалогу.Период = Период;
			ЗаписьПоНалогу.ОКАТО  = НалогКУплате[КодСтрокиОКАТО];
			ЗаписьПоНалогу.КБК    = НалогКУплате[КодСтрокиКБК];
			ЗаписьПоНалогу.Сумма  = НалогКУплате[КодСтрокиСумма];
			
			// Минимальный налог.
			ЗаписьПоНалогу = ТаблицаДанныхРеглОтчета.Добавить();
			ЗаписьПоНалогу.Период = Период;
			ЗаписьПоНалогу.ОКАТО  = НалогКУплате[КодСтрокиОКАТО];
			ЗаписьПоНалогу.КБК    = НалогКУплате[КодСтрокиКБКМинимальногоНалога];
			ЗаписьПоНалогу.Сумма  = НалогКУплате[КодСтрокиСуммаМинимальногоНалога];
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаСумм = ТаблицаДанныхРеглОтчета;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаЭлектронныхПредставленийРегламентированнойОтчетности

Процедура ВыбратьДанныеЭП(РеквизитыЭлектронногоПредставления, ТаблицаСумм)
	
	Если РеквизитыЭлектронногоПредставления.ВидОтчета = Справочники.ВидыОтправляемыхДокументов.Прибыль Тогда
		ВыбратьДанныеЭП_РегламентированныйОтчетПрибыль(РеквизитыЭлектронногоПредставления, ТаблицаСумм);
	ИначеЕсли РеквизитыЭлектронногоПредставления.ВидОтчета = Справочники.ВидыОтправляемыхДокументов.НДС Тогда
		ВыбратьДанныеЭП_РегламентированныйОтчетНДС(РеквизитыЭлектронногоПредставления, ТаблицаСумм);
	ИначеЕсли РеквизитыЭлектронногоПредставления.ВидОтчета
			= Справочники.ВидыОтправляемыхДокументов.ДоходыИностраннымОрганизациям Тогда
		ВыбратьДанныеЭП_РегламентированныйОтчетДоходыИностраннымОрганизациям(РеквизитыЭлектронногоПредставления, ТаблицаСумм);
	ИначеЕсли РеквизитыЭлектронногоПредставления.ВидОтчета = Справочники.ВидыОтправляемыхДокументов.Форма6_НДФЛ Тогда
		ВыбратьДанныеЭП_РегламентированныйОтчет6_НДФЛ(РеквизитыЭлектронногоПредставления, ТаблицаСумм);
	ИначеЕсли РеквизитыЭлектронногоПредставления.ВидОтчета = Справочники.ВидыОтправляемыхДокументов.РасчетПоСтраховымВзносам Тогда
		ВыбратьДанныеЭП_РегламентированныйОтчетРасчетПоСтраховымВзносам(РеквизитыЭлектронногоПредставления, ТаблицаСумм);
	ИначеЕсли РеквизитыЭлектронногоПредставления.ВидОтчета = Справочники.ВидыОтправляемыхДокументов.ИсчисленныеСуммыНалогов Тогда
		ВыбратьДанныеЭП_РегламентированноеУведомлениеИсчисленныеСуммыНалогов(РеквизитыЭлектронногоПредставления, ТаблицаСумм);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыбратьДанныеЭП_РегламентированныйОтчетПрибыль(РеквизитыЭлектронногоПредставления, ТаблицаСумм)
	
	Перем АтрибутыЭлемента;
	
	ПоказателиВыгрузки = Новый Соответствие;
	
	ОбъектЧтениеXML = РеквизитыЭлектронногоПредставления.ОбъектЧтение;
	
	ИмяЭлементаСодержащегоКБК = "";
	
	ДобавлятьПоказатели = Истина;
	НовыйКБК = Ложь;
	
	ТекОКТМО = "";
	ТекКБК = "";
	
	Пока ОбъектЧтениеXML.Прочитать() Цикл
		Если ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = ОбъектЧтениеXML.Имя;
			
			Если ИмяЭлемента = "Прибыль" Тогда
				ДобавлятьПоказатели = Ложь;
			КонецЕсли;
			
			Если ИмяЭлементаСодержащегоКБК = "" Тогда
				НовыйКБК = Ложь;
				АтрибутыЭлемента = Новый СписокЗначений;
			КонецЕсли;
			
			Пока ОбъектЧтениеXML.ПрочитатьАтрибут() Цикл
				ИмяАтрибута = ВРег(ОбъектЧтениеXML.Имя);
				Если ИмяАтрибута = "ОКТМО" Тогда
					ТекОКТМО = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					Продолжить;
				ИначеЕсли ИмяАтрибута = "КБК" Тогда
					ИмяЭлементаСодержащегоКБК = ИмяЭлемента;
					ТекКБК = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					НовыйКБК = Истина;
					Продолжить;
				КонецЕсли;
				
				АтрибутыЭлемента.Добавить(ИмяАтрибута, ОбъектЧтениеXML.Значение);
				
				Если ДобавлятьПоказатели И ПоказателиВыгрузки[ОбъектЧтениеXML.Имя] = Неопределено Тогда
					ПоказателиВыгрузки.Вставить(ОбъектЧтениеXML.Имя, ОбъектЧтениеXML.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НовыйКБК
		   И ОбъектЧтениеXML.Имя = ИмяЭлементаСодержащегоКБК 
		   И ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ИмяЭлементаСодержащегоКБК = "";
			
			Если ЗначениеЗаполнено(ТекКБК) ИЛИ ЗначениеЗаполнено(ТекОКТМО) Тогда
				ИменаАтрибутовСумм = СтрРазделить("НалПУ,АвПлат1,АвПлат2,АвПлат3", ",");
				Для Инд = 0 По ИменаАтрибутовСумм.ВГраница() Цикл
					Для Каждого ЭлементАтрибута Из АтрибутыЭлемента Цикл
						Если ЭлементАтрибута.Значение = ВРег(ИменаАтрибутовСумм[Инд]) Тогда
							СуммаНалога = ЧислоИзСтроки(ЭлементАтрибута.Представление);
							ЗаписьПоНалогу = ТаблицаСумм.Добавить();
							ЗаписьПоНалогу.КБК   = ТекКБК;
							ЗаписьПоНалогу.ОКТМО = ТекОКТМО;
							ЗаписьПоНалогу.Сумма = СуммаНалога;
							ЗаписьПоНалогу.Дополнительно = ИменаАтрибутовСумм[Инд];
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыбратьДанныеЭП_РегламентированныйОтчетНДС(РеквизитыЭлектронногоПредставления, ТаблицаСумм)
	
	Перем АтрибутыЭлемента;
	
	ПоказателиВыгрузки = Новый Соответствие;
	
	ОбъектЧтениеXML = РеквизитыЭлектронногоПредставления.ОбъектЧтение;
	
	ИмяЭлементаСодержащегоКБК = "";
	
	ДобавлятьПоказатели = Истина;
	НовыйКБК = Ложь;
	
	ТекОКТМО = "";
	ТекКБК = "";
	
	Пока ОбъектЧтениеXML.Прочитать() Цикл
		Если ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = ОбъектЧтениеXML.Имя;
			
			Если ИмяЭлемента = "НДС" Тогда
				ДобавлятьПоказатели = Ложь;
			КонецЕсли;
			
			Если ИмяЭлементаСодержащегоКБК = "" Тогда
				НовыйКБК = Ложь;
				АтрибутыЭлемента = Новый СписокЗначений;
			КонецЕсли;
			
			Пока ОбъектЧтениеXML.ПрочитатьАтрибут() Цикл
				ИмяАтрибута = ВРег(ОбъектЧтениеXML.Имя);
				Если ИмяАтрибута = "ОКТМО" ИЛИ ИмяАтрибута = "ОКАТО" Тогда
					ТекОКТМО = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					Продолжить;
				ИначеЕсли ИмяАтрибута = "КБК" Тогда
					ИмяЭлементаСодержащегоКБК = ИмяЭлемента;
					ТекКБК = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					НовыйКБК = Истина;
					Продолжить;
				КонецЕсли;
				
				АтрибутыЭлемента.Добавить(ИмяАтрибута, ОбъектЧтениеXML.Значение);
				
				Если ДобавлятьПоказатели И ПоказателиВыгрузки[ОбъектЧтениеXML.Имя] = Неопределено Тогда
					ПоказателиВыгрузки.Вставить(ОбъектЧтениеXML.Имя, ОбъектЧтениеXML.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НовыйКБК
		   И ОбъектЧтениеXML.Имя = ИмяЭлементаСодержащегоКБК 
		   И ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ИмяЭлементаСодержащегоКБК = "";
			
			Если ЗначениеЗаполнено(ТекКБК) ИЛИ ЗначениеЗаполнено(ТекОКТМО) Тогда
				ИменаАтрибутовСумм = СтрРазделить("СумПУ_173.5,СумПУ_173.1,СумИсчисл", ",");
				Для Инд = 0 По ИменаАтрибутовСумм.ВГраница() Цикл
					Для Каждого ЭлементАтрибута Из АтрибутыЭлемента Цикл
						Если ЭлементАтрибута.Значение = ВРег(ИменаАтрибутовСумм[Инд]) Тогда
							СуммаНалога = ЧислоИзСтроки(ЭлементАтрибута.Представление);
							ЗаписьПоНалогу = ТаблицаСумм.Добавить();
							ЗаписьПоНалогу.КБК   = ТекКБК;
							ЗаписьПоНалогу.ОКТМО = ТекОКТМО;
							ЗаписьПоНалогу.Сумма = СуммаНалога;
							ЗаписьПоНалогу.Дополнительно = ИменаАтрибутовСумм[Инд];
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
				
				ИменаАтрибутовСумм = СтрРазделить("СумЗВ_176.1", ",");
				Для Инд = 0 По ИменаАтрибутовСумм.ВГраница() Цикл
					Для Каждого ЭлементАтрибута Из АтрибутыЭлемента Цикл
						Если ЭлементАтрибута.Значение = ВРег(ИменаАтрибутовСумм[Инд]) Тогда
							СуммаНалога = ЧислоИзСтроки(ЭлементАтрибута.Представление);
							ЗаписьПоНалогу = ТаблицаСумм.Добавить();
							ЗаписьПоНалогу.КБК   = ТекКБК;
							ЗаписьПоНалогу.ОКТМО = ТекОКТМО;
							ЗаписьПоНалогу.Сумма = -СуммаНалога;
							ЗаписьПоНалогу.Дополнительно = ИменаАтрибутовСумм[Инд];
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыбратьДанныеЭП_РегламентированныйОтчетДоходыИностраннымОрганизациям(
				РеквизитыЭлектронногоПредставления, ТаблицаСумм)
	
	Перем АтрибутыЭлемента;
	
	ПоказателиВыгрузки = Новый Соответствие;
	
	ОбъектЧтениеXML = РеквизитыЭлектронногоПредставления.ОбъектЧтение;
	
	ИмяЭлементаСодержащегоКБК = "";
	
	ДобавлятьПоказатели = Истина;
	НовыйКБК = Ложь;
	
	ТекОКТМО = "";
	ТекКБК = "";
	
	Пока ОбъектЧтениеXML.Прочитать() Цикл
		Если ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = ОбъектЧтениеXML.Имя;
			
			Если ИмяЭлемента = "ДохИнОрг" Тогда
				ДобавлятьПоказатели = Ложь;
			КонецЕсли;
			
			Если ИмяЭлементаСодержащегоКБК = "" Тогда
				НовыйКБК = Ложь;
				АтрибутыЭлемента = Новый СписокЗначений;
			КонецЕсли;
			
			Пока ОбъектЧтениеXML.ПрочитатьАтрибут() Цикл
				ИмяАтрибута = ВРег(ОбъектЧтениеXML.Имя);
				Если ИмяАтрибута = "ОКТМО" Тогда
					ТекОКТМО = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					Продолжить;
				ИначеЕсли ИмяАтрибута = "КБК" Тогда
					ИмяЭлементаСодержащегоКБК = ИмяЭлемента;
					ТекКБК = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					НовыйКБК = Истина;
					Продолжить;
				КонецЕсли;
				
				АтрибутыЭлемента.Добавить(ИмяАтрибута, ОбъектЧтениеXML.Значение);
				
				Если ДобавлятьПоказатели И ПоказателиВыгрузки[ОбъектЧтениеXML.Имя] = Неопределено Тогда
					ПоказателиВыгрузки.Вставить(ОбъектЧтениеXML.Имя, ОбъектЧтениеXML.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НовыйКБК
		   И ОбъектЧтениеXML.Имя = ИмяЭлементаСодержащегоКБК 
		   И ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ИмяЭлементаСодержащегоКБК = "";
			
			Если ЗначениеЗаполнено(ТекКБК) ИЛИ ЗначениеЗаполнено(ТекОКТМО) Тогда
				ИменаАтрибутовСумм = СтрРазделить("НалПУ", ",");
				Для Инд = 0 По ИменаАтрибутовСумм.ВГраница() Цикл
					ЭлементСумма = АтрибутыЭлемента.НайтиПоЗначению(ВРег(ИменаАтрибутовСумм[Инд]));
					Если ЭлементСумма <> Неопределено Тогда
						СуммаНалога = ЧислоИзСтроки(ЭлементСумма.Представление);
						ЗаписьПоНалогу = ТаблицаСумм.Добавить();
						ЗаписьПоНалогу.КБК   = ТекКБК;
						ЗаписьПоНалогу.ОКТМО = ТекОКТМО;
						ЗаписьПоНалогу.Сумма = СуммаНалога;
						
						ЭлементСрок = АтрибутыЭлемента.НайтиПоЗначению(ВРег("СрокУпл"));
						ЗаписьПоНалогу.Дополнительно = ?(ЭлементСрок <> Неопределено, ЭлементСрок.Представление, ИменаАтрибутовСумм[Инд]);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыбратьДанныеЭП_РегламентированныйОтчет6_НДФЛ(РеквизитыЭлектронногоПредставления, ТаблицаСумм)
	
	Перем АтрибутыЭлемента;
	
	КодыОП = КодыОтчетныхПериодов();
	
	ПоказателиВыгрузки = Новый Соответствие;
	
	ОбъектЧтениеXML = РеквизитыЭлектронногоПредставления.ОбъектЧтение;
	
	ИмяЭлементаСодержащегоКБК = "";
	
	ДобавлятьПоказатели = Истина;
	НовыйКБК = Ложь;
	
	ТекОКТМО = "";
	ТекКБК = "";
	
	Пока ОбъектЧтениеXML.Прочитать() Цикл
		Если ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = ОбъектЧтениеXML.Имя;
			
			Если ИмяЭлемента = "НДФЛ6.2" Тогда
				ДобавлятьПоказатели = Ложь;
			ИначеЕсли ИмяЭлемента = "РасчСумНал" Тогда
				Прервать;
			КонецЕсли;
			
			Если ИмяЭлементаСодержащегоКБК = "" Тогда
				НовыйКБК = Ложь;
				АтрибутыЭлемента = Новый СписокЗначений;
			КонецЕсли;
			
			Пока ОбъектЧтениеXML.ПрочитатьАтрибут() Цикл
				ИмяАтрибута = ВРег(ОбъектЧтениеXML.Имя);
				Если ИмяАтрибута = "ОКТМО" Тогда
					ТекОКТМО = ОбъектЧтениеXML.Значение;
					Продолжить;
				ИначеЕсли ИмяАтрибута = "КБК" Тогда
					ИмяЭлементаСодержащегоКБК = ИмяЭлемента;
					ТекКБК = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					НовыйКБК = Истина;
					Продолжить;
				КонецЕсли;
				
				Если ИмяЭлемента <> "СведСумНалВоз" Тогда // не учитываем суммы возвращенного налога
					АтрибутыЭлемента.Добавить(ИмяАтрибута, ОбъектЧтениеXML.Значение);
				КонецЕсли;  
				
				Если ДобавлятьПоказатели И ПоказателиВыгрузки[ОбъектЧтениеXML.Имя] = Неопределено Тогда
					ПоказателиВыгрузки.Вставить(ОбъектЧтениеXML.Имя, ОбъектЧтениеXML.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НовыйКБК
		   И ОбъектЧтениеXML.Имя = ИмяЭлементаСодержащегоКБК 
		   И ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ИмяЭлементаСодержащегоКБК = "";
			
			Если ЗначениеЗаполнено(ТекКБК) ИЛИ ЗначениеЗаполнено(ТекОКТМО) Тогда
				ИменаСуммНал = СтрРазделить("СУМНАЛ,СУМНАЛ1СРОК,СУМНАЛ2СРОК,СУМНАЛ3СРОК,СУМНАЛ4СРОК,СУМНАЛ5СРОК,СУМНАЛ6СРОК", ",");
				ИменаСрокНал = "СРОКНАЛ";
				СрокУплаты = Неопределено;
				Для Каждого ЭлементАтрибута Из АтрибутыЭлемента Цикл
					Если ИменаСуммНал.Найти(ЭлементАтрибута.Значение) <> Неопределено Тогда
						СуммаНалога = ЧислоИзСтроки(ЭлементАтрибута.Представление);
						
						ЗаписьПоНалогу = ТаблицаСумм.Добавить();
						ЗаписьПоНалогу.КБК   = ТекКБК;
						ЗаписьПоНалогу.ОКТМО = ТекОКТМО;
						ЗаписьПоНалогу.Сумма = СуммаНалога;
						
						ОкончаниеИмени = СтрЗаменить(ЭлементАтрибута.Значение, ИменаСуммНал[0], "");
						Если ЗначениеЗаполнено(ОкончаниеИмени) Тогда
							НомерСрокаУплаты = Лев(ОкончаниеИмени, 1);
							Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерСрокаУплаты) Тогда
								ОтчетГод = ЧислоИзСтроки(ПоказателиВыгрузки["ОтчетГод"]);
								СвойстваОП = КодыОП[ПоказателиВыгрузки["Период"]];
								Если ТипЗнч(СвойстваОП) = Тип("Структура") Тогда
									Если ОтчетГод > 2023 Тогда
										НомСрокаУплаты = Число(НомерСрокаУплаты);
										НомМесОП = СвойстваОП.НМ + Окр(НомСрокаУплаты / 2) - 3;
										Если НомСрокаУплаты % 2 = 0 Тогда // четный номер срока уплаты
											СрокУплаты = КонецМесяца(Дата(ОтчетГод, НомМесОП, 1));
										ИначеЕсли НомМесОП < 1 Тогда
											СрокУплаты = Дата(ОтчетГод, 12, 31, 23, 59, 59);
										Иначе
											СрокУплаты = Дата(ОтчетГод, НомМесОП, 22, 23, 59, 59);
										КонецЕсли;
									Иначе
										Если НомерСрокаУплаты = "4" И СвойстваОП.НМ <> 12 Тогда
											ТаблицаСумм.Удалить(ЗаписьПоНалогу);
											Продолжить;
										КонецЕсли;
										НомМесОП = СвойстваОП.НМ + Число(НомерСрокаУплаты) - 3;
										Если НомМесОП < 1 ИЛИ НомМесОП > 12 Тогда
											СрокУплаты = Дата(ОтчетГод, 12, 31, 23, 59, 59);
										Иначе
											СрокУплаты = Дата(ОтчетГод, НомМесОП, 22, 23, 59, 59);
										КонецЕсли;
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						
						ДополнительнаяИнформация = Новый Структура;
						ДополнительнаяИнформация.Вставить("СрокУплаты", СрокУплаты);
						ДополнительнаяИнформация.Вставить("ИменаАтрибутовСумм", ЭлементАтрибута.Значение);
						
						ЗаписьПоНалогу.Дополнительно = ДополнительнаяИнформация;
						
						СрокУплаты = Неопределено;
					ИначеЕсли ЭлементАтрибута.Значение = ИменаСрокНал Тогда
						СрокУплаты = ДатаИзСтроки(ЭлементАтрибута.Представление);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыбратьДанныеЭП_РегламентированныйОтчетРасчетПоСтраховымВзносам(РеквизитыЭлектронногоПредставления, ТаблицаСумм)
	
	Перем АтрибутыЭлемента;
	
	ПоказателиВыгрузки = Новый Соответствие;
	
	ОбъектЧтениеXML = РеквизитыЭлектронногоПредставления.ОбъектЧтение;
	
	ИмяЭлементаСодержащегоКБК = "";
	
	ДобавлятьПоказатели = Истина;
	НовыйКБК = Ложь;
	
	ТекОКТМО = "";
	ТекКБК = "";
	
	Пока ОбъектЧтениеXML.Прочитать() Цикл
		Если ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = ОбъектЧтениеXML.Имя;
			
			Если ИмяЭлемента = "РасчетСВ" Тогда
				ДобавлятьПоказатели = Ложь;
			КонецЕсли;
			
			Если ИмяЭлементаСодержащегоКБК = "" Тогда
				НовыйКБК = Ложь;
				АтрибутыЭлемента = Новый СписокЗначений;
			КонецЕсли;
			
			Пока ОбъектЧтениеXML.ПрочитатьАтрибут() Цикл
				ИмяАтрибута = ВРег(ОбъектЧтениеXML.Имя);
				Если ИмяАтрибута = "ОКТМО" Тогда
					ТекОКТМО = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					Продолжить;
				ИначеЕсли ИмяАтрибута = "КБК" Тогда
					ИмяЭлементаСодержащегоКБК = ИмяЭлемента;
					ТекКБК = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					НовыйКБК = Истина;
					Продолжить;
				КонецЕсли;
				
				АтрибутыЭлемента.Добавить(ИмяАтрибута, ОбъектЧтениеXML.Значение);
				
				Если ДобавлятьПоказатели И ПоказателиВыгрузки[ОбъектЧтениеXML.Имя] = Неопределено Тогда
					ПоказателиВыгрузки.Вставить(ОбъектЧтениеXML.Имя, ОбъектЧтениеXML.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НовыйКБК
		   И ОбъектЧтениеXML.Имя = ИмяЭлементаСодержащегоКБК 
		   И ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ИмяЭлементаСодержащегоКБК = "";
			
			Если ЗначениеЗаполнено(ТекКБК) ИЛИ ЗначениеЗаполнено(ТекОКТМО) Тогда
				ЕстьСуммы3М = Ложь;
				ИменаАтрибутовСумм = СтрРазделить("СумСВУпл1М,СумСВУпл2М,СумСВУпл3М,СумСВУплПер", ",");
				Для Инд = 0 По ИменаАтрибутовСумм.ВГраница() Цикл
					СуммаНалога = 0;
					ЭлементСумма = АтрибутыЭлемента.НайтиПоЗначению(ВРег(ИменаАтрибутовСумм[Инд]));
					Если ЭлементСумма <> Неопределено Тогда
						СуммаНалога = ЧислоИзСтроки(ЭлементСумма.Представление);
						Если Инд < 3 Тогда
							ЕстьСуммы3М = Истина;
						ИначеЕсли ЕстьСуммы3М Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					
					ЗаписьПоНалогу = ТаблицаСумм.Добавить();
					ЗаписьПоНалогу.КБК   = ТекКБК;
					ЗаписьПоНалогу.ОКТМО = ТекОКТМО;
					ЗаписьПоНалогу.Сумма = СуммаНалога;
					ЗаписьПоНалогу.Дополнительно = ИменаАтрибутовСумм[Инд];
				КонецЦикла;
				
				Если ОбъектЧтениеXML.Имя = "УплПревОСС" Тогда
					ЕстьСуммы3М = Ложь;
					ИменаАтрибутовСумм = СтрРазделить("ПревРасхСВ1М,ПревРасхСВ2М,ПревРасхСВ3М,ПревРасхСВПер", ",");
					Для Инд = 0 По ИменаАтрибутовСумм.ВГраница() Цикл
						СуммаНалога = 0;
						ЭлементСумма = АтрибутыЭлемента.НайтиПоЗначению(ВРег(ИменаАтрибутовСумм[Инд]));
						Если ЭлементСумма <> Неопределено Тогда
							СуммаНалога = ЧислоИзСтроки(ЭлементСумма.Представление);
							Если Инд < 3 Тогда
								ЕстьСуммы3М = Истина;
							ИначеЕсли ЕстьСуммы3М Тогда
								Продолжить;
							КонецЕсли;
							
							ЗаписьПоНалогу = ТаблицаСумм.Добавить();
							ЗаписьПоНалогу.КБК   = ТекКБК;
							ЗаписьПоНалогу.ОКТМО = ТекОКТМО;
							ЗаписьПоНалогу.Сумма = СуммаНалога;
							ЗаписьПоНалогу.Дополнительно = ИменаАтрибутовСумм[Инд];
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыбратьДанныеЭП_РегламентированноеУведомлениеИсчисленныеСуммыНалогов(
				РеквизитыЭлектронногоПредставления, ТаблицаСумм)
	
	Перем АтрибутыЭлемента;
	
	ПоказателиВыгрузки = Новый Соответствие;
	
	ОбъектЧтениеXML = РеквизитыЭлектронногоПредставления.ОбъектЧтение;
	
	ИмяЭлементаСодержащегоКБК = "";
	
	ДобавлятьПоказатели = Истина;
	НовыйКБК = Ложь;
	
	ТекОКТМО = "";
	ТекКБК = "";
	
	Пока ОбъектЧтениеXML.Прочитать() Цикл
		Если ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = ОбъектЧтениеXML.Имя;
			
			Если ИмяЭлемента = "УвИсчСумНалог" Тогда
				ДобавлятьПоказатели = Ложь;
			КонецЕсли;
			
			Если ИмяЭлементаСодержащегоКБК = "" Тогда
				НовыйКБК = Ложь;
				АтрибутыЭлемента = Новый СписокЗначений;
			КонецЕсли;
			
			Пока ОбъектЧтениеXML.ПрочитатьАтрибут() Цикл
				ИмяАтрибута = ВРег(ОбъектЧтениеXML.Имя);
				Если ИмяАтрибута = "ОКТМО" Тогда
					ТекОКТМО = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					Продолжить;
				ИначеЕсли ИмяАтрибута = "КБК" Тогда
					ИмяЭлементаСодержащегоКБК = ИмяЭлемента;
					ТекКБК = ОбъектЧтениеXML.Значение;
					ДобавлятьПоказатели = Ложь;
					НовыйКБК = Истина;
					Продолжить;
				КонецЕсли;
				
				АтрибутыЭлемента.Добавить(ИмяАтрибута, ОбъектЧтениеXML.Значение);
				
				Если ДобавлятьПоказатели И ПоказателиВыгрузки[ОбъектЧтениеXML.Имя] = Неопределено Тогда
					ПоказателиВыгрузки.Вставить(ОбъектЧтениеXML.Имя, ОбъектЧтениеXML.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если НовыйКБК
		   И ОбъектЧтениеXML.Имя = ИмяЭлементаСодержащегоКБК 
		   И ОбъектЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			ИмяЭлементаСодержащегоКБК = "";
			
			Если ЗначениеЗаполнено(ТекКБК) ИЛИ ЗначениеЗаполнено(ТекОКТМО) Тогда
				ИменаАтрибутовСумм = СтрРазделить("СумНалогАванс", ",");
				Для Инд = 0 По ИменаАтрибутовСумм.ВГраница() Цикл
					Для Каждого ЭлементАтрибута Из АтрибутыЭлемента Цикл
						Если ЭлементАтрибута.Значение = ВРег(ИменаАтрибутовСумм[Инд]) Тогда
							СуммаНалога = ЧислоИзСтроки(ЭлементАтрибута.Представление);
							ЗаписьПоНалогу = ТаблицаСумм.Добавить();
							ЗаписьПоНалогу.КБК   = ТекКБК;
							ЗаписьПоНалогу.ОКТМО = ТекОКТМО;
							ЗаписьПоНалогу.Сумма = СуммаНалога;
							
							ЭлемСрокУпл = АтрибутыЭлемента.НайтиПоЗначению("СрокУпл");
							Если ЭлемСрокУпл = Неопределено Тогда
								ЭлемПериод = АтрибутыЭлемента.НайтиПоЗначению("Период");
								ЭлемНомерМесКварт = АтрибутыЭлемента.НайтиПоЗначению("НомерМесКварт");
								Если ЭлемПериод <> Неопределено
								   И ЭлемНомерМесКварт <> Неопределено Тогда
									ЗаписьПоНалогу.Дополнительно = Новый Структура("Период,НомерМесКварт",
										ЭлемПериод.Представление, ЭлемНомерМесКварт.Представление);
								КонецЕсли;
							Иначе
								ЗаписьПоНалогу.Дополнительно = Новый Структура("СрокУпл", ЭлемСрокУпл.Представление);
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Получает значение показателя из сведений раздела отчета с проверкой его существования.
//
// Параметры:
//  СведенияОтчета - Структура - сведения раздела отчета
//  ИмяПоказателя - Строка
//  ЗначПоУмолчанию - Произвольный - используется если показатель не найден в переданных сведениях
//
// Возвращаемое значение:
//  Произвольный - значение показателя
//
Функция ЗначениеПоказателя(СведенияОтчета, ИмяПоказателя, ЗначПоУмолчанию = Неопределено)
	
	ЗначениеПоказателя = ЗначПоУмолчанию;
	
	Если СведенияОтчета.Свойство(ИмяПоказателя) Тогда
		ЗначениеПоказателя = СведенияОтчета[ИмяПоказателя];
	КонецЕсли;
	
	Возврат ЗначениеПоказателя;
	
КонецФункции

Функция СтрокаЧГ0(ИсходноеЧисло)
	
	Возврат Формат(ИсходноеЧисло, "ЧН=; ЧГ=0");
	
КонецФункции

Функция ЧислоИзСтроки(Стр)
	
	Если ЗначениеЗаполнено(Стр) Тогда
		Значение = XMLЗначение(Тип("Число"), " " + СокрЛП(СтрЗаменить(СтрЗаменить(Стр, Символы.НПП, ""), ",", ".")));
	Иначе
		Значение = 0;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция ДатаИзСтроки(СтрДата)
	
	Разделители = "-.,/";
	
	ДлинаСтроки = СтрДлина(СтрДата);
	
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("");
	
	Для НС = 1 По ДлинаСтроки Цикл
		Сим = Сред(СтрДата, НС, 1);
		Если Найти(Разделители, Сим) > 0 Тогда
			МассивПолей.Добавить("");
		ИначеЕсли Найти("0123456789", Сим) > 0 Тогда
			МассивПолей[МассивПолей.ВГраница()] = МассивПолей[МассивПолей.ВГраница()] + Сим;
		КонецЕсли;
	КонецЦикла;
	
	День  = Макс(1, Число("0" + СокрЛП(МассивПолей[0])));
	Месяц = Макс(1, Число("0" + ?(МассивПолей.ВГраница() < 1, "1", СокрЛП(МассивПолей[1]))));
	Год   = Макс(1, Число("0" + ?(МассивПолей.ВГраница() < 2, "1", СокрЛП(МассивПолей[2]))));
	
	Возврат Дата(Год, Месяц, День);
	
КонецФункции

Функция ПредставлениеДокумента(Ссылка, Наименование, ПредставлениеПериода, ПредставлениеВида)
	
	ПредставлениеДок = Наименование;
	
	ТипДокумента = ТипЗнч(Ссылка);
	
	Если ТипДокумента = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	РегламентированныеОтчеты.Представление КАК Представление
			|ИЗ
			|	Справочник.РегламентированныеОтчеты КАК РегламентированныеОтчеты
			|ГДЕ
			|	РегламентированныеОтчеты.ЭтоГруппа = ЛОЖЬ
			|	И РегламентированныеОтчеты.ПометкаУдаления = ЛОЖЬ
			|	И РегламентированныеОтчеты.ИсточникОтчета = &ИсточникОтчета
			|";
		Запрос.УстановитьПараметр("ИсточникОтчета", Ссылка.ИсточникОтчета);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			
			ПредставлениеДок = ВыборкаДетальныеЗаписи.Представление;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставлениеПериода) Тогда
		ПредставлениеДок = ПредставлениеДок + СтрШаблон(НСтр("ru = ' за %1'"), НРег(ПредставлениеПериода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставлениеВида) Тогда
		ПредставлениеДок = ПредставлениеДок + СтрШаблон(НСтр("ru = ' (%1)'"),
			?(СтрНачинаетсяС(ПредставлениеВида, "-"), НСтр("ru = 'П'"), ПредставлениеВида));
	КонецЕсли;
	
	Возврат ПредставлениеДок;
	
КонецФункции

#КонецОбласти
