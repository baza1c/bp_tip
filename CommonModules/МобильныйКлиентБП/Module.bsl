
#Область ОбработчикиСобытий
// Показывает форму недоступной функциональности для форм, неадаптированных к мобильному клиенту
// Источниками для подписки могут быть только объекты метаданных, у которых в указана ФормаСписка или ФормаОбъекта поумолчанию.
//
Процедура МобильныйКлиентБПСобытия(Источник, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	ОткрытьДоступнуюНаМобильномФорму(Источник, ВидФормы, Параметры, ВыбраннаяФорма, СтандартнаяОбработка);
КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Процедура обновляет данные о последнем входя пользователя с использованием мобильного клиента
Процедура ОбновитьСведенияОЗапуске() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Пользователь = Пользователи.ТекущийПользователь();
	ДатаПоследнейАктивности = НачалоДня(ТекущаяДатаСеанса());
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СведенияОПользователяхМК");
	ЭлементБлокировки.УстановитьЗначение("Пользователь", Пользователь);
	
	НаборЗаписей = РегистрыСведений.СведенияОПользователяхМК.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			НаборЗаписей.Добавить();
			НаборЗаписей[0].Пользователь = Пользователь;
		КонецЕсли;
		Если НаборЗаписей[0].ДатаПоследнейАктивности <> ДатаПоследнейАктивности Тогда
			НаборЗаписей[0].ДатаПоследнейАктивности = ДатаПоследнейАктивности;
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
		КонецЕсли;
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ПроверитьИЗарегистроватьСобытиеДляОценки(ИдентификаторОперации, ПоказыватьФормуОценки) Экспорт
	
	Если НЕ ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСобытия = РегистрыСведений.СчетчикСобытийОценкаПриложения.ПараметрыСобытияОценки(ИдентификаторОперации);
	
	ПоказыватьФормуОценки = ПараметрыСобытия.ПоказыватьФормуОценки;
	
	Если ПараметрыСобытия.РегистрироватьСобытие Тогда
		РегистрыСведений.СчетчикСобытийОценкаПриложения.СоздатьЗаписьПоСобытию(ИдентификаторОперации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьДоступнуюНаМобильномФорму(Источник, ВидФормы, Параметры, ВыбраннаяФорма, СтандартнаяОбработка)
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ОтключитьПроверкуПриОткрытииФорм")
		И Параметры.ОтключитьПроверкуПриОткрытииФорм Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиИсточника = СтрРазделить(Источник, ".");
	Менеджер = ЧастиИсточника[0];
	ИмяОбъекта = ЧастиИсточника[1];
	
	//Проверяются только основные формы объектов: ФормаОбъекта, ФормаСписка, ФормаВыбора, ОсновнаяФорма
	//В подписку включены все: Документы, ЖурналыДокументов.
	//Отчеты и обработки частично:
	//У которых можно и нужно переопределить основную форму на заглушку
	//Часть недоступных обработок и отчетов - заблокированы в командах в интерфейсе
	
	ФормаДоступнаБезЗапроса = Ложь;
	Если Менеджер = "ЖурналДокументовМенеджер" Тогда
		ФормаДоступнаБезЗапроса = ФормаЖурналаДокументовАдаптирована(ИмяОбъекта, ВидФормы);
	ИначеЕсли Менеджер = "ДокументМенеджер" Тогда
		ФормаДоступнаБезЗапроса = (ВидФормы = "ФормаВыбора")
		Или ФормаДокументаАдаптирована(ИмяОбъекта, ВидФормы);
	КонецЕсли;
	
	МобильныйКлиентБППереопределяемый.ПриОпределенииДоступностиФормы(Менеджер, ИмяОбъекта, ВидФормы, ФормаДоступнаБезЗапроса);
	
	Если ФормаДоступнаБезЗапроса Тогда
		Возврат;
	КонецЕсли;
	
	//Если форма включена в подписку, но не запрещена - покажем предупреждение
	//Запрещенные - явно блокируем
	ФормаДоступнаСПредупреждением = Не ФормаЗапрещенаВМобильном(Менеджер, ИмяОбъекта, ВидФормы);
	
	// Выбираем режим открытия для формы.
	ОткрываемаяФормаУказанаЯвно = (ВыбраннаяФорма <> Неопределено
								И ТипЗнч(ВыбраннаяФорма) <> Тип("Строка"));
	Если ОткрываемаяФормаУказанаЯвно Тогда // форма-объект метаданных

		ИмяФормы = ВыбраннаяФорма.ПолноеИмя();
		
	Иначе // требуется подобрать форму
		
		ОписаниеИсточника = Метаданные.НайтиПоТипу(ТипЗнч(Источник));
		ИмяФормы = ОписаниеИсточника.ПолноеИмя() + "." +
			?(ВыбраннаяФорма = Неопределено, ВидФормы, "Форма." + ВыбраннаяФорма);
			
	КонецЕсли;
	
	ЗаголовокФормы = "";
	Если Не Параметры.Свойство("Заголовок", ЗаголовокФормы) Тогда
		
		Если ОткрываемаяФормаУказанаЯвно Тогда
			
			ЗаголовокФормы = ВыбраннаяФорма.Синоним;
			
		Иначе
			
			Если ВидФормы = "ФормаСписка"
				И Не ПустаяСтрока(ОписаниеИсточника.РасширенноеПредставлениеСписка) Тогда
				
				ЗаголовокФормы = ОписаниеИсточника.РасширенноеПредставлениеСписка;
				
			ИначеЕсли ВидФормы = "ФормаСписка"
				И Не ПустаяСтрока(ОписаниеИсточника.ПредставлениеСписка) Тогда
				
				ЗаголовокФормы = ОписаниеИсточника.ПредставлениеСписка;
				
			Иначе
				
				ЗаголовокФормы = ОписаниеИсточника.Синоним;
				
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
	
	Ключ = Неопределено;
	Параметры.Свойство("Ключ", Ключ);
	ОписаниеНедоступногоДействия = ОписаниеНедоступногоДействия(ИмяФормы, ВидФормы, ЗаголовокФормы, Ключ);
	
	СтандартнаяОбработка = Ложь;
	ВыбраннаяФорма = "ОбщаяФорма.НедоступноНаМобильномКлиенте";
	
	ПараметрыОткрываемойФормы = Новый Структура;
	Для каждого ПараметрФормы Из Параметры Цикл
		ПараметрыОткрываемойФормы.Вставить(ПараметрФормы.Ключ, ПараметрФормы.Значение);
	КонецЦикла; 
	
	ПараметрыОткрытия = МобильныйКлиентБПКлиентСервер.НовыйПараметрыОткрытияФормыСПредупреждением();
	ПараметрыОткрытия.ИмяФормы                = ИмяФормы;
	ПараметрыОткрытия.Заголовок               = ЗаголовокФормы;
	ПараметрыОткрытия.ПереопределенаНаСервере = Истина;
	
	Параметры.Очистить();
	
	ПараметрыФормыНедоступно = МобильныйКлиентБПКлиентСервер.НовыйПараметрыФормаНедоступноНаМобильном();
	ПараметрыФормыНедоступно.ОписаниеНедоступногоДействия = ОписаниеНедоступногоДействия;
	ПараметрыФормыНедоступно.ФормаДоступнаСПредупреждением = ФормаДоступнаСПредупреждением;
	
	Параметры.Вставить("ПараметрыФормыНедоступно",  ПараметрыФормыНедоступно);
	Параметры.Вставить("ПараметрыОткрываемойФормы", ПараметрыОткрываемойФормы);
	Параметры.Вставить("ПереопределенаНаСервере",   ПараметрыОткрытия.ПереопределенаНаСервере);
	Параметры.Вставить("ПараметрыОткрытия",         ПараметрыОткрытия);
    
КонецПроцедуры

Функция ФормаЗапрещенаВМобильном(Менеджер, ИмяОбъекта, ВидФормы)
	
		ЗапрещеноПоказыватьВМобильном = Ложь;
	Если Менеджер = "ДокументМенеджер" Тогда
		ЗапрещеноПоказыватьВМобильном = 
			ИмяОбъекта = "РегламентированныйОтчет"
			Или ИмяОбъекта = "УведомлениеОСпецрежимахНалогообложения"
			Или ИмяОбъекта = "ЭлектронныйДокументВходящийЭДО" //запрет из-за отсутствия механизмов подписания
			Или ИмяОбъекта = "ЭлектронныйДокументИсходящийЭДО";
	ИначеЕсли Менеджер = "ЖурналДокументовМенеджер" Тогда
		ЗапрещеноПоказыватьВМобильном = Ложь;
	ИначеЕсли Менеджер = "РегистрСведенийМенеджер" Тогда
		//Запретим особенные формы
		ЗапрещеноПоказыватьВМобильном = ИмяОбъекта = "КорреспонденцииСчетов"
			Или ИмяОбъекта = "ЖурналСтатусовФинОтчетностиВБанки";
	ИначеЕсли Менеджер = "ОбработкаМенеджер" Тогда
		//обработки перечисленные в подписке, кроме
		ЗапрещеноПоказыватьВМобильном = Не (ИмяОбъекта = "ВводНачальныхОстатков");
	Иначе //отчеты перечисленные в подписке
		ЗапрещеноПоказыватьВМобильном = Истина;
	КонецЕсли;
	
	Возврат ЗапрещеноПоказыватьВМобильном;
	
КонецФункции

Функция ФормаЖурналаДокументовАдаптирована(ИмяОбъекта, ВидФормы)
	
	ФормаАдаптирована = 
		ИмяОбъекта = "Документы"
		или ИмяОбъекта = "Деньги"
		или ИмяОбъекта = "РозничныеПродажи"
		или ИмяОбъекта = "ЖурналОпераций";
	
	Возврат ФормаАдаптирована;
	
КонецФункции

Функция ФормаДокументаАдаптирована(ИмяОбъекта, ВидФормы)
	
	ФормаАдаптирована =
		ИмяОбъекта = "СчетНаОплатуПокупателю"
		или ИмяОбъекта = "СчетНаОплатуПоставщика"
		или ИмяОбъекта = "ПлатежноеПоручение"
		или ИмяОбъекта = "ПоступлениеНаРасчетныйСчет"
		или ИмяОбъекта = "СписаниеСРасчетногоСчета"
		или ИмяОбъекта = "ПриходныйКассовыйОрдер"
		или ИмяОбъекта = "РасходныйКассовыйОрдер"
		или ИмяОбъекта = "АктСверкиВзаиморасчетов"
		или ИмяОбъекта = "ВозвратТоваровОтПокупателя"
		или ИмяОбъекта = "ВозвратТоваровПоставщику"
		или ИмяОбъекта = "Доверенность"
		или ИмяОбъекта = "КоммерческоеПредложение"
		или ИмяОбъекта = "ПоступлениеТоваровУслуг"
		или ИмяОбъекта = "РеализацияТоваровУслуг"
		или ИмяОбъекта = "РеализацияОтгруженныхТоваров"
		или ИмяОбъекта = "РозничнаяПродажа"
		или ИмяОбъекта = "ОтчетОРозничныхПродажах"
		или ИмяОбъекта = "ОплатаПлатежнойКартой"
		или ИмяОбъекта = "СчетФактураПолученный"
		или ИмяОбъекта = "СчетФактураВыданный"
		или ИмяОбъекта = "ПоступлениеДопРасходов"
		или ИмяОбъекта = "ОперацияСПатентом"
		или ИмяОбъекта = "ИнвентаризацияТоваровНаСкладе"
		или ИмяОбъекта = "ЭлектронноеПисьмоИсходящее"
		или ИмяОбъекта = "ЭлектронноеПисьмоВходящее"
		или ИмяОбъекта = "УведомлениеОбИсчисленныхСуммахНалогов"
		или ИмяОбъекта = "УведомлениеОбУменьшенииНалогаПоПатенту"
		или ИмяОбъекта = "УстановкаЦенНоменклатуры"
		//Добавлен для целостности учета, но не адаптирован
		или ИмяОбъекта = "ДокументРасчетовСКонтрагентом"
		или ИмяОбъекта = "ТелефонныйЗвонок"
		или ИмяОбъекта = "Встреча"
		или ИмяОбъекта = "ПрочееВзаимодействие";
	
	Возврат ФормаАдаптирована;
	
КонецФункции

Функция ОписаниеНедоступногоДействия(ИмяФормы, ВидФормы, ЗаголовокФормы, Ключ = Неопределено)
	
	ТекстШаблонаРекомендация = НСтр("ru = '%1 ""%2""'");
	Если ИмяФормы = "Документ.РегламентированныйОтчет.ФормаОбъекта"
		Или ИмяФормы = "Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаДокумента"
		Или СтрНачинаетсяС(ИмяФормы, "Отчет.") Тогда
		ТипСтрокой = "Форма отчета";
		Если Не ЗначениеЗаполнено(Ключ) Тогда
			ПредставлениеОбъекта = ЗаголовокФормы;
		Иначе
			ПредставлениеОбъекта = Строка(Ключ);
		КонецЕсли;
		ТекстПредупреждения = СтрШаблон(ТекстШаблонаРекомендация,
			ТипСтрокой,
			ПредставлениеОбъекта);
	ИначеЕсли СтрНачинаетсяС(ИмяФормы, "Документ.") Тогда
		Если ВидФормы = "ФормаСписка" Тогда
			ТипСтрокой = "Форма списка документов";
		Иначе
			ТипСтрокой = "Форма документа";
		КонецЕсли;
		
		Если ПустаяСтрока(Ключ) Тогда
			ПредставлениеОбъекта = ЗаголовокФормы;
		Иначе
			ПредставлениеОбъекта = Строка(Ключ);
		КонецЕсли;
		ТекстПредупреждения = СтрШаблон(ТекстШаблонаРекомендация,
			ТипСтрокой,
			ПредставлениеОбъекта);
	Иначе
		ТекстПредупреждения = СтрШаблон(НСтр("ru = 'Форма ""%1""'"),
			ЗаголовокФормы);
	КонецЕсли;
	
	Возврат ТекстПредупреждения;
	
КонецФункции

#КонецОбласти


