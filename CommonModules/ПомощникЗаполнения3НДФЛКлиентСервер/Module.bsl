#Область ПрограммныйИнтерфейс

// Проверяет ИНН и КПП на соответствие требованиям
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма
//   ПроверитьИНН - Булево - Признак проверки ИНН
//   ПроверитьКПП - Булево - Признак проверки КПП
//
Процедура ПроверитьИННКПП(Форма, ПроверитьИНН, ПроверитьКПП) Экспорт
	
	ЭтоЮрЛицо = Форма.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо");
	
	Если ПроверитьИНН Тогда
		
		РезультатПроверки  = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(Форма.ИНН, ЭтоЮрЛицо);
		Форма.ОшибокПоИННнет     = НЕ ЗначениеЗаполнено(РезультатПроверки.ОписаниеОшибки);
		
		Форма.РезультатПроверкиИНН = Новый ФорматированнаяСтрока(РезультатПроверки.ОписаниеОшибки,, Форма.ЦветВыделенияНекорректногоЗначение);
		
		Форма.НадписьПоясненияНекорректногоИНН = Новый ФорматированнаяСтрока(Форма.РезультатПроверкиИНН);
		Форма.Элементы.НадписьПоясненияНекорректногоИНН.Видимость = НЕ ПустаяСтрока(Форма.РезультатПроверкиИНН);
		
	КонецЕсли;
	
	Если ПроверитьКПП Тогда
		
		РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямФорматаКПП(Форма.КПП, ЭтоЮрЛицо);
		Форма.ОшибокПоКППнет = РезультатПроверки.СоответствуетТребованиям;
		
		Форма.РезультатПроверкиКПП = Новый ФорматированнаяСтрока(РезультатПроверки.ОписаниеОшибки,, Форма.ЦветВыделенияНекорректногоЗначение);
		
		Форма.НадписьПоясненияНекорректногоКПП = Новый ФорматированнаяСтрока(Форма.РезультатПроверкиКПП);
		Форма.Элементы.НадписьПоясненияНекорректногоКПП.Видимость = НЕ ПустаяСтрока(Форма.РезультатПроверкиКПП);
		
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает видимость полей контрагента
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма
//   ОтображатьРеквизитыФизЛица - Булево - Признак отображения реквизитов физического лица
//
Процедура УстановитьВидимостьПолейКонтрагента(Форма, ОтображатьРеквизитыФизЛица = Истина) Экспорт
	
	ЭтоЮридическоеЛицо = 
		Форма.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо");
	
	ЭтоФизическоеЛицо = 
		Форма.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	
	Элементы = Форма.Элементы;
	
	ВидимостьГруппыПоискаИНННаименование = ЭтоЮридическоеЛицо И Форма.ЗаполнениеРеквизитовПлашкой;
	Элементы.ПолеПоискаИНННаименование.Видимость = ВидимостьГруппыПоискаИНННаименование;
	Элементы.ЗаполнитьРеквизитыПоДаннымЕГР.Видимость = ВидимостьГруппыПоискаИНННаименование;
	
	Элементы.Наименование.Видимость = ЭтоЮридическоеЛицо;
	Элементы.КнопкаЗаполнитьРеквизитыПоНаименованию.Видимость = НЕ Форма.ЗаполнениеРеквизитовПлашкой И ЭтоЮридическоеЛицо;
	
	Если Элементы.Найти("ФИО") <> Неопределено Тогда
		Элементы.ФИО.Видимость = ЭтоФизическоеЛицо;
	КонецЕсли;
	
	Элементы.ИНН.Видимость = ОтображатьРеквизитыФизЛица ИЛИ ЭтоЮридическоеЛицо;
	Элементы.КомандаЗаполнитьПоИНН.Видимость = НЕ Форма.ЗаполнениеРеквизитовПлашкой И ЭтоЮридическоеЛицо;
	
	Элементы.КПП.Видимость       = ЭтоЮридическоеЛицо;
	
	Если Элементы.Найти("ОКТМО") <> Неопределено Тогда
		Элементы.ОКТМО.Видимость     = ОтображатьРеквизитыФизЛица ИЛИ ЭтоЮридическоеЛицо;
	КонецЕсли;
	
	Элементы.НадписьПоясненияНекорректногоИНН.Видимость = (ОтображатьРеквизитыФизЛица ИЛИ ЭтоЮридическоеЛицо) 
		И НЕ ПустаяСтрока(Форма.РезультатПроверкиИНН);
	Элементы.НадписьПоясненияНекорректногоКПП.Видимость = ЭтоЮридическоеЛицо И НЕ ПустаяСтрока(Форма.РезультатПроверкиКПП);
	
КонецПроцедуры

// Устанавливает ключ сохранения положения окна формы
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма
//   ПрефиксКлючаСохранения - Строка - Префикс ключа сохранения положения окна формы
//
Процедура УстановитьКлючСохраненияПоложенияОкна(Форма, ПрефиксКлючаСохранения) Экспорт
	
	ЭтоЮридическоеЛицо = 
		Форма.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо");
	ЭтоФизическоеЛицо = 
		Форма.ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	
	Если ЭтоЮридическоеЛицо Тогда
		ИмяКлюча = ПрефиксКлючаСохранения + ?(Форма.ЗаполнениеРеквизитовПлашкой,
			"ЮридическоеЛицо", "ЮридическоеЛицоНовый");
	ИначеЕсли ЭтоФизическоеЛицо Тогда
		ИмяКлюча = ПрефиксКлючаСохранения + "ФизическоеЛицо";
	Иначе
		ИмяКлюча = ПрефиксКлючаСохранения;
	КонецЕсли;
	
	Форма.КлючСохраненияПоложенияОкна = ИмяКлюча;
	
КонецПроцедуры

// Возвращает структуру вариантов действия с налогом к возврату.
// 
// Возвращаемое значение:
//   Структура - Описание:
//     * НеВозвращать - Строка - не заполнять заявление на возврат налога.
//     * ВернутьНаСчет - Строка - заполнить заявление на возврат налога на банковский счет.
//
Функция ВариантыДействийСНалогомКВозврату() Экспорт
	
	Варианты = Новый Структура;
	Варианты.Вставить("ВернутьНаСчет", "ВернутьНаСчет");
	Варианты.Вставить("НеВозвращать", "НеВозвращать");
	
	Возврат Варианты;
	
КонецФункции

// Возвращает имя периода, который используется как суффикс для показателей.
//
// Параметры:
//   НомерПериода - Число - Номер периода по порядку 
//
// Возвращаемое значение:
//   Строка - Имя периода
//
Функция ИмяПериодаНарастающимИтогом(НомерПериода) Экспорт
	
	Если НомерПериода = 1 Тогда
		Возврат "1Квартал";
	ИначеЕсли НомерПериода = 2 Тогда
		Возврат "Полугодие";
	ИначеЕсли НомерПериода = 3 Тогда
		Возврат "9Месяцев";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции 

// Управление свойствами формы
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма
//
Процедура УправлениеФормой(Форма) Экспорт

	Элементы = Форма.Элементы;
	
	КоличествоДоступныхДоходов = Форма.РаспределениеПоДоходам.Количество();
	
	Если Элементы.Найти("ВидДохода") <> Неопределено Тогда
		Элементы.ВидДохода.Видимость = (КоличествоДоступныхДоходов = 0);
	КонецЕсли;
	
	Элементы.ГруппаВидДохода.Видимость = (КоличествоДоступныхДоходов > 0);
	Элементы.ОдинВидДохода.Видимость = (КоличествоДоступныхДоходов = 1);
	Элементы.ГруппаНесколькоДоходов.Видимость = (КоличествоДоступныхДоходов > 1);
	Элементы.РаспределениеПоДоходам.Видимость = (Форма.ВариантРаспределенияВычета = 1);
	Элементы.ВидДоходаВычета.Доступность = (Форма.ВариантРаспределенияВычета = 0);
	
	ВычетУказан = (Форма.СуммаВычета > 0);
	
	Элементы.ВариантРаспределенияВычетаНадпись.Доступность = ВычетУказан;
	Элементы.ГруппаВариантыРаспределенияВычета.Доступность = ВычетУказан;
	Элементы.РаспределениеПоДоходам.Доступность = ВычетУказан;
	
	Если Форма.РаспределениеПоДоходам.Итог("СуммаВычета") > Форма.СуммаВычета Тогда
		РаспределитьСуммуВычета(Форма);
	КонецЕсли;
	
	Форма.НалоговаяБазаВсего = Форма.РаспределениеПоДоходам.Итог("НалоговаяБаза");
	Форма.РаспределеноВычетаВсего = Форма.РаспределениеПоДоходам.Итог("СуммаВычета");
	
	ЭлементСуммаВычета = Элементы.Найти("ДекорацияСуммаВычета");
	
	Если ЭлементСуммаВычета <> Неопределено Тогда
		
		Если ВычетУказан Тогда
			ЭлементСуммаВычета.Видимость = Истина;
			ЭлементСуммаВычета.Заголовок = СтрШаблон(
				НСтр("ru = 'Вам полагается вычет в размере: %1 руб.'"),
				Формат(Форма.СуммаВычета, "ЧДЦ=2;"));
		ИначеЕсли СтрНайти(Форма.ИмяФормы, "ПродажаИмущества") > 0 Тогда
			ЭлементСуммаВычета.Заголовок = НСтр("ru = 'Имущественный вычет был применён ранее'");
			ЭлементСуммаВычета.Видимость = Истина;
		Иначе
			ЭлементСуммаВычета.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Распределяет сумму положенного вычета по умолчанию: по попрядку следования доходов
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма, для которой настраивается распределение вычета по доходам
//
Процедура РаспределитьСуммуВычета(Форма) Экспорт
	
	СуммаКРаспределению = Форма.СуммаВычета;
	
	Для Каждого ТекущаяСтрока Из Форма.РаспределениеПоДоходам Цикл
		Если СуммаКРаспределению <= 0 Тогда
			ТекущаяСтрока.СуммаВычета = 0;
			Продолжить;
		КонецЕсли; 
		ТекущаяСтрока.СуммаВычета = Мин(СуммаКРаспределению, ТекущаяСтрока.НалоговаяБаза);
		СуммаКРаспределению = СуммаКРаспределению - ТекущаяСтрока.СуммаВычета;
	КонецЦикла;
	
	Форма.НалоговаяБазаВсего = Форма.РаспределениеПоДоходам.Итог("НалоговаяБаза");
	Форма.РаспределеноВычетаВсего = Форма.РаспределениеПоДоходам.Итог("СуммаВычета");
	
КонецПроцедуры

// Возвращает признак разделения шага помощника на отдельные этапы: доходы и вычеты
//
// Параметры:
//   ВыбраннаяФорма - Строка - Имя формы регламентированного отчета Декларации 3-НДФЛ
//
// Возвращаемое значение:
//   Булево - Истина для формы декларации 3-НДФЛ с 2021 г.
//
Функция ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма) Экспорт
	
	Если ВыбраннаяФорма = "ФормаОтчета2021кв1"
		Или ВыбраннаяФорма = "ФормаОтчета2020кв1"
		Или ВыбраннаяФорма = "ФормаОтчета2019кв1"
		Или ВыбраннаяФорма = "ФормаОтчета2018кв1" Тогда
			
		Возврат Ложь;
	
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

// Возвращает идентификатор основной налоговой базы НДФЛ: 13 - 22% с 2025 г., 13 - 15% за период 2021 - 2024 гг.
//
// Параметры:
//   ВыбраннаяФорма - Строка - Имя редакции формы декларации
//
// Возвращаемое значение:
//    Число - идентификатор прогрессивной шкалы НДФЛ
//
Функция ИдентификаторОсновнойНалоговойБазы(ВыбраннаяФорма) Экспорт
	
	Если УпрощеннаяФормаДекларации(ВыбраннаяФорма) Тогда
		Возврат "13/15/18/20/22";
	Иначе
		Возврат "13/15";
	КонецЕсли;
	
КонецФункции

// Возвращает курс валюты с учетом кратности
//
// Параметры:
//   КурсВалюты - Число - Курс валюты, как он указан в форме ввода дохода
//   Кратность  - Число - Кратность валюты, как она указана в форме ввода дохода
//
// Возвращаемое значение:
//   Число
//
Функция КурсВалютыДляОтчетности(КурсВалюты, Кратность) Экспорт
	
	// В форме декларации 3-НДФЛ указывается только курс валюты, который уже должен учитывать кратность
	Возврат КурсВалюты / ?(ЗначениеЗаполнено(Кратность), Кратность, 1);
	
КонецФункции

// Возвращает признак того, что для выбранной редакции формы декларации 3-НДФЛ
// используется измененная форма регламентированного отчета
//
// Параметры:
//   ВыбраннаяФорма - Строка - Имя формы регламентированного отчета Декларации 3-НДФЛ
//
// Возвращаемое значение:
//   Булево
//
Функция УпрощеннаяФормаДекларации(ВыбраннаяФорма) Экспорт
	
	Возврат ВыбраннаяФорма = "ФормаОтчета2025Кв4";
	
КонецФункции

// Возвращает дату начала действия нового отчета декларации 3-НДФЛ
//
// Возвращаемое значение:
//   Дата
//
Функция НачалоДействияУпрощеннойФормыДекларации() Экспорт
	
	Возврат '20260101';
	
КонецФункции

// Возвращает признак того, что для выбранной редакции формы декларации 3-НДФЛ
// требуется проверка реквизитов брокера для инвестиционного вычета
//
// Параметры:
//   Период - Дата - Период, за который заполняется декларация
//
// Возвращаемое значение:
//   Булево
//
Функция ПроверятьРеквизитыБрокера(Период) Экспорт
	
	Возврат Период > '20240101';
	
КонецФункции

// Возвращает идентификаторы групп доходов декларации 3-НДФЛ
//
// Возвращаемое значение:
//   Структура:
//   * Основная - Строка - Идентификатор основной группы доходов
//   * Имущество - Строка - Идентификатор группы доходов по имуществу
//   * ОсобыеУсловия - Строка - Идентификатор группы доходов по особым условиям
//   * ВыигрышЭкономияПаи - Строка - Идентификатор группы доходов по выигрышам, экономии на процентах и паям
//   * ИныеДоходы - Строка - Идентификатор группы доходов по иным доходам резидентов по иным ставкам
//   * Нерезиденты - Строка - Идентификатор группы доходов по доходам нерезидентов
//   * ДивидендыНерезидентов - Строка - Идентификатор группы доходов по дивидендам нерезидентов
//   * ОплатаТрудаНерезидентов - Строка - Идентификатор группы доходов по оплате труда нерезидентов
//   * ДивидендыПАОНерезиденты - Строка - Идентификатор группы доходов по дивидендам от ПАО нерезидентов
//   * БанковскиеПроцентыНерезиденты - Строка - Идентификатор группы доходов по банковским процентам нерезидентов
//   * ИныеДоходыНерезидентов - Строка - Идентификатор группы доходов по иным доходам нерезидентов по иным ставкам
//
Функция ГруппыДоходов() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Основная", "01");
	Результат.Вставить("Имущество", "02");
	Результат.Вставить("ОсобыеУсловия", "03");
	Результат.Вставить("ВыигрышЭкономияПаи", "04");
	Результат.Вставить("ИныеДоходы", "05");
	Результат.Вставить("Нерезиденты", "06");
	Результат.Вставить("ДивидендыНерезидентов", "07");
	Результат.Вставить("ОплатаТрудаНерезидентов", "08");
	Результат.Вставить("ДивидендыПАОНерезиденты", "09");
	Результат.Вставить("БанковскиеПроцентыНерезиденты", "10");
	Результат.Вставить("ИныеДоходыНерезидентов", "11");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция КлючПоляВычетГТО() Экспорт
	
	Возврат "СтандартныйВычетЗаСдачуНормГТО";
	
КонецФункции

Функция КлючРасшифровкиДоходов() Экспорт
	
	Возврат "ДоходыПоГруппе";
	
КонецФункции

Функция КлючРасшифровкиВычетов() Экспорт
	
	Возврат "ВычетыПоГруппе";
	
КонецФункции

Функция ПредставлениеДоходаОтПродажиПрочегоИмущества(ВыбраннаяФорма) Экспорт
	
	Если УпрощеннаяФормаДекларации(ВыбраннаяФорма) Тогда
		Результат = НСтр("ru = 'Прочее имущество, криптовалюта, майнинг'");
	ИначеЕсли ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма) Тогда
		Результат = НСтр("ru = 'Продажа прочего имущества, криптовалюты'");
	Иначе
		Результат = НСтр("ru = 'Продажа прочего имущества'");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторыВидовИмущества() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Жилая", "Жилая");
	Результат.Вставить("Недвижимость", "Недвижимость");
	Результат.Вставить("ПрочаяНедвижимость", "ПрочаяНедвижимость");
	Результат.Вставить("Доля", "Доля");
	Результат.Вставить("ДоляУставногоКапитала", "ДоляУставногоКапитала");
	Результат.Вставить("ДолевоеСтроительство", "ДолевоеСтроительство");
	Результат.Вставить("Автомобиль", "Автомобиль");
	Результат.Вставить("ПрочееИмущество", "ПрочееИмущество");
	Результат.Вставить("Криптовалюта", "Криптовалюта");
	Результат.Вставить("Майнинг", "Майнинг");
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоДоходОтПродажиДолиУставногоКапитала(ВидИмущества) Экспорт
	
	Возврат ВидИмущества = ИдентификаторыВидовИмущества().ДоляУставногоКапитала;
	
КонецФункции

Функция ЭтоДолевоеСтроительство(ВидИмущества) Экспорт
	
	Возврат ВидИмущества = ИдентификаторыВидовИмущества().ДолевоеСтроительство;
	
КонецФункции

Функция ЭтоПродажаАвтомобиля(ВидИмущества) Экспорт
	
	Возврат ВидИмущества = ИдентификаторыВидовИмущества().Автомобиль;
	
КонецФункции

Функция ЭтоПродажаКриптовалюты(ВидИмущества) Экспорт
	
	Возврат ВидИмущества = ИдентификаторыВидовИмущества().Криптовалюта;
	
КонецФункции

Функция ВидПолученияДоходаПоИмуществу() Экспорт
	
	// Для заполнения соответствующего признака в Расчете к Приложению 1
	Возврат "1";
	
КонецФункции

Функция ПризнакПополненияСчетаИИС() Экспорт
	
	Возврат "1";
	
КонецФункции

Функция ПризнакДоходаПоОперациямИИС() Экспорт
	
	Возврат "2";
	
КонецФункции

#КонецОбласти
