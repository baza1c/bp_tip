


#Область ПрограммныйИнтерфейс

// Получает мобильные номера телефонов контактных лиц, контрагента.
// Рекомендуется передавать номера мобильных телефонов контактного лица и контрагента, где они заполнены.
// Мобильный телефон используется для отправки сообщения через whatsapp на конкретный номер.
//
// Параметры:
//  Контрагент           - ОпределяемыйТип.КонтрагентБЭД - ссылка на элемент справочника контрагенты.
//  КонтактнаяИнформация - Массив из см. ИнтеграцияShareКлиентСервер.НовыйКонтактнаяИнформация - контактная информация.
//
Процедура КонтактныеЛицаСНомеромТелефона(Знач Контрагент, КонтактнаяИнформация) Экспорт
	
КонецПроцедуры

// Определяет состав команд для использования сервиса 1С:Share.
//
// Параметры:
//  СоставКоманд - См. ИнтеграцияShare.НовыйСоставКомандShare
//
Процедура ПриОпределенииСоставаКомандShare(СоставКоманд) Экспорт
	
	СоставКоманд.Входящие.Добавить(Метаданные.Документы.СчетНаОплатуПоставщика.ПолноеИмя());
	СоставКоманд.Входящие.Добавить(Метаданные.Документы.АктСверкиВзаиморасчетов.ПолноеИмя());
	
КонецПроцедуры

// Переопределяет гиперссылку на инструкцию по работе с сервисом 1С:Share для прикладной базы.
// Например, url-страница сайта ИТС с описанием сервиса как в нем делиться документом и загружать.
//
// Параметры:
//  НавигационнаяСсылка - Строка - гиперссылка на инструкцию по работе с сервисом 1С:Share для прикладной базы.
//
Процедура ПриОпределенииГиперссылкиНаИнструкциюПоРаботеССервисом(НавигационнаяСсылка) Экспорт
	
КонецПроцедуры

// Переопределяет файлы документа для выбора к публикации в сервис 1С:Share.
// При стандартной обработке файлы заполняются из присоединенных файлов к документу и не помеченные на удаление,
// используется метод БСП РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту и метод интеграция с 1С:Документооборотом
// ИнтеграцияС1СДокументооборотБазоваяФункциональность.ДанныеФайловОбъектаИС.
//
// Параметры:
//  ДанныеФайлов - Массив из см. ИнтеграцияShare.НовыеДанныеПрисоединенногоФайла - данные файлов для выбора к публикации.
//                                                                                 Выходной параметр.
//  СсылкаНаДокумент - ДокументСсылка, СправочникСсылка - ссылка на публикуемый документ.
//  СтандартнаяОбработка - Булево - признак стандартной обработки. По умолчанию Истина.
//
Процедура ПриОпределенииФайловДокументаДляВыбораКПубликации(ДанныеФайлов, Знач СсылкаНаДокумент, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Переопределяет данные публикуемого документа в сервис 1С:Share.
//
// Параметры:
//  ДанныеТранспортногоКонтейнера - ДеревоЗначений из см. ДеревоЭлектронногоДокументаБЭД.ДеревоЭлектронногоДокумента -
//                                  данные для заполнения транспортного контейнера. Строится по макету. 
//                                 см. Обработка.СервисShare.Макет.ТранспортныйКонтейнер. Выходной параметр.
//  СсылкаНаДокумент - ДокументСсылка, СправочникСсылка - электронный документ к публикации.
//
Процедура ПриОпределенииДанныхПубликуемогоДокумента(ДанныеТранспортногоКонтейнера, Знач СсылкаНаДокумент) Экспорт
	
КонецПроцедуры

// Переопределяет представление публикуемого документа.
// При необходимости можно переопределить параметры из свойства
// "ПараметрыПубликуемогоДокументаДляВизуализации.СвойстваПредставления" при использовании стандартной обработки.
//
// Параметры:
//  ОписаниеПредставленияДокумента - Структура из См. РаботаСФайламиБЭД.НовоеОписаниеФайла - представление документа
//      в поддерживаемом формате визуализации. См. ИнтеграцияShareКлиентСервер.ДоступныеРасширенияФайловВизуализации
//      Имя файла используется в качестве имени отправляемого документа, которое отображается пользователю при просмотре
//      файла визуализации. Выходной параметр.
//  ПараметрыПубликуемогоДокументаДляВизуализации - См. ИнтеграцияShare.НовыеПараметрыПубликуемогоДокументаДляВизуализации -
//                            параметры публикуемого документа. 
//  СтандартнаяОбработка    - Булево - признак стандартной обработки. По умолчанию Истина.
//
Процедура ПриОпределенииВизуализацииПубликуемогоДокумента(ОписаниеПредставленияДокумента,
	Знач ПараметрыПубликуемогоДокументаДляВизуализации, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ПараметрыПубликуемогоДокументаДляВизуализации.ОбъектУчета) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
	
		СтандартнаяОбработка = Ложь;
		
		КомандыПечати = УправлениеПечатью.КомандыПечатиФормы("Документ.СчетНаОплатуПокупателю.Форма.ФормаСписка");
		НастройкиСохранения = УправлениеПечатью.НастройкиСохранения();
		ФорматыСохранения = Новый Массив;
		ФорматыСохранения.Добавить(СтандартныеПодсистемыСервер.ТипФайлаТабличногоДокументаPDF());
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(ПараметрыПубликуемогоДокументаДляВизуализации.ОбъектУчета);
		НастройкиСохранения.ФорматыСохранения = ФорматыСохранения;
		КоллекцияПечатныхФорм = УправлениеПечатью.НапечататьВФайл(КомандыПечати[0],МассивОбъектов, НастройкиСохранения);
		
		Для Каждого ПечатнаяФорма Из КоллекцияПечатныхФорм Цикл
			ОписаниеПредставленияДокумента.ИмяФайла = ПечатнаяФорма.ИмяФайла;
			ОписаниеПредставленияДокумента.ДвоичныеДанные = ПечатнаяФорма.ДвоичныеДанные;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет настройки формирования электронного документа.
//
// Параметры:
//  СсылкаНаДокумент      - СправочникСсылка, ДокументСсылка, - документ или справочник объекта учета.
//  НастройкиФормирования - Структура - настройки источника для формирования обмена электронными документами.
//                                      Выходной параметр:
//    * ТипДокумента       - Произвольный - значение элемента структуры возвращаемой
//                           см. ОбменСКонтрагентами.ТипыДокументов.
//    * Формат             - Строка - формат значения элемента структуры возвращаемой см. ОбменСКонтрагентами.ФорматыДокументов().
//    * Направление        - Произвольный - значение элемента структуры возвращаемой.
//                           см. ОбменСКонтрагентами.НаправленияДокументов.
//    * Организация        - ОпределяемыйТип.Организация - организация документа.
//    * Контрагент         - ОпределяемыйТип.КонтрагентБЭД - контрагент документа.
//    * ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО - договор контрагента.
//
Процедура ЗаполнитьНастройкиФормированияДокумента(СсылкаНаДокумент, НастройкиФормирования) Экспорт
	
	ПоддерживаемыеФорматыЭДО = ФорматыЭДО.ПоддерживаемыеФорматы();
	ТипыДокументов           = ОбменСКонтрагентами.ТипыДокументов();
	НаправленияДокументов    = ОбменСКонтрагентами.НаправленияДокументов();
	
	ТипИсточника = ТипЗнч(СсылкаНаДокумент);
	
	Реквизиты = "Организация, Контрагент, ДоговорКонтрагента";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокумент, Реквизиты);
	НастройкиФормирования.Направление        = НаправленияДокументов.Исходящий;
	НастройкиФормирования.Организация        = ЗначенияРеквизитов.Организация;
	НастройкиФормирования.Контрагент         = ЗначенияРеквизитов.Контрагент;
	НастройкиФормирования.ДоговорКонтрагента = ЗначенияРеквизитов.ДоговорКонтрагента;
	
	Если ТипИсточника = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		НастройкиФормирования.ТипДокумента   = ТипыДокументов.СчетНаОплату;
		НастройкиФормирования.Формат         = ПоддерживаемыеФорматыЭДО.CML208;
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.АктСверкиВзаиморасчетов") Тогда
		НастройкиФормирования.ТипДокумента = ТипыДокументов.АктСверкиВзаиморасчетов;
		НастройкиФормирования.Формат = ПоддерживаемыеФорматыЭДО.ФНС.АктСверкиВзаиморасчетов.ИнформацияОтправителя;
	КонецЕсли;
	
КонецПроцедуры

// Переопределяет настройки загрузки документа из сервиса 1С:Share.
//
// По умолчанию заполняются настройки документа в зависимости от состояния документа и указанных типов объектов из 
// метода ИнтеграцияЭДО.ТипыОбъектовУчетаПоТипуЭлектронногоДокумента. При отличии способов отражения учета от этого
// метода можно переопределить способы отражения и их отображения для пользователя.
// Так же в методе можно выполнять проверки на наличие доступа к учетным документам или данным у пользователя,
// и любые другие проверки перед загрузкой документа. В случае наличия ошибок необходимо установить Отказ = Истина,
// заполнить ТекстОшибки.
//
// 1)Пример для открытия документа, когда документ уже ранее был загружен:
//
//	ПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
//	ПараметрОтраженияВУчете.СсылкаНаОбъектУчетаВБазе      = СсылкаНаОбъектУчета;
//	ПараметрОтраженияВУчете.ПредставлениеДокумента        = Строка(СсылкаНаОбъектУчета);
//	ПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Открыть %1'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ПараметрыОтраженияВУчете.ПредставлениеДокумента);
//	ПараметрОтраженияВУчете.КартинкаРезультатаПоиска      = БиблиотекаКартинок.ПерейтиПоНавигационнойСсылке;
// 
// 2)Пример для загрузки документа, когда документ можно загрузить только как один тип объекта:
//
//	ПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
//	ПараметрОтраженияВУчете.СсылкаНаОбъектУчетаВБазе = Документы.ЗаказПоставщику.ПустаяСсылка();
//	ПараметрОтраженияВУчете.СпособОтраженияВУчете    = "ЗаказПоставщику";
//	ПараметрОтраженияВУчете.ПредставлениеДокумента   = СтрШаблон(НСтр("ru = '%1 от %2'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеЭлектронногоДокумента.ПредставлениеДокументаОтправителя,
//		ОписаниеЭлектронногоДокумента.НаименованиеОтправителя);
//	ПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Загрузить %1'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ПредставлениеДокумента);
//	ПараметрОтраженияВУчете.КартинкаРезультатаПоиска      = БиблиотекаКартинок.ЗагрузитьShare;
//
// 3)Пример для загрузки документа, когда документ можно загрузить разными типами объекта:
//
//	ПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
//	ПараметрОтраженияВУчете.СсылкаНаОбъектУчетаВБазе = Документы.ЗаказПоставщику.ПустаяСсылка();
//	ПараметрОтраженияВУчете.СпособОтраженияВУчете    = "ЗаказПоставщику";
//	ПараметрОтраженияВУчете.ПредставлениеДокумента   = НСтр("ru = 'Заказ поставщику'",
//		ОбщегоНазначения.КодОсновногоЯзыка());
//	ПараметрОтраженияВУчете.КартинкаРезультатаПоиска = БиблиотекаКартинок.ЗагрузитьShare;
//	ПараметрОтраженияВУчете.ПредставлениеВПодменюЗагрузить = СтрШаблон(НСтр("ru = 'Как %1'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ПредставлениеДокумента);
//	ПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Загрузить как %1'",
//		ОбщегоНазначения.КодОсновногоЯзыка()), ПредставлениеДокумента).
//
// Параметры:
//  НастройкиЗагрузки - Структура Из см. ИнтеграцияShare.НовыеНастройкиЗагрузкиДокумента
//  ОписаниеЭлектронногоДокумента - см. ИнтеграцияShare.НовоеКраткоеОписаниеЭлектронногоДокумента.
//  Отказ - Булево - признак наличия ошибки или закрытия доступа к загрузке документа, например, при отсутствие прав.
//  ТекстОшибки - Строка - текст сообщения для отображения пользователю.
Процедура ПриОпределенииНастроекЗагрузкиДокумента(НастройкиЗагрузки, Знач ОписаниеЭлектронногоДокумента,
	Отказ = Ложь, ТекстОшибки = "") Экспорт

КонецПроцедуры

#КонецОбласти

