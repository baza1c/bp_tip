#Область СлужебныйПрограммныйИнтерфейс

// Возвращает все балансовые счета, где присутствует субконто Банковские счета
//
// Параметры:
//
// Возвращаемое значение:
//	СписокСчетов - Массив - массив счетов
//
Функция ПолучитьСчетаССубконтоБанковскиеСчета(СчетаИсключения = Неопределено) Экспорт
	
	СписокСчетов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета)
	|	И НЕ ХозрасчетныйВидыСубконто.Ссылка.Забалансовый
	|	И НЕ ХозрасчетныйВидыСубконто.Ссылка В (&СчетаИсключения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	
	Если ЗначениеЗаполнено(СчетаИсключения) Тогда
		Запрос.УстановитьПараметр("СчетаИсключения", БухгалтерскийУчетПовтИсп.СчетаВИерархии(СчетаИсключения));
	Иначе
		Запрос.УстановитьПараметр("СчетаИсключения", Новый Массив);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		СписокСчетов = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(СписокСчетов);
	
КонецФункции

Функция ПолучитьСтатьюДДСПоВидуОперации(ВидОперации) Экспорт
	
	Возврат Справочники.СтатьиДвиженияДенежныхСредств.ПолучитьСтатьюДДСПоВидуОперации(ВидОперации);
	
КонецФункции

Функция ПредопределеннаяСтатьяДДСПоКонтексту(ВидОперации) Экспорт
	
	Возврат Справочники.СтатьиДвиженияДенежныхСредств.ПредопределеннаяСтатьяДДСПоКонтексту(ВидОперации);
	
КонецФункции

Функция СписокБанковСВалютнойВыпиской() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Макет = Обработки.КлиентБанк.ПолучитьМакет("СписокБанковСВалютнойВыпиской");
	Макет.КодЯзыка = Метаданные.Языки.Русский.КодЯзыка;
	
	Возврат Макет;
	
КонецФункции

Функция ТаблицаПоставляемыхДанныхПоВидуНалога(Период, ВидНалога) Экспорт
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("ВидНалога", ВидНалога);
	ТаблицаПоставляемыхДанных =
		Справочники.ВидыНалоговИПлатежейВБюджет.ПрочитатьПоставляемыеДанныеКлассификатора(Период, СтруктураОтбора);
	
	Возврат ТаблицаПоставляемыхДанных;
	
КонецФункции

Функция ТаблицаПоставляемыхДанныхПоКБК(Период, КБК) Экспорт
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("КодБК", КБК);
	ТаблицаПоставляемыхДанных =
		Справочники.ВидыНалоговИПлатежейВБюджет.ПрочитатьПоставляемыеДанныеКлассификатора(Период, СтруктураОтбора);
	
	Возврат ТаблицаПоставляемыхДанных;
	
КонецФункции

Функция ВыделятьНДССКомиссииБанка(Организация, Дата) Экспорт
	
	ПрименяетсяНДССКомиссииБанка = УчетНДСКлиентСервер.ПрименяетсяНДССКомиссииБанка(Дата);
	ПлательщикНДССВычетами = УчетнаяПолитика.ПлательщикНДССВычетами(Организация, Дата);
	
	Результат = ПрименяетсяНДССКомиссииБанка И ПлательщикНДССВычетами;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
