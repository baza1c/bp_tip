#Область ПрограммныйИнтерфейс

Функция СтавкаНДСПоРаспознанномуТексту(Ставка, ТекущееЗначение) Экспорт
	
	СтавкаНДС = СтавкаНДСПоИмени(Ставка);
	
	Если СтавкаНДС <> Перечисления.СтавкиНДС.ПустаяСсылка() Тогда
		Возврат СтавкаНДС;
	Иначе
		Возврат ТекущееЗначение;
	КонецЕсли;
	
КонецФункции

Функция ОпределитьСтавкуНДС(Всего, СуммаНДС) Экспорт
	
	Делитель = Всего - СуммаНДС;
	Если Делитель = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	Ставка = Окр(СуммаНДС * 100 / Делитель);
	СтруктураСтавкиНДС = Новый Структура("Числом", Ставка);
	
	Если Ставка = 0 Тогда
		СтавкаСсылкой = Перечисления.СтавкиНДС.БезНДС;
	Иначе
		СтавкаСсылкой = УчетНДС.СтавкаНДСПоЧислу(Ставка);
		
		Если СтавкаСсылкой = Перечисления.СтавкиНДС.БезНДС Тогда
			СтавкаСсылкой = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если СтавкаСсылкой = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		СтруктураСтавкиНДС.Вставить("Ссылкой", СтавкаСсылкой);
		Возврат СтруктураСтавкиНДС;
	КонецЕсли;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

#Область ПраваДоступа

// Настраивает в профиле групп доступа роли, предоставляющие право интеграции.
// Описывает соответствующие роли подсистемы.
//
// Параметры:
//  ОписаниеПрофиля - см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа() - описание профиля

Процедура РазрешитьИнтеграцию(ОписаниеПрофиля) Экспорт
	
	ОписаниеПрофиля.Роли.Добавить("РаспознаваниеДокументов");
	
КонецПроцедуры

Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Документы.РаспознанныйДокумент, Истина);
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьДобавленныеКолонкиТаблиц(Объект, Строки, Очистить = Ложь) Экспорт
	
	ДанныеОбъекта = Новый Структура;
	ДанныеОбъекта.Вставить("Организация", РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Объект, РаспознаваниеДокументовСлужебный.ОсновныеРеквизиты(Объект)["Организация"], Справочники.Организации.ПустаяСсылка()));
	ДанныеОбъекта.Вставить("Контрагент", РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Объект, РаспознаваниеДокументовСлужебный.ОсновныеРеквизиты(Объект)["Контрагент"], Справочники.Контрагенты.ПустаяСсылка()));
	
	ДанныеОбъекта.Вставить("Дата", РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Объект, "ДатаДокумента", '20100101'));
	ДанныеОбъекта.Вставить("Склад", РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Объект, "Склад", Справочники.Склады.ПустаяСсылка()));
	ДанныеОбъекта.Вставить("ДоговорКонтрагента", РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Объект, "Договор", Справочники.ДоговорыКонтрагентов.ПустаяСсылка()));
	ДанныеОбъекта.Вставить("ВидОперации", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары);
	
	ДанныеОбъекта.Вставить("ПодразделениеОрганизации", Неопределено);
	
	СтрокиТовары = Новый Массив;
	СтрокиУслуги = Новый Массив;
	
	Если ТипЗнч(Строки) = Тип("ДанныеФормыКоллекция") Тогда
		СписокНоменклатур = Строки.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура");
	ИначеЕсли ТипЗнч(Строки) = Тип("ТаблицаЗначений") Тогда
		СписокНоменклатур = Строки.ВыгрузитьКолонку("Номенклатура");
	ИначеЕсли ТипЗнч(Строки) = Тип("Массив") Тогда
		СписокНоменклатур = Новый Массив;
		Для Каждого СтрокаМассива из Строки Цикл
			Если ТипЗнч(СтрокаМассива) = Тип("ДанныеФормыЭлементКоллекции") И СтрокаМассива.Свойство("Номенклатура") тогда
				СписокНоменклатур.Добавить(СтрокаМассива.Номенклатура);
			КонецЕсли;
		КонецЦикла;
	Иначе
		СписокНоменклатур = Новый Массив;
	КонецЕсли;
	
	СписокУслугиПоНоменклатурам = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокНоменклатур, "Услуга");
	
	Для каждого СтрокаТаблицы Из Строки Цикл
		Если Очистить Тогда
			ОчиститьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТаблицы);
		КонецЕсли;
		
		ЭтоУслуга = СписокУслугиПоНоменклатурам.Получить(СтрокаТаблицы.Номенклатура);
		Если ЭтоУслуга = Неопределено Тогда
			ЭтоУслуга = Ложь;
		КонецЕсли;
		
		Если ЭтоУслуга Тогда
			СтрокиУслуги.Добавить(СтрокаТаблицы);
		Иначе
			СтрокиТовары.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Если Объект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
		СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиТовары, "Товары", ДанныеОбъекта, Документы.РеализацияТоваровУслуг);
		СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиУслуги, "Услуги", ДанныеОбъекта, Документы.РеализацияТоваровУслуг);
	Иначе
		СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиТовары, "Товары", ДанныеОбъекта, Документы.ПоступлениеТоваровУслуг);
		СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиУслуги, "Услуги", ДанныеОбъекта, Документы.ПоступлениеТоваровУслуг);
	КонецЕсли;
	
	ЗаполнениеОтражениеВУСН(ДанныеОбъекта, Строки);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОчиститьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТабличнойЧасти)
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("СчетУчета", Неопределено);
	ДанныеЗаполнения.Вставить("СчетДоходов", Неопределено);
	ДанныеЗаполнения.Вставить("Субконто", Неопределено);
	ДанныеЗаполнения.Вставить("СчетРасходов", Неопределено);
	ДанныеЗаполнения.Вставить("СчетУчетаНДСПоРеализации", Неопределено);
	ДанныеЗаполнения.Вставить("ПереданныеСчетУчета", Неопределено);
	ДанныеЗаполнения.Вставить("СчетУчетаНДС", Неопределено);
	ДанныеЗаполнения.Вставить("СчетРасчетов", Неопределено);
	ДанныеЗаполнения.Вставить("СпособУчетаНДС", Неопределено);
	ДанныеЗаполнения.Вставить("ОтражениеВУСН", Неопределено);
	ДанныеЗаполнения.Вставить("СчетЗатрат", Неопределено);
	ДанныеЗаполнения.Вставить("ПодразделениеЗатрат", Неопределено);
	ДанныеЗаполнения.Вставить("Субконто1", Неопределено);
	ДанныеЗаполнения.Вставить("Субконто2", Неопределено);
	ДанныеЗаполнения.Вставить("Субконто3", Неопределено);
	ДанныеЗаполнения.Вставить("СчетЗатратНУ", Неопределено);
	ДанныеЗаполнения.Вставить("СубконтоНУ1", Неопределено);
	ДанныеЗаполнения.Вставить("СубконтоНУ2", Неопределено);
	ДанныеЗаполнения.Вставить("СубконтоНУ3", Неопределено);
	ДанныеЗаполнения.Вставить("Контрагент", Неопределено);
	ДанныеЗаполнения.Вставить("ДоговорКонтрагента", Неопределено);
	
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения)
	
КонецПроцедуры

Процедура ЗаполнениеОтражениеВУСН(Объект, ТабличнаяЧасть)
	
	Если УчетнаяПолитика.Существует(Объект.Организация, Объект.Дата) Тогда
		
		ДоговорУказан = ЗначениеЗаполнено(Объект.ДоговорКонтрагента);
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДоговорКонтрагента, "ВидДоговора");
		ЭтоКомиссия = ДоговорУказан И РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
		
		ВариантОтраженияУСН = ПоступлениеТоваровУслугФормыКлиентСервер.ОтражениеВУСН(Объект.ВидОперации, ЭтоКомиссия);
		
		// Заполняем отражение в УСН для ТЧ
		Для Каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
			СтрокаТаблицы.ОтражениеВУСН = ВариантОтраженияУСН;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ВыборкаДляСозданияКорректировок(ОбработкаОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	Товары.ДокументОснованиеКорректировки КАК ДокументОснованиеКорректировки,
		|	Товары.Количество КАК Количество,
		|	Товары.Цена КАК Цена,
		|	Товары.Сумма КАК Сумма,
		|	Товары.СуммаНДС КАК СуммаНДС,
		|	Товары.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
		|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Товары.ЭтоУслуга КАК ЭтоУслуга
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	&Товары КАК Товары
		|ГДЕ
		|	НЕ Товары.ДокументОснованиеКорректировки В (&ПустыеСсылки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.ДокументОснованиеКорректировки КАК ДокументОснованиеКорректировки,
		|	ВТ_Товары.Количество КАК Количество,
		|	ВТ_Товары.Цена КАК Цена,
		|	ВТ_Товары.Сумма КАК Сумма,
		|	ВТ_Товары.СуммаНДС КАК СуммаНДС,
		|	ВТ_Товары.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
		|	ПРЕДСТАВЛЕНИЕ(ВТ_Товары.ДокументОснованиеКорректировки) КАК Представление,
		|	ВТ_Товары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ВТ_Товары.ЭтоУслуга КАК ЭтоУслуга
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|ИТОГИ ПО
		|	ДокументОснованиеКорректировки";
	
	ПустыеСсылки = Новый Массив;
	Для Каждого Тип Из Метаданные.ОпределяемыеТипы.ДокументОснованиеКорректировкиБРД.Тип.Типы() Цикл
		ПустаяСсылка = Новый(Тип);
		ПустыеСсылки.Добавить(ПустаяСсылка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Товары", ОбработкаОбъект.Товары);
	Запрос.УстановитьПараметр("ПустыеСсылки", ПустыеСсылки);
	
	Возврат Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

Функция ВыборкаДляСозданияВозвратаПоставщику(ОбработкаОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	Товары.ДокументОснованиеКорректировки КАК ДокументОснованиеКорректировки,
		|	Товары.Количество КАК Количество,
		|	Товары.Цена КАК Цена,
		|	Товары.Сумма КАК Сумма,
		|	Товары.СуммаНДС КАК СуммаНДС,
		|	Товары.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
		|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Товары.ЭтоУслуга КАК ЭтоУслуга,
		|	Товары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	&Товары КАК Товары
		|ГДЕ
		|	НЕ Товары.ДокументОснованиеКорректировки В (&ПустыеСсылки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.ДокументОснованиеКорректировки КАК ДокументОснованиеКорректировки,
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровУслугТовары.Количество - ВТ_Товары.Количество КАК Количество,
		|	ПоступлениеТоваровУслугТовары.Цена КАК Цена,
		|	ПоступлениеТоваровУслугТовары.Сумма - ВТ_Товары.Сумма КАК Сумма,
		|	ПоступлениеТоваровУслугТовары.СуммаНДС - ВТ_Товары.СуммаНДС КАК СуммаНДС,
		|	ВТ_Товары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|		ПО ВТ_Товары.ДокументОснованиеКорректировки = ПоступлениеТоваровУслугТовары.Ссылка
		|			И ВТ_Товары.НомерИсходнойСтроки = ПоступлениеТоваровУслугТовары.НомерСтроки
		|			И ВТ_Товары.Номенклатура = ПоступлениеТоваровУслугТовары.Номенклатура";
	
	ПустыеСсылки = Новый Массив;
	Для Каждого Тип Из Метаданные.ОпределяемыеТипы.ДокументОснованиеКорректировкиБРД.Тип.Типы() Цикл
		ПустаяСсылка = Новый(Тип);
		ПустыеСсылки.Добавить(ПустаяСсылка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Товары", ОбработкаОбъект.Товары);
	Запрос.УстановитьПараметр("ПустыеСсылки", ПустыеСсылки);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ЗаполнитьСтрокиТаблицыТоварыКорректировкиИзОснований(Объект, СУчетомНоменклатуры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСопоставленияСтрокКорректировки(СУчетомНоменклатуры);
	
	Основания = Объект.ДокументыОснованияКорректировки.Выгрузить().ВыгрузитьКолонку("ДокументОснование");
	
	Запрос.УстановитьПараметр("ТаблицаТовары", Объект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("Основания", Основания);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ЕстьСтрокаОснования И Выборка.ЕстьСтрокаКорректировки Тогда
			
			ИзменяемаяСтрока = Объект.Товары[Выборка.НомерСтроки - 1];
		
			ЗаполнитьЗначенияСвойств(ИзменяемаяСтрока, Выборка);
			ИзменяемаяСтрока.ВсегоПоДокументу = ИзменяемаяСтрока.СуммаПоДокументу + ?(Объект.СуммаВключаетНДС, 0, ИзменяемаяСтрока.СуммаНДСПоДокументу);
			ИзменяемаяСтрока.НомерИсходнойСтрокиЗаполненоВручную = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСопоставленныеВручнуюСтрокиТаблицыТоварыКорректировки(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ТаблицаТовары.ДокументОснованиеКорректировки КАК ДокументОснованиеКорректировки,
		|	ТаблицаТовары.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_СтрокиКорректировки
		|ИЗ
		|	&ТаблицаТовары КАК ТаблицаТовары
		|ГДЕ
		|	(ТаблицаТовары.НомерИсходнойСтрокиЗаполненоВручную
		|			ИЛИ ТаблицаТовары.ДокументОснованиеКорректировкиЗаполненоВручную)
		|	И ТаблицаТовары.КоличествоПоДокументу = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровУслугТовары.Количество КАК КоличествоПоДокументу,
		|	ПоступлениеТоваровУслугТовары.Цена КАК ЦенаПоДокументу,
		|	ПоступлениеТоваровУслугТовары.Сумма КАК СуммаПоДокументу,
		|	ПоступлениеТоваровУслугТовары.СтавкаНДС КАК СтавкаНДСПоДокументу,
		|	ПоступлениеТоваровУслугТовары.СуммаНДС КАК СуммаНДСПоДокументу,
		|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхожденияПоДокументу,
		|	ВТ_СтрокиКорректировки.НомерСтроки КАК НомерСтроки,
		|	ЛОЖЬ КАК ЭтоУслуга
		|ИЗ
		|	ВТ_СтрокиКорректировки КАК ВТ_СтрокиКорректировки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|		ПО ВТ_СтрокиКорректировки.ДокументОснованиеКорректировки = ПоступлениеТоваровУслугТовары.Ссылка
		|			И ВТ_СтрокиКорректировки.НомерИсходнойСтроки = ПоступлениеТоваровУслугТовары.НомерСтроки
		|			И ВТ_СтрокиКорректировки.Номенклатура = ПоступлениеТоваровУслугТовары.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугУслуги.Номенклатура,
		|	ПоступлениеТоваровУслугУслуги.Количество,
		|	ПоступлениеТоваровУслугУслуги.Цена,
		|	ПоступлениеТоваровУслугУслуги.Сумма,
		|	ПоступлениеТоваровУслугУслуги.СтавкаНДС,
		|	ПоступлениеТоваровУслугУслуги.СуммаНДС,
		|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка),
		|	ВТ_СтрокиКорректировки.НомерСтроки,
		|	ИСТИНА КАК ЭтоУслуга
		|ИЗ
		|	ВТ_СтрокиКорректировки КАК ВТ_СтрокиКорректировки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|		ПО ВТ_СтрокиКорректировки.ДокументОснованиеКорректировки = ПоступлениеТоваровУслугУслуги.Ссылка
		|			И ВТ_СтрокиКорректировки.НомерИсходнойСтроки = ПоступлениеТоваровУслугУслуги.НомерСтроки
		|			И ВТ_СтрокиКорректировки.Номенклатура = ПоступлениеТоваровУслугУслуги.Номенклатура";
	
	Запрос.УстановитьПараметр("ТаблицаТовары", Объект.Товары.Выгрузить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ИзменяемаяСтрока = Объект.Товары[Выборка.НомерСтроки - 1];
		
		ЗаполнитьЗначенияСвойств(ИзменяемаяСтрока, Выборка);
		ИзменяемаяСтрока.ВсегоПоДокументу = ИзменяемаяСтрока.СуммаПоДокументу + ?(Объект.СуммаВключаетНДС, 0, ИзменяемаяСтрока.СуммаНДСПоДокументу);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьКорректировочныйСчетФактуру(СозданныеДокументы, ОбработкаОбъект) Экспорт
	
	// 1. Создаем счет фактуру на основании первого созданного объекта корректировки
	
	ОбъектКСФ = Документы.СчетФактураПолученный.СоздатьДокумент();
	ОбъектКСФ.Заполнить(СозданныеДокументы[0]);
	
	ОбъектКСФ.НомерВходящегоДокумента = ОбработкаОбъект.НомерДокумента;
	ОбъектКСФ.ДатаВходящегоДокумента  = ОбработкаОбъект.ДатаДокумента;
	
	// 2. К созданной счет фактуре в список оснований добавляем каждое из оставшихся
	
	Для Индекс = 1 По СозданныеДокументы.ВГраница() Цикл
		
		СтрокаТаблицы = ОбъектКСФ.ДокументыОснования.Добавить();
		СтрокаТаблицы.ДокументОснование = СозданныеДокументы[Индекс];
		
	КонецЦикла;
	
	// 3. Вызываем метод счет фактуры ОпределениеПараметровСчетаФактуры
	
	ОбъектКСФ.ОпределениеПараметровСчетаФактуры();
	
	// 4. Повторяем метод из формы ЗагрузитьСписокИсходныхСчетовФактур и код ниже
	
	Для Каждого Основание Из ОбъектКСФ.ДокументыОснования Цикл
		
		Если Не ЗначениеЗаполнено(Основание.ИсходныйДокумент) Тогда
			
			РеквизитыИсходногоСчетаФактуры = Документы.СчетФактураПолученный.ПолучитьРеквизитыИсходногоСчетаФактурыДляКорректировки(
			Основание.ДокументОснование, ОбъектКСФ.Ссылка, Истина, Основание.ИсходныйДокумент);
			
			Если РеквизитыИсходногоСчетаФактуры <> Неопределено Тогда 
				Основание.ИсходныйДокумент = РеквизитыИсходногоСчетаФактуры.СчетФактура;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектКСФ.ДоговорКонтрагента, "ВидДоговора");
	Если ВидДоговораКонтрагента <> Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку Тогда
		ОбъектКСФ.Продавец = Неопределено;
	КонецЕсли;
	
	ТаблицаОснований = ОбъектКСФ.ДокументыОснования.Выгрузить(, "НомерИсходногоДокумента, ДатаИсходногоДокумента");
	ТаблицаОснований.Свернуть("НомерИсходногоДокумента, ДатаИсходногоДокумента");
	
	Если УчетНДСКлиентСервер.ВерсияКодовВидовОпераций(ОбъектКСФ.Дата) >= 3 Тогда
		// Получение единого корректировочного счета-фактуры
		ОбъектКСФ.КодВидаОперации             = "01";
		ОбъектКСФ.КодВидаОперацииНаУменьшение = "01";
	Иначе
		ОбъектКСФ.КодВидаОперации             = "01";
		ОбъектКСФ.КодВидаОперацииНаУменьшение = "18";
	КонецЕсли;
	
	ОбъектКСФ.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат ОбъектКСФ;
	
КонецФункции

Процедура УстановитьСвязиПрикрепитьИзображения(Источник) Экспорт
	
	Чеки = КассовыеЧекиДокумента(Источник);
	
	РаспознанныеДокументы = РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.РаспознанныеДокументыПоСвязанным(Чеки);
	Картинки = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(РаспознанныеДокументы, "ИсходноеИзображение", Истина);
	
	Для Каждого РаспознанныйДокумент Из РаспознанныеДокументы Цикл
		
		РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ЗаписатьЗначения(
			Источник.Ссылка,
			РаспознанныйДокумент,
			Ложь);
			
		Картинка = Картинки.Получить(РаспознанныйДокумент);
		Если Картинка = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		АдресКартинки = ПоместитьВоВременноеХранилище(Картинка.Получить());
		РаспознаваниеДокументовСлужебный.ДобавитьПрисоединенныйФайл(РаспознанныйДокумент, Источник.Ссылка, АдресКартинки);
		УдалитьИзВременногоХранилища(АдресКартинки);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтавкаНДСПоИмени(Ставка)
	
	Попытка
		Результат = Перечисления.СтавкиНДС[Ставка];
	Исключение
		Результат = Перечисления.СтавкиНДС.ПустаяСсылка();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаСопоставленияСтрокКорректировки(СУчетомНоменклатуры)
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	ТаблицаТовары.ДокументОснованиеКорректировки КАК ДокументОснованиеКорректировки,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.КоличествоДоКорректировки КАК Количество,
		|	ТаблицаТовары.ЦенаДоКорректировки КАК Цена,
		|	ТаблицаТовары.СуммаДоКорректировки КАК Сумма,
		|	ЛОЖЬ КАК ЕстьСтрокаОснования,
		|	ИСТИНА КАК ЕстьСтрокаКорректировки,
		|	ТаблицаТовары.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_СтрокиКорректировки
		|ИЗ
		|	&ТаблицаТовары КАК ТаблицаТовары
		|ГДЕ
		|	НЕ ТаблицаТовары.НомерИсходнойСтрокиЗаполненоВручную
		|	И НЕ ТаблицаТовары.ДокументОснованиеКорректировкиЗаполненоВручную
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.Количество КАК Количество,
		|	ВТ_Товары.Цена КАК Цена,
		|	ВТ_Товары.Сумма КАК Сумма,
		|	ВТ_Товары.ЕстьСтрокаОснования КАК ЕстьСтрокаОснования,
		|	ВТ_Товары.ЕстьСтрокаКорректировки КАК ЕстьСтрокаКорректировки,
		|	ВТ_Товары.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
		|	ВТ_Товары.НомерСтроки КАК НомерСтроки,
		|	ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
		|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка) КАК СтранаПроисхождения,
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка) КАК СтавкаНДС,
		|	0 КАК СуммаНДС,
		|	ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка) КАК ДокументОснование,
		|	ЛОЖЬ КАК ЭтоУслуга
		|ПОМЕСТИТЬ ВТ_ОснованияКорректировки
		|ИЗ
		|	ВТ_СтрокиКорректировки КАК ВТ_Товары
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.Номенклатура,
		|	ПоступлениеТоваровУслугТовары.Количество,
		|	ПоступлениеТоваровУслугТовары.Цена,
		|	ПоступлениеТоваровУслугТовары.Сумма,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ПоступлениеТоваровУслугТовары.НомерСтроки,
		|	0,
		|	ПоступлениеТоваровУслугТовары.Номенклатура.ЕдиницаИзмерения,
		|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения,
		|	ПоступлениеТоваровУслугТовары.СтавкаНДС,
		|	ПоступлениеТоваровУслугТовары.СуммаНДС,
		|	ПоступлениеТоваровУслугТовары.Ссылка,
		|	ЛОЖЬ
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|ГДЕ
		|	ПоступлениеТоваровУслугТовары.Ссылка В(&Основания)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугУслуги.Номенклатура,
		|	ПоступлениеТоваровУслугУслуги.Количество,
		|	ПоступлениеТоваровУслугУслуги.Цена,
		|	ПоступлениеТоваровУслугУслуги.Сумма,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ПоступлениеТоваровУслугУслуги.НомерСтроки,
		|	0,
		|	ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка),
		|	ПоступлениеТоваровУслугУслуги.СтавкаНДС,
		|	ПоступлениеТоваровУслугУслуги.СуммаНДС,
		|	ПоступлениеТоваровУслугУслуги.Ссылка,
		|	ИСТИНА
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|ГДЕ
		|	ПоступлениеТоваровУслугУслуги.Ссылка В(&Основания)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
	
	Если СУчетомНоменклатуры Тогда
		ТекстЗапроса = ТекстЗапроса
			+ "
			|	ВЫБРАТЬ
			|	ВТ_ОснованияКорректировки.Номенклатура КАК Номенклатура,
			|	ВТ_ОснованияКорректировки.Количество КАК КоличествоПоДокументу,
			|	ВТ_ОснованияКорректировки.Цена КАК ЦенаПоДокументу,
			|	ВТ_ОснованияКорректировки.Сумма КАК СуммаПоДокументу,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ЕстьСтрокаОснования) КАК ЕстьСтрокаОснования,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ЕстьСтрокаКорректировки) КАК ЕстьСтрокаКорректировки,
			|	ВТ_ОснованияКорректировки.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.НомерСтроки) КАК НомерСтроки,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ЕдиницаИзмерения) КАК НоменклатураЕдиницаИзмерения,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.СтранаПроисхождения) КАК СтранаПроисхожденияПоДокументу,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.СтавкаНДС) КАК СтавкаНДСПоДокументу,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.СуммаНДС) КАК СуммаНДСПоДокументу,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ДокументОснование) КАК ДокументОснованиеКорректировки,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ЭтоУслуга) КАК ЭтоУслуга
			|ИЗ
			|	ВТ_ОснованияКорректировки КАК ВТ_ОснованияКорректировки
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТ_ОснованияКорректировки.Номенклатура,
			|	ВТ_ОснованияКорректировки.Количество,
			|	ВТ_ОснованияКорректировки.Цена,
			|	ВТ_ОснованияКорректировки.Сумма,
			|	ВТ_ОснованияКорректировки.НомерИсходнойСтроки
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(ВТ_ОснованияКорректировки.Номенклатура) = 2";
	Иначе
		ТекстЗапроса = ТекстЗапроса
			+ "ВЫБРАТЬ
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.Номенклатура) КАК Номенклатура,
			|	ВТ_ОснованияКорректировки.Количество КАК КоличествоПоДокументу,
			|	ВТ_ОснованияКорректировки.Цена КАК ЦенаПоДокументу,
			|	ВТ_ОснованияКорректировки.Сумма КАК СуммаПоДокументу,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ЕстьСтрокаОснования) КАК ЕстьСтрокаОснования,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ЕстьСтрокаКорректировки) КАК ЕстьСтрокаКорректировки,
			|	ВТ_ОснованияКорректировки.НомерИсходнойСтроки КАК НомерИсходнойСтроки,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.НомерСтроки) КАК НомерСтроки,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ЕдиницаИзмерения) КАК НоменклатураЕдиницаИзмерения,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.СтранаПроисхождения) КАК СтранаПроисхожденияПоДокументу,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.СтавкаНДС) КАК СтавкаНДСПоДокументу,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.СуммаНДС) КАК СуммаНДСПоДокументу,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ДокументОснование) КАК ДокументОснованиеКорректировки,
			|	МАКСИМУМ(ВТ_ОснованияКорректировки.ЭтоУслуга) КАК ЭтоУслуга
			|ИЗ
			|	ВТ_ОснованияКорректировки КАК ВТ_ОснованияКорректировки
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТ_ОснованияКорректировки.Количество,
			|	ВТ_ОснованияКорректировки.Цена,
			|	ВТ_ОснованияКорректировки.Сумма,
			|	ВТ_ОснованияКорректировки.НомерИсходнойСтроки
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(ВТ_ОснованияКорректировки.НомерИсходнойСтроки) = 2";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция КассовыеЧекиДокумента(ДокументОбъект)
	
	ИменаТаблиц = Новый Массив;
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.АвансовыйОтчет") Тогда
		ИменаТаблиц.Добавить("Товары");
		ИменаТаблиц.Добавить("Прочее");
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.РасходыПредпринимателя") Тогда
		ИменаТаблиц.Добавить("Расходы");
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПутевойЛист") Тогда
		ИменаТаблиц.Добавить("Топливо");
	КонецЕсли;
	
	Чеки = Новый Массив;
	Для Каждого ИмяТаблицы Из ИменаТаблиц Цикл
		Для Каждого Строка Из ДокументОбъект[ИмяТаблицы] Цикл
			Если ЗначениеЗаполнено(Строка.КассовыйЧек)
				И Чеки.Найти(Строка.КассовыйЧек) = Неопределено Тогда
				Чеки.Добавить(Строка.КассовыйЧек);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Чеки;
	
КонецФункции

#КонецОбласти
