
#Область СлужебныйПрограммныйИнтерфейс

Функция ТекстПоляОтветственный(ПсевдонимТаблицы, ПсевдонимДокумента, ТипыДокументов,
	ДатаНачала = '00010101', ДатаОкончания = '00010101', Организация = Неопределено) Экспорт
	
 	РазрешенныеДокументы = Новый Массив;
	Для Каждого ТипДокумента Из ТипыДокументов Цикл
		Документ = Метаданные.НайтиПоТипу(ТипДокумента);
		Если Документ <> Неопределено
			И ПравоДоступа("Чтение", Документ) 
			И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(Документ) Тогда
			РазрешенныеДокументы.Добавить(Документ);
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(РазрешенныеДокументы) Тогда
		Возврат ТекстПоляОтветственныйПоУмолчанию();
	КонецЕсли;
	
	ТекстПоляОптоваяПродажаОтветственный = "";
	
	Запрос = Новый Запрос;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	
	Индекс = 0;
	
	Для Каждого Документ Из РазрешенныеДокументы Цикл
		Если Индекс = 0 Тогда
			ПакетЗапросов = СхемаЗапроса.ПакетЗапросов[0];
		Иначе
			ПакетЗапросов = СхемаЗапроса.ПакетЗапросов.Добавить(Тип("ЗапросВыбораСхемыЗапроса"));
		КонецЕсли;
		
		ПакетЗапросов.ВыбиратьРазрешенные = Истина;
		
		ОператорЗапроса = ПакетЗапросов.Операторы[0];
		ОператорЗапроса.КоличествоПолучаемыхЗаписей = 1;
		ОператорЗапроса.Источники.Добавить(Документ.ПолноеИмя(), "ТаблицаДокумента");
		ОператорЗапроса.ВыбираемыеПоля.Добавить("Ссылка");
		ОператорЗапроса.Отбор.Добавить("Проведен");
		ОператорЗапроса.Отбор.Добавить("Организация В (&Организации)");
		Если ЗначениеЗаполнено(ДатаНачала) Тогда
			ОператорЗапроса.Отбор.Добавить("Дата >= &ДатаНачала");
		КонецЕсли;
		Если ЗначениеЗаполнено(ДатаОкончания) Тогда
			ОператорЗапроса.Отбор.Добавить("Дата <= &ДатаОкончания");
		КонецЕсли;
		Если Документ = Метаданные.Документы.ПриходныйКассовыйОрдер Тогда
			ОператорЗапроса.Отбор.Добавить("ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.РозничнаяВыручка)");
		ИначеЕсли Документ = Метаданные.Документы.ОплатаПлатежнойКартой Тогда
			ОператорЗапроса.Отбор.Добавить("ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОплатаПлатежнойКартой.РозничнаяВыручка)");
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Если ЗначениеЗаполнено(ДатаНачала) Тогда
		Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	КонецЕсли;
	Если ЗначениеЗаполнено(ДатаОкончания) Тогда
		Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания));
	КонецЕсли;
	Если ЗначениеЗаполнено(Организация) Тогда
		Если ТипЗнч(Организация) = Тип("Массив") Тогда
			Организации = Организация;
		Иначе
			Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
		КонецЕсли;
	Иначе
		Организации = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	КонецЕсли;
	Запрос.УстановитьПараметр("Организации", Организации);
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	ИндексРезультата = 0;
	Для Каждого Результат Из РезультатПакета Цикл
		Если Не Результат.Пустой() Тогда
			Документ = РазрешенныеДокументы[ИндексРезультата];
			ТекстПоляОптоваяПродажаОтветственный = ТекстПоляОптоваяПродажаОтветственный + Символы.ПС 
				+ СтрШаблон("КОГДА %1.%2 ССЫЛКА %3
							|	ТОГДА ВЫРАЗИТЬ(%1.%2 КАК %3).Ответственный",
				ПсевдонимТаблицы,
				ПсевдонимДокумента,
				Документ.ПолноеИмя());
		КонецЕсли;
		
		ИндексРезультата = ИндексРезультата + 1;
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстПоляОптоваяПродажаОтветственный) Тогда
		Возврат ТекстПоляОтветственныйПоУмолчанию();
	Иначе
		Возврат "ВЫБОР"
			+ ТекстПоляОптоваяПродажаОтветственный + Символы.ПС
			+ "КОНЕЦ";
	КонецЕсли;
	
КонецФункции

Функция ТекстПоляОтветственныйПоУмолчанию() Экспорт
	
	Возврат "ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)";
	
КонецФункции

Функция ТипыДокументовПродажи() Экспорт
	
	СписокДокументов = ТипыДокументовОптовойПродажи();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокДокументов, ТипыДокументовРозничнойПродажи());

	Возврат СписокДокументов;
	
КонецФункции

Функция ТипыДокументовОптовойПродажи() Экспорт
	
	СписокДокументов = Новый Массив;
	
	СписокДокументов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.РеализацияОтгруженныхТоваров"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтПокупателя"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.РеализацияУслугПоПереработке"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.ОказаниеУслуг"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.НачислениеПеней"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.ПередачаНМА"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.ПередачаОС"));

	Возврат СписокДокументов;
	
КонецФункции

Функция ТипыДокументовРозничнойПродажи() Экспорт

	СписокДокументов = Новый Массив;
	
	СписокДокументов.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.ОплатаПлатежнойКартой"));
	
	Возврат СписокДокументов;
	
КонецФункции

Функция ВыручкаИСебестоимость(ПараметрыОтчета, ПрошлыйПериод = Ложь) Экспорт
	
	НоменклатурнаяГруппаНоменклатура = Новый Массив;
	НоменклатурнаяГруппаНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
	НоменклатурнаяГруппаНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	НоменклатурнаяГруппаСтавкаНДСНоменклатура = Новый Массив;
	НоменклатурнаяГруппаСтавкаНДСНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
	НоменклатурнаяГруппаСтавкаНДСНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС);
	НоменклатурнаяГруппаСтавкаНДСНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Счета расчетов с розничными покупателями.
	СчетаКассы = Новый Массив;
	СчетаКассы.Добавить(ПланыСчетов.Хозрасчетный.Касса);                          // 50
	СчетаКассы.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);                 // 51
	СчетаКассы.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРозничнымиПокупателями); // 62.Р
	СчетаКассы.Добавить(ПланыСчетов.Хозрасчетный.ПродажиПоПлатежнымКартам);       //57.03
	СчетаКассы = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаКассы);
	
	СчетаУчетаПроизводства = БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.ОсновноеПроизводство_);
	
	// Счета расчетов с покупателями, в том числе и розничными.
	СчетаРасчетовСПокупателями = БухгалтерскиеОтчеты.СчетаРасчетовСПокупателямиССубсчетами();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасчетовСПокупателями, СчетаКассы);
	
	СчетРасчетовПоНалогамИСборам = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам);
	
	СчетАкцизов = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Продажи_Акцизы);
	
	СчетаВзаиморасчетовСПоставщиками = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
	
	СчетаРасходов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасходов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаРасходов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы));
		
	Подразделение        = Неопределено;
	Контрагент           = Неопределено;
	НоменклатурнаяГруппа = Неопределено;
	Номенклатура         = Неопределено;
	Организация          = Неопределено;
	ОрганизацияФормулаСравнения = "";
	ПодразделениеФормулаСравнения = "";
	КонтрагентФормулаСравнения = "";
	НоменклатураФормулаСравнения = "";
	НоменклатурнаяГруппаФормулаСравнения = "";

	Если ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда
		
		Организация = ПараметрыОтчета.Организация;
		ОрганизацияФормулаСравнения = ФормулаСравнения(ВидСравненияКомпоновкиДанных.Равно);
		
	КонецЕсли;
	
	Для Каждого ЭлементОтбора Из ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор.Элементы Цикл
		
		Если Не ЭлементОтбора.Использование
			Или ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
			
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.Организации.ПустаяСсылка();
			КонецЕсли;
			
			Организация = ЭлементОтбора.ПравоеЗначение;
			ОрганизацияФормулаСравнения = ФормулаСравнения(ЭлементОтбора.ВидСравнения);
				
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") Тогда
			
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			
			Подразделение = ЭлементОтбора.ПравоеЗначение;
			ПодразделениеФормулаСравнения = ФормулаСравнения(ЭлементОтбора.ВидСравнения);

		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Контрагент") Тогда
			
			Контрагент = ЭлементОтбора.ПравоеЗначение;
			КонтрагентФормулаСравнения = ФормулаСравнения(ЭлементОтбора.ВидСравнения);
			
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Номенклатура") Тогда
			
			Номенклатура = ЭлементОтбора.ПравоеЗначение;
			НоменклатураФормулаСравнения = ФормулаСравнения(ЭлементОтбора.ВидСравнения);

			
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НоменклатурнаяГруппа") Тогда
			
			НоменклатурнаяГруппа = ЭлементОтбора.ПравоеЗначение;
			НоменклатурнаяГруппаФормулаСравнения = ФормулаСравнения(ЭлементОтбора.ВидСравнения);
			
		КонецЕсли;
		
	КонецЦикла;

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация", Организация);
	ПараметрыОтбора.Вставить("ОрганизацияФормулаСравнения", ОрганизацияФормулаСравнения); 
	ПараметрыОтбора.Вставить("ВключатьОбособленныеПодразделения", ПараметрыОтчета.ВключатьОбособленныеПодразделения);
	ПараметрыОтбора.Вставить("Подразделение", Подразделение);
	ПараметрыОтбора.Вставить("ПодразделениеФормулаСравнения", ПодразделениеФормулаСравнения);
	ПараметрыОтбора.Вставить("Контрагент", Контрагент);
	ПараметрыОтбора.Вставить("КонтрагентФормулаСравнения", КонтрагентФормулаСравнения);
	ПараметрыОтбора.Вставить("Номенклатура", Номенклатура);
	ПараметрыОтбора.Вставить("НоменклатураФормулаСравнения", НоменклатураФормулаСравнения);
	ПараметрыОтбора.Вставить("НоменклатурнаяГруппа", Подразделение);
	ПараметрыОтбора.Вставить("НоменклатурнаяГруппаФормулаСравнения", НоменклатурнаяГруппаФормулаСравнения);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыУчета = ОбщегоНазначенияБП.ОпределитьПараметрыУчета();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ИспользоватьОборотнуюНоменклатуру = ПараметрыУчета.ИспользоватьОборотнуюНоменклатуру;
	СубконтоНТТ = Новый Массив;
	СубконтоНТТ.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);

	Если ИспользоватьОборотнуюНоменклатуру Тогда
		СубконтоНТТ.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	КонецЕсли;
	
	СубконтоНоменклатураСклад = Новый Массив;
	СубконтоНоменклатураСклад.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	СубконтоНоменклатураСклад.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);

	СубконтоНоменклатураСкладПартии = Новый Массив;
	СубконтоНоменклатураСкладПартии.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	СубконтоНоменклатураСкладПартии.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	СубконтоНоменклатураСкладПартии.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии);
	
	СубконтоНоменклатураПартии = Новый Массив;
	СубконтоНоменклатураПартии.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	СубконтоНоменклатураПартии.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии);
	
	СубконтоНоменклатурныеГруппы = Новый Массив;
	СубконтоНоменклатурныеГруппы.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
	
	НачалоПериода = ПараметрыОтчета.НачалоПериода;
	КонецПериода = КонецДня(ПараметрыОтчета.КонецПериода);
	
	// Счета выручки.
	СчетаВыручки     = Новый Массив(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Выручка));
	
	СчетаВыручкиЕНВД = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ВыручкаЕНВД);
	СчетаВыручки     = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаВыручки, СчетаВыручкиЕНВД);
	
	СчетаВыпускПродукции = Новый Массив(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ВыпускПродукции));
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(Акцизы.Сумма) КАК Сумма,
	|	Акцизы.Регистратор КАК Регистратор,
	|	Акцизы.Организация КАК Организация,
	|	Акцизы.Подразделение КАК Подразделение,
	|	Акцизы.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Акцизы.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ Акцизы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.НоменклатурныеГруппы) КАК НоменклатурнаяГруппа,
	|		ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.Номенклатура) КАК Номенклатура,
	|		ХозрасчетныйОбороты.СуммаОборотДт КАК Сумма,
	|		ХозрасчетныйОбороты.Регистратор КАК Регистратор,
	|		ХозрасчетныйОбороты.Организация КАК Организация,
	|		ЕСТЬNULL(ХозрасчетныйОбороты.Подразделение, &ПустоеПодразделение) КАК Подразделение,
	|		ХозрасчетныйОбороты.ПериодМесяц КАК Период
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетАкцизов), &НоменклатурнаяГруппаНоменклатура, ИСТИНА, КорСчет В (&СчетРасчетовПоНалогамИСборам), ) КАК ХозрасчетныйОбороты) КАК Акцизы
	|
	|СГРУППИРОВАТЬ ПО
	|	Акцизы.Организация,
	|	Акцизы.Подразделение,
	|	Акцизы.НоменклатурнаяГруппа,
	|	Акцизы.Номенклатура,
	|	Акцизы.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Продажи.Период КАК Период,
	|	Продажи.Организация КАК Организация,
	|	Продажи.Подразделение КАК Подразделение,
	|	Продажи.Регистратор КАК Регистратор,
	|	Продажи.Контрагент КАК Контрагент,
	|	Продажи.Договор КАК Договор,
	|	Продажи.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Продажи.Номенклатура КАК Номенклатура,
	|	Продажи.Счет КАК Счет,
	|	Продажи.КорСчет КАК КорСчет,
	|	СУММА(Продажи.Количество) КАК Количество,
	|	Продажи.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(Продажи.ВыручкаБезНДС) КАК Сумма,
	|	Продажи.Розница КАК Розница,
	|	Продажи.КонтрагентПредставление КАК КонтрагентПредставление,
	|	Продажи.ДоговорПредставление КАК ДоговорПредставление,
	|	Продажи.ЭтоСчетСОсобымПорядкомНалогообложения КАК ЭтоСчетСОсобымПорядкомНалогообложения,
	|	Продажи.Ответственный КАК Ответственный
	|ПОМЕСТИТЬ Продажи
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХозрасчетныйОбороты.КоличествоОборотКт КАК Количество,
	|		ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто1, &РозничныеПокупатели) КАК Контрагент,
	|		ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто2, &РозничнаяПродажа) КАК Договор,
	|		ХозрасчетныйОбороты.Субконто1 КАК НоменклатурнаяГруппа,
	|		ХозрасчетныйОбороты.Субконто2 КАК СтавкаНДС,
	|		ХозрасчетныйОбороты.Субконто3 КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.КорСубконто2 ССЫЛКА Справочник.ДоговорыКонтрагентов
	|					И ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто2 КАК Справочник.ДоговорыКонтрагентов).УчетАгентскогоНДСПокупателем
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт
	|			КОГДА ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_18
	|					ИЛИ ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_118
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт * 100 / 118
	|			КОГДА ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_10
	|					ИЛИ ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_110
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт * 100 / 110
	|			КОГДА ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_20
	|					ИЛИ ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_120
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт * 100 / 120
	|			КОГДА ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_22
	|					ИЛИ ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_122
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт * 100 / 122
	|			КОГДА ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_5
	|					ИЛИ ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_105
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт * 100 / 105
	|			КОГДА ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_7
	|					ИЛИ ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_107
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт * 100 / 107
	|			КОГДА ХозрасчетныйОбороты.Субконто2 = &СтавкаНДС_0
	|					ИЛИ ХозрасчетныйОбороты.Субконто2 = &БезНДС
	|				ТОГДА ХозрасчетныйОбороты.СуммаОборотКт
	|		КОНЕЦ КАК ВыручкаБезНДС,
	|		ХозрасчетныйОбороты.Регистратор КАК Регистратор,
	|		ХозрасчетныйОбороты.Организация КАК Организация,
	|		ЕСТЬNULL(ХозрасчетныйОбороты.Подразделение, &ПустоеПодразделение) КАК Подразделение,
	|		ХозрасчетныйОбороты.ПериодМесяц КАК Период,
	|		ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.КорСчет В (&СчетаКассы)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ КАК Розница,
	|		ПРЕДСТАВЛЕНИЕ(ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто1, &РозничныеПокупатели)) КАК КонтрагентПредставление,
	|		ПРЕДСТАВЛЕНИЕ(ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто2, &РозничнаяПродажа)) КАК ДоговорПредставление,
	|		ХозрасчетныйОбороты.Счет КАК Счет,
	|		ХозрасчетныйОбороты.КорСчет КАК КорСчет,
	|		&ТекстПоляХозрасчетныйОборотыОтветственный КАК Ответственный,
	|		ЛОЖЬ КАК ЭтоСчетСОсобымПорядкомНалогообложения
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетаВыручки), &НоменклатурнаяГруппаСтавкаНДСНоменклатура, ИСТИНА, КорСчет В (&СчетаРасчетовСПокупателями), ) КАК ХозрасчетныйОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОбороты.КоличествоОборотКт,
	|		ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто1, &РозничныеПокупатели),
	|		ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто2, &РозничнаяПродажа),
	|		ХозрасчетныйОбороты.Субконто1,
	|		ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС),
	|		ХозрасчетныйОбороты.Субконто2,
	|		ХозрасчетныйОбороты.СуммаОборотКт,
	|		ХозрасчетныйОбороты.Регистратор,
	|		ХозрасчетныйОбороты.Организация,
	|		ЕСТЬNULL(ХозрасчетныйОбороты.Подразделение, &ПустоеПодразделение),
	|		ХозрасчетныйОбороты.ПериодМесяц,
	|		ВЫБОР
	|			КОГДА ХозрасчетныйОбороты.КорСчет В (&СчетаКассы)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ,
	|		ПРЕДСТАВЛЕНИЕ(ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто1, &РозничныеПокупатели)),
	|		ПРЕДСТАВЛЕНИЕССЫЛКИ(ЕСТЬNULL(ХозрасчетныйОбороты.КорСубконто2, &РозничнаяПродажа)),
	|		ХозрасчетныйОбороты.Счет,
	|		ХозрасчетныйОбороты.КорСчет,
	|		&ТекстПоляХозрасчетныйОборотыОтветственный,
	|		ИСТИНА
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетаВыручкиЕНВД), &НоменклатурнаяГруппаНоменклатура, ИСТИНА, КорСчет В (&СчетаРасчетовСПокупателями), ) КАК ХозрасчетныйОбороты) КАК Продажи
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Контрагент,
	|	Продажи.Договор,
	|	Продажи.НоменклатурнаяГруппа,
	|	Продажи.СтавкаНДС,
	|	Продажи.Номенклатура,
	|	Продажи.Регистратор,
	|	Продажи.Организация,
	|	Продажи.Подразделение,
	|	Продажи.Период,
	|	Продажи.Розница,
	|	Продажи.КонтрагентПредставление,
	|	Продажи.ДоговорПредставление,
	|	Продажи.Счет,
	|	Продажи.КорСчет,
	|	Продажи.Ответственный,
	|	Продажи.ЭтоСчетСОсобымПорядкомНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Продажи.Период КАК Период,
	|	Продажи.Организация КАК Организация,
	|	Продажи.Подразделение КАК Подразделение,
	|	Продажи.Регистратор КАК Регистратор,
	|	Продажи.Контрагент КАК Контрагент,
	|	Продажи.Договор КАК Договор,
	|	Продажи.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Продажи.Номенклатура КАК Номенклатура,
	|	Продажи.Счет КАК Счет,
	|	Продажи.КорСчет КАК КорСчет,
	|	СУММА(Продажи.Количество) КАК Количество,
	|	Продажи.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(Продажи.Сумма - ЕСТЬNULL(Акцизы.Сумма, 0)) КАК Сумма,
	|	Продажи.Розница КАК Розница,
	|	Продажи.КонтрагентПредставление КАК КонтрагентПредставление,
	|	Продажи.ДоговорПредставление КАК ДоговорПредставление,
	|	Продажи.Ответственный КАК Ответственный,
	|	Продажи.ЭтоСчетСОсобымПорядкомНалогообложения КАК ЭтоСчетСОсобымПорядкомНалогообложения
	|ПОМЕСТИТЬ СкорректированныеПродажи
	|ИЗ
	|	Продажи КАК Продажи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Акцизы КАК Акцизы
	|		ПО Продажи.Организация = Акцизы.Организация
	|			И Продажи.Подразделение = Акцизы.Подразделение
	|			И Продажи.Номенклатура = Акцизы.Номенклатура
	|			И Продажи.НоменклатурнаяГруппа = Акцизы.НоменклатурнаяГруппа
	|			И Продажи.Регистратор = Акцизы.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	Продажи.Контрагент,
	|	Продажи.Договор,
	|	Продажи.НоменклатурнаяГруппа,
	|	Продажи.СтавкаНДС,
	|	Продажи.Номенклатура,
	|	Продажи.Регистратор,
	|	Продажи.Организация,
	|	Продажи.Подразделение,
	|	Продажи.Период,
	|	Продажи.Розница,
	|	Продажи.КонтрагентПредставление,
	|	Продажи.ДоговорПредставление,
	|	Продажи.Счет,
	|	Продажи.КорСчет,
	|	Продажи.Ответственный,
	|	Продажи.ЭтоСчетСОсобымПорядкомНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Акцизы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Продажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОбороты.Период КАК Период,
	|	ХозрасчетныйОбороты.Регистратор КАК Регистратор,
	|	ХозрасчетныйОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ХозрасчетныйОбороты.Номенклатура КАК Номенклатура,
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	СУММА(ХозрасчетныйОбороты.Количество) КАК Количество,
	|	СУММА(ХозрасчетныйОбороты.Стоимость) КАК Стоимость,
	|	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	|	ХозрасчетныйОбороты.Склад КАК Склад,
	|	ХозрасчетныйОбороты.Партия КАК Партия,
	|	ХозрасчетныйОбороты.Счет КАК Счет,
	|	ХозрасчетныйОбороты.ЭтоКорректировка КАК ЭтоКорректировка,
	|	ХозрасчетныйОбороты.ЭтоВыпускПродукции КАК ЭтоВыпускПродукции
	|ПОМЕСТИТЬ СебестоимостьОборот
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.ПериодМесяц КАК Период,
	|		ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	|		ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК НоменклатурнаяГруппа,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК Номенклатура,
	|		ХозрасчетныйОборотыДтКт.Организация КАК Организация,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.КоличествоОборотКт, 0) КАК Количество,
	|		ХозрасчетныйОборотыДтКт.СуммаОборот КАК Стоимость,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение) КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &ВедетсяСуммовойУчетПоСкладам
	|				ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт2
	|			ИНАЧЕ &ПустойСклад
	|		КОНЕЦ КАК Склад,
	|		НЕОПРЕДЕЛЕНО КАК Партия,
	|		ХозрасчетныйОборотыДтКт.СчетДт КАК Счет,
	|		ЛОЖЬ КАК ЭтоКорректировка,
	|		ЛОЖЬ КАК ЭтоВыпускПродукции
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетУчетаНоменклатурыБезПартии), &СубконтоНоменклатураСклад, ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.ПериодМесяц,
	|		ХозрасчетныйОборотыДтКт.Регистратор,
	|		ХозрасчетныйОборотыДтКт.СубконтоДт1,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|		ХозрасчетныйОборотыДтКт.Организация,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.КоличествоОборотКт, 0),
	|		ХозрасчетныйОборотыДтКт.СуммаОборот,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение),
	|		ВЫБОР
	|			КОГДА &ВедетсяСуммовойУчетПоСкладам
	|				ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт2
	|			ИНАЧЕ &ПустойСклад
	|		КОНЕЦ,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт3,
	|		ХозрасчетныйОборотыДтКт.СчетДт,
	|		ЛОЖЬ,
	|		ЛОЖЬ
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетУчетаНоменклатурыСПартиями), &СубконтоНоменклатураСкладПартии, ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.ПериодМесяц,
	|		ХозрасчетныйОборотыДтКт.Регистратор,
	|		ХозрасчетныйОборотыДтКт.СубконтоДт1,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|		ХозрасчетныйОборотыДтКт.Организация,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.КоличествоОборотКт, 0),
	|		ХозрасчетныйОборотыДтКт.СуммаОборот,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение),
	|		&ПустойСклад,
	|		НЕОПРЕДЕЛЕНО,
	|		ХозрасчетныйОборотыДтКт.СчетДт,
	|		ЛОЖЬ,
	|		ЛОЖЬ
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетУчетаНоменклатурыБезСкладаБезПартии), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура), ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.ПериодМесяц,
	|		ХозрасчетныйОборотыДтКт.Регистратор,
	|		ХозрасчетныйОборотыДтКт.СубконтоДт1,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|		ХозрасчетныйОборотыДтКт.Организация,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.КоличествоОборотКт, 0),
	|		ХозрасчетныйОборотыДтКт.СуммаОборот,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение),
	|		&ПустойСклад,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт2,
	|		ХозрасчетныйОборотыДтКт.СчетДт,
	|		ЛОЖЬ,
	|		ЛОЖЬ
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетУчетаНоменклатурыСПартиямиБезСклада), &СубконтоНоменклатураПартии, ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СебестоимостьБракВПроизводстве.ПериодМесяц,
	|		СебестоимостьБракВПроизводстве.Регистратор,
	|		СебестоимостьБракВПроизводстве.СубконтоДт1,
	|		СебестоимостьБракВПроизводстве.СубконтоКт1,
	|		СебестоимостьБракВПроизводстве.Организация,
	|		ЕСТЬNULL(СебестоимостьБракВПроизводстве.КоличествоОборотКт, 0),
	|		СебестоимостьБракВПроизводстве.СуммаОборот,
	|		ЕСТЬNULL(СебестоимостьБракВПроизводстве.ПодразделениеДт, &ПустоеПодразделение),
	|		ВЫБОР
	|			КОГДА &ВедетсяСуммовойУчетПоСкладам
	|				ТОГДА СебестоимостьБракВПроизводстве.СубконтоКт2
	|			ИНАЧЕ &ПустойСклад
	|		КОНЕЦ,
	|		НЕОПРЕДЕЛЕНО,
	|		СебестоимостьБракВПроизводстве.СчетДт,
	|		ЛОЖЬ,
	|		ЛОЖЬ
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.БракВПроизводстве), , ИСТИНА) КАК СебестоимостьБракВПроизводстве
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеКорректировки.ПериодМесяц,
	|		ДанныеКорректировки.Регистратор,
	|		ДанныеКорректировки.СубконтоДт1,
	|		&ПустаяНоменклатура,
	|		ДанныеКорректировки.Организация,
	|		ЕСТЬNULL(ДанныеКорректировки.КоличествоОборотКт, 0),
	|		ДанныеКорректировки.СуммаОборот,
	|		ЕСТЬNULL(ДанныеКорректировки.ПодразделениеДт, &ПустоеПодразделение),
	|		ВЫБОР
	|			КОГДА &ВедетсяСуммовойУчетПоСкладам
	|				ТОГДА ЕСТЬNULL(Корректировка.СубконтоДт2, &ПустойСклад)
	|			ИНАЧЕ &ПустойСклад
	|		КОНЕЦ,
	|		НЕОПРЕДЕЛЕНО,
	|		ДанныеКорректировки.СчетДт,
	|		ИСТИНА,
	|		ЛОЖЬ
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетаВзаиморасчетовСПоставщиками), , ИСТИНА) КАК ДанныеКорректировки
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетУчетаНоменклатурыСПартиями), &НоменклатураСклады, СчетКт В (&СчетаВзаиморасчетовСПоставщиками), , ИСТИНА) КАК Корректировка
	|			ПО ДанныеКорректировки.СубконтоКт3 = Корректировка.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СебестоимостьПроизводства.ПериодМесяц,
	|		СебестоимостьПроизводства.Регистратор,
	|		ВЫРАЗИТЬ(СебестоимостьПроизводства.СубконтоДт1 КАК Справочник.НоменклатурныеГруппы),
	|		ВЫБОР
	|			КОГДА СебестоимостьПроизводства.СубконтоКт3 ЕСТЬ NULL
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ИНАЧЕ ВЫРАЗИТЬ(СебестоимостьПроизводства.СубконтоКт3 КАК Справочник.Номенклатура)
	|		КОНЕЦ,
	|		СебестоимостьПроизводства.Организация,
	|		ЕСТЬNULL(СебестоимостьПроизводства.КоличествоОборотКт, 0),
	|		СебестоимостьПроизводства.СуммаОборот,
	|		ЕСТЬNULL(СебестоимостьПроизводства.ПодразделениеДт, &ПустоеПодразделение),
	|		&ПустойСклад,
	|		НЕОПРЕДЕЛЕНО,
	|		СебестоимостьПроизводства.СчетДт,
	|		ЛОЖЬ,
	|		ИСТИНА
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетаУчетаПроизводства), , ИСТИНА) КАК СебестоимостьПроизводства
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СебестоимостьНоменклатурыНТТ.ПериодМесяц,
	|		СебестоимостьНоменклатурыНТТ.Регистратор,
	|		СебестоимостьНоменклатурыНТТ.СубконтоДт1,
	|		&НоменклатураНТТ,
	|		СебестоимостьНоменклатурыНТТ.Организация,
	|		ЕСТЬNULL(СебестоимостьНоменклатурыНТТ.КоличествоОборотКт, 0),
	|		СебестоимостьНоменклатурыНТТ.СуммаОборот,
	|		ЕСТЬNULL(СебестоимостьНоменклатурыНТТ.ПодразделениеДт, &ПустоеПодразделение),
	|		ВЫБОР
	|			КОГДА &ВедетсяСуммовойУчетПоСкладам
	|				ТОГДА СебестоимостьНоменклатурыНТТ.СубконтоКт1
	|			ИНАЧЕ &ПустойСклад
	|		КОНЕЦ,
	|		НЕОПРЕДЕЛЕНО,
	|		СебестоимостьНоменклатурыНТТ.СчетДт,
	|		ИСТИНА,
	|		ЛОЖЬ
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетУчетаНТТ), &СубконтоНТТ, ИСТИНА) КАК СебестоимостьНоменклатурыНТТ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВыпускПродукции.ПериодМесяц,
	|		ВыпускПродукции.Регистратор,
	|		ВЫРАЗИТЬ(ВыпускПродукции.СубконтоДт1 КАК Справочник.НоменклатурныеГруппы),
	|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|		ВыпускПродукции.Организация,
	|		ЕСТЬNULL(ВыпускПродукции.КоличествоОборотКт, 0),
	|		ВыпускПродукции.СуммаОборот,
	|		ЕСТЬNULL(ВыпускПродукции.ПодразделениеДт, &ПустоеПодразделение),
	|		&ПустойСклад,
	|		НЕОПРЕДЕЛЕНО,
	|		ВыпускПродукции.СчетДт,
	|		ЛОЖЬ,
	|		ИСТИНА
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетаВыпускПродукции), &СубконтоНоменклатурныеГруппы, ИСТИНА) КАК ВыпускПродукции) КАК ХозрасчетныйОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Период,
	|	ХозрасчетныйОбороты.Регистратор,
	|	ХозрасчетныйОбороты.НоменклатурнаяГруппа,
	|	ХозрасчетныйОбороты.Номенклатура,
	|	ХозрасчетныйОбороты.Организация,
	|	ХозрасчетныйОбороты.Подразделение,
	|	ХозрасчетныйОбороты.Склад,
	|	ХозрасчетныйОбороты.Партия,
	|	ХозрасчетныйОбороты.Счет,
	|	ХозрасчетныйОбороты.ЭтоКорректировка,
	|	ХозрасчетныйОбороты.ЭтоВыпускПродукции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Склад,
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОбороты.Период КАК Период,
	|	ХозрасчетныйОбороты.Номенклатура КАК Номенклатура,
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	СУММА(ХозрасчетныйОбороты.Наценка) КАК Наценка,
	|	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	|	ХозрасчетныйОбороты.Склад КАК Склад,
	|	ХозрасчетныйОбороты.ЭтоКорректировка КАК ЭтоКорректировка,
	|	ХозрасчетныйОбороты.Партия КАК Партия,
	|	ХозрасчетныйОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|ПОМЕСТИТЬ ТорговаяНаценка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.Период КАК Период,
	|		ХозрасчетныйОборотыДтКт.Организация КАК Организация,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение) КАК Подразделение,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК Номенклатура,
	|		ХозрасчетныйОборотыДтКт.СуммаОборот КАК Наценка,
	|		ВЫБОР
	|			КОГДА &ВедетсяСуммовойУчетПоСкладам
	|				ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт2
	|			ИНАЧЕ &ПустойСклад
	|		КОНЕЦ КАК Склад,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт3 КАК Партия,
	|		ЛОЖЬ КАК ЭтоКорректировка,
	|		ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК НоменклатурнаяГруппа
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Месяц, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетТорговойНаценкиСПартиями), &СубконтоНоменклатураСкладПартии, ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.Период,
	|		ХозрасчетныйОборотыДтКт.Организация,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение),
	|		ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|		ХозрасчетныйОборотыДтКт.СуммаОборот,
	|		ВЫБОР
	|			КОГДА &ВедетсяСуммовойУчетПоСкладам
	|				ТОГДА ХозрасчетныйОборотыДтКт.СубконтоКт2
	|			ИНАЧЕ &ПустойСклад
	|		КОНЕЦ,
	|		НЕОПРЕДЕЛЕНО,
	|		ЛОЖЬ,
	|		ХозрасчетныйОборотыДтКт.СубконтоДт1
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Месяц, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетТорговойНаценкиБезПартии), &СубконтоНоменклатураСклад, ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.Период,
	|		ХозрасчетныйОборотыДтКт.Организация,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение),
	|		ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|		ХозрасчетныйОборотыДтКт.СуммаОборот,
	|		&ПустойСклад,
	|		ХозрасчетныйОборотыДтКт.СубконтоКт2,
	|		ЛОЖЬ,
	|		ХозрасчетныйОборотыДтКт.СубконтоДт1
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Месяц, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетТорговойНаценкиБезСкладаСПартиями), &СубконтоНоменклатураПартии, ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОборотыДтКт.Период,
	|		ХозрасчетныйОборотыДтКт.Организация,
	|		ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение),
	|		ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|		ХозрасчетныйОборотыДтКт.СуммаОборот,
	|		&ПустойСклад,
	|		НЕОПРЕДЕЛЕНО,
	|		ЛОЖЬ,
	|		ХозрасчетныйОборотыДтКт.СубконтоДт1
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Месяц, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетТорговойНаценкиБезСкладаБезПартии), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура), ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТорговаяНаценкаНТТ.Период,
	|		ТорговаяНаценкаНТТ.Организация,
	|		ЕСТЬNULL(ТорговаяНаценкаНТТ.ПодразделениеДт, &ПустоеПодразделение),
	|		&НоменклатураНТТ,
	|		ТорговаяНаценкаНТТ.СуммаОборот,
	|		ВЫБОР
	|			КОГДА &ВедетсяСуммовойУчетПоСкладам
	|				ТОГДА ТорговаяНаценкаНТТ.СубконтоКт1
	|			ИНАЧЕ &ПустойСклад
	|		КОНЕЦ,
	|		НЕОПРЕДЕЛЕНО,
	|		ИСТИНА,
	|		ТорговаяНаценкаНТТ.СубконтоДт1
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Месяц, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетТорговойНаценкиНТТ), &СубконтоНТТ, ИСТИНА) КАК ТорговаяНаценкаНТТ) КАК ХозрасчетныйОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Номенклатура,
	|	ХозрасчетныйОбороты.Организация,
	|	ХозрасчетныйОбороты.Подразделение,
	|	ХозрасчетныйОбороты.Склад,
	|	ХозрасчетныйОбороты.Период,
	|	ХозрасчетныйОбороты.ЭтоКорректировка,
	|	ХозрасчетныйОбороты.Партия,
	|	ХозрасчетныйОбороты.НоменклатурнаяГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Себестоимость.Период КАК Период,
	|	Себестоимость.Номенклатура КАК Номенклатура,
	|	Себестоимость.Организация КАК Организация,
	|	Себестоимость.Подразделение КАК Подразделение,
	|	Себестоимость.Склад КАК Склад,
	|	Себестоимость.Партия КАК Партия,
	|	СУММА(Себестоимость.Стоимость) КАК Стоимость,
	|	СУММА(Себестоимость.Количество) КАК Количество,
	|	Себестоимость.ЭтоКорректировка
	|		ИЛИ СУММА(Себестоимость.Количество) = 0 КАК ЭтоКорректировка,
	|	Себестоимость.ЭтоВыпускПродукции КАК ЭтоВыпускПродукции,
	|	Себестоимость.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|ПОМЕСТИТЬ СкорректированнаяСтоимость
	|ИЗ
	|	(ВЫБРАТЬ
	|		СебестоимостьОборот.Период КАК Период,
	|		СебестоимостьОборот.Номенклатура КАК Номенклатура,
	|		СебестоимостьОборот.Организация КАК Организация,
	|		СебестоимостьОборот.Подразделение КАК Подразделение,
	|		СебестоимостьОборот.Склад КАК Склад,
	|		СебестоимостьОборот.Партия КАК Партия,
	|		СебестоимостьОборот.Стоимость КАК Стоимость,
	|		СебестоимостьОборот.Количество КАК Количество,
	|		СебестоимостьОборот.ЭтоКорректировка КАК ЭтоКорректировка,
	|		СебестоимостьОборот.ЭтоВыпускПродукции КАК ЭтоВыпускПродукции,
	|		СебестоимостьОборот.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|	ИЗ
	|		СебестоимостьОборот КАК СебестоимостьОборот
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТорговаяНаценка.Период,
	|		ТорговаяНаценка.Номенклатура,
	|		ТорговаяНаценка.Организация,
	|		ТорговаяНаценка.Подразделение,
	|		ТорговаяНаценка.Склад,
	|		ТорговаяНаценка.Партия,
	|		ТорговаяНаценка.Наценка,
	|		0,
	|		ТорговаяНаценка.ЭтоКорректировка,
	|		ЛОЖЬ,
	|		ТорговаяНаценка.НоменклатурнаяГруппа
	|	ИЗ
	|		ТорговаяНаценка КАК ТорговаяНаценка) КАК Себестоимость
	|
	|СГРУППИРОВАТЬ ПО
	|	Себестоимость.Номенклатура,
	|	Себестоимость.Организация,
	|	Себестоимость.Подразделение,
	|	Себестоимость.Склад,
	|	Себестоимость.Партия,
	|	Себестоимость.Период,
	|	Себестоимость.ЭтоВыпускПродукции,
	|	Себестоимость.НоменклатурнаяГруппа,
	|	Себестоимость.ЭтоКорректировка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Склад,
	|	Партия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТорговаяНаценка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СебестоимостьОборот.Период КАК Период,
	|	СебестоимостьОборот.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	СкорректированнаяСтоимость.Номенклатура КАК Номенклатура,
	|	СкорректированнаяСтоимость.Организация КАК Организация,
	|	СкорректированнаяСтоимость.Подразделение КАК Подразделение,
	|	СкорректированнаяСтоимость.Склад КАК Склад,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СебестоимостьОборот.Регистратор) КАК КоличествоРегистраторов
	|ПОМЕСТИТЬ ПодсчетДокументовДляКорректировки
	|ИЗ
	|	СкорректированнаяСтоимость КАК СкорректированнаяСтоимость
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СебестоимостьОборот КАК СебестоимостьОборот
	|		ПО СкорректированнаяСтоимость.Период = СебестоимостьОборот.Период
	|			И СкорректированнаяСтоимость.Организация = СебестоимостьОборот.Организация
	|			И СкорректированнаяСтоимость.Подразделение = СебестоимостьОборот.Подразделение
	|			И СкорректированнаяСтоимость.Номенклатура = СебестоимостьОборот.Номенклатура
	|			И СкорректированнаяСтоимость.НоменклатурнаяГруппа = СебестоимостьОборот.НоменклатурнаяГруппа
	|			И СкорректированнаяСтоимость.Склад = СебестоимостьОборот.Склад
	|			И (СкорректированнаяСтоимость.ЭтоКорректировка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СкорректированнаяСтоимость.Подразделение,
	|	СкорректированнаяСтоимость.Склад,
	|	СкорректированнаяСтоимость.Организация,
	|	СкорректированнаяСтоимость.Номенклатура,
	|	СебестоимостьОборот.НоменклатурнаяГруппа,
	|	СебестоимостьОборот.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СебестоимостьОборот.Период КАК Период,
	|	СебестоимостьОборот.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	СкорректированнаяСтоимость.Номенклатура КАК Номенклатура,
	|	СкорректированнаяСтоимость.Организация КАК Организация,
	|	СкорректированнаяСтоимость.Подразделение КАК Подразделение,
	|	СкорректированнаяСтоимость.Склад КАК Склад,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(СкорректированнаяСтоимость.Количество, 0) = 0
	|					ИЛИ ЕСТЬNULL(СебестоимостьОборот.Количество, 0) = 0
	|				ТОГДА 0
	|			ИНАЧЕ СкорректированнаяСтоимость.Стоимость * СебестоимостьОборот.Количество / СкорректированнаяСтоимость.Количество
	|		КОНЕЦ) КАК Стоимость,
	|	СебестоимостьОборот.Регистратор КАК Регистратор,
	|	СебестоимостьОборот.Счет КАК Счет,
	|	СУММА(ЕСТЬNULL(СебестоимостьОборот.Количество, 0)) КАК Количество,
	|	СкорректированнаяСтоимость.ЭтоВыпускПродукции КАК ЭтоВыпускПродукции
	|ПОМЕСТИТЬ СкорректированнаяСебестоимостьПоДокументам
	|ИЗ
	|	СкорректированнаяСтоимость КАК СкорректированнаяСтоимость
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СебестоимостьОборот КАК СебестоимостьОборот
	|		ПО СкорректированнаяСтоимость.Период = СебестоимостьОборот.Период
	|			И СкорректированнаяСтоимость.Организация = СебестоимостьОборот.Организация
	|			И СкорректированнаяСтоимость.Подразделение = СебестоимостьОборот.Подразделение
	|			И СкорректированнаяСтоимость.Номенклатура = СебестоимостьОборот.Номенклатура
	|			И СкорректированнаяСтоимость.НоменклатурнаяГруппа = СебестоимостьОборот.НоменклатурнаяГруппа
	|			И (СкорректированнаяСтоимость.Склад = СебестоимостьОборот.Склад
	|					И СкорректированнаяСтоимость.Партия = СебестоимостьОборот.Партия
	|				ИЛИ СебестоимостьОборот.ЭтоКорректировка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СебестоимостьОборот.НоменклатурнаяГруппа,
	|	СкорректированнаяСтоимость.Номенклатура,
	|	СкорректированнаяСтоимость.Организация,
	|	СкорректированнаяСтоимость.Подразделение,
	|	СкорректированнаяСтоимость.Склад,
	|	СебестоимостьОборот.Регистратор,
	|	СебестоимостьОборот.Счет,
	|	СебестоимостьОборот.Период,
	|	СкорректированнаяСтоимость.ЭтоВыпускПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СебестоимостьОборот.Период,
	|	СебестоимостьОборот.НоменклатурнаяГруппа,
	|	СкорректированнаяСтоимость.Номенклатура,
	|	СкорректированнаяСтоимость.Организация,
	|	СкорректированнаяСтоимость.Подразделение,
	|	СкорректированнаяСтоимость.Склад,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПодсчетДокументовДляКорректировки.КоличествоРегистраторов, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ СкорректированнаяСтоимость.Стоимость / ПодсчетДокументовДляКорректировки.КоличествоРегистраторов
	|	КОНЕЦ,
	|	СебестоимостьОборот.Регистратор,
	|	СебестоимостьОборот.Счет,
	|	СкорректированнаяСтоимость.Количество,
	|	СкорректированнаяСтоимость.ЭтоВыпускПродукции
	|ИЗ
	|	СкорректированнаяСтоимость КАК СкорректированнаяСтоимость
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СебестоимостьОборот КАК СебестоимостьОборот
	|		ПО СкорректированнаяСтоимость.Период = СебестоимостьОборот.Период
	|			И СкорректированнаяСтоимость.Организация = СебестоимостьОборот.Организация
	|			И СкорректированнаяСтоимость.Подразделение = СебестоимостьОборот.Подразделение
	|			И СкорректированнаяСтоимость.Номенклатура = СебестоимостьОборот.Номенклатура
	|			И СкорректированнаяСтоимость.НоменклатурнаяГруппа = СебестоимостьОборот.НоменклатурнаяГруппа
	|			И СкорректированнаяСтоимость.Склад = СебестоимостьОборот.Склад
	|			И (СкорректированнаяСтоимость.ЭтоКорректировка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПодсчетДокументовДляКорректировки КАК ПодсчетДокументовДляКорректировки
	|		ПО СкорректированнаяСтоимость.Период = ПодсчетДокументовДляКорректировки.Период
	|			И СкорректированнаяСтоимость.Организация = ПодсчетДокументовДляКорректировки.Организация
	|			И СкорректированнаяСтоимость.Подразделение = ПодсчетДокументовДляКорректировки.Подразделение
	|			И СкорректированнаяСтоимость.Номенклатура = ПодсчетДокументовДляКорректировки.Номенклатура
	|			И СкорректированнаяСтоимость.НоменклатурнаяГруппа = ПодсчетДокументовДляКорректировки.НоменклатурнаяГруппа
	|			И СкорректированнаяСтоимость.Склад = ПодсчетДокументовДляКорректировки.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкорректированнаяСебестоимостьПоДокументам.Период КАК Период,
	|	СкорректированнаяСебестоимостьПоДокументам.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	СкорректированнаяСебестоимостьПоДокументам.Номенклатура КАК Номенклатура,
	|	СкорректированнаяСебестоимостьПоДокументам.Организация КАК Организация,
	|	СкорректированнаяСебестоимостьПоДокументам.Подразделение КАК Подразделение,
	|	СкорректированнаяСебестоимостьПоДокументам.Склад КАК Склад,
	|	СкорректированнаяСебестоимостьПоДокументам.Регистратор КАК Регистратор,
	|	СкорректированнаяСебестоимостьПоДокументам.Счет КАК Счет,
	|	СкорректированнаяСебестоимостьПоДокументам.Стоимость КАК Стоимость,
	|	СкорректированнаяСебестоимостьПоДокументам.Количество КАК Количество
	|ПОМЕСТИТЬ СебестоимостьПоДокументам
	|ИЗ
	|	СкорректированнаяСебестоимостьПоДокументам КАК СкорректированнаяСебестоимостьПоДокументам
	|ГДЕ
	|	СкорректированнаяСебестоимостьПоДокументам.Стоимость <> 0
	|	И НЕ СкорректированнаяСебестоимостьПоДокументам.ЭтоВыпускПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СкорректированнаяСебестоимостьПоДокументам.Период,
	|	СкорректированнаяСебестоимостьПоДокументам.НоменклатурнаяГруппа,
	|	СкорректированнаяСебестоимостьПоДокументам.Номенклатура,
	|	СкорректированнаяСебестоимостьПоДокументам.Организация,
	|	СкорректированнаяСебестоимостьПоДокументам.Подразделение,
	|	СкорректированнаяСебестоимостьПоДокументам.Склад,
	|	СкорректированнаяСебестоимостьПоДокументам.Регистратор,
	|	СкорректированнаяСебестоимостьПоДокументам.Счет,
	|	СебестоимостьОборот.Стоимость - СкорректированнаяСебестоимостьПоДокументам.Стоимость,
	|	0
	|ИЗ
	|	СебестоимостьОборот КАК СебестоимостьОборот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СкорректированнаяСебестоимостьПоДокументам КАК СкорректированнаяСебестоимостьПоДокументам
	|		ПО СебестоимостьОборот.Период = СкорректированнаяСебестоимостьПоДокументам.Период
	|			И СебестоимостьОборот.Регистратор = СкорректированнаяСебестоимостьПоДокументам.Регистратор
	|			И СебестоимостьОборот.Организация = СкорректированнаяСебестоимостьПоДокументам.Организация
	|			И СебестоимостьОборот.Подразделение = СкорректированнаяСебестоимостьПоДокументам.Подразделение
	|			И СебестоимостьОборот.Склад = СкорректированнаяСебестоимостьПоДокументам.Склад
	|			И СебестоимостьОборот.Номенклатура = СкорректированнаяСебестоимостьПоДокументам.Номенклатура
	|			И СебестоимостьОборот.НоменклатурнаяГруппа = СкорректированнаяСебестоимостьПоДокументам.НоменклатурнаяГруппа
	|ГДЕ
	|	СебестоимостьОборот.Стоимость - СкорректированнаяСебестоимостьПоДокументам.Стоимость <> 0
	|	И СебестоимостьОборот.ЭтоВыпускПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкорректированныеПродажи.Организация КАК Организация,
	|	СкорректированныеПродажи.Подразделение КАК Подразделение,
	|	СкорректированныеПродажи.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	СкорректированныеПродажи.Номенклатура КАК Номенклатура,
	|	СкорректированныеПродажи.Регистратор КАК Регистратор,
	|	СУММА(СкорректированныеПродажи.Количество) КАК Количество
	|ПОМЕСТИТЬ ПродажиГруппировка
	|ИЗ
	|	СкорректированныеПродажи КАК СкорректированныеПродажи
	|
	|СГРУППИРОВАТЬ ПО
	|	СкорректированныеПродажи.Организация,
	|	СкорректированныеПродажи.Подразделение,
	|	СкорректированныеПродажи.Номенклатура,
	|	СкорректированныеПродажи.НоменклатурнаяГруппа,
	|	СкорректированныеПродажи.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Себестоимость.Период КАК Период,
	|	Себестоимость.Номенклатура КАК Номенклатура,
	|	Себестоимость.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Себестоимость.Организация КАК Организация,
	|	Себестоимость.Подразделение КАК Подразделение,
	|	Себестоимость.Склад КАК Склад,
	|	СУММА(Себестоимость.Стоимость) КАК Стоимость,
	|	Себестоимость.Регистратор КАК Регистратор,
	|	Себестоимость.Счет КАК Счет,
	|	СУММА(Себестоимость.Количество) КАК Количество
	|ПОМЕСТИТЬ СебестоимостьПоДокументамПродажи
	|ИЗ
	|	(ВЫБРАТЬ
	|		СебестоимостьПоДокументам.Период КАК Период,
	|		СебестоимостьПоДокументам.Номенклатура КАК Номенклатура,
	|		СебестоимостьПоДокументам.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|		СебестоимостьПоДокументам.Организация КАК Организация,
	|		СебестоимостьПоДокументам.Подразделение КАК Подразделение,
	|		СебестоимостьПоДокументам.Склад КАК Склад,
	|		СебестоимостьПоДокументам.Стоимость КАК Стоимость,
	|		СебестоимостьПоДокументам.Регистратор КАК Регистратор,
	|		СебестоимостьПоДокументам.Счет КАК Счет,
	|		СебестоимостьПоДокументам.Количество КАК Количество
	|	ИЗ
	|		СебестоимостьПоДокументам КАК СебестоимостьПоДокументам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродажиГруппировка КАК ПродажиГруппировка
	|			ПО СебестоимостьПоДокументам.Организация = ПродажиГруппировка.Организация
	|				И СебестоимостьПоДокументам.Подразделение = ПродажиГруппировка.Подразделение
	|				И СебестоимостьПоДокументам.Номенклатура = ПродажиГруппировка.Номенклатура
	|				И СебестоимостьПоДокументам.НоменклатурнаяГруппа = ПродажиГруппировка.НоменклатурнаяГруппа
	|				И СебестоимостьПоДокументам.Регистратор = ПродажиГруппировка.Регистратор
	|	ГДЕ
	|		СебестоимостьПоДокументам.Стоимость <> 0) КАК Себестоимость
	|
	|СГРУППИРОВАТЬ ПО
	|	Себестоимость.НоменклатурнаяГруппа,
	|	Себестоимость.Организация,
	|	Себестоимость.Подразделение,
	|	Себестоимость.Номенклатура,
	|	Себестоимость.Период,
	|	Себестоимость.Склад,
	|	Себестоимость.Счет,
	|	Себестоимость.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Себестоимость.Период КАК Период,
	|	Себестоимость.Номенклатура КАК Номенклатура,
	|	Себестоимость.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Себестоимость.Организация КАК Организация,
	|	Себестоимость.Подразделение КАК Подразделение,
	|	Себестоимость.Склад КАК Склад,
	|	СУММА(Себестоимость.Стоимость) КАК Стоимость,
	|	Себестоимость.Регистратор КАК Регистратор,
	|	Себестоимость.Счет КАК Счет,
	|	СУММА(Себестоимость.Количество) КАК Количество,
	|	Себестоимость.Счет = &СчетСебестоимостьПродажЕНВД КАК ЭтоСчетСОсобымПорядкомНалогообложения
	|ПОМЕСТИТЬ Себестоимость
	|ИЗ
	|	(ВЫБРАТЬ
	|		СебестоимостьПоДокументамПродажи.Период КАК Период,
	|		СебестоимостьПоДокументамПродажи.Номенклатура КАК Номенклатура,
	|		СебестоимостьПоДокументамПродажи.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|		СебестоимостьПоДокументамПродажи.Организация КАК Организация,
	|		СебестоимостьПоДокументамПродажи.Подразделение КАК Подразделение,
	|		СебестоимостьПоДокументамПродажи.Склад КАК Склад,
	|		СебестоимостьПоДокументамПродажи.Стоимость КАК Стоимость,
	|		СебестоимостьПоДокументамПродажи.Регистратор КАК Регистратор,
	|		СебестоимостьПоДокументамПродажи.Счет КАК Счет,
	|		СебестоимостьПоДокументамПродажи.Количество КАК Количество
	|	ИЗ
	|		СебестоимостьПоДокументамПродажи КАК СебестоимостьПоДокументамПродажи
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СебестоимостьПоДокументам.Период,
	|		СебестоимостьПоДокументам.Номенклатура,
	|		СебестоимостьПоДокументам.НоменклатурнаяГруппа,
	|		СебестоимостьПоДокументам.Организация,
	|		СебестоимостьПоДокументам.Подразделение,
	|		СебестоимостьПоДокументам.Склад,
	|		СебестоимостьПоДокументам.Стоимость,
	|		СебестоимостьПоДокументам.Регистратор,
	|		СебестоимостьПоДокументам.Счет,
	|		СебестоимостьПоДокументам.Количество
	|	ИЗ
	|		СебестоимостьПоДокументам КАК СебестоимостьПоДокументам
	|	ГДЕ
	|		СебестоимостьПоДокументам.Стоимость <> 0
	|		И НЕ СебестоимостьПоДокументам.Регистратор В
	|					(ВЫБРАТЬ
	|						СебестоимостьПоДокументамПродажи.Регистратор КАК Регистратор
	|					ИЗ
	|						СебестоимостьПоДокументамПродажи КАК СебестоимостьПоДокументамПродажи)) КАК Себестоимость
	|
	|СГРУППИРОВАТЬ ПО
	|	Себестоимость.НоменклатурнаяГруппа,
	|	Себестоимость.Организация,
	|	Себестоимость.Подразделение,
	|	Себестоимость.Номенклатура,
	|	Себестоимость.Период,
	|	Себестоимость.Склад,
	|	Себестоимость.Счет,
	|	Себестоимость.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Номенклатура,
	|	НоменклатурнаяГруппа,
	|	Регистратор,
	|	ЭтоСчетСОсобымПорядкомНалогообложения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПодсчетДокументовДляКорректировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СкорректированнаяСебестоимостьПоДокументам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПродажиГруппировка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СебестоимостьПоДокументам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СкорректированныеПродажи.Период КАК Период,
	|	СкорректированныеПродажи.Организация КАК Организация,
	|	ЕСТЬNULL(СкорректированныеПродажи.Подразделение, &ПустоеПодразделение) КАК Подразделение,
	|	СкорректированныеПродажи.Договор КАК Договор,
	|	СкорректированныеПродажи.Розница КАК Розница,
	|	СУММА(ЕСТЬNULL(СкорректированныеПродажи.Сумма, 0)) КАК Выручка,
	|	СУММА(ЕСТЬNULL(Себестоимость.Стоимость, 0)) КАК Себестоимость,
	|	СУММА(ЕСТЬNULL(СкорректированныеПродажи.Сумма, 0) - ЕСТЬNULL(Себестоимость.Стоимость, 0)) КАК ВаловаяПрибыль,
	|	СкорректированныеПродажи.КонтрагентПредставление КАК КонтрагентПредставление,
	|	СкорректированныеПродажи.ДоговорПредставление КАК ДоговорПредставление,
	|	ВЫБОР
	|		КОГДА СкорректированныеПродажи.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА &РаботыИУслуги
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(СкорректированныеПродажи.Номенклатура)
	|	КОНЕЦ КАК НоменклатураПредставление,
	|	СкорректированныеПродажи.Контрагент КАК Контрагент,
	|	СкорректированныеПродажи.Номенклатура КАК Номенклатура,
	|	СкорректированныеПродажи.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	СкорректированныеПродажи.Счет КАК Счет,
	|	СкорректированныеПродажи.СтавкаНДС КАК СтавкаНДС,
	|	ЕСТЬNULL(Себестоимость.Склад, &ПустойСклад) КАК Склад,
	|	СУММА(ЕСТЬNULL(СкорректированныеПродажи.Количество, 0)) КАК Количество,
	|	СкорректированныеПродажи.Ответственный КАК Ответственный
	|ПОМЕСТИТЬ ВыручкаИСебестоимость
	|ИЗ
	|	СкорректированныеПродажи КАК СкорректированныеПродажи
	|		ЛЕВОЕ СОЕДИНЕНИЕ СебестоимостьПоДокументамПродажи КАК Себестоимость
	|		ПО СкорректированныеПродажи.Организация = Себестоимость.Организация
	|			И СкорректированныеПродажи.Подразделение = Себестоимость.Подразделение
	|			И СкорректированныеПродажи.Номенклатура = Себестоимость.Номенклатура
	|			И СкорректированныеПродажи.НоменклатурнаяГруппа = Себестоимость.НоменклатурнаяГруппа
	|			И СкорректированныеПродажи.Регистратор = Себестоимость.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	СкорректированныеПродажи.КонтрагентПредставление,
	|	СкорректированныеПродажи.ДоговорПредставление,
	|	СкорректированныеПродажи.Период,
	|	СкорректированныеПродажи.Организация,
	|	СкорректированныеПродажи.Розница,
	|	СкорректированныеПродажи.Контрагент,
	|	ЕСТЬNULL(СкорректированныеПродажи.Подразделение, &ПустоеПодразделение),
	|	СкорректированныеПродажи.Номенклатура,
	|	СкорректированныеПродажи.НоменклатурнаяГруппа,
	|	СкорректированныеПродажи.Счет,
	|	СкорректированныеПродажи.СтавкаНДС,
	|	ЕСТЬNULL(Себестоимость.Склад, &ПустойСклад),
	|	СкорректированныеПродажи.Договор,
	|	СкорректированныеПродажи.Ответственный
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Период,
	|	ХозрасчетныйОборотыДтКт.Организация,
	|	ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение),
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|	ЛОЖЬ,
	|	СУММА(0),
	|	СУММА(ЕСТЬNULL(ХозрасчетныйОборотыДтКт.СуммаОборот, 0)),
	|	СУММА(ЕСТЬNULL(-ХозрасчетныйОборотыДтКт.СуммаОборот, 0)),
	|	&ПустоеЗначение,
	|	"""",
	|	"""",
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|	ХозрасчетныйОборотыДтКт.СчетДт,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка),
	|	&ПустойСклад,
	|	СУММА(ЕСТЬNULL(ХозрасчетныйОборотыДтКт.КоличествоОборотКт, 0)),
	|	&ТекстПоляОтветственныйПоУмолчанию
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Регистратор, СчетДт В (&СчетаСебестоимости), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), СчетКт В (&СчетаРасходов), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат), ИСТИНА) КАК ХозрасчетныйОборотыДтКт
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОборотыДтКт.Период,
	|	ХозрасчетныйОборотыДтКт.Организация,
	|	ХозрасчетныйОборотыДтКт.СубконтоКт1,
	|	ЕСТЬNULL(ХозрасчетныйОборотыДтКт.ПодразделениеДт, &ПустоеПодразделение),
	|	ХозрасчетныйОборотыДтКт.СчетДт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Себестоимость.Период,
	|	Себестоимость.Организация,
	|	ЕСТЬNULL(Себестоимость.Подразделение, &ПустоеПодразделение),
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|	ЛОЖЬ,
	|	СУММА(0),
	|	СУММА(ЕСТЬNULL(Себестоимость.Стоимость, 0)),
	|	СУММА(ЕСТЬNULL(-Себестоимость.Стоимость, 0)),
	|	&ВыпускПродукции,
	|	"""",
	|	ВЫБОР
	|		КОГДА Себестоимость.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|			ТОГДА &КорректировкаВыпускаПродукции
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(Себестоимость.Номенклатура)
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	Себестоимость.Номенклатура,
	|	Себестоимость.НоменклатурнаяГруппа,
	|	Себестоимость.Счет,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка),
	|	ЕСТЬNULL(Себестоимость.Склад, &ПустойСклад),
	|	СУММА(Себестоимость.Количество),
	|	&ТекстПоляОтветственныйПоУмолчанию
	|ИЗ
	|	Себестоимость КАК Себестоимость
	|ГДЕ
	|	НЕ Себестоимость.Регистратор В
	|				(ВЫБРАТЬ
	|					СебестоимостьПоДокументамПродажи.Регистратор КАК Регистратор
	|				ИЗ
	|					СебестоимостьПоДокументамПродажи КАК СебестоимостьПоДокументамПродажи)
	|
	|СГРУППИРОВАТЬ ПО
	|	Себестоимость.Организация,
	|	ЕСТЬNULL(Себестоимость.Подразделение, &ПустоеПодразделение),
	|	Себестоимость.Номенклатура,
	|	Себестоимость.НоменклатурнаяГруппа,
	|	Себестоимость.Счет,
	|	Себестоимость.Период,
	|	ЕСТЬNULL(Себестоимость.Склад, &ПустойСклад)";

	
	СхемаЗапроса = СхемыЗапросов.Создать(ТекстЗапроса);
	
	Для Каждого ОписаниеЗапроса Из СхемаЗапроса.ПакетЗапросов Цикл
		
		Контекст = Новый Структура;
		Контекст.Вставить("Схема",  СхемаЗапроса);
		Контекст.Вставить("Запрос", ОписаниеЗапроса);
		
		Если ТипЗнч(ОписаниеЗапроса) <> Тип("ЗапросВыбораСхемыЗапроса") Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяВременнойТаблицы =  ОписаниеЗапроса.ТаблицаДляПомещения;
		ИндексКолонки = -1;
		
		Если ИмяВременнойТаблицы = "ВыручкаИСебестоимость" 
			И ЗначениеЗаполнено(ПараметрыОтбора.Контрагент)
			И Не ПустаяСтрока(ПараметрыОтбора.КонтрагентФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.КонтрагентФормулаСравнения, 
				"СкорректированныеПродажи.Контрагент", "&Контрагент");
			Оператор = ОписаниеЗапроса.Операторы[0];
			
			Если Оператор.Отбор.Количество() = 0 Тогда
				Оператор.Отбор.Добавить(Новый ВыражениеСхемыЗапроса(Условие));
			Иначе
				Оператор.Отбор[0] = Новый ВыражениеСхемыЗапроса(Условие);
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого ОператорВременннойТаблицы Из ОписаниеЗапроса.Операторы Цикл
			
			РегистраторДобавлен = Ложь;
			
			Для Каждого ИсточникВременннойТаблицы Из ОператорВременннойТаблицы.Источники Цикл
				
				Если ТипЗнч(ИсточникВременннойТаблицы.Источник) <> Тип("ВложенныйЗапросСхемыЗапроса")
					И Не (ТипЗнч(ИсточникВременннойТаблицы.Источник) = Тип("ТаблицаСхемыЗапроса") 
						И ИмяВременнойТаблицы = "ВыручкаИСебестоимость") Тогда
					Продолжить;
				КонецЕсли;
			
				Если ИмяВременнойТаблицы = "ВыручкаИСебестоимость" Тогда
					
					Если ПараметрыОтчета.ИдентификаторОтчета = "ВаловаяПрибыль" И Не РегистраторДобавлен Тогда
						
						РегистраторДобавлен = Истина;
						ИмяТаблицы = ИсточникВременннойТаблицы.Источник.Псевдоним;
	
						Если ИндексКолонки < 0 Тогда 
							ОператорВременннойТаблицы.ВыбираемыеПоля.Добавить(СтрШаблон("%1.Регистратор", ИмяТаблицы));
							ОператорВременннойТаблицы.Группировка.Добавить(СтрШаблон("%1.Регистратор", ИмяТаблицы));
							ИндексКолонки = ОператорВременннойТаблицы.ВыбираемыеПоля.Количество() - 1;
						Иначе
							ОператорВременннойТаблицы.ВыбираемыеПоля.Добавить(СтрШаблон("%1.Регистратор", ИмяТаблицы), ИндексКолонки);
							ОператорВременннойТаблицы.Группировка.Добавить(СтрШаблон("%1.Регистратор", ИмяТаблицы));
						КонецЕсли;
						
					КонецЕсли;
					
					Контекст.Вставить("Таблица", ИсточникВременннойТаблицы.Источник);
					Если ИсточникВременннойТаблицы.Источник.Псевдоним = "ХозрасчетныйОборотыДтКт" Тогда
						УстановитьОтборыВыручкаИСебестоимость(Контекст, ПараметрыОтбора);
					КонецЕсли;

				Иначе
					Для Каждого ОператораТаблицы Из ИсточникВременннойТаблицы.Источник.Запрос.Операторы Цикл
						
						Для Каждого ИсточникТаблицы Из ОператораТаблицы.Источники Цикл
							
							Контекст.Вставить("Таблица", ИсточникТаблицы.Источник);
							
							Если ИмяВременнойТаблицы = "Акцизы" Тогда
								
								УстановитьОтборыАкциз(Контекст, ПараметрыОтбора);
								
							ИначеЕсли ИмяВременнойТаблицы = "Продажи" Тогда
								
								УстановитьОтборыПродажи(Контекст, ПараметрыОтбора);
								
							ИначеЕсли ИмяВременнойТаблицы = "СебестоимостьОборот" Тогда
								
								Если Контекст.Таблица.Псевдоним = "СебестоимостьНоменклатурыНТТ" Тогда
									УстановитьВыбираемоеПолеНоменклатураНТТ(ОператораТаблицы, Контекст, ИспользоватьОборотнуюНоменклатуру);
								КонецЕсли;
								
								УстановитьОтборыСебестоимость(Контекст, ПараметрыОтбора);
								
							ИначеЕсли ИмяВременнойТаблицы = "ТорговаяНаценка" Тогда
								
								Если Контекст.Таблица.Псевдоним = "ТорговаяНаценкаНТТ" Тогда
									УстановитьВыбираемоеПолеНоменклатураНТТ(ОператораТаблицы, Контекст, ИспользоватьОборотнуюНоменклатуру);
								КонецЕсли;
								
								УстановитьОтборыТорговаяНаценка(Контекст, ПараметрыОтбора);
								
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЦикла;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();

	Если ПараметрыОтчета.Свойство("Периодичность") Тогда
		ПериодичностьОтчета = Новый Соответствие;
		ПериодичностьОтчета.Вставить(6, "ДЕНЬ");
		ПериодичностьОтчета.Вставить(9, "МЕСЯЦ");
		ПериодичностьОтчета.Вставить(10, "КВАРТАЛ");
		ПериодичностьОтчета.Вставить(11, "ПОЛУГОДИЕ");
		ПериодичностьОтчета.Вставить(12, "ГОД");
		
		Периодичность = ПериодичностьОтчета.Получить(ПараметрыОтчета.Периодичность);
		
		// Скорректируем текст запроса для выбранной периодичности.
		Если Периодичность <> Неопределено Тогда
			ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			".ПериодМесяц",
			".Период" + Периодичность);
			
		КонецЕсли;
	КонецЕсли;
	
	ИспользоватьНесколькоОтветственных = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОтветственных");
	Если ИспользоватьНесколькоОтветственных Тогда
		ТекстПоляХозрасчетныйОборотыОтветственный = ТекстПоляОтветственный(
			"ХозрасчетныйОбороты",
			"Регистратор",
			ТипыДокументовПродажи(),
			ПараметрыОтчета.НачалоПериода,
			ПараметрыОтчета.КонецПериода,
			ПараметрыОтчета.Организация);
	Иначе
		ТекстПоляХозрасчетныйОборотыОтветственный = ТекстПоляОтветственныйПоУмолчанию();
	КонецЕсли;

	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстПоляХозрасчетныйОборотыОтветственный",
		ТекстПоляХозрасчетныйОборотыОтветственный);
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстПоляОтветственныйПоУмолчанию",
		ТекстПоляОтветственныйПоУмолчанию());
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("БезНДС", Перечисления.СтавкиНДС.БезНДС);
	Запрос.УстановитьПараметр("СчетаВыручкиЕНВД", СчетаВыручкиЕНВД);  
	Запрос.УстановитьПараметр("СчетСебестоимостьПродажЕНВД", ПланыСчетов.Хозрасчетный.СебестоимостьПродажЕНВД);

	Запрос.УстановитьПараметр("НоменклатурнаяГруппаНоменклатура", НоменклатурнаяГруппаНоменклатура);
	Запрос.УстановитьПараметр("НоменклатурнаяГруппаСтавкаНДСНоменклатура", НоменклатурнаяГруппаСтавкаНДСНоменклатура);
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	Запрос.УстановитьПараметр("СтавкаНДС_0", Перечисления.СтавкиНДС.НДС0);
	Запрос.УстановитьПараметр("СтавкаНДС_10", Перечисления.СтавкиНДС.НДС10);
	Запрос.УстановитьПараметр("СтавкаНДС_110", Перечисления.СтавкиНДС.НДС10_110);
	Запрос.УстановитьПараметр("СтавкаНДС_118", Перечисления.СтавкиНДС.НДС18_118);
	Запрос.УстановитьПараметр("СтавкаНДС_120", Перечисления.СтавкиНДС.НДС20_120);
	Запрос.УстановитьПараметр("СтавкаНДС_122", Перечисления.СтавкиНДС.НДС22_122);
	Запрос.УстановитьПараметр("СтавкаНДС_105", Перечисления.СтавкиНДС.НДС5_105);
	Запрос.УстановитьПараметр("СтавкаНДС_107", Перечисления.СтавкиНДС.НДС7_107);
	Запрос.УстановитьПараметр("СтавкаНДС_18", Перечисления.СтавкиНДС.НДС18);
	Запрос.УстановитьПараметр("СтавкаНДС_20", Перечисления.СтавкиНДС.НДС20);
	Запрос.УстановитьПараметр("СтавкаНДС_22", Перечисления.СтавкиНДС.НДС22);
	Запрос.УстановитьПараметр("СтавкаНДС_5", Перечисления.СтавкиНДС.НДС5);
	Запрос.УстановитьПараметр("СтавкаНДС_7", Перечисления.СтавкиНДС.НДС7);
	Запрос.УстановитьПараметр("СчетаВыручки", СчетаВыручки);
	Запрос.УстановитьПараметр("СчетаКассы", СчетаКассы);
	Запрос.УстановитьПараметр("СчетаРасчетовСПокупателями", СчетаРасчетовСПокупателями);
	Запрос.УстановитьПараметр("СчетаСебестоимости", БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.СебестоимостьПродаж));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("СубконтоНТТ", СубконтоНТТ);
	Запрос.УстановитьПараметр("СчетаВыпускПродукции", СчетаВыпускПродукции);
	Запрос.УстановитьПараметр("ПустаяНоменклатура", Справочники.Номенклатура.ПустаяСсылка());
	
	Если Не ИспользоватьОборотнуюНоменклатуру Тогда
		Запрос.УстановитьПараметр("ПараметрНоменклатураНТТ", Справочники.Номенклатура.ПустаяСсылка());
	Иначе
		Запрос.УстановитьПараметр("ПараметрНоменклатураНТТ", Номенклатура);
	КонецЕсли;
	
	СчетУчетаНТТ = БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ);
	Запрос.УстановитьПараметр("СчетУчетаНТТ", СчетУчетаНТТ);
	
	СчетТорговойНаценкиНТТ = БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.ТорговаяНаценкаНТТ);
		
	Запрос.УстановитьПараметр("СчетТорговойНаценкиНТТ", СчетТорговойНаценкиНТТ);

	Запрос.УстановитьПараметр("СчетРасчетовПоНалогамИСборам", СчетРасчетовПоНалогамИСборам);
	Запрос.УстановитьПараметр("СчетАкцизов", СчетАкцизов);
	Запрос.УстановитьПараметр("СчетаРасходов", СчетаРасходов);
	Запрос.УстановитьПараметр("КорректировкаВыпускаПродукции", НСтр("ru='Корректировка выпуска продукции (услуг)'"));
	Запрос.УстановитьПараметр("ВыпускПродукции", НСтр("ru='Выпуск продукции'"));
	Запрос.УстановитьПараметр("РаботыИУслуги", НСтр("ru='Работы и услуги'"));
	Запрос.УстановитьПараметр("ПустоеЗначение", ПустоеЗначение());
	Запрос.УстановитьПараметр("ПотериОтБрака", НСтр("ru='Потери от брака'"));
	Запрос.УстановитьПараметр("ПустойСклад", Справочники.Склады.ПустаяСсылка());
	Запрос.УстановитьПараметр("ВедетсяСуммовойУчетПоСкладам",
		БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары));
	Запрос.УстановитьПараметр("СубконтоНоменклатураСклад", СубконтоНоменклатураСклад);
	Запрос.УстановитьПараметр("СубконтоНоменклатураСкладПартии", СубконтоНоменклатураСкладПартии);
	Запрос.УстановитьПараметр("СубконтоНоменклатураПартии", СубконтоНоменклатураПартии);
	Запрос.УстановитьПараметр("СчетаВзаиморасчетовСПоставщиками", СчетаВзаиморасчетовСПоставщиками);
	Запрос.УстановитьПараметр("СчетаУчетаПроизводства", СчетаУчетаПроизводства);
	Запрос.УстановитьПараметр("СубконтоНоменклатурныеГруппы", СубконтоНоменклатурныеГруппы);
	КонтрагентыДоговоры = Новый Массив;
	КонтрагентыДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	КонтрагентыДоговоры.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	Запрос.УстановитьПараметр("КонтрагентыДоговоры", КонтрагентыДоговоры);	
	Запрос.УстановитьПараметр("РозничныеПокупатели", НСтр("ru='Розничные покупатели'"));
	Запрос.УстановитьПараметр("РозничнаяПродажа", НСтр("ru='Розничная продажа'"));

	НоменклатураСклады = Новый Массив;
	НоменклатураСклады.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	НоменклатураСклады.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	Запрос.УстановитьПараметр("НоменклатураСклады", НоменклатураСклады);

	// Счета учета номенклатуры.
	СчетаУчетаНоменклатуры = СчетаУчетаТоваров();
	
	СчетаБезПартионногоУчетаБезСклада = Новый Массив(БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РезервыПодСнижениеСтоимостиМЦ));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаБезПартионногоУчетаБезСклада, СчетаУчетаНоменклатуры.СчетаБезПартионногоУчетаБезСклада);
	
	Запрос.УстановитьПараметр("СчетУчетаНоменклатурыСПартиями", СчетаУчетаНоменклатуры.СчетаСПартионнымУчетом);
	Запрос.УстановитьПараметр("СчетУчетаНоменклатурыБезПартии", СчетаУчетаНоменклатуры.СчетаБезПартионногоУчета);
	Запрос.УстановитьПараметр("СчетУчетаНоменклатурыСПартиямиБезСклада", СчетаУчетаНоменклатуры.СчетаСПартионнымУчетомБезСклада);
	Запрос.УстановитьПараметр("СчетУчетаНоменклатурыБезСкладаБезПартии", СчетаБезПартионногоУчетаБезСклада);
	
	// Счета учета торговой наценки.
	СчетаУчетаТорговойНаценки = СчетаУчетаТорговойНаценки();
	Запрос.УстановитьПараметр("СчетТорговойНаценкиСПартиями", СчетаУчетаТорговойНаценки.СчетаСПартионнымУчетом);
	Запрос.УстановитьПараметр("СчетТорговойНаценкиБезПартии", СчетаУчетаТорговойНаценки.СчетаБезПартионногоУчета);
	Запрос.УстановитьПараметр("СчетТорговойНаценкиБезСкладаСПартиями", СчетаУчетаТорговойНаценки.СчетаСПартионнымУчетомБезСклада);
	Запрос.УстановитьПараметр("СчетТорговойНаценкиБезСкладаБезПартии", СчетаУчетаТорговойНаценки.СчетаБезПартионногоУчетаБезСклада);
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", НоменклатурнаяГруппа);
	
	Если ПрошлыйПериод Тогда
		НачалоПериода = ДобавитьМесяц(ПараметрыОтчета.НачалоПериода, -12);
		КонецПериода = ПолучитьКонецПериодаЗаПрошлыйГод(ПараметрыОтчета);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Выполнить();

	Запрос.Текст = "
		|УНИЧТОЖИТЬ Себестоимость";
	
	Запрос.Выполнить();

	Возврат Запрос;
		
КонецФункции

Функция ПустоеЗначение() Экспорт

	Возврат НСтр("ru='<...>'");

КонецФункции

Функция РаботыИУслуги() Экспорт

	Возврат НСтр("ru='Работы и услуги'");

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СчетаУчетаТоваров()
	
	СпецоснасткаИСпецодеждаВЭксплуатации = БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.СпецоснасткаИСпецодеждаВЭксплуатации); //10.11

	СчетаИсключаемые = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(СпецоснасткаИСпецодеждаВЭксплуатации);
	СчетаИсключаемые.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку);            // 10.07
	СчетаИсключаемые.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ); // 41.12
	СчетаИсключаемые.Добавить(ПланыСчетов.Хозрасчетный.КорректировкаТоваровПрошлогоПериода);        // 41.К
	
	СчетаУчетаТоваров = БухгалтерскиеОтчеты.СчетаУчетаТоваров(СчетаИсключаемые);
	
	Возврат СчетаУчета(СчетаУчетаТоваров);
	
КонецФункции

Функция СчетаУчетаТорговойНаценки()
	
	СчетаУчета = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);
	
	Возврат СчетаУчета(СчетаУчета);
	
КонецФункции

Функция СчетаУчета(СчетаУчета)
		
	СчетаСПартионнымУчетом            = Новый Массив;
	СчетаБезПартионногоУчета          = Новый Массив;
	СчетаСПартионнымУчетомБезСклада   = Новый Массив;
	СчетаБезПартионногоУчетаБезСклада = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.Ссылка КАК Счет,
	|	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто
	|ПОМЕСТИТЬ Счета
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.Ссылка В(&СчетаУчета)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Счета.Счет КАК Счет,
	|	Счета.ВидСубконто КАК ВидСубконто,
	|	ИСТИНА КАК НаСчетеВедетсяПартионныйУчет
	|ПОМЕСТИТЬ СчетаСПартионнымУчетом
	|ИЗ
	|	Счета КАК Счета
	|ГДЕ
	|	Счета.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии)
	|
	|СГРУППИРОВАТЬ ПО
	|	Счета.ВидСубконто,
	|	Счета.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Счета.Счет КАК Счет,
	|	Счета.ВидСубконто КАК ВидСубконто,
	|	ИСТИНА КАК ВедетсяУчетПоСкладам
	|ПОМЕСТИТЬ СчетаСоСкладскимУчетом
	|ИЗ
	|	Счета КАК Счета
	|ГДЕ
	|	Счета.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады)
	|
	|СГРУППИРОВАТЬ ПО
	|	Счета.ВидСубконто,
	|	Счета.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Счета.Счет КАК Счет,
	|	ЕСТЬNULL(СчетаСПартионнымУчетом.НаСчетеВедетсяПартионныйУчет, ЛОЖЬ) КАК НаСчетеВедетсяПартионныйУчет,
	|	ЕСТЬNULL(СчетаСоСкладскимУчетом.ВедетсяУчетПоСкладам, ЛОЖЬ) КАК ВедетсяУчетПоСкладам
	|ИЗ
	|	Счета КАК Счета
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаСПартионнымУчетом КАК СчетаСПартионнымУчетом
	|		ПО Счета.Счет = СчетаСПартионнымУчетом.Счет
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаСоСкладскимУчетом КАК СчетаСоСкладскимУчетом
	|		ПО Счета.Счет = СчетаСоСкладскимУчетом.Счет";
	
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НаСчетеВедетсяПартионныйУчет Тогда
			
			Если Выборка.ВедетсяУчетПоСкладам Тогда
				СчетаСПартионнымУчетом.Добавить(Выборка.Счет);
			Иначе
				СчетаСПартионнымУчетомБезСклада.Добавить(Выборка.Счет);
			КонецЕсли;
		Иначе
			Если Выборка.ВедетсяУчетПоСкладам Тогда
				СчетаБезПартионногоУчета.Добавить(Выборка.Счет);
			Иначе
				СчетаБезПартионногоУчетаБезСклада.Добавить(Выборка.Счет);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("СчетаСПартионнымУчетом",            СчетаСПартионнымУчетом);
	Результат.Вставить("СчетаБезПартионногоУчета",          СчетаБезПартионногоУчета);
	Результат.Вставить("СчетаСПартионнымУчетомБезСклада",   СчетаСПартионнымУчетомБезСклада);
	Результат.Вставить("СчетаБезПартионногоУчетаБезСклада", СчетаБезПартионногоУчетаБезСклада);
	
	Возврат Результат;

КонецФункции

Функция ПолучитьКонецПериодаЗаПрошлыйГод(ПараметрыОтчета)
	
	Если КонецДня(ПараметрыОтчета.КонецПериода) = КонецМесяца(ПараметрыОтчета.КонецПериода) Тогда
		КонецПериода = КонецМесяца(ДобавитьМесяц(ПараметрыОтчета.КонецПериода, -12));
	Иначе
		КонецПериода = КонецДня(ДобавитьМесяц(ПараметрыОтчета.КонецПериода, -12));
	КонецЕсли;
	
	Возврат КонецПериода;
	
КонецФункции

Функция ФормулаСравнения(ВидСравнения) Экспорт
	
	ФормулаСравнения = "%1 = %2";
	
	Если ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
		ФормулаСравнения = "%1 В (%2)";
	ИначеЕсли ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии 
		Или ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда
		ФормулаСравнения = "%1 В ИЕРАРХИИ (%2)"; 
	ИначеЕсли ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке Тогда
		ФормулаСравнения = "НЕ %1 В (%2)";
	ИначеЕсли ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии
		Или ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
		ФормулаСравнения = "НЕ %1 В ИЕРАРХИИ (%2)";
	ИначеЕсли ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно Тогда
		ФормулаСравнения = "%1 <> %2";
	ИначеЕсли  ВидСравнения = ВидСравненияКомпоновкиДанных.Больше Тогда
		ФормулаСравнения = "%1 > %2";
	ИначеЕсли ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно Тогда
		ФормулаСравнения = "%1 >= %2";
	ИначеЕсли ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше Тогда
		ФормулаСравнения = "%1 < %2";
	ИначеЕсли ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
		ФормулаСравнения = "%1 <= %2";
	ИначеЕсли ВидСравнения = ВидСравненияКомпоновкиДанных.Подобно Тогда
		ФормулаСравнения = "%1 ПОДОБНО %2";
	КонецЕсли;
	
	Возврат ФормулаСравнения;
	
КонецФункции

Процедура УстановитьОтборыАкциз(Контекст, ПараметрыОтбора)
	
	Если ЗначениеЗаполнено(ПараметрыОтбора.Организация)
		И Не ПустаяСтрока(ПараметрыОтбора.ОрганизацияФормулаСравнения) Тогда
		
		Если ПараметрыОтбора.ВключатьОбособленныеПодразделения Тогда 
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация.ГоловнаяОрганизация", "&Организация");
		Иначе
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация", "&Организация");
		КонецЕсли;

		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтбора.Подразделение) 
		И Не ПустаяСтрока(ПараметрыОтбора.ПодразделениеФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(Подразделение, &ПустоеПодразделение)", "&Подразделение");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;

	Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа) 
		И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "Субконто1", "&НоменклатурнаяГруппа");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура)
		И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "Субконто2", "&Номенклатура");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтборыПродажи(Контекст, ПараметрыОтбора)
	
	Если ЗначениеЗаполнено(ПараметрыОтбора.Организация)
		И Не ПустаяСтрока(ПараметрыОтбора.ОрганизацияФормулаСравнения) Тогда
		
		Если ПараметрыОтбора.ВключатьОбособленныеПодразделения Тогда 
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация.ГоловнаяОрганизация", "&Организация");
		Иначе
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация", "&Организация");
		КонецЕсли;

		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтбора.Подразделение)
		И Не ПустаяСтрока(ПараметрыОтбора.ПодразделениеФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(Подразделение, &ПустоеПодразделение)", "&Подразделение");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;

	ВыражениеДляПроверки = Новый ВыражениеСхемыЗапроса("&НоменклатурнаяГруппаСтавкаНДСНоменклатура");
	
	Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа)
		И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "Субконто1", "&НоменклатурнаяГруппа");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;

	Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура)
		И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
		Если Контекст.Таблица.Параметры[4].Выражение = ВыражениеДляПроверки Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "Субконто3", "&Номенклатура");
		Иначе
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "Субконто2", "&Номенклатура");
		КонецЕсли;
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;
		
КонецПроцедуры

Процедура УстановитьОтборыСебестоимость(Контекст, ПараметрыОтбора)

	Если ЗначениеЗаполнено(ПараметрыОтбора.Организация)
		И Не ПустаяСтрока(ПараметрыОтбора.ОрганизацияФормулаСравнения) Тогда
		
		Если ПараметрыОтбора.ВключатьОбособленныеПодразделения Тогда 
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация.ГоловнаяОрганизация", "&Организация");
		Иначе
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация", "&Организация");
		КонецЕсли;
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);

	КонецЕсли;

	Если ЗначениеЗаполнено(ПараметрыОтбора.Подразделение)
		И Не ПустаяСтрока(ПараметрыОтбора.ПодразделениеФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(ПодразделениеДт, &ПустоеПодразделение)", "&Подразделение");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;
	
	Если Контекст.Таблица.Псевдоним = "ДанныеКорректировки" Тогда
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа)
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "СубконтоДт1", "&НоменклатурнаяГруппа");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;

	ИначеЕсли Контекст.Таблица.Псевдоним = "Корректировка" Тогда
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "СубконтоДт1", "&Номенклатура");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
	ИначеЕсли Контекст.Таблица.Псевдоним = "СебестоимостьПроизводства" Тогда
	
		Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "СубконтоДт1", "&НоменклатурнаяГруппа");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "СубконтоКт3", "&Номенклатура");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
	ИначеЕсли Контекст.Таблица.Псевдоним = "СебестоимостьНоменклатурыНТТ" Тогда
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "СубконтоДт1", "&НоменклатурнаяГруппа");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "&ПараметрНоменклатураНТТ", "&Номенклатура");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
	ИначеЕсли Контекст.Таблица.Псевдоним = "ВыпускПродукции" Тогда
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "СубконтоДт1", "&НоменклатурнаяГруппа");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "&ВыпускПродукции", "&Номенклатура");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "СубконтоДт1", "&НоменклатурнаяГруппа");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "СубконтоКт1", "&Номенклатура");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтборыТорговаяНаценка(Контекст, ПараметрыОтбора)
	
	Если ЗначениеЗаполнено(ПараметрыОтбора.Организация)
		И Не ПустаяСтрока(ПараметрыОтбора.ОрганизацияФормулаСравнения) Тогда
		
		Если ПараметрыОтбора.ВключатьОбособленныеПодразделения Тогда 
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация.ГоловнаяОрганизация", "&Организация");
		Иначе
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация", "&Организация");
		КонецЕсли;
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		
	КонецЕсли;

	Если ЗначениеЗаполнено(ПараметрыОтбора.Подразделение) 
			И Не ПустаяСтрока(ПараметрыОтбора.ПодразделениеФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(ПодразделениеДт, &ПустоеПодразделение)", "&Подразделение");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;

	Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "СубконтоДт1", "&НоменклатурнаяГруппа");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;

	Если Контекст.Таблица.Псевдоним = "ТорговаяНаценкаНТТ" Тогда
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "&ПараметрНоменклатураНТТ", "&Номенклатура");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.Номенклатура) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатураФормулаСравнения) Тогда
			Условие = СтрШаблон(ПараметрыОтбора.НоменклатураФормулаСравнения, "СубконтоКт1", "&Номенклатура");
			СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтборыВыручкаИСебестоимость(Контекст, ПараметрыОтбора)

	Если ЗначениеЗаполнено(ПараметрыОтбора.Организация)
		И Не ПустаяСтрока(ПараметрыОтбора.ОрганизацияФормулаСравнения) Тогда
		
		Если ПараметрыОтбора.ВключатьОбособленныеПодразделения Тогда 
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация.ГоловнаяОрганизация", "&Организация");
		Иначе
			Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация", "&Организация");
		КонецЕсли;
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
		
	КонецЕсли;

	Если ЗначениеЗаполнено(ПараметрыОтбора.Подразделение) 
			И Не ПустаяСтрока(ПараметрыОтбора.ПодразделениеФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(ПодразделениеДт, &ПустоеПодразделение)", "&Подразделение");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;

	Если ЗначениеЗаполнено(ПараметрыОтбора.НоменклатурнаяГруппа) 
			И Не ПустаяСтрока(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения) Тогда
		Условие = СтрШаблон(ПараметрыОтбора.НоменклатурнаяГруппаФормулаСравнения, "СубконтоКт1", "&НоменклатурнаяГруппа");
		СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВыбираемоеПолеНоменклатураНТТ(ОператораТаблицы, Контекст, ИспользоватьОборотнуюНоменклатуру)
	
	ИндексПоляНоменклатура = 3;
	
	Если ИспользоватьОборотнуюНоменклатуру Тогда
		Выражение = Новый ВыражениеСхемыЗапроса(СтрШаблон("%1.СубконтоКт2", Контекст.Таблица.Псевдоним));
	Иначе
		Выражение = Новый ВыражениеСхемыЗапроса("ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)");
	КонецЕсли;
	
	ОператораТаблицы.ВыбираемыеПоля.Установить(ИндексПоляНоменклатура, Выражение);
	
КонецПроцедуры

#КонецОбласти