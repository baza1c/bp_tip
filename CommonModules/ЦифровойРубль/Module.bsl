#Область ПрограммныйИнтерфейс

Функция РазобратьВыпискуБанкаПоЦифровомуРублю(ДвоичныеДанныеФайла, РеквизитыОрганизации) Экспорт
	
	// По схеме cbdc.011.001.04_ReturnOrganisationWalletInfo_2024.03.xsd
	
	ДанныеОрганизации = Новый Структура("ИНН, Наименование, НаименованиеМеждународное");
	ДанныеБанка       = Новый Структура("БИК, SWIFT, Наименование, НаименованиеМеждународное");
	
	ДанныеОрганизации.ИНН          = РеквизитыОрганизации.ИННОрганизации;
	ДанныеОрганизации.Наименование = РеквизитыОрганизации.Наименование;
	
	ДанныеБанка.БИК                = БИКПлатформыЦифровогоРубля();
	ДанныеБанка.Наименование       = НаименованиеПлатформыЦифровогоРубля();
	
	ОбъектXML = Новый ЧтениеXML;
	
	Попытка
		ОбъектXML.ОткрытьПоток(ДвоичныеДанныеФайла.ОткрытьПотокДляЧтения());
		Statement = ФабрикаXDTO.ПрочитатьXML(ОбъектXML);
	Исключение
		РазборВыпискиВызыватьИсключение();
	КонецПопытки;
	
	ОбъектXML.Закрыть();
	
	RtrWlltInf = ЗначениеСвойстваXDTO(Statement, "RtrWlltInf");
	Если RtrWlltInf = Неопределено Или ЗначениеСвойстваXDTO(RtrWlltInf, "Rpt") = Неопределено Тогда
		РазборВыпискиВызыватьИсключение();
	КонецЕсли;
	
	// Детальные отчеты по Кошельку
	Если ТипЗнч(RtrWlltInf.Rpt) = Тип("ОбъектXDTO") Тогда // Report
		Выписки = Новый Массив;
		Выписки.Добавить(RtrWlltInf.Rpt);
	Иначе
		Выписки = RtrWlltInf.Rpt;
	КонецЕсли;
	
	ИдентификаторДокумента = ЗначениеСвойстваXDTO(RtrWlltInf, "MsgHdr.MsgId");
	ДатаФормирования       = ЗначениеСвойстваXDTO(RtrWlltInf, "MsgHdr.CreDt");
	
	ТаблицаВыписок = НовыйТаблицаВыписок();
	
	Для Каждого Выписка Из Выписки Цикл
		СтрокаВыписки = ТаблицаВыписок.Добавить();
		СтрокаВыписки.Организация = ДанныеОрганизации;
		СтрокаВыписки.Банк        = ДанныеБанка;
		ЗаполнитьШапку(СтрокаВыписки, Выписка, ДатаФормирования, ИдентификаторДокумента, РеквизитыОрганизации);
		
		// Информация по операциям
		Операции = ЗначениеСвойстваXDTO(Выписка, "OprtnsInf"); // OperationsInformation
		Если Операции = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаОпераций = НовыйТаблицаОпераций();
		Если ТипЗнч(Операции) = Тип("ОбъектXDTO") Тогда
			ПрочитатьОперациюВыписки(СтрокаВыписки, Операции, ТаблицаОпераций, РеквизитыОрганизации);
		Иначе
			Для Каждого Операция Из Операции Цикл
				ПрочитатьОперациюВыписки(СтрокаВыписки, Операция, ТаблицаОпераций, РеквизитыОрганизации);
			КонецЦикла;
		КонецЕсли;
		
		СтрокаВыписки.Операции = ТаблицаОпераций;
	КонецЦикла;
	
	ПрочитанныеДанныеСервиса = ДанныеВыписокБанкаВКоллекцию(ТаблицаВыписок, РеквизитыОрганизации);
	
	Возврат ПрочитанныеДанныеСервиса;
	
КонецФункции

Процедура ВыгрузитьПорученияПоЦР(ПотокВыгрузки, ПлатежныеДокументы, ИдентификаторСчета) Экспорт
	
	Данные = СформироватьПлатежныеДанные(ПлатежныеДокументы, ИдентификаторСчета);
	
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Unix, Символы.Таб);
	ДанныеJSON = ОбщегоНазначенияБП.ЗначениеВСтрокуJSON(Данные, ПараметрыЗаписиJSON);
	
	ПотокВыгрузки.ЗаписатьСтроку(ДанныеJSON);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеВыписокБанкаВКоллекцию(ТаблицаВыписок, РеквизитыОрганизации) Экспорт
	
	ДанныеВыписки = ОбменСБанкомВФормате1С.НовыйДанныеИзБанка();
	Протокол = ОбменСБанкомВФормате1С.НовыйПротоколЧтенияФайла();
	
	Для Каждого СтрокаВыписки Из ТаблицаВыписок Цикл
		Индекс = 0;
		
		ДанныеВыписки.Заголовок.ДатаСоздания = СтрокаВыписки.ДатаФормирования;
		ДанныеВыписки.Заголовок.Отправитель  = НаименованиеПлатформыЦифровогоРубля();
		ДанныеВыписки.Заголовок.Кодировка    = "Windows";
		
		Если СтрокаВыписки.НомерСчета <> РеквизитыОрганизации.НомерСчетаЦР Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный счет ЦР в выписке %1'"), СтрокаВыписки.НомерСчета);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеВыписки.Условия.ДатаНачала) Тогда
			ДанныеВыписки.Условия.ДатаНачала = Мин(СтрокаВыписки.НачалоПериода, ДанныеВыписки.Условия.ДатаКонца);
		Иначе
			ДанныеВыписки.Условия.ДатаНачала = СтрокаВыписки.НачалоПериода;
		КонецЕсли;
		
		ДанныеВыписки.Условия.ДатаКонца  = Макс(СтрокаВыписки.КонецПериода, ДанныеВыписки.Условия.ДатаКонца);
		ДанныеВыписки.Условия.РасчСчет.Добавить(СтрокаВыписки.НомерСчета);
		
		Если ЗначениеЗаполнено(СтрокаВыписки.НачальныйОстаток) Или ЗначениеЗаполнено(СтрокаВыписки.КонечныйОстаток)
			Или ЗначениеЗаполнено(СтрокаВыписки.ОборотВходящихПлатежей)
			Или ЗначениеЗаполнено(СтрокаВыписки.ОборотИсходящихПлатежей) Тогда
			СтрокаОстатка = ДанныеВыписки.Остатки.Добавить();
			СтрокаОстатка.РасчСчет   = СтрокаВыписки.НомерСчета;
			СтрокаОстатка.ДатаНачала = СтрокаВыписки.НачалоПериода;
			СтрокаОстатка.ДатаКонца  = СтрокаВыписки.КонецПериода;
			
			// Остатки
			СтрокаОстатка.НачальныйОстаток = Формат(СтрокаВыписки.НачальныйОстаток, "ЧРД=.; ЧГ=");
			СтрокаОстатка.КонечныйОстаток  = Формат(СтрокаВыписки.КонечныйОстаток,  "ЧРД=.; ЧГ=");
			
			// Обороты
			СтрокаОстатка.ВсегоПоступило   = Формат(СтрокаВыписки.ОборотВходящихПлатежей,  "ЧРД=.; ЧГ=");
			СтрокаОстатка.ВсегоСписано     = Формат(СтрокаВыписки.ОборотИсходящихПлатежей, "ЧРД=.; ЧГ=");
		КонецЕсли;
		
		Для Каждого НовСтрока Из СтрокаВыписки.Операции Цикл
			ОперацияВыписки = ДанныеВыписки.Документы.Добавить();
			
			Если НовСтрока.НаправлениеПлатежа = "2" Тогда
				Сторона = "Плательщик";
				ПротивоположнаяСтрона = "Получатель";
				
				ОперацияВыписки.ДатаПоступило = НовСтрока.ДатаПоступило;
			Иначе
				Сторона = "Получатель";
				ПротивоположнаяСтрона = "Плательщик";
				
				ОперацияВыписки.ДатаСписано = НовСтрока.ДатаСписано;
			КонецЕсли;
			
			ОперацияВыписки.Номер       = НовСтрока.НомерДокумента;
			ОперацияВыписки.Дата        = НовСтрока.ДатаДокумента;
			ОперацияВыписки.Сумма       = НовСтрока.СуммаДокумента;
			ОперацияВыписки.Код         = НовСтрока.УникальныйИдентификаторПлатежа;
			ОперацияВыписки.ВидОплаты   = НовСтрока.ВидОперации;
			ОперацияВыписки.Очередность = НовСтрока.Очередность;
			
			РеквизитыУчастников = ОбменСБанкомВФормате1С.ЭлементыРеквизитовУчастника();
			Для Каждого РеквизитУчастника Из РеквизитыУчастников Цикл
				ОперацияВыписки.ПлательщикРеквизиты.Вставить(РеквизитУчастника, "");
				ОперацияВыписки.ПолучательРеквизиты.Вставить(РеквизитУчастника, "");
			КонецЦикла;
			
			Если НовСтрока.ВидОперацииЦР    = БанковскиеПравила.ОперацияDCBU() // Пополнение ЦР. Перевод между счетами (банковский счет -> ЦР)
				Или НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияDCSE() // Вывод средств. Перевод между счетами (банковский счет <- ЦР)
				Или НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияB2B()
				Или НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияC2B()
				Или НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияB2C() Тогда
				
				ОперацияВыписки.ВидОперацииЦР     = НовСтрока.ВидОперацииЦР;
				ОперацияВыписки.НазначениеПлатежа = НовСтрока.НазначениеПлатежа;
				
			Иначе // пока не обслуживаем
				
				ОперацияВыписки.ВидОперацииЦР = ВРег(НовСтрока.ВидОперацииЦР);
				Если НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияDCRF() Тогда
					ОперацияВыписки.НазначениеПлатежа = НСтр("ru = '#Возврат цифровой валюты при неуспешной операции#'");
				ИначеЕсли НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияOOPN() Тогда
					ОперацияВыписки.НазначениеПлатежа = НСтр("ru = '#Другие операции#'");
				ИначеЕсли НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияOPTR() Тогда
					ОперацияВыписки.НазначениеПлатежа = НовСтрока.НазначениеПлатежа;
				КонецЕсли;
				
				ОписаниеПроблемы = СтрШаблон(
					НСтр("ru = 'Неподдерживаемый вид %1 в операции %2'"), НовСтрока.ВидОперацииЦР, НовСтрока.НомерДокумента);
				Запись = Протокол.Добавить();
				Запись.НомерСтроки = Индекс;
				Запись.КодСобытия  = ОписаниеПроблемы;
				
			КонецЕсли;
			
			ОперацияВыписки.ПлательщикСчет = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовСтрока.Плательщик, "РасчСчет");
			ОперацияВыписки.ПолучательСчет = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовСтрока.Получатель, "РасчСчет");
			
			ОперацияВыписки.ПлательщикИНН  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовСтрока.Плательщик, "ИНН", "");
			ОперацияВыписки.ПолучательИНН  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовСтрока.Получатель, "ИНН", "");
			
			Если НовСтрока.Плательщик.Свойство("Банк") Тогда
				ЗаполнитьЗначенияСвойств(ОперацияВыписки.ПлательщикРеквизиты, НовСтрока.Плательщик,, "Банк");
				БанкПлательщика = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовСтрока.Плательщик, "Банк");
				Если ЗначениеЗаполнено(БанкПлательщика) Тогда
					ОперацияВыписки.ПлательщикРеквизиты.БИК     = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						БанкПлательщика, "БИК", "");
					ОперацияВыписки.ПлательщикРеквизиты.Банк    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						БанкПлательщика, "Наименование", "");
					ОперацияВыписки.ПлательщикРеквизиты.КорСчет = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						БанкПлательщика, "КоррСчет", "");
				КонецЕсли;
			Иначе
				ЗаполнитьЗначенияСвойств(ОперацияВыписки.ПлательщикРеквизиты, НовСтрока.Плательщик);
			КонецЕсли;
			
			Если НовСтрока.Получатель.Свойство("Банк") Тогда
				ЗаполнитьЗначенияСвойств(ОперацияВыписки.ПолучательРеквизиты, НовСтрока.Получатель,, "Банк");
				БанкПолучателя = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовСтрока.Получатель, "Банк");
				Если ЗначениеЗаполнено(БанкПолучателя) Тогда
					ОперацияВыписки.ПолучательРеквизиты.БИК     = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						БанкПолучателя, "БИК", "");
					ОперацияВыписки.ПолучательРеквизиты.Банк    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						БанкПолучателя, "Наименование", "");
					ОперацияВыписки.ПолучательРеквизиты.КорСчет = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
						БанкПолучателя, "КоррСчет", "");
				КонецЕсли;
			Иначе
				ЗаполнитьЗначенияСвойств(ОперацияВыписки.ПолучательРеквизиты, НовСтрока.Получатель);
			КонецЕсли;
			
			ОбменСБанкомВФормате1С.ИсправитьОшибкиУчастникаСчет(ОперацияВыписки, "Плательщик");
			ОбменСБанкомВФормате1С.ИсправитьОшибкиУчастникаСчет(ОперацияВыписки, "Получатель");
			Для Каждого Колонка Из ДанныеВыписки.Документы.Колонки Цикл
				ОбменСБанкомВФормате1С.ОчиститьТолькоНулиВСтроке(ОперацияВыписки[Колонка.Имя]);
			КонецЦикла;
			
			ОперацияВыписки.Идентификатор = Формат(Индекс, "ЧН=0; ЧГ=;");
			Индекс = Индекс + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	ПрочитанныеДанныеСервиса = Новый Структура;
	ПрочитанныеДанныеСервиса.Вставить("ДанныеИзБанка", ДанныеВыписки);
	ПрочитанныеДанныеСервиса.Вставить("Протокол",      Протокол);
	
	Возврат ПрочитанныеДанныеСервиса;
	
КонецФункции

Функция ТаблицаВыписокБанкаВКоллекцию(ТаблицаВыписок, РеквизитыОрганизации) Экспорт
	
	МассивСчетов = Новый Массив;
	ДеревоДанных = ДеревоЭлектронногоДокументаБЭД.ДеревоЭлектронногоДокумента("ОбменСБанками.ВыпискаБанка");
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДанных, ТаблицаВыписок, "Выписки");
	ПрочитанныеДанныеСервиса = ЭлектронноеВзаимодействиеБПВызовСервера.ПрочитанныеДанныеВыпискиПоЦРИзДерева(
		ДеревоДанных, РеквизитыОрганизации.Организация, МассивСчетов);
	
	Возврат ПрочитанныеДанныеСервиса;
	
КонецФункции

Функция ПериодДоступенПлатежОбменСЦифровымКошельком() Экспорт
	
	Период = '20250101';
	
	Возврат Период;
	
КонецФункции

Функция ДоступенПлатежОбменСЦифровымКошельком(Период) Экспорт
	
	Результат = ЗначениеЗаполнено(Период) И Период >= ПериодДоступенПлатежОбменСЦифровымКошельком();
	
	Возврат Результат;
	
КонецФункции

Функция БИКПлатформыЦифровогоРубля() Экспорт
	
	БИК = "044595002";
	
	Возврат БИК;
	
КонецФункции

Функция НомерСчетаПлатформыЦифровогоРубля() Экспорт
	
	Счет = "30000810945950000002";
	
	Возврат Счет;
	
КонецФункции

Функция НаименованиеПлатформыЦифровогоРубля() Экспорт
	
	Наименование = НСтр("ru = 'Оператор платформы цифрового рубля'");
	
	Возврат Наименование;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПроцедурыИФункции

#Область ИсторияОперацийСлужебная

Процедура ЗаполнитьШапку(СтрокаВыписки, Выписка, ДатаФормирования, ИдДокумента, РеквизитыОрганизации)
	
	СтрокаВыписки.ДатаФормирования       = ДатаФормирования;
	СтрокаВыписки.ИдентификаторДокумента = ИдДокумента;
	
	СтрокаВыписки.НомерСчета = ЗначениеСвойстваXDTO(Выписка, "WlltPtyInf.Id");
	СтрокаВыписки.НомерСчетаЦифровой = РеквизитыОрганизации.НомерСчета;
	Если СтрокаВыписки.НомерСчета <> РеквизитыОрганизации.НомерСчетаЦР Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный счет ЦР в выписке %1'"), СтрокаВыписки.НомерСчета);
	КонецЕсли;
	
	DCAcctNb = ЗначениеСвойстваXDTO(Выписка, "WlltPtyInf.DCAcctNb"); // DCAccountNumber
	Если DCAcctNb <> Неопределено Тогда
		СтрокаВыписки.НомерСчетаЦифровой = DCAcctNb.Id; // 20-значный
	КонецЕсли;
	
	Prd = ЗначениеСвойстваXDTO(Выписка, "Prd");
	Если Prd <> Неопределено Тогда
		СтрокаВыписки.НачалоПериода = XMLЗначение(Тип("Дата"), ЗначениеСвойстваXDTO(Prd, "FrDtTm", ТекущаяДатаСеанса()));
		СтрокаВыписки.КонецПериода  = XMLЗначение(Тип("Дата"), ЗначениеСвойстваXDTO(Prd, "ToDtTm", ТекущаяДатаСеанса()));
	КонецЕсли;
	
	// Остатки
	OpngBal = ЗначениеСвойстваXDTO(Выписка, "OpngBal");
	Если OpngBal <> Неопределено Тогда
		НачальныйОстатокПоследовательность = ЗначениеСвойстваXDTO(OpngBal, "TtlAmt");
		Если ТипЗнч(НачальныйОстатокПоследовательность) = Тип("ОбъектXDTO") Тогда
			НачальныйОстатокСтрокой = "";
			НачальныйОстатокСтрокойПоследовательность = НачальныйОстатокПоследовательность.Последовательность();
			Если НачальныйОстатокСтрокойПоследовательность.Количество() > 0 Тогда
				НачальныйОстатокСтрокой = НачальныйОстатокСтрокойПоследовательность.ПолучитьТекст(0);
			КонецЕсли;
			
			СтрокаВыписки.НачальныйОстаток = XMLЗначение(Тип("Число"), НачальныйОстатокСтрокой);
		КонецЕсли;
	КонецЕсли;
	
	OutgngBal = ЗначениеСвойстваXDTO(Выписка, "OutgngBal");
	Если OutgngBal <> Неопределено Тогда
		КонечныйОстатокПоследовательность = ЗначениеСвойстваXDTO(OutgngBal, "TtlAmt");
		Если ТипЗнч(КонечныйОстатокПоследовательность) = Тип("ОбъектXDTO") Тогда
			КонечныйОстатокСтрокой = "";
			КонечныйОстатокСтрокойПоследовательность = КонечныйОстатокПоследовательность.Последовательность();
			Если КонечныйОстатокСтрокойПоследовательность.Количество() > 0 Тогда
				КонечныйОстатокСтрокой = КонечныйОстатокСтрокойПоследовательность.ПолучитьТекст(0);
			КонецЕсли;
			
			СтрокаВыписки.КонечныйОстаток = XMLЗначение(Тип("Число"), КонечныйОстатокСтрокой);
		КонецЕсли;
	КонецЕсли;
	
	// Обороты
	TtlAmtCdt = ЗначениеСвойстваXDTO(Выписка, "TtlAmtCdt");
	Если TtlAmtCdt <> Неопределено Тогда
		ОборотВходящихПлатежейПоследовательность = ЗначениеСвойстваXDTO(TtlAmtCdt, "TtlAmt");
		Если ТипЗнч(ОборотВходящихПлатежейПоследовательность) = Тип("ОбъектXDTO") Тогда
			ОборотВходящихПлатежейСтрокой = "";
			ОборотВходящихПлатежейСтрокойПоследовательность = ОборотВходящихПлатежейПоследовательность.Последовательность();
			Если ОборотВходящихПлатежейСтрокойПоследовательность.Количество() > 0 Тогда
				ОборотВходящихПлатежейСтрокой = ОборотВходящихПлатежейСтрокойПоследовательность.ПолучитьТекст(0);
			КонецЕсли;
			
			СтрокаВыписки.ОборотВходящихПлатежей = XMLЗначение(Тип("Число"), ОборотВходящихПлатежейСтрокой);
		КонецЕсли;
	КонецЕсли;
	
	TtlAmtDbt = ЗначениеСвойстваXDTO(Выписка, "TtlAmtDbt");
	Если TtlAmtDbt <> Неопределено Тогда
		ОборотИсходящихПлатежейПоследовательность = ЗначениеСвойстваXDTO(TtlAmtDbt, "TtlAmt");
		Если ТипЗнч(ОборотИсходящихПлатежейПоследовательность) = Тип("ОбъектXDTO") Тогда
			ОборотИсходящихПлатежейСтрокой = "";
			ОборотИсходящихПлатежейСтрокойПоследовательность = ОборотИсходящихПлатежейПоследовательность.Последовательность();
			Если ОборотИсходящихПлатежейСтрокойПоследовательность.Количество() > 0 Тогда
				ОборотИсходящихПлатежейСтрокой = ОборотИсходящихПлатежейСтрокойПоследовательность.ПолучитьТекст(0);
			КонецЕсли;
			
			СтрокаВыписки.ОборотИсходящихПлатежей = XMLЗначение(Тип("Число"), ОборотИсходящихПлатежейСтрокой);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьОперациюВыписки(СтрокаВыписки, Операция, ТаблицаОпераций, РеквизитыОрганизации)
	
	НовСтрока = ТаблицаОпераций.Добавить();
	НовСтрока.ВидОперацииЦР = ВРег(ЗначениеСвойстваXDTO(Операция, "Tp", БанковскиеПравила.ОперацияOOPN()));
	
	СуммаПоследовательность = ЗначениеСвойстваXDTO(Операция, "Amt.TtlAmt");
	Если ТипЗнч(СуммаПоследовательность) = Тип("ОбъектXDTO") Тогда
		СуммаСтрокой = "";
		СуммаСтрокойПоследовательность = СуммаПоследовательность.Последовательность();
		Если СуммаСтрокойПоследовательность.Количество() > 0 Тогда
			СуммаСтрокой = СуммаСтрокойПоследовательность.ПолучитьТекст(0);
		КонецЕсли;
		НовСтрока.СуммаДокумента = XMLЗначение(Тип("Число"), СуммаСтрокой);
	КонецЕсли;
	
	КредитДебет = ЗначениеСвойстваXDTO(Операция, "CdtDbtInd", "DBIT");
	НовСтрока.НаправлениеПлатежа = ?(КредитДебет = "DBIT", "1", "2");
	
	НовСтрока.НомерДокумента = ЗначениеСвойстваXDTO(Операция, "OprId");
	НовСтрока.ДатаОперации  = XMLЗначение(Тип("Дата"), ЗначениеСвойстваXDTO(Операция, "SttlmDtTm"));
	НовСтрока.ДатаДокумента = НовСтрока.ДатаОперации;
	НовСтрока.ВидОперации   = "01";
	НовСтрока.Очередность   = Число(УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаПоУмолчанию());
	
	ДебитКредит = ЗначениеСвойстваXDTO(Операция, "CdtDbtInd");
	
	Если ДебитКредит = "CRDT" Тогда
		Сторона = "Плательщик";
		ПротивоположнаяСтрона = "Получатель";
		
		НовСтрока.ДатаПоступило = НовСтрока.ДатаОперации;
	Иначе
		Сторона = "Получатель";
		ПротивоположнаяСтрона = "Плательщик";
		
		НовСтрока.ДатаСписано = НовСтрока.ДатаОперации;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыОрганизации.ИННОрганизации) Тогда
		НовСтрока[ПротивоположнаяСтрона] = Новый Структура;
		НовСтрока[ПротивоположнаяСтрона].Вставить("ИНН",                РеквизитыОрганизации.ИННОрганизации);
		НовСтрока[ПротивоположнаяСтрона].Вставить("РасчСчет",           СтрокаВыписки.НомерСчета);
		НовСтрока[ПротивоположнаяСтрона].Вставить("НомерСчетаЦифровой", СтрокаВыписки.НомерСчетаЦифровой);
		НовСтрока[ПротивоположнаяСтрона].Вставить("Банк",               Новый Структура);
		НовСтрока[ПротивоположнаяСтрона].Банк.Вставить("БИК",           БИКПлатформыЦифровогоРубля());
		НовСтрока[ПротивоположнаяСтрона].Банк.Вставить("КоррСчет",      НомерСчетаПлатформыЦифровогоРубля());
		НовСтрока[ПротивоположнаяСтрона].Банк.Вставить("Наименование",  НаименованиеПлатформыЦифровогоРубля());
	Иначе
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'У организации %1 не задан ИНН. Укажите его в карточке организации.'"), РеквизитыОрганизации.Наименование);
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеПроблемы);
		ВызватьИсключение ОписаниеПроблемы;
	КонецЕсли;
	
	ПрочитатьДанныеПоВидуОперации(Операция, НовСтрока, Сторона, РеквизитыОрганизации);
	
КонецПроцедуры

Процедура ПрочитатьДанныеПоВидуОперации(Операция, НовСтрока, Сторона, РеквизитыОрганизации)
	
	ДеталиОперации = ЗначениеСвойстваXDTO(Операция, "OthrDtls");
	
	// Перевод между счетами
	Если НовСтрока.ВидОперацииЦР    = БанковскиеПравила.ОперацияDCBU()       // Пополнение ЦР (банковский счет -> ЦР)
		Или НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияDCSE() Тогда // Вывод средств (банковский счет <- ЦР)
		
		ПрочитатьДанныеОбменМеждуСчетомИКошелькомЦР(ДеталиОперации, НовСтрока, Сторона, РеквизитыОрганизации);
		
	ИначеЕсли ЗначениеСвойстваXDTO(ДеталиОперации, "B2B") <> Неопределено Тогда
		
		ПрочитатьДанныеОперацияB2B(ДеталиОперации, НовСтрока, Сторона);
		
	ИначеЕсли ЗначениеСвойстваXDTO(ДеталиОперации, "C2B") <> Неопределено Тогда
		
		ПрочитатьДанныеОперацияC2B(ДеталиОперации, НовСтрока, Сторона);
		
	ИначеЕсли ЗначениеСвойстваXDTO(ДеталиОперации, "B2C") <> Неопределено Тогда
		
		ПрочитатьДанныеОперацияB2C(ДеталиОперации, НовСтрока, Сторона);
		
	Иначе // пока не обслуживаем
		
		НовСтрока[Сторона] = Новый Структура;
		
		Если НовСтрока.ВидОперацииЦР = БанковскиеПравила.ОперацияOPTR() Тогда
			ПрочитатьДанныеОперацияOPTrf(ДеталиОперации, НовСтрока);
			Возврат;
		КонецЕсли;
		
		// По остальным операциям нет описания.
		
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'Неподдерживаемый вид %1 в операции с идентификатором %2'"),
			НовСтрока.ВидОперацииЦР, НовСтрока.НомерДокумента);
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,,, ОписаниеПроблемы);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеОбменМеждуСчетомИКошелькомЦР(ДеталиОперации, НовСтрока, Сторона, РеквизитыОрганизации)
	
	НовСтрока[Сторона] = Новый Структура;
	Если ЗначениеЗаполнено(РеквизитыОрганизации.ИННОрганизации) Тогда
		НовСтрока[Сторона].Вставить("ИНН", РеквизитыОрганизации.ИННОрганизации);
	КонецЕсли;
	
	// Детали операций обмена ЦР на безналичные средства и наоборот
	Xchg = ЗначениеСвойстваXDTO(ДеталиОперации, "Xchg"); // Exchange
	Если Xchg = Неопределено Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'Некорректные данные в операции с идентификатором %1. В теге <Tp> указан вид операции %2, но отсутствует или не заполнен тег <Xchg>'"),
				НовСтрока.НомерДокумента, НовСтрока.ВидОперацииЦР);
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеПроблемы);
		Возврат;
	КонецЕсли;
	
	// Банковский счет Субъекта ПлЦР
	PtyInf = ЗначениеСвойстваXDTO(Xchg, "PtyInf"); // PartyInformation
	NtlId = Неопределено;
	Если PtyInf <> Неопределено Тогда
		// Информация о реквизитах Субъекта ПлЦР-резидента, необходимых для проведения банковского платежа.
		NtlId = ЗначениеСвойстваXDTO(PtyInf, "NtlId"); // NationalIdentification
		Если NtlId <> Неопределено Тогда
			НовСтрока[Сторона].Вставить("РасчСчет", ЗначениеСвойстваXDTO(NtlId, "Acct.Id"));
		КонецЕсли;
	КонецЕсли;
	
	БИК = "";
	БИКПосредника = "";
	Наименование = "";
	НовСтрока[Сторона].Вставить("Банк", Новый Структура);
	
	Если ПустаяСтрока(БИК) И NtlId <> Неопределено Тогда
		// Servicer - Банк, обслуживающий счет
		БИК = ЗначениеСвойстваXDTO(NtlId, "Svcr.MmbId");
	КонецЕсли;
	
	Intrmy = ЗначениеСвойстваXDTO(Xchg, "Intrmy"); // Intermediary (Посредник)
	// Идентификатор посредника, через которого происходит взаимодействие Клиента с ПлЦР по операции.
	Если Intrmy <> Неопределено Тогда
		БИКПосредника = ЗначениеСвойстваXDTO(Intrmy, "ClrSysIdr.MmbId", "");
		Наименование  = ЗначениеСвойстваXDTO(Intrmy, "Nm");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(БИК) Тогда
		НовСтрока[Сторона].Банк.Вставить("БИК", БИК);
		
		Если ЗначениеЗаполнено(Наименование) И БИК = БИКПосредника Тогда
			НовСтрока[Сторона].Банк.Вставить("Наименование", Наименование);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеОперацияB2B(ДеталиОперации, НовСтрока, Сторона)
	
	Если НовСтрока.ВидОперацииЦР <> БанковскиеПравила.ОперацияB2B() Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'Некорректные данные в операции с идентификатором %1. В теге <Tp> указан вид операции %2, вместо %3.
			| Т.к. присутствует и заполнен тег <B2B>'"),
				НовСтрока.НомерДокумента, НовСтрока.ВидОперацииЦР, БанковскиеПравила.ОперацияB2B());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеПроблемы);
	КонецЕсли;
	
	НовСтрока[Сторона] = Новый Структура;
	
	// Данные участника операции - контрагента ЮЛ
	РеквизитыКонтрагента = ЗначениеСвойстваXDTO(ДеталиОперации.B2B, "BizCtrPty"); // BusinessCounterparty
	Если РеквизитыКонтрагента <> Неопределено Тогда
		НовСтрока[Сторона].Вставить("ИНН",          ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "INN"));
		НовСтрока[Сторона].Вставить("КПП",          ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "KPP"));
		НовСтрока[Сторона].Вставить("Наименование", ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "Nm"));
		НовСтрока[Сторона].Вставить("РасчСчет",     ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "Wllt.Id"));
		
		НовСтрока[Сторона].Вставить("Банк", Новый Структура);
		ДанныеСчета = ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "Wllt.DCAcctNb"); // DCAccountNumber
		Если ДанныеСчета <> Неопределено Тогда
			// Указывается номер СЦР Субъекта ПлЦР
			НовСтрока[Сторона].Вставить("НомерСчетаЦифровой", ЗначениеСвойстваXDTO(ДанныеСчета, "Id", "")); // 20-значный
			// Данные банка (платформы цифрового рубля) имеет смысл заполнять
			// только в случае заполненного цифрового номера счета.
			Если ЗначениеЗаполнено(НовСтрока[Сторона].НомерСчетаЦифровой) Тогда
				НовСтрока[Сторона].Банк.Вставить("БИК",          БИКПлатформыЦифровогоРубля());
				НовСтрока[Сторона].Банк.Вставить("КоррСчет",     НомерСчетаПлатформыЦифровогоРубля());
				НовСтрока[Сторона].Банк.Вставить("Наименование", НаименованиеПлатформыЦифровогоРубля());
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НазначениеПлатежа = ЗначениеСвойстваXDTO(ДеталиОперации.B2B, "Purp");
	Если НазначениеПлатежа <> Неопределено Тогда
		НовСтрока.НазначениеПлатежа = НазначениеПлатежа;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеОперацияC2B(ДеталиОперации, НовСтрока, Сторона)
	
	Если НовСтрока.ВидОперацииЦР <> БанковскиеПравила.ОперацияC2B() Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'Некорректные данные в операции с идентификатором %1. В теге <Tp> указан вид операции %2, вместо %3.
			|Т.к. присутствует и заполнен тег <C2B>'"),
				НовСтрока.НомерДокумента, НовСтрока.ВидОперацииЦР, БанковскиеПравила.ОперацияC2B());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеПроблемы);
	КонецЕсли;
	
	НовСтрока[Сторона] = Новый Структура;
	
	// Данные участника операции - контрагента ФЛ
	РеквизитыКонтрагента = ЗначениеСвойстваXDTO(ДеталиОперации.C2B, "CstmrCtrPty"); // CustomerCounterparty
	Если РеквизитыКонтрагента <> Неопределено Тогда
		НовСтрока[Сторона].Вставить("Наименование", ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "PAM"));
		НовСтрока[Сторона].Вставить("РасчСчет",     ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "Wllt.Id"));
		
		НовСтрока[Сторона].Вставить("Банк", Новый Структура);
		ДанныеСчета = ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "Wllt.DCAcctNb"); // DCAccountNumber
		Если ДанныеСчета <> Неопределено Тогда
			// Указывается номер СЦР Субъекта ПлЦР
			НовСтрока[Сторона].Вставить("НомерСчетаЦифровой", ЗначениеСвойстваXDTO(ДанныеСчета, "Id", "")); // 20-значный
			// Данные банка (платформы цифрового рубля) имеет смысл заполнять
			// только в случае заполненного цифрового номера счета.
			Если ЗначениеЗаполнено(НовСтрока[Сторона].НомерСчетаЦифровой) Тогда
				НовСтрока[Сторона].Банк.Вставить("БИК",          БИКПлатформыЦифровогоРубля());
				НовСтрока[Сторона].Банк.Вставить("КоррСчет",     НомерСчетаПлатформыЦифровогоРубля());
				НовСтрока[Сторона].Банк.Вставить("Наименование", НаименованиеПлатформыЦифровогоРубля());
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НазначениеПлатежа = ЗначениеСвойстваXDTO(ДеталиОперации.C2B, "Purp");
	Если НазначениеПлатежа <> Неопределено Тогда
		НовСтрока.НазначениеПлатежа = НазначениеПлатежа;
	КонецЕсли;
	
	// Информация о референсах, предоставленная получателем средств, позволяющая идентифицировать исходные документы.
	CdtrRefInf = ЗначениеСвойстваXDTO(ДеталиОперации.C2B, "RmtInf.CdtrRefInf"); // CreditorReferenceInformation
	Если CdtrRefInf = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПризнакИдентификаторПлатежа = ЗначениеСвойстваXDTO(CdtrRefInf, "Tp", ""); // Type
	Если ПризнакИдентификаторПлатежа = "UIP2" Тогда
		ИдентификаторПлатежа = ЗначениеСвойстваXDTO(CdtrRefInf, "Ref");       // Reference
		Если ИдентификаторПлатежа <> Неопределено Тогда
			НовСтрока.УникальныйИдентификаторПлатежа = ИдентификаторПлатежа;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеОперацияB2C(ДеталиОперации, НовСтрока, Сторона)
	
	Если НовСтрока.ВидОперацииЦР <> БанковскиеПравила.ОперацияB2C() Тогда
		ОписаниеПроблемы = СтрШаблон(
			НСтр("ru = 'Некорректные данные в операции с идентификатором %1. В теге <Tp> указан вид операции %2, вместо %3.
			| Т.к. присутствует и заполнен тег <B2C>'"),
				НовСтрока.НомерДокумента, НовСтрока.ВидОперацииЦР, БанковскиеПравила.ОперацияB2C());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ОписаниеПроблемы);
	КонецЕсли;
	
	НовСтрока[Сторона] = Новый Структура;
	
	// Данные участника операции - контрагента ФЛ
	РеквизитыКонтрагента = ЗначениеСвойстваXDTO(ДеталиОперации.B2C, "CstmrCtrPty"); // CustomerCounterparty
	Если РеквизитыКонтрагента <> Неопределено Тогда
		НовСтрока[Сторона].Вставить("Наименование", ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "PAM"));
		НовСтрока[Сторона].Вставить("РасчСчет",     ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "Wllt.Id"));
		
		НовСтрока[Сторона].Вставить("Банк", Новый Структура);
		ДанныеСчета = ЗначениеСвойстваXDTO(РеквизитыКонтрагента, "Wllt.DCAcctNb"); // DCAccountNumber
		Если ДанныеСчета <> Неопределено Тогда
			// Указывается номер СЦР Субъекта ПлЦР
			НовСтрока[Сторона].Вставить("НомерСчетаЦифровой", ЗначениеСвойстваXDTO(ДанныеСчета, "Id", "")); // 20-значный
			// Данные банка (платформы цифрового рубля) имеет смысл заполнять
			// только в случае заполненного цифрового номера счета.
			Если ЗначениеЗаполнено(НовСтрока[Сторона].НомерСчетаЦифровой) Тогда
				НовСтрока[Сторона].Банк.Вставить("БИК",          БИКПлатформыЦифровогоРубля());
				НовСтрока[Сторона].Банк.Вставить("КоррСчет",     НомерСчетаПлатформыЦифровогоРубля());
				НовСтрока[Сторона].Банк.Вставить("Наименование", НаименованиеПлатформыЦифровогоРубля());
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НазначениеПлатежа = ЗначениеСвойстваXDTO(ДеталиОперации.B2C, "Purp");
	Если НазначениеПлатежа <> Неопределено Тогда
		НовСтрока.НазначениеПлатежа = НазначениеПлатежа;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеОперацияOPTrf(ДеталиОперации, НовСтрока)
	
	// Операция "Перевод оператором".
	// Для данной операции пока заполняем только Назначение платежа.
	
	НазначениеПлатежа = ЗначениеСвойстваXDTO(ДеталиОперации.OPTrf, "Purp");
	Если НазначениеПлатежа <> Неопределено Тогда
		НовСтрока.НазначениеПлатежа = НазначениеПлатежа;
	КонецЕсли;
	
КонецПроцедуры

// Формирует пустую таблицу для заполнения данных по выписке
//
// Возвращаемое значение:
//  ТаблицаЗначений - данные для выписки. Содержит колонки:
//   *ДатаФормирования        - Дата - дата формирования документа
//   *ИдентификаторДокумента  - Строка - уникальный идентификатор документа
//   *ИдентификаторЗапроса    - Строка - идентификатор запроса, по которому была сформирована выписка.
//   *НачалоПериода           - Дата - дата начала периода выписки
//   *КонецПериода            - Дата - дата окончания периода выписки
//   *НомерСчета              - Строка - счет по которому сформирована выписка
//   *НомерСчетаЦифровой      - Строка - 20-значный номер счета
//   *Организация             - Структура - реквизиты организации, см. макет ОбмнеСБанками.ВыпискаБанка
//   *Банк                    - Структура - реквизиты банка,       см. макет ОбмнеСБанками.ВыпискаБанка
//   *НачальныйОстаток        - Число - входящий остаток по выписке
//   *ОборотВходящихПлатежей  - Число - поступило денежных средств за период
//   *ОборотИсходящихПлатежей - Число - оплачено за период
//   *КонечныйОстаток         - Число - остаток денежных средств на конец периода
//   *Операции                - ТаблицаЗначений - операции по выписке, см. НовыйТаблицаОпераций()
//   *Штамп                   - Структура - данных для штампа
//   *ЭлектронныеПодписи      - ТаблицаЗначений - данные электронных подписей.
//
Функция НовыйТаблицаВыписок()
	
	ТаблицаВыписок = Новый ТаблицаЗначений;
	ТаблицаВыписок.Колонки.Добавить("ДатаФормирования",       ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаВыписок.Колонки.Добавить("ИдентификаторДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(36));
	ТаблицаВыписок.Колонки.Добавить("НачалоПериода",          ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаВыписок.Колонки.Добавить("КонецПериода",           ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаВыписок.Колонки.Добавить("НомерСчета",             БанковскиеПравила.ТипЦифровойНомерСчета());
	ТаблицаВыписок.Колонки.Добавить("НомерСчетаЦифровой",     // 20-значный
		ОбщегоНазначения.ОписаниеТипаСтрока(БанковскиеПравила.МаксимальнаяДлинаНациональногоНомераСчета()));
	ТаблицаВыписок.Колонки.Добавить("Организация",             Новый ОписаниеТипов("Структура"));
	ТаблицаВыписок.Колонки.Добавить("Банк",                    Новый ОписаниеТипов("Структура"));
	ТаблицаВыписок.Колонки.Добавить("НачальныйОстаток",        ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаВыписок.Колонки.Добавить("ОборотВходящихПлатежей",  ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаВыписок.Колонки.Добавить("ОборотИсходящихПлатежей", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаВыписок.Колонки.Добавить("КонечныйОстаток",         ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаВыписок.Колонки.Добавить("Операции",                Новый ОписаниеТипов("ТаблицаЗначений"));
	
	// Для механизмов БЭД
	ТаблицаВыписок.Колонки.Добавить("ИдентификаторЗапроса",    Новый ОписаниеТипов("Строка"));
	ТаблицаВыписок.Колонки.Добавить("Штамп",                   Новый ОписаниеТипов("Структура"));
	ТаблицаВыписок.Колонки.Добавить("ЭлектронныеПодписи",      Новый ОписаниеТипов("ТаблицаЗначений"));
	
	Возврат ТаблицаВыписок;
	
КонецФункции

Функция НовыйТаблицаОпераций()
	
	ТаблицаОпераций = Новый ТаблицаЗначений;
	ТаблицаОпераций.Колонки.Добавить("ВидОперации",        ОбщегоНазначения.ОписаниеТипаСтрока(2)); // ВидОплаты / TransitionKind
	ТаблицаОпераций.Колонки.Добавить("ВидОперацииЦР",      Новый ОписаниеТипов("Строка"));          // Вид операции цифрового рубля 
	ТаблицаОпераций.Колонки.Добавить("Очередность",        ОбщегоНазначения.ОписаниеТипаЧисло(1));
	ТаблицаОпераций.Колонки.Добавить("ДатаОперации",       ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаОпераций.Колонки.Добавить("ДатаДокумента",      ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаОпераций.Колонки.Добавить("ДатаСписано",        ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаОпераций.Колонки.Добавить("ДатаПоступило",      ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаОпераций.Колонки.Добавить("НаправлениеПлатежа", ОбщегоНазначения.ОписаниеТипаСтрока(1));
	ТаблицаОпераций.Колонки.Добавить("НомерДокумента",     ОбщегоНазначения.ОписаниеТипаСтрока(36));
	ТаблицаОпераций.Колонки.Добавить("СуммаДокумента",     ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаОпераций.Колонки.Добавить("Плательщик",         Новый ОписаниеТипов("Структура"));
	ТаблицаОпераций.Колонки.Добавить("Получатель",         Новый ОписаниеТипов("Структура"));
	ТаблицаОпераций.Колонки.Добавить("НазначениеПлатежа",  Новый ОписаниеТипов("Строка"));
	ТаблицаОпераций.Колонки.Добавить("УникальныйИдентификаторПлатежа", ОбщегоНазначения.ОписаниеТипаСтрока(35));
	
	Возврат ТаблицаОпераций;
	
КонецФункции

Процедура РазборВыпискиВызыватьИсключение()
	
	ШаблонСообщения = НСтр("ru = 'При разборе выписки по счету Цифрового рубля (ЦР) возникла ошибка:
		|%1.
		|Возможно файл не является выпиской по счету ЦР.'");
	КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	ТекстОшибки = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки);
	Если Пользователи.ЭтоПолноправныйПользователь(, , Ложь) Тогда
		ТекстСообщения = ТекстОшибки;
	Иначе
		ТекстСообщения = НСтр("ru = 'Файл выписки Цифровому рублю (ЦР), полученный из банка, невозможно прочитать.
			|Файл содержит ошибку или не является выпиской по счету ЦР.'");
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		ТекстОшибки);
	
	ВызватьИсключение ТекстСообщения;
	
КонецПроцедуры

#КонецОбласти

#Область ПлатежныеПорученияЦРСлужебная

Функция СформироватьПлатежныеДанные(ПлатежныеДокументы, ИдентификаторСчета)
	
	ДанныеПлатежей = Новый Структура;
	СписокПлатежей = Новый Массив;
	ДанныеОрганизации = Новый Структура;
	
	Если ПлатежныеДокументы.Количество() = 0 Тогда
		ДанныеПлатежей.Вставить("Org",      ДанныеОрганизации);
		ДанныеПлатежей.Вставить("Payments", СписокПлатежей);
		
		Возврат ДанныеПлатежей;
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПлатежныеДокументы[0].ОрганизацияСчет, "Владелец");
		
		Если ПлатежныеДокументы[0].ОрганизацияЦифровойСчет Тогда
			НомерСчета = ПлатежныеДокументы[0].ОрганизацияНомерСчета;
		Иначе
			ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
			БанковскиеСчетаЦифровые = Справочники.БанковскиеСчета.БанковскиеСчетаОрганизации(
				Владелец, ВалютаРеглУчета, , Истина);
			Если БанковскиеСчетаЦифровые.Количество() > 0 Тогда
				НомерСчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскиеСчетаЦифровые[0].Ссылка, "НомерСчета");
			КонецЕсли;
		КонецЕсли;
		
		ОГРН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ОГРН");
		УстановитьПривилегированныйРежим(Ложь);
		
		ДанныеОрганизации.Вставить("Nm",           ПлатежныеДокументы[0].ОрганизацияКратко);
		ДанныеОрганизации.Вставить("WalletNumber", НомерСчета);
		ДанныеОрганизации.Вставить("WltId",        ИдентификаторСчета);
		ДанныеОрганизации.Вставить("INN",          ПлатежныеДокументы[0].ОрганизацияИНН);
		ДанныеОрганизации.Вставить("KPP",          ПлатежныеДокументы[0].ОрганизацияКПП);
		ДанныеОрганизации.Вставить("OGRN",         ОГРН);
		
		ДанныеПлатежей.Вставить("Org", ДанныеОрганизации);
	КонецЕсли;
	
	Для Каждого СтрокаДокумента Из ПлатежныеДокументы Цикл
		
		Если Не СтрокаДокумента.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеПлатежа = ПодготовитьДанныеПлатежа(СтрокаДокумента, ИдентификаторСчета);
		Если ДанныеПлатежа <> Неопределено Тогда
			
			СписокПлатежей.Добавить(ДанныеПлатежа);
			СтрокаДокумента.Готовность = -2;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеПлатежей.Вставить("Payments", СписокПлатежей);
	
	Возврат ДанныеПлатежей;
	
КонецФункции

Функция ПодготовитьДанныеПлатежа(СтрокаДокумента, ИдентификаторСчета)
	
	ДанныеПлатежа = Неопределено;
	
	Если СтрокаДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет Тогда
		Если СтрокаДокумента.КонтрагентИдентификаторЦифровогоСчета = ИдентификаторСчета Тогда
			ДанныеПлатежа = ПодготовитьДанныеПлатежаПополнениеСредствЦР(СтрокаДокумента);
		Иначе
			ДанныеПлатежа = ПодготовитьДанныеПлатежаВыводСредствЦР(СтрокаДокумента);
		КонецЕсли;
	ИначеЕсли СтрокаДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику
		Или СтрокаДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПрочиеРасчетыСКонтрагентами Тогда
		ДанныеПлатежа = ПодготовитьДанныеПлатежаB2B(СтрокаДокумента);
	КонецЕсли;
	
	Возврат ДанныеПлатежа;
	
КонецФункции

Функция ПодготовитьДанныеПлатежаВыводСредствЦР(СтрокаДокумента)
	
	ДанныеDCSE = Новый Структура;
	ДанныеDCSE.Вставить("TtlAmt", СтрокаДокумента.СуммаДокумента);
	ДанныеDCSE.Вставить("Tp",     "DCSE");
	ДанныеDCSE.Вставить("OprId",  СтрокаДокумента.Номер);
	
	ДанныеКонтрагента = Новый Структура;
	ДанныеКонтрагента.Вставить("AccountId", СтрокаДокумента.КонтрагентНомерСчета);
	ДанныеКонтрагента.Вставить("MmbId",     СтрокаДокумента.КонтрагентБИКБанка);
	
	ДанныеDCSE.Вставить("Recipient", ДанныеКонтрагента);
	
	Возврат ДанныеDCSE;
	
КонецФункции

Функция ПодготовитьДанныеПлатежаПополнениеСредствЦР(СтрокаДокумента)
	
	DCBU = Новый Структура;
	DCBU.Вставить("TtlAmt",    СтрокаДокумента.СуммаДокумента);
	DCBU.Вставить("Tp",        "DCBU");
	DCBU.Вставить("OprId",     СтрокаДокумента.Номер);
	DCBU.Вставить("AccountId", СтрокаДокумента.ОрганизацияНомерСчета);
	DCBU.Вставить("MmbId",     СтрокаДокумента.ОрганизацияБИКБанка);
	
	Возврат DCBU;
	
КонецФункции

Функция ПодготовитьДанныеПлатежаB2B(СтрокаДокумента)
	
	ДанныеB2BD = Новый Структура;
	ДанныеB2BD.Вставить("TtlAmt",  СтрокаДокумента.СуммаДокумента);
	ДанныеB2BD.Вставить("Tp",      "BTOB");
	ДанныеB2BD.Вставить("OprId",   СтрокаДокумента.Номер);
	
	Если ЗначениеЗаполнено(СтрокаДокумента.НазначениеПлатежа) Тогда
		НазначениеПлатежа = СтрЗаменить(СтрокаДокумента.НазначениеПлатежа, Символы.ПС + Символы.ВК, " ");
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, Символы.ПС, " ");
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, Символы.ВК, " ");
		ДанныеB2BD.Вставить("Purpose", СокрЛП(НазначениеПлатежа));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаДокумента.ИдентификаторПлатежа) Тогда
		ДанныеB2BD.Вставить("UIP", СтрокаДокумента.ИдентификаторПлатежа);
	КонецЕсли;
	
	ДанныеКонтрагента = Новый Структура;
	Если ЗначениеЗаполнено(СтрокаДокумента.КонтрагентИдентификаторЦифровогоСчета) Тогда
		ДанныеКонтрагента.Вставить("Type", "WALLETID");
		ДанныеКонтрагента.Вставить("Value", СтрокаДокумента.КонтрагентИдентификаторЦифровогоСчета);
		ДанныеКонтрагента.Вставить("Nm",    СтрокаДокумента.Контрагент);
	ИначеЕсли ЗначениеЗаполнено(СтрокаДокумента.КонтрагентНомерСчета) Тогда
		ДанныеКонтрагента.Вставить("Type", "WALLETNUMBER");
		ДанныеКонтрагента.Вставить("Value", СтрокаДокумента.КонтрагентНомерСчета);
		ДанныеКонтрагента.Вставить("Nm",    СтрокаДокумента.Контрагент);
	ИначеЕсли ЗначениеЗаполнено(СтрокаДокумента.КонтрагентОГРН)
		И ЗначениеЗаполнено(СтрокаДокумента.ИННПолучателя) Тогда
		ДанныеКонтрагента.Вставить("Type",  "REQUISITES");
		ДанныеКонтрагента.Вставить("OGRN",  СтрокаДокумента.КонтрагентОГРН);
		ДанныеКонтрагента.Вставить("INN",   СтрокаДокумента.ИННПолучателя);
		
		Если ЗначениеЗаполнено(СтрокаДокумента.КПППолучателя) Тогда
			ДанныеКонтрагента.Вставить("KPP", СтрокаДокумента.КПППолучателя);
		Иначе
			ДанныеКонтрагента.Вставить("KPP", СтрокаДокумента.КонтрагентКПП);
		КонецЕсли;
		
		ДанныеКонтрагента.Вставить("Nm",    СтрокаДокумента.Контрагент);
	Иначе
		// Нет данных
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеB2BD.Вставить("Recipient", ДанныеКонтрагента);
	
	Возврат ДанныеB2BD;
	
КонецФункции

#КонецОбласти

Функция ИмяСобытияЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'Загрузка выписки по счету цифрового рубля'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Функция ЗначениеСвойстваXDTO(ОбъектXDTO,
							 ИмяСвойства,
							 ЗначениеПоУмолчанию = Неопределено,
							 Тип = Неопределено,
							 ЭтоСписок = Ложь,
							 НеопределеноПоУмолчанию = Ложь)
	
	Возврат РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(
		ОбъектXDTO, ИмяСвойства, ЗначениеПоУмолчанию, Тип, ЭтоСписок, НеопределеноПоУмолчанию);
	
КонецФункции

#КонецОбласти
