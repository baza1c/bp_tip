#Область ПрограммныйИнтерфейс

//Дополнительные действия прикладной конфигурации при изменении статуса документа ЗЕРНО.
//
//Параметры:
//   ДокументСсылка   - ДокументСсылка     - ссылка на документ с изменением статуса.
//   ПредыдущийСтатус - ПеречислениеСсылка - предыдущий статус обработки.
//   НовыйСтатус      - ПеречислениеСсылка - новый статус обработки.
//   ПараметрыОбновленияСтатуса - Структура, Неопределено - (См. ИнтеграцияЗЕРНОСлужебныйКлиентСервер.ПараметрыОбновленияСтатуса).
//
Процедура ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса = Неопределено) Экспорт

	Возврат;

КонецПроцедуры

// Процедура определяет использование транспортных средств
//
// Параметры:
//  Указывается - Булево - Признак использования транспортных средств.
Процедура УказываетсяТранспортноеСредство(Указывается) Экспорт
	
	Указывается = Истина;
	
КонецПроцедуры

#Область ПересчетКоличества

// Заполняет количество номенклатуры по количеству ЗЕРНО:
//   * Имя колонки с количеством ЗЕРНО откуда идет пересчет: <Количество[суффикс]ЗЕРНО>,
//   * Имя колонки с прикладным количеством: <Количество[суффикс]>.
//
// Параметры:
//  ОбъектыПересчета - ДанныеФормыЭлементКоллекции, Массив Из СтрокаТабличнойЧасти - объекты для пересчета.
//  Суффикс - Строка - Окончание наименования колонки, содержащей количество.
Процедура ЗаполнитьКоличествоПоКоличествуЗЕРНО(ОбъектыПересчета, Суффикс = "") Экспорт
	
	СтрокиВМассиве = ОбъектыПересчета;
	Если ТипЗнч(ОбъектыПересчета) <> Тип("Массив") Тогда
		СтрокиВМассиве = Новый Массив;
		СтрокиВМассиве.Добавить(ОбъектыПересчета);
	КонецЕсли;
	
	Коэффициенты = Новый Соответствие;
	Для Каждого Строка Из СтрокиВМассиве Цикл
		Коэффициенты.Вставить(Строка.Номенклатура, 1);	
	КонецЦикла;
	
	Для Каждого ЭлементМассива Из СтрокиВМассиве Цикл
		Если Не ЗначениеЗаполнено(Коэффициенты.Получить(ЭлементМассива.Номенклатура)) Тогда
			ЭлементМассива["Количество" + Суффикс] = 0;
		Иначе
			ЭлементМассива["Количество" + Суффикс] = ЭлементМассива["Количество" + Суффикс + "ЗЕРНО"] * Коэффициенты.Получить(ЭлементМассива.Номенклатура);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийДокументов

//Вызывается при вводе документа на основании, при выполнении метода Заполнить или при интерактивном вводе нового.
//
//Параметры:
//   ДокументОбъект - ДокументОбъект - заполняемый документ,
//   ДанныеЗаполнения - Произвольный - значение, которое используется как основание для заполнения,
//   ТекстЗаполнения - Строка, Неопределено - текст, используемый для заполнения документа,
//   СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполненияДокумента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
	СобытияФормЗЕРНОБПВызовСервера.ОбработкаЗаполненияДокумента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);

КонецПроцедуры

// В процедуре необходимо реализовать запись сопоставления ключей адресов и ключей реквизитов организаций ЗЕРНО 
//   с прикладными справочниками конфигурации
//
// Параметры:
//  ДокументОснование - ДокументСсылка, ДокументОбъект - прикладной документ конфигурации,
//  ДокументОбъект    - ДокументСсылка, ДокументОбъект - связанный с ним документ библиотеки.
//
Процедура ЗаполнитьСоответствиеШапкиОбъектов(ДокументОснование, ДокументОбъект) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

// В процедуре реализуется заполнение обособленных подразделений для коллекции организаций. В переданной таблице
// необходимо заполнить колонки ОрганизацияКонтрагент, Подразделение.
//
// Параметры:
//  КоллекцияОрганизаций - ТаблицаЗначений - массив ссылок, по которым нужно получить реквизиты
//  ТаблицаПодразделенйОрганизации - см. ИнтеграцияЗЕРНО.НоваяТаблицаОрганизацияКонтрагентПодразделение
Процедура ПриОпределенииОбособленныхПодразделенийОрганизации(КоллекцияОрганизаций, ТаблицаПодразделенйОрганизации) Экспорт
	
	Если КоллекцияОрганизаций.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КоллекцияОрганизаций", КоллекцияОрганизаций);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПодразделенияОрганизаций.Ссылка КАК Подразделение,
	               |	ПодразделенияОрганизаций.Владелец КАК ОрганизацияКонтрагент
	               |ИЗ
	               |	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	               |ГДЕ
	               |	ПодразделенияОрганизаций.ОбособленноеПодразделение
	               |	И ПодразделенияОрганизаций.Владелец В(&КоллекцияОрганизаций)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаПодразделенйОрганизации.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

// В процедуре требуется дополнить в переданной таблице по Организации/контрагенту и подразделению
// данные ИНН/КПП/ОГРН/Наименование/ТипОрганизации/КодАльфа3/ЮридическийАдрес/Фамилия/Имя/Отчество из информационной базы.
// КоллекцияОрганизацийПодразделений индексирован по "ОрганизацияКонтрагент, Подразделение".
//
// Параметры:
//  КоллекцияОрганизацийПодразделений - см. ИнтеграцияЗЕРНО.НоваяТаблицаОрганизацияКонтрагентПодразделение
Процедура ПриОпределенииРеквизитовОрганизацийКонтрагентов(КоллекцияОрганизацийПодразделений) Экспорт
	
	Если КоллекцияОрганизацийПодразделений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОтбора", КоллекцияОрганизацийПодразделений);
	
	Запрос.Текст  = 
	"ВЫБРАТЬ
	|	ТаблицаОтбора.ОрганизацияКонтрагент КАК ОрганизацияКонтрагент,
	|	ТаблицаОтбора.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ТаблицаОтбора
	|ИЗ
	|	&ТаблицаОтбора КАК ТаблицаОтбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникКонтрагенты.ИНН КАК ИНН,
	|	СправочникКонтрагенты.КПП КАК КПП,
	|	СправочникКонтрагенты.РегистрационныйНомер КАК ОГРН,
	|	СправочникКонтрагенты.НаименованиеПолное КАК Наименование,
	|	ВЫБОР
	|		КОГДА СправочникКонтрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|				И СправочникКонтрагенты.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|				И СправочникКонтрагенты.ИНН = """"
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИностраннаяОрганизация)
	|		КОГДА СправочникКонтрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИндивидуальныйПредприниматель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ЮридическоеЛицо)
	|	КОНЕЦ КАК ТипОрганизации,
	|	ЕСТЬNULL(СправочникКонтрагенты.СтранаРегистрации.КодАльфа3, """") КАК КодАльфа3,
	|	СправочникКонтрагенты.Ссылка КАК Ссылка,
	|	ТаблицаОтбора.Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ИндивидуальныйПредприниматель
	|ИЗ
	|	ТаблицаОтбора КАК ТаблицаОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ПО ТаблицаОтбора.ОрганизацияКонтрагент = СправочникКонтрагенты.Ссылка
	|ГДЕ
	|	НЕ СправочникКонтрагенты.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СправочникОрганизации.ИНН,
	|	ЕСТЬNULL(СправочникПодразделения.КПП, СправочникОрганизации.КПП),
	|	СправочникОрганизации.ОГРН,
	|	СправочникОрганизации.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.ИностраннаяОрганизация
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИностраннаяОрганизация)
	|		КОГДА СправочникОрганизации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИндивидуальныйПредприниматель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ЮридическоеЛицо)
	|	КОНЕЦ,
	|	ЕСТЬNULL(СправочникОрганизации.СтранаРегистрации.КодАльфа3, """"),
	|	СправочникОрганизации.Ссылка,
	|	ТаблицаОтбора.Подразделение,
	|	СправочникОрганизации.ФамилияИП + "" "" + СправочникОрганизации.ИмяИП + "" "" + СправочникОрганизации.ОтчествоИП
	|ИЗ
	|	ТаблицаОтбора КАК ТаблицаОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО ТаблицаОтбора.ОрганизацияКонтрагент = СправочникОрганизации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК СправочникПодразделения
	|		ПО ТаблицаОтбора.Подразделение = СправочникПодразделения.Ссылка
	|			И (СправочникПодразделения.ОбособленноеПодразделение)
	|ГДЕ
	|	НЕ СправочникОрганизации.Ссылка ЕСТЬ NULL";
	
	КоллекцияКонтактнаяИнформация = Новый Массив();
	Выборка = Запрос.Выполнить().Выбрать();
	
	Отбор = Новый Структура("ОрганизацияКонтрагент, Подразделение");
	Пока Выборка.Следующий() Цикл
		
		Отбор.ОрганизацияКонтрагент = Выборка.Ссылка;
		Отбор.Подразделение         = Выборка.Подразделение;
		
		НайденныеСтроки = КоллекцияОрганизацийПодразделений.НайтиСтроки(Отбор);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
			ЗаполнитьЗначенияСвойств(НайденнаяСтрока, Выборка);
			Если Выборка.ТипОрганизации = Перечисления.ТипыОрганизацийЗЕРНО.ИндивидуальныйПредприниматель Тогда
				
				Если ТипЗнч(Выборка.Ссылка) = Тип("СправочникСсылка.Организации")
					И ЗначениеЗаполнено(Выборка.ИндивидуальныйПредприниматель) Тогда
					ПолноеНаименование = Выборка.ИндивидуальныйПредприниматель;
				Иначе
					ПолноеНаименование = Выборка.Наименование;
				КонецЕсли;
				
				ПоисковаяСтрока = НСтр("ru = 'Индивидуальный предприниматель'");
				Если ВРег(Лев(ПолноеНаименование, 3))  = "ИП " Тогда 
					ПолноеНаименование = Сред(ПолноеНаименование, 4);
				ИначеЕсли ВРег(Прав(ПолноеНаименование, 3))  = " ИП" Тогда 
					ПолноеНаименование = Лев(ПолноеНаименование, СтрДлина(ПолноеНаименование) - 3);
				ИначеЕсли ВРег(Лев(ПолноеНаименование, СтрДлина(ПоисковаяСтрока))) = ВРег(ПоисковаяСтрока) Тогда
					ПолноеНаименование = Прав(ПолноеНаименование, СтрДлина(ПолноеНаименование) - СтрДлина(ПоисковаяСтрока));
				КонецЕсли;
				
				ЧастиИмени = ФизическиеЛицаКлиентСервер.ЧастиИмени(ПолноеНаименование);
				НайденнаяСтрока.Фамилия  = ЧастиИмени.Фамилия;
				НайденнаяСтрока.Имя      = ЧастиИмени.Имя;
				НайденнаяСтрока.Отчество = ЧастиИмени.Отчество;
				
			КонецЕсли;
			
		КонецЦикла;
		
		КоллекцияКонтактнаяИнформация.Добавить(Выборка.Ссылка);
		
	КонецЦикла;
	
	ВидыКонтактнойИнформации = Новый Массив();
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
	ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		КоллекцияКонтактнаяИнформация,, ВидыКонтактнойИнформации);
	
	Отбор = Новый Структура("ОрганизацияКонтрагент");
	Для Каждого СтрокаТаблицы Из ТаблицаКИ Цикл
		
		Отбор.ОрганизацияКонтрагент = СтрокаТаблицы.Объект;
		НайденныеСтроки = КоллекцияОрганизацийПодразделений.НайтиСтроки(Отбор);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НайденнаяСтрока.ЮридическийАдрес = СтрокаТаблицы.Представление;
			НайденнаяСтрока.ЮридическийАдресJSON = СтрокаТаблицы.Значение;  
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры


// Определение ссылок на организации и контрагенты по реквизитам. Переданной таблице реквизитов необходимо заполнить
// колонки:
//  - Организация - Ссылка на организацию, найденную по переданным реквизитам
//  - Контрагент  - Ссылка на контрагент, найденного по переданным реквизитам,
//  - Подразделение - Ссылка на обособленное подразделение, найденного по переданным реквизитам.
// Параметры:
//  ТаблицаРеквизитов - см. Справочники.КлючиРеквизитовОрганизацийЗЕРНО.НоваяТаблицаРеквизитовКлючейРеквизитовОрганизаций
Процедура ПриОпределенииКонтрагентовОрганизацийПоРеквизитам(ТаблицаРеквизитов) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

#Область ОКПД2

// В данной процедуре требуется переопределить текст запроса, помещающий выборку из прикладного классификатора ОКПД2
// во временную таблицу ВременнаяТаблица.
//   Требования к тексту запроса:
//     Если классификатор ОКПД2 не используется, переопределение также не заполнять.
//     Результат запроса обязательно должен содержать следующие поля:
//   Колонки временной таблицы "ДанныеШтрихкодовУпаковок":
//     Ссылка             - ОпределяемыйТип.ОКПД2ИС - ссылка на элемент классификатора ОКПД2.
//     Код                - Строка - код.
//     НаименованиеПолное - Строка - наименование.
// Параметры:
//  ТекстЗапроса - Строка - Переопределяемый текст запроса.
Процедура ПриОпределенииТекстаЗапросаКлассификатораОКПД2(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КлассификаторОКПД2.Ссылка КАК Ссылка,
	|	КлассификаторОКПД2.Код КАК Код,
	|	КлассификаторОКПД2.НаименованиеПолное КАК НаименованиеПолное
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Справочник.КлассификаторОКПД2 КАК КлассификаторОКПД2
	|";
		
КонецПроцедуры

// Предназначена для поиска по коду элемента в Классификаторе ОКПД2.
// Если элемент не найден, то, при использовании классификатора, создать элемент справочника в соответствии с классификатором ОКПД2.
// 
// Параметры:
//  ОКПД2 - Строка - Строка с кодом классификатора ОКПД2.
//  ЭлементСправочника - Произвольный - Переопределяемый параметр, ссылка на элемент классификатора.
Процедура ПриОпределенииСопоставленногоКлассификатораОКПД2(ОКПД2, ЭлементСправочника) Экспорт
	
	ЭлементСправочника = Справочники.КлассификаторОКПД2.НайтиСоздатьПоКоду(ОКПД2);
	
КонецПроцедуры

#КонецОбласти

#Область ТНВЭД

// В данной процедуре требуется переопределить текст запроса, помещающий выборку из прикладного классификатора ТН ВЭД
// во временную таблицу ВременнаяТаблица.
//   Требования к тексту запроса:
//     Если классификатор ТН ВЭД не используется, переопределение также не заполнять.
//     Результат запроса обязательно должен содержать следующие поля:
//   Колонки временной таблицы "ДанныеШтрихкодовУпаковок":
//     Ссылка             - Произвольный - ссылка на элемент классификатора ТН ВЭД.
//     Код                - Строка - код.
//     НаименованиеПолное - Строка - наименование.
// Параметры:
//  ТекстЗапроса - Строка - Переопределяемый текст запроса.
Процедура ПриОпределенииТекстаЗапросаКлассификатораТНВЭД(ТекстЗапроса) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КлассификаторТНВЭД.Ссылка КАК Ссылка,
	|	КлассификаторТНВЭД.Код КАК Код,
	|	КлассификаторТНВЭД.НаименованиеПолное КАК НаименованиеПолное
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|";
	

КонецПроцедуры

// Предназначена для поиска по коду элемента в Классификаторе ТН ВЭД.
// Если элемент не найден, то, при использовании классификатора, создать элемент справочника в соответствии с классификатором ТН ВЭД ЕАЭС.
// 
// Параметры:
//  ТНВЭД - Строка - Строка с кодом классификатора ТН ВЭД.
//  Наименование - Строка - наименование элемента классификатора ТН ВЭД.
//  ЭлементСправочника - Произвольный - Переопределяемый параметр, ссылка на элемент классификатора.
Процедура ПриОпределенииСопоставленногоКлассификатораТНВЭД(ТНВЭД, Наименование, ЭлементСправочника) Экспорт
	
	ЭлементСправочника = Справочники.КлассификаторТНВЭД.НайтиСоздатьЭлементКлассификатораТНВЭД(ТНВЭД);	
	
КонецПроцедуры

#КонецОбласти

// Процедура заполняет признак использования договоров контрагентов.
//
// Параметры:
//  Используется - Булево - Признак использования договоров контрагентов.
Процедура ПриОпределенииИспользованияДоговоровКонтрагентов(Используется) Экспорт

	Используется = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");

КонецПроцедуры

// Процедура заполняет признак использования договоров между организациями.
//
// Параметры:
//  Используется - Булево - Признак использования договоров между организациями.
Процедура ПриОпределенииИспользованияДоговоровОрганизаций(Используется) Экспорт

	Используется = Ложь;

КонецПроцедуры

// Заполняет соответствие договору дату и номер договора.
//
// Параметры:
//  МассивСсылок - Массив Из ОпределяемыйТип.ДоговорКонтрагентаИС - массив ссылок договоры.
//  
//  ВозвращаемоеСоответствие - Соответствие из ОпределяемыйТип.ДоговорКонтрагентаИС:
//                             * Ключ - ОпределяемыйТип.ДоговорКонтрагентаИС - ссылка на выгружаемый учетный документ.
//                             * Значение - Структура:
//                               ** НомерДоговора - Строка.
//                               ** ДатаДоговора - Дата.
//
Процедура ПриОпределенииНомераДатыДоговораДокументов(МассивСсылок, ВозвращаемоеСоответствие) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоговорыКонтрагентов.Дата КАК ДатаДоговора,
	|	ДоговорыКонтрагентов.Номер КАК НомерДоговора,
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка В (&МассивСсылок)");
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ВозвращаемоеСоответствие.Вставить(
			Выборка.Ссылка, Новый Структура("НомерДоговора, ДатаДоговора", Выборка.НомерДоговора, Выборка.ДатаДоговора));
	КонецЦикла;
	
КонецПроцедуры

// Заполняет соответствие госконтракту  дату, номер и номер закупки ЕИС.
//
// Параметры:
//  МассивСсылок - Массив Из ОпределяемыйТип.ГосударственныеКонтрактыИС - массив ссылок государственные контракты.
//  
//  ВозвращаемоеСоответствие - Соответствие из ОпределяемыйТип.ГосударственныеКонтрактыИС:
//                             * Ключ - ОпределяемыйТип.ГосударственныеКонтрактыИС - ссылка на ссылка на гос.контракт.
//                             * Значение - Структура:
//                               ** НомерГосКонтракта - Строка.
//                               ** ДатаГосКонтракта  - Дата.
//                               ** НомерЗакупкиЕИС   - Строка.
//
Процедура ПриОпределенииРеквизитовГосКонтракта(МассивСсылок, ВозвращаемоеСоответствие) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

// Предназначена для получения площади земельного участка если в качестве определяемого типа ЗемльныйУчастокИС
// используется тип, отличный от СправочникСсылка.ЗемельныеУчасткиИС.
//
// Параметры:
//  ЗемельныйУчасток - ОпределяемыйТип.ЗемельныйУчастокИС - Ссылка на земельный участок.
//  ПлощадьЗемельногоУчастка - Число - Площадь земельного участка в гектарах.
//
Процедура ПриОпределенииПлощадиЗемельногоУчастка(ЗемельныйУчасток, ПлощадьЗемельногоУчастка) Экспорт
	
	
	Возврат;
	
КонецПроцедуры

#Область КорректировочныеДокументы

// Процедура определяет, какого типа документ подсистемы ЗЕРНО должен создаваться на основании
// прикладного документа. Необходимо для указания документов, по которым требуется создание корректировочных
// документов ЗЕРНО.
// 
// Параметры:
//  МетаданныеДокументаЗЕРНО - ОбъектМетаданных - метаданные документа ЗЕРНО
//  Документ - ОпределяемыйТип.ОснованиеОформлениеСДИЗЗЕРНО - прикладной документ, 
//  СоздатьДокументКорректировки - Булево - Истина, если по документу надо создать корректировочный.
//				Доступно только если метаданные документа ЗЕРНО поддерживают такую функциональность
//
// Возвращаемое значение:
//  Булево - Истина, если по документу надо создать корректировочный. Доступно только если метаданные документа ЗЕРНО
//		поддерживают такую функциональность
Процедура ТребуетсяСозданиеКорректировочногоДокумента(МетаданныеДокументаЗЕРНО, Документ, СоздатьДокументКорректировки) Экспорт
		
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		СоздатьДокументКорректировки = Истина;
	Иначе
		СоздатьДокументКорректировки = Ложь;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

// Определение ссылок на организации и контрагенты по реквизитам. Переданной таблице реквизитов необходимо заполнить
// колонки:
//  - Организация - Ссылка на организацию, найденную по переданным реквизитам
//  - Контрагент  - Ссылка на контрагент, найденного по переданным реквизитам
// Параметры:
//  ТаблицаРеквизитов - см. Справочники.КлючиРеквизитовОрганизацийЗЕРНО.НоваяТаблицаРеквизитовКлючейРеквизитовОрганизаций
Процедура ПриОпределенииКонратрагентовОрганизацийПоРеквизитам(ТаблицаРеквизитов) Экспорт
	
	Если ТаблицаРеквизитов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаРеквизитов.Колонки.Найти("ИндексИсходнойСтроки") = Неопределено Тогда
		ТаблицаРеквизитов.Колонки.Добавить("ИндексИсходнойСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(5));
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаРеквизитов Цикл
		СтрокаТаблицы.ИндексИсходнойСтроки = ТаблицаРеквизитов.Индекс(СтрокаТаблицы);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаОтбора", ТаблицаРеквизитов);
	
	Запрос.Текст  = "ВЫБРАТЬ
	                |	ТаблицаОтбора.ИНН КАК ИНН,
	                |	ТаблицаОтбора.КПП КАК КПП,
	                |	ТаблицаОтбора.ОГРН КАК ОГРН,
	                |	ТаблицаОтбора.Наименование КАК Наименование,
	                |	ТаблицаОтбора.ТипОрганизации КАК ТипОрганизации,
	                |	ТаблицаОтбора.ИндексИсходнойСтроки КАК ИндексИсходнойСтроки
	                |ПОМЕСТИТЬ ТаблицаОтбора
	                |ИЗ
	                |	&ТаблицаОтбора КАК ТаблицаОтбора
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	Контрагенты.Ссылка КАК Контрагент,
	                |	НЕОПРЕДЕЛЕНО КАК Организация,
	                |	НЕОПРЕДЕЛЕНО КАК Подразделение,
	                |	ТаблицаОтбора.ИндексИсходнойСтроки КАК ИндексИсходнойСтроки
	                |ИЗ
	                |	Справочник.Контрагенты КАК Контрагенты
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОтбора КАК ТаблицаОтбора
	                |		ПО Контрагенты.ИНН = ТаблицаОтбора.ИНН
	                |			И Контрагенты.КПП = ТаблицаОтбора.КПП
	                |			И (ТаблицаОтбора.ТипОрганизации В (ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ЮридическоеЛицо), ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИностраннаяОрганизация)))
	                |			И (НЕ Контрагенты.ПометкаУдаления)
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	Контрагенты.Ссылка,
	                |	НЕОПРЕДЕЛЕНО,
	                |	НЕОПРЕДЕЛЕНО,
	                |	ТаблицаОтбора.ИндексИсходнойСтроки
	                |ИЗ
	                |	Справочник.Контрагенты КАК Контрагенты
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОтбора КАК ТаблицаОтбора
	                |		ПО Контрагенты.ИНН = ТаблицаОтбора.ИНН
	                |			И (ТаблицаОтбора.ТипОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИндивидуальныйПредприниматель))
	                |			И (НЕ Контрагенты.ПометкаУдаления)
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	Контрагенты.Ссылка,
	                |	НЕОПРЕДЕЛЕНО,
	                |	НЕОПРЕДЕЛЕНО,
	                |	ТаблицаОтбора.ИндексИсходнойСтроки
	                |ИЗ
	                |	Справочник.Контрагенты КАК Контрагенты
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОтбора КАК ТаблицаОтбора
	                |		ПО Контрагенты.Наименование = ТаблицаОтбора.Наименование
	                |			И (ТаблицаОтбора.ТипОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИностраннаяОрганизацияБезРегистрацииВРФ))
	                |			И (НЕ Контрагенты.ПометкаУдаления)
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	НЕОПРЕДЕЛЕНО,
	                |	Организации.Ссылка,
	                |	НЕОПРЕДЕЛЕНО,
	                |	ТаблицаОтбора.ИндексИсходнойСтроки
	                |ИЗ
	                |	Справочник.Организации КАК Организации
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОтбора КАК ТаблицаОтбора
	                |		ПО Организации.ИНН = ТаблицаОтбора.ИНН
	                |			И Организации.КПП = ТаблицаОтбора.КПП
	                |			И (ТаблицаОтбора.ТипОрганизации В (ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ЮридическоеЛицо), ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИностраннаяОрганизация)))
	                |			И (НЕ Организации.ПометкаУдаления)
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	НЕОПРЕДЕЛЕНО,
	                |	Организации.Ссылка,
	                |	НЕОПРЕДЕЛЕНО,
	                |	ТаблицаОтбора.ИндексИсходнойСтроки
	                |ИЗ
	                |	Справочник.Организации КАК Организации
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОтбора КАК ТаблицаОтбора
	                |		ПО Организации.ИНН = ТаблицаОтбора.ИНН
	                |			И Организации.ОГРН = ТаблицаОтбора.ОГРН
	                |			И (ТаблицаОтбора.ТипОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИндивидуальныйПредприниматель))
	                |			И (НЕ Организации.ПометкаУдаления)
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	НЕОПРЕДЕЛЕНО,
	                |	Организации.Ссылка,
	                |	НЕОПРЕДЕЛЕНО,
	                |	ТаблицаОтбора.ИндексИсходнойСтроки
	                |ИЗ
	                |	Справочник.Организации КАК Организации
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОтбора КАК ТаблицаОтбора
	                |		ПО Организации.Наименование = ТаблицаОтбора.Наименование
	                |			И (ТаблицаОтбора.ТипОрганизации = ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЗЕРНО.ИностраннаяОрганизацияБезРегистрацииВРФ))
	                |			И (НЕ Организации.ПометкаУдаления)
	                |
	                |ОБЪЕДИНИТЬ ВСЕ
	                |
	                |ВЫБРАТЬ
	                |	НЕОПРЕДЕЛЕНО,
	                |	РегистрацииВНалоговомОргане.Владелец,
	                |	РегистрацииВНалоговомОргане.Ссылка,
	                |	ТаблицаОтбора.ИндексИсходнойСтроки
	                |ИЗ
	                |	ТаблицаОтбора КАК ТаблицаОтбора
	                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	                |		ПО ТаблицаОтбора.ИНН = РегистрацииВНалоговомОргане.Владелец.ИНН
	                |			И ТаблицаОтбора.КПП = РегистрацииВНалоговомОргане.КПП";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		СтрокаИсходнойТаблицы = ТаблицаРеквизитов.Получить(СтрокаДанных.ИндексИсходнойСтроки);
		Если ЗначениеЗаполнено(СтрокаДанных.Контрагент) Тогда
			СтрокаИсходнойТаблицы.Контрагент = СтрокаДанных.Контрагент;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаДанных.Организация) Тогда
			СтрокаИсходнойТаблицы.Организация = СтрокаДанных.Организация;
			Если ЗначениеЗаполнено(СтрокаДанных.Подразделение) Тогда
				СтрокаИсходнойТаблицы.Подразделение = СтрокаДанных.Подразделение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Вызывается расширением формы при необходимости проверки заполнения реквизитов при записи или при проведении документа в форме,
// а также при выполнении метода ПроверитьЗаполнение.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - проверяемый документ,
//  Отказ - Булево - признак отказа от проведения документа,
//  ПроверяемыеРеквизиты - Массив из Строка- массив путей к реквизитам, для которых будет выполнена проверка заполнения,
//  МассивНепроверяемыхРеквизитов - Массив из Строка - массив путей к реквизитам, для которых не будет выполнена проверка заполнения.
Процедура ПриОпределенииОбработкиПроверкиЗаполнения(ДокументОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов) Экспорт
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ФормированиеПартийЗЕРНО")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СписаниеПартийЗЕРНО")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВнесениеСведенийОСобранномУрожаеЗЕРНО")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОформлениеСДИЗЗЕРНО")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПогашениеСДИЗЗЕРНО") Тогда
		
		ПараметрыПроверки = Новый Структура;
		ПараметрыПроверки.Вставить("Объект", ДокументОбъект);
		ПараметрыПроверки.Вставить("МассивНепроверяемыхРеквизитов", МассивНепроверяемыхРеквизитов);
		ПараметрыПроверки.Вставить("ИмяТабличнойЧасти", "Товары");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Серия");

	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ФормированиеПартийИзДругихПартийЗЕРНО")
		Или ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ФормированиеПартийПриПроизводствеЗЕРНО") Тогда
		
		ПараметрыПроверки = Новый Структура;
		ПараметрыПроверки.Вставить("Объект", ДокументОбъект);
		ПараметрыПроверки.Вставить("МассивНепроверяемыхРеквизитов", МассивНепроверяемыхРеквизитов);
		ПараметрыПроверки.Вставить("НоменклатураВШапкеДокумента", Истина);
		МассивНепроверяемыхРеквизитов.Добавить("Характеристика");
		МассивНепроверяемыхРеквизитов.Добавить("Серия");
		МассивНепроверяемыхРеквизитов.Добавить("Сырье.Характеристика");
		МассивНепроверяемыхРеквизитов.Добавить("Сырье.Серия");
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

