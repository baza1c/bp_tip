////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиHttpBridgeКлиент: механизм работы с внешней конпонентой HttpBridge.
// Конпонентой HttpBridge позволяет выполнять HTTP запросы из веб-клиента, в том числе запрос на http-ресурс,
//  находясь на сайте с https
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет инициализирование внешней компоненты HttpBridge и отправку через нее htpp-запроса
//
// Параметры:
//  Параметры - Структура - содержит поля:
//   * ПараметрыЗапроса - Структура - 
//   * ПослеВыполненияЗапросаСКлиента - ОписаниеОповещения - оповещение, вызываемое после выполнения метода
//      * Результат - Структура - результат аутентификации, содержит поля,
//          подробнее см. ОбменСБанкамиСлужебныйКлиентСервер.СтруктураВозвратаHttpЗапроса()
//      * ДополнительныеПараметры - Структура - контекст выполнения метода
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками, Неопределено - текущая настройка обмена с банком
//
Процедура ВыполнитьЗапрос(Параметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодключенияHttpBridge", ЭтотОбъект, Параметры);
	ПараметрыПодключения = ОбщегоНазначенияКлиент.ПараметрыПодключенияКомпоненты();
	ПараметрыПодключения.ТекстПояснения = НСтр("ru = 'Для продолжения необходимо установить внешний модуль HttpBridge'");
	ПараметрыПодключения.Изолированно = Истина;
	АдресВнешнейКомпоненты = "Обработка.ОбменСБанками.Макет.КомпонентаHttpBridge";
	ОбщегоНазначенияКлиент.ПодключитьКомпонентуИзМакета(
		Оповещение, "HttpBridge", АдресВнешнейКомпоненты, ПараметрыПодключения);
	
КонецПроцедуры

Процедура ПослеПодключенияHttpBridge(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Подключено Тогда
		
		Лог = НСтр("ru = 'Подключен внешний модуль HttpBridge'");
		ОбменСБанкамиSberApiСлужебныйВызовСервера.ДобавитьЗаписьВЖурналОбмена(ДополнительныеПараметры.НастройкаОбмена, Лог);
		
		ДополнительныеПараметры.Вставить("ПодключаемыйМодульHttpBridge", Результат.ПодключаемыйМодуль);
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияВерсии", ЭтотОбъект, ДополнительныеПараметры);
		Результат.ПодключаемыйМодуль.НачатьВызовВерсия(Оповещение);
		
	Иначе
		
		ОписаниеОшибки = ?(ЗначениеЗаполнено(Результат.ОписаниеОшибки), Результат.ОписаниеОшибки, "");
		Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки);
		КонецЕсли;
		
		Лог = НСтр("ru = 'Ошибка при подлкючении внешнего модуля HttpBridge. %1'");
		Лог = СтрШаблон(Лог, ОписаниеОшибки);
		ОбменСБанкамиSberApiСлужебныйВызовСервера.ДобавитьЗаписьВЖурналОбмена(ДополнительныеПараметры.НастройкаОбмена, Лог);
		
		РезультатЗапроса = ОбменСБанкамиСлужебныйКлиентСервер.СтруктураВозвратаHttpЗапроса();
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ПослеВыполненияЗапросаСКлиента,
			РезультатЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияВерсии(Результат, ВозвращаемыеПараметры, ДополнительныеПараметры) Экспорт
	
	Текст = НСтр("ru = 'Версия HttpBridge %1'");
	Текст = СтрШаблон(Текст, Результат);
	ОбменСБанкамиSberApiСлужебныйВызовСервера.ДобавитьЗаписьВЖурналОбмена(ДополнительныеПараметры.НастройкаОбмена, Текст);
	
	НастройкаОбмена = Неопределено;
	КаталогДляЖурналирования = "";
	ИспользоватьЖурналирование = Ложь;
	СоздатьЛог = Ложь;
	Если ДополнительныеПараметры.Свойство("НастройкаОбмена", НастройкаОбмена) И ЗначениеЗаполнено(НастройкаОбмена) Тогда
		
		Параметры = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаСБанком(НастройкаОбмена);
		Если ЗначениеЗаполнено(Параметры)
			И Параметры.ИспользоватьЖурналирование
			И ЗначениеЗаполнено(Параметры.КаталогДляЖурналирования) Тогда
			
			КаталогДляЖурналирования = Параметры.КаталогДляЖурналирования;
			ИспользоватьЖурналирование = Параметры.ИспользоватьЖурналирование;
			
		КонецЕсли;
		
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ЭтоВебКлиент = Истина;
	#Иначе
		ЭтоВебКлиент = Ложь;
	#КонецЕсли
	
	ДополнительныеПараметры.Вставить("ИспользоватьЖурналирование", ИспользоватьЖурналирование);
	
	Если ЭтоВебКлиент И Не ЗначениеЗаполнено(КаталогДляЖурналирования) Тогда
		
		// В Веб-клиенте если мы не указали в настройке обмена опцию логирования и не указали папку для сохранения логов,
		//  то мы не сможем никак сохранить лог во временный файл. Поэтому лог не подключаем
		ПослеПодключенияЛога(НеуспешныйПризнакВыполненияМетодаКомпоненты(), Неопределено, ДополнительныеПараметры);
		Возврат;
		
	ИначеЕсли ЗначениеЗаполнено(КаталогДляЖурналирования) Тогда
		
		// Если заполнено КаталогДляЖурналирования, сохраним лог туда
		СоздатьЛог = Истина;
		
		ИмяФайла = Строка(ТекущаяДата());
		ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла, "_");
		ИмяФайла = "Log_DB_HB_" + ИмяФайла + ".txt";
		ЛогФайлHttpBridge = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(КаталогДляЖурналирования) + ИмяФайла;
		
	ИначеЕсли Не ЭтоВебКлиент И Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
		
		// Если это не ВебКлиент и не заполнено НастройкаОбмена, то возможно это создание настройки обмена 
		//  и мы можем сохранить лог во временный файл
		СоздатьЛог = Истина;
		#Если Не ВебКлиент Тогда
			ЛогФайлHttpBridge = ПолучитьИмяВременногоФайла("txt");
		#КонецЕсли
		
	КонецЕсли;
	
	Если СоздатьЛог Тогда
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПодключенияЛога",
			ЭтотОбъект,
			ДополнительныеПараметры,
			"ПослеПодключенияЛогаОшибка",
			ЭтотОбъект);
			
		ДополнительныеПараметры.ПодключаемыйМодульHttpBridge.НачатьВызовУстановитьЛог(Оповещение, ЛогФайлHttpBridge);
		
	Иначе
		
		ПослеПодключенияЛога(НеуспешныйПризнакВыполненияМетодаКомпоненты(), Неопределено, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает значение, которое вернулось бы при отрицательном результате выполнения метода компоненты
Функция НеуспешныйПризнакВыполненияМетодаКомпоненты()
	
	Возврат -1;
	
КонецФункции

Процедура ПослеПодключенияЛога(Результат, ВозвращаемыеПараметры, ДополнительныеПараметры) Экспорт
	
	Если Результат <> НеуспешныйПризнакВыполненияМетодаКомпоненты()
		И ЗначениеЗаполнено(ВозвращаемыеПараметры) 
		И ТипЗнч(ВозвращаемыеПараметры) = Тип("Массив")
		И ВозвращаемыеПараметры.Количество() <> 0 Тогда
			
		Текст = НСтр("ru = 'Для HttpBridge подключен лог файл %1'");
		Текст = СтрШаблон(Текст, ВозвращаемыеПараметры[0]);
		ОбменСБанкамиSberApiСлужебныйВызовСервера.ДобавитьЗаписьВЖурналОбмена(ДополнительныеПараметры.НастройкаОбмена, Текст);
		
		ДополнительныеПараметры.Вставить("ЛогФайлHttpBridge", ВозвращаемыеПараметры[0]);
		
	Иначе
		
		Текст = НСтр("ru = 'Для HttpBridge лог файл не подключен'");
		ОбменСБанкамиSberApiСлужебныйВызовСервера.ДобавитьЗаписьВЖурналОбмена(ДополнительныеПараметры.НастройкаОбмена, Текст);
		
	КонецЕсли;
	
	ПослатьЗапрос(ДополнительныеПараметры);;
	
КонецПроцедуры

Процедура ПослеПодключенияЛогаОшибка(Результат, ВозвращаемыеПараметры, ДополнительныеПараметры) Экспорт
	
	Лог = НСтр("ru = 'Ошибка подключения лог файл для HttpBridge'");
	ОбменСБанкамиSberApiСлужебныйВызовСервера.ДобавитьЗаписьВЖурналОбмена(ДополнительныеПараметры.НастройкаОбмена, Лог);
	
	ПослатьЗапрос(ДополнительныеПараметры);
		
КонецПроцедуры

Процедура ПослатьЗапрос(ДополнительныеПараметры) Экспорт
	
	ИспользоватьЖурналирование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры,
		"ИспользоватьЖурналирование",
		Ложь);
	
	ПараметрыЗапроса = ДополнительныеПараметры.ПараметрыЗапроса;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтправкиЗапроса",
		ЭтотОбъект,
		ДополнительныеПараметры,
		"ПослеОтправкиЗапросаОбработкаОшибки",
		ЭтотОбъект);
	
	ЗаголовкиJSON = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ЗаголовкиJSON(ПараметрыЗапроса.Заголовки);
	
	НастрокаПодключения = Новый Структура;
	НастрокаПодключения.Вставить("Host", ПараметрыЗапроса.АдресСервера);
	НастрокаПодключения.Вставить("Url", ПараметрыЗапроса.АдресМетодаСервера);
	НастрокаПодключенияJSON = ЗаписатьЗначениеJSON(НастрокаПодключения);
	
	Если ИспользоватьЖурналирование Или Не ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
		
		URL = ПараметрыЗапроса.АдресСервера + ПараметрыЗапроса.АдресМетодаСервера;
		Тело = ПараметрыЗапроса.ТекстЗапроса;
		Тело = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ОбработатьТелоЗапроса(Тело);
		ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPЗапросВЛог(
			"HttpBridge.POST.",
			ДополнительныеПараметры.НастройкаОбмена,
			URL,
			ПараметрыЗапроса.АдресСервера,
			,
			ПараметрыЗапроса.Заголовки,
			Тело);
		
	КонецЕсли;
	
	СтрокаОтвет = "";
	ДополнительныеПараметры.ПодключаемыйМодульHttpBridge.НачатьВызовPost(
		Оповещение,
		НастрокаПодключенияJSON,
		ЗаголовкиJSON,
		ПараметрыЗапроса.ТекстЗапроса,
		СтрокаОтвет);
	
КонецПроцедуры

Процедура ПослеОтправкиЗапроса(Результат, ВозвращаемыеПараметры, ДополнительныеПараметры) Экспорт
	
	ИспользоватьЖурналирование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры,
		"ИспользоватьЖурналирование",
		Ложь);
	
	РезультатЗапроса = ОбменСБанкамиСлужебныйКлиентСервер.СтруктураВозвратаHttpЗапроса();
	РезультатЗапроса.КодСостояния = Результат;
	РезультатЗапроса.Тело = ВозвращаемыеПараметры[3];
	
	Если (Результат = НеуспешныйПризнакВыполненияМетодаКомпоненты()) ИЛИ (Результат <> 200) Тогда
		
		РезультатЗапроса.Статус = Ложь;
		Если Результат = НеуспешныйПризнакВыполненияМетодаКомпоненты() Тогда
			
			ДополнительныеПараметры.Вставить("РезультатЗапроса", РезультатЗапроса);
			
			СтрокаОтвет = "";
			Оповещение = Новый ОписаниеОповещения("ПослеПолученияОшибки", ЭтотОбъект, ДополнительныеПараметры);
			ДополнительныеПараметры.ПодключаемыйМодульHttpBridge.НачатьВызовПолучитьОшибку(Оповещение, СтрокаОтвет);
			
			Возврат;
			
		КонецЕсли;
		
	Иначе
		
		РезультатЗапроса.Статус = Истина;
		ДополнительныеПараметры.Вставить("РезультатЗапроса", РезультатЗапроса);
		
		Если ИспользоватьЖурналирование Или Не ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
		
			ЗаголовкиОтвета = ВозвращаемыеПараметры[1];
			Тело = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ОбработатьТелоЗапроса(РезультатЗапроса.Тело);
			ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPОтветВЛог(
				ДополнительныеПараметры.НастройкаОбмена,
				РезультатЗапроса.КодСостояния,
				СтрШаблон("%1. %2", "HttpBridge", Тело),
				ЗаголовкиОтвета);
			
		КонецЕсли;
			
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ПослеВыполненияЗапросаСКлиента,
		РезультатЗапроса);
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросаОбработкаОшибки(Результат, ДополнительныеПараметры) Экспорт
	
	РезультатЗапроса = ОбменСБанкамиСлужебныйКлиентСервер.СтруктураВозвратаHttpЗапроса();
	
	ИспользоватьЖурналирование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры,
		"ИспользоватьЖурналирование",
		Ложь);
	
	Если ИспользоватьЖурналирование Или Не ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
		
		ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPОтветВЛог(
			ДополнительныеПараметры.НастройкаОбмена,
			"",
			НСтр("ru = 'HttpBridge. Ошибка отправки запроса POST'"));
		
	КонецЕсли;
	
	ЛогФайлHttpBridge = Неопределено;
	Если ДополнительныеПараметры.Свойство("ЛогФайлHttpBridge", ЛогФайлHttpBridge) И ЗначениеЗаполнено(ЛогФайлHttpBridge) Тогда
		
		Попытка
			
			ФайлЛога = Новый ЧтениеТекста(ЛогФайлHttpBridge);
			Лог = ФайлЛога.Прочитать();
			ФайлЛога.Закрыть();
			
			ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPОтветВЛог(
				ДополнительныеПараметры.НастройкаОбмена,
				"",
				НСтр("ru = 'HttpBridge. Текст из лог файла:'") + Символы.ПС + Лог);
			
		Исключение
			
			Операция = НСтр("ru = 'Чтение лог файла HttpBridge'");
			ТекстОшибки = НСтр("ru = 'Ошибка чтения лог файла HttpBridge'");
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
			
		КонецПопытки;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ПослеВыполненияЗапросаСКлиента,
			РезультатЗапроса);
	
КонецПроцедуры

Процедура ПослеПолученияОшибки(Результат, ВозвращаемыеПараметры, ДополнительныеПараметры) Экспорт
	
	ИспользоватьЖурналирование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеПараметры,
		"ИспользоватьЖурналирование",
		Ложь);
	
	ДополнительныеПараметры.РезультатЗапроса.Вставить("СообщениеОбОшибке", ВозвращаемыеПараметры[0]);
	
	Если ИспользоватьЖурналирование Или Не ЗначениеЗаполнено(ДополнительныеПараметры.НастройкаОбмена) Тогда
		
		ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPОтветВЛог(
			ДополнительныеПараметры.НастройкаОбмена,
			"",
			НСтр("ru = 'HttpBridge. Ошибка выполнения запроса:'") + ВозвращаемыеПараметры[0]);
		
	КонецЕсли;
	
	ЛогФайлHttpBridge = Неопределено;
	Если ДополнительныеПараметры.Свойство("ЛогФайлHttpBridge", ЛогФайлHttpBridge) И ЗначениеЗаполнено(ЛогФайлHttpBridge) Тогда
		
		Попытка
		
			ФайлЛога = Новый ЧтениеТекста(ЛогФайлHttpBridge);
			Лог = ФайлЛога.Прочитать();
			ФайлЛога.Закрыть();
			
			ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPОтветВЛог(
				ДополнительныеПараметры.НастройкаОбмена,
				"",
				НСтр("ru = 'HttpBridge. Текст из лог файла:'") + Символы.ПС + Лог);
			
		Исключение
			
			Операция = НСтр("ru = 'Чтение лог файла HttpBridge'");
			ТекстОшибки = НСтр("ru = 'Ошибка чтения лог файла HttpBridge'");
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(Операция, ТекстОшибки);
			
		КонецПопытки;
			
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ПослеВыполненияЗапросаСКлиента,
			ДополнительныеПараметры.РезультатЗапроса);
	
КонецПроцедуры

#КонецОбласти