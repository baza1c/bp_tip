#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет, есть ли у организации в указанном периоде имущество, облагаемое по среднегодовой стоимости
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  НачалоПериода - Дата - Начало отчетного периода
//  КонецПериода - Дата - Конец отчетного периода
// 
// Возвращаемое значение:
//  Булево - Истина - есть имущество по среднегодовой стоимости
Функция ЕстьИмуществоПоСреднегодовойСтоимости(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.УстановитьПараметр("Организация",   Организация);
	
	АмортизационныеГруппы = Новый Массив;
	АмортизационныеГруппы.Добавить(Перечисления.АмортизационныеГруппы.ПерваяГруппа);
	АмортизационныеГруппы.Добавить(Перечисления.АмортизационныеГруппы.ВтораяГруппа);
	Запрос.УстановитьПараметр("АмортизационныеГруппы", АмортизационныеГруппы);
	
	ГруппыОС = Новый Массив;
	ГруппыОС.Добавить(Перечисления.ГруппыОС.ЗемельныеУчастки);
	ГруппыОС.Добавить(Перечисления.ГруппыОС.ОбъектыПриродопользования);
	Запрос.УстановитьПараметр("ГруппыОС", ГруппыОС);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ СписокОС
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.НедвижимоеИмущество
	|	И НЕ ОсновныеСредства.АмортизационнаяГруппа В (&АмортизационныеГруппы)
	|	И НЕ ОсновныеСредства.ГруппаОС В (&ГруппыОС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК Период,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза КАК НалоговаяБаза,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению КАК НеПодлежитНалогообложению
	|ПОМЕСТИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|ГДЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация = &Организация
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период <= &КонецПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";

	Запрос.Выполнить();
	
	Период = НачалоПериода;
	Счетчик = 0;
	
	Пока Период <= НачалоМесяца(КонецПериода) Цикл
		
		Период = ДобавитьМесяц(НачалоПериода, Счетчик);
		Если Счетчик > 0 Тогда
			Период = НачалоМесяца(Период);
		КонецЕсли;
		
		ПериодДляВыборкиДанных = ?(Период < КонецПериода, Период, КонецПериода);
		Запрос.УстановитьПараметр("Период", ПериодДляВыборкиДанных);
		
		Счетчик = Счетчик + 1;
		
		// Срез последних записей по основному средству в регистре "Ставки налога на имущество по отдельным основным средствам".
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
		|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК ПериодСреза
		|ПОМЕСТИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период <= &Период
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	ПериодСреза
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ОСИсключаемыеИзРасчетаПоСреднейСтоимости
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС_Записи КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов КАК СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов.ОсновноеСредство
		|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период = СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов.ПериодСреза
		|ГДЕ
		|	(СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|	ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокОС.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ СписокОСПериода
		|ИЗ
		|	СписокОС КАК СписокОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОСИсключаемыеИзРасчетаПоСреднейСтоимости КАК Исключения
		|		ПО СписокОС.Ссылка = Исключения.ОсновноеСредство
		|ГДЕ
		|	Исключения.ОсновноеСредство ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СостоянияОСОрганизаций.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.СостоянияОСОрганизаций.СрезПоследних(&Период, Организация = &Организация
		|	И ОсновноеСредство В
		|		(ВЫБРАТЬ
		|			СписокОСПериода.Ссылка
		|		ИЗ
		|			СписокОСПериода)) КАК СостоянияОСОрганизаций
		|ГДЕ
		|	СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОСИсключаемыеИзРасчетаПоСреднейСтоимости
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СписокОСПериода";
		
		// Если есть имущество, облагаемое по среднегодовой стоимости, то дальше можно не продолжать
		Если Не Запрос.Выполнить().Пустой() Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Определяет, есть ли у организации в указанном периоде имущество, облагаемое по кадастровой стоимости
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  НачалоПериода - Дата - Начало отчетного периода
//  КонецПериода - Дата - Конец отчетного периода
// 
// Параметры могут быть пропущены, но только все сразу. В этом случае функция проверит наличие в ИБ хотя бы
// одной записи о регистрации имущества, облагаемого по кадастровой стоимости.

// Возвращаемое значение:
//  Булево - Истина - есть имущество по кадастровой стоимости
Функция ЕстьИмуществоПоКадастровойСтоимости(Организация = Неопределено, НачалоПериода = Неопределено, КонецПериода = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если Организация = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтавкиНалога.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалога
		|ГДЕ
		|	СтавкиНалога.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|	И НЕ СтавкиНалога.НеПодлежитНалогообложению";
		
	Иначе
	
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
		Запрос.УстановитьПараметр("Организация",   Организация);
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтавкиНалога.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СтавкиНалога
		|ГДЕ
		|	СтавкиНалога.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|	И СтавкиНалога.ДатаРегистрацииПраваСобственности <= &КонецПериода
		|	И (СтавкиНалога.ДатаПрекращенияПраваСобственности >= &НачалоПериода
		|			ИЛИ СтавкиНалога.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1))
		|	И НЕ СтавкиНалога.НеПодлежитНалогообложению
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтавкиНалога.Организация
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалога
		|ГДЕ
		|	СтавкиНалога.Организация = &Организация
		|	И СтавкиНалога.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СтавкиНалога.ДатаРегистрацииПраваСобственности <= &КонецПериода
		|	И (СтавкиНалога.ДатаПрекращенияПраваСобственности >= &НачалоПериода
		|			ИЛИ СтавкиНалога.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1))
		|	И СтавкиНалога.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|	И НЕ СтавкиНалога.НеПодлежитНалогообложению";
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли