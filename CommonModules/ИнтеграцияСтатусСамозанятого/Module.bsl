#Область ПрограммныйИнтерфейс

// Возвращает признак того, что можно использовать сервис получения статусов самозанятых
//
// Возвращаемое значение:
//  Булево
//
Функция СервисИспользуется() Экспорт 
	
	Если Не ПолучитьФункциональнуюОпцию("РазрешитьДоступКИнтернетСервисам") Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	Использовать = ИнтеграцияСтатусСамозанятогоПовтИсп.ПараметрыСервиса().Использовать
		И ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
	Возврат Использовать;
	
КонецФункции

// Получает статус самозанятого из сервиса.
// 
// Параметры:
//  ИНН - Строка - ИНН физического лица
//  НаДату - Дата, Неопределено - дата в часовом поясе "Europe/Moscow",
//           на которую нужно получить статус самозанятого
//
// Возвращаемое значение:
//   Структура - результат выполнения метода:
//    * ИНН - Строка - ИНН физического лица
//    * НаДату - Дата - дата, на которую выполнялась проверка
//    * ПлательщикНПД - Булево - Истина, если физ.лицо применяет НПД
//    * ДатаРегистрации - Дата, Неопределено - дата изменения статуса
//
Функция ПолучитьСтатусСамозанятого(ИНН, НаДату) Экспорт
	
	МетодВзаимодействия = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия().СоздатьЗадание;
	ПараметрыМетода = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия);
	
	СтрокаСодержанияЗадания = ИнтеграцияСтатусСамозанятогоКлиентСервер.НоваяСтрокаСодержанияЗадания(
		ИНН, НаДату);
	ПараметрыМетода.СодержаниеЗадания.Добавить(СтрокаСодержанияЗадания);
	
	СтрокаРезультата = ИнтеграцияСтатусСамозанятогоКлиентСервер.НоваяСтрокаРезультатаВыполненияЗадания();
	
	ДанныеОтвета = ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	СтатусыСамозанятости = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОтвета, "СтатусыСамозанятости", Новый Массив);
	Если ЗначениеЗаполнено(СтатусыСамозанятости) Тогда
		ЗаполнитьЗначенияСвойств(СтрокаРезультата, СтатусыСамозанятости[0]);
	КонецЕсли;
	
	Возврат СтрокаРезультата;
	
КонецФункции

// Получает статусы самозанятых из сервиса.
// 
// Параметры:
//  ТаблицаЗадания - ТаблицаЗначений: см. ИнтеграцияСтатусСамозанятого.НоваяТаблицаЗадания()
// 
// Возвращаемое значение:
//  ТаблицаЗначений: см. ИнтеграцияСтатусСамозанятого.НоваяТаблицаВыполненияЗадания() - результат выполнения задания.
//
Функция ПолучитьСтатусыСамозанятых(ТаблицаЗадания) Экспорт
	
	МетодВзаимодействия = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия().СоздатьЗадание;
	ПараметрыМетода = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия);
	
	Для Каждого СтрокаЗадания Из ТаблицаЗадания Цикл
		СтрокаСодержанияЗадания = ИнтеграцияСтатусСамозанятогоКлиентСервер.НоваяСтрокаСодержанияЗадания(
			СтрокаЗадания.ИНН, СтрокаЗадания.НаДату);
		ПараметрыМетода.СодержаниеЗадания.Добавить(СтрокаСодержанияЗадания);
	КонецЦикла;
	
	ТаблицаВыполненияЗадания = НоваяТаблицаВыполненияЗадания();
	
	Результат = ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	
	СтатусыСамозанятости = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "СтатусыСамозанятости", Новый Массив);
	Для Каждого СтрокаРезультата Из СтатусыСамозанятости Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаВыполненияЗадания.Добавить(), СтрокаРезультата);
	КонецЦикла;
	
	Возврат ТаблицаВыполненияЗадания;
	
КонецФункции

// Конструктор таблицы задания.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новая таблица задания:
// * ИНН - Строка - ИНН физического лица
// * НаДату - Дата - дата, на которую осуществляется проверка. Например, дата документа.
//
Функция НоваяТаблицаЗадания() Экспорт
	
	ТаблицаЗадания = Новый ТаблицаЗначений();
	ТаблицаЗадания.Колонки.Добавить("ИНН", ОбщегоНазначения.ОписаниеТипаСтрока(12));
	ТаблицаЗадания.Колонки.Добавить("НаДату", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	
	Возврат ТаблицаЗадания;
	
КонецФункции

// Получает статус задания.
// 
// Параметры:
//  Идентификатор - Строка - Идентификатор задания
//  
// Возвращаемое значение:
//   Структура - см. ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыйСтатусЗадания
//
Функция ПолучитьСтатусЗадания(Идентификатор) Экспорт
	
	МетодВзаимодействия = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия().СтатусЗадания;
	
	ПараметрыМетода = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия);
	ПараметрыМетода.Идентификатор = Идентификатор;
	
	СтатусЗадания = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыйСтатусЗадания();
	ДанныеОтвета = ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	ЗаполнитьЗначенияСвойств(СтатусЗадания, ДанныеОтвета);
	
	Возврат СтатусЗадания;
	
КонецФункции

// Получает настройки лимитов.
// 
// Возвращаемое значение:
//   Структура - см. ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыйНастройкиЛимитов
//
Функция ПолучитьНастройкиЛимитов() Экспорт
	
	МетодВзаимодействия = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия().НастройкиЛимитов;
	
	ПараметрыМетода = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия);
	
	НастройкиЛимитов = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыйНастройкиЛимитов();
	ДанныеОтвета = ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	ЗаполнитьЗначенияСвойств(НастройкиЛимитов, ДанныеОтвета);
	
	Возврат НастройкиЛимитов;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Запускает фоновое выполнение метода взаимодействия с сервисом.
//
// Параметры:
//  ПараметрыМетода - Структура: см. ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия
//  ИдентификаторФормы - УникальныйИдентификатор, Неопределено - идентификатор формы, откуда была вызвана функция
// 
// Возвращаемое значение:
//   Структура: см. ДлительныеОперации.ВыполнитьВФоне
// 
Функция ВыполнитьВФоне(ПараметрыМетода, ИдентификаторФормы = Неопределено) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = 
		СтрШаблон(НСтр("ru = '%1: выполнение метода %2'"),
			ИнтеграцияСтатусСамозанятогоКлиентСервер.СинонимПодсистемы(),
			ПараметрыМетода.МетодВзаимодействия);
		
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСтатусСамозанятого.ВыполнитьМетодВзаимодействияВФоне",
		ПараметрыМетода, 
		ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

// Возвращает список методов взаимодействия с сервисом.
//
// Возвращаемое значение:
//  Структура - где ключ - имя метода, значение - Структура: см. НовыйОписаниеМетода
//
Функция МетодыВзаимодействия() Экспорт
	
	ИменаМетодов = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия();
	ОписанияМетодов = ОписанияМетодовВзаимодействия(ИменаМетодов);
	
	Возврат ОписанияМетодов;
	
КонецФункции

// Процедура выполнения метода взаимодействия с сервисом для запуска в фоновом задании.
// При вызове на клиенте должна выполняться асинхронно.
//
// Параметры:
//  ПараметрыМетода - Структура: см. ИнтеграцияСтатусСамозанятого.НовыеПараметрыМетодаВзаимодействия
//  АдресРезультата - Строка - адрес временного хранилища, в которое будет помещен результат выполнения
//
Процедура ВыполнитьМетодВзаимодействияВФоне(ПараметрыМетода, АдресРезультата) Экспорт
	
	ДанныеОтвета = ВыполнитьМетодВзаимодействия(ПараметрыМетода);
	
	ПоместитьВоВременноеХранилище(ДанныеОтвета, АдресРезультата);
	
КонецПроцедуры

// Записывает ошибку в журнал регистрации
// 
// Параметры:
//  КраткоеПредставлениеОшибки - Строка - краткое представление ошибки
//  ПодробноеПредставлениеОшибки - Строка, Неопределено - подробное представление ошибки
//
Процедура ЗаписатьОшибкуВЖурналРегистрации(КраткоеПредставлениеОшибки, ПодробноеПредставлениеОшибки = Неопределено) Экспорт
	
	ЗаписатьОшибку(КраткоеПредставлениеОшибки, ПодробноеПредставлениеОшибки);
	
КонецПроцедуры

// Возвращает признак использования режима отладки
//
// Возвращаемое значение:
//   Булево
//
Функция РежимОтладки() Экспорт
	
	ПараметрЗапускаПриложения = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("ПараметрЗапуска");
	
	Возврат СтрНайти(ПараметрЗапускаПриложения, "РежимОтладкиСтатусСамозанятого") > 0;
	
КонецФункции

// Возвращает дату, приведенную к часовому поясу "Europe/Moscow"
//
// Параметры:
//  ДатаПроверки - Дата
//  ЧасовойПояс - Строка - часовой пояс даты проверки
//
// Возвращаемое значение:
//  Дата
//
Функция ДатаПоСервисуСтатусаСамозанятого(ДатаПроверки, ЧасовойПояс) Экспорт
	
	ДатаПроверкиУниверсальная = УниверсальноеВремя(ДатаПроверки, ЧасовойПояс);
	ЧасовойПоясСервиса = "Europe/Moscow";
	Возврат МестноеВремя(ДатаПроверкиУниверсальная, ЧасовойПоясСервиса);
	
КонецФункции

// Удаляет токен из безопасного хранилища
//
Процедура УдалитьТокенАвторизации() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(
		ИнтеграцияСтатусСамозанятогоКлиентСервер.ИмяПодсистемы(), "ТокенАвторизации");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыполнениеРегламентногоЗадания

Функция НоваяТаблицаВыполненияЗадания()
	
	ТаблицаВыполненияЗадания = НоваяТаблицаЗадания();
	ТаблицаВыполненияЗадания.Колонки.Добавить("ПлательщикНПД", Новый ОписаниеТипов("Булево"));
	ТаблицаВыполненияЗадания.Колонки.Добавить("ДатаРегистрации", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	
	Возврат ТаблицаВыполненияЗадания;
	
КонецФункции

#КонецОбласти

#Область Журналирование

Процедура ЗаписатьОшибку(КраткоеПредставлениеОшибки, ПодробноеПредставлениеОшибки = Неопределено)
	
	ЗаписьЖурналаРегистрации(ИнтеграцияСтатусСамозанятогоКлиентСервер.ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		?(ПодробноеПредставлениеОшибки <> Неопределено, ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки));
	
КонецПроцедуры

Процедура ЗаписатьИнформациюПриВыполненииМетодаВзаимодействия(СоединениеHttp, МетодHttp, ЗапросHttp, ОтветHttp)
	
	Представления = Новый Массив;
	Представления.Добавить(СтрШаблон("%1 %2",
		МетодHttp, ПредставлениеАдресаРесурса(СоединениеHttp, ЗапросHttp.АдресРесурса)));
	Представления.Добавить(ПредставлениеЗаголовков(ЗапросHttp.Заголовки));
	
	ТелоЗапроса = ЗапросHttp.ПолучитьТелоКакСтроку();
	Если ЗначениеЗаполнено(ТелоЗапроса) Тогда
		Представления.Добавить("");
		Представления.Добавить(ТелоЗапроса);
	КонецЕсли;
	
	Представления.Добавить("");
	Представления.Добавить(СтрШаблон("HTTP %1", ОтветHttp.КодСостояния));
	Представления.Добавить(ПредставлениеЗаголовков(ОтветHttp.Заголовки));
	
	ТелоОтвета = ОтветHttp.ПолучитьТелоКакСтроку();
	Если ЗначениеЗаполнено(ТелоОтвета) Тогда
		Представления.Добавить("");
		Представления.Добавить(ТелоОтвета);
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИнтеграцияСтатусСамозанятогоКлиентСервер.ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация, , , СтрСоединить(Представления, Символы.ПС));
	
КонецПроцедуры

Функция ПредставлениеАдресаРесурса(СоединениеHttp, АдресРесурса)
	
	ПредставлениеАдреса = Новый Массив();
	
	Если СоединениеHttp.ЗащищенноеСоединение <> Неопределено Тогда
		ПредставлениеАдреса.Добавить("https://" + СоединениеHttp.Сервер);
		Если ЗначениеЗаполнено(СоединениеHttp.Порт) И СоединениеHttp.Порт <> 443 Тогда
			ПредставлениеАдреса.Добавить(":" + Формат(СоединениеHttp.Порт, "ЧГ=0;"));
		КонецЕсли;
	Иначе
		ПредставлениеАдреса.Добавить("http://" + СоединениеHttp.Сервер);
		Если ЗначениеЗаполнено(СоединениеHttp.Порт) И СоединениеHttp.Порт <> 80 Тогда
			ПредставлениеАдреса.Добавить(":" + Формат(СоединениеHttp.Порт, "ЧГ=0;"));
		КонецЕсли;
	КонецЕсли;
	
	ПредставлениеАдреса.Добавить("/" + АдресРесурса);
	
	Возврат СтрСоединить(ПредставлениеАдреса);
	
КонецФункции

Функция ПредставлениеЗаголовков(Заголовки)
	
	ЗаголовкиСекретов = ЗаголовкиСекретов();
	
	Представления = Новый Массив;
	Для Каждого Заголовок Из Заголовки Цикл
		Если ЗаголовкиСекретов.Найти(НРег(Заголовок.Ключ)) <> Неопределено Тогда
			Представления.Добавить(СтрШаблон("%1: ***", Заголовок.Ключ));
		Иначе
			Представления.Добавить(СтрШаблон("%1: %2", Заголовок.Ключ, Заголовок.Значение));
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(Представления, Символы.ПС);
	
КонецФункции

#КонецОбласти

#Область ВыполнениеМетодаВзаимодействия

Функция ВыполнитьМетодВзаимодействия(ПараметрыМетода)
	
	Если РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = '%1: Работа с внешними ресурсами заблокирована'"),
			ИнтеграцияСтатусСамозанятогоКлиентСервер.СинонимПодсистемы());
	КонецЕсли;
	
	Если ОбщегоНазначения.РазделениеВключено() И Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = '%1: Используется только в разделенном сеансе'"),
			ИнтеграцияСтатусСамозанятогоКлиентСервер.СинонимПодсистемы());
	КонецЕсли;
	
	Если ПолучитьТекущийСеансИнформационнойБазы().ПолучитьФоновоеЗадание() = Неопределено Тогда
		ЗаписатьОшибку(НСтр("ru = 'Запрос к внешнему ресурсу вне фонового задания'"));
	КонецЕсли;
	
	Попытка
		ДанныеОтвета = ПолучитьОтветСервиса(ПараметрыМетода);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписатьОшибку(КраткоеПредставлениеОшибки, ПодробноеПредставлениеОшибки);
		ВызватьИсключение КраткоеПредставлениеОшибки
	КонецПопытки;
	
	Попытка
		ОбработкаПолученныхДанных(ДанныеОтвета);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписатьОшибку(КраткоеПредставлениеОшибки, ПодробноеПредставлениеОшибки);
	КонецПопытки;
	
	Возврат ДанныеОтвета;

КонецФункции

Процедура ОбработкаПолученныхДанных(ДанныеОтвета)
	
	Если ТипЗнч(ДанныеОтвета) = Тип("Структура") Тогда
		СтатусыСамозанятости = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОтвета, "СтатусыСамозанятости");
		Если СтатусыСамозанятости <> Неопределено Тогда
			ИнтеграцияСтатусСамозанятогоПереопределяемый.ПриПолученииСтатусовСамозанятости(СтатусыСамозанятости);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьОтветСервиса(ПараметрыМетода, НомерПопытки = 1)
	
	// Получаем описание метода
	ОписаниеМетода = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		МетодыВзаимодействия(), ПараметрыМетода.МетодВзаимодействия);
	Если ОписаниеМетода = Неопределено Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный метод: %1'"), ПараметрыМетода.МетодВзаимодействия);
	КонецЕсли;
	
	// Читаем и при необходимости обновляем токен
	ТокенАвторизации = ТокенАвторизации();
	
	// Готовим параметры запроса
	ПараметрыЗапроса = ПараметрыЗапросаКСервису(ПараметрыМетода, ОписаниеМетода, ТокенАвторизации);
	
	РезультатЗапроса = ВыполнитьЗапрос(ПараметрыЗапроса, АдресСервиса());
	ДанныеРезультатаЗапроса = ДанныеОтвета(РезультатЗапроса);
	
	Если РезультатЗапроса.КодСостояния = 200 Тогда
		
		Если Не ЗначениеЗаполнено(ОписаниеМетода.МетодПолученияРезультата) Тогда
			// Синхронный метод
			Возврат ДанныеРезультатаЗапроса;
		КонецЕсли;
		
		ПараметрыМетодаПолученияРезультата = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия(
			ОписаниеМетода.МетодПолученияРезультата);
		ЗаполнитьЗначенияСвойств(ПараметрыМетодаПолученияРезультата, ДанныеРезультатаЗапроса);
		РезультатВыполнения = ПолучитьРезультатВыполненияЗадания(ПараметрыМетодаПолученияРезультата);
		ДанныеРезультатаВыполнения = ДанныеОтвета(РезультатВыполнения);
		
		Если РезультатВыполнения.КодСостояния = 200 Тогда
			Возврат ДанныеРезультатаВыполнения;
		ИначеЕсли РезультатВыполнения.КодСостояния = 408 И НомерПопытки < МаксимальноКоличествоПопытокВыполненияМетода() Тогда
			// Истек срок получения результата задания
			Идентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыМетодаПолученияРезультата, "Идентификатор");
			ЗаписатьОшибку(СтрШаблон(НСтр("ru = 'Истек срок получения результата задания %1'"), Идентификатор));
			Возврат ПолучитьОтветСервиса(ПараметрыМетода, НомерПопытки + 1);
		ИначеЕсли РезультатВыполнения.КодСостояния = 429 Тогда
			ВызватьИсключение НСтр("ru = 'Превышен лимит обращений к сервису. Повторите запрос позже.'");
		Иначе
			Причина = ОписаниеОшибкиИзОтвета(ДанныеРезультатаВыполнения, РезультатЗапроса.КодСостояния);
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не удалось получить результат: %1'"), Причина);
		КонецЕсли;
		
	ИначеЕсли РезультатЗапроса.КодСостояния = 401 И НомерПопытки < МаксимальноКоличествоПопытокВыполненияМетода() Тогда
		
		Причина = ОписаниеОшибкиИзОтвета(ДанныеРезультатаЗапроса, РезультатЗапроса.КодСостояния);
		ЗаписатьОшибку(СтрШаблон(НСтр("ru = 'Не удалось получить результат: %1'"), Причина));
		
		УдалитьТокенАвторизации();
		
		Возврат ПолучитьОтветСервиса(ПараметрыМетода, НомерПопытки + 1);
		
	Иначе
		Причина = ОписаниеОшибкиИзОтвета(ДанныеРезультатаЗапроса, РезультатЗапроса.КодСостояния);
		ВызватьИсключение Причина;
	КонецЕсли;
	
КонецФункции

Функция ОписаниеОшибкиИзОтвета(ДанныеОтвета, КодСостояния)
	
	Если ДанныеОтвета <> Неопределено Тогда
		Ошибка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОтвета, "Ошибка");
		Если Ошибка <> Неопределено Тогда
			ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Ошибка, "Описание");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		
		Возврат ОписаниеОшибки;
		
	Иначе
		Если КодСостояния = 307 Тогда
			ОписаниеОшибки = НСтр("ru = 'Временное перенаправление'");
		ИначеЕсли КодСостояния = 308 Тогда
			ОписаниеОшибки = НСтр("ru = 'Постоянное перенаправление'");
		ИначеЕсли КодСостояния = 400 Тогда
			ОписаниеОшибки = НСтр("ru = 'Некорректный запрос'");
		ИначеЕсли КодСостояния = 401 Тогда
			ОписаниеОшибки = НСтр("ru = 'Пользователь не авторизован'");
		ИначеЕсли КодСостояния = 403 Тогда
			ОписаниеОшибки = НСтр("ru = 'Доступ запрещен'");
		ИначеЕсли КодСостояния = 404 Тогда
			ОписаниеОшибки = НСтр("ru = 'Не найдено'");
		ИначеЕсли КодСостояния = 408 Тогда
			ОписаниеОшибки = НСтр("ru = 'Истекло время ожидания'");
		ИначеЕсли КодСостояния = 429 Тогда
			ОписаниеОшибки = НСтр("ru = 'Слишком много запросов'");
		ИначеЕсли КодСостояния = 500 Тогда
			ОписаниеОшибки = НСтр("ru = 'Внутренняя ошибка сервера'");
		Иначе
			ОписаниеОшибки = НСтр("ru = 'Неизвестная ошибка'");
		КонецЕсли;
		
		Возврат СтрШаблон("%1 (%2)", ОписаниеОшибки, КодСостояния);
		
	КонецЕсли;
	
КонецФункции
Функция ПолучитьРезультатВыполненияЗадания(ПараметрыМетода)
	
	НачалоВыполнения = ТекущаяУниверсальнаяДатаВМиллисекундах();
	МаксимальноеВремяВыполнения = МаксимальноеВремяОжиданияПолученияРезультата();
	МаксимальнаяПауза = МаксимальнаяПаузаПриПолученияРезультата();
	
	// Получаем описание метода
	МетодВзаимодействия = ПараметрыМетода.МетодВзаимодействия;
	ОписаниеМетода = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		МетодыВзаимодействия(), МетодВзаимодействия);
	Если ОписаниеМетода = Неопределено Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный метод: %1'"), МетодВзаимодействия);
	КонецЕсли;
	
	Для НомерПопытки = 1 По МаксимальноКоличествоПопытокПолученияРезультата() Цикл
		
		// Читаем и при необходимости обновляем токен
		ТокенАвторизации = ТокенАвторизации();
		
		// Готовим параметры запроса
		ПараметрыЗапроса = ПараметрыЗапросаКСервису(ПараметрыМетода, ОписаниеМетода, ТокенАвторизации);
		
		// Проверяем общее время выполнения
		ОбщееВремяВыполненияСекунд = (ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоВыполнения) / 1000;
		Если ОбщееВремяВыполненияСекунд > МаксимальноеВремяВыполнения Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Превышено максимальное время выполнения метода: %1'"), МетодВзаимодействия);
		КонецЕсли;
		
		РезультатВыполнения = ВыполнитьЗапрос(ПараметрыЗапроса, АдресСервиса());
		
		Если РезультатВыполнения.КодСостояния = 202 Тогда
			Пауза = Мин(Макс(1, Цел(НомерПопытки / 2)), МаксимальнаяПауза);
			ОбщегоНазначенияБТС.Пауза(Пауза);
		Иначе
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатВыполнения;
	
КонецФункции

Функция ТокенАвторизации()
	
	ТокенАвторизации = ПрочитатьТокенАвторизации();
	
	Если Не ТокенДействителен(ТокенАвторизации) Тогда
		ТокенАвторизации = ПолучитьНовыйТокенАвторизации();
		СохранитьТокенАвторизации(ТокенАвторизации);
	КонецЕсли;
	
	Возврат ТокенАвторизации;
	
КонецФункции

Функция ТокенДействителен(ТокенАвторизации)
	
	Если Не ЗначениеЗаполнено(ТокенАвторизации) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СрокДействия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТокенАвторизации, "СрокДействия");
	Если ЗначениеЗаполнено(СрокДействия) Тогда
		Возврат СрокДействия > ТекущаяУниверсальнаяДата() + ТаймаутСоединения();
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьНовыйТокенАвторизации()
	
	ИменаМетодовВзаимодействиям = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия();
	ПараметрыМетода = ИнтеграцияСтатусСамозанятогоКлиентСервер.НовыеПараметрыМетодаВзаимодействия(
		ИменаМетодовВзаимодействиям.СоздатьТокенАвторизации);
	ПараметрыМетода.Тикет = ТикетАутентификацииНаПорталеПоддержки();
	ПараметрыМетода.ИмяКонфигурации = Метаданные.Имя;
	
	ОписаниеМетода = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		МетодыВзаимодействия(), ПараметрыМетода.МетодВзаимодействия);
	
	ПараметрыЗапроса = ПараметрыЗапросаКСервису(ПараметрыМетода, ОписаниеМетода);
	ОтветСервиса = ВыполнитьЗапрос(ПараметрыЗапроса, АдресСервиса());
	ДанныеОтвета = ДанныеОтвета(ОтветСервиса);
	
	Если ОтветСервиса.КодСостояния = 200 Тогда
		Возврат ДанныеОтвета.РеквизитыТокена;
	Иначе
		Причина = ОписаниеОшибкиИзОтвета(ДанныеОтвета, ОтветСервиса.КодСостояния);
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не удалось получить токен авторизации: %1'"), Причина);
	КонецЕсли;
	
КонецФункции

Функция ПрочитатьТокенАвторизации()
	
	УстановитьПривилегированныйРежим(Истина);
	ТокенАвторизации = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ИнтеграцияСтатусСамозанятогоКлиентСервер.ИмяПодсистемы(), "ТокенАвторизации");
	
	Возврат ТокенАвторизации;
	
КонецФункции

Процедура СохранитьТокенАвторизации(ТокенАвторизации)
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		ИнтеграцияСтатусСамозанятогоКлиентСервер.ИмяПодсистемы(), ТокенАвторизации, "ТокенАвторизации");
	
КонецПроцедуры

Функция ТикетАутентификацииНаПорталеПоддержки()
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = ИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(
		ИмяСервисаНаПорталеИТС());
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЗначениеЗаполнено(ДанныеАутентификации.Тикет) Тогда
		Возврат ДанныеАутентификации.Тикет;
	Иначе
		ЗаписатьОшибку(ДанныеАутентификации.ИнформацияОбОшибке);
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не удалось получить тикет на использование сервиса: %1'"),
			ДанныеАутентификации.СообщениеОбОшибке);
	КонецЕсли;
	
КонецФункции

Функция ВыполнитьЗапросИзменитьАдрес(ПараметрыЗапроса, НовыйАдресСервиса)
	
	ОтветСервиса = ВыполнитьЗапрос(ПараметрыЗапроса, НовыйАдресСервиса);
	
	Если Цел(ОтветСервиса.КодСостояния / 100) = 2 Тогда
		// Запрос выполнен успешно, сохраним новый адрес сервиса
		УстановитьПривилегированныйРежим(Истина);
		Константы.АдресСервисаСтатусСамозанятого.Установить(НовыйАдресСервиса);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ОтветСервиса;
	
КонецФункции

Функция ВыполнитьЗапрос(ПараметрыЗапроса, АдресСервиса)
	
	СтруктураАдреса = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресСервиса);
	СоединениеHttp = НовоеСоединениеССервисом(СтруктураАдреса, ТаймаутСоединения());
	ЗапросHttp = НовыйЗапросКСервису(СтруктураАдреса, ПараметрыЗапроса);
	
	ОтветHttp = СоединениеHttp.ВызватьHTTPМетод(ПараметрыЗапроса.МетодHttp, ЗапросHttp);
	
	КодСостояния = ОтветHttp.КодСостояния;
	ТелоОтвета = ОтветHttp.ПолучитьТелоКакСтроку();
	
	Если Цел(КодСостояния / 100) = 2 Тогда
		Если РежимОтладки() Тогда
			ЗаписатьИнформациюПриВыполненииМетодаВзаимодействия(
				СоединениеHttp, ПараметрыЗапроса.МетодHttp, ЗапросHttp, ОтветHttp);
		КонецЕсли;
	ИначеЕсли Цел(КодСостояния / 100) = 3 Тогда
		Если РежимОтладки() Тогда
			ЗаписатьИнформациюПриВыполненииМетодаВзаимодействия(
				СоединениеHttp, ПараметрыЗапроса.МетодHttp, ЗапросHttp, ОтветHttp);
		КонецЕсли;
		НовыйАдресСервиса = ОтветHttp.Заголовки.Получить("Location");
		Если ЗначениеЗаполнено(НовыйАдресСервиса) И НовыйАдресСервиса <> АдресСервиса Тогда
			Если КодСостояния = 307 Тогда
				Возврат ВыполнитьЗапрос(ПараметрыЗапроса, НовыйАдресСервиса);
			ИначеЕсли КодСостояния = 308 Тогда
				Возврат ВыполнитьЗапросИзменитьАдрес(ПараметрыЗапроса, НовыйАдресСервиса);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЗаписатьИнформациюПриВыполненииМетодаВзаимодействия(
			СоединениеHttp, ПараметрыЗапроса.МетодHttp, ЗапросHttp, ОтветHttp);
		ПредставлениеОшибки = СтрШаблон(
			НСтр("ru = 'Ошибка %1 при выполнении метода %2
				|%3'", ОбщегоНазначения.КодОсновногоЯзыка()),
			КодСостояния,
			ПараметрыЗапроса.МетодВзаимодействия,
			ТелоОтвета);
		ЗаписатьОшибку(ПредставлениеОшибки);
	КонецЕсли;
	
	ОтветСервиса = НовыйОтветСервиса(ПараметрыЗапроса.МетодВзаимодействия, КодСостояния, ТелоОтвета);
	
	Возврат ОтветСервиса;
	
КонецФункции

Функция НовыйОтветСервиса(МетодВзаимодействия, КодСостояния, ТелоОтвета = Неопределено)
	
	Если ЗначениеЗаполнено(ТелоОтвета) Тогда
		Данные = ОбщегоНазначения.JSONВЗначение(ТелоОтвета, ИменаСвойствСоЗначениямиДата(), Ложь);
	Иначе
		Данные = Неопределено;
	КонецЕсли;
	
	Ответ = Новый Структура;
	Ответ.Вставить("МетодВзаимодействия", МетодВзаимодействия);
	Ответ.Вставить("КодСостояния", КодСостояния);
	Ответ.Вставить("Данные", Данные);
	
	Возврат Ответ;
	
КонецФункции

Функция НовыйЗапросКСервису(СтруктураАдреса, ПараметрыЗапроса)
	
	Запрос = Новый HTTPЗапрос;
	
	Если Не ПустаяСтрока(СтруктураАдреса.ПутьНаСервере) Тогда
		Запрос.АдресРесурса = СтрШаблон("%1/%2", СтруктураАдреса.ПутьНаСервере, ПараметрыЗапроса.АдресРесурса);
	Иначе
		Запрос.АдресРесурса = ПараметрыЗапроса.АдресРесурса;
	КонецЕсли;
	
	Запрос.Заголовки.Вставить("Accept", "application/json");
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Данные) Тогда
		Запрос.Заголовки.Вставить("Content-Type", "application/json");
	КонецЕсли;

	Для Каждого Заголовок Из ПараметрыЗапроса.Заголовки Цикл
		Запрос.Заголовки.Вставить(Заголовок.Ключ, Заголовок.Значение);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Данные) Тогда
		ТелоЗапроса = ОбщегоНазначения.ЗначениеВJSON(ПараметрыЗапроса.Данные);
		Запрос.УстановитьТелоИзСтроки(ТелоЗапроса);
	КонецЕсли;
	
	Возврат Запрос;
	
КонецФункции

Функция НовоеСоединениеССервисом(СтруктураАдреса, Таймаут, Пользователь = Неопределено, Пароль = Неопределено)
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси(СтруктураАдреса.Схема);
	КонецЕсли;
	
	Если НРег(СтруктураАдреса.Схема) = "https" Тогда
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	КонецЕсли;
	
	Соединение = Новый HTTPСоединение(
		СтруктураАдреса.Хост,
		СтруктураАдреса.Порт,
		Пользователь,
		Пароль,
		Прокси,
		Таймаут,
		ЗащищенноеСоединение);
	
	Возврат Соединение;
	
КонецФункции

#КонецОбласти

#Область РасшифровкаОтвета

Функция ДанныеОтвета(ОтветСервиса)
	
	ИменаМетодов = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия();
	ПараметрыРесурсов = ПараметрыРесурсов();
	
	Если ОтветСервиса.МетодВзаимодействия = ИменаМетодов.СоздатьТокенАвторизации Тогда
		Возврат ДанныеОтвета_СоздатьТокенАвторизации(ОтветСервиса.Данные, ПараметрыРесурсов);
	ИначеЕсли ОтветСервиса.МетодВзаимодействия = ИменаМетодов.СоздатьЗадание Тогда
		Возврат ДанныеОтвета_СоздатьЗадание(ОтветСервиса.Данные, ПараметрыРесурсов);
	ИначеЕсли ОтветСервиса.МетодВзаимодействия = ИменаМетодов.РезультатВыполненияЗадания Тогда
		Возврат ДанныеОтвета_РезультатВыполненияЗадания(ОтветСервиса.Данные, ПараметрыРесурсов);
	ИначеЕсли ОтветСервиса.МетодВзаимодействия = ИменаМетодов.СтатусЗадания Тогда
		Возврат ДанныеОтвета_СтатусЗадания(ОтветСервиса.Данные, ПараметрыРесурсов);
	ИначеЕсли ОтветСервиса.МетодВзаимодействия = ИменаМетодов.НастройкиЛимитов Тогда
		Возврат ДанныеОтвета_НастройкиЛимитов(ОтветСервиса.Данные, ПараметрыРесурсов);
	КонецЕсли;
	
КонецФункции

Функция ДанныеОтвета_СоздатьТокенАвторизации(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура("РеквизитыТокена, Ошибка");
	
	ДанныеОтвета.РеквизитыТокена = ДанныеОтвета_РеквизитыТокена(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.РеквизитыТокена), ПараметрыРесурсов);
	ДанныеОтвета.Ошибка = ДанныеОтвета_Ошибка(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Ошибка), ПараметрыРесурсов);
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_РеквизитыТокена(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура("ТокенBase64, СрокДействия");
	
	ДанныеОтвета.ТокенBase64 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.ТокенBase64);
	
	СрокДействия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.СрокДействия);
	Если ЗначениеЗаполнено(СрокДействия) Тогда
		ДанныеОтвета.СрокДействия = УниверсальноеВремя(СрокДействия);
	Иначе
		ДанныеОтвета.СрокДействия = Дата(1, 1, 1);
	КонецеслИ;
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_СоздатьЗадание(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура("Идентификатор, Ошибка");
	
	ДанныеОтвета.Идентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Идентификатор);
	ДанныеОтвета.Ошибка = ДанныеОтвета_Ошибка(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Ошибка), ПараметрыРесурсов);
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_РезультатВыполненияЗадания(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура("СтатусыСамозанятости, Ошибка");
	
	ДанныеОтвета.СтатусыСамозанятости = ДанныеОтвета_СтатусыСамозанятости(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.СтатусыСамозанятости), ПараметрыРесурсов);
	ДанныеОтвета.Ошибка = ДанныеОтвета_Ошибка(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Ошибка), ПараметрыРесурсов);
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_СтатусыСамозанятости(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	ДанныеОтвета = Новый Массив;
	
	Если ТипЗнч(ДанныеОтветаСервиса) = Тип("Массив") Тогда
		Для каждого СтрокаДанных Из ДанныеОтветаСервиса Цикл
			НоваяСтрокаОтвета = ДанныеОтвета_СтатусСамозанятости(СтрокаДанных, ПараметрыРесурсов);
			ДанныеОтвета.Добавить(НоваяСтрокаОтвета);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_СтатусСамозанятости(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	//@skip-check structure-consructor-too-many-keys
	ДанныеОтвета = ИнтеграцияСтатусСамозанятогоКлиентСервер.НоваяСтрокаРезультатаВыполненияЗадания();
	
	ДанныеОтвета.НаДату = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.НаДату);
	ДанныеОтвета.ИНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.ИНН);
	ДанныеОтвета.ПлательщикНПД = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.ПлательщикНПД);
	ДанныеОтвета.ДатаРегистрации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.ДатаРегистрации);
	ДанныеОтвета.ЭтоОкончательноеЗначение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса , ПараметрыРесурсов.ЭтоОкончательноеЗначение, Ложь);
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_СтатусЗадания(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура("Идентификатор, Статус, Ошибка");
	
	ДанныеОтвета.Идентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Идентификатор);
	ДанныеОтвета.Статус = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Статус);
	ДанныеОтвета.Ошибка = ДанныеОтвета_Ошибка(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Ошибка), ПараметрыРесурсов);
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_НастройкиЛимитов(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	//@skip-check structure-consructor-too-many-keys
	ДанныеОтвета = Новый Структура("МаксимальноеЧислоСтрокВЗадании, ВремяЖизниЗадания, ВремяКэшированияТекущегоСтатуса, Ошибка");
	
	ДанныеОтвета.МаксимальноеЧислоСтрокВЗадании = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.МаксимальноеЧислоСтрокВЗадании);
	ДанныеОтвета.ВремяЖизниЗадания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.ВремяЖизниЗадания);
	ДанныеОтвета.ВремяКэшированияТекущегоСтатуса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.ВремяКэшированияТекущегоСтатуса);
	ДанныеОтвета.Ошибка = ДанныеОтвета_Ошибка(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Ошибка), ПараметрыРесурсов);
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеОтвета_Ошибка(ДанныеОтветаСервиса, ПараметрыРесурсов)
	
	Если ДанныеОтветаСервиса = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ДанныеОтвета = Новый Структура("Идентификатор, Вид, Описание");
	
	ДанныеОтвета.Идентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Идентификатор);
	ДанныеОтвета.Вид = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Вид);
	ДанныеОтвета.Описание = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеОтветаСервиса, ПараметрыРесурсов.Описание);
	
	Возврат ДанныеОтвета;
	
КонецФункции

#КонецОбласти

#Область ПодготовкаПараметровЗапроса

Функция ПараметрыЗапросаКСервису(ПараметрыМетода, ОписаниеМетода, ТокенАвторизации = Неопределено)
	
	ПараметрыЗапроса = НовыйПараметрыЗапросаКСервису();
	ПараметрыЗапроса.МетодВзаимодействия = ОписаниеМетода.МетодВзаимодействия;
	ПараметрыЗапроса.МетодHttp = ОписаниеМетода.МетодHttp;
	ПараметрыЗапроса.АдресРесурса = АдресРесурса(ОписаниеМетода.ШаблонАдресаРесурса, ПараметрыМетода);
	ПараметрыЗапроса.Данные = ДанныеЗапроса(ПараметрыМетода);
	
	Если ТокенАвторизации <> Неопределено Тогда
		ПараметрыЗапроса.Заголовки.Вставить("User-Token", ТокенАвторизации.ТокенBase64);
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

Функция НовыйПараметрыЗапросаКСервису()
	
	Результат = Новый Структура;
	Результат.Вставить("МетодВзаимодействия", "");
	Результат.Вставить("МетодHttp", "");
	Результат.Вставить("АдресРесурса", "");
	Результат.Вставить("Заголовки", Новый Соответствие);
	Результат.Вставить("Данные", Неопределено);
	Возврат Результат;
	
КонецФункции

Функция ДанныеЗапроса(ПараметрыМетода)
	
	ИменаМетодов = ИнтеграцияСтатусСамозанятогоКлиентСервер.ИменаМетодовВзаимодействия();
	
	Если ПараметрыМетода.МетодВзаимодействия = ИменаМетодов.СоздатьТокенАвторизации Тогда
		Возврат ДанныеЗапроса_СоздатьТокенАвторизации(ПараметрыМетода);
	ИначеЕсли ПараметрыМетода.МетодВзаимодействия = ИменаМетодов.СоздатьЗадание Тогда
		Возврат ДанныеЗапроса_СоздатьЗадание(ПараметрыМетода);
	КонецЕслИ;
	
КонецФункции

Функция ДанныеЗапроса_СоздатьТокенАвторизации(ПараметрыМетода)
	
	ИмяКонфигурации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыМетода, "ИмяКонфигурации", "");
	Тикет = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыМетода, "Тикет", "");
	
	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("application_name", ИмяКонфигурации);
	ДанныеЗапроса.Вставить("ticket", Тикет);
	
	Возврат ДанныеЗапроса;
	
КонецФункции

Функция ДанныеЗапроса_СоздатьЗадание(ПараметрыМетода)
	
	ДанныеЗапроса = Новый Массив;
	
	СодержаниеЗадания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыМетода, "СодержаниеЗадания", Новый Массив);
	
	ТекущаяДата = НачалоДня(ТекущаяУниверсальнаяДата());
	Для Каждого СтрокаСодержания Из СодержаниеЗадания Цикл
		Если Не ЗначениеЗаполнено(СтрокаСодержания.НаДату) Тогда
			СтрокаСодержания.НаДату = ТекущаяДата;
		КонецЕсли;
		ДанныеЗапроса.Добавить(Новый Структура("inn, date", СтрокаСодержания.ИНН, СтрокаСодержания.НаДату));
	КонецЦикла;
	
	Возврат ДанныеЗапроса;
	
КонецФункции

Функция АдресРесурса(ШаблонАдресаРесурса, ПараметрыМетода)
	
	ПараметрыПодстановки = Новый Структура;
	
	Для Каждого КлючИЗначение Из ПараметрыРесурсов() Цикл
		
		Если СтрНайти(ШаблонАдресаРесурса, СтрШаблон("[%1]", КлючИЗначение.Значение)) > 0 Тогда
			
			ЗначениеПараметра = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыМетода, КлючИЗначение.Ключ);
			Если ЗначениеПараметра <> Неопределено Тогда
				ПараметрыПодстановки.Вставить(КлючИЗначение.Значение, ЗначениеПараметра);
			Иначе
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Не заполнен параметр %1'"), КлючИЗначение.Значение);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;

	Если ПараметрыМетода.Количество() > 0 Тогда
		АдресРесурса = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонАдресаРесурса, ПараметрыПодстановки);
	Иначе
		АдресРесурса = ШаблонАдресаРесурса;
	КонецЕсли;
	
	Возврат АдресРесурса;
	
КонецФункции

#КонецОбласти

#Область ОписанияМетодовВзаимодействия

Функция ОписанияМетодовВзаимодействия(ИменаМетодов)
	
	ОписанияМетодов = Новый Структура;
	
	// API аутентификации
	
	ДобавитьОписаниеМетода(ОписанияМетодов,
		ИменаМетодов.СоздатьТокенАвторизации,
		НСтр("ru = 'Создать токен'"),
		"POST",
		"auth/token");
	
	// API для работы с заданиями
	
	ДобавитьОписаниеМетода(ОписанияМетодов,
		ИменаМетодов.СоздатьЗадание,
		НСтр("ru = 'Создать задание'"),
		"POST",
		"status/tasks",
		ИменаМетодов.РезультатВыполненияЗадания);
	
	ДобавитьОписаниеМетода(ОписанияМетодов,
		ИменаМетодов.РезультатВыполненияЗадания,
		НСтр("ru = 'Результат выполнения задания'"),
		"GET",
		"status/tasks/[ueid]");
	
	ДобавитьОписаниеМетода(ОписанияМетодов,
		ИменаМетодов.СтатусЗадания,
		НСтр("ru = 'Статус задания'"),
		"GET",
		"status/tasks/[ueid]/status");
	
	ДобавитьОписаниеМетода(ОписанияМетодов,
		ИменаМетодов.НастройкиЛимитов,
		НСтр("ru = 'Настройки лимитов'"),
		"GET",
		"status/limits");
	
	Возврат ОписанияМетодов;
	
КонецФункции

Процедура ДобавитьОписаниеМетода(ВсеМетоды, МетодВзаимодействия, Представление, МетодHttp, ШаблонАдресаРесурса, МетодПолученияРезультата = Неопределено)
	
	ОписаниеМетода = НовыйОписаниеМетода();
	ОписаниеМетода.МетодВзаимодействия = МетодВзаимодействия;
	ОписаниеМетода.Представление = Представление;
	ОписаниеМетода.МетодHttp = МетодHttp;
	ОписаниеМетода.ШаблонАдресаРесурса = ШаблонАдресаРесурса;
	ОписаниеМетода.МетодПолученияРезультата = МетодПолученияРезультата;
	
	ВсеМетоды.Вставить(МетодВзаимодействия, ОписаниеМетода);
	
КонецПроцедуры

Функция НовыйОписаниеМетода()
	
	Результат = Новый Структура;
	Результат.Вставить("МетодВзаимодействия", "");
	Результат.Вставить("Представление", "");
	Результат.Вставить("МетодHttp", "");
	Результат.Вставить("ШаблонАдресаРесурса", "");
	Результат.Вставить("МетодПолученияРезультата");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Настройки

Функция АдресСервиса()
	
	ПараметрыСервиса = ИнтеграцияСтатусСамозанятогоПовтИсп.ПараметрыСервиса();
	
	Если Не ПараметрыСервиса.Использовать Тогда 
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Сервис %1 не используется'"),
			ИнтеграцияСтатусСамозанятогоКлиентСервер.СинонимПодсистемы());
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыСервиса.АдресСервиса) Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не указан адрес сервиса %1'"),
			ИнтеграцияСтатусСамозанятогоКлиентСервер.СинонимПодсистемы());
	КонецЕсли;
	
	Возврат ПараметрыСервиса.АдресСервиса;
	
КонецФункции

Функция ИмяСервисаНаПорталеИТС()

	Возврат "1c-self-employed";

КонецФункции

Функция ТаймаутСоединения()
	
	Возврат 30;
	
КонецФункции

Функция МаксимальноКоличествоПопытокВыполненияМетода()
	
	Возврат 3;
	
КонецФункции

Функция МаксимальноКоличествоПопытокПолученияРезультата()
	
	Возврат Цел(МаксимальноеВремяОжиданияПолученияРезультата() / МаксимальнаяПаузаПриПолученияРезультата());
	
КонецФункции

Функция МаксимальнаяПаузаПриПолученияРезультата()
	
	Возврат 60; // В секундах
	
КонецФункции

Функция МаксимальноеВремяОжиданияПолученияРезультата()
	
	Возврат 3600; // В секундах
	
КонецФункции

Функция ЗаголовкиСекретов()
	
	Ключи = Новый Массив;
	Ключи.Добавить(НРег("User-Token"));
	
	Возврат Ключи;
	
КонецФункции

Функция ПараметрыРесурсов()
	
	Параметры = Новый Структура;
	Параметры.Вставить("РеквизитыТокена", "user_token");
	Параметры.Вставить("ТокенBase64", "token");
	Параметры.Вставить("СрокДействия", "expire_time");
	
	Параметры.Вставить("СтатусыСамозанятости", "statuses");
	Параметры.Вставить("НаДату", "date");
	Параметры.Вставить("ИНН", "inn");
	Параметры.Вставить("ДатаРегистрации", "registration_date");
	Параметры.Вставить("ПлательщикНПД", "is_selfemployed");
	Параметры.Вставить("ЭтоОкончательноеЗначение", "is_final");
	
	Параметры.Вставить("Ошибка", "error");
	Параметры.Вставить("Идентификатор", "ueid");
	Параметры.Вставить("Вид", "type");
	Параметры.Вставить("Описание", "detail");
	
	Параметры.Вставить("Статус", "status");
	
	Параметры.Вставить("МаксимальноеЧислоСтрокВЗадании", "task_size");
	Параметры.Вставить("ВремяЖизниЗадания", "task_lifetime");
	Параметры.Вставить("ВремяКэшированияТекущегоСтатуса", "current_status_timeout");
	
	Возврат Параметры;
	
КонецФункции

Функция ИменаСвойствСоЗначениямиДата()
	
	Поля = Новый Массив;
	Поля.Добавить("expire_time");
	Поля.Добавить("date");
	Поля.Добавить("registration_date");
	
	Возврат Поля;
	
КонецФункции

#КонецОбласти

#КонецОбласти
