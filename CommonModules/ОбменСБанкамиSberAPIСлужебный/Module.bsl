////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиSberAPIСлужебный: обмен со Сбербанком через сервис SberAPI.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ПлатежныеПоручения

// Отправляет платежные поручения в банк
//
// Параметры:
//   НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//   ТекущаяСессия - Структура - параметры установленных сессий.
//   МассивСообщенийОбмена - Массив из ДокументСсылка.СообщениеОбменСБанками
//
// Возвращаемое значение:
//   Структура - состоит из полей:
//    * СчетаТребующиеАутентификации - Массив из Строка - банковские счета,
//       платежные документы по которым не удалось выполнить отправку.
//    * НеотправленныеСообщенияОбмена - Массив из ДокументСсылка.СообщениеОбменСБанками - сообщения обмена,
//       которые не удалось отправить.
//    * ОтправленныеСообщенияОбмена - Массив из ДокументСсылка.СообщениеОбменСБанками - отправленные в банк документы.
//
Функция ОтправитьПлатежныеПоручения(НастройкаОбмена, ТекущаяСессия, МассивСообщенийОбмена) Экспорт
	
	БанковскиеСчета = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивСообщенийОбмена, "НомерСчета");
	СчетаТребующиеАутентификации = Новый Массив;
	НеотправленныеСообщенияОбмена = Новый Массив;
	ОтправленныеСообщенияОбмена = Новый Массив;
	
	// Дополнительный флаг, означающий что выявлена истекщая авторизация при попытке отправки документов
	ТребуетсяПовторнаяАутентификация = Ложь;
	
	Для Каждого КлючЗначение Из БанковскиеСчета Цикл
		
		ТокенДоступа = "";
		Если ТекущаяСессия = Неопределено Или Не ТекущаяСессия.Свойство("ТокенДоступа", ТокенДоступа)
			Или Не ЗначениеЗаполнено(ТокенДоступа) Тогда
			СчетаТребующиеАутентификации.Добавить(КлючЗначение.Значение);
			НеотправленныеСообщенияОбмена.Добавить(КлючЗначение.Ключ);
			Продолжить;
		КонецЕсли;
		
		ЗапрашиваемыеРеквизиты = Новый Структура("ИспользуетсяКриптография");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			НастройкаОбмена, ЗапрашиваемыеРеквизиты);
		Если ЗапрашиваемыеРеквизиты.ИспользуетсяКриптография Тогда
			
			МассивПодписей = ОбменСБанкамиСлужебный.ПодписиСообщенияОбмена(КлючЗначение.Ключ);
			Если МассивПодписей.Количество() = 0 Тогда
				СчетаТребующиеАутентификации.Добавить(КлючЗначение.Значение);
				НеотправленныеСообщенияОбмена.Добавить(КлючЗначение.Ключ);
				Продолжить;
			КонецЕсли;
			
			ДанныеСообщения = ОбменСБанкамиSberAPIСлужебныйВызовСервера.ДанныеПодписанногоСообщения(
				КлючЗначение.Ключ,
				НастройкаОбмена,
				МассивПодписей);
			ДанныеЭД = ПолучитьДвоичныеДанныеИзСтроки(ДанныеСообщения);
			
		Иначе
			
			ДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(КлючЗначение.Ключ);
			
		КонецЕсли;
		
		Результат = Неопределено;
		ТребуетсяАутентификация = Ложь;
		ОтправитьПлатежныйДокумент(НастройкаОбмена, ТокенДоступа, ДанныеЭД, Результат, ТребуетсяАутентификация);
		
		Если ТребуетсяАутентификация Тогда
			СчетаТребующиеАутентификации.Добавить(КлючЗначение.Значение);
			НеотправленныеСообщенияОбмена.Добавить(КлючЗначение.Ключ);
			ТребуетсяПовторнаяАутентификация = Истина;
			Продолжить;
		КонецЕсли;
		
		ОтправленныеСообщенияОбмена.Добавить(КлючЗначение.Ключ);
		
		УстановитьСтатусДокумента(КлючЗначение.Ключ, Результат)
		
	КонецЦикла;
	
	СчетаТребующиеАутентификации = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СчетаТребующиеАутентификации);
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("СчетаТребующиеАутентификации", СчетаТребующиеАутентификации);
	СтруктураВозврата.Вставить("НеотправленныеСообщенияОбмена", НеотправленныеСообщенияОбмена);
	СтруктураВозврата.Вставить("ОтправленныеСообщенияОбмена", ОтправленныеСообщенияОбмена);
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		НастройкаОбмена, "Банк, ПоказыватьОкноПодтвержденияПлатежей");
	СтруктураВозврата.Вставить("Банк", РеквизитыНастройкиОбмена.Банк);
	СтруктураВозврата.Вставить(
		"ПоказыватьОкноПодтвержденияПлатежей", РеквизитыНастройкиОбмена.ПоказыватьОкноПодтвержденияПлатежей);
	СтруктураВозврата.Вставить("ТребуетсяПовторнаяАутентификация", ТребуетсяПовторнаяАутентификация);
	
	Возврат СтруктураВозврата
	
КонецФункции

// Формирует данные электронного документа Платежное поручение
//
// Параметры:
//  СсылкаНаДокумент - ДокументСсылка - ссылка на документ информационной базы,
//                                      на основании которого формируется электронный документ;
//  ДеревоДанных - ДеревоЗначений - данные, помещаемые в электронный документ
//  АдресФайлаВоВременномХранилище - Строка - адрес во временном хранилище с данными, возвращаемыми процедурой.
//
Процедура СформироватьПлатежноеПоручение(СсылкаНаДокумент,
										 ДеревоДанных,
										 АдресФайлаВоВременномХранилище,
										 Подписывать = Ложь) Экспорт
	
	ТекстОшибки = "";
	
	ДанныеПлатежа = Новый Структура;
	Сумма = ЗначениеРеквизитаВДереве(ДеревоДанных, "Сумма");
	ДанныеПлатежа.Вставить("amount", Сумма);
	Дата = ЗначениеРеквизитаВДереве(ДеревоДанных, "Дата");
	ДанныеПлатежа.Вставить("date", Формат(Дата, "ДФ=yyyy-MM-dd;"));
	
	// Вид платежа должен быть в нижнем регистре.
	ВидПлатежа = НРег(ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлатежа.ВидПлатежа"));
	Если Не ЗначениеЗаполнено(ВидПлатежа) Или (ВидПлатежа <> "электронно" И ВидПлатежа <> "срочно") Тогда
		ВидПлатежа = "0"
	КонецЕсли;
	
	ДанныеПлатежа.Вставить("deliveryKind", ВидПлатежа);
	
	ЭтоПеречислениеВБюджет = Ложь;
	ЗаполнитьБюджетныеРеквизиты(ДеревоДанных, ДанныеПлатежа, ЭтоПеречислениеВБюджет);
	
	ИдДокумента = ЗначениеРеквизитаВДереве(ДеревоДанных, "ИдДокумента");
	ДанныеПлатежа.Вставить("externalId", ИдДокумента);
	
	КодВидаДохода = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлатежа.КодВидаДохода");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "incomeTypeCode", КодВидаДохода);
	
	Номер = СокрЛП(ЗначениеРеквизитаВДереве(ДеревоДанных, "Номер"));
	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Номер) Тогда
		ТекстОшибки = ТекстОшибки + ?(ПустаяСтрока(ТекстОшибки), "", Символы.ПС)
			+ НСтр("ru = 'Номер документа должен содержать только цифры.'");
	КонецЕсли;
	
	ДанныеПлатежа.Вставить("number", Номер);
	ДанныеПлатежа.Вставить("operationCode", "01");
	
	ЗаполнитьПлательщикаИПолучателя(ДеревоДанных, ДанныеПлатежа, ЭтоПеречислениеВБюджет);
	
	Очередность = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлатежа.Очередность");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "priority", Очередность);
	
	НазначениеПлатежа = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлатежа.НазначениеПлатежа");
	ДанныеПлатежа.Вставить("purpose", СокрЛП(НазначениеПлатежа));
	ДанныеПлатежа.Вставить("vat", "null"); // указание банку, чтобы брал НДС из текста назначения платежа
	
	Если Подписывать Тогда
		
		ДайджестПлатежногоПоручения = ДайджестПлатежногоПорученияИзСтруктуры(ДанныеПлатежа);
		ДайджестПлатежногоПорученияBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(
			ДайджестПлатежногоПоручения);
		ДайджестПлатежногоПорученияBase64 = СтрЗаменить(
			СтрЗаменить(ДайджестПлатежногоПорученияBase64, Символы.ВК, ""), Символы.ПС, "");
			
		// Заполним шаблон дайджеста платежного поручения. Потом он будет подписан и заменится данными с подписью.
		//  Пока вместо реального ИдентификаторСертификата передаем "uuid", после подписания он будет так же
		//  заменен на реальный идентификатор.
		ЗаполнитьДанныеДайджестаПлатежногоПоручения(ДанныеПлатежа, ДайджестПлатежногоПорученияBase64, "uuid");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ВызватьИсключение ТекстОшибки;
	Иначе
		
		СтрокаJSON = ОбменСБанкамиСлужебный.ЗначениеВJSON(ДанныеПлатежа);
		// Необходимо передавать null как значение, а не строковой литерал
		СтрокаJSON = СтрЗаменить(СтрокаJSON, """null""", "null");
		ДвоичныеДанные = ДвоичныеДанныеИзСтроки(СтрокаJSON);
		
		АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	КонецЕсли;
	
КонецПроцедуры

// Отравляет в банк документы по определенному счету
//
// Параметры:
//  Параметры - Структура - параметры выполнения, содержит поля:
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена, если она есть.
//   * ТекущаяСессия - Структура - параметры установленной сессии.
//   * ДанныеДляОтправки - Структура - отправляемые данные.
//  Адрес - Строка - адрес временного хранилища, содержащий структуру с полями:
//   * СтруктураВозврата - Структура - результат отправки.
//
Процедура ОтправитьПлатежныеПорученияПоСчету(Параметры, Адрес) Экспорт
	
	Результат = ОтправитьПлатежныеПоручения(
		Параметры.НастройкаОбмена, Параметры.ТекущаяСессия, Параметры.ДанныеДляОтправки.НеотправленныеСообщенияОбмена);
	ПоместитьВоВременноеХранилище(Результат, Адрес);
	
КонецПроцедуры

// Отправляет в банк платежный документ.
//
// Параметры:
//   НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//   ТокенДоступа - Строка - токен аутентификации на сервисе SberAPI
//   ДанныеЭД - ДвоичныеДанные - двоичные данные платежного поручения
//   ОтветБанка - Структура - данные ответа из JSON
//   ТребуетсяАутентификация - Булево - если Истина, то требуется повторная аутентификация.
//
Процедура ОтправитьПлатежныйДокумент(НастройкаОбмена,
									 ТокенДоступа,
									 ДанныеЭД,
									 ОтветБанка,
									 ТребуетсяАутентификация) Экспорт
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(НастройкаОбмена, "ОтправитьПлатежныйДокумент");
	
	АдресРесурса = "fintech/api/v1/payments";
	
	ТекстЗапроса = ОбменСБанкамиСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(ДанныеЭД);
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "POST";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.ТокенДоступа = ТокенДоступа;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	Если Не РезультатВыполнения.Статус И РезультатВыполнения.КодСостояния <> 201 Тогда
		ВидОперации = НСтр("ru = 'Отправка платежного поручения через SberAPI'");
		СообщениеОбОшибке = РезультатВыполнения.СообщениеОбОшибке;
		Если РезультатВыполнения.КодСостояния = 401 Тогда
			ДанныеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДанныеОшибки(
				РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			Если ДанныеОшибки.ТэгОшибки = "AUTH_TOKEN_IS_EXPIRED" Или ДанныеОшибки.ТэгОшибки = "UNAUTHORIZED" Тогда
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
					ВидОперации, ДанныеОшибки.ТекстСообщения, , НастройкаОбмена);
				ТребуетсяАутентификация = Истина;
				
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РезультатВыполнения.КодСостояния) Тогда
			ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
				РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			КраткоеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.КороткоеПредставлениеОшибки(
				РезультатВыполнения.Тело);
			Шаблон = НСтр("ru = 'При отправке платежного поручения произошла ошибка:
								|Сообщение об ошибке %1
								|Тело ответа: %2'");
			ТекстОшибки = СтрШаблон(Шаблон, СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
				ВидОперации, ТекстОшибки, КраткоеПредставлениеОшибки, НастройкаОбмена);
		КонецЕсли;
		
		Если РезультатВыполнения.КодСостояния = 500 Тогда
			КраткоеПредставлениеОшибки = СтрШаблон(НСтр("ru = '%1. %2'"),
				КраткоеПредставлениеОшибки, НСтр("ru = 'Документы не отправлены.'"));
		КонецЕсли;
		
		ВызватьИсключение КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	ОтветБанка = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ДайджестЭлектронногоДокумента

// Формирует дайджест электронного документа из электронного документа
//
// Параметры:
//   ВидЭД - ПеречислениеСсылка.ВидыЭДОбменСБанками - вид электронного документа.
//   АдресФайлаВоВременномХранилище - Строка - адрес временного хранилища.
//
// Возвращаемое значение:
//   Строка - дайджест электронного документа, закодированный в Base64
//
Функция ДайджестЭлектронногоДокумента(ВидЭД, АдресФайлаВоВременномХранилище) Экспорт
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайлаВоВременномХранилище);
	
	Если ВидЭД = Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение Тогда
		
		СтрокаВозврата = ДайджестПлатежногоПоручения(ДвоичныеДанныеФайла);
		
	КонецЕсли;
	
	Возврат ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(СтрокаВозврата);
	
КонецФункции

// Заполняет данные дайджеста платежного поручения
//
// Параметры:
//   ДанныеПлатежа - Структура - данные платежа, куда требуется добавить дайджест
//   ДайджестBase64 - Строка - дайджест данных платежного поручения в Base64
//   ИдентификаторСертификата - Строка - идентификатор сертификата 
//
Процедура ЗаполнитьДанныеДайджестаПлатежногоПоручения(ДанныеПлатежа, ДайджестBase64, ИдентификаторСертификата) Экспорт
	
	СтруктураПодписи = Новый Структура();
	СтруктураПодписи.Вставить("certificateUuid", ИдентификаторСертификата);
	СтруктураПодписи.Вставить("base64Encoded", ДайджестBase64);
	
	digestSignatures = Новый Массив;
	digestSignatures.Добавить(СтруктураПодписи);
	
	ДанныеПлатежа.Вставить("digestSignatures", digestSignatures);
	
КонецПроцедуры

#КонецОбласти

#Область Выписка

// Получает из банка выписки за период по дням
//
// Параметры:
//  Параметры - Структура - параметры выполнения, содержит поля:
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена, если она есть.
//   * ИдентификаторСессии - Строка - идентификатор сессии 1С
//   * ТокенДоступа - Строка - токен, полученный из сервиса SberAPI
//   * НомерСчета - Строка - номер счета
//   * ДатаНачала - Дата - дата начала периода запроса выписки
//   * ДатаОкончания - Дата - дата окончания периода запроса выписки
//  Адрес - Строка - адрес временного хранилища содержащий структуру с полями:
//   * ТребуетсяАутентификация - Булево - если Истина, то закончилось время жизни сессии
//   									  и нужно заново пройти процесс аутентификации.
//   * Выписки - Массив из ДокументСсылка.СообщениеОбменСБанками - выписки банка.
//   			 Возвращается, когда ТребуетсяАутентификация = Ложь.
//
Процедура ПолучитьВыпискуБанка(Параметры, Адрес) Экспорт
	
	Дата = НачалоДня(Параметры.ДатаНачала);
	ДатаОкончания = НачалоДня(Параметры.ДатаОкончания);
	
	ТребуетсяАутентификация = Ложь;
	
	Если Параметры.Свойство("ТокенДоступа") Тогда
		ТокенДоступа = Параметры.ТокенДоступа;
	ИначеЕсли Параметры.Свойство("ТекущаяСессия") Тогда
		ТокенДоступа = Параметры.ТекущаяСессия.ТокенДоступа;
	КонецЕсли;
	
	ВыпискиПоСчету = ПолучитьВыпискиПоСчетамБанка(Параметры, ТокенДоступа,
		Параметры.МассивБанковскихСчетов, Дата, ДатаОкончания, ТребуетсяАутентификация);
	
	Если ТребуетсяАутентификация Тогда
		ПоместитьВоВременноеХранилище(Новый Структура("ТребуетсяАутентификация", Истина), Адрес);
		Возврат;
	КонецЕсли;
	
	ДлинаНомера = 0;
	Для Каждого Номер Из Параметры.МассивБанковскихСчетов Цикл
		ДлинаНомера = Макс(СтрДлина(Номер), ДлинаНомера);
	КонецЦикла;
	
	Если ДлинаНомера = 0 Тогда
		ДлинаНомера = 34; // максимальная длина IBAN-счетов
	КонецЕсли;
	
	ТаблицаВыписок = Новый ТаблицаЗначений;
	ТаблицаВыписок.Колонки.Добавить("Номер", ОбщегоНазначения.ОписаниеТипаСтрока(ДлинаНомера));
	ТаблицаВыписок.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаВыписок.Колонки.Добавить("Данные");
	
	СоздаватьОперацииВыписки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Параметры,
		"СоздаватьОперацииВыписки",
		Ложь);
	
	Для Каждого КлючЗначениеВыписки Из ВыпискиПоСчету Цикл
		НомерСчета    = КлючЗначениеВыписки.Ключ;
		ДанныеВыписок = КлючЗначениеВыписки.Значение;
		Для Каждого КлючЗначение Из ДанныеВыписок Цикл
			НоваяВыписка = СохранитьВыписку(
				Параметры.НастройкаОбмена, НомерСчета, КлючЗначение.Значение, КлючЗначение.Ключ, СоздаватьОперацииВыписки);
			Выписка = ТаблицаВыписок.Добавить();
			Выписка.Номер  = НомерСчета;
			Выписка.Период = КлючЗначение.Ключ;
			Выписка.Данные = НоваяВыписка;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаВыписок.Сортировать("Номер, Период");
	Выписки = ТаблицаВыписок.ВыгрузитьКолонку("Данные");
	
	ПоместитьВоВременноеХранилище(Новый Структура("Выписки", Выписки), Адрес);
	
КонецПроцедуры

#КонецОбласти

#Область СтатусыДокументов

// Получает из банка статус документа по SberAPI и меняет статус документа СообщениеОбмена
//
// Параметры:
//  Параметры - Структура - параметры выполнения, содержит поля:
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена, если она есть.
//   * ИдентификаторСессии - Строка - идентификатор сессии 1С
//   * ТокенДоступа - Строка - токен, полученный из сервиса SberAPI
//   * НомерСчета - Строка - номер счета
//   * ИдентификаторДокумента - Строка - идентификатор платежного документа
//   * СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение с данными платежа
//  Адрес - Строка - адрес временного хранилища, содержит структуру с полями:
//   * Статус - ПеречислениеСсылка.СтатусыОбменСБанками - новый статус электронного документа.
//   * ТребуетсяАутентификация - Булево - если Истина, то необходимо пройти процесс аутентификации.
//
Процедура ПолучитьИУстановитьСтатусДокумента(Параметры, Адрес) Экспорт
	
	Результат = Неопределено;
	ТребуетсяАутентификация = Ложь;
	Результат = ПолучитьСтатусДокумента(
		Параметры.НастройкаОбмена,
		Параметры.ТокенДоступа,
		Параметры.ИдентификаторДокумента,
		ТребуетсяАутентификация);
	
	Если ТребуетсяАутентификация Тогда
		СтруктураВозврата = Новый Структура("ТребуетсяАутентификация", Истина);
		ПоместитьВоВременноеХранилище(СтруктураВозврата, Адрес);
		Возврат;
	КонецЕсли;
	
	УстановитьСтатусДокумента(Параметры.СообщениеОбмена, Результат);
	
	ТекущийСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.СообщениеОбмена, "Статус");
	
	СтруктураВозврата = Новый Структура("Статус", ТекущийСтатус);
	ПоместитьВоВременноеХранилище(СтруктураВозврата, Адрес);
	
КонецПроцедуры

// Получает из банка статус электронного документа
//
// Параметры:
//   НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена, если она есть.
//   ТокенДоступа - Строка - токен аутентификации в сервисе SberAPI
//   ИдентификаторДокумента - Строка - идентификатор платежного документа
//   ТребуетсяАутентификация - Булево - если Истина, то необходимо пройти процесс аутентификации.
//
// Возвращаемое значение:
//   Структура - данные, полученные из сервиса SberAPI.
//
Функция ПолучитьСтатусДокумента(НастройкаОбмена,
								ТокенДоступа,
								ИдентификаторДокумента,
								ТребуетсяАутентификация) Экспорт
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(НастройкаОбмена, "ПолучитьСтатусДокумента");
	
	АдресРесурса = "fintech/api/v1/payments/" + ИдентификаторДокумента + "/state";
	
	СтрокаЗапроса = "";
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "GET";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.ТокенДоступа = ТокенДоступа;
	ПараметрыHTTPЗапроса.Данные = СтрокаЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	Если Не РезультатВыполнения.Статус Тогда
		СообщениеОбОшибке = РезультатВыполнения.СообщениеОбОшибке;
		Если ЗначениеЗаполнено(РезультатВыполнения.КодСостояния) Тогда
			ВидОперации = НСтр("ru = 'Получение данных через сервис SberAPI'");
			Если РезультатВыполнения.КодСостояния = 401 Тогда
				ДанныеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДанныеОшибки(
					РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
				Если ДанныеОшибки.ТэгОшибки = "AUTH_TOKEN_IS_EXPIRED" Или ДанныеОшибки.ТэгОшибки = "UNAUTHORIZED" Тогда
					ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
						ВидОперации, ДанныеОшибки.ТекстСообщения, ДанныеОшибки.ТекстСообщения, НастройкаОбмена);
					ТребуетсяАутентификация = Истина;
					
					Возврат Неопределено;
				КонецЕсли;
			КонецЕсли;
			
			Шаблон = НСтр("ru = 'При получении состояния документа через сервис SberAPI произошла ошибка.
								|Код ошибки: %1
								|Сообщение об ошибке: %2
								|Тело ответа: %3'");
			ТекстОшибки = СтрШаблон(Шаблон, РезультатВыполнения.КодСостояния, СообщениеОбОшибке, РезультатВыполнения.Тело);
			ТекстСообщения = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
				РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			Если СтрНайти(НРег(РезультатВыполнения.Тело), "<html>") > 0 Тогда
				СообщениеОбОшибке = РезультатВыполнения.Тело;
			Иначе
				СообщениеОбОшибке = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.СформироватьДетальныйТекстСообщения(
					ТекстСообщения, СообщениеОбОшибке);
			КонецЕсли;
			
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
				ВидОперации, ТекстОшибки, ТекстСообщения, НастройкаОбмена);
		КонецЕсли;
		
		ВызватьИсключение СообщениеОбОшибке;
	КонецЕсли;
	
	Результат = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
	
	Возврат Результат;
	
КонецФункции

// Получает статусы документов через сервис SberAPI
//
// Параметры:
//  Параметры - Структура - параметры выполнения процедуры в фоне:
//   * ТекущаяСессия - Структура - параметры установленных сессий.
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//   * НомераСчетов - Массив из Строка - номер счетов, по которым нужно выполнить синхронизацию.
//  Адрес - Строка - адрес временного хранилища с результатом выполнения
//   * СчетаТребующиеАутентификацию - Массив - содержит номера счетов, для которых необходимо выполнить аутентификацию.
//   * КоличествоПолученных - Число - количество полученных выписок банка.
//
Процедура ПолучитьСтатусыДокументов(Параметры, Адрес) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("СчетаТребующиеАутентификацию", Новый Массив);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДляПолученияСтатусовПлатежей();
	
	Запрос.УстановитьПараметр("НастройкаОбмена", Параметры.НастройкаОбмена);
	
	Если ЗначениеЗаполнено(Параметры.МассивБанковскихСчетов) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбораПоСчетам", "СообщениеОбменСБанками.НомерСчета В (&НомераСчетов)");
		Запрос.УстановитьПараметр("МассивБанковскихСчетов", Параметры.МассивБанковскихСчетов);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбораПоСчетам", "ИСТИНА");
	КонецЕсли;
	
	МассивБанковскихСчетов = Новый Массив;
	
	ВыборкаПоСчетам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСчетам.Следующий() Цикл
		
		Если Параметры.ТекущаяСессия.ДоступныеСчета.Получить(ВыборкаПоСчетам.НомерСчета) = Неопределено Тогда
			СтруктураВозврата.СчетаТребующиеАутентификацию.Добавить(ВыборкаПоСчетам.НомерСчета);
			Продолжить;
		КонецЕсли;
		
		ТокенДоступа = Параметры.ТекущаяСессия.ДоступныеСчета.Получить(ВыборкаПоСчетам.НомерСчета);
		Если Не Параметры.Свойство("ТокенДоступа") Тогда
			Параметры.Вставить("ТокенДоступа", ТокенДоступа);
		КонецЕсли;
		ВыборкаИдентификаторов = ВыборкаПоСчетам.Выбрать();
		
		Пока ВыборкаИдентификаторов.Следующий() Цикл
			
			МассивБанковскихСчетов.Добавить(ВыборкаПоСчетам.НомерСчета);
			ТребуетсяАутентификация = Ложь;
			Результат = ПолучитьСтатусДокумента(Параметры.НастройкаОбмена, ТокенДоступа,
				ВыборкаИдентификаторов.Идентификатор, ТребуетсяАутентификация);
			Если ТребуетсяАутентификация Тогда
				СтруктураВозврата.СчетаТребующиеАутентификацию.Добавить(ВыборкаПоСчетам.НомерСчета);
				Прервать;
			КонецЕсли;
			
			УстановитьСтатусДокумента(ВыборкаИдентификаторов.Ссылка, Результат);
			
		КонецЦикла;
	КонецЦикла;
	
	СтруктураВозврата.Вставить("КоличествоПолученных", 0);
	
	Если ЗначениеЗаполнено(СтруктураВозврата.СчетаТребующиеАутентификацию) Тогда
		ПоместитьВоВременноеХранилище(СтруктураВозврата, Адрес);
		
		Возврат;
	Иначе
		ОбменСБанкамиСлужебный.СохранитьДатуСинхронизации(Параметры.НастройкаОбмена);
	КонецЕсли;
	
	Если МассивБанковскихСчетов.Количество() > 0 Тогда
		ПолучитьПоследниеВыписки(Параметры, МассивБанковскихСчетов, СтруктураВозврата.КоличествоПолученных);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураВозврата, Адрес);
	
КонецПроцедуры

// Устанавливает статус документа в соответствии со статусом в банке
//
// Параметры:
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - текущий платежный документ
//  Результат - Структура - ответ банка из JSON
//
Процедура УстановитьСтатусДокумента(СообщениеОбмена, Результат) Экспорт
	
	Если Результат.bankStatus = "ACCEPTED" Или Результат.bankStatus = "ACCEPTED_BY_ABS"
		Или Результат.bankStatus = "DELIVERED"
		Или Результат.bankStatus = "EXPORTED" // неактуальный статус
		Или Результат.bankStatus = "DELIVERED_RZK"
		Или Результат.bankStatus = "FRAUDALLOW" Или Результат.bankStatus = "SUBMITTED" Тогда
		ОбменСБанкамиСлужебный.УстановитьСтатусСообщенияОбмена(СообщениеОбмена, Перечисления.СтатусыОбменСБанками.Принят);
	ИначеЕсли Результат.bankStatus = "FRAUDREVIEW" Или Результат.bankStatus = "FRAUDSENT"
		Или Результат.bankStatus = "SENDING_TO_RZK" Или Результат.bankStatus = "PROCESSING_RZK"
		Или Результат.bankStatus = "PROCESSING" Тогда
		ОбменСБанкамиСлужебный.УстановитьСтатусСообщенияОбмена(СообщениеОбмена, Перечисления.СтатусыОбменСБанками.Доставлен);
	ИначеЕсли Результат.bankStatus = "CARD2" Или Результат.bankStatus = "DELAYED" Тогда
		ОбменСБанкамиСлужебный.УстановитьСтатусСообщенияОбмена(СообщениеОбмена, Перечисления.СтатусыОбменСБанками.Приостановлен);
	ИначеЕсли Результат.bankStatus = "CREATED" Или Результат.bankStatus = "FRAUDSMS" Тогда
		ОбменСБанкамиСлужебный.УстановитьСтатусСообщенияОбмена(СообщениеОбмена, Перечисления.СтатусыОбменСБанками.НеПодтвержден);
	ИначеЕсли Результат.bankStatus = "IMPLEMENTED" Тогда
		ОбменСБанкамиСлужебный.УстановитьСтатусСообщенияОбмена(СообщениеОбмена, Перечисления.СтатусыОбменСБанками.Исполнен);
	ИначеЕсли Результат.bankStatus = "PARTSIGNED" Тогда
		ОбменСБанкамиСлужебный.УстановитьСтатусСообщенияОбмена(СообщениеОбмена, Перечисления.СтатусыОбменСБанками.ЧастичноПодписан);
	ИначеЕсли Результат.bankStatus = "SIGNED" Тогда
		ОбменСБанкамиСлужебный.УстановитьСтатусСообщенияОбмена(СообщениеОбмена, Перечисления.СтатусыОбменСБанками.Подписан);
	ИначеЕсли Результат.bankStatus = "CHECKERROR" Или Результат.bankStatus = "CHECKERROR_BANK"
		Или Результат.bankStatus = "FRAUDDENY" Или Результат.bankStatus = "INVALIDEDS"
		Или Результат.bankStatus = "REFUSED_BY_RZK" Или Результат.bankStatus = "NOT_ACCEPTED_RZK"
		Или Результат.bankStatus = "REFUSEDBYBANK" Или Результат.bankStatus = "RECALL"
		Или Результат.bankStatus = "REFUSEDBYABS" Или Результат.bankStatus = "REQUISITEERROR" Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Статус", Перечисления.СтатусыОбменСБанками.ОтклоненБанком);
		СтруктураПараметров.Вставить("ПричинаОтклонения", Результат.bankComment);
		ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(СообщениеОбмена, СтруктураПараметров);
	ИначеЕсли Результат.bankStatus = "DELETED" Или Результат.bankStatus = "REQUESTED_RECALL" Тогда
		ОбменСБанкамиСлужебный.УстановитьСтатусСообщенияОбмена(СообщениеОбмена, Перечисления.СтатусыОбменСБанками.Аннулирован);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Авторизация

// Производит аутентификацию по логину на сервере Сбербанка
//
// Параметры:
//  Параметры - Структура - параметры выполнения процедуры:
//    * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена.
//    * Логин - Строка - логин пользователя в системе банка
//    * Пароль - Строка - пароль для логина.
//    * ФродПараметры - Структура - параметры фрод-мониторинга, полученные на клиенте.
//  АдресРезультата - Строка - адрес временного хранилища для помещения результата.
//    * Структура - возвращаемые параметры:
//         ** ТребуетсяСМСАутентификация - Булево - требуется подтверждение сессии;
//         ** ТребуетсяТокен - Булево - пользователь должен использовать токен;
//
Процедура ВыполнитьАутентификациюПоЛогину(Параметры, АдресРезультата) Экспорт
	
	ТекстОшибки = "";
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(Параметры, "ВыполнитьАутентификациюПоЛогину");
	
	ПараметрыВозврата = Новый Структура;
	ПараметрыВозврата.Вставить("ТребуетсяСМСАутентификация", Ложь);
	ПараметрыВозврата.Вставить("ТребуетсяТокен", Ложь);
	ПараметрыВозврата.Вставить("ИдентификаторСессии", "");
	
	АдресРесурса = "fintech/api/auth/v1/prelogin";
	
	userLogin = Новый Структура("userLogin", Параметры.Логин);
	ТекстЗапроса = ОбменСБанкамиСлужебный.ЗначениеВJSON(userLogin);
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "POST";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = Параметры.НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	ТэгОшибки = "";
	Если Не РезультатВыполнения.Статус  Тогда
		ДанныеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДанныеОшибки(
			РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
		ТэгОшибки = ДанныеОшибки.ТэгОшибки;
	КонецЕсли;
	
	Если РезультатВыполнения.Статус Тогда
		
		JSON = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
		sessionIdBase64Строка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "sessionId", "");
		sessionId = ПолучитьИДСессииИзBase64Строки(sessionIdBase64Строка);
		
		srpB = JSON.srpB; // B-параметр
		srpS = JSON.srpS; // Соль
		
		K = "";
		A = "";
		РассчитатьСверткуПароляСбербанк(Параметры.Логин, Параметры.Пароль, srpS, srpB, K, A);
		srpM = Base64Строка(K);
		srpA = Base64Строка(A);
		
		Данные = Новый Структура();
		Данные.Вставить("sessionId", sessionId);
		Данные.Вставить("srpM", srpM);
		Данные.Вставить("srpA", srpA);
		Данные.Вставить("fraudParams", Параметры.ФродПараметры);

		ТекстЗапроса = ОбменСБанкамиСлужебный.ЗначениеВJSON(Данные);
		
		АдресРесурса = "fintech/api/auth/v1/login";
		
		ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
		ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
		ПараметрыHTTPЗапроса.ВидЗапроса = "POST";
		ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
		ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
		ПараметрыHTTPЗапроса.НастройкаОбмена = Параметры.НастройкаОбмена;
		ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
		ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
		
		РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
		
		УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
		
		ВидОперации = НСтр("ru = 'Аутентификация SberAPI'");
		
		Если Не РезультатВыполнения.Статус Тогда
			ДанныеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДанныеОшибки(
				РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			ТэгОшибки = ДанныеОшибки.ТэгОшибки;
		КонецЕсли;
		
		Если РезультатВыполнения.Статус Тогда
			
			JSON = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
			sessionIdBase64Строка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "sessionId", "");
			sessionId = ПолучитьИДСессииИзBase64Строки(sessionIdBase64Строка);
			srpM2 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "srpM2", ""); // M2 - параметр
			requireSmsVerification = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "requireSmsVerification", Ложь); // СМС-подтверждение
			
			ПараметрыВозврата.ИдентификаторСессии = sessionId;
			
			Если requireSmsVerification = Истина Тогда
				
				ПараметрыВозврата.ТребуетсяСМСАутентификация = Истина;
				
			КонецЕсли;
			
		ИначеЕсли РезультатВыполнения.КодСостояния = 401 И ТэгОшибки = "NEED_TOKEN_TO_ACCESS" Тогда
			
			ПараметрыВозврата.ТребуетсяТокен = Истина;
			
		Иначе
			
			ИнформацияОбОшибке = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТекстОшибки(
				РезультатВыполнения.КодСостояния, ТэгОшибки);
			
			ТекстСообщения = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
				РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			
			Если ИнформацияОбОшибке = Неопределено Тогда
				ШаблонОшибки = НСтр("ru = 'При аутентификации произошла ошибка.
					|Код: %1. Пояснение: %2'");
				
				ТекстОшибки = СтрШаблон(ШаблонОшибки, РезультатВыполнения.КодСостояния, ТекстСообщения);
			Иначе
				
				Если ИнформацияОбОшибке.Код = "01" Тогда
					ТекстОшибки = ИнформацияОбОшибке.Описание;
				Иначе
					ШаблонОшибки = НСтр("ru = 'При аутентификации произошла ошибка.
						|Код: %1
						|Описание: %2'");
					ТекстОшибки = СтрШаблон(ШаблонОшибки, РезультатВыполнения.КодСостояния, ИнформацияОбОшибке.Описание);
				КонецЕсли;
				
			КонецЕсли;
			
			ШаблонОшибки = НСтр("ru = '%1
				|Детали ошибки: %2'");
			ПодробноеПредставлениеОшибки = СтрШаблон(ШаблонОшибки, ТекстОшибки, ТекстСообщения);
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
				ВидОперации, ПодробноеПредставлениеОшибки, ПодробноеПредставлениеОшибки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
	Иначе
		
		УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
		
		ВидОперации = НСтр("ru = 'Преаутентификация SberAPI'");
		
		ИнформацияОбОшибке = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТекстОшибки(
			РезультатВыполнения.КодСостояния, ТэгОшибки);
		
		ТекстСообщения = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
			РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			
		Если ИнформацияОбОшибке = Неопределено И ТекстСообщения = Неопределено
				И РезультатВыполнения.КодСостояния = 0 и ЗначениеЗаполнено(РезультатВыполнения.СообщениеОбОшибке) Тогда
			ШаблонОшибки = НСтр("ru = 'При преаутентификации произошла ошибка.
				|Пояснение: %1'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, РезультатВыполнения.СообщениеОбОшибке);
		ИначеЕсли ИнформацияОбОшибке = Неопределено Тогда
			ШаблонОшибки = НСтр("ru = 'При преаутентификации произошла ошибка.
				|Код: %1.
				|Пояснение: %2'");
			
			Если РезультатВыполнения.КодСостояния = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.КодВнутренняяОшибкаСервера() Тогда
				ТекстОшибки = ТекстСообщения;
			Иначе
				ТекстОшибки = СтрШаблон(ШаблонОшибки, РезультатВыполнения.КодСостояния, ТекстСообщения);
			КонецЕсли;
		Иначе
			
			Если ИнформацияОбОшибке.Код = "01" Тогда
				ТекстОшибки = ИнформацияОбОшибке.Описание;
			Иначе
				ШаблонОшибки = НСтр("ru = 'При преаутентификации произошла ошибка.
					|Код: %1
					|Описание: %2'");
				ТекстОшибки = СтрШаблон(ШаблонОшибки, РезультатВыполнения.КодСостояния, ИнформацияОбОшибке.Описание);
			КонецЕсли;
			
		КонецЕсли;
		
		ШаблонОшибки = НСтр("ru = '%1
			|Детали ошибки: %2'");
		ПодробноеПредставлениеОшибки = СтрШаблон(ШаблонОшибки, ТекстОшибки, ТекстСообщения);
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
			ВидОперации, ПодробноеПредставлениеОшибки, ПодробноеПредставлениеОшибки);
		
		Если СтрНайти(ВРег(ПодробноеПредставлениеОшибки), ВРег("ERR_SECURE_CONNECT_FAIL")) > 0
			Или СтрНайти(ВРег(ПодробноеПредставлениеОшибки), ВРег("ERR_ACCESS_DENIED")) > 0
			Или СтрНайти(ВРег(ПодробноеПредставлениеОшибки), ВРег("CERTIFICATE_ACCESS_EXCEPTION")) > 0
			Или СтрНайти(ВРег(ПодробноеПредставлениеОшибки), ВРег("The SSL certificate error")) > 0
			Или СтрНайти(ВРег(ПодробноеПредставлениеОшибки), ВРег("Forbidden certificate")) > 0
			Или СтрНайти(ВРег(ПодробноеПредставлениеОшибки), ВРег("No required SSL certificate was sent")) > 0 Тогда
			// Для данных ошибок нужно выводить специальное окно ошибки интернет-соединения.
			ТекстОшибки = ПодробноеПредставлениеОшибки;
		КонецЕсли;
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	ПоместитьВоВременноеХранилище(ПараметрыВозврата, АдресРезультата);
	
КонецПроцедуры

Процедура ОтправитьОдноразовыйПароль(Параметры, АдресРезультата) Экспорт
	
	ПараметрыВозврата = Новый Структура;
	ПараметрыВозврата.Вставить("ИдентификаторСессии", "");
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(Параметры, "ОтправитьОдноразовыйПароль");
	
	ИдентификаторСессии = Параметры.ИдентификаторСессии;
	
	ТекстЗапроса = "";
	ПараметрыСессии = Новый Структура();
	ПараметрыСессии.Вставить("sessionId", ИдентификаторСессии);
	ПараметрыСессии.Вставить("smsCode", Параметры.ОдноразовыйПароль);
	ТекстЗапроса = ОбменСБанкамиСлужебный.ЗначениеВJSON(ПараметрыСессии);
	
	АдресРесурса = "fintech/api/auth/v1/sms-session-verification";
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "POST";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = Параметры.НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	Если РезультатВыполнения.Статус Тогда
		
		JSON = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
		sessionIdBase64Строка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "sessionId", "");
		sessionId = ПолучитьИДСессииИзBase64Строки(sessionIdBase64Строка);
		
		ПараметрыВозврата.Вставить("ИдентификаторСессии", sessionId);
		
	Иначе
		
		ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурнал(Параметры.НастройкаОбмена, "sms-session-verification", "", "", "");
		ВидОперации = НСтр("ru = 'SMS аутентификация'");
		ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
			РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
		КраткоеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.КороткоеПредставлениеОшибки(
			РезультатВыполнения.Тело);
		Шаблон = НСтр("ru = 'При СМС-аутентификации SberAPI произошла ошибка:
							|Сообщение об ошибке %1
							|Тело ответа: %2'");
		ТекстОшибки = СтрШаблон(Шаблон, РезультатВыполнения.СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
		
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, КраткоеПредставлениеОшибки, Параметры.НастройкаОбмена);
		
		ВызватьИсключение КраткоеПредставлениеОшибки;
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(ПараметрыВозврата, АдресРезультата);
	
КонецПроцедуры

Процедура ПолучитьТокенДоступа(Параметры, АдресРезультата) Экспорт
	
	ПараметрыВозврата = Новый Структура;
	ПараметрыВозврата.Вставить("ИдентификаторСессии", "");
	ПараметрыВозврата.Вставить("accessToken", "");
	ПараметрыВозврата.Вставить("idToken", "");
	ПараметрыВозврата.Вставить("expiresIn", "");
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(Параметры, "ПолучитьТокенДоступа");
	
	ТекстЗапроса = ТекстЗапросаТокена(
		ПараметрыПодключения.ИдентификаторОрганизации,
		ПараметрыПодключения.КлиентскийСекрет,
		Параметры.ИдентификаторСессии);
		
	АдресРесурса = "fintech/api/auth/v1/get-access-token";
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "POST";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = Параметры.НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	Если РезультатВыполнения.Статус Тогда
		
		JSON = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
		accessToken = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "accessToken", "");
		idToken = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "idToken", "");
		expiresIn = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "expiresIn", "");
		
		ПараметрыВозврата.ИдентификаторСессии = Параметры.ИдентификаторСессии;
		ПараметрыВозврата.accessToken = accessToken;
		ПараметрыВозврата.idToken = idToken;
		ПараметрыВозврата.expiresIn = expiresIn;
		
	Иначе
		
		ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурнал(Параметры.НастройкаОбмена, "get-access-token", "", "", "");
		ВидОперации = НСтр("ru = 'Получение токена доступа'");
		
		Ошибка = НСтр("ru = 'При получение токена доступа SberAPI произошла ошибка.'");
		КраткоеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.КороткоеПредставлениеОшибки(
			РезультатВыполнения.Тело, Ошибка);
		Если Ошибка <> КраткоеПредставлениеОшибки Тогда
			Шаблон = "%1
				|%2";
			КраткоеПредставлениеОшибки = СтрШаблон(Шаблон, Ошибка, КраткоеПредставлениеОшибки);
		КонецЕсли;
		
		ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
			РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
		
		Шаблон = НСтр("ru = '%1:
							|Сообщение об ошибке %2
							|Тело ответа: %3'");
		ТекстОшибки = СтрШаблон(Шаблон, Ошибка, РезультатВыполнения.СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
		
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, КраткоеПредставлениеОшибки, Параметры.НастройкаОбмена);
		
		ВызватьИсключение КраткоеПредставлениеОшибки;
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(ПараметрыВозврата, АдресРезультата);
	
КонецПроцедуры

Функция ОбновитьКлиентскийСекрет(ИдентификаторОрганизации,
								   КлиентскийСекрет,
								   ДвоичныеДанныеСертификата,
								   ПарольСертификата,
								   ПредыдущийКлиентскийСекретОбновленный) Экспорт
	
	ПредыдущийКлиентскийСекретОбновленный = "";
	ТекстЗапроса = "";
	
	ПарметрыОбновления = Новый Структура();
	ПарметрыОбновления.Вставить("clientId", ИдентификаторОрганизации);
	ПарметрыОбновления.Вставить("clientSecret", КлиентскийСекрет);
	ТекстЗапроса = ОбменСБанкамиСлужебный.ЗначениеВJSON(ПарметрыОбновления);
	
	АдресРесурса = "fintech/api/applications/secrets/v1/refresh-client-secret";
	
	ФайлСертификата = ПолучитьИмяВременногоФайла("p12");
	ДвоичныеДанныеСертификата.Записать(ФайлСертификата);
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "POST";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.ФайлСертификата = ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПарольСертификата;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	УдалитьВременныйФайлСертификата(ФайлСертификата);
	
	Если РезультатВыполнения.Статус Тогда
		
		JSON = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
		ПредыдущийКлиентскийСекретОбновленный = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "clientSecret", "");
		Если ПустаяСтрока(ПредыдущийКлиентскийСекретОбновленный) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Возврат Истина;
		
	ИначеЕсли ЗначениеЗаполнено(РезультатВыполнения.КодСостояния) Тогда
		
		ВидОперации = НСтр("ru = 'Получение client secret для SberAPI'");
		ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
			РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
		Шаблон = НСтр("ru = 'При прлучении client secret произошла ошибка:
							|Тело ответа: %1'");
		ТекстОшибки = СтрШаблон(Шаблон, ПодробноеПредставлениеОшибки);
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки);
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет, соответствуют ли запрашиваемые данные доступным на сервере.
//
// Параметры:
//  Параметры - Структура - параметры выполнения, содержит поля:
//   * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена, если она есть.
//   * Организация - ОпределяемыйТип.Организация - ссылка на организацию
//   * Банк - ОпределяемыйТип.БанкОбменСБанками - ссылка на банк.
//   * НомерСчета - Строка - номер счета, к которому нужен доступ.
//  Адрес - Строка - адрес временного хранилища, содержащий структуру с полями:
//   * Результат - Булево - если Истина, то есть доступ к требуемым данным.
//   * ДоступныеСчета - Массив из Строка - номера счетов, к которым есть доступ.
//
Процедура ПроверитьНаличиеДоступаКДанным(Параметры, Адрес) Экспорт
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(Параметры, "ПроверитьНаличиеДоступаКДанным");
	
	АдресРесурса = "fintech/api/v1/client-info";
	
	ЗаписьДанныхСообщения = НовыйJSON();
	СтрокаЗапроса = СтрокаJSON(ЗаписьДанныхСообщения);
	
	ТекстЗапроса = "";
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "GET";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.ТокенДоступа = Параметры.ТокенДоступа;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = Параметры.НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	Если Не РезультатВыполнения.Статус Тогда
		ТребуетсяАутентификация = Ложь;
		Если ЗначениеЗаполнено(РезультатВыполнения.КодСостояния) Тогда
			Шаблон = НСтр("ru = 'При получении данных об организации через сервис SberAPI произошла ошибка.
								|Сообщение об ошибке %1
								|Тело ответа: %2'");
			
			ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
				РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			
			ТекстОшибки = СтрШаблон(Шаблон, РезультатВыполнения.СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
			ВидОперации = НСтр("ru = 'Получение данных через сервис SberAPI'");
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
				ВидОперации, РезультатВыполнения.СообщениеОбОшибке, ТекстОшибки, Параметры.НастройкаОбмена);
			
			Если РезультатВыполнения.КодСостояния = 401 Тогда
				ДанныеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДанныеОшибки(
					РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
				Если ДанныеОшибки.ТэгОшибки = "AUTH_TOKEN_IS_EXPIRED" Или ДанныеОшибки.ТэгОшибки = "UNAUTHORIZED" Тогда
					ТребуетсяАутентификация = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ТребуетсяАутентификация Тогда
			Результат = Новый Структура;
			Результат.Вставить("ТребуетсяАутентификация", Истина);
			ПоместитьВоВременноеХранилище(Результат, Адрес);
			
			Возврат;
		КонецЕсли;
		
		ВызватьИсключение РезультатВыполнения.СообщениеОбОшибке;
	КонецЕсли;
	
	ПараметрыОтвета = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
	
	Результат = РезультатПроверкиДоступаКДанным(
		ПараметрыОтвета, Параметры.Организация, Параметры.Банк, Параметры.НомерСчета);
	
	ПоместитьВоВременноеХранилище(Результат, Адрес);
	
КонецПроцедуры

Процедура ПолучитьСертификатыОрганизацииИДанныеПользователя(Параметры, АдресРезультата) Экспорт
	
	ПараметрыВозврата = Новый Структура;
	ПараметрыВозврата.Вставить("СертификатыОрганизацииЕИО", Новый Массив);
	ПараметрыВозврата.Вставить("СертификатыОрганизации", Новый Массив);
	ПараметрыВозврата.Вставить("ИнформацияОПользователе", Неопределено);
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(Параметры, "ПолучитьСертификатыОрганизацииИДанныеПользователя");
	НастройкаОбмена = ПараметрыПодключения.НастройкаОбмена;
	Организация = ПараметрыПодключения.Организация;
	
	Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
		ТекущаяСессия = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	ИначеЕсли ЗначениеЗаполнено(Организация) Тогда
		ТекущаяСессия = СлужебныеДанныеАвторизации(Организация);
	КонецЕсли;
	
	ТокенДоступа = ТекущаяСессия.ТокенДоступа;
	
	СертификатПодключения = Новый Структура(
		"ФайлСертификата,СертификатПодключенияПароль",
		ПараметрыПодключения.ФайлСертификата,
		ПараметрыПодключения.СертификатПодключенияПароль);
	
	// Получим информацию о пользователе, для определения ЕИО и вида подписи
	ИнформацияОПользователе = ИнформацияОПользователе(НастройкаОбмена, ТокенДоступа, СертификатПодключения);
	ПараметрыВозврата.ИнформацияОПользователе = ИнформацияОПользователе;
	
	Если ИнформацияОПользователе.ЭтоЕИО Тогда
	
		// Получим заново СертификатПодключения, т.к. для каждого нового соединения
		//  нужно использовать новый файл того же сертификата
		СертификатПодключения = Новый Структура(
			"ФайлСертификата,СертификатПодключенияПароль",
			ПараметрыПодключения.ФайлСертификата,
			ПараметрыПодключения.СертификатПодключенияПароль);
		
		// Получим сертификаты организации для ЕИО
		АдресРесурса = "fintech/api/v1/crypto/eio";
		СертификатыОрганизацииЕИО = ПолучитьСертификатыОрганизации(
			АдресРесурса,
			ТокенДоступа,
			НастройкаОбмена,
			СертификатПодключения);
		ПараметрыВозврата.СертификатыОрганизацииЕИО = СертификатыОрганизацииЕИО;
		
	КонецЕсли;
	
	// Получим заново СертификатПодключения, т.к. для каждого нового соединения
	//  нужно использовать новый файл того же сертификата
	СертификатПодключения = Новый Структура(
		"ФайлСертификата,СертификатПодключенияПароль",
		ПараметрыПодключения.ФайлСертификата,
		ПараметрыПодключения.СертификатПодключенияПароль);
	
	// Получим сертификаты организации для
	АдресРесурса = "fintech/api/v1/crypto";
	СертификатыОрганизации = ПолучитьСертификатыОрганизации(
		АдресРесурса,
		ТокенДоступа,
		НастройкаОбмена,
		СертификатПодключения);
	ПараметрыВозврата.СертификатыОрганизации = СертификатыОрганизации;
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	ПоместитьВоВременноеХранилище(ПараметрыВозврата, АдресРезультата);
	
КонецПроцедуры

Процедура СохранитьСлужебныеДанныеАвторизации(Организация,
											  ПредыдущийКлиентскийСекретИзФайла,
											  ПредыдущийКлиентскийСекретОбновленный,
											  СертификатПодключения,
											  СертификатПодключенияПароль,
											  ИдентификаторОрганизации,
											  ТокенДоступа = "") Экспорт
	
	ИмяСлужебныхНастроекSberAPI = ИмяСлужебныхНастроекSberAPI();
	
	ДанныеПоОрганизации = Новый Структура();
	ДанныеПоОрганизации.Вставить("ПредыдущийКлиентскийСекретИзФайла", ПредыдущийКлиентскийСекретИзФайла);
	ДанныеПоОрганизации.Вставить("ПредыдущийКлиентскийСекретОбновленный", ПредыдущийКлиентскийСекретОбновленный);
	ДанныеПоОрганизации.Вставить("СертификатПодключения", СертификатПодключения);
	ДанныеПоОрганизации.Вставить("СертификатПодключенияПароль", СертификатПодключенияПароль);
	ДанныеПоОрганизации.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	ДанныеПоОрганизации.Вставить("ТокенДоступа", ТокенДоступа);
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПоОрганизациям = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ИмяСлужебныхНастроекSberAPI, "ДанныеПоОрганизациям");
	Если Не ЗначениеЗаполнено(ДанныеПоОрганизациям) Тогда
		ДанныеПоОрганизациям = Новый Соответствие();
	КонецЕсли;
	
	ДанныеПоОрганизациям.Вставить(Организация, ДанныеПоОрганизации);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		ИмяСлужебныхНастроекSberAPI, ДанныеПоОрганизациям, "ДанныеПоОрганизациям");
	
КонецПроцедуры

#КонецОбласти

#Область Прочие

Функция АдресSberAPIIntegration() Экспорт
	
	ИспользуетсяТестовыйРежим = Ложь;
	ОбменСБанкамиПереопределяемый.ПроверитьИспользованиеТестовогоРежима(ИспользуетсяТестовыйРежим);
	
	Если ИспользуетсяТестовыйРежим Тогда
		
		Возврат "https://iftfintech.testsbi.sberbank.ru:9443";
		
	Иначе
		
		Возврат "https://fintech.sberbank.ru:9443";
		
	КонецЕсли;
	
КонецФункции

Функция СлужебныеДанныеАвторизации(Организация) Экспорт
	
	СлужебныеДанныеАвторизации = Неопределено;
	
	ИмяСлужебныхНастроекSberAPI = ИмяСлужебныхНастроекSberAPI();
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПоОрганизациям = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ИмяСлужебныхНастроекSberAPI, "ДанныеПоОрганизациям");
	Если ЗначениеЗаполнено(ДанныеПоОрганизациям) Тогда
		
		СлужебныеДанныеАвторизации = ДанныеПоОрганизациям.Получить(Организация);
		
		Возврат СлужебныеДанныеАвторизации;
		
	Иначе
		
		Возврат СлужебныеДанныеАвторизации;
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьФродПараметрыВСтруктуру(ФродПараметры, Параметры) Экспорт
	
	ФродЭлемент = Новый Структура;
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	Язык = Сред(КодЛокализацииИнформационнойБазы(), 1, 2);
	
	СтрокаФрод = "version=3.4.1.0_1&pm_fpua=" + Параметры.РазрядностьОС + "|0|"
		+ Параметры.РазрядностьПроцессора + "|" + Язык + "|"
		+ ОбменСБанкамиСлужебныйПовтИсп.ВерсияПрограммыКлиентаДляБанка()
		+ "&pm_fpsc=" + Параметры.ГлубинаЦвета + "|" + Параметры.ШиринаМонитора
		+ "|" + Параметры.ВысотаМонитора + "|" + Параметры.ШиринаРабочегоСтола
		+ "&pm_fpsw" + Параметры.УстановленныеКомпоненты + "&pm_fptz="
		+ Параметры.СмещениеЧасовогоПояса + "&pm_fpln=lang=" + Язык + "|syslang="
		+ Параметры.ЯзыкСистемы + "|userlang=" + Параметры.ЯзыкПользователя
		+ "&pm_fpjv=0&pm_fpco=0&pm_fpasw=&pm_fpan=SBB&pm_fpacn=SBB&pm_fpol="
		+ Параметры.Онлайн + "&pm_fposp=&pm_fpup=&pm_fpsaw="
		+ Параметры.ШиринаМонитора + "&pm_fpspd=" + Параметры.ГлубинаЦвета
		+ "&pm_fpsbd=&pm_fpsdx=" + Параметры.ПлотностьПикселяX + "&pm_fpsdy="
		+ Параметры.ПлотностьПикселяY + "&pm_fpslx=" + Параметры.ПлотностьПикселяX
		+ "&pm_fpsly=" + Параметры.ПлотностьПикселяY + "&pm_fpsfse="
		+ Параметры.СглаживаниеШрифтов + "&pm_fpsui=&pm_os=Windows&pm_brmjv="
		+ СистемнаяИнформация.ВерсияПриложения + "&pm_br=SBB&pm_inpt=&pm_expt=";
	
	СтрокаФродКодированная = КодироватьСтроку(СтрокаФрод, СпособКодированияСтроки.КодировкаURL);
	ТекстОшибки = "";
	
	ФродЭлемент.Вставить("devicePrint", СтрокаФродКодированная);
	
	СвойстваКомпьютера = Параметры.ИдентификаторКомпьютера + ";"
		+ Параметры.ИдентификаторПроцессора + ";" + Параметры.ИдентификаторBIOS + ";";
	СвойстваКомпьютера = КодироватьСтроку(СвойстваКомпьютера, СпособКодированияСтроки.КодировкаURL);
	
	ФродЭлемент.Вставить("ipMacAddresses", Параметры.IP + ";" + Параметры.MAC);
	ФродЭлемент.Вставить("geolocationInfo", ";;;;4");
	ФродЭлемент.Вставить("pcProp", СвойстваКомпьютера);
	
	ИнформацияОТокене = Параметры.КонфигурационнаяСтрока;
	Если Параметры.Свойство("НомерКонтейнера")
			И ЗначениеЗаполнено(ИнформацияОТокене) Тогда
		МассивПараметровТокена = СтрРазделить(ИнформацияОТокене, "&");
		ПараметрыТокена = Новый Структура;
		Для Каждого Строка Из МассивПараметровТокена Цикл
			ПозицияРавно = СтрНайти(Строка, "=");
			ПозицияПервойКавычки = СтрНайти(Строка, """");
			ДлинаЗначения = СтрДлина(Строка) - ПозицияПервойКавычки - 1;
			ПараметрыТокена.Вставить(Сред(Строка, 1, ПозицияРавно - 1), Сред(Строка, ПозицияПервойКавычки + 1, ДлинаЗначения));
		КонецЦикла;
		TokenInfo = "TOKEN;" + ПараметрыТокена.devcfg + ";"
			+ ПараметрыТокена.rnginitdate + ";" + ПараметрыТокена.serial + ";"
			+ ПараметрыТокена.buildid + ";" + Параметры.НомерКонтейнера;
		ФродЭлемент.Вставить("tokenInfo", TokenInfo);
	Иначе
		ФродЭлемент.Вставить("tokenInfo", ";");
	КонецЕсли;
	
	ФродЭлемент.Вставить("httpAcceptLanguage", "ru-RU");
	
	ФродПараметры = Новый ФиксированнаяСтруктура(ФродЭлемент);
	
КонецПроцедуры

Процедура ОпределитьПараметрыСайтаСерверныйВызов(Знач АдресСайта,
													ЗащищенноеСоединение,
													Адрес,
													Протокол,
													СертификатПодключения,
													ИмяЦепочкиСертификатов) Экспорт
	
	АдресСайта = СокрЛП(АдресСайта);
	
	АдресСайта = СтрЗаменить(АдресСайта, "\", "/");
	АдресСайта = СтрЗаменить(АдресСайта, " ", "");
	
	Если НРег(Лев(АдресСайта, 7)) = "http://" Тогда
		
		Протокол = "http";
		Адрес = Сред(АдресСайта,8);
		ЗащищенноеСоединение = Неопределено;
		
	ИначеЕсли НРег(Лев(АдресСайта, 8)) = "https://" Тогда
		
		Протокол = "https";
		Адрес = Сред(АдресСайта,9);
		ЗащищенноеСоединение = СоздатьЗащищенноеСоединение(СертификатПодключения, ИмяЦепочкиСертификатов);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьДанныеАвторизации(Организация) Экспорт
	
	ИмяСлужебныхНастроекSberAPI = ИмяСлужебныхНастроекSberAPI();
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПоОрганизациям = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ИмяСлужебныхНастроекSberAPI, "ДанныеПоОрганизациям");
	
	Если ЗначениеЗаполнено(ДанныеПоОрганизациям) Тогда
		
		НовыеДанныеПоОрганизациям = Новый Соответствие();
		Для Каждого ДанныеПоОрганизации Из ДанныеПоОрганизациям Цикл
			
			Если ДанныеПоОрганизации.Ключ <> Организация Тогда
				
				НовыеДанныеПоОрганизациям.Вставить(ДанныеПоОрганизации.Ключ, ДанныеПоОрганизации.Значение);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если НовыеДанныеПоОрганизациям.Количество() <> 0 Тогда
			
			ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
				ИмяСлужебныхНастроекSberAPI, НовыеДанныеПоОрганизациям, "ДанныеПоОрганизациям");
			
		Иначе
			
			ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(ИмяСлужебныхНастроекSberAPI);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком
//  ПараметрыСессии - Структура - может содержать следующие поля: 
//  	* ИдентификаторСессии - Строка - идентификатор установленной сессии
//  	* ФродПараметры - Структура - фрод
//  	* Логин - Строка - логин пользователя
//  	* ТокенДоступа - Строка - токен доступа, выданный сервисом
//  	* ДоступныеСчета - Массив из Строка - доступные счета в банке для текущего идентификатора сессии.
//  ТокенаДоступа - Строка - кокена доступа выданный банком
//
Функция ОтозватьТокенДоступа(Параметры) Экспорт
	
	ТекущаяСессия = Неопределено;
	ТокенДоступа = Неопределено;
	ТокенДоступа = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ТокенДоступа", Неопределено);
	
	Если Не ЗначениеЗаполнено(ТокенДоступа)
		И Параметры.Свойство("ТекущаяСессия", ТекущаяСессия)
		И ЗначениеЗаполнено(ТекущаяСессия) Тогда
		
		ТокенДоступа = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущаяСессия, "ТокенДоступа", Неопределено);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТокенДоступа) Тогда
		
		ТекущаяСессия = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(Параметры.НастройкаОбмена);
		Если ЗначениеЗаполнено(ТекущаяСессия) Тогда
			ТекущаяСессия.Свойство("ТокенДоступа", ТокенДоступа);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТокенДоступа) Тогда
		
		// Не найден ТокенДоступа, поэтому выходим
		Возврат Ложь;
		
	КонецЕсли;
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(Параметры, "ОтозватьТокенДоступа");
	
	ТекстЗапроса = "";
	
	// Пример текста ссылки с сайта банка с документацией
	// "/ic/sso/api/v2/oauth/revoke?client_id=92233764567396&client_secret=vyYPX12dET&token=xbgKDVrgf756ghi415Wdl012mNKFtEpqr678&token_type_hint=access_token"
	
	АдресРесурса = "ic/sso/api/v2/oauth/revoke?"
		+ "client_id=" +  КодироватьСтроку(ПараметрыПодключения.ИдентификаторОрганизации, СпособКодированияСтроки.КодировкаURL)
		+ "&client_secret=" + КодироватьСтроку(ПараметрыПодключения.КлиентскийСекрет, СпособКодированияСтроки.КодировкаURL)
		+ "&token=" + КодироватьСтроку(ТокенДоступа, СпособКодированияСтроки.КодировкаURL)
		+ "&token_type_hint=access_token";
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "POST";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = Параметры.НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
	ПараметрыHTTPЗапроса.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	Если РезультатВыполнения.Статус Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурнал(Параметры.НастройкаОбмена, "revoke", "", "", "");
	ВидОперации = НСтр("ru = 'Отзыв токена доступа'");
	
	Ошибка = НСтр("ru = 'При отзыве токена доступа SberAPI произошла ошибка.'");
	КраткоеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.КороткоеПредставлениеОшибки(
		РезультатВыполнения.Тело, Ошибка);
	Если Ошибка <> КраткоеПредставлениеОшибки Тогда
		Шаблон = "%1
			|%2";
		КраткоеПредставлениеОшибки = СтрШаблон(Шаблон, Ошибка, КраткоеПредставлениеОшибки);
	КонецЕсли;
	
	ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
		РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
	
	Шаблон = НСтр("ru = '%1:
						|Сообщение об ошибке %2
						|Тело ответа: %3'");
	ТекстОшибки = СтрШаблон(Шаблон, Ошибка, РезультатВыполнения.СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
	
	ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
		ВидОперации, ТекстОшибки, КраткоеПредставлениеОшибки, Параметры.НастройкаОбмена);
	
	Возврат Ложь;
	
КонецФункции

Функция СлужебныеПараметрыПодключения(ПередаваемыеПараметры, ИмяПроцедуры)
	
	ПараметрыПодключения = Новый Структура();
	ПараметрыПодключения.Вставить("НастройкаОбмена", Неопределено);
	ПараметрыПодключения.Вставить("Организация", Неопределено);
	ПараметрыПодключения.Вставить("ИдентификаторОрганизации", "");
	ПараметрыПодключения.Вставить("ФайлСертификата", Неопределено);
	ПараметрыПодключения.Вставить("СертификатПодключенияПароль", "");
	ПараметрыПодключения.Вставить("КлиентскийСекрет", "");
	
	ЗаполнитьСлужебныеПараметрыПодключения(ПараметрыПодключения, ПередаваемыеПараметры);
	
	Если Не ЗначениеЗаполнено(ПараметрыПодключения.ФайлСертификата) Тогда
		
		ШаблонОшибки = НСтр("ru = 'При попытке выполнении процедуры %1 произошла ошибка.
			|Не найден сертификат подключения для настройки обмена с банком %2.'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ИмяПроцедуры, Строка(ПараметрыПодключения.НастройкаОбмена));
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыПодключения.СертификатПодключенияПароль) Тогда
		
		ШаблонОшибки = НСтр("ru = 'При попытке выполнении процедуры %1 произошла ошибка.
			|Не получен пароль сертификата подключения для настройки обмена с банком %2.'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ИмяПроцедуры, Строка(ПараметрыПодключения.НастройкаОбмена));
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Возврат ПараметрыПодключения;
	
КонецФункции

Процедура ЗаполнитьСлужебныеПараметрыПодключения(ПараметрыПодключения, ПередаваемыеПараметры)
	
	// Настройки заполняются в следующем порядке: если передана НастройкаОбмена, то данные берутся из нее и
	//  из БезопасногоХранилища. Если НастройкаОбмена не передана, то данные берутся из СлужебныеДанныеАвторизации
	
	Если ТипЗнч(ПередаваемыеПараметры) = Тип("Структура") И ПередаваемыеПараметры.Свойство("НастройкаОбмена") Тогда
		
		ПараметрыПодключения.НастройкаОбмена = ПередаваемыеПараметры.НастройкаОбмена;
		
	ИначеЕсли ТипЗнч(ПередаваемыеПараметры) = Тип("СправочникСсылка.НастройкиОбменСБанками") Тогда
		
		ПараметрыПодключения.НастройкаОбмена = ПередаваемыеПараметры;
		
	КонецЕсли;
	
	// Параметр "ИгнорироватьСохраненныеНастройки" передается в случае, если метод вызывается в момент создания настройки
	//  обмена, когда нужно параметры создания взять из файла JSON, а не из справочника НастройкиОбменСБанками
	// Параметр "НовыйИдентификаторОрганизации" передается когда мы пересоздаем настройки обмена, например из УПШ,
	//  а должно стать SberAPI, и ИдентификаторОрганизации нужно взять новый из файла JSON.
	Если ЗначениеЗаполнено(ПараметрыПодключения.НастройкаОбмена) Тогда
		
		Если ТипЗнч(ПередаваемыеПараметры) = Тип("Структура") Тогда
			
			ИгнорироватьСохраненныеНастройки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ПередаваемыеПараметры,
				"ИгнорироватьСохраненныеНастройки",
				Ложь);
				
			НовыйИдентификаторОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ПередаваемыеПараметры,
				"НовыйИдентификаторОрганизации",
				"");
		Иначе
			
			ИгнорироватьСохраненныеНастройки = Ложь;
			НовыйИдентификаторОрганизации = "";
			
		КонецЕсли;
		
		РеквизитыНастройкиОбмена = Новый Структура("СертификатПодключения, ИдентификаторОрганизации, Организация");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			ПараметрыПодключения.НастройкаОбмена,
			РеквизитыНастройкиОбмена);
		
		// Если мы пересоздаем настройки (флаг ИгнорироватьСохраненныеНастройки), то файл TLS сертификата нужно взять не из
		//  предыдущих настроек, а из файла JSON, т.е. из СлужебныеДанныеАвторизации, которые получаются ниже
		Если НЕ ИгнорироватьСохраненныеНастройки Тогда
			
			ПараметрыПодключения.ФайлСертификата = ПолучитьИмяВременногоФайла("p12");
			ДанныеТраспортногоСертификата = РеквизитыНастройкиОбмена.СертификатПодключения.Получить();
			Если ЗначениеЗаполнено(ДанныеТраспортногоСертификата) Тогда
				ДанныеТраспортногоСертификата.Записать(ПараметрыПодключения.ФайлСертификата);
			Иначе
				
				ШаблонОшибки = НСтр("ru = 'При определении параметров подключения SberAPI произошла ошибка.
					|Не получен TLS-сертификат из настройки обмена с банком %1.'");
				ТекстОшибки = СтрШаблон(ШаблонОшибки, Строка(ПараметрыПодключения.НастройкаОбмена));
				
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НовыйИдентификаторОрганизации) Тогда
			ПараметрыПодключения.ИдентификаторОрганизации = НовыйИдентификаторОрганизации;
		Иначе
			ПараметрыПодключения.ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
		КонецЕсли;
		ПараметрыПодключения.Организация = РеквизитыНастройкиОбмена.Организация;
		
		Если Не ИгнорироватьСохраненныеНастройки Тогда
			// Ищем ДанныеАвторизации в БезопасномХранилище
			ДанныеАвторизацииSberAPI = ДанныеАвторизации(ПараметрыПодключения.НастройкаОбмена);
			Если ЗначениеЗаполнено(ДанныеАвторизацииSberAPI) Тогда
				
				ПараметрыПодключения.СертификатПодключенияПароль = ДанныеАвторизацииSberAPI.СертификатПодключенияПароль;
				ПараметрыПодключения.КлиентскийСекрет = ДанныеАвторизацииSberAPI.КлиентскийСекрет;
				
				Возврат;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыПодключения.Организация)
		И ТипЗнч(ПередаваемыеПараметры) = Тип("Структура")
		И ЗначениеЗаполнено(ПередаваемыеПараметры.Организация) Тогда
		
		ПараметрыПодключения.Организация = ПередаваемыеПараметры.Организация;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыПодключения.Организация) Тогда
		
		ШаблонОшибки = НСтр("ru = 'При определении параметров подключения SberAPI произошла ошибка.
			|Не найдена организация для настройки обмена с банком %1.'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Строка(ПараметрыПодключения.НастройкаОбмена));
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	СлужебныеДанныеАвторизации = СлужебныеДанныеАвторизации(ПараметрыПодключения.Организация);
	СертификатПодключения = Неопределено;
	
	Если ЗначениеЗаполнено(СлужебныеДанныеАвторизации)
		И (Не ЗначениеЗаполнено(ПараметрыПодключения.ФайлСертификата)
			Или Не ЗначениеЗаполнено(ПараметрыПодключения.СертификатПодключенияПароль)
			Или Не ЗначениеЗаполнено(ПараметрыПодключения.КлиентскийСекрет)
			Или Не ЗначениеЗаполнено(ПараметрыПодключения.ИдентификаторОрганизации)) Тогда
		
		Если Не ЗначениеЗаполнено(ПараметрыПодключения.ФайлСертификата)
			И СлужебныеДанныеАвторизации.Свойство("СертификатПодключения", СертификатПодключения)
			И ЗначениеЗаполнено(СертификатПодключения) Тогда
			
			ПараметрыПодключения.ФайлСертификата = ПолучитьИмяВременногоФайла("p12");
			СертификатПодключения = ПолучитьДвоичныеДанныеИзBase64Строки(СертификатПодключения);
			СертификатПодключения.Записать(ПараметрыПодключения.ФайлСертификата);
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыПодключения.СертификатПодключенияПароль) Тогда
			
			ПараметрыПодключения.СертификатПодключенияПароль = СлужебныеДанныеАвторизации.СертификатПодключенияПароль;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыПодключения.КлиентскийСекрет) Тогда
			
			ПараметрыПодключения.КлиентскийСекрет = СлужебныеДанныеАвторизации.ПредыдущийКлиентскийСекретОбновленный;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыПодключения.ИдентификаторОрганизации) Тогда
			
			ПараметрыПодключения.ИдентификаторОрганизации = СлужебныеДанныеАвторизации.ИдентификаторОрганизации;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеАвторизации(НастройкаОбмена)
	
	ДанныеАвторизацииSberAPI = Неопределено;
	ДанныеАвторизацииНастройкаОбмена = ОбменСБанкамиСлужебный.ДанныеАвторизацииНастройкаОбмена(НастройкаОбмена);
	Если ЗначениеЗаполнено(ДанныеАвторизацииНастройкаОбмена)
		И ДанныеАвторизацииНастройкаОбмена.Свойство("ДанныеАвторизацииSberAPI", ДанныеАвторизацииSberAPI)
		И ЗначениеЗаполнено(ДанныеАвторизацииSberAPI) Тогда
		
		Возврат ДанныеАвторизацииSberAPI;
		
	Иначе
		
		Возврат ДанныеАвторизацииSberAPI;
		
	КонецЕсли;
	
КонецФункции

Процедура УдалитьВременныйФайлСертификата(ФайлСертификата)
	
	ВременныйФайл = Новый Файл(ФайлСертификата);
	Если ВременныйФайл.Существует() Тогда
		
		ФайловаяСистема.УдалитьВременныйФайл(ФайлСертификата);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьСверткуПароляСбербанк(Логин, Пароль, Соль, B, K, A)
	
	СлучайноеЧисло_a = СлучайноеЧисло256Бит();
	
	B = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеИзBase64Строки(B); // srpB B-параметр
	Соль = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеИзBase64Строки(Соль);
	
	N = 125617018995153554710546479714086468244499594888726646874671447258204721048803; // HEX 0115B8B692E0E045692CF280B436735C77A5A9E8A9E7ED56C965F87DB5B2A2ECE3
	Константа_k = 1255395351873185901587718910969259987480671485520; //HEX DBE5DFE0704FEE4C85FF106ECD38117D33BCFE50
	ЧислоA = ВозвестиВСтепень(2, СлучайноеЧисло_a, N);
	
	ЛогинПароль = Логин + ":" + Пароль;
	
	Хеш = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(ЛогинПароль, "windows-1251");
	ДвоичныеДанные = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеИзBase64Строки(Хеш);
	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA1);
	Хеширование.Добавить(ДвоичныеДанные);
	Hp = Хеширование.ХешСумма;
	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA1);
	Хеширование.Добавить(Соль);
	Хеширование.Добавить(Hp);
	x = Хеширование.ХешСумма;
	
	РазмерА = РазмерЧисла(ЧислоA);
	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA1);
	Если РазмерА < 33 Тогда // 33 - размер числа N
		Для Счетчик = РазмерА + 1 По 33 Цикл
			Хеширование.Добавить(Символ(0));
		КонецЦикла;
	КонецЕсли;
	
	A = ДвоичныеДанныеИзЧисла(ЧислоA);
	Хеширование.Добавить(A);
	РазмерВ = B.Размер();
	Если РазмерВ < 33 Тогда
		Для Счетчик = РазмерВ + 1 По 33 Цикл
			Хеширование.Добавить(Символ(0));
		КонецЦикла;
	КонецЕсли;
	
	Хеширование.Добавить(B);
	u = Хеширование.ХешСумма;
	ЧислоВ = ЧислоИзДвоичныхДанных(B);
	ЧислоХ = ЧислоИзДвоичныхДанных(x);
	Base1 = ЧислоВ - Константа_k * ВозвестиВСтепень(2, ЧислоХ, N);
	Base = Base1;
	Base2 = 0;
	Если Base < 0 Тогда
		Base = Константа_k * N + ЧислоВ - Константа_k
			* ВозвестиВСтепень(2, ЧислоХ, N);
		Base2 = Base;
	КонецЕсли;
	
	S = ВозвестиВСтепень(Base, СлучайноеЧисло_a + ЧислоИзДвоичныхДанных(u) * ЧислоХ, N);
	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA1);
	Хеширование.Добавить(ДвоичныеДанныеИзЧисла(S));
	K = Хеширование.ХешСумма;
	
КонецПроцедуры

Процедура ДобавитьЗаполненноеЗначение(СтруктураДанных, НазваниеПоля, Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		СтруктураДанных.Вставить(НазваниеПоля, Значение);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьИДСессииИзBase64Строки(sessionId)
	
	Попытка
		
		ИДСессииИзJSON = ПолучитьСтрокуИзДвоичныхДанных(
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеИзBase64Строки(sessionId));
		ИдентификаторСессии = Новый УникальныйИдентификатор(ИДСессииИзJSON);
		
		Возврат ИДСессииИзJSON;
		
	Исключение
		
		// В ЖурналРегистрации сообщение не пишем, т.к. это просто проверка верности формата ответа сервера банка
		Попытка
			
			ИДСессииИзJSON = ПолучитьСтрокуИзДвоичныхДанных(
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеИзBase64Строки(ИДСессииИзJSON));
			ИдентификаторСессии = Новый УникальныйИдентификатор(ИДСессииИзJSON);
			
			Возврат ИДСессииИзJSON;
			
		Исключение
			
			// В ЖурналРегистрации сообщение не пишем, т.к. это просто проверка верности формата ответа сервера банка
			Возврат "";
			
		КонецПопытки;
		
	КонецПопытки;
	
КонецФункции

Функция ТекстЗапросаТокена(ИдентификаторОрганизации, clientsecret, ИдентификаторСессии)
	
	Данные = Новый Структура();
	Данные.Вставить("clientId", ИдентификаторОрганизации);
	Данные.Вставить("clientSecret", clientsecret);
	
	МассивScope = Новый Массив();
	МассивScope.Добавить("openid");
	МассивScope.Добавить("acr");
	МассивScope.Добавить("amr");
	МассивScope.Добавить("aud");
	МассивScope.Добавить("auth_time");
	МассивScope.Добавить("azp");
	МассивScope.Добавить("exp");
	МассивScope.Добавить("iat");
	МассивScope.Добавить("iss");
	МассивScope.Добавить("nonce");
	МассивScope.Добавить("sid2");
	МассивScope.Добавить("sub");
	МассивScope.Добавить("BANK_CONTROL_STATEMENT");
	МассивScope.Добавить("BUSINESS_CARDS_TRANSFER");
	МассивScope.Добавить("CERTIFICATE_REQUEST");
	МассивScope.Добавить("CONFIRMATORY_DOCUMENTS_INQUIRY");
	МассивScope.Добавить("CORPORATE_CARDS");
	МассивScope.Добавить("CRYPTO_CERT_REQUEST_EIO");
	МассивScope.Добавить("CURRENCY_OPERATION_DETAILS");
	МассивScope.Добавить("CURR_CONTROL_INFO_REQ");
	МассивScope.Добавить("CURR_CONTROL_MESSAGE_FROM_BANK");
	МассивScope.Добавить("CURR_CONTROL_MESSAGE_TO_BANK");
	МассивScope.Добавить("DICT");
	МассивScope.Добавить("FILES");
	МассивScope.Добавить("GENERIC_LETTER_FROM_BANK");
	МассивScope.Добавить("GENERIC_LETTER_TO_BANK");
	МассивScope.Добавить("GET_CLIENT_ACCOUNTS");
	МассивScope.Добавить("GET_CORRESPONDENTS");
	МассивScope.Добавить("GET_CRYPTO_INFO");
	МассивScope.Добавить("GET_CRYPTO_INFO_EIO");
	МассивScope.Добавить("GET_STATEMENT_ACCOUNT");
	МассивScope.Добавить("GET_STATEMENT_TRANSACTION");
	МассивScope.Добавить("NOMINAL_ACCOUNTS");
	МассивScope.Добавить("OrgName");
	МассивScope.Добавить("PAYROLL");
	МассивScope.Добавить("PAY_DOC_CUR");
	МассивScope.Добавить("PAY_DOC_RU");
	МассивScope.Добавить("SALARY_AGREEMENT");
	МассивScope.Добавить("accounts");
	МассивScope.Добавить("aud");
	МассивScope.Добавить("email");
	МассивScope.Добавить("individualExecutiveAgency");
	МассивScope.Добавить("inn");
	МассивScope.Добавить("iss");
	МассивScope.Добавить("name");
	МассивScope.Добавить("offerExpirationDate");
	МассивScope.Добавить("orgActualAddress");
	МассивScope.Добавить("orgFullName");
	МассивScope.Добавить("orgJuridicalAddress");
	МассивScope.Добавить("orgKpp");
	МассивScope.Добавить("orgLawForm");
	МассивScope.Добавить("orgLawFormShort");
	МассивScope.Добавить("orgOgrn");
	МассивScope.Добавить("orgOktmo");
	МассивScope.Добавить("phone_number");
	МассивScope.Добавить("sub");
	МассивScope.Добавить("terBank");
	МассивScope.Добавить("userPosition");
	МассивScope.Добавить("userSignatureType");
	Данные.Вставить("scope", МассивScope);
	Данные.Вставить("sessionId", ИдентификаторСессии);
	
	ТекстЗапроса = ОбменСБанкамиСлужебный.ЗначениеВJSON(Данные);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СлучайноеЧисло256Бит()
	
	ИтоговоеЧисло = "";
	Для Счетчик = 1 По 8 Цикл
		ГСЧ = Новый ГенераторСлучайныхЧисел();
		СлучайноеЧисло = ГСЧ.СлучайноеЧисло(100000000, 999999999);
		ИтоговоеЧисло = ИтоговоеЧисло + Формат(СлучайноеЧисло, "ЧГ=");
	КонецЦикла;
	
	СлучайноеЧисло = ГСЧ.СлучайноеЧисло(100000, 999999);
	ИтоговоеЧисло = ИтоговоеЧисло + Формат(СлучайноеЧисло, "ЧГ=");
	
	Возврат Число(ИтоговоеЧисло);
	
КонецФункции

Функция ВозвестиВСтепень(Знач ИсходноеЧисло, Знач Степень, Знач Делитель)
	
	Результат = 1;
	Пока Степень <> 0 Цикл
		Если Степень % 2 = 1 Тогда
			Результат = (Результат * ИсходноеЧисло) % Делитель;
			Степень = Степень - 1;
		КонецЕсли;
		
		Степень = Степень / 2;
		ИсходноеЧисло = (ИсходноеЧисло * ИсходноеЧисло) % Делитель;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция РазмерЧисла(Знач Число)
	
	ШестнадцатеричнаяСтрока = ШестнадцатеричнаяСтрока(Число);
	
	Возврат СтрДлина(ШестнадцатеричнаяСтрока) / 2;
	
КонецФункции

Функция ЧислоИзДвоичныхДанных(ДвоичныеДанные)
	
	ДвоичныеДанныеСтрокой = СтрЗаменить(Строка(ДвоичныеДанные), " ", "");
	ВозвращаемоеЗначение = 0;
	ДлинаСтроки = СтрДлина(ДвоичныеДанныеСтрокой);
	Для Счетчик = 1 По ДлинаСтроки Цикл
		КодСимвола = КодСимвола(ДвоичныеДанныеСтрокой, Счетчик);
		Если КодСимвола >= КодСимвола("0") И КодСимвола <= КодСимвола("9") Тогда
			Разница = КодСимвола - КодСимвола("0");
		Иначе
			Разница = КодСимвола - КодСимвола("A") + 10;
		КонецЕсли;
		
		ВозвращаемоеЗначение = ВозвращаемоеЗначение * 16 + Разница;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ДвоичныеДанныеИзЧисла(Знач Число)
	
	ШестнадцатеричнаяСтрока = ШестнадцатеричнаяСтрока(Число);
	ТипXDTOДвоичныеДанные = ФабрикаXDTO.Тип("http://www.w3.org/2001/XMLSchema", "hexBinary");
	ЗначениеHEX = ФабрикаXDTO.Создать(ТипXDTOДвоичныеДанные, ШестнадцатеричнаяСтрока);
	
	Возврат ЗначениеHEX.Значение;
	
КонецФункции

Функция ШестнадцатеричнаяСтрока(Знач Число)
	
	Если Число = 0 Тогда
		Возврат "0";
	КонецЕсли;
	
	ВозвращаемоеЗначение = "";
	Пока Число > 0 Цикл
		Остаток = Число % 16;
		Если Остаток >= 10 Тогда
			ВозвращаемоеЗначение = Символ(КодСимвола("A") + Остаток - 10) + ВозвращаемоеЗначение;
		Иначе
			ВозвращаемоеЗначение = Строка(Остаток) + ВозвращаемоеЗначение;
		КонецЕсли;
		
		Число = Цел(Число / 16);
	КонецЦикла;
	
	Если СтрДлина(ВозвращаемоеЗначение) % 2 > 0 Тогда
		ВозвращаемоеЗначение = "0" + ВозвращаемоеЗначение;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Отправляет данные через интернет.
//
// Параметры:
//  ПараметрыHTTPЗапроса - Структура - см.ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
//
// Возвращаемое значение:
//   Структура - Структура со свойствами:
//      * Статус - Булево - результат получения файла.
//      * Тело - ДвоичныеДанные, Строка, Неопределено - данные ответа сервера.
//      * СообщениеОбОшибке - Строка, Неопределено - сообщение об ошибке, если статус Ложь.
//      * КодСостояния - Число, Неопределено - код состояния HTTP-ответа. Наличие кода означает, что был ответ от сервера.
//
Функция ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса)
	
	ИспользоватьЖурналирование = Ложь;
	Если ПараметрыHTTPЗапроса.НастройкаОбмена <> Неопределено Тогда
		
		ПараметрыЖурналирования = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыЖурналирования(
			ПараметрыHTTPЗапроса.НастройкаОбмена);
		ИспользоватьЖурналирование = ПараметрыЖурналирования.ИспользоватьЖурналирование;
		
	КонецЕсли;
	
	Возврат ОтправитьHTTPЗапросРекурсивно(ПараметрыHTTPЗапроса, ИспользоватьЖурналирование);
	
КонецФункции

Функция ОтправитьHTTPЗапросРекурсивно(ПараметрыHTTPЗапроса, ИспользоватьЖурналирование)
	
	ПолучитьТелоКакСтроку = Истина;
	
	СтруктураВозврата = ОбменСБанкамиСлужебныйКлиентСервер.СтруктураВозвратаHttpЗапроса();
	
	АдресСервера = АдресSberAPIIntegration();
	
	Если ЗначениеЗаполнено(ПараметрыHTTPЗапроса.ТокенДоступа) Тогда
		ПараметрыHTTPЗапроса.Заголовки.Вставить("Authorization", "Bearer " + ПараметрыHTTPЗапроса.ТокенДоступа);
	КонецЕсли;
	
	HTTPЗапрос = Новый HTTPЗапрос(ПараметрыHTTPЗапроса.АдресРесурса, ПараметрыHTTPЗапроса.Заголовки);
	
	Если ТипЗнч(ПараметрыHTTPЗапроса.Данные) = Тип("ДвоичныеДанные") Тогда
		HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(ПараметрыHTTPЗапроса.Данные);
	ИначеЕсли ТипЗнч(ПараметрыHTTPЗапроса.Данные) = Тип("Строка") Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ПараметрыHTTPЗапроса.Данные);
	КонецЕсли;
	
	// Для подключения пробуем перебрать Сертификаты: Операционной системы, Минцифры, Сбербанка
	ИменаЦепочекСертификатов = Новый Массив;
	ИменаЦепочекСертификатов.Добавить(""); // По умолчанию из операционной системы 
	ИменаЦепочекСертификатов.Добавить("sberca_ext"); // УЦ Сбербанка в формате PEM, собран из sberca_ext и sberca_root_ext
	
	КоличествоПопыток = 0;
	
	Для Каждого ИмяЦепочкиСертификатов Из ИменаЦепочекСертификатов Цикл
		
		СертификатПодключения = Новый СертификатКлиентаФайл(
			ПараметрыHTTPЗапроса.ФайлСертификата,
			ПараметрыHTTPЗапроса.СертификатПодключенияПароль);
		
		Соединение = СоединениеССервером(
			АдресСервера, , СертификатПодключения, ИмяЦепочкиСертификатов);
			
		КоличествоПопыток = КоличествоПопыток + 1;
		
		Попытка
			
			Если ИспользоватьЖурналирование Или Не ЗначениеЗаполнено(ПараметрыHTTPЗапроса.НастройкаОбмена) Тогда
				URL = АдресСервера + "/" + ПараметрыHTTPЗапроса.АдресРесурса;
				Тело = HTTPЗапрос.ПолучитьТелоКакСтроку();
				ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPЗапросВЛог(
					ПараметрыHTTPЗапроса.ВидЗапроса,
					ПараметрыHTTPЗапроса.НастройкаОбмена,
					URL,
					Соединение.Сервер,
					Соединение.Порт,
					ПараметрыHTTPЗапроса.Заголовки,
					Тело);
			КонецЕсли;
			
			Если ПараметрыHTTPЗапроса.ВидЗапроса = "POST" Тогда
				ОтветHTTP = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
			Иначе
				ОтветHTTP = Соединение.Получить(HTTPЗапрос);
			КонецЕсли;
			
			ДвоичныеДанныеВТеле = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеВТелеHTTPОтвета(ОтветHTTP.Заголовки);
			Если ДвоичныеДанныеВТеле Тогда
				ТелоОвета = ОтветHTTP.ПолучитьТелоКакДвоичныеДанные();
			Иначе
				ТелоОвета = ОтветHTTP.ПолучитьТелоКакСтроку();
			КонецЕсли;
			
			Если ИспользоватьЖурналирование Или Не ЗначениеЗаполнено(ПараметрыHTTPЗапроса.НастройкаОбмена) Тогда
				ОбменСБанкамиSberAPIСлужебныйВызовСервера.СохранитьHTTPОтветВЛог(
					ПараметрыHTTPЗапроса.НастройкаОбмена,
					ОтветHTTP.КодСостояния,
					ТелоОвета,
					ОтветHTTP.Заголовки);
			КонецЕсли;
			
			Прервать;
			
		Исключение
			
			Если КоличествоПопыток = ИменаЦепочекСертификатов.Количество() Тогда
			
				ВидОперации = НСтр("ru = 'Ошибка при выполнение Http запроса SberAPI'");
				ИспользуемыйСертификат = ?(ИмяЦепочкиСертификатов = "", "По-умолчанию (из операционной системы)", ИмяЦепочкиСертификатов);
				
				ТекстЗаголовка = НСтр("ru = 'Вид запроса: %1. Попытка номер: %2. Использован сертификат: %3'");
				ТекстЗаголовка = СтрШаблон(ТекстЗаголовка, ПараметрыHTTPЗапроса.АдресРесурса, КоличествоПопыток, ИспользуемыйСертификат);
				ТекстСообщения = ТекстЗаголовка + Символы.ПС;;
				
				ОписаниеОшибки = ОписаниеОшибки();
				Инфо = ИнформацияОбОшибке();
				ТекстСообщения = ТекстСообщения + ОписаниеОшибки + Символы.ПС;
				ТекстСообщения = ТекстСообщения + Инфо.Описание + Символы.ПС;
				ТекстСообщения = ТекстСообщения + Инфо.ИмяМодуля + Символы.ПС;
				ТекстСообщения = ТекстСообщения + Инфо.НомерСтроки + Символы.ПС;
				ТекстСообщения = ТекстСообщения + Инфо.ИсходнаяСтрока + Символы.ПС;
				ТекстСообщения = ТекстСообщения + Инфо.Причина + Символы.ПС;
				ТекстСообщения = ТекстСообщения + Инфо.ДополнительнаяИнформация + Символы.ПС;
				
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
					ВидОперации,
					ТекстСообщения,
					ТекстСообщения,
					ПараметрыHTTPЗапроса.НастройкаОбмена);
				
				СтруктураВозврата.Статус = Ложь;
				СтруктураВозврата.СообщениеОбОшибке = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				
				Возврат СтруктураВозврата;
				
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	HTTPЗапрос = Неопределено;
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		СтруктураВозврата.Статус = Истина;
		Если ПолучитьТелоКакСтроку Тогда
			СтруктураВозврата.Тело = ОтветHTTP.ПолучитьТелоКакСтроку();
		Иначе
			СтруктураВозврата.Тело = ОтветHTTP.ПолучитьТелоКакДвоичныеДанные();
		КонецЕсли;
	Иначе
		СтруктураВозврата.Статус = Ложь;
		СтруктураВозврата.СообщениеОбОшибке = ИнтернетСоединениеБЭД.РасшифровкаКодаСостоянияHTTP(ОтветHTTP.КодСостояния);
		СтруктураВозврата.Тело = ОтветHTTP.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	СтруктураВозврата.КодСостояния = ОтветHTTP.КодСостояния;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Создает соединение с сервером в интернет.
//
// Параметры:
//  АдресСервера - Строка - URI
//  Таймаут - Число - Определяет время ожидания осуществляемого соединения и операций, в секундах. 0 - таймаут не установлен.
// 
// Возвращаемое значение:
// HTTPСоединение - предназначен для работы с файлами на http-серверах.
//
Функция СоединениеССервером(АдресСервера, Таймаут, СертификатПодключения, ИмяЦепочкиСертификатов)
	
	Перем ЗащищенноеСоединение;
	
	Адрес = "";
	Протокол = "";
	
	ОпределитьПараметрыСайтаСерверныйВызов(
		АдресСервера, ЗащищенноеСоединение, Адрес, Протокол, СертификатПодключения, ИмяЦепочкиСертификатов);
	Прокси = ИнтернетСоединениеБЭД.СформироватьПрокси(Протокол);
	
	Соединение = Новый HTTPСоединение(Адрес, , , , Прокси, Таймаут, ЗащищенноеСоединение);
	
	Возврат Соединение;
	
КонецФункции

Функция СоздатьЗащищенноеСоединение(СертификатПодключения, ИмяЦепочкиСертификатов)
	
	Если ЗначениеЗаполнено(ИмяЦепочкиСертификатов) Тогда
		ИмяФайла = ПолучитьИмяВременногоФайла("pem");
		ДвоичныеДанные = Обработки.ОбменСБанками.ПолучитьМакет(ИмяЦепочкиСертификатов);
		ДвоичныеДанные.Записать(ИмяФайла);
		
		СертификатыУдостоверяющихЦентров = Новый СертификатыУдостоверяющихЦентровФайл(ИмяФайла);
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение(
			СертификатПодключения,
			СертификатыУдостоверяющихЦентров);
		ФайловаяСистема.УдалитьВременныйФайл(ИмяФайла);
	Иначе
		СертификатыУдостоверяющихЦентров = Новый СертификатыУдостоверяющихЦентровОС;
		ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение(
			СертификатПодключения,
			СертификатыУдостоверяющихЦентров);
	КонецЕсли;
	
	Возврат ЗащищенноеСоединение;
	
КонецФункции

// Преобразует строку в двоичные данные
//
// Параметры:
//   СтрокаДанных - Строка - исходная строка
//   КодировкаТекста - Строка - кодировка сохранения строки
//
// Возвращаемое значение:
//   ДвоичныеДанные - данные строки в формате двоичных данных
//
Функция ДвоичныеДанныеИзСтроки(СтрокаДанных, КодировкаТекста = "UTF-8")
	
	ПотокВПамяти = Новый ПотокВПамяти();
	Текст = Новый ЗаписьТекста(ПотокВПамяти, КодировкаТекста, , Символы.ПС);
	Текст.Записать(СтрокаДанных);
	Текст.Закрыть();
	
	Возврат ПотокВПамяти.ЗакрытьИПолучитьДвоичныеДанные();
	
КонецФункции

Функция ЗначениеРеквизитаВДереве(ДеревоДанных, ПолныйПуть, СообщатьОбОшибке = Истина, НайденнаяСтрока = Неопределено)
	
	Возврат ДеревоЭлектронногоДокументаБЭД.ЗначениеРеквизитаВДереве(
		ДеревоДанных, ПолныйПуть, СообщатьОбОшибке, НайденнаяСтрока);
	
КонецФункции

Функция ДайджестПлатежногоПоручения(ДвоичныеДанныеФайла)
	
	ДанныеФайла = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанныеФайла);
	
	JSON = ОбщегоНазначения.JSONВЗначение(ДанныеФайла, , Ложь);
	
	СтрокаВозврата = "";
	digestSignatures = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(JSON, "digestSignatures", Неопределено);
	Если ЗначениеЗаполнено(digestSignatures)
		И ТипЗнч(digestSignatures) = Тип("Массив") И digestSignatures.Количество() <> 0 Тогда
		
		СтрокаВозврата = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(digestSignatures[0], "base64Encoded", "");
		
	КонецЕсли;
	
	Возврат СтрокаВозврата;
	
КонецФункции

Функция ДайджестПлатежногоПорученияИзСтруктуры(Структура)
	
	СтрокаВозврата = "";
	
	ОбязательныеПоляДайджеста = ОбязательныеПоляДайджеста();
	
	Для Каждого СтрокаДайджеста Из ОбязательныеПоляДайджеста Цикл
		
		ИмяПоля = "";
		ЗначениеПоля = "";
		Если Найти(СтрокаДайджеста.ИмяПоляДаджеста, "departmentalInfo") Тогда
			
			departmentalInfo = Неопределено;
			ИмяПодПоля = СтрЗаменить(СтрокаДайджеста.ИмяПоляДаджеста, "departmentalInfo.", "");
			
			Если Структура.Свойство("departmentalInfo", departmentalInfo)
				И ЗначениеЗаполнено(departmentalInfo)
				И departmentalInfo.Свойство(ИмяПодПоля, ЗначениеПоля)
				И ЗначениеЗаполнено(ЗначениеПоля) Тогда
				
				ИмяПоля = СтрокаДайджеста.ИмяПоляДаджеста;
				
			КонецЕсли;
			
		ИначеЕсли Структура.Свойство(СтрокаДайджеста.ИмяПоляДаджеста, ЗначениеПоля) 
			И ЗначениеЗаполнено(ЗначениеПоля)
			И НРег(Строка(ЗначениеПоля)) <> "null" Тогда
			
			ИмяПоля = СтрокаДайджеста.ИмяПоляДаджеста;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИмяПоля) И ЗначениеЗаполнено(ЗначениеПоля) Тогда
			
			Если ЗначениеЗаполнено(СтрокаДайджеста.ФорматнаяСтрока) Тогда
				
				НовоеЗначение = Формат(ЗначениеПоля, СтрокаДайджеста.ФорматнаяСтрока);
				
			ИначеЕсли ТипЗнч(ЗначениеПоля) = Тип("Число") Тогда
				
				НовоеЗначение = СтрЗаменить(Строка(ЗначениеПоля), Символы.НПП, "");
				НовоеЗначение = СтрЗаменить(НовоеЗначение, ",", ".");
				НовоеЗначение = СтрЗаменить(НовоеЗначение, ".00", "");
				
			Иначе
				
				НовоеЗначение = ЗначениеПоля;
				
			КонецЕсли;
			
			СтрокаВозврата = СтрокаВозврата + СтрокаДайджеста.ИмяПоляДаджеста + "=" + НовоеЗначение + Символ(10);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаВозврата = СокрЛП(СтрокаВозврата);
	
	Возврат СтрокаВозврата;
	
КонецФункции

Функция ОбязательныеПоляДайджеста()
	
	ОбязательныеПоля = Новый ТаблицаЗначений();
	ОбязательныеПоля.Колонки.Добавить("ИмяПоляДаджеста");
	ОбязательныеПоля.Колонки.Добавить("ИмяПоляПлатежки");
	ОбязательныеПоля.Колонки.Добавить("ФорматнаяСтрока");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "amount", "amount");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "date", "date", "ДФ=yyyy-MM-dd");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "departmentalInfo.docNumber108", "departmentalInfo.docNumber108");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "departmentalInfo.drawerStatus101", "departmentalInfo.drawerStatus101");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "departmentalInfo.kbk", "departmentalInfo.kbk");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "departmentalInfo.oktmo", "departmentalInfo.oktmo");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "departmentalInfo.paymentKind110", "departmentalInfo.paymentKind110");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "departmentalInfo.reasonCode106", "departmentalInfo.reasonCode106");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "departmentalInfo.uip", "departmentalInfo.uip");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "externalId", "externalId");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "incomeTypeCode", "incomeTypeCode");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "operationCode", "operationCode");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payeeAccount", "payeeAccount");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payeeBankBic", "payeeBankBic");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payeeBankCorrAccount", "payeeBankCorrAccount");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payeeInn", "payeeInn");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payeeKpp", "payeeKpp");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payeeName", "payeeName");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payerAccount", "payerAccount");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payerBankBic", "payerBankBic");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payerBankCorrAccount", "payerBankCorrAccount");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payerInn", "payerInn");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payerKpp", "payerKpp");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "payerName", "payerName");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "priority", "priority");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "purpose", "purpose");
	ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, "voCode", "voCode");
	
	Возврат ОбязательныеПоля;
	
КонецФункции

Процедура ДобавитьОбязательноеПолеДайджеста(ОбязательныеПоля, ИмяПоляДаджеста, ИмяПоляПлатежки, ФорматнаяСтрока = "")
	
	НоваяСтрока = ОбязательныеПоля.Добавить();
	НоваяСтрока.ИмяПоляДаджеста = ИмяПоляДаджеста;
	НоваяСтрока.ИмяПоляПлатежки = ИмяПоляПлатежки;
	НоваяСтрока.ФорматнаяСтрока = ФорматнаяСтрока;
	
КонецПроцедуры

Функция ИмяСлужебныхНастроекSberAPI()
	
	Возврат "ОбменСБанкамиSberAPI";
	
КонецФункции

#Область Авторизация

Функция РезультатПроверкиДоступаКДанным(ПараметрыОтвета, Организация, Банк, НомерСчета)
	
	ВозвращаемоеЗначение = Новый Структура("Успех", Ложь);
	
	ДоступныеСчета = Новый Массив;
	
	Для Каждого Элемент Из ПараметрыОтвета.accounts Цикл
		ДоступныеСчета.Добавить(Элемент.number)
	КонецЦикла;
	
	ВозвращаемоеЗначение.Вставить("ДоступныеСчета", ДоступныеСчета);
	
	Если ЗначениеЗаполнено(НомерСчета) Тогда
		ВозвращаемоеЗначение.Успех = ДоступныеСчета.Найти(НомерСчета) <> Неопределено;
	Иначе
		БИК = ОбменСБанкамиСлужебный.БИК(Банк);
		РеквизитИНН = ОбменСБанкамиСлужебный.ИмяПрикладногоРеквизита("ИННОрганизации");
		ИНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, РеквизитИНН);
		ЕстьДоступ = Ложь;
		Для Каждого Элемент Из ПараметрыОтвета.accounts Цикл
			БИКСовпадает = Ложь;
			ИННСовпадает = Ложь;
			ЕстьБИК = Ложь;
			ЕстьИНН = Ложь;
			Если Элемент.Свойство("bic") И ЗначениеЗаполнено(Элемент.bic) Тогда
				ЕстьБИК = Истина;
				Если БИК = Элемент.bic Тогда
					БИКСовпадает = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если Элемент.Свойство("inn") И ЗначениеЗаполнено(Элемент.inn) Тогда
				ЕстьИНН = Истина;
				Если ИНН = Элемент.inn Тогда
					ИННСовпадает = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если БИКСовпадает И ИННСовпадает Или Не ЕстьБИК И Не ЕстьИНН
				Или Не ЕстьИНН И БИКСовпадает Или Не ЕстьБИК И ИННСовпадает Тогда
					ЕстьДоступ = Истина;
					Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ВозвращаемоеЗначение.Успех = ЕстьДоступ И ЗначениеЗаполнено(ДоступныеСчета);
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение
	
КонецФункции

#КонецОбласти

#Область ПлатежныеПоручения

Процедура ЗаполнитьБюджетныеРеквизиты(ДеревоДанных, ДанныеПлатежа, ЭтоПеречислениеВБюджет)
	
	СтатусСоставителя = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет.СтатусСоставителя");
	ПлатежиВБюджетВДереве = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет");
	ЭтоПеречислениеВБюджет = ПлатежиВБюджетВДереве = Истина И ЗначениеЗаполнено(СтатусСоставителя);
	Код = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлатежа.Код");
	КодВыплат = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет.КодВыплат");
	
	Если ЭтоПеречислениеВБюджет Или ЗначениеЗаполнено(Код) Или ЗначениеЗаполнено(КодВыплат) Тогда
		
		БюджетныеРеквизиты = Новый Структура;
		
		Если ЭтоПеречислениеВБюджет И Не ЗначениеЗаполнено(Код) Тогда
			Код = "0";
		КонецЕсли;
		
		ДобавитьЗаполненноеЗначение(БюджетныеРеквизиты, "uip", Код);
		
		Если ЭтоПеречислениеВБюджет Тогда
			
			БюджетныеРеквизиты.Вставить("drawerStatus101", СтатусСоставителя);
			
			ПоказательКБК = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет.ПоказательКБК");
			ПоказательКБК = ?(Не ПустаяСтрока(ПоказательКБК), СокрЛП(ПоказательКБК), "0");
			
			БюджетныеРеквизиты.Вставить("kbk", ПоказательКБК);
			
			ОКТМО = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет.ОКТМО");
			Если Не ЗначениеЗаполнено(ОКТМО) Или ПустаяСтрока(ОКТМО) Тогда
				ОКТМО = "0";
			КонецЕсли;
			
			БюджетныеРеквизиты.Вставить("oktmo", ОКТМО);
			
			ПоказательОснования = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет.ПоказательОснования");
			Если Не ЗначениеЗаполнено(ПоказательОснования) Или ПустаяСтрока(ПоказательОснования) Тогда
				ПоказательОснования = "0";
			КонецЕсли;
			
			БюджетныеРеквизиты.Вставить("reasonCode106", ПоказательОснования);
			
			ПоказательПериода = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет.ПоказательПериода");
			Если Не ЗначениеЗаполнено(ПоказательПериода) Или ПустаяСтрока(ПоказательПериода)
				Или ПоказательПериода = "  .  .    " Тогда
				ПоказательПериода = "0";
			КонецЕсли;
			
			БюджетныеРеквизиты.Вставить("taxPeriod107", ПоказательПериода);
			
			ПоказательНомера = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет.ПоказательНомера");
			Если Не ЗначениеЗаполнено(ПоказательНомера) Или ПустаяСтрока(ПоказательНомера) Тогда
				ПоказательНомера = "0";
			КонецЕсли;
			
			БюджетныеРеквизиты.Вставить("docNumber108", ПоказательНомера);
			
			ПоказательДаты = ЗначениеРеквизитаВДереве(ДеревоДанных, "ПлатежиВБюджет.ПоказательДаты");
			Если Не ЗначениеЗаполнено(ПоказательДаты) Тогда
				ПоказательДаты = "0";
			КонецЕсли;
			
			БюджетныеРеквизиты.Вставить("docDate109", ПоказательДаты);
			
		КонецЕсли;
		
		ДобавитьЗаполненноеЗначение(БюджетныеРеквизиты, "paymentKind110", КодВыплат);
		ДанныеПлатежа.Вставить("departmentalInfo", БюджетныеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПлательщикаИПолучателя(ДеревоДанных, ДанныеПлатежа, ЭтоПеречислениеВБюджет)
	
	ПолучательРасчСчет = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПолучателя.РасчСчет");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payeeAccount", ПолучательРасчСчет);
	
	ПолучательБИКБанка = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПолучателя.Банк.БИК");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payeeBankBic", ПолучательБИКБанка);
	
	ПолучательКоррСчет = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПолучателя.Банк.КоррСчет");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payeeBankCorrAccount", ПолучательКоррСчет);
	
	ПолучательИНН = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПолучателя.ИНН");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payeeInn", ПолучательИНН);
	
	ПолучательКПП = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПолучателя.КПП");
	Если Не ЗначениеЗаполнено(ПолучательКПП) И ЭтоПеречислениеВБюджет Тогда
		ПолучательКПП = "0";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПолучательКПП) Тогда
		ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payeeKpp", ПолучательКПП);
	КонецЕсли;
	
	ПолучательНаименование = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПолучателя.Наименование");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payeeName", ПолучательНаименование);
	
	ПлательщикРасчСчет = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлательщика.РасчСчет");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payerAccount", ПлательщикРасчСчет);
	
	ПлательщикБИКБанка = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлательщика.Банк.БИК");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payerBankBic", ПлательщикБИКБанка);
	
	ПлательщикКоррСчет = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлательщика.Банк.КоррСчет");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payerBankCorrAccount", ПлательщикКоррСчет);
	
	ПлательщикИНН = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлательщика.ИНН");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payerInn", ПлательщикИНН);
	
	ПлательщикКПП = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлательщика.КПП");
	Если Не ЗначениеЗаполнено(ПлательщикКПП) И ЭтоПеречислениеВБюджет Тогда
		ПлательщикКПП = "0";
	КонецЕсли;
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payerKpp", ПлательщикКПП);
	
	ПлательщикНаименование = ЗначениеРеквизитаВДереве(ДеревоДанных, "РеквизитыПлательщика.Наименование");
	ДобавитьЗаполненноеЗначение(ДанныеПлатежа, "payerName", ПлательщикНаименование);
	
КонецПроцедуры

Функция ТекстЗапросаДляПолученияСтатусовПлатежей()
	
	Возврат
	"ВЫБРАТЬ
	|	СообщениеОбменСБанками.Идентификатор,
	|	СообщениеОбменСБанками.НомерСчета КАК НомерСчета,
	|	СообщениеОбменСБанками.Ссылка
	|ИЗ
	|	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	|ГДЕ
	|	СообщениеОбменСБанками.НастройкаОбмена = &НастройкаОбмена
	|	И СообщениеОбменСБанками.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение)
	|	И СообщениеОбменСБанками.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.Доставлен),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.Отправлен), ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.Подписан),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.Принят), ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.Приостановлен),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.ЧастичноПодписан),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.НеПодтвержден))
	|	И НЕ СообщениеОбменСБанками.ПометкаУдаления
	|	И &УсловиеОтбораПоСчетам
	|ИТОГИ
	|ПО
	|	НомерСчета";
	
КонецФункции

#КонецОбласти

#Область Выписка

Функция СохранитьВыписку(НастройкаОбмена, НомерСчета, ВыпискаСтрокой, Дата, СоздаватьОперацииВыписки)
	
	ДвоичныеДанныеФайлаОтвета = ДвоичныеДанныеИзСтроки(ВыпискаСтрокой);
	СсылкаНаХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайлаОтвета);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Статус", Перечисления.СтатусыОбменСБанками.Получен);
	СтруктураРеквизитов.Вставить("ВидЭД", Перечисления.ВидыЭДОбменСБанками.ВыпискаБанка);
	СтруктураРеквизитов.Вставить("НастройкаОбмена", НастройкаОбмена);
	СтруктураРеквизитов.Вставить("Направление", Перечисления.НаправленияЭДО.Входящий);
	СтруктураРеквизитов.Вставить("АдресФайлаВоВременномХранилище", СсылкаНаХранилище);
	СтруктураРеквизитов.Вставить("СсылкаНаОбъект", НастройкаОбмена);
	СтруктураРеквизитов.Вставить("ДатаНачала", Дата);
	СтруктураРеквизитов.Вставить("ДатаОкончания", Дата);
	СтруктураРеквизитов.Вставить("НомерСчета", НомерСчета);
	СтруктураРеквизитов.Вставить("ФорматФайла", "json");
	
	СообщениеВыписка = Неопределено;
	ОбменСБанкамиСлужебный.СохранитьСообщениеОбмена(СтруктураРеквизитов, СообщениеВыписка);
	
	ДанныеВыписок = ОбменСБанками.ДанныеВыпискиБанкаТаблицаЗначений(СообщениеВыписка);
	ДанныеВыписки = ДанныеВыписок.Получить(0);
	ОбменСБанкамиСлужебный.ОпределитьИсполненныеПлатежныеПоручения(НастройкаОбмена, ДанныеВыписки);
	
	ОбменСБанкамиСлужебный.СохранитьДатуПоследнейОперации(НастройкаОбмена, Дата);
	
	Если СоздаватьОперацииВыписки Тогда
		ОбменСБанкамиПереопределяемый.ПриПолученииВыписки(СообщениеВыписка);
	КонецЕсли;
	
	Возврат СообщениеВыписка;
	
КонецФункции

// Получает из банка выписки по счетам банка за период
//
// Параметры:
//   НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена, если она есть.
//   ТокенАутентификации - Строка - токен аутентификации в сервисе SberAPI
//   НомерСчета - Строка - номер счета
//   ДатаНачала - Дата - дата начала периода запроса выписки
//   ДатаОкончания - Дата - дата окончания периода запроса выписки
//   ТребуетсяАутентификация - Булево - если Истина, то закончилось время жизни сессии
//   									и нужно заново пройти процесс аутентификации.
//
// Возвращаемое значение:
//  Соответствие - данные выписок по счетам:
//    * Ключ - Строка - номер счета
//    * Значение - Соответствие - данные выписок по датам в формате JSON
//
Функция ПолучитьВыпискиПоСчетамБанка(Параметры,
						  ТокенДоступа,
						  МассивБанковскихСчетов,
						  ДатаНачала,
						  ДатаОкончания,
						  ТребуетсяАутентификация)
	
	ТребуетсяАутентификация = Ложь;
	Выписки = Новый Соответствие;
	
	Исключения = Новый Массив;
	СообщениеОбОшибках = Новый Массив;
	Счетчик = 0;
	
	Для Каждого НомерСчета Из МассивБанковскихСчетов Цикл
		Выписки.Вставить(НомерСчета, Новый Соответствие);
		ДатаВыписки = ДатаНачала;
		Пока ДатаВыписки <= ДатаОкончания Цикл
			Отказ = Ложь;
			ПолнаяВыписка = Неопределено;
			
			КоличествоОшибок = Исключения.Количество();
			ПолучитьИтогиПоВыписке(
				Параметры,
				ТокенДоступа,
				НомерСчета,
				ДатаВыписки,
				ПолнаяВыписка,
				ТребуетсяАутентификация,
				Исключения,
				СообщениеОбОшибках);
			Счетчик = Счетчик + 1;
			Если КоличествоОшибок < Исключения.Количество() Тогда // при получении итогов вызвано исключение
				ДатаВыписки = ДатаВыписки + 60 * 60 * 24; // добавить день
				Продолжить;
			КонецЕсли;
			
			Если ТребуетсяАутентификация Тогда
				Возврат Неопределено;
			КонецЕсли;
			
			Если ПолнаяВыписка = Неопределено Тогда
				Продолжить; // выписка формируется на стороне банка
			КонецЕсли;
			
			ВыпискиНет = Ложь;
			Операции = Новый Массив;
			ПолучитьОперацииЗаДень(
				Параметры, ТокенДоступа, НомерСчета, ДатаВыписки, Операции, ТребуетсяАутентификация, ВыпискиНет);
			
			Если ТребуетсяАутентификация Тогда
				Возврат Неопределено;
			КонецЕсли;
			
			Если ВыпискиНет Тогда
				ДатаВыписки = ДатаВыписки + 60 * 60 * 24; // добавить день
				Продолжить;
			КонецЕсли;
			
			ПолнаяВыписка.Вставить("transactions", Новый Массив);
			
			Для Каждого Операция Из Операции Цикл
				ПолнаяВыписка.transactions.Добавить(Операция);
			КонецЦикла;
			
			Выписки[НомерСчета].Вставить(ДатаВыписки, ОбменСБанкамиСлужебный.ЗначениеВJSON(ПолнаяВыписка));
			
			ДатаВыписки = ДатаВыписки + 60 * 60 * 24; // добавить день
			
		КонецЦикла;
	КонецЦикла;
	
	Если Счетчик > 0 И Исключения.Количество() = Счетчик Тогда
		ВызватьИсключение Исключения[0];
	КонецЕсли;
	
	Возврат Выписки;
	
КонецФункции

Процедура ПолучитьИтогиПоВыписке(Параметры,
								 ТокенДоступа,
								 НомерСчета,
								 Дата,
								 Итоги,
								 ТребуетсяАутентификация,
								 Исключения,
								 СообщениеОбОшибках)
	
	Путь = "v2/statement/summary?accountNumber=" + НомерСчета + "&statementDate=" + Формат(Дата, "ДФ=yyyy-MM-dd");
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(Параметры, "ПолучитьИтогиПоВыписке");
	
	АдресРесурса = "fintech/api/" + Путь;
	
	ТекстЗапроса = "";
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "GET";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.ТокенДоступа = ТокенДоступа;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = Параметры.НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
	Если РезультатВыполнения.Статус Тогда
		
		Итоги = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
		Возврат;
		
	КонецЕсли;
	
	ТребуетсяВызватьИсключение = Истина;
	СообщениеОбОшибке = РезультатВыполнения.СообщениеОбОшибке;
	Если ЗначениеЗаполнено(РезультатВыполнения.КодСостояния) Тогда
		ВидОперации = НСтр("ru = 'Получение выписки через сервис SberAPI'");
		Если РезультатВыполнения.КодСостояния = 401 Тогда
			
			ДанныеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДанныеОшибки(
				РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			Если ДанныеОшибки.ТэгОшибки = "AUTH_TOKEN_IS_EXPIRED" Или ДанныеОшибки.ТэгОшибки = "UNAUTHORIZED" Тогда
					
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
					ВидОперации, ДанныеОшибки.ТекстСообщения, ДанныеОшибки.ТекстСообщения, Параметры.НастройкаОбмена);
				ТребуетсяАутентификация = Истина;
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если РезультатВыполнения.КодСостояния = 202 Тогда
			Возврат; // выписка формируется на стороне банка.
		Иначе
			Шаблон = НСтр("ru = 'При получении итогов по выписке произошла ошибка.
				|Код ошибки: %1
				|Сообщение об ошибке: %2
				|Тело ответа: %3'");
			ТекстОшибки = СтрШаблон(Шаблон, РезультатВыполнения.КодСостояния, СообщениеОбОшибке, РезультатВыполнения.Тело);
			
			Если РезультатВыполнения.КодСостояния =
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.КодЗапрашиваемыйРесурсНеНайденНаСервере() Тогда
				// Отдельно обрабатываем код отсутствия сформированной выписки за день.
				// Чтобы не прерывать загрузку выписки за другие дни периода,
				// выводим сообщение об ошибке, а не вызываем исключение.
				// Но т.к. сообщение будет выведено еще и в ПолучитьСтраницуВыписки(), тут его не выводим.
				ТекстСообщения = "";
				ТребуетсяВызватьИсключение = Ложь;
			Иначе
				ТекстСообщения = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
					РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			КонецЕсли;
			
			Если СтрНайти(НРег(РезультатВыполнения.Тело), "<html>") > 0 Тогда
				СообщениеОбОшибке = РезультатВыполнения.Тело;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекстСообщения) Тогда
				Если СообщениеОбОшибках.Найти(ТекстСообщения) = Неопределено Тогда
					СообщениеОбОшибках.Добавить(ТекстСообщения);
					ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
						ВидОперации, ТекстОшибки, ТекстСообщения, Параметры.НастройкаОбмена);
					СообщениеОбОшибке = ТекстСообщения;
				КонецЕсли;
			Иначе
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, , Параметры.НастройкаОбмена);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ТребуетсяВызватьИсключение Тогда
		Исключения.Добавить(СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьОперацииЗаДень(Параметры,
								 ТокенДоступа,
								 НомерСчета,
								 Дата,
								 Операции,
								 ТребуетсяАутентификация,
								 ВыпискиНет)
	
	href = "?accountNumber=" + НомерСчета + "&statementDate=" + Формат(Дата, "ДФ=yyyy-MM-dd") + "&page=1";
	
	ПараметрыПодключения = СлужебныеПараметрыПодключения(Параметры, "ПолучитьОперацииЗаДень");
	
	АдресРесурса = "fintech/api/v2/statement/transactions" + href;
	
	ВыпискаПолностьюПолучена = Ложь;
	
	Пока Не ВыпискаПолностьюПолучена Цикл
		
		Страница = Неопределено;
		
		ПараметрыЗапроса = Новый Структура();
		ПараметрыЗапроса.Вставить("Дата", Дата);
		ПараметрыЗапроса.Вставить("ТокенДоступа", ТокенДоступа);
		ПараметрыЗапроса.Вставить("АдресРесурса", АдресРесурса);
		ПараметрыЗапроса.Вставить("ФайлСертификата", ПараметрыПодключения.ФайлСертификата);
		ПараметрыЗапроса.Вставить("СертификатПодключенияПароль", ПараметрыПодключения.СертификатПодключенияПароль);
		
		ПолучитьСтраницуВыписки(Параметры, ПараметрыЗапроса, Страница, ТребуетсяАутентификация, ВыпискиНет);
		
		Если ТребуетсяАутентификация Или ВыпискиНет Тогда
			Возврат;
		КонецЕсли;
		
		ЕстьСледующаяСтраница = Ложь;
		Если Страница.Свойство("cause") И Страница.cause = "STATEMENT_RESPONSE_PROCESSING" Тогда
			// Выписка еще не сформирована
			Продолжить;
		ИначеЕсли Страница._links.Количество() Тогда
			Для Каждого Элемент Из Страница._links Цикл
				Если Элемент.rel = "next" Тогда
					ЕстьСледующаяСтраница = Истина;
					href = Элемент.href;
					АдресРесурса = "fintech/api/v2/statement/transactions" + href;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Не ЕстьСледующаяСтраница Тогда
				ВыпискаПолностьюПолучена = Истина;
			КонецЕсли;
		Иначе
			ВыпискаПолностьюПолучена = Истина;
		КонецЕсли;
		
		Для Каждого Операция Из Страница.transactions Цикл
			Операции.Добавить(Операция);
		КонецЦикла;
		
	КонецЦикла;
	
	УдалитьВременныйФайлСертификата(ПараметрыПодключения.ФайлСертификата);
	
КонецПроцедуры

Процедура ПолучитьСтраницуВыписки(Параметры,
								  ПараметрыЗапроса,
								  Страница,
								  ТребуетсяАутентификация,
								  ВыпискиНет)
	
	ТекстЗапроса = "";
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "GET";
	ПараметрыHTTPЗапроса.АдресРесурса = ПараметрыЗапроса.АдресРесурса;
	ПараметрыHTTPЗапроса.ТокенДоступа = ПараметрыЗапроса.ТокенДоступа;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = Параметры.НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = ПараметрыЗапроса.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = ПараметрыЗапроса.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	Если Не РезультатВыполнения.Статус Тогда
		СообщениеОбОшибке = РезультатВыполнения.СообщениеОбОшибке;
		Если ЗначениеЗаполнено(РезультатВыполнения.КодСостояния) Тогда
			ВидОперации = НСтр("ru = 'Получение выписки через сервис SberAPI'");
			Если РезультатВыполнения.КодСостояния = 401 Тогда
				ДанныеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДанныеОшибки(
					РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
				Если ДанныеОшибки.ТэгОшибки = "AUTH_TOKEN_IS_EXPIRED" Или ДанныеОшибки.ТэгОшибки = "UNAUTHORIZED" Тогда
					ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
						ВидОперации, ДанныеОшибки.ТекстСообщения, ДанныеОшибки.ТекстСообщения, Параметры.НастройкаОбмена);
					ТребуетсяАутентификация = Истина;
					
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Шаблон = НСтр("ru = 'При получении выписки произошла ошибка.
								|Код ошибки: %1
								|Сообщение об ошибке %2
								|Тело ответа: %3'");
			ТекстОшибки = СтрШаблон(Шаблон, РезультатВыполнения.КодСостояния, СообщениеОбОшибке, РезультатВыполнения.Тело);
			Если РезультатВыполнения.КодСостояния =
				ОбменСБанкамиSberAPIСлужебныйКлиентСервер.КодЗапрашиваемыйРесурсНеНайденНаСервере() Тогда
				// Отдельно обрабатываем код отсутствия сформированной выписки за день.
				// Чтобы не прерывать загрузку выписки за другие дни периода,
				// выводим сообщение об ошибке, а не вызываем исключение.
				ШаблонСообщения =
					НСтр("ru = 'Выписка за %1 недоступна, пожалуйста, обратитесь в техническую поддержку банка'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, Формат(ПараметрыЗапроса.Дата, "ДЛФ=D"));
				ВыпискиНет = Истина;
			Иначе
				ТекстСообщения = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
					РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
			КонецЕсли;
			
			СообщениеОбОшибке = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.СформироватьДетальныйТекстСообщения(
				ТекстСообщения, СообщениеОбОшибке);
			ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(
				ВидОперации, ТекстОшибки, ТекстСообщения, Параметры.НастройкаОбмена);
		КонецЕсли;
		
		Если Не ВыпискиНет Тогда
			ВызватьИсключение СообщениеОбОшибке;
		КонецЕсли;
	КонецЕсли;
	
	Страница = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
	
КонецПроцедуры

Процедура ПолучитьПоследниеВыписки(Параметры, МассивБанковскихСчетов, КоличествоПолучено)
	
	ПараметрыЗапроса = ОбщегоНазначения.СкопироватьРекурсивно(Параметры);
	
	ПараметрыЗапроса.Вставить("ДатаНачала", ОбменСБанкамиСлужебный.ДатаНачалаЗапросаВыписки(Параметры.НастройкаОбмена));
	ПараметрыЗапроса.Вставить("ДатаОкончания", ТекущаяДатаСеанса());
	ПараметрыЗапроса.Вставить("МассивБанковскихСчетов", МассивБанковскихСчетов);
	ПараметрыЗапроса.Вставить("СоздаватьОперацииВыписки", Истина);
	
	Адрес = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	ПолучитьВыпискуБанка(ПараметрыЗапроса, Адрес);
	
	ДанныеВыписок = ПолучитьИзВременногоХранилища(Адрес);
	Если ЗначениеЗаполнено(ДанныеВыписок) Тогда
	
		Для Каждого ДанныеВыписки Из ДанныеВыписок Цикл
			КоличествоПолучено = КоличествоПолучено + 1;
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область JSON

Функция СтрокаJSON(ЗаписьДанныхСообщения)
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

Функция НовыйJSON()
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	Возврат ЗаписьДанныхСообщения;
	
КонецФункции

#КонецОбласти

Функция ИнформацияОПользователе(НастройкаОбмена, ТокенДоступа, СертификатПодключения)
	
	//ВидПодписи - могут быть значения "Единственная подпись", "Первася подпись", "Вторая подпись"
	ИнформацияОПользователе = Новый Структура("ВидПодписи, ЭтоЕИО", "", Ложь);
	
	ТекстЗапроса = "";
	АдресРесурса = "ic/sso/api/v2/oauth/user-info";
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "GET";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.ТокенДоступа = ТокенДоступа;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = СертификатПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = СертификатПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	СообщениеОбОшибке = "";
	ПодробноеПредставлениеОшибки = "";
	
	Если РезультатВыполнения.Статус Тогда
		
		Тело = РезультатВыполнения.Тело;
		
		ПерваяТочка = СтрНайти(Тело, ".",,, 1);
		ВтораяТочка = СтрНайти(Тело, ".",,, 2);
		МожноВыполнятьДальше = Истина;
		
		Если ПерваяТочка = 0 Или ВтораяТочка = 0 Тогда
			
			ПодробноеПредставлениеОшибки = НСтр("ru = 'Нечитаемые данные метода user-info'");
			МожноВыполнятьДальше = Ложь;
			
		КонецЕсли;
		
		Если МожноВыполнятьДальше Тогда
			
			СтрокаBase64 = Сред(Тело, ПерваяТочка+1, ВтораяТочка-ПерваяТочка-1);
			Base64Значение = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДвоичныеДанныеИзBase64Строки(СтрокаBase64);
			
			Если Не ТипЗнч(Base64Значение) = Тип("ДвоичныеДанные") Или Не ЗначениеЗаполнено(Base64Значение) Тогда
				
				ПодробноеПредставлениеОшибки = НСтр("ru = 'Не удалось расшифровать данные ответа в Base64 метода user-info'");
				МожноВыполнятьДальше = Ложь;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если МожноВыполнятьДальше Тогда
			
			ОтветJSON = ПолучитьСтрокуИзДвоичныхДанных(Base64Значение);
			
			Если Найти(ОтветJSON, "{") = 0 Тогда
				
				ПодробноеПредставлениеОшибки = НСтр("ru = 'Не удалось найти JSON в ответе метода user-info'");
				МожноВыполнятьДальше = Ложь;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если МожноВыполнятьДальше Тогда
			
			ПараметрыОтвета = ОбщегоНазначения.JSONВЗначение(ОтветJSON, , Ложь);
			
			userSignatureType = "";
			Если ПараметрыОтвета.Свойство("userSignatureType", userSignatureType) И ЗначениеЗаполнено(userSignatureType) Тогда
				
				ИнформацияОПользователе.ВидПодписи = userSignatureType;
				
			КонецЕсли;
			
			individualExecutiveAgency = "";
			Если ПараметрыОтвета.Свойство("individualExecutiveAgency", individualExecutiveAgency)
				И Строка(individualExecutiveAgency) = "1" Тогда
				
				ИнформацияОПользователе.ЭтоЕИО = Истина;
				
			КонецЕсли;
			
		Иначе
			
			СообщениеОбОшибке = НСтр("ru = 'Нечитаемые данные метода user-info'");
			
		КонецЕсли;
		
	Иначе
		
		ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
			РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
		СообщениеОбОшибке = РезультатВыполнения.СообщениеОбОшибке;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(СообщениеОбОшибке) Или Не ПустаяСтрока(ПодробноеПредставлениеОшибки) Тогда
		
		ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурнал(НастройкаОбмена, "user-info", "", "", "");
		ВидОперации = НСтр("ru = 'Получение информации о пользователе'");
		
		Ошибка = НСтр("ru = 'При получении информации о пользователе произошла ошибка'");
		Шаблон = НСтр("ru = '%1:
							|Сообщение об ошибке %2
							|Тело ответа: %3'");
		ТекстОшибки = СтрШаблон(Шаблон, Ошибка, РезультатВыполнения.СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
		
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, Ошибка, НастройкаОбмена);
		
	КонецЕсли;
	
	Возврат ИнформацияОПользователе;
	
КонецФункции

Функция ПолучитьСертификатыОрганизации(АдресРесурса, ТокенДоступа, НастройкаОбмена, СертификатПодключения)
	
	СертификатыОрганизации = Новый Массив;
	
	ТекстЗапроса = "";
	
	ПараметрыHTTPЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ПараметрыОтправкиHTTPЗапроса();
	ПараметрыHTTPЗапроса.ТипЗапроса = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ТипОтправкиHTTPЗапросаСервер();
	ПараметрыHTTPЗапроса.ВидЗапроса = "GET";
	ПараметрыHTTPЗапроса.АдресРесурса = АдресРесурса;
	ПараметрыHTTPЗапроса.ТокенДоступа = ТокенДоступа;
	ПараметрыHTTPЗапроса.Данные = ТекстЗапроса;
	ПараметрыHTTPЗапроса.НастройкаОбмена = НастройкаОбмена;
	ПараметрыHTTPЗапроса.ФайлСертификата = СертификатПодключения.ФайлСертификата;
	ПараметрыHTTPЗапроса.СертификатПодключенияПароль = СертификатПодключения.СертификатПодключенияПароль;
	
	РезультатВыполнения = ОтправитьHTTPЗапрос(ПараметрыHTTPЗапроса);
	
	Если НЕ РезультатВыполнения.Статус Тогда
		
		ОбменСБанкамиСлужебный.ДобавитьЗаписьВЖурнал(НастройкаОбмена, АдресРесурса, "", "", "");
		ВидОперации = НСтр("ru = 'Получение сертификатов организации'");
		
		ПодробноеПредставлениеОшибки = ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ДетальноеСообщениеИзТекстаОшибки(
			РезультатВыполнения.Тело, РезультатВыполнения.КодСостояния);
		
		Ошибка = СтрШаблон(НСтр("ru = 'При получении сертификатов организации (%1) произошла ошибка'"), АдресРесурса);
		Шаблон = НСтр("ru = '%1:
							|Сообщение об ошибке %2
							|Тело ответа: %3'");
		ТекстОшибки = СтрШаблон(Шаблон, Ошибка, РезультатВыполнения.СообщениеОбОшибке, ПодробноеПредставлениеОшибки);
		
		ОбменСБанкамиSberAPIСлужебныйКлиентСервер.ОбработатьОшибку(ВидОперации, ТекстОшибки, Ошибка, НастройкаОбмена);
		
		Возврат СертификатыОрганизации;
		
	КонецЕсли;
		
	Результат = ОбщегоНазначения.JSONВЗначение(РезультатВыполнения.Тело, , Ложь);
	
	Если СтрНайти(АдресРесурса,"crypto/eio") <> 0 Тогда
		
		// Получим сертификаты для метода crypto/eio
		cryptoUserInfos = Неопределено;
		Если Результат.Свойство("cryptoUserInfos", cryptoUserInfos)
			И ЗначениеЗаполнено(cryptoUserInfos)
			И ТипЗнч(cryptoUserInfos) = Тип("Массив") Тогда
			
			Для Каждого КриптоИнформацияПользователя Из cryptoUserInfos Цикл
				
				// Для справки: поля в cryptoUserInfos: sub, cryptoProfileInfos
				
				Для Каждого КриптоИнформацияПрофиля Из КриптоИнформацияПользователя.cryptoProfileInfos Цикл
					
					// Для справки: поля в cryptoProfileInfos: alias, certificateInfos, typeName
					
					Если КриптоИнформацияПрофиля.typeName <> "Инфокрипт" Тогда
						
						// В Контексте банка под typeName = "Инфокрипт" понимается два вида токена: Инфокрипт и Рутокен
						Продолжить;
						
					КонецЕсли;
					
					Для Каждого ДанныеСертификата Из КриптоИнформацияПрофиля.certificateInfos Цикл
						
						// Для справки: поля в certificateInfos: issuer, serialNumber, uuid, active, cert
						
						Если Не ДанныеСертификата.active Тогда
							
							Продолжить;
							
						КонецЕсли;
						
						СертификатыОрганизации.Добавить(ДанныеСертификата);
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		// Получим сертфикаты для метода crypto
		cryptoProfileInfos = Неопределено;
		Если Результат.Свойство("cryptoProfileInfos", cryptoProfileInfos)
			И ЗначениеЗаполнено(cryptoProfileInfos)
			И ТипЗнч(cryptoProfileInfos) = Тип("Массив") Тогда
			
			Для Каждого КриптоИнформацияПрофиля Из cryptoProfileInfos Цикл
				
				// Для справки: поля в КриптоИнформацияПрофиля: alias, certificateInfos, typeName
				
				Если КриптоИнформацияПрофиля.typeName <> "Инфокрипт" Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				Для Каждого ДанныеСертификата Из КриптоИнформацияПрофиля.certificateInfos Цикл
					
					// Для справки: поля в ДанныеСертификата: issuer, serialNumber, uuid, active, cert
					
					Если Не ДанныеСертификата.active Тогда
						
						Продолжить;
						
					КонецЕсли;
					
					СертификатыОрганизации.Добавить(ДанныеСертификата);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СертификатыОрганизации;
	
КонецФункции

#КонецОбласти
