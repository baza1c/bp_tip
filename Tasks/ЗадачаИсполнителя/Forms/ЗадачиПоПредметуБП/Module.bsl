
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Предмет = Параметры.Предмет;
	
	Список.Параметры.УстановитьЗначениеПараметра("Предмет", Параметры.Предмет);
	
	УстановитьУсловноеОформление();
	
	РолиГруппыПользователя = МоиЗадачиСерверБП.РолиГруппыАвторизованногоПользователя();
	ГруппыИсполнителяЗадач.ЗагрузитьЗначения(РолиГруппыПользователя.Группы);
	РолиИсполнителя.ЗагрузитьЗначения(РолиГруппыПользователя.Роли);
	
	ЕстьПравоИзменениеВзаимодействие = ПравоДоступа("Редактирование", Метаданные.Документы.ПрочееВзаимодействие);
	Элементы.ГруппаСоздатьВзаимодействиеПоЗадаче.Видимость = ЕстьПравоИзменениеВзаимодействие;
	
	ОтборСтатусЗадачи = Перечисления.СтатусЗадачи.НеВыполнена;
	
	МнеОтМеняВсе = МоиЗадачиСерверБП.КодЗадачиМне();
	
	Если ПользователиБП.КоличествоАктуальныхПользователей() <= 1 Тогда
		Элементы.МнеОтМеняВсе.Видимость = Ложь;
		Элементы.РазделительТумблеров.Видимость = Ложь;
	Иначе
		Элементы.МнеОтМеняВсе.Видимость = Истина;
		Элементы.РазделительТумблеров.Видимость = Истина;
	КонецЕсли;
	
	ПриИзмененииОтбораВШапке();
	
	МобильныйКлиентАдаптацияФормы(ЕстьПравоИзменениеВзаимодействие);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЗадачаИсполнителя" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПерехода(ОбъектПерехода, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ОбъектПерехода) Или ОбъектПерехода = Элементы.Список.ТекущаяСтрока Тогда
		Возврат;
	КонецЕсли;
	
	ОтборСтатусЗадачи = ПредопределенноеЗначение("Перечисление.СтатусЗадачи.НеВыполнена");
	УстановитьОтбор();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МнеОтМеняВсеПриИзменении(Элемент)
	
	ПриИзмененииОтбораВШапке();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусЗадачиПриИзменении(Элемент)
	
	ПриИзмененииОтбораВШапке();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	Оповестить("Запись_ЗадачаИсполнителя", ПараметрыОповещения, Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДоступноИзменениСтатуса = Элемент.ТекущиеДанные.ДоступноИзменениеСтатуса;
	
	Элементы.КомандаУстановитьСтатусВыполнена.Доступность   = ДоступноИзменениСтатуса;
	Элементы.КомандаУстановитьСтатусНеВыполнена.Доступность = ДоступноИзменениСтатуса;
	Элементы.КомандаУстановитьСтатусОтменена.Доступность    = ДоступноИзменениСтатуса;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		Если Элемент.ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(Элемент.ТекущиеДанные.БизнесПроцесс) Тогда
			// Если задача создана через бизнес-процесс, тогда копирование запрещаем
			ТестСообщения = СтрШаблон(НСтр("ru = 'Задача: ""%1"" не может быть скопирована'"), Элемент.ТекущиеДанные.Наименование);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТестСообщения);
		КонецЕсли;
	Иначе
		Отказ = Истина;
		ПараметрыСоздания = Новый Структура("Предмет", Предмет);
		ОткрытьФорму("Задача.ЗадачаИсполнителя.Форма.ФормаЗадачиБП", ПараметрыСоздания, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	МоиЗадачиСерверБП.ЗадачиПриПолученииДанныхНаСервере(Строки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаУстановитьСтатусВыполнена(Команда)
	
	УстановитьСтатусЗадачиНаКлиенте(ПредопределенноеЗначение("Перечисление.СтатусЗадачи.Выполнена"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусНеВыполнена(Команда)
	
	УстановитьСтатусЗадачиНаКлиенте(ПредопределенноеЗначение("Перечисление.СтатусЗадачи.НеВыполнена"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусОтменена(Команда)
	
	УстановитьСтатусЗадачиНаКлиенте(ПредопределенноеЗначение("Перечисление.СтатусЗадачи.Отменена"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗвонок(Команда)

	СоздатьНовоеВзаимодействие("ТелефонныйЗвонок");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПисьмо(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЭлектронноеПисьмоПродолжение", ЭтотОбъект, );
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВстречу(Команда)
	
	СоздатьНовоеВзаимодействие("Встреча");
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПрочееВзаимодействие(Команда)
	
	СоздатьНовоеВзаимодействие("ПрочееВзаимодействие");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтбор()
	
	ПараметрыОтбора = Новый Соответствие();
	ПараметрыОтбора.Вставить("Пользователь",           Пользователи.АвторизованныйПользователь());
	ПараметрыОтбора.Вставить("СтатусЗадачи",           ОтборСтатусЗадачи);
	ПараметрыОтбора.Вставить("МнеОтМеняВсе",           МнеОтМеняВсе);
	ПараметрыОтбора.Вставить("РолиИсполнителя",        Новый ФиксированныйМассив(РолиИсполнителя.ВыгрузитьЗначения()));
	ПараметрыОтбора.Вставить("ГруппыИсполнителяЗадач", Новый ФиксированныйМассив(ГруппыИсполнителяЗадач.ВыгрузитьЗначения()));
	УстановитьОтборСписка(Список, ПараметрыОтбора);
	ОбновитьСписокЗадачНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьОтборСписка(Список, ПараметрыОтбора)
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Исполнитель");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Выполнена");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Автор");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "СтатусЗадачи");
	
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ПараметрыОтбора["Пользователь"]);
	Список.Параметры.УстановитьЗначениеПараметра("РолиИсполнителя", ПараметрыОтбора["РолиИсполнителя"]);
	Список.Параметры.УстановитьЗначениеПараметра("ГруппыИсполнителяЗадач", ПараметрыОтбора["ГруппыИсполнителяЗадач"]);
	
	Если ЗначениеЗаполнено(ПараметрыОтбора["СтатусЗадачи"]) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"СтатусЗадачи",
			ПараметрыОтбора["СтатусЗадачи"]);
	КонецЕсли;
	
	Если ПараметрыОтбора["МнеОтМеняВсе"] = МоиЗадачиСерверБП.КодЗадачиМне() Тогда // мне
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Исполнитель",
			ПараметрыОтбора["Пользователь"]);
	ИначеЕсли ПараметрыОтбора["МнеОтМеняВсе"] = МоиЗадачиСерверБП.КодЗадачиОтМеня() Тогда // от меня
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Автор",
			ПараметрыОтбора["Пользователь"]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	МоиЗадачиСерверБП.УстановитьОформлениеЗадач(Список.КомпоновщикНастроек.Настройки.УсловноеОформление);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокЗадачНаСервере()
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОтбораВШапке()
	
	Элементы.Исполнитель.Видимость = МнеОтМеняВсе <> МоиЗадачиСерверБП.КодЗадачиМне();
	Элементы.Автор.Видимость       = МнеОтМеняВсе <> МоиЗадачиСерверБП.КодЗадачиОтМеня();
	
	Элементы.КомандаУстановитьСтатусОтменена.Видимость    = ОтборСтатусЗадачи <> Перечисления.СтатусЗадачи.Отменена;
	Элементы.КомандаУстановитьСтатусВыполнена.Видимость   = ОтборСтатусЗадачи <> Перечисления.СтатусЗадачи.Выполнена;
	Элементы.КомандаУстановитьСтатусНеВыполнена.Видимость = ОтборСтатусЗадачи <> Перечисления.СтатусЗадачи.НеВыполнена;
	
	УстановитьОтбор();
	ОбновитьСписокЗадачНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьСтатусЗадачи(ЗадачиДляИзменения, СтатусЗадачи)
	
	МоиЗадачиСерверБП.УстановитьСтатусЗадачи(ЗадачиДляИзменения, СтатусЗадачи);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусЗадачиНаКлиенте(СтатусЗадачи)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	УстановитьСтатусЗадачи(ВыделенныеСтроки, СтатусЗадачи);
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ОповеститьОбИзменении(ВыделеннаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлектронноеПисьмоПродолжение(ПроверкаВыполнена, ДополнительныеПараметры) Экспорт
	
	Если ПроверкаВыполнена = Истина Тогда
		СоздатьНовоеВзаимодействие("ЭлектронноеПисьмоИсходящее");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеВзаимодействие(ТипОбъекта)

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеУчастника = Новый Структура;
	
	ДанныеУчастника.Вставить("Контакт", Предмет);
	ДанныеУчастника.Вставить("КакСвязаться", "");
	ДанныеУчастника.Вставить("Представление", Строка(Предмет));
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ДанныеУчастника", ДанныеУчастника);
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ПредметВзаимодействия", ТекущиеДанные.Ссылка);
	ЗначенияЗаполнения.Вставить("Тема", ТекущиеДанные.Наименование);
	ПараметрыСоздания.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие(ТипОбъекта, ПараметрыСоздания, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы(РазрешеноРедактирование)
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы(РазрешеноРедактирование);
	
	АдаптироватьМКОтборыФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы(РазрешеноРедактирование)
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.КомандаСоздать = "ФормаСоздать";
	КомандыПоля.РазрешеноИзменение = РазрешеноРедактирование;
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКОтборыФормы()
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКБыстрыеОтборы(ЭтотОбъект, Элементы.ГруппаОтбор);
	МобильныйКлиентБПФормаСписка.УстановитьВидимостьМКБыстрыеОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.ГруппаСоздатьВзаимодействиеПоЗадаче,
		Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы,
		Элементы.ГруппаИзменитьСтатус,
		Элементы.Список.КонтекстноеМеню);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормы(ЭтотОбъект);
	
	//В реквизитах установить "Использовать всегда" для источника КолонкаСостояниеДокумента
	МобильныйКлиентБПФормаСписка.ДобавитьКолонкаСостояниеДокумента(ЭтотОбъект, "СтандартнаяКартинка", "Список");
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СостояниеДокумента(ЭтотОбъект, Элементы.ПрикрепленныеФайлы);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, Элементы.СрокИсполнения);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, Элементы.Время);
	
	Элементы.Наименование.РастягиватьПоГоризонтали = Истина;
	Элементы.Наименование.АвтоВысотаЯчейки = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Наименование);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Исполнитель, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.СтатусЗадачи, 2);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Автор, 3);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
КонецПроцедуры

#КонецОбласти

