
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.СтатусЗадачи = Перечисления.СтатусЗадачи.НеВыполнена;
		Объект.СрокИсполнения = КонецДня(ОбщегоНазначения.ТекущаяДатаПользователя());
		Если ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Предмет")) Тогда
			Параметры.Свойство("Предмет", Объект.Предмет);
			ЗаполнитьИсполнителяПоПредмету();
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Объект.Исполнитель) Тогда
			Объект.Исполнитель = ТекущийПользователь;
		КонецЕсли;
		ЗаполнитьНастройкиОповещения();
	КонецЕсли;
	
	Элементы.ТипПредмета.СписокВыбора.Очистить();
	ДоступныеТипыПредмета = МоиЗадачиСерверБП.ДоступныеТипыСвязиСЗадачей();
	Для Каждого ТекущийТип Из ДоступныеТипыПредмета Цикл
		Элементы.ТипПредмета.СписокВыбора.Добавить(ТекущийТип.Значение, ТекущийТип.Представление);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Объект.Предмет) Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипЗнч(Объект.Предмет));
		ТипПредмета = Новый ОписаниеТипов(МассивТипов);
	ИначеЕсли ДоступныеТипыПредмета.Количество() > 0 Тогда
		ТипПредмета = ДоступныеТипыПредмета[0].Значение;
	КонецЕсли;
	
	ВысокаяВажность = (Объект.Важность = Перечисления.ВариантыВажностиЗадачи.Высокая);
	
	Исполнитель = ?(Объект.Исполнитель = ТекущийПользователь, Справочники.Пользователи.ПустаяСсылка(), Объект.Исполнитель);
	МнеНаКонтроле = ?(Объект.Исполнитель = ТекущийПользователь, ЗначениеМне(), ЗначениеИсполнителю());
	
	Время = ?(Объект.СрокИсполнения = КонецДня(Объект.СрокИсполнения), "01010001000000", Объект.СрокИсполнения);
	УстановитьВремя = ?(Объект.СрокИсполнения = КонецДня(Объект.СрокИсполнения), 0, 1);
	
	ЕстьПравоИзменениеВзаимодействие = ПравоДоступа("Редактирование", Метаданные.Документы.ПрочееВзаимодействие);
	Элементы.ГруппаСоздатьВзаимодействиеПоЗадаче.Видимость = ЕстьПравоИзменениеВзаимодействие;
	
	// В качестве исполнителя задачи мы можем выбирать только пользователя.
	Элементы.Исполнитель.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
	Элементы.Исполнитель.Видимость = (ПользователиБП.КоличествоАктуальныхПользователей() > 1);
	Элементы.ГруппаПредмет.Видимость = (ДоступныеТипыПредмета.Количество() > 0);
	
	ОграничитьТипПредмета(ЭтотОбъект);

	ЗаполнитьСписокВыбораЭлементаВремяНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьНастройкиОповещения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БизнесПроцессыИЗадачиКлиент.ФормаЗадачиОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НастроитьОповещенияПриЗаписиЗадачи(ТекущийОбъект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(Объект.Исполнитель) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Исполнитель'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Исполнитель",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ЗадачаИсполнителя", ПараметрыЗаписи, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МнеНаКонтролеПриИзменении(Элемент)
	
	МнеНаКонтролеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Объект.Исполнитель = Исполнитель;
	МнеНаКонтроле = ЗначениеИсполнителю();
	
	НастроитьОповещенияПриИзмененииИсполнителя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВажностьПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Если ВысокаяВажность Тогда
		Объект.Важность = ПредопределенноеЗначение("Перечисление.ВариантыВажностиЗадачи.Высокая");
	Иначе
		Объект.Важность = ПредопределенноеЗначение("Перечисление.ВариантыВажностиЗадачи.Обычная");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СрокИсполненияПриИзменении(Элемент)
	
	УстановитьСрокИсполнения();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВремяПриИзменении(Элемент)
	
	УстановитьСрокИсполнения(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Время) Тогда
		Объект.СрокИсполнения = НачалоДня(Объект.СрокИсполнения) + Час(Время)*60*60 + Минута(Время)*60;
	Иначе
		Объект.СрокИсполнения = НачалоДня(Объект.СрокИсполнения);
	КонецЕсли;
	
	УстановитьВремя = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсторияВыполненияНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("РезультатВыполнения", Объект.РезультатВыполнения);
	ОткрытьФорму("Задача.ЗадачаИсполнителя.Форма.ИсторияВыполненияБП", ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПредметаПриИзменении(Элемент)
	
	ОграничитьТипПредмета(ЭтотОбъект);
	
	// При переключении типа предмета необходимо сбросить значение предыдущего выбора
	Объект.Предмет = ТипПредмета.ПривестиЗначение(Объект.Предмет);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Предмет) Тогда
		ЗаполнитьИсполнителяПоПредмету();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ТипПредметаКонтакт(ТипПредмета) Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыВыбораПредмета = Новый Структура("Лид", Истина);
		ОткрытьФорму("Справочник.КонтактныеЛица.ФормаВыбора", ПараметрыВыбораПредмета, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметСоздание(Элемент, СтандартнаяОбработка)
	
	Если ТипПредметаКонтакт(ТипПредмета) Тогда
		СтандартнаяОбработка = Ложь;
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Лид", Истина);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("ТекстЗаполнения",    Элемент.ТекстРедактирования);
		ПараметрыФормы.Вставить("РежимВыбора",        Истина);
		
		ОткрытьФорму("Справочник.КонтактныеЛица.Форма.ФормаЛида", ПараметрыФормы, Элемент);
	Иначе
		РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	Если Не ТипПредметаКонтакт(ТипПредмета) Тогда
		РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Не ТипПредметаКонтакт(ТипПредмета) Тогда
		РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
			Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если Не ТипПредметаКонтакт(ТипПредмета) Тогда
		РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
			Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВремениОповещенияПриИзменении(Элемент)
	
	ОповещатьОСобытии = 
		ИнтервалВремениОповещения <> ИмяИнтервалаОповещенияНеНужно();
	
	УстановитьВидимостьДатыОповещения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить(Команда)
	
	ЗаписатьНаСервере();
	ПараметрыОповещения = Новый Структура;
	Оповестить("Запись_ЗадачаИсполнителя", ПараметрыОповещения, Объект.Ссылка);
	ОповеститьОбИзменении(Объект.Ссылка);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗадачу(Команда)
	
	ЗаписатьНаСервере();
	ПараметрыОповещения = Новый Структура;
	Оповестить("Запись_ЗадачаИсполнителя", ПараметрыОповещения, Объект.Ссылка);
	ОповеститьОбИзменении(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗвонок(Команда)

	СоздатьНовоеВзаимодействиеСПроверкой("ТелефонныйЗвонок");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПисьмо(Команда)

	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЭлектронноеПисьмоПродолжение", ЭтотОбъект, );
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВстречу(Команда)
	
	СоздатьНовоеВзаимодействиеСПроверкой("Встреча");
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПрочееВзаимодействие(Команда)
	
	СоздатьНовоеВзаимодействиеСПроверкой("ПрочееВзаимодействие");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНаСервере()
	
	ЗаполнитьСозданиеЗадачиВИсториюВыполнения();
	ЗаполнитьНаправлениеЗадачиВИсториюВыполнения();
	ЗаполнитьСтатусЗадачиВИсториюВыполнения();
	
	Записать();
	Прочитать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаправлениеЗадачиВИсториюВыполнения()
	
	ЗадачаДоИзменений = ЗадачаДоИзменений();
	ИсполнительДоИзменений = ЗадачаДоИзменений.Исполнитель;
	
	Если ИсполнительДоИзменений = Объект.Исполнитель Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РезультатВыполнения = МоиЗадачиСерверБП.ПредставлениеРезультатаНаправленияЗадачи(
		Пользователи.АвторизованныйПользователь(),
		Объект.Исполнитель,
		Объект.РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Функция ЗадачаДоИзменений()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗадачаИсполнителя.Ссылка КАК Ссылка,
	|	ЗадачаИсполнителя.Исполнитель КАК Исполнитель,
	|	ЗадачаИсполнителя.СтатусЗадачи КАК СтатусЗадачи
	|ИЗ
	|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
	|ГДЕ
	|	ЗадачаИсполнителя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗадачаДоИзменений = Выборка;
	Иначе
		ЗадачаДоИзменений = Объект;
	КонецЕсли;
	
	Возврат ЗадачаДоИзменений;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСозданиеЗадачиВИсториюВыполнения()
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РезультатВыполнения = МоиЗадачиСерверБП.ПредставлениеРезультатаСозданияЗадачи(
		Пользователи.АвторизованныйПользователь(),
		Объект.РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусЗадачиВИсториюВыполнения()
	
	ЗадачаДоИзменений = ЗадачаДоИзменений();
	СтатусЗадачиДоИзменений = ЗадачаДоИзменений.СтатусЗадачи;
	
	Если СтатусЗадачиДоИзменений = Объект.СтатусЗадачи Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РезультатВыполнения = МоиЗадачиСерверБП.ПредставлениеРезультатаИзмененияЗадачи(
		Пользователи.АвторизованныйПользователь(),
		Объект.СтатусЗадачи,
		Объект.РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораЭлементаВремяНаСервере()
	
	Элементы.Время.СписокВыбора.Очистить();
	
	НачалоРабочегоДня = НачалоДня(Объект.СрокИсполнения) + 8 * 60 * 60; // 8 часов дня
	КонецРабочегоДня  = НачалоДня(Объект.СрокИсполнения) + 19 * 60 * 60; // 19 часов дня
	
	ВремяДляВывода = НачалоРабочегоДня;
	Пока ВремяДляВывода <= КонецРабочегоДня Цикл
		Элементы.Время.СписокВыбора.Добавить(ВремяДляВывода, Формат(ВремяДляВывода, "ДФ=HH:mm"));
		ВРемяДляВывода = ВремяДляВывода + 30 * 60;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура МнеНаКонтролеПриИзмененииНаСервере()
	
	Если МнеНаКонтроле = ЗначениеМне() Тогда
		Исполнитель = Справочники.Пользователи.ПустаяСсылка();
		Объект.Исполнитель = ТекущийПользователь;
	Иначе
		Объект.Исполнитель = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСрокИсполнения(ДоПолудня = Ложь);
	
	Если УстановитьВремя Тогда
		Время = ?(ДоПолудня = Истина, Дата("01.01.0001 12:00:00"), Время);
		Объект.СрокИсполнения = НачалоДня(Объект.СрокИсполнения) + Час(Время)*60*60 + Минута(Время)*60;
	Иначе
		Время = Дата("01.01.0001 00:00:00");
		Объект.СрокИсполнения = КонецДня(Объект.СрокИсполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОграничитьТипПредмета(Форма)
	
	Элементы = Форма.Элементы;
	
	ПараметрыВыбораЛида = Новый Массив;
	Если ТипПредметаКонтакт(Форма.ТипПредмета) Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Лид", Истина);
		ПараметрыВыбораЛида.Добавить(НовыйПараметр);
	КонецЕсли;
	
	Элементы.Предмет.ОграничениеТипа = Форма.ТипПредмета;
	Элементы.Предмет.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЛида);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТипПредметаКонтакт(ОписаниеТипа)
	
	Возврат ОписаниеТипа.СодержитТип(Тип("СправочникСсылка.КонтактныеЛица"));
	
КонецФункции

&НаСервере
Процедура ЗаполнитьИсполнителяПоПредмету()
	
	МетаданныеПредмета = Объект.Предмет.Метаданные();
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Ответственный", МетаданныеПредмета) Тогда
		ОтветственныйПоПредмету = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Предмет, "Ответственный");
		Если ЗначениеЗаполнено(ОтветственныйПоПредмету) И Объект.Исполнитель <> ОтветственныйПоПредмету Тогда
			Объект.Исполнитель = ОтветственныйПоПредмету;
			Исполнитель = ?(Объект.Исполнитель = ТекущийПользователь, Справочники.Пользователи.ПустаяСсылка(), Объект.Исполнитель);
			МнеНаКонтроле = ?(Объект.Исполнитель = ТекущийПользователь, ЗначениеМне(), ЗначениеИсполнителю());
			
			НастроитьОповещенияПриИзмененииИсполнителя(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиОповещения()
	
	ОповещенияПользователейДоступно = НапоминанияПользователя.ИспользуютсяНапоминанияПользователя();
	
	Если Не ОповещенияПользователейДоступно Тогда
		Элементы.ГруппаОповещения.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекущийСтатус = Объект.СтатусЗадачи;
	
	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ДатаОповещенияОСобытии = Объект.СрокИсполнения;
	
	ЗаполнитьСписокИнтерваловВремениОповещения(ЭтотОбъект, Истина);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПрочитатьНастройкиОповещений();
	КонецЕсли;
	
	УстановитьВидимостьДатыОповещения(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиОповещений()
	
	Если ТекущийПользователь = Объект.Исполнитель Тогда
		ИдентификаторОповещения = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияИсполнителяЗадачи();
	ИначеЕсли ТекущийПользователь = Объект.Автор Тогда
		ИдентификаторОповещения = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияАвтораЗадачи();
	Иначе
		ИдентификаторОповещения = "";
	КонецЕсли;
	
	НапоминанияПоПредмету = НапоминанияПользователя.НайтиНапоминания(Объект.Ссылка, ИдентификаторОповещения);
	
	Для Каждого Напоминание Из НапоминанияПоПредмету Цикл
		ОповещатьОСобытии = Истина;
		
		Если Напоминание.СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ВУказанноеВремя Тогда
			ИнтервалВремениОповещения = ИмяИнтервалаОповещенияВУказаноеВремя();
			ДатаОповещенияОСобытии = Напоминание.ВремяСобытия;
		ИначеЕсли Напоминание.СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета Тогда
			Если Напоминание.ИнтервалВремениНапоминания = ИнтервалВремениДесятьМинут() Тогда
				ИнтервалВремениОповещения = ИмяИнтервалаОповещенияЗаДесятьМинут();
			ИначеЕсли Напоминание.ИнтервалВремениНапоминания = ИнтервалВремениОдинЧас() Тогда
				ИнтервалВремениОповещения = ИмяИнтервалаОповещенияЗаЧас();
			КонецЕсли;
		КонецЕсли;
		
		Прервать;
	КонецЦикла;
	
	Если ТекущийПользователь = Объект.Автор Тогда
		ОповещатьПриИзмененииСтатуса =
			РегистрыСведений.НастройкиОповещенийПоЗадачам.ОповещениеПриИзмененииСтатусаЗадачиВключено(Объект.Ссылка);
		Если ОповещатьПриИзмененииСтатуса Тогда
			ИнтервалВремениОповещения = ИмяИнтервалаОповещенияПриИзмененииСтатуса();
			ОповещатьОСобытии = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОповещенияПриЗаписиЗадачи(Задача)
	
	Если Не ОповещенияПользователейДоступно Тогда
		Возврат;
	КонецЕсли;
	
	Если ОповещатьОСобытии Тогда
		ПереустановитьОповещенияПоЗадаче(Задача);
	Иначе
		Если ТекущийПользователь = Объект.Автор Тогда
			ОповещенияПользователейОСобытиях.УстановитьНастройкиОповещенияПриИзмененииСтатусаЗадачи(Задача, Ложь);
		КонецЕсли;
		ОповещенияПользователейОСобытиях.УдалитьПодключенныеОповещенияПоЗадаче(Задача);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		И Объект.Автор <> Объект.Исполнитель Тогда
		ОповещенияПользователейОСобытиях.ОтправитьОповещениеОНовойЗадаче(Задача);
	КонецЕсли;
	
	ПроверитьИОтправитьОповещенияПриИзмененииСтатусаЗадачи(Задача);
	
КонецПроцедуры

&НаСервере
Процедура ПереустановитьОповещенияПоЗадаче(Задача)
	
	Если ТекущийПользователь = Объект.Исполнитель Тогда
		ИдентификаторОповещения = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияИсполнителяЗадачи();
	ИначеЕсли ТекущийПользователь = Объект.Автор Тогда
		ИдентификаторОповещения = ОповещенияПользователейОСобытияхКлиентСервер.ИдентификаторОповещенияАвтораЗадачи();
	Иначе
		ИдентификаторОповещения = "";
	КонецЕсли;
	
	ОповещенияПользователейОСобытиях.УдалитьПодключенныеОповещенияПоЗадаче(Задача, ИдентификаторОповещения);
	
	ИмяИнтервалаИзменениеСтатуса = ИмяИнтервалаОповещенияПриИзмененииСтатуса();
	
	Если ТекущийПользователь = Объект.Автор Тогда
		ОповещатьПриИзмененииСтатуса = ИнтервалВремениОповещения = ИмяИнтервалаИзменениеСтатуса;
		
		ОповещенияПользователейОСобытиях.УстановитьНастройкиОповещенияПриИзмененииСтатусаЗадачи(
			Задача, ОповещатьПриИзмененииСтатуса);
	КонецЕсли;
	
	Если ИнтервалВремениОповещения <> ИмяИнтервалаИзменениеСтатуса Тогда
		
		ОписаниеНапоминания = НапоминанияПользователяКлиентСервер.ОписаниеНапоминания(, Истина);
		
		Если ИнтервалВремениОповещения = ИмяИнтервалаОповещенияВУказаноеВремя() Тогда
			ОписаниеНапоминания.ВремяСобытия = ДатаОповещенияОСобытии;
			ОписаниеНапоминания.СрокНапоминания = ДатаОповещенияОСобытии;
			ОписаниеНапоминания.СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ВУказанноеВремя;
		ИначеЕсли ИнтервалВремениОповещения = ИмяИнтервалаОповещенияЗаДесятьМинут() Тогда
			ОписаниеНапоминания.ИмяРеквизитаИсточника = "СрокИсполнения";
			ОписаниеНапоминания.СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета;
			ОписаниеНапоминания.ИнтервалВремениНапоминания = ИнтервалВремениДесятьМинут();
			ОписаниеНапоминания.ВремяСобытия = Объект.СрокИсполнения;
			ОписаниеНапоминания.СрокНапоминания = ОписаниеНапоминания.ВремяСобытия - ОписаниеНапоминания.ИнтервалВремениНапоминания;
		ИначеЕсли ИнтервалВремениОповещения = ИмяИнтервалаОповещенияЗаЧас() Тогда
			ОписаниеНапоминания.ИмяРеквизитаИсточника = "СрокИсполнения";
			ОписаниеНапоминания.СпособУстановкиВремениНапоминания = Перечисления.СпособыУстановкиВремениНапоминания.ОтносительноВремениПредмета;
			ОписаниеНапоминания.ИнтервалВремениНапоминания = ИнтервалВремениОдинЧас();
			ОписаниеНапоминания.ВремяСобытия = Объект.СрокИсполнения;
			ОписаниеНапоминания.СрокНапоминания = ОписаниеНапоминания.ВремяСобытия - ОписаниеНапоминания.ИнтервалВремениНапоминания;
		КонецЕсли;
		
		ОписаниеНапоминания.Пользователь = ТекущийПользователь;
		ОписаниеНапоминания.Источник = Задача;
		ОписаниеНапоминания.Описание = СтрШаблон(НСтр("ru = 'Напоминание по задаче: %1'"), Объект.Наименование);
		ОписаниеНапоминания.Идентификатор = ИдентификаторОповещения;
		ОписаниеНапоминания.Вставить("НавигационнаяСсылка", ПолучитьНавигационнуюСсылку(Задача));
		
		ОповещенияПользователейОСобытиях.ПодключитьОповещениеПоЗадаче(ОписаниеНапоминания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьИОтправитьОповещенияПриИзмененииСтатусаЗадачи(Задача)
	
	Если ТекущийПользователь = Объект.Автор Или ТекущийСтатус = Объект.СтатусЗадачи Тогда
		Возврат;
	КонецЕсли;
	
	ОповещатьПриИзмененииСтатуса =
		РегистрыСведений.НастройкиОповещенийПоЗадачам.ОповещениеПриИзмененииСтатусаЗадачиВключено(Задача);
	
	Если ОповещатьПриИзмененииСтатуса Тогда
		ОповещенияПользователейОСобытиях.ОтправитьОповещениеОбИзмененииСтатусаЗадачи(
			Задача, Объект.Наименование, Объект.Автор, Объект.СтатусЗадачи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьОповещенияПриИзмененииИсполнителя(Форма)
	
	Если Не Форма.ОповещенияПользователейДоступно Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокИнтерваловВремениОповещения(Форма);
	
	УстановитьВидимостьДатыОповещения(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокИнтерваловВремениОповещения(Форма, УстанавливатьЗначениеПоУмолчанию = Ложь)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	СписокВыбораИнтервала = Элементы.ИнтервалВремениОповещения.СписокВыбора;
	
	СписокВыбораИнтервала.Очистить();
	
	СписокВыбораИнтервала.Добавить(
		ИмяИнтервалаОповещенияНеНужно(),
		НСтр("ru = 'не нужно'"));
	
	Если Форма.ТекущийПользователь = Объект.Исполнитель Тогда
		СписокВыбораИнтервала.Добавить(
			ИмяИнтервалаОповещенияЗаДесятьМинут(),
			НСтр("ru = 'за 10 минут'"));
		
		СписокВыбораИнтервала.Добавить(
			ИмяИнтервалаОповещенияЗаЧас(),
			НСтр("ru = 'за 1 час'"));
	ИначеЕсли Форма.ТекущийПользователь = Объект.Автор Тогда
		СписокВыбораИнтервала.Добавить(
			ИмяИнтервалаОповещенияПриИзмененииСтатуса(),
			НСтр("ru = 'при изменении статуса'"));
	КонецЕсли;
	
	СписокВыбораИнтервала.Добавить(
		ИмяИнтервалаОповещенияВУказаноеВремя(),
		НСтр("ru = 'в указанное время'"));
	
	Если УстанавливатьЗначениеПоУмолчанию Тогда
		Форма.ИнтервалВремениОповещения = ИмяИнтервалаОповещенияНеНужно();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДатыОповещения(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ДатаОповещенияОСобытии.Видимость =
		Форма.ИнтервалВремениОповещения = ИмяИнтервалаОповещенияВУказаноеВремя();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяИнтервалаОповещенияНеНужно()
	
	Возврат "НеНужно";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяИнтервалаОповещенияПриИзмененииСтатуса()
	
	Возврат "ПриИзмененииСтатуса";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяИнтервалаОповещенияЗаДесятьМинут()
	
	Возврат "ЗаДесятьМинут";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяИнтервалаОповещенияЗаЧас()
	
	Возврат "ЗаЧас";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяИнтервалаОповещенияВУказаноеВремя()
	
	Возврат "ВУказанноеВремя";
	
КонецФункции

&НаСервереБезКонтекста
Функция ИнтервалВремениДесятьМинут()
	
	Возврат 600;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИнтервалВремениОдинЧас()
	
	Возврат 3600;
	
КонецФункции

&НаКлиенте
Процедура СоздатьЭлектронноеПисьмоПродолжение(ПроверкаВыполнена, ДополнительныеПараметры) Экспорт
	
	Если ПроверкаВыполнена = Истина Тогда
		СоздатьНовоеВзаимодействиеСПроверкой("ЭлектронноеПисьмоИсходящее");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеВзаимодействиеСПроверкой(ТипОбъекта)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда
		
		ДополнительныеПараметры = Новый Структура("ТипОбъекта", ТипОбъекта);
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ПроверкаЗадачиЗавершение",
			ЭтотОбъект, ДополнительныеПараметры);
		ТекстВопроса = НСтр("ru = 'Задача не записана. Записать?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		СоздатьНовоеВзаимодействие(ТипОбъекта);
	КонецЕсли;

КонецПроцедуры	

&НаКлиенте
Процедура СоздатьНовоеВзаимодействие(ТипОбъекта)

	Контакт = Объект.Предмет;
	
	ДанныеУчастника = Новый Структура;
	
	ДанныеУчастника.Вставить("Контакт", Контакт);	
	ДанныеУчастника.Вставить("КакСвязаться", "");
	ДанныеУчастника.Вставить("Представление", Строка(Контакт));

	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ДанныеУчастника", ДанныеУчастника);
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ПредметВзаимодействия", Объект.Ссылка);
	ЗначенияЗаполнения.Вставить("Тема", Объект.Наименование);
	ПараметрыСоздания.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ВзаимодействияКлиент.СоздатьНовоеВзаимодействие(ТипОбъекта, ПараметрыСоздания, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаЗадачиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьНаСервере();
	
	Если Не Объект.Ссылка.Пустая() Тогда	
		СоздатьНовоеВзаимодействие(ДополнительныеПараметры.ТипОбъекта);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеМне()
	
	Возврат 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеИсполнителю()
	
	Возврат 1;
	
КонецФункции

#КонецОбласти
