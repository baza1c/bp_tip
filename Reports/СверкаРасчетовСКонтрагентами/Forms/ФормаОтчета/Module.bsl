
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СервисСверкиРасчетовПереопределяемый.ОтчетПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	СервисСверкиРасчетовПереопределяемый.ЗаполнитьСписокОрганизаций(Элементы.ПолеОрганизация, СоответствиеОрганизаций);
	
	РежимРасшифровки          = Параметры.РежимРасшифровки;
	ОтложенноеФормирование    = Параметры.ОтложенноеФормирование;
	ИдентификаторыРасхождений = Параметры.ИдентификаторыРасхождений;
	
	ОбновитьДатуПолученияДанных(Ложь);
	
	Если РежимРасшифровки Тогда
		СтандартнаяОбработка = Ложь;
		Отчет.Организация   = Параметры.Организация;
		Отчет.Контрагент    = Параметры.Контрагент;
		Отчет.НачалоПериода = Параметры.НачалоПериода;
		Отчет.КонецПериода  = Параметры.КонецПериода;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ПриОткрытии(ЭтотОбъект, Отказ);
	
	Если ОтложенноеФормирование Тогда
		Возврат;
	КонецЕсли;
		
	Если РежимРасшифровки Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		ПодключитьОбработчикОжидания("ЗапуститьФормированиеОтчета", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбсужденияПодключены" Тогда
		ОповещенияСверкиВключены = СервисСверкиРасчетовВызовСервера.ВключитьОповещениеСверки();
		Если ОповещенияСверкиВключены Тогда 
			ПоказатьБаннерВключитьОповещения();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ПолучениеНовыхРасхожденийПоОрганизации" Тогда
		Если ИдетПолучениеРасхождений
			И Параметр.Организация = Отчет.Организация
			И (Параметр.КонецПериода >= Отчет.НачалоПериода И Параметр.НачалоПериода <= Отчет.КонецПериода)
			И ЭтотОбъект.ДатаПолученияДанныхСервисаСверкиРасчетов < Параметр.ВремяПолученияРасхождений Тогда
			ПослеЗавершенияОбмена();
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	ЗапуститьФормированиеОтчета();
	
	Элементы.Результат.АктивизироватьПоУмолчанию = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкиИСформироватьОтчет(Команда)
	
	СформироватьОтчет(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", Отчет.НачалоПериода, Отчет.КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	СервисСверкиРасчетовКлиентПереопределяемый.ОткрытьФормуВыбораПериода(ПараметрыВыбора, Элементы.ВыбратьПериод, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	ОткрытьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ОтчетСохранитьКак(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	СкрытьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьОповещения(Команда)

	Если НЕ СервисСверкиРасчетовВызовСервера.ОбсужденияПодключены() Тогда
		МодульОбсужденияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбсужденияКлиент");
		МодульОбсужденияКлиент.ПоказатьПодключение();
	Иначе 
		ОповещенияСверкиВключены = СервисСверкиРасчетовВызовСервера.ВключитьОповещениеСверки();
		Если ОповещенияСверкиВключены Тогда
			ПоказатьБаннерВключитьОповещения();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НеВключатьОповещения(Команда)
	
	СкрытьБаннерВключитьОповещения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменятьсяССервисом(Команда)
	
	ИдетПолучениеРасхождений = Истина;
	УправлениеФормой(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("ОбновитьДанныеОтчета", 1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ПолеОрганизацияПриИзменении(Элемент, ПолеОрганизация,
		Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	СервисСверкиРасчетовКлиентПереопределяемый.ОрганизацияПриИзменении(ЭтотОбъект, Элемент);
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		ОтменитьВыполнениеЗаданияОбмена();
	КонецЕсли;
	
	ИдетПолучениеРасхождений = Ложь;
	ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	ОбновитьДатуПолученияДанных(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, 
		СоответствиеОрганизаций, Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		ОтменитьВыполнениеЗаданияОбмена();
	КонецЕсли;
	
	ИдетПолучениеРасхождений = Ложь;
	ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	ОбновитьДатуПолученияДанных(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		ОтменитьВыполнениеЗаданияОбмена();
	КонецЕсли;
	
	ИдетПолучениеРасхождений = Ложь;
	ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	ОбновитьДатуПолученияДанных(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьОтборНовыеРасхожденияНажатие(Элемент)
	
	ИдентификаторыРасхождений.Очистить();
	Элементы.ГруппаОтборНовыеРасхождения.Видимость = Ложь;
	ЗапуститьФормированиеОтчета();
	
	Элементы.Результат.АктивизироватьПоУмолчанию = Ложь;
	
КонецПроцедуры

#Область НастройкиОтчета

&НаКлиенте
Процедура ГруппировкаСнятьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Отчет.Группировка Цикл
		СтрокаТаблицы.Использование = Ложь;
	КонецЦикла;
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаУстановитьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Отчет.Группировка Цикл
		СтрокаТаблицы.Использование = Истина;
	КонецЦикла;
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаПриИзменении(Элемент)

	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ГруппировкаПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);  
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаПередНачаломИзменения(Элемент, Отказ)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ГруппировкаПередНачаломИзменения(ЭтаФорма, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПриИзменении(Элемент)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ОтборыПриИзменении(ЭтотОбъект, Элемент, Ложь);
	Элементы.ГруппаБаннер.Видимость = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ОтборыПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломИзменения(Элемент, Отказ)

	СервисСверкиРасчетовКлиентПереопределяемый.ОтборыПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура РазмещениеДополнительныхПолейПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПоляПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПоляПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ДополнительныеПоляПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПоляПередНачаломИзменения(Элемент, Отказ)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ДополнительныеПоляПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьЗаголовокПриИзменении(Элемент)

	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийПоляРезультата

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Если ТипЗнч(Расшифровка) = Тип("Строка") Тогда
		СтандартнаяОбработка = Ложь;
		Если СтрНайти(Расшифровка, "http") = 1 
			ИЛИ СтрНайти(Расшифровка, "КонтрагентыБизнесСеть") Тогда
			ПерейтиПоНавигационнойСсылке(Расшифровка);
		КонецЕсли;
	Иначе
		СервисСверкиРасчетовКлиентПереопределяемый.ОбработкаРасшифровкиСтандартногоОтчета(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	// Не будем обрабатывать нажатие на правую кнопку мыши.
	// Покажем стандартное контекстное меню ячейки табличного документа.
	Расшифровка = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.ИдетПолучениеРасхождений Тогда 
		Элементы.Обновить.Доступность = Ложь;
		Элементы.Обновить.Заголовок   = НСтр("ru='Получение новых данных из 1С:Сверка 2.0'");
		Элементы.Обновить.Картинка    = БиблиотекаКартинок.СинхронизацияДанныхДлительнаяОперация;
	Иначе
		Элементы.Обновить.Доступность = Истина;
		Элементы.Обновить.Заголовок   = НСтр("ru='Получить новые данные и сформировать'");
		Элементы.Обновить.Картинка    = БиблиотекаКартинок.СинхронизацияДанных;
	КонецЕсли;
	
	Если Элементы.Результат.Редактирование Тогда
		Элементы.ГруппаБаннер.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеОтчета()
	
	// Проверим не получены ли более свежие данные в другом сеансе.
	// Если получены, то не генерируем новый обмен.
	ДатаАктуальностиДО = ДатаПолученияДанныхСервисаСверкиРасчетов;
	ОбновитьДатуПолученияДанных(Ложь);
	Если ДатаАктуальностиДО < ДатаПолученияДанныхСервисаСверкиРасчетов Тогда
		ПослеЗавершенияОбмена();
		Возврат;
	КонецЕсли;
	
	ОбменССервисом = ОбменятьсяДаннымиССервисом();
	Если ОбменССервисом.Свойство("ОтказПроверкиЗаполнения")
		И ОбменССервисом.ОтказПроверкиЗаполнения Тогда
		// Необходимо заполнить обязательные для обмена поля.
		ИдетПолучениеРасхождений = Ложь;
		УправлениеФормой(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗаданияОбмена = ОбменССервисом.ИдентификаторЗадания;
	Если ОбменССервисом.Статус = "Выполнено" Тогда
		ПослеЗавершенияОбмена(ОбменССервисом);
		Возврат
	КонецЕсли;
	
	ОповещениеОЗавершении   = Новый ОписаниеОповещения("ПослеЗавершенияОбмена", ЭтотОбъект);
	ПараметрыОжидания       = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОбменССервисом, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОбменятьсяДаннымиССервисом()
	
	ЭтоФормированиеОтчета = Ложь;
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ОтказПроверкиЗаполнения", Истина);
	КонецЕсли;
	
	НаименованиеЗадания = СтрШаблон("%1. Обмен с сервисом", СервисСверкиРасчетов.ИмяСервиса());
	ИмяМетода           = "СервисСверкиРасчетов.ОбменятьсяССервисом";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыПроцедуры = Новый Структура("ЭтоВызовИзОтчета, Организация, НачальнаяДатаПолученияРасхождений, КонечнаяДатаПолученияРасхождений",
		Истина, Отчет.Организация, Отчет.НачалоПериода, Отчет.КонецПериода);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода, ПараметрыПроцедуры);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияОбмена(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	
	Если Не ИдетПолучениеРасхождений Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		Если Результат.Статус <> "Выполнено" Тогда
			Возврат;
		Иначе
			Если ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
				ИдентификаторЗаданияОбмена = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьДатуПолученияДанных();
	
	Если ИдетПолучениеРасхождений Тогда
		ПодключитьОбработчикОжидания("ПроверитьЗавершенияОбмена", 60, Истина);
		Возврат;
	КонецЕсли;
	
	Элементы.Результат.ОтображениеСостояния.Видимость                      = Истина;
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	Элементы.Результат.ОтображениеСостояния.Картинка                       = БиблиотекаКартинок.ДлительнаяОперация48;
	Элементы.Результат.ОтображениеСостояния.Текст                          = НСтр("ru = 'Получены новые данные из сервиса 1С:Сверка 2.0. Формируется актуальный отчет...'");
	
	ЭтоФормированиеПослеПолученияРасхождений = Истина;
	ПодключитьОбработчикОжидания("ЗапуститьФормированиеОтчета", 3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗавершенияОбмена() Экспорт
	
	Если ИдетПолучениеРасхождений Тогда
		ПослеЗавершенияОбмена();
	Иначе
		ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакПолученияРасхождений()
	
	ИдетПолучениеРасхождений = Ложь;
	
	Если Не ЗначениеЗаполнено(Отчет.Организация)
		ИЛИ Не ЗначениеЗаполнено(Отчет.НачалоПериода)
		ИЛИ Не ЗначениеЗаполнено(Отчет.КонецПериода) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим не получаются ли расхождения в других сеансах.
	ЗаблокированныеМесяца =
		РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов.ЗаблокированныеМесяцаДляПолученияРасхождений(
		Отчет.Организация, Отчет.НачалоПериода, Отчет.КонецПериода);
	
	ИдетПолучениеРасхождений = ЗаблокированныеМесяца.Количество() > 0;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДатуПолученияДанных(ОбновлятьПризнакПолученияРасхождений = Истина)
	
	ДатаПолученияДанныхСервисаСверкиРасчетов = 
		РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов.ДатаАктуальностиРасхождений(
		Отчет.Организация, Отчет.НачалоПериода, Отчет.КонецПериода);
		
	Если ОбновлятьПризнакПолученияРасхождений Тогда
		УстановитьПризнакПолученияРасхождений();
	КонецЕсли;
	
	Если Не ИдетПолучениеРасхождений Тогда // не меняем дату актуальности на форме в процессе обмена
		Если ЗначениеЗаполнено(ДатаПолученияДанныхСервисаСверкиРасчетов) Тогда
			ЧастиСообщения = Новый Массив;
			ЧастиСообщения.Добавить(НСтр("ru='Данные отчета актуальны на '"));
			ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Строка(ДатаПолученияДанныхСервисаСверкиРасчетов), Новый Шрифт(Новый Шрифт,,,Истина)));
			ИтоговоеСообщение = Новый ФорматированнаяСтрока(ЧастиСообщения);
			Элементы.НадписьДатаАктуальности.Заголовок = ИтоговоеСообщение;
		Иначе
			Элементы.НадписьДатаАктуальности.Заголовок = НСтр("ru='Данные для отчета не получались'");
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения)

	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполнения, "Статус", "Отменено") = "Отменено" Тогда
		
		ЭтотОбъект.Результат = Новый ТабличныйДокумент;
		ЭтотОбъект.ДанныеРасшифровки = Неопределено;
		// ОписаниеЗаданияФормированияОтчета не очищаем на случай, если отмена произошла перед началом нового формирования отчета,
		// и описание уже относится к новому запуску.
		Возврат;
		
	ИначеЕсли РезультатВыполнения.Статус = "Ошибка" Тогда
		// Это результат аварийного завершения фонового задания.
		ОбщегоНазначения.СообщитьПользователю(РезультатВыполнения.КраткоеПредставлениеОшибки);
		
		ЭтотОбъект.Результат = Новый ТабличныйДокумент;
		ЭтотОбъект.ДанныеРасшифровки = Неопределено;
		
		ПозицияТочки = СтрНайти(ЭтотОбъект.ИмяФормы, ".", ,, 2);
		ПолноеИмяМетаданного = ?(ПозицияТочки = 0, ЭтотОбъект.ИмяФормы, Лев(ЭтотОбъект.ИмяФормы, ПозицияТочки - 1));
		ОтчетМетаданные = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданного);
		ОтчетОрганизация = ?(ЭтотОбъект.Отчет.Свойство("Организация"), ЭтотОбъект.Отчет.Организация, Неопределено);
		
		ЗаписьЖурналаРегистрации("ОшибкаФормированияОтчета",
			УровеньЖурналаРегистрации.Ошибка,
			ОтчетМетаданные,
			ОтчетОрганизация,
			РезультатВыполнения.ПодробноеПредставлениеОшибки,
			РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
		
		Возврат;
		
	КонецЕсли;
	
	ЭтотОбъект.ОписаниеЗаданияФормированияОтчета = Неопределено;
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ЭтотОбъект.Элементы.Результат, "НеИспользовать");
	
	ДанныеОтчета = ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	УдалитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	Если ДанныеОтчета = Неопределено Или Не ДанныеОтчета.Свойство("Результат") Тогда
		// Некорректный результат формирования отчета.
		ЭтотОбъект.Результат = Новый ТабличныйДокумент;
		ЭтотОбъект.ДанныеРасшифровки = Неопределено;
		
		ПозицияТочки = СтрНайти(ЭтотОбъект.ИмяФормы, ".", ,, 2);
		ПолноеИмяМетаданного = ?(ПозицияТочки = 0, ЭтотОбъект.ИмяФормы, Лев(ЭтотОбъект.ИмяФормы, ПозицияТочки - 1));
		ОтчетМетаданные = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданного);
		ОтчетОрганизация = ?(ЭтотОбъект.Отчет.Свойство("Организация"), ЭтотОбъект.Отчет.Организация, Неопределено);
		
		ЗаписьЖурналаРегистрации("ОшибкаФормированияОтчета",
			УровеньЖурналаРегистрации.Ошибка,
			ОтчетМетаданные,
			ОтчетОрганизация,
			НСтр("ru = 'Некорректный результат формирования отчета: отсутствует Результат'"),
			РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
			
		Возврат;
		
	КонецЕсли;
		
	ЭтотОбъект.Результат = ДанныеОтчета.Результат;
	ДанныеОтчета.Результат = Неопределено;
	ЭтотОбъект.ДанныеРасшифровки = ДанныеОтчета.ДанныеРасшифровки;
	ДанныеОтчета.ДанныеРасшифровки = Неопределено;
	
	ПоказатьБаннер();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьБаннерВключитьОповещения()

	НеПоказыватьПредложениеПодключитьОповещения = ХранилищеОбщихНастроек.Загрузить(ВРег("СервисСверкиРасчетовНеПоказыватьПредложениеПодключитьОповещения"));
	ПолныеПраваИлиПривилегированныйРежим = Пользователи.ЭтоПолноправныйПользователь();
	Если ЗначениеЗаполнено(НеПоказыватьПредложениеПодключитьОповещения) Тогда
		Элементы.ГруппаВключениеОповещений.Видимость = ПолныеПраваИлиПривилегированныйРежим
		И Не СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены()
		И Не НеПоказыватьПредложениеПодключитьОповещения;
	Иначе
		Элементы.ГруппаВключениеОповещений.Видимость = ПолныеПраваИлиПривилегированныйРежим
			И Не СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьБаннерВключитьОповещения()
	
	ХранилищеОбщихНастроек.Сохранить(ВРег("СервисСверкиРасчетовНеПоказыватьПредложениеПодключитьОповещения"),, Истина);
	Элементы.ГруппаВключениеОповещений.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьБаннер()
	
	Если ИдентификаторыРасхождений.Количество() > 0 Тогда
		// Если показываем только новые расхождения, то вместо баннера с итогами расхождений
		// показываем плашку отбора по новым расхождениям. 
		Элементы.ГруппаБаннер.Видимость                = Ложь;
		Элементы.ГруппаОтборНовыеРасхождения.Видимость = Истина;
		ОчиститьОбластьСИтогами();
		Возврат;
	Иначе
		Элементы.ГруппаОтборНовыеРасхождения.Видимость = Ложь;
	КонецЕсли;
	
	ИтогиСверкиПоТаблице = Отчеты.СверкаРасчетовСКонтрагентами.ИтогиСверкиПоТаблице(Результат);
	
	Если ИтогиСверкиПоТаблице.НетИтогов Тогда
		Элементы.ГруппаБаннер.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ПоказатьБаннерВключитьОповещения();
	
	Элементы.ГруппаБаннер.Видимость = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() = 0;
	
	КоличествоДокументов   = ИтогиСверкиПоТаблице.КоличествоДокументов;
	НеОтраженоОрганизацией = ИтогиСверкиПоТаблице.НеОтраженоОрганизацией;
	НеОтраженоКонтрагентом = ИтогиСверкиПоТаблице.НеОтраженоКонтрагентом;
	РазличаютсяРеквизиты   = ИтогиСверкиПоТаблице.РазличаютсяРеквизиты;
	БезРасхождений         = ИтогиСверкиПоТаблице.БезРасхождений;
	НетИтогов              = ИтогиСверкиПоТаблице.НетИтогов;
	
	ЗапятаяПослеНеОтраженоОрганизацией   = "";
	ЗапятаяПослеНеОтраженоКонтрагентом   = "";
	ТочкаПослеСРазличающимисяРеквизитами = ""; 
	
	НеОтраженоОрганизациейНиз = 0;
	НеОтраженоКонтрагентомНиз = 0;
	
	Если НЕ НетИтогов Тогда
		
		ОчиститьОбластьСИтогами();
		
	КонецЕсли;
	
	МассивСтрок = Новый Массив;
	
	Если КоличествоДокументов = 0 Тогда
		
		Элементы.ГруппаБаннер.Видимость = Ложь;
		
	Иначе
		
		Если НеОтраженоКонтрагентом > 0
			ИЛИ РазличаютсяРеквизиты > 0 Тогда
				
			ЗапятаяПослеНеОтраженоОрганизацией = ", ";
				
		КонецЕсли;
			
		Если РазличаютсяРеквизиты > 0 Тогда
				
			ЗапятаяПослеНеОтраженоКонтрагентом = ", ";
				
		КонецЕсли;
		
		Элементы.ГруппаБаннер.ЦветФона = Новый Цвет(215, 240, 199);
		
		Сверено = НСтр("ru='Сверено'");
		Если Прав(Строка(КоличествоДокументов), 1) = "1"
			И Прав(Строка(КоличествоДокументов), 2) <> "11" Тогда
			Сверено = Лев(Сверено,6);
		КонецЕсли;
		
		СвереноДокументов = ПолучитьСклоненияСтрокиПоЧислу("документ", КоличествоДокументов,,,"ПД=Винительный");
		ДокументовБезРасхождений = ПолучитьСклоненияСтрокиПоЧислу("документ", БезРасхождений,,,"ПД=Винительный");
		
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru='%1 %2'"),
			Сверено,
			СвереноДокументов[0])));
			
		// Для управления сворачиванием/разворачивание группирок отчета из баннера
		ОбластьНеОтраженыОрганизацией = Результат.НайтиТекст(Строка("Не отражено организацией"));
		ОбластьНеОтраженыКонтрагентом = Результат.НайтиТекст(Строка("Не отражено контрагентом"));
		ОбластьРазличаютсяРеквизиты = Результат.НайтиТекст(Строка("Различаются реквизиты"));
		
		Если ОбластьНеОтраженыОрганизацией <> Неопределено Тогда
			Если ОбластьНеОтраженыКонтрагентом <> Неопределено Тогда
				НеОтраженоОрганизациейНиз = ОбластьНеОтраженыКонтрагентом.Низ - 1;
			ИначеЕсли ОбластьРазличаютсяРеквизиты <> Неопределено Тогда
				НеОтраженоОрганизациейНиз = ОбластьРазличаютсяРеквизиты.Низ - 1;
			Иначе
				НеОтраженоОрганизациейНиз = Результат.ВысотаТаблицы;
			КонецЕсли;
		КонецЕсли;
		
		Если ОбластьНеОтраженыКонтрагентом <> Неопределено Тогда
			Если ОбластьРазличаютсяРеквизиты <> Неопределено Тогда
				НеОтраженоКонтрагентомНиз = ОбластьРазличаютсяРеквизиты.Низ - 1;
			Иначе
				НеОтраженоКонтрагентомНиз = Результат.ВысотаТаблицы;
			КонецЕсли;
		КонецЕсли;
	
		// Формулировка баннера
		Если ОбластьНеОтраженыОрганизацией <> Неопределено
			ИЛИ ОбластьНеОтраженыКонтрагентом <> Неопределено
			ИЛИ ОбластьРазличаютсяРеквизиты <> Неопределено Тогда
			
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=': '")));
			Элементы.ГруппаБаннер.ЦветФона = ЦветаСтиля.ЦветФонаОтрицательногоЗначения;
			
			Если ОбластьНеОтраженыОрганизацией <> Неопределено Тогда
				
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
					НСтр("ru='не отражено организацией'"),,,,
					"Не отражено организацией"));
					
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru=' (%1)%2'"),
					НеОтраженоОрганизацией,
					ЗапятаяПослеНеОтраженоОрганизацией)));
					
			КонецЕсли;
			
			Если ОбластьНеОтраженыКонтрагентом <> Неопределено Тогда
				
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
					НСтр("ru='не отражено контрагентом'"),,,,
					"Не отражено контрагентом"));
					
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru=' (%1)%2'"),
					НеОтраженоКонтрагентом,
					ЗапятаяПослеНеОтраженоКонтрагентом)));
					
			КонецЕсли;
			
			Если ОбластьРазличаютсяРеквизиты <> Неопределено Тогда
				
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
					НСтр("ru='с различающимися реквизитами'"),,,,
					"Различаются реквизиты"));
					
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru=' (%1)'"),
					РазличаютсяРеквизиты)));
					
			КонецЕсли;
				
			Если БезРасхождений > 0 Тогда
				
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru=' и еще %1 без расхождений'"),
					ДокументовБезРасхождений[0])));
				
			КонецЕсли;
			
		Иначе
			
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru=': расхождений не обнаружено'")));
			
		КонецЕсли;
		
	КонецЕсли;
	
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='.'")));
	
	Элементы.Баннер.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета()
	
	ОчиститьСообщения();
	Элементы.ГруппаБаннер.Видимость = Ложь;
	ОтказПроверкиЗаполнения = Ложь;
	
	ИдетПолучениеРасхождений = Ложь;
	ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	Если РезультатВыполнения.Свойство("ОтказПроверкиЗаполнения") Тогда
		ОтказПроверкиЗаполнения = РезультатВыполнения.ОтказПроверкиЗаполнения;
	КонецЕсли;
	
	Если НЕ ОтказПроверкиЗаполнения Тогда
		
		Если РезультатВыполнения.Статус = "Выполняется"
			И Не ЭтоФормированиеПослеПолученияРасхождений Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
		
		СкрытьНастройки();
		
	КонецЕсли;
	
	ЭтоФормированиеПослеПолученияРасхождений = Ложь;
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере() Экспорт
	
	ОбновитьДатуПолученияДанных(Ложь);
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализироватьКомпоновщикаНастроек();
	КонецЕсли;
	
	ЭтоФормированиеОтчета = Истина;
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено, ОтказПроверкиЗаполнения", Истина, Истина);
	КонецЕсли;
	
	Если ЭтотОбъект.ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
		
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ЭтотОбъект.ОписаниеЗаданияФормированияОтчета.ИдентификаторЗадания);
		ЭтотОбъект.ОписаниеЗаданияФормированияОтчета = Неопределено;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	Настройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
	Настройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
	Настройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";
	Настройки.ДополнительныеСвойства.Вставить("ВыводитьЗаголовок", ВыводитьЗаголовок);
	Настройки.ДополнительныеСвойства.Вставить("ВыводитьПодвал"   , ВыводитьПодвал);
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();
	
	ОписаниеЗаданияФормированияОтчета = ЗапуститьПодготовкуОтчета(ЭтотОбъект, ПараметрыОтчета);
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	
	Возврат ОписаниеЗаданияФормированияОтчета;
	
КонецФункции

&НаСервере
Функция ЗапуститьПодготовкуОтчета(Форма, ПараметрыОтчета)
	
	ПараметрыЗадания = ДлительныеОперации.ПараметрыВыполненияФункции(Форма.УникальныйИдентификатор);
	
	НаименованиеЗадания = НСтр("ru = 'Выполнение отчета: %1'");
	ИмяОтчета = "СверкаРасчетовСКонтрагентами";
	
	ПараметрыЗадания.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеЗадания, ИмяОтчета);
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ПараметрыЗадания.ЗапуститьНеВФоне = Истина;
	КонецЕсли;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыЗадания, "СервисСверкиРасчетовВызовСервера.ПодготовитьОтчет", ПараметрыОтчета);
	
КонецФункции

&НаСервере
Функция ПодготовитьПараметрыОтчетаНаСервере()
	
	МенеджерОтчета = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ЭтотОбъект.ИмяФормы);
	
	ПараметрыОтчета = МенеджерОтчета.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.Организация                       = Отчет.Организация;
	ПараметрыОтчета.ВключатьОбособленныеПодразделения = Отчет.ВключатьОбособленныеПодразделения;
	ПараметрыОтчета.Контрагент                        = Отчет.Контрагент;
	ПараметрыОтчета.НачалоПериода                     = Отчет.НачалоПериода;
	ПараметрыОтчета.КонецПериода                      = Отчет.КонецПериода;
	ПараметрыОтчета.РежимРасшифровки                  = Отчет.РежимРасшифровки;
	ПараметрыОтчета.СхемаКомпоновкиДанных             = ПолучитьИзВременногоХранилища(СхемаКомпоновкиДанных);
	ПараметрыОтчета.ИдентификаторОтчета               = "СверкаРасчетовСКонтрагентами";
	ПараметрыОтчета.ДанныеРасшифровки                 = ДанныеРасшифровки;
	ПараметрыОтчета.НастройкиКомпоновкиДанных         = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	ПараметрыОтчета.Вставить("ВыводитьЗаголовок"        , ВыводитьЗаголовок);
	ПараметрыОтчета.Вставить("ВыводитьПодвал"           , Ложь);
	ПараметрыОтчета.Вставить("ВыводитьОрганизацию"      , НЕ ЗначениеЗаполнено(Отчет.Организация));
	
	ПараметрыОтчета.Вставить("ИдентификаторМакета"      , "Основной"); // Ключ текущего варианта настроек отчета
	ПараметрыОтчета.Вставить("Группировка"              , Отчет.Группировка.Выгрузить());
	ПараметрыОтчета.Вставить("ДополнительныеПоля"       , Отчет.ДополнительныеПоля.Выгрузить());
	ПараметрыОтчета.Вставить("ИдентификаторыРасхождений", ИдентификаторыРасхождений);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикаНастроек()
	
	СервисСверкиРасчетовПереопределяемый.ИнициализацияКомпоновщикаНастроек(ЭтотОбъект, Истина, КлючТекущегоВарианта);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗапрещенныеПоля(Режим = "") Экспорт
	
	СписокПолей = Новый Массив;
	
	СписокПолей.Добавить("UserFields");
	СписокПолей.Добавить("DataParameters");
	СписокПолей.Добавить("SystemFields");
	СписокПолей.Добавить("Показатели");
	СписокПолей.Добавить("Период");
	СписокПолей.Добавить("НомерСтроки");
	
	СписокПолей.Добавить("КоличествоДокументов");
	СписокПолей.Добавить("ПризнакОтраженияНДС");
	СписокПолей.Добавить("СимволСноски");
	СписокПолей.Добавить("ДатаВходящегоДокумента");
	СписокПолей.Добавить("НомерВходящегоДокумента");
	
	СписокПолей.Добавить("ОшДокументРегистратор");
	СписокПолей.Добавить("ОшСтавкаНДС");
	СписокПолей.Добавить("ОшНоменклатура");
	СписокПолей.Добавить("ОшКоличествоДокументов");
	
	
	Для Каждого ДоступноеПоле Из Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если ДоступноеПоле.Ресурс Тогда
			СписокПолей.Добавить(Строка(ДоступноеПоле.Поле));
		КонецЕсли;
	КонецЦикла;
		
	Если Режим = "Отбор" Тогда
		СписокПолей.Добавить("РасшифровкаНачислениеРеализацияДокументРегистратор");
		СписокПолей.Добавить("ПризнакНаличияОшибок");
	ИначеЕсли Режим = "Порядок" Тогда
		СервисСверкиРасчетовКлиентПереопределяемый.ДобавитьПоляРесурсовВЗапрещенныеПоля(ЭтаФорма, СписокПолей);
		СписокПолей.Добавить("ПризнакНаличияОшибок");
	ИначеЕсли Режим = "Группировка" Тогда
		СписокПолей.Добавить("ЗаписьДополнительногоЛиста");
	ИначеЕсли Режим = "Выбор" Тогда
		СписокПолей.Добавить("ПризнакНаличияОшибок");
		СписокПолей.Добавить("ОтражениеНДСВУчете");
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(СписокПолей);
	
КонецФункции

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		ОтменитьВыполнениеЗаданияОбмена();
	КонецЕсли;
	
	ИдетПолучениеРасхождений = Ложь;
	ОтключитьОбработчикОжидания("ПроверитьЗавершенияОбмена");
	ОбновитьДатуПолученияДанных(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
	
КонецПроцедуры

&НаКлиенте
Процедура БаннерОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НаименованиеГруппировки = Строка(НавигационнаяСсылкаФорматированнойСтроки);
	НайденнаяОбласть = Результат.НайтиТекст(НаименованиеГруппировки);
	
	Если НайденнаяОбласть <> Неопределено Тогда
		
		Результат.ПоказатьУровеньГруппировокСтрок(0);
		Элементы.Результат.ТекущаяОбласть = НайденнаяОбласть;
		
		Если НаименованиеГруппировки = "Различаются реквизиты" Тогда
			Результат.Область("R" + Формат(НайденнаяОбласть.Низ, "ЧГ=0")+
			":R" + Формат(Результат.ВысотаТаблицы, "ЧГ=0")).Видимость = Истина;
		КонецЕсли;
		
		Если НаименованиеГруппировки = "Не отражено организацией" Тогда
			Результат.Область("R" + Формат(НайденнаяОбласть.Низ, "ЧГ=0")+
			":R" + Формат(НеОтраженоОрганизациейНиз, "ЧГ=0")).Видимость = Истина;
		КонецЕсли;
		
		Если НаименованиеГруппировки = "Не отражено контрагентом" Тогда
			Результат.Область("R" + Формат(НайденнаяОбласть.Низ, "ЧГ=0")+
			":R" + Формат(НеОтраженоКонтрагентомНиз, "ЧГ=0")).Видимость = Истина;
		КонецЕсли;
		
		Элементы.Результат.АктивизироватьПоУмолчанию = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы 
		И ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		ОтменитьВыполнениеЗаданияОбмена();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьВыполнениеЗаданияОбмена()
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗаданияОбмена);
	КонецЕсли;
	
	ИдентификаторЗаданияОбмена = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьОбластьСИтогами()
	
	Результат.УдалитьОбласть(Результат.Область("R"+Формат(Результат.ВысотаТаблицы -1, "ЧГ=0")+":R"+
	Формат(Результат.ВысотаТаблицы, "ЧГ=0")),
	ТипСмещенияТабличногоДокумента.ПоВертикали);

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Отчет.НачалоПериода) И ЗначениеЗаполнено(Отчет.КонецПериода)
		И Отчет.НачалоПериода > Отчет.КонецПериода Тогда
		ТекстСообщения = НСтр("ru = 'Дата начала периода не может быть больше даты конца периода'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.НачалоПериода",, Отказ);
	КонецЕсли;
	
	Если ЭтоФормированиеОтчета Тогда
		// Прочие проверки нужны только для получения данных из сервиса.
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.НачалоПериода) Тогда
		ТекстСообщения = НСтр("ru = 'Для получения данных из сервиса укажите начало периода'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.НачалоПериода",, Отказ);
	ИначеЕсли Отчет.НачалоПериода > КонецМесяца(ТекущаяДатаСеанса()) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru='Получение данных из сервиса доступно за периоды до %1'"),
			Формат(КонецМесяца(ТекущаяДатаСеанса()), "ДЛФ=Д") );
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.НачалоПериода",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.КонецПериода) Тогда
		ТекстСообщения = НСтр("ru = 'Для получения данных из сервиса укажите конец периода'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.КонецПериода",, Отказ);
	ИначеЕсли Отчет.КонецПериода < СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов() Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru='Получение данных из сервиса возможно с %1'"),
			Формат(СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов(), "ДЛФ=Д") );
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Отчет.КонецПериода",, Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Отчет.Организация)Тогда
		ТекстСообщения = НСтр("ru = 'Для получения данных из сервиса укажите организацию'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПолеОрганизация", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти