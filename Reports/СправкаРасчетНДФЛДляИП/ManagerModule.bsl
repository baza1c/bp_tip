#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВариантыОтчетов
// 
// Процедура используется подсистемой варианты отчетов
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя, Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

// Выполняет настройки вариантов отчета
//
// Параметры:
//   Настройки      - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   ОписаниеОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Подсистемы = Метаданные.Подсистемы;
	
	Для Каждого Вариант Из ВариантыНастроек Цикл
		
		Размещение = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение;
		
		Размещение.Вставить(Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийУчет, "");
		Размещение.Вставить(Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийИНалоговыйУчет, "");
		Размещение.Вставить(Подсистемы.БухгалтерияПредприятияПодсистемы.Подсистемы.ПростойИнтерфейс.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты, "");
		
	КонецЦикла;
	
КонецПроцедуры

// Вызывается при работе в модели сервиса для получения сведений о предопределенных вариантах отчета.
//
// Возвращаемое значение:
//  Массив из Структура:
//    * Имя           - Строка - имя варианта отчета; например, "Основной";
//    * Представление - Строка - имя варианта отчета; например, НСтр("ru = 'Динамика изменений файлов'").
//
Функция ВариантыНастроек() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление", "РасчетНДФЛДляИП", "Расчет НДФЛ для ИП"));
	
	Возврат Массив;
	
КонецФункции

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета", Ложь);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата", Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки", Ложь);
	Результат.Вставить("ИспользоватьПриВыводеЗаголовка", Истина);
	Результат.Вставить("ИспользоватьВнешниеНаборыДанных", Истина);
	Результат.Вставить("ИспользоватьФиксированныйМакетЗаголовкаИПодвала", Истина);
	
	Возврат Результат;
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
			"КонецПериода",
			КонецКвартала(ПараметрыОтчета.КонецПериода));
	Иначе
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
			"КонецПериода",
			КонецКвартала(ОбщегоНазначения.ТекущаяДатаПользователя()));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
			"Организация",
			ПараметрыОтчета.Организация);
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"НалоговаяСтавкаПоУмолчанию",
		УчетНДФЛПредпринимателя.МинимальнаяСтавка(ПараметрыОтчета.НачалоПериода));
	
	ПрименяетсяПрогрессивнаяШкала = ПараметрыОтчета.НачалоПериода >= УчетНДФЛПредпринимателяКлиентСервер.ДатаНачалаПрогрессивнойШкалы();
	
	Для Каждого ТекущаяСтрока Из КомпоновщикНастроек.Настройки.Структура Цикл
		
		Если ТекущаяСтрока.Имя = "СправкаРасчет" Тогда
			ТекущаяСтрока.Использование = Не ПрименяетсяПрогрессивнаяШкала;
		Иначе
			ТекущаяСтрока.Использование = ПрименяетсяПрогрессивнаяШкала;
		КонецЕсли;
		
	КонецЦикла;
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПриВыводеЗаголовка(ПараметрыОтчета, КомпоновщикНастроек, Результат) Экспорт
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьОрганизация = Макет.ПолучитьОбласть("Организация");
	
	// Организация
	ТекстОрганизация = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(ПараметрыОтчета.Организация, , ПараметрыОтчета.КонецПериода);
	ОбластьОрганизация.Параметры.НазваниеОрганизации = ТекстОрганизация;
	Результат.Вывести(ОбластьОрганизация);
	
	// Заголовок
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = "" + ПолучитьТекстЗаголовка(ПараметрыОтчета);
	Результат.Вывести(ОбластьЗаголовок);
	
	Результат.Область("R1:R" + Формат(Результат.ВысотаТаблицы, "ЧДЦ=0; ЧН=0; ЧГ=0")).Имя = "Заголовок";
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	Результат.ФиксацияСверху = 0;
	Результат.ФиксацияСлева  = 0;
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
КонецПроцедуры

Функция ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки) Экспорт
	
	НалоговыеСтавки = УчетНДФЛПредпринимателя.НалоговыеСтавки(ПараметрыОтчета.НачалоПериода);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("НалоговыеСтавки", НалоговыеСтавки);
	
	Возврат ВнешниеНаборыДанных;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт
	
	НачалоПериода = НачалоГода(ПараметрыОтчета.НачалоПериода);
	КонецПериода  = КонецКвартала(ПараметрыОтчета.КонецПериода);
	
	ШаблонСтроки = НСтр("ru = 'Справка-расчет авансового платежа по НДФЛ за %1'");
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		НачалоПериода, КонецПериода, Истина);
	
	Возврат СтрШаблон(ШаблонСтроки, ПредставлениеПериода);
	
КонецФункции

#КонецОбласти

#КонецЕсли