#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	ПодготовитьОтчет(ПараметрыОтчета);
	СписокСформированныхЛистов = ПараметрыОтчета.СписокСформированныхЛистов;
	
	Возврат СписокСформированныхЛистов; 
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет структуру настроек универсального формата из реквизитов формы.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПустыеПараметрыКомпоновкиОтчета()
//  Форма - УправляемаяФорма - содержит основновной реквизит Отчет .
//
Процедура ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма) Экспорт
	
	Отчет = Форма.Отчет;
	
	ПараметрыОтчета.Организация   = Отчет.Организация;
	ПараметрыОтчета.НачалоПериода = Отчет.НачалоПериода;
	ПараметрыОтчета.КонецПериода  = Отчет.КонецПериода;
	ПараметрыОтчета.Патент        = Отчет.Патент;
	
	ПараметрыОтчета.Заголовок     = Форма.Заголовок;
	ПараметрыОтчета.ТипФайла      = Форма.ТипФайла;
	ПараметрыОтчета.Расширение    = Форма.Расширение;
	
	ПараметрыОтчета.СписокСформированныхЛистов = Отчет.СписокСформированныхЛистов;
	
КонецПроцедуры

Функция ЗаполнитьПараметрыОтчета(ПараметрыОтчета) Экспорт
	
	ПараметрыОтчета.СписокСформированныхЛистов.Очистить();
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Патент)
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыОтчета, "РежимРасшифровки", Ложь) Тогда
		СвойстваПатента = УчетПСН.СведенияДействующегоПатента(ПараметрыОтчета.Патент);
		ПараметрыОтчета.НачалоПериода = НачалоДня(СвойстваПатента.ДатаНачала);
		ПараметрыОтчета.КонецПериода  = КонецДня(СвойстваПатента.ДатаОкончания);
	Иначе
		ПараметрыОтчета.НачалоПериода = НачалоДня(ПараметрыОтчета.НачалоПериода);
		ПараметрыОтчета.КонецПериода  = КонецДня(ПараметрыОтчета.КонецПериода);
	КонецЕсли;
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Функция-конструктор для хранения настроек отчета в универсальном формате.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Общая структура настроек.
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ИдентификаторОтчета", "КнигаУчетаДоходовПатент"); // имя объекта метаданных
	ПараметрыОтчета.Вставить("РежимРасшифровки", Ложь); // Истина - если настройки сгенерированы автоматически
	
	// Отборы из шапки отчета.
	ПараметрыОтчета.Вставить("Организация",   Справочники.Организации.ПустаяСсылка()); // по организации
	ПараметрыОтчета.Вставить("НачалоПериода", '00010101');
	ПараметрыОтчета.Вставить("КонецПериода",  '00010101');
	ПараметрыОтчета.Вставить("Патент",        Справочники.Патенты.ПустаяСсылка());
	
	ПараметрыОтчета.Вставить("СписокСформированныхЛистов", Новый СписокЗначений);
	
	ПараметрыОтчета.Вставить("Заголовок", "");
	ПараметрыОтчета.Вставить("ТипФайла");
	ПараметрыОтчета.Вставить("Расширение", "");
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Функция ПодготовитьОбщийФайлОтчета(ПараметрыОтчета) Экспорт
	
	ПодготовитьОтчет(ПараметрыОтчета);
	
	ВозвращаемыеПараметры = Новый Структура;
	Если ЗначениеЗаполнено(ПараметрыОтчета.Заголовок) Тогда
		ПараметрыОтчета.Вставить("Заголовок", ПараметрыОтчета.Заголовок);
	Иначе
		ПараметрыОтчета.Вставить("Заголовок", СтрШаблон(НСтр("ru = 'Книга доходов по патенту %1'"),
			ПараметрыОтчета.Патент));
	КонецЕсли;
	
	ВозвращаемыеПараметры = ЗаполнениеФинОтчетностиВБанки.ПодготовитьДвоичныеДанныеПакетаОтображаемыхДокументов(
		ПараметрыОтчета);
	
	Возврат ВозвращаемыеПараметры;
	
КонецФункции

Процедура ПодготовитьОтчет(ПараметрыОтчета) Экспорт
	
	ЗаполнитьПараметрыОтчета(ПараметрыОтчета);
	
	// ПОДГОТОВКА ОТЧЕТА ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ
	
	ПодготовитьДанныеОтчета(ПараметрыОтчета);
	
	// ПОСТРОЕНИЕ ОТЧЕТА
	
	СформироватьТитульныйЛист(ПараметрыОтчета);
	
	СформироватьТаблицуДоходы(ПараметрыОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеОтчета(ПараметрыОтчета)
	
	НомераТаблиц = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",   ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("Патент",        ПараметрыОтчета.Патент);
	Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  ПараметрыОтчета.КонецПериода);
	Запрос.УстановитьПараметр("ПустаяДата",    Дата("00010101"));
	
	Запрос.Текст = ТекстЗапросаТитульныйЛист(НомераТаблиц)
		+ ТекстЗапросаКнигаУчетаДоходовПатент(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		
		Если Результат[НомерТаблицы.Значение] <> Неопределено Тогда
			ПараметрыОтчета.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение]);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаТитульныйЛист(НомераТаблиц)
	
	НомераТаблиц.Вставить("БанковскиеСчета", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ДанныеПатента",   НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	БанковскиеСчета.ИдентификаторЦифровогоСчета КАК ИдентификаторЦифровогоСчета,
	|	БанковскиеСчета.Банк.Наименование КАК НаименованиеБанка,
	|	БанковскиеСчета.ЦифровойСчет КАК ЦифровойСчет
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец = &Организация
	|	И (БанковскиеСчета.ДатаОткрытия = &ПустаяДата
	|			ИЛИ БанковскиеСчета.ДатаОткрытия <= &КонецПериода)
	|	И (БанковскиеСчета.ДатаЗакрытия = &ПустаяДата
	|			ИЛИ БанковскиеСчета.ДатаЗакрытия >= &НачалоПериода)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЦифровойСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияСПатентом.НомерПатента КАК НомерПатента,
	|	ВЫБОР
	|		КОГДА ОперацияСПатентом.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВыданПоМестуЖительства,
	|	ОперацияСПатентом.КодПоОКТМО КАК КодПоОКТМО,
	|	ЕСТЬNULL(ОперацияСПатентом.НалоговыйОрган.КодРегиона, """") КАК НалоговыйОрганКодРегиона
	|ИЗ
	|	Документ.ОперацияСПатентом КАК ОперацияСПатентом
	|ГДЕ
	|	ОперацияСПатентом.Патент = &Патент
	|	И ОперацияСПатентом.Проведен
	|	И &КонецПериода МЕЖДУ ОперацияСПатентом.ДатаНачала И КОНЕЦПЕРИОДА(ОперацияСПатентом.ДатаОкончания, ДЕНЬ)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаКнигаУчетаДоходовПатент(НомераТаблиц)
	
	НомераТаблиц.Вставить("КнигаУчетаДоходовПатент", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КнигаУчетаДоходовПатент.Регистратор КАК Регистратор,
	|	КнигаУчетаДоходовПатент.РеквизитыПервичногоДокумента КАК РеквизитыПервичногоДокумента,
	|	КнигаУчетаДоходовПатент.Содержание КАК Содержание,
	|	КнигаУчетаДоходовПатент.Графа4 КАК Графа4
	|ИЗ
	|	РегистрНакопления.КнигаУчетаДоходовПатент КАК КнигаУчетаДоходовПатент
	|ГДЕ
	|	КнигаУчетаДоходовПатент.Организация = &Организация
	|	И КнигаУчетаДоходовПатент.Патент = &Патент
	|	И КнигаУчетаДоходовПатент.Активность
	|	И КнигаУчетаДоходовПатент.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ КнигаУчетаДоходовПатент.Графа4 = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	КнигаУчетаДоходовПатент.Период,
	|	КнигаУчетаДоходовПатент.Регистратор,
	|	КнигаУчетаДоходовПатент.НомерСтроки
	|ИТОГИ
	|	СУММА(Графа4)
	|ПО
	|	ОБЩИЕ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Процедура СформироватьТитульныйЛист(ПараметрыОтчета)
	
	СписокПоказателей = Новый СписокЗначений;
	СписокПоказателей.Добавить("", "ФИО");
	СписокПоказателей.Добавить("", "АдрПрописки");
	СписокПоказателей.Добавить("", "ОКПО");
	СписокПоказателей.Добавить("", "ИННФЛ");
	СписокПоказателей.Добавить("", "ОргСубъект");
	
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода, СписокПоказателей);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Автомасштаб          = Истина;
	ТабличныйДокумент.ЧерноБелаяПечать     = Истина;
	ТабличныйДокумент.ТолькоПросмотр       = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КнигаУчетаДоходовИРасходов_ТитульныйЛист";
	
	ИмяМакета = ИмяМакетаТитульногоЛиста(ПараметрыОтчета.КонецПериода);
	Макет = ПолучитьМакет(ИмяМакета);
	
	Макет.Параметры.НаПериод = СтрШаблон(НСтр("ru = 'на %1 год'"), Формат(Год(ПараметрыОтчета.КонецПериода), "ЧГ="));
	Макет.Параметры.ФИО      = СведенияОбОрганизации.ФИО;
	Макет.Параметры.ОКПО     = СведенияОбОрганизации.ОКПО;
	
	ФормаКУДПоПриказу_ЕА_7_3_816 =
		ПараметрыОтчета.КонецПериода >= УчетПСНКлиентСервер.ДатаНачалаДействияФормыКнигиДоходовПоПриказу_ЕА_7_3_816();
	
	Если Не ФормаКУДПоПриказу_ЕА_7_3_816 Тогда
		Макет.Параметры.АдрПрописки = РегламентированнаяОтчетностьКлиентСервер.ПредставлениеАдресаВФормате9Запятых(
			СведенияОбОрганизации.АдрПрописки);
	КонецЕсли;
	
	ДанныеПатента = ПараметрыОтчета.ДанныеПатента.Выбрать();
	
	Если ДанныеПатента.Следующий() Тогда
		
		Если ФормаКУДПоПриказу_ЕА_7_3_816 Тогда
			Макет.Параметры.НомерПатента = ДанныеПатента.НомерПатента;
		Иначе
			Если ДанныеПатента.ВыданПоМестуЖительства Тогда
				НалоговыйОрган = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					ПараметрыОтчета.Организация, "РегистрацияВНалоговомОргане");
				
				Макет.Параметры.ОКАТО = Справочники.РегистрацииВНалоговомОргане.КодТерритории(
					НалоговыйОрган, ПараметрыОтчета.КонецПериода);
				Макет.Параметры.НаименованиеСубъектаРФ = СведенияОбОрганизации.ОргСубъект;
			Иначе
				Макет.Параметры.ОКАТО = ДанныеПатента.КодПоОКТМО;
				Макет.Параметры.НаименованиеСубъектаРФ = АдресныйКлассификатор.НаименованиеРегионаПоКоду(
					ДанныеПатента.НалоговыйОрганКодРегиона);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Макет.Параметры.СрокПатента = СтрШаблон(
		НСтр("ru = 'с %1 г. по %2 г.'"),
		Формат(ПараметрыОтчета.НачалоПериода, "ДФ='дд ММММ гггг'"),
		Формат(ПараметрыОтчета.КонецПериода, "ДФ='дд ММММ гггг'"));
	
	Счета = "";
	Выборка = ПараметрыОтчета.БанковскиеСчета.Выбрать();
	Пока Выборка.Следующий() Цикл    
		Если Выборка.ЦифровойСчет Тогда
			Счета = Счета + ?(ПустаяСтрока(Счета), "", ", ")
				+ СтрШаблон(НСтр("ru = 'Цифровой рубль: %1'"), СокрЛП(Выборка.ИдентификаторЦифровогоСчета));
		Иначе
			Счета = Счета + ?(ПустаяСтрока(Счета), "", ", ")
			+ СтрШаблон(НСтр("ru = '№ %1 в %2'"), СокрЛП(Выборка.НомерСчета), СокрЛП(Выборка.НаименованиеБанка));
		КонецЕсли;
	КонецЦикла;
	Макет.Параметры.Счета = Счета;
	
	ТабличныйДокумент.Вывести(Макет);
	
	ИНН = СведенияОбОрганизации.ИННФЛ;
	
	Если СтрДлина(ИНН) <> 12 Тогда
		// Налогоплательщик - юр. лицо
		ИНН = "00" + ИНН;
	КонецЕсли;
	
	Ном = 1;
	Пока Ном > 0 Цикл
		ИмяОбластиИНН = "ПрИНН";
		Если Не (Ном > 12) Тогда
			ТабличныйДокумент.Область(ИмяОбластиИНН + Ном).Текст = ?(Число(ИНН) > 0, Сред(ИНН, Ном, 1), "");
			
			Ном = Ном + 1;
			Продолжить;
		КонецЕсли;
		Ном = 0;
	КонецЦикла;
	
	ПараметрыОтчета.СписокСформированныхЛистов.Добавить(ТабличныйДокумент, "Титульный лист");
	
КонецПроцедуры

Процедура СформироватьТаблицуДоходы(ПараметрыОтчета)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Автомасштаб          = Истина;
	ТабличныйДокумент.ЧерноБелаяПечать     = Истина;
	ТабличныйДокумент.ТолькоПросмотр       = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КнигаУчетаДоходовПатент_ТаблицаДоходы";
	
	ИмяМакета = ИмяМакетаТаблицыДоходов(ПараметрыОтчета.КонецПериода);
	Макет = ПолучитьМакет(ИмяМакета);
	
	ОбластьШапка  = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого  = Макет.ПолучитьОбласть("Итого");
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	ВыборкаОбщие = ПараметрыОтчета.КнигаУчетаДоходовПатент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОБЩИЕ");
	Если ВыборкаОбщие.Следующий() Тогда
		
		НПП = 0;
		
		Выборка = ВыборкаОбщие.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НПП = НПП + 1;
			
			ОбластьСтрока.Параметры.НомерПП = НПП;
			ОбластьСтрока.Параметры.Заполнить(Выборка);
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		ОбластьИтого.Параметры.Заполнить(ВыборкаОбщие);
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
	КонецЕсли;
	
	ТабличныйДокумент.ПовторятьПриПечатиСтроки = ТабличныйДокумент.Область(6, , 6, );
	
	ПараметрыОтчета.СписокСформированныхЛистов.Добавить(ТабличныйДокумент, "Доходы");
	
КонецПроцедуры

Функция ИмяМакетаТитульногоЛиста(Период)
	
	Если Период < УчетПСН.ДатаНачалаДействияПатентнойСистемы() Тогда
		ИмяМакета = "ТитульныйЛист_154н";
	ИначеЕсли Период < УчетПСНКлиентСервер.ДатаНачалаДействияФормыКнигиДоходовПоПриказу_ЕА_7_3_816() Тогда
		ИмяМакета = "ТитульныйЛист_135н";
	Иначе
		ИмяМакета = "ТитульныйЛист_патент";
	КонецЕсли;
	
	Возврат ИмяМакета;
	
КонецФункции

Функция ИмяМакетаТаблицыДоходов(Период)
	
	Если Период < УчетПСН.ДатаНачалаДействияПатентнойСистемы() Тогда
		ИмяМакета = "Раздел1_154н";
	ИначеЕсли Период < УчетПСНКлиентСервер.ДатаНачалаДействияФормыКнигиДоходовПоПриказу_ЕА_7_3_816() Тогда
		ИмяМакета = "Раздел1_135н";
	Иначе
		ИмяМакета = "Раздел1_патент";
	КонецЕсли;
	
	Возврат ИмяМакета;
	
КонецФункции

#КонецОбласти

#КонецЕсли