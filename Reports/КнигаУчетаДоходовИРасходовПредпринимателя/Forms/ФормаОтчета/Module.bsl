
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимРасшифровки = Параметры.РежимРасшифровки;
	ПараметрФормированияТаблиц = РежимРасшифровки;
	
	Если РежимРасшифровки Тогда
		
		Отчет.Организация   = Параметры.Организация;
		Отчет.НачалоПериода = НачалоМесяца(Параметры.НачалоПериода);
		Отчет.КонецПериода  = КонецМесяца(Параметры.КонецПериода);
		
		Если Параметры.ФормироватьВсеТаблицы Тогда
			ПараметрФормированияТаблиц = Ложь;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметры.Расширение) И ЗначениеЗаполнено(Параметры.ТипФайла) Тогда
			Расширение = Параметры.Расширение;
			ТипФайла   = Параметры.ТипФайла;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
	Иначе
		
		ОрганизацияПоУмолчанию = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		ЭтоЮрЛицо = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(ОрганизацияПоУмолчанию);
		
		Отчет.Организация = ?(ЭтоЮрЛицо, Справочники.Организации.ПустаяСсылка(), ОрганизацияПоУмолчанию);
		
		НалоговыйПериод = НалоговыйПериод(Отчет.Организация, ОбщегоНазначения.ТекущаяДатаПользователя());
		
		Отчет.НачалоПериода = НалоговыйПериод.Начало;
		Отчет.КонецПериода  = НалоговыйПериод.Конец;
		
	КонецЕсли;
	
	ТекущаяДатаОтчета = Отчет.КонецПериода;
	
	Отчеты.КнигаУчетаДоходовИРасходовПредпринимателя.ОбновитьСписокТаблиц(Отчет.СписокТаблиц, ПараметрФормированияТаблиц);
	
	ОрганизацияПриИзмененииНаСервере();
	
	БухгалтерскиеОтчеты.ИнициализироватьРежимВыгрузкиБП(ЭтотОбъект);
	БухгалтерскиеОтчеты.УстановитьАктивностьКнопокКоманднойПанели(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	БухгалтерскиеОтчетыКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
	Если РежимРасшифровки Тогда
		ПодключитьОбработчикОжидания("Подключаемый_СформироватьПриОткрытии", БухгалтерскиеОтчетыКлиент.ИнтервалЗапускаФормированияОтчетаПриОткрытии(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки)
	
	Если ИспользуютсяСтандартныеНастройки Тогда
		Возврат;
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки, Истина);
	ЭтоЮрЛицо = ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Отчет.Организация);
	Если ЭтоЮрЛицо Тогда
		Отчет.Организация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	ТекущаяДатаОтчета = Отчет.КонецПериода;
	
	Отчеты.КнигаУчетаДоходовИРасходовПредпринимателя.ОбновитьСписокТаблиц(Отчет.СписокТаблиц);
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(Отчет.НачалоПериода) ИЛИ НЕ ЗначениеЗаполнено(Отчет.КонецПериода) Тогда
		ТекстСообщения = НСтр("ru = 'Не задан период формирования отчета'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Период", , Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отчет.ВидДеятельности) Тогда
		
		ХарактерДеятельности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отчет.ВидДеятельности, "ХарактерДеятельности");
		
		ХарактерыДеятельностиОСН       = УчетДоходовИРасходовПредпринимателя.ХарактерыДеятельностиОСН();
		ХарактерыДеятельностиЕНВД      = УчетДоходовИРасходовПредпринимателя.ХарактерыДеятельностиЕНВД();
		ХарактерыДеятельностиУСНПатент = УчетДоходовИРасходовПредпринимателя.ХарактерыДеятельностиУСНПатент();
		
		Если ХарактерыДеятельностиОСН.Найти(ХарактерДеятельности) = Неопределено Тогда
			
			Если НЕ ХарактерыДеятельностиЕНВД.Найти(ХарактерДеятельности) = Неопределено Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Вид деятельности ""%1"" облагается ЕНВД'"), Отчет.ВидДеятельности);
			ИначеЕсли НЕ ХарактерыДеятельностиУСНПатент.Найти(ХарактерДеятельности) = Неопределено Тогда
				Если Отчет.КонецПериода < УчетПСН.ДатаНачалаДействияПатентнойСистемы() Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В отношении вида деятельности ""%1"" применяется УСН на патенте'"), Отчет.ВидДеятельности);
				Иначе
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'В отношении деятельности ""%1"" применяется патентная система налогообложения'"), Отчет.ВидДеятельности);
				КонецЕсли;
			Иначе
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Для вида деятельности ""%1"" указан некорректный характер деятельности'"), Отчет.ВидДеятельности);
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ВидДеятельности", "Отчет", Отказ);
			
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	БухгалтерскиеОтчетыКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ПериодПриИзменении(Отчет.НачалоПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	ПериодПриИзменении(Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДеятельностиПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛистПриИзменении(Элемент)
	
	ПоказатьВыбранныйЛист();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьРеквизитыПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОблагаетсяНДСПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		"Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриИзменении(Элемент)
	
	РучнаяКорректировка = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТаблицВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура;
	Если Год(Отчет.НачалоПериода) <> Год(Отчет.КонецПериода) Тогда
		// Установлен расширенный налоговый период, но форма ВыборСтандартногоПериода предназначена
		// только для выбора периода в пределах года. Поэтому в качестве начала периода используем
		// минимально возможную дату.
		ПараметрыВыбора.Вставить("НачалоПериода", НачалоГода(Отчет.КонецПериода));
	Иначе
		ПараметрыВыбора.Вставить("НачалоПериода", Отчет.НачалоПериода);
	КонецЕсли;
	ПараметрыВыбора.Вставить("КонецПериода",  Отчет.КонецПериода);
	ПараметрыВыбора.Вставить("Кратность", ПредопределенноеЗначение("Перечисление.Периодичность.Месяц"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Если Элементы.Результат.ОтображениеСостояния.Видимость Тогда
		
		ТекущийЭлемент = Элементы.Сформировать;
		ПоказатьПредупреждение( , НСтр("ru = 'Нажмите ""Сформировать"" для получения отчета'"),
								, НСтр("ru = 'Отчет не сформирован'"));
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОтчета = ЗаполнитьПараметрыОтчетаНаСервере();
	
	ПараметрыОтчета.Вставить("РучнаяКорректировка", РучнаяКорректировка);
	ПараметрыОтчета.Вставить("РучнаяНастройка",
		БухгалтерскиеОтчетыКлиент.ЕстьОтличияНастроекОтЭталонныхБП(ЭтотОбъект.ПараметрыРежимаВыгрузки, Отчет));
	
	Закрыть(ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СформироватьОтчетНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	КнопкаДействия = ?(Элементы.Выгрузить.Видимость, Элементы.Выгрузить, Элементы.Сформировать);
	КнопкаДействия.КнопкаПоУмолчанию = Истина;
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СписокТаблицВключить(Команда)
	
	Для Каждого Таблица Из Отчет.СписокТаблиц Цикл
		Таблица.Формировать = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокТаблицВыключить(Команда)
	
	Для Каждого Таблица Из Отчет.СписокТаблиц Цикл
		Таблица.Формировать = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_СформироватьПриОткрытии()
	
	СформироватьОтчетНаКлиенте();
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПараметрыОтчетаНаСервере()
	
	// Нельзя использовать ПодготовитьПараметрыОтчета(), которая используется при формировании отчета
	// - т.к. требуется другая структура данных: вместо таблицы значений СписокТаблиц,
	// требуется список значений СписокСформированныхЛистов.
	
	ПараметрыОтчета = Отчеты.КнигаУчетаДоходовИРасходовПредпринимателя.ПустыеПараметрыКомпоновкиОтчета();
	Отчеты.КнигаУчетаДоходовИРасходовПредпринимателя.ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, ЭтотОбъект);
	
	ВозвращаемыеПараметры = ЗаполнениеФинОтчетностиВБанки.ПодготовитьДвоичныеДанныеПакетаОтображаемыхДокументов(
		ПараметрыОтчета);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ВозвращаемыеПараметры, ПараметрыОтчета, Истина);
	
	Возврат ВозвращаемыеПараметры;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТекстЗаголовка(Форма)
	
	Отчет = Форма.Отчет;
	
	ЗаголовокОтчета = НСтр("ru = 'Книга доходов и расходов предпринимателя'") +
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.НачалоПериода, Отчет.КонецПериода);

	Если ЗначениеЗаполнено(Отчет.Организация) И Форма.ИспользуетсяНесколькоОрганизаций Тогда
		ЗаголовокОтчета = ЗаголовокОтчета + " " + БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Отчет.Организация);
	КонецЕсли;
	
	Форма.Заголовок = ЗаголовокОтчета;
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		Отчет.ОблагаетсяНДС = Не УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(Отчет.Организация, Отчет.НачалоПериода);
	Иначе
		Отчет.ОблагаетсяНДС = Истина;
	КонецЕсли;
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	ОтобразитьПоясненияКПериодуОтчета(ЭтотОбъект);
	
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыОтчета()
	
	ПараметрыОтчета = Новый Структура(
		"Организация, НачалоПериода, КонецПериода, 
		|ВидДеятельности, ОблагаетсяНДС, ВыводитьРеквизиты");
	
	ЗаполнитьЗначенияСвойств(ПараметрыОтчета, Отчет);
	
	СписокТаблиц = Отчет.СписокТаблиц.Выгрузить();
	Для Каждого Таблица Из СписокТаблиц Цикл
		Если Таблица.Формировать Тогда
			Таблица.АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Иначе
			Таблица.АдресХранилища = "";
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыОтчета.Вставить("СписокТаблиц", СписокТаблиц);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчетНаКлиенте()
	
	ОчиститьСообщения();
	ОтказПроверкиЗаполнения = Ложь;
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	РезультатВыполнения.Свойство("ОтказПроверкиЗаполнения", ОтказПроверкиЗаполнения);
	
	Если ОтказПроверкиЗаполнения = Истина Тогда
		
		ПоказатьНастройки("");
		
	Иначе
		
		Если РезультатВыполнения.Статус = "Выполняется" Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);

		Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
		
	КонецЕсли;
	
	РучнаяКорректировка = Ложь;
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере()
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено, ОтказПроверкиЗаполнения", Истина, Истина);
	КонецЕсли;
	
	БухгалтерскиеОтчеты.ОтменитьВыполнениеЗадания(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	ПараметрыЗадания = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыЗадания.ЗапуститьВФоне = Истина;
	ПараметрыЗадания.НаименованиеФоновогоЗадания =
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтотОбъект);
			
	ПараметрыОтчета = ПодготовитьПараметрыОтчета();
			
	ОписаниеЗаданияФормированияОтчета = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыЗадания, "Отчеты.КнигаУчетаДоходовИРасходовПредпринимателя.СформироватьОтчет", ПараметрыОтчета);
	
	КнопкаДействия = ?(Элементы.Выгрузить.Видимость, Элементы.Выгрузить, Элементы.Сформировать);
	КнопкаДействия.КнопкаПоУмолчанию = Истина;
	
	Возврат ОписаниеЗаданияФормированияОтчета;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения)
	
	СписокТаблиц = ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	Если ТипЗнч(СписокТаблиц) = Тип("ТаблицаЗначений") Тогда
		Для Каждого СтараяТаблица Из Отчет.СписокТаблиц Цикл
			Если ЭтоАдресВременногоХранилища(СтараяТаблица.АдресХранилища) Тогда
				УдалитьИзВременногоХранилища(СтараяТаблица.АдресХранилища);
			КонецЕсли;
		КонецЦикла;
		Отчет.СписокТаблиц.Загрузить(СписокТаблиц);
	КонецЕсли;
	
	Элементы.Лист.СписокВыбора.Очистить();
	Для Каждого Таблица Из Отчет.СписокТаблиц Цикл
		Если Таблица.Формировать Тогда
			Если ПустаяСтрока(Таблица.ЗаголовокТаблицы) Тогда
				ПредставлениеТаблицы = Таблица.Наименование;
			Иначе
				ПредставлениеТаблицы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)",
					Таблица.ЗаголовокТаблицы, Таблица.Наименование);
			КонецЕсли;
			
			Элементы.Лист.СписокВыбора.Добавить(Таблица.КодТаблицы, ПредставлениеТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	ЕстьВыборЛиста = (Элементы.Лист.СписокВыбора.Количество() > 0);
	
	Если Не ЕстьВыборЛиста Тогда
		Лист = 0;
	ИначеЕсли Элементы.Лист.СписокВыбора.НайтиПоЗначению(Лист) = Неопределено Тогда
		Лист = Элементы.Лист.СписокВыбора[0].Значение;
	КонецЕсли;
	
	ПоказатьВыбранныйЛист();
	
	ОписаниеЗаданияФормированияОтчета = Неопределено;
	
	Элементы.Лист.Доступность = ЕстьВыборЛиста;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
КонецПроцедуры

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьВыбранныйЛист()
	
	Перем ВыбранныйЛист;
	
	Результат.Очистить();
	
	НайденныеСтроки = Отчет.СписокТаблиц.НайтиСтроки(Новый Структура("КодТаблицы", Лист));
	
	Если НайденныеСтроки.Количество() > 0 И ЭтоАдресВременногоХранилища(НайденныеСтроки[0].АдресХранилища) Тогда
		
		СформированныйЛист = ПолучитьИзВременногоХранилища(НайденныеСтроки[0].АдресХранилища);
		
		Если ТипЗнч(СформированныйЛист) = Тип("ТабличныйДокумент") Тогда
			Результат.Вывести(СформированныйЛист);
			Результат.Автомасштаб         = СформированныйЛист.Автомасштаб;
			Результат.ЧерноБелаяПечать    = СформированныйЛист.ЧерноБелаяПечать;
			Результат.ТолькоПросмотр      = СформированныйЛист.ТолькоПросмотр;
			Результат.ОриентацияСтраницы  = СформированныйЛист.ОриентацияСтраницы;
			Результат.КлючПараметровПечати = СформированныйЛист.КлючПараметровПечати;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрОрганизацияФункциональныхОпцийФормы(
		ЭтотОбъект,
		Отчет.Организация,
		Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВыбора.НачалоПериода = НачалоГода(РезультатВыбора.НачалоПериода) Тогда
		// Приводим начало периода к началу налогового периода с учетом возможности расширения налогового периода до даты регистрации.
		Отчет.НачалоПериода = НачалоРасширенногоНалоговогоПериода(Отчет.Организация, РезультатВыбора.НачалоПериода);
	Иначе
		Отчет.НачалоПериода = РезультатВыбора.НачалоПериода;
	КонецЕсли;
	Отчет.КонецПериода = РезультатВыбора.КонецПериода;
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	ОтобразитьПоясненияКПериодуОтчета(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция НачалоРасширенногоНалоговогоПериода(Знач Организация, Знач НачалоПериода)
	
	Возврат УчетДоходовИРасходовПредпринимателя.НалоговыйПериод(Организация, НачалоПериода).Начало;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьПоясненияКПериодуОтчета(Форма)
	
	Элементы = Форма.Элементы;
	Отчет = Форма.Отчет;
	
	Если ЗначениеЗаполнено(Отчет.Организация) И ЗначениеЗаполнено(Отчет.КонецПериода) Тогда
		ТекстПояснения = ТекстПоясненияРасширенныйНалоговыйПериод(Отчет.Организация, Отчет.КонецПериода);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстПояснения) Тогда
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = Истина;
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Заголовок = ТекстПояснения;
	Иначе
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстПоясненияРасширенныйНалоговыйПериод(Знач Организация, Знач КонецПериода)
	
	НалоговыйПериод = НалоговыйПериод(Организация, КонецПериода);
	Если КонецПериода < НалоговыйПериод.Начало Тогда
		Возврат "";
	КонецЕсли;
	
	НалоговыйПериодПропущен = (КонецПериода < НалоговыйПериод.Период);
	НалоговыйПериодРасширен = (НалоговыйПериод.Начало < НалоговыйПериод.Период);
	
	ГодОтчета = Год(КонецПериода);
	
	Если НалоговыйПериодПропущен Тогда
		ТекстПояснения = СтрШаблон(
			НСтр("ru = 'Книгу доходов и расходов за %1 год формировать не нужно. Период с даты регистрации %2 по %3 включается в отчет за %4 год.'"),
			Формат(ГодОтчета, "ЧГ=0"),
			Формат(НалоговыйПериод.Начало, "ДЛФ=D"),
			Формат(НалоговыйПериод.Период - 1, "ДЛФ=D"),
			Формат(ГодОтчета + 1, "ЧГ=0"));
	ИначеЕсли НалоговыйПериодРасширен Тогда
		ТекстПояснения = СтрШаблон(
			НСтр("ru = 'Период с даты регистрации %1 по %2 включается в отчет за %3 год.'"),
			Формат(НалоговыйПериод.Начало, "ДЛФ=D"),
			Формат(НалоговыйПериод.Период - 1, "ДЛФ=D"),
			Формат(ГодОтчета, "ЧГ=0"));
	Иначе
		ТекстПояснения = "";
	КонецЕсли;
	
	Возврат ТекстПояснения;
	
КонецФункции

&НаСервереБезКонтекста
Функция НалоговыйПериод(Знач Организация, Знач КонецПериода)
	
	Возврат УчетДоходовИРасходовПредпринимателя.НалоговыйПериод(Организация, КонецПериода);
	
КонецФункции

&НаКлиенте
Процедура ПериодПриИзменении(Период)
	
	НалоговыйПериод = НалоговыйПериод(Отчет.Организация, Период);
	
	Отчет.НачалоПериода = Макс(НачалоМесяца(Отчет.НачалоПериода), НалоговыйПериод.Начало);
	Отчет.КонецПериода = Мин(КонецМесяца(Отчет.КонецПериода), НалоговыйПериод.Конец);
	
	Если Отчет.КонецПериода <> ТекущаяДатаОтчета Тогда
		УстановитьФункциональныеОпцииФормы();
		ТекущаяДатаОтчета = Отчет.КонецПериода;
	КонецЕсли;
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	ОтобразитьПоясненияКПериодуОтчета(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанельФормаОтчета(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы,
		Элементы.ГруппаОсновнаяКоманднаяПанельОтчета.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы,
		Элементы.ГруппаПечать.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКомандуСохранитьВМенюФормаОтчета(Элементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВПодменюМКПоделиться(Элементы, Элементы.Сохранить);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаКоманднаяПанельОтчета);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы,
		Элементы.ГруппаБыстрыеОтборы);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы,
		Элементы.ГруппаРасширенныйНалоговыйПериод);
	Элементы.СписокТаблиц.РежимВыбора = Истина;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Лист);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.РазделыОтчета);
	
	Элементы.НачалоПериода.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаПериод.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаПериод.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.Организация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВидДеятельности.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ГруппаБыстрыеОтборы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

