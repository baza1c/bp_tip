
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//   Структура - Флаги, задающие необходимость дополнительных действий.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт

	Параметры = Новый Структура();
	Параметры.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Параметры.Вставить("ИспользоватьПослеКомпоновкиМакета", Истина);
	Параметры.Вставить("ИспользоватьПриВыводеЗаголовка", Истина); 
	Параметры.Вставить("ИспользоватьДанныеРасшифровки", Истина);
	Параметры.Вставить("ИспользоватьПривилегированныйРежим", Истина);

	Возврат Параметры;

КонецФункции

// Формирует текст, выводимый в заголовке отчета.
//
// Параметры:
//   ПараметрыОтчета - Структура - См. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//
// Возвращаемое значение:
//   Строка - текст заголовка с учетом периода.
//
Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт

	ПредставлениеОтчета = ПредставлениеОтчетаПрайсЛист();

	Возврат СтрШаблон(НСтр("ru = '%1 от %2'"), ПредставлениеОтчета.Представление, Формат(ПараметрыОтчета.Период, "ДЛФ=ДД"));

КонецФункции

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//   ОписаниеОтчета - СтрокаДереваЗначений - Настройки этого отчета, сформированные через ВариантыОтчетов.ОписаниеОтчета().
//
Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт

	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, ПредставлениеОтчетаПрайсЛист().Имя);
	ОписаниеВарианта.Размещение.Вставить(Метаданные.Подсистемы.Руководителю.Подсистемы.Продажи, "");

	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;

КонецПроцедуры

// Заполняет описание настроек отчета в коллекции "Настройки".
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
Процедура НастройкиОтчета(Настройки) Экспорт

	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя, Вариант.Представление);
	КонецЦикла;

КонецПроцедуры

// Возвращает набор параметров, которые необходимо сохранять в рассылке отчетов.
// Значения параметров используются при формировании отчета в рассылке.
//
// Возвращаемое значение:
//   Структура - Структура настроек, сохраняемых в рассылке с неинициализированными значениями.
//
Функция НастройкиОтчетаСохраняемыеВРассылке() Экспорт

	КоллекцияНастроек = Новый Структура;
	КоллекцияНастроек.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	КоллекцияНастроек.Вставить("ТипЦен", Справочники.ТипыЦенНоменклатуры.ПустаяСсылка());
	КоллекцияНастроек.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	КоллекцияНастроек.Вставить("ВНаличии", Ложь);
	КоллекцияНастроек.Вставить("ПоказательКод", Ложь);
	КоллекцияНастроек.Вставить("ПоказательАртикул", Ложь);
	КоллекцияНастроек.Вставить("ПоказательОстаток", Ложь);
	КоллекцияНастроек.Вставить("ПоказательЕдиницаИзмерения", Ложь);
	КоллекцияНастроек.Вставить("МакетОформления", Неопределено);
	КоллекцияНастроек.Вставить("НастройкиКомпоновкиДанных", Неопределено);
	КоллекцияНастроек.Вставить("ИспользоватьПредставлениеОстатка1", Ложь);
	КоллекцияНастроек.Вставить("ИспользоватьПредставлениеОстатка2", Ложь);
	КоллекцияНастроек.Вставить("ИспользоватьПредставлениеОстатка3", Ложь);
	КоллекцияНастроек.Вставить("ПредставлениеОстатка1", "");
	КоллекцияНастроек.Вставить("ПредставлениеОстатка2", "");
	КоллекцияНастроек.Вставить("ПредставлениеОстатка3", "");
	КоллекцияНастроек.Вставить("ЗначениеОстаткаОт1", 0);
	КоллекцияНастроек.Вставить("ЗначениеОстаткаОт2", 0);
	КоллекцияНастроек.Вставить("ЗначениеОстаткаДо", 0);

	Возврат КоллекцияНастроек;

КонецФункции

// Возвращает структуру параметров, наличие которых требуется для успешного формирования отчета.
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт

	// Часть параметров компоновки отчета используется так же и в рассылке отчета.
	ПараметрыОтчета = НастройкиОтчетаСохраняемыеВРассылке();

	// Дополним параметрами, влияющими на формирование отчета.
	ПараметрыОтчета.Вставить("Период", Дата(1, 1, 1));
	ПараметрыОтчета.Вставить("РежимРасшифровки", Ложь);
	ПараметрыОтчета.Вставить("ДанныеРасшифровки", Неопределено);
	ПараметрыОтчета.Вставить("СхемаКомпоновкиДанных", Неопределено);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета", "");
	ПараметрыОтчета.Вставить("ВыводитьПодвал", Ложь);
	
	Возврат ПараметрыОтчета;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//   ПараметрыОтчета - Структура - См. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//   Схема - СхемаКомпоновкиДанных - Описание получаемых данных.
//   КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - Связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт

	// Формирование полей отчета

	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
	
	ПоляОтчета = Новый Массив;
	ПоляОтчета.Добавить("Номенклатура");
	ПоляОтчета.Добавить("Цена");
	
	Если ПараметрыОтчета.ПоказательОстаток Тогда
		ПоляОтчета.Вставить(1, "Остаток");
	КонецЕсли;
	
	Если ПараметрыОтчета.ПоказательЕдиницаИзмерения Тогда
		ПоляОтчета.Вставить(1, "ЕдиницаИзмерения");
	КонецЕсли;

	Если ПараметрыОтчета.ПоказательАртикул Тогда
		ПоляОтчета.Вставить(1, "Артикул");
	КонецЕсли;

	Если ПараметрыОтчета.ПоказательКод Тогда
		ПоляОтчета.Вставить(1, "Код");
	КонецЕсли;
	
	Для Каждого ПолеОтчета Из ПоляОтчета Цикл
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(КомпоновщикНастроек.Настройки.Выбор, ПолеОтчета);
	КонецЦикла;

	// Установка параметров отчета
	ЕстьСкладскойУчет = БухгалтерскийУчет.ВедетсяУчетПоСкладам(ПланыСчетов.Хозрасчетный.ТоварыНаСкладах);

	ГраницаПериод = Новый Граница(КонецДня(ПараметрыОтчета.Период), ВидГраницы.Включая);

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Период", ГраницаПериод);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ТипЦен", ПараметрыОтчета.ТипЦен);

	// Сортировка по умолчанию по представлению номенклатуры

	КомпоновщикНастроек.Настройки.Порядок.Элементы.Очистить();

	// Отбор по остаткам

	ЭлементыОтборОстаткиУдалить = Новый Массив;
	ЭлементОтбораОстаткиДобавить = ПараметрыОтчета.ВНаличии;

	Для каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл

		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
			И ЭлементОтбора.Представление = "ОстатокОтбор" Тогда

			Если ПараметрыОтчета.ВНаличии Тогда
				ЭлементОтбораОстаткиДобавить = Ложь;
			Иначе
				ЭлементыОтборОстаткиУдалить.Добавить(ЭлементОтбора);
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Субконто = Новый Массив;
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ИспользуетсяНесколькоСкладов = Справочники.Склады.ИспользуетсяНесколькоСкладов();

	Если ИспользуетсяНесколькоСкладов И ЕстьСкладскойУчет Тогда
		Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Субконто", Субконто);

	Если ЭлементОтбораОстаткиДобавить Или ПараметрыОтчета.ПоказательОстаток Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СчетаУчетаТоваров", БухгалтерскиеОтчеты.СчетаУчетаТоваров());
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Организация", ПараметрыОтчета.Организация);
	КонецЕсли;
	
	Если ЭлементОтбораОстаткиДобавить Тогда

		ГруппаОтбора = КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		ГруппаОтбора.Использование = Истина;
		ГруппаОтбора.Представление = "ОстатокОтбор";
		ГруппаОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Остаток");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
		ЭлементОтбора.ПравоеЗначение = 0;
		ЭлементОтбора.Использование  = Истина;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;

		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Услуга");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Истина;
		ЭлементОтбора.Использование  = Истина;

		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;

	КонецЕсли;
	
	УсловноеОформление(КомпоновщикНастроек.Настройки.УсловноеОформление, "НетОстатков", ПараметрыОтчета);
	УсловноеОформление(КомпоновщикНастроек.Настройки.УсловноеОформление, "ОстаткиВДиапазоне", ПараметрыОтчета);
	УсловноеОформление(КомпоновщикНастроек.Настройки.УсловноеОформление, "ОстатковДостаточно", ПараметрыОтчета);
	
	Для каждого ЭлементОтбораОстаткиУдалить Из ЭлементыОтборОстаткиУдалить Цикл
		КомпоновщикНастроек.Настройки.Отбор.Элементы.Удалить(ЭлементОтбораОстаткиУдалить); 
	КонецЦикла;

КонецПроцедуры

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПриВыводеЗаголовка.
//
// Параметры:
//   ПараметрыОтчета - Структура - См. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//   КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - описание связи настроек и схемы отчета.
//   Результат - ТабличныйДокумент - табличный документ с сформированным отчетом.
//
Процедура ПриВыводеЗаголовка(ПараметрыОтчета, КомпоновщикНастроек, Результат) Экспорт 

	// Данные заголовка отчета

	ДанныеОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ПараметрыОтчета.Организация, ПараметрыОтчета.Период);
	ФайлЛоготип = Справочники.Организации.ДвоичныеДанныеКартинкиОрганизации(ПараметрыОтчета.Организация, "ФайлЛоготип");

	ПредставлениеОрганизация = ?(ПустаяСтрока(ДанныеОрганизации.НаименованиеДляПечатныхФорм), "",
		СтрШаблон(НСтр("ru = 'Организация: %1'"), ДанныеОрганизации.НаименованиеДляПечатныхФорм));

	ПредставлениеАдрес = ?(ПустаяСтрока(ДанныеОрганизации.ЮридическийАдрес), "",
		СтрШаблон(НСтр("ru = 'Адрес: %1'"), ДанныеОрганизации.ЮридическийАдрес));

	ПредставлениеТелефон = ?(ПустаяСтрока(ДанныеОрганизации.Телефоны), "",
		СтрШаблон(НСтр("ru = 'Телефон: %1'"), ДанныеОрганизации.Телефоны));

	ПредставлениеEmail = ?(ПустаяСтрока(ДанныеОрганизации.Email), "",
		СтрШаблон(НСтр("ru = 'Email: %1'"), ДанныеОрганизации.Email));
	
	ДанныеЗаголовка = Новый Структура;
	ДанныеЗаголовка.Вставить("ПредставлениеОрганизация", ПредставлениеОрганизация);
	ДанныеЗаголовка.Вставить("ПредставлениеАдрес", ПредставлениеАдрес);
	ДанныеЗаголовка.Вставить("ПредставлениеТелефон", ПредставлениеТелефон);
	ДанныеЗаголовка.Вставить("ПредставлениеEmail", ПредставлениеEmail);

	// Формирование заголовка отчета

	Макет = Отчеты.ПрайсЛист.ПолучитьМакет("ЗаголовокПрайсЛиста");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.Заголовок = ПолучитьТекстЗаголовка(ПараметрыОтчета);
	Результат.Вывести(ОбластьЗаголовок);

	Если ЗначениеЗаполнено(ФайлЛоготип) Тогда

		ДанныеОрганизацииСЛоготипом = Макет.ПолучитьОбласть("ДанныеОрганизацииСЛоготипом");  
		ДанныеОрганизацииСЛоготипом.Область("КартинкаЛоготип").Картинка = Новый Картинка(ФайлЛоготип);
		ДанныеОрганизацииСЛоготипом.Параметры.Заполнить(ДанныеЗаголовка);
		Результат.Вывести(ДанныеОрганизацииСЛоготипом);

	Иначе

		ДанныеОрганизацииБезЛоготипа = Макет.ПолучитьОбласть("ДанныеОрганизацииБезЛоготипа|Строка");

		Для каждого ТекущиеДанныеЗаголовка Из ДанныеЗаголовка Цикл

			Если Не ПустаяСтрока(ТекущиеДанныеЗаголовка.Значение) Тогда

				ДанныеОрганизацииБезЛоготипа.Параметры.ДанныеСтроки = ТекущиеДанныеЗаголовка.Значение;
				Результат.Вывести(ДанныеОрганизацииБезЛоготипа);

			КонецЕсли;

		КонецЦикла;

		Результат.Вывести(Макет.ПолучитьОбласть("ДанныеОрганизацииБезЛоготипа|Отступ"));

	КонецЕсли;

КонецПроцедуры

// Обработка макета компоновки после его компоновки, но до вывода в отчет.
//
// Параметры:
//   ПараметрыОтчета - Структура - См. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//   МакетКомпоновки - МакетКомпоновкиДанных - описание выводимых данных.
//
Процедура ПослеКомпоновкиМакета(ПараметрыОтчета, МакетКомпоновки) Экспорт

	// Удалим строку заголовка иерархии номенклатуры 

	МакетШапкиОтчета = БухгалтерскиеОтчеты.ПолучитьМакетШапки(МакетКомпоновки);
	
	МакетШапкиОтчета.Макет.Удалить(МакетШапкиОтчета.Макет[0]);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВариантыНастроек()

	ПредставлениеОтчета = ПредставлениеОтчетаПрайсЛист();

	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("Имя, Представление", ПредставлениеОтчета.Имя, ПредставлениеОтчета.Представление));

	Возврат Массив;

КонецФункции

Функция ПредставлениеОтчетаПрайсЛист()

	ПредставлениеОтчета = Новый Структура;
	ПредставлениеОтчета.Вставить("Имя", Метаданные.Отчеты.ПрайсЛист.Имя);
	ПредставлениеОтчета.Вставить("Представление", Метаданные.Отчеты.ПрайсЛист.Представление());

	Возврат ПредставлениеОтчета;

КонецФункции

Процедура УсловноеОформление(УсловноеОформление, ГруппаОстатков, ПараметрыОтчета)
	ЭлементОформления = Неопределено;
	ИмяПоля = "Остаток";
	Для Каждого Элемент Из УсловноеОформление.Элементы Цикл
		
		Если Элемент.Представление = ГруппаОстатков Тогда
			ЭлементОформления = Элемент;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭлементОформления = Неопределено Тогда
		ЭлементОформления = УсловноеОформление.Элементы.Вставить(1);
		ЭлементОформления.Представление = ГруппаОстатков;
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Использование = Истина;
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
		ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	КонецЕсли;
	
	Если ГруппаОстатков = "НетОстатков" Тогда
		
		Использование = ПараметрыОтчета.ПоказательОстаток 
			И ПараметрыОтчета.ИспользоватьПредставлениеОстатка1;
		
		ЭлементОформления.Использование = Использование;
		
		ГруппаОтбора = НовыйГруппаОтбора(ЭлементОформления, Использование);
		
		ЭлементОтбора = НовыйЭлементОтбораОстаток(ГруппаОтбора, ПараметрыОтчета, Использование);
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
		ЭлементОтбора.ПравоеЗначение = 0;
		
		ИсключениеИзОтбораУслуг(ГруппаОтбора, Использование);
		
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ПараметрыОтчета.ПредставлениеОстатка1);

	ИначеЕсли ГруппаОстатков = "ОстаткиВДиапазоне" Тогда
		
		Использование = ПараметрыОтчета.ПоказательОстаток 
			И ПараметрыОтчета.ИспользоватьПредставлениеОстатка2;
			
		ЭлементОформления.Использование = Использование;
		
		ГруппаОтбора = НовыйГруппаОтбора(ЭлементОформления, Использование);
		
		ЭлементОтбора = НовыйЭлементОтбораОстаток(ГруппаОтбора, ПараметрыОтчета, Использование);
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
		ЭлементОтбора.ПравоеЗначение = ПараметрыОтчета.ЗначениеОстаткаОт1;
		
		ЭлементОтбора = НовыйЭлементОтбораОстаток(ГруппаОтбора, ПараметрыОтчета, Использование);
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
		ЭлементОтбора.ПравоеЗначение = ПараметрыОтчета.ЗначениеОстаткаДо;
		
		ИсключениеИзОтбораУслуг(ГруппаОтбора, Использование);

		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ПараметрыОтчета.ПредставлениеОстатка2);

	ИначеЕсли ГруппаОстатков = "ОстатковДостаточно" Тогда
		
		Использование = ПараметрыОтчета.ПоказательОстаток 
			И ПараметрыОтчета.ИспользоватьПредставлениеОстатка3;
		
		ЭлементОформления.Использование = Использование;
		
		ГруппаОтбора = НовыйГруппаОтбора(ЭлементОформления, Использование);

		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.Использование = Использование;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
		ЭлементОтбора.ПравоеЗначение = ПараметрыОтчета.ЗначениеОстаткаОт2;
		
		ИсключениеИзОтбораУслуг(ГруппаОтбора, Использование);
		
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ПараметрыОтчета.ПредставлениеОстатка3);
		
	КонецЕсли;
	
КонецПроцедуры

Функция НовыйЭлементОтбораОстаток(ГруппаОтбора, ПараметрыОтчета, Использование)
	
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Использование;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Остаток");

	Возврат ЭлементОтбора;
	
КонецФункции

Процедура ИсключениеИзОтбораУслуг(ГруппаОтбора, Использование)
	
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Использование;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Услуга");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Ложь;

КонецПроцедуры

Функция НовыйГруппаОтбора(ЭлементОформления, Использование)
	
	ГруппаОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.Использование = Использование;
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;

	Возврат ГруппаОтбора;
	
КонецФункции

#КонецОбласти

#КонецЕсли