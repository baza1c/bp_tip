#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//   Структура   - флаги, задающие необходимость дополнительных действий.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета",          Истина);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",           Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",               Истина);
	Результат.Вставить("ИспользоватьРасширенныеПараметрыРасшифровки", Истина);
	Результат.Вставить("ИспользоватьВременныеТаблицыЗапроса",         Истина);
	
	Результат.Вставить("КонтролироватьОтрицательныеОстатки",         Истина);
	
	Возврат Результат;
	
КонецФункции

// Формирует текст, выводимый в заголовке отчета.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  ОрганизацияВНачале - Булево - флаг, используемый для вывода представления организации в начале текста,
//                                если организацию нужно выводить в тексте заголовка.
//
// Возвращаемое значение:
//   Строка      - текст заголовка с учётом периода.
//
Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт 
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Анализ продаж%1'"),
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода));
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Схема        - СхемаКомпоновкиДанных - описание получаемых данных.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
	
	#Область УстановкаПараметровСхемыКомпоновки
	
	ПредставлениеПериода = СтрЗаменить(
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода),
		"за",
		"");
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"ПредставлениеПериода",
		СОкрЛП(ПредставлениеПериода));
		
	// Периодичность.
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"Периодичность",
		ПараметрыОтчета.Периодичность);
		
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"РаботыИУслуги", ОтчетыРуководителя.РаботыИУслуги());
	
	СхемаЭталон = ПолучитьМакет("СхемаКомпоновкиДанных");
	
	ПериодичностьОтчета = Новый Соответствие;
	ПериодичностьОтчета.Вставить(6, "ДЕНЬ");
	ПериодичностьОтчета.Вставить(9, "МЕСЯЦ");
	ПериодичностьОтчета.Вставить(10, "КВАРТАЛ");
	ПериодичностьОтчета.Вставить(11, "ПОЛУГОДИЕ");
	ПериодичностьОтчета.Вставить(12, "ГОД");
	
	Периодичность = ПериодичностьОтчета.Получить(ПараметрыОтчета.Периодичность);
	
	// Скорректируем текст запроса для выбранной периодичности.
	Если Периодичность <> Неопределено Тогда
		ТекстЗапроса = СхемаЭталон.НаборыДанных.ВаловаяПрибыль.Запрос;
		Схема.НаборыДанных.ВаловаяПрибыль.Запрос = СтрЗаменить(
			ТекстЗапроса,
			".ПериодДень",
			".Период" + Периодичность);
		
	КонецЕсли;
	
	// Начало периода.
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"НачалоПериода",
			НачалоДня(ПараметрыОтчета.НачалоПериода));
			
	КонецЕсли;
	
	// Конец периода.
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"КонецПериода",
			КонецДня(ПараметрыОтчета.КонецПериода));
		
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ПараметрПериод",
			КонецДня(ПараметрыОтчета.КонецПериода));
			
	Иначе
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПараметрПериод", КонецДня(ТекущаяДата()));
	КонецЕсли;
	
	// Если отключен суммовой учет по складам на счетах учета товаров,
	// то расчет себестоимости выполняется без детализации по складам.
	// Проверяем 41 счет: если на нем включен суммовой учет по складам, 
	// то считаем, что на всех остальных товарных счетах также включен.
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"ВедетсяСуммовойУчетПоСкладам",
		БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары));
	
	#КонецОбласти
	
	#Область ФормированиеСтруктурыОтчета
	
	ВыводитьДиаграмму = Неопределено;
	
	Если НЕ ПараметрыОтчета.Свойство("ВыводитьДиаграмму", ВыводитьДиаграмму) Тогда
		
		ВыводитьДиаграмму = Истина;
		
	КонецЕсли;
	
	Диаграмма = Неопределено;
	Для Каждого ЭлементСтруктуры Из КомпоновщикНастроек.Настройки.Структура Цикл
		Если ЭлементСтруктуры.Имя = "Диаграмма" Тогда
			Диаграмма = ЭлементСтруктуры;
		КонецЕсли;
	КонецЦикла;
	
	// Настройка диаграммы.
	Если Диаграмма <> Неопределено Тогда
		
		Если ВыводитьДиаграмму Тогда
			
			// Группировка.
			Диаграмма.Использование = Ложь;
			Диаграмма.Точки.Очистить();
			Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл
				Если ПолеВыбраннойГруппировки.Использование Тогда
					Группировка = Диаграмма.Точки.Добавить();
					
					БухгалтерскиеОтчетыВызовСервера.ЗаполнитьГруппировку(ПолеВыбраннойГруппировки, Группировка);
					
					Группировка.Выбор.Элементы.Очистить();
					ЭлементВыбора = Группировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
					ЭлементВыбора.Поле = Новый ПолеКомпоновкиДанных(ПолеВыбраннойГруппировки.Поле);
					
					Диаграмма.Использование = Истина;
					
					// Диаграмма группируется по самой первой группировке.
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			
			Диаграмма.Использование = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Группировка
	БухгалтерскиеОтчетыВызовСервера.ДобавитьГруппировки(ПараметрыОтчета, КомпоновщикНастроек);
	
	#КонецОбласти
	
	// Дополнительные данные.
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	// Ресурс Валовая прибыль есть в отчете всегда.
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(КомпоновщикНастроек, "ВаловаяПрибыльБезНДС");
	
	// Отбор по организации.
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);

	// Отбор по количеству и выручке
	ОтборПоКоличествуИВыручке(КомпоновщикНастроек);
	
КонецПроцедуры

// Возвращает менеджер временных таблиц
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
// Возвращаемое значение:
//   МенеджерВременныхТаблиц - Менеджер временных таблиц.
//
Функция МенеджерВременныхТаблицОтчета(ПараметрыОтчета, КомпоновщикНастроек) Экспорт
	
	Запрос = ОтчетыРуководителя.ВыручкаИСебестоимость(ПараметрыОтчета);
	Возврат Запрос.МенеджерВременныхТаблиц;
	 
КонецФункции

// В процедуре можно изменить табличный документ после вывода в него данных.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Результат    - ТабличныйДокумент - сформированный отчет.
//
Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	Для Каждого Рисунок Из Результат.Рисунки Цикл
		Если ТипЗнч(Рисунок.Объект) = Тип("Диаграмма") Тогда
			Рисунок.Объект.ОбластьПостроения.ШкалаТочек.ОриентацияПодписей = ОриентацияПодписейДиаграммы.Горизонтально;
			Рисунок.Объект.ПоложениеПодписей = ПоложениеПодписейКДиаграмме.КрайАвто;
			Рисунок.Объект.ОбластьПостроения.ШкалаЗначений.ПоложениеПодписейШкалы = ПоложениеПодписейШкалыДиаграммы.Нет;
		КонецЕсли;
	КонецЦикла;
	
	Результат.ФиксацияСлева = 0;
	БыстрыеНастройкиОтчетовСервер.ВывестиПримечанияОкругления(ПараметрыОтчета, Результат);

КонецПроцедуры

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//   НастройкиОтчета - СтрокаДереваЗначений - Настройки этого отчета,
//       уже сформированные при помощи функции ВариантыОтчетов.ОписаниеОтчета() и готовые к изменению.
//
Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ВаловаяПрибыль");
	
	ОписаниеВарианта.Размещение.Вставить(Метаданные.Подсистемы.Руководителю.Подсистемы.Продажи, "");
	
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
КонецПроцедуры

// Заполняет описание настроек отчета в коллекции Настройки
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя, Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет параметры расшифровки ячейки отчета.
//
// Параметры:
//	Адрес - Строка - Адрес временного хранилища с данными расшифровки отчета.
//	Расшифровка - Произвольный - Значения полей расшифровки.
//	ПараметрыРасшифровки - Структура - Коллеккция параметров расшифроки, которую требуется заполнить. 
//		Подробнее см. БухгалтерскиеОтчетыВызовСервера.ПолучитьМассивПолейРасшифровки()
//
Процедура ЗаполнитьПараметрыРасшифровкиОтчета(Адрес, Расшифровка, ПараметрыРасшифровки) Экспорт
	
	// Инициализируем список пунктов меню
	СписокПунктовМеню = Новый СписокЗначений();
	
	// Заполниим соответствие полей которые мы хотим получить из данных расшифровки
	СоответствиеПолей = Новый Соответствие;
	ДанныеОтчета = ПолучитьИзВременногоХранилища(Адрес);
	
	// Укажем, что открывать объект сразу не нужно
	ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Ложь);
	
	Если ДанныеОтчета <> Неопределено Тогда
		ЗначениеРасшифровки = ДанныеОтчета.ДанныеРасшифровки.Элементы[Расшифровка];
		Если ТипЗнч(ЗначениеРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
			Для Каждого ПолеРасшифровки Из ЗначениеРасшифровки.ПолучитьПоля() Цикл
				Если ЗначениеЗаполнено(ПолеРасшифровки.Значение) Тогда
					ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
					ПараметрыРасшифровки.Вставить("Значение", ПолеРасшифровки.Значение);
					Возврат;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
		Возврат;
	КонецЕсли;
	
	// Прежде всего интересны данные группировочных полей
	Для Каждого Группировка Из ДанныеОтчета.Объект.Группировка Цикл
		
		Если Группировка.Использование Тогда
			
			СоответствиеПолей.Вставить(Группировка.Поле);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СоответствиеПолей.Вставить("Период");
		
	// Инициализация пользовательских настроек
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("РежимРасшифровки",          Истина);
	ДополнительныеСвойства.Вставить("Организация",               ДанныеОтчета.Объект.Организация);
	ДополнительныеСвойства.Вставить("НачалоПериода",             ДанныеОтчета.Объект.НачалоПериода);
	ДополнительныеСвойства.Вставить("КонецПериода",              ДанныеОтчета.Объект.КонецПериода);
	ДополнительныеСвойства.Вставить("ВыводитьЗаголовок",         ДанныеОтчета.Объект.ВыводитьЗаголовок);
	ДополнительныеСвойства.Вставить("ВыводитьПодвал",            ДанныеОтчета.Объект.ВыводитьПодвал);
	ДополнительныеСвойства.Вставить("ОкруглениеСумм",            ДанныеОтчета.Объект.ОкруглениеСумм);
	ДополнительныеСвойства.Вставить("МакетОформления",           ДанныеОтчета.Объект.МакетОформления);
	ДополнительныеСвойства.Вставить("Периодичность",             ДанныеОтчета.Объект.Периодичность);
	ДополнительныеСвойства.Вставить("ВыводитьДиаграмму",         Ложь);
	ДополнительныеСвойства.Вставить("КлючТекущегоВарианта",      ДанныеОтчета.Объект.КлючТекущегоВарианта);
	ДополнительныеСвойства.Вставить("ОчищатьТаблицуГруппировок", Истина);
	ДополнительныеСвойства.Вставить("ОчищатьДополнительныеПоля", Истина);
	
	ОтборПоЗначениямРасшифровки = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ОтборПоЗначениямРасшифровки.ИдентификаторПользовательскойНастройки = "Отбор";
	
	// Получаем структуру полей доступных в расшифровке
	Данные_Расшифровки = БухгалтерскиеОтчеты.ПолучитьДанныеРасшифровки(ДанныеОтчета.ДанныеРасшифровки, СоответствиеПолей, Расшифровка);
	
	// Если в отчете уже есть регистратор, то дальше не расшифровываем, а открываем документ.
	Регистратор = Данные_Расшифровки.Получить("Регистратор");

	Если Регистратор <> Неопределено Тогда
		ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
		ПараметрыРасшифровки.Вставить("Значение", Регистратор);
		Возврат;
	КонецЕсли;
	
	// Расшифрвку розничного покупателя или розничной продажи преобразовываем в отбор по полю - "Розница".
	Договор = Данные_Расшифровки.Получить("Договор");
	
	Контрагент = Данные_Расшифровки.Получить("Контрагент");
	Если Контрагент <> Неопределено И Контрагент = "Розничные покупатели" Тогда
		
		Данные_Расшифровки.Вставить("Розница", Истина);
		Данные_Расшифровки.Удалить("Контрагент");
		
	КонецЕсли;
	
	Если Договор <> Неопределено И Договор <> "Розничная продажа" Тогда
		
		ДополнительныеСвойства.Вставить("Организация", Договор.Организация);
		
	ИначеЕсли Договор = "Розничная продажа" Тогда
		
		Данные_Расшифровки.Вставить("Розница", Истина);
		Данные_Расшифровки.Удалить("Договор");
		
	КонецЕсли;
	
	// Периодичность преобразовываем в другой период.
	Период = Данные_Расшифровки.Получить("Период");
	
	Если Период <> Неопределено Тогда
		
		Периодичность = БухгалтерскиеОтчетыКлиентСервер.ПолучитьЗначениеПериодичности(ДанныеОтчета.Объект.Периодичность, ДанныеОтчета.Объект.НачалоПериода, ДанныеОтчета.Объект.КонецПериода);
		ДополнительныеСвойства.Вставить("Периодичность", Периодичность);
		ДополнительныеСвойства.Вставить("КонецПериода",  КонецДня(БухгалтерскиеОтчетыКлиентСервер.КонецПериода(Период, Периодичность)));
		ДополнительныеСвойства.Вставить("НачалоПериода", НачалоДня(БухгалтерскиеОтчетыКлиентСервер.НачалоПериода(Период, Периодичность)));

	КонецЕсли;
	
	// Формируем отборы нового отчета.
	Для Каждого ЗначениеРасшифровки Из Данные_Расшифровки Цикл
		Если ЗначениеРасшифровки.Ключ <> "Период" Тогда
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ОтборПоЗначениямРасшифровки, ЗначениеРасшифровки.Ключ, ЗначениеРасшифровки.Значение);
		КонецЕсли;
	КонецЦикла;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.ЗагрузитьНастройки(ДанныеОтчета.ДанныеРасшифровки.Настройки);
	КомпоновщикНастроек.Инициализировать(
		Новый ИсточникДоступныхНастроекКомпоновкиДанных(ДанныеОтчета.Объект.СхемаКомпоновкиДанных));
	
	МассивПолей = БухгалтерскиеОтчетыВызовСервера.ПолучитьМассивПолейРасшифровки(
		Расшифровка, ДанныеОтчета.ДанныеРасшифровки, КомпоновщикНастроек, Истина);
	
	Для Каждого Отбор Из МассивПолей Цикл
		Если ТипЗнч(Отбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
			И Отбор.Представление = "###ОтборПоОрганизацииСОП###" Тогда
			Для Каждого ЭлементОтбора Из Отбор.Элементы Цикл
				Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
					ДополнительныеСвойства.Вставить("Организация", ЭлементОтбора.ПравоеЗначение);
					ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения", Истина);
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТипЗнч(Отбор) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если Отбор.Представление = "###ОтборПоОрганизации###" Тогда
				ДополнительныеСвойства.Вставить("Организация", Отбор.ПравоеЗначение);
				ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения", Ложь);
			ИначеЕсли Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") 
				И Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
				ДополнительныеСвойства.Вставить("Подразделение", Отбор.ПравоеЗначение);
			Иначе
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
					ОтборПоЗначениямРасшифровки, Отбор.ЛевоеЗначение, Отбор.ПравоеЗначение, Отбор.ВидСравнения);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Формируем группировки нового отчета.
	Группировка = Новый Массив();
	ЕстьГруппировкаПоДокументу = Ложь;
	Для Каждого СтрокаГруппировки Из ДанныеОтчета.Объект.Группировка Цикл
		Если СтрокаГруппировки.Использование Тогда
			
			СтрокаДляРасшифровки = Новый Структура("Использование, Поле, Представление, ТипГруппировки");
			ЗаполнитьЗначенияСвойств(СтрокаДляРасшифровки, СтрокаГруппировки);
			Группировка.Добавить(СтрокаДляРасшифровки);
			
			Если СтрокаГруппировки.Поле = "Регистратор" Тогда
				ЕстьГруппировкаПоДокументу = Истина;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЕстьГруппировкаПоДокументу Тогда
		
		СтрокаДляРасшифровки = Новый Структура();
		СтрокаДляРасшифровки.Вставить("Использование", 	Истина);
		СтрокаДляРасшифровки.Вставить("Поле", 			"Регистратор");
		СтрокаДляРасшифровки.Вставить("Представление", 	"Документ");
		СтрокаДляРасшифровки.Вставить("ТипГруппировки", 0);
		
		Группировка.Добавить(СтрокаДляРасшифровки);
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("Группировка", Группировка);
	
	// Формируем дополнительные поля.
	ДополнительныеПоля = Новый Массив();
	
	Для Каждого ДополнительноеПоле Из ДанныеОтчета.Объект.ДополнительныеПоля Цикл
		Если ДополнительноеПоле.Использование Тогда
			
			СтрокаДляРасшифровки = Новый Структура("Использование, Поле, Представление");
			ЗаполнитьЗначенияСвойств(СтрокаДляРасшифровки, ДополнительноеПоле);
			ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
			
		КонецЕсли;
	КонецЦикла;

	ДополнительныеСвойства.Вставить("ДополнительныеПоля", ДополнительныеПоля);
	
	СписокПунктовМеню.Добавить("ВаловаяПрибыль", "Анализ продаж");
	
	НастройкиРасшифровки = Новый Структура();
	НастройкиРасшифровки.Вставить("ВаловаяПрибыль", ПользовательскиеНастройки);
	ДанныеОтчета.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	ПоместитьВоВременноеХранилище(ДанныеОтчета, Адрес);
	
	ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
	
КонецПроцедуры

// Возвращает коллекцию вариантов настроек отчета
//
Функция ВариантыНастроек() Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Новый Структура(
		"Имя, Представление",
		"ВаловаяПрибыль",
		НСтр("ru = 'Анализ продаж'")));
	
КонецФункции

// Возвращает набор параметров, которые необходимо сохранять в рассылке отчетов.
// Значения параметров используются при формировании отчета в рассылке.
//
// Возвращаемое значение:
//   Структура - структура настроек, сохраняемых в рассылке с неинициализированными значениями.
//
Функция НастройкиОтчетаСохраняемыеВРассылке() Экспорт
	
	КоллекцияНастроек = Новый Структура;
	КоллекцияНастроек.Вставить("Организация"                      , Справочники.Организации.ПустаяСсылка());
	КоллекцияНастроек.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	КоллекцияНастроек.Вставить("Периодичность"                    , 0);
	КоллекцияНастроек.Вставить("РазмещениеДополнительныхПолей"    , 0);
	КоллекцияНастроек.Вставить("Группировка"                      , Неопределено);
	КоллекцияНастроек.Вставить("ДополнительныеПоля"               , Неопределено);
	КоллекцияНастроек.Вставить("ВыводитьДиаграмму"                , Ложь);
	КоллекцияНастроек.Вставить("ВыводитьЗаголовок"                , Ложь);
	КоллекцияНастроек.Вставить("ВыводитьПодвал"                   , Ложь);
	КоллекцияНастроек.Вставить("МакетОформления"                  , Неопределено);
	КоллекцияНастроек.Вставить("НастройкиКомпоновкиДанных"        , Неопределено);
	КоллекцияНастроек.Вставить("ВыводитьПримечанияОкругления"     , Ложь);
	
	Возврат КоллекцияНастроек;
	
КонецФункции

// Возвращает структуру параметров, наличие которых требуется для успешного формирования отчета.
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Общая структура настроек.
	ПараметрыОтчета = БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.ИдентификаторОтчета = "ВаловаяПрибыль";
	
	ОтчетОбъект = Отчеты.ВаловаяПрибыль.Создать();
	ПараметрыОтчета.Вставить("Группировка", ОтчетОбъект.Группировка.ВыгрузитьКолонки());
	ПараметрыОтчета.Вставить("РазмещениеДополнительныхПолей",
		БухгалтерскиеОтчетыКлиентСервер.РазмещениеДополнительныхПолей().ВместеСВладельцем);
	ПараметрыОтчета.Вставить("ДополнительныеПоля", ОтчетОбъект.ДополнительныеПоля.ВыгрузитьКолонки());
	
	ПараметрыОтчета.Вставить("Периодичность",
		БухгалтерскиеОтчетыКлиентСервер.Периодичность().Период); // без детализации по периодам
	ПараметрыОтчета.Вставить("КлючТекущегоВарианта", "");
	ПараметрыОтчета.Вставить("ВыводитьДиаграмму", Ложь);
	ПараметрыОтчета.Вставить("ВыводитьПримечанияОкругления", Ложь);
	ПараметрыОтчета.Вставить("ОкруглениеСумм", "");
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Заполняет структуру настроек универсального формата из реквизитов формы.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПустыеПараметрыКомпоновкиОтчета()
//  Форма - УправляемаяФорма - содержит основновной реквизит Отчет .
//
Процедура ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма) Экспорт
	
	БухгалтерскиеОтчеты.ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма);
	
	Отчет = Форма.Отчет;

	ПараметрыОтчета.Периодичность     = Отчет.Периодичность;
	ПараметрыОтчета.ВыводитьДиаграмму = Форма.ВыводитьДиаграмму;
	ПараметрыОтчета.ОкруглениеСумм    = Форма.ОкруглениеСумм;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтборПоКоличествуИВыручке(КомпоновщикНастроек)
	
	Для Каждого Группировка Из КомпоновщикНастроек.Настройки.Структура Цикл
		
		Если ТипЗнч(Группировка) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
				Группировка.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
			
			ДобавитьОтборПоКоличествуИВыручке(ГруппаОтбора);
				
			ПараметрВыводитьОтбор = Группировка.ПараметрыВывода.Элементы.Найти("ВыводитьОтбор");
			ПараметрВыводитьОтбор.Использование = Истина;
			ПараметрВыводитьОтбор.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить;
		ИначеЕсли ТипЗнч(Группировка) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			Для Каждого Точка Из Группировка.Точки Цикл
				ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
					Точка.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
				ДобавитьОтборПоКоличествуИВыручке(ГруппаОтбора);
			КонецЦикла;
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОтборПоКоличествуИВыручке(ГруппаОтбора)
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных("Количество");
	НовыйЭлемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлемент.Использование  = Истина;
	НовыйЭлемент.ЛевоеЗначение  = ПолеОтбора;
	НовыйЭлемент.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	НовыйЭлемент.ПравоеЗначение = 0;
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных("ВыручкаБезНДС");
	НовыйЭлемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлемент.Использование  = Истина;
	НовыйЭлемент.ЛевоеЗначение  = ПолеОтбора;
	НовыйЭлемент.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	НовыйЭлемент.ПравоеЗначение = 0;
	
	ПолеОтбора = Новый ПолеКомпоновкиДанных("Стоимость");
	НовыйЭлемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлемент.Использование  = Истина;
	НовыйЭлемент.ЛевоеЗначение  = ПолеОтбора;
	НовыйЭлемент.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	НовыйЭлемент.ПравоеЗначение = 0;

КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

// Обработчик обновления на версию 3.0.76.5
// Включает вывод показателя "Количество" в отчете для всех пользователей в текущих пользовательских настройках отчета.
// Не обрабатывает пользовательские настройки, явно сохраненные пользователем.
//
Процедура ВключитьПоказательКоличество() Экспорт
	
	ВариантыОтчета = ВариантыНастроек();
	
	ОтборНастроек = Новый Структура("КлючОбъекта");
	
	Для Каждого ВариантОтчета Из ВариантыОтчета Цикл
		
		ОтборНастроек.КлючОбъекта = СтрШаблон("Отчет.ВаловаяПрибыль/%1/ТекущиеПользовательскиеНастройки", ВариантОтчета.Имя);
		
		ВыборкаНастроек = ХранилищеСистемныхНастроек.Выбрать(ОтборНастроек);
		
		Пока ВыборкаНастроек.Следующий() Цикл
			
			ПользовательскиеНастройки = ВыборкаНастроек.Настройки;
			
			Если ТипЗнч(ПользовательскиеНастройки) <> Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ДанныеОтчета")
				Или Не ТипЗнч(ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеОтчета) = Тип("ХранилищеЗначения") Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеОтчета = ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеОтчета.Получить();
			
			Если ТипЗнч(ДанныеОтчета) <> Тип("Структура") Или Не ДанныеОтчета.Свойство("ДополнительныеПоля") Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаКоличество = ДанныеОтчета.ДополнительныеПоля.Найти("Количество", "Поле");
			Если СтрокаКоличество = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаКоличество.Использование = Истина;
			
			// Перемещаем колонку "Количество" на первое место в порядке выводимых полей.
			ИндекСтрокиКоличество = ДанныеОтчета.ДополнительныеПоля.Индекс(СтрокаКоличество);
			ДанныеОтчета.ДополнительныеПоля.Сдвинуть(ИндекСтрокиКоличество, -ИндекСтрокиКоличество);
			
			ПользовательскиеНастройки.ДополнительныеСвойства.ДанныеОтчета = Новый ХранилищеЗначения(ДанныеОтчета);
			
			ХранилищеСистемныхНастроек.Сохранить(
				ВыборкаНастроек.КлючОбъекта, ,
				ПользовательскиеНастройки, ,
				ВыборкаНастроек.Пользователь);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли