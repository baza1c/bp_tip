#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Процедура используется подсистемой варианты отчетов
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя,Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийУчет, "");
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийИНалоговыйУчет, "");
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.БухгалтерияПредприятияПодсистемы.Подсистемы.ПростойИнтерфейс.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты, "");
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантыНастроек() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление","РасчетНалогаУСН", "Расчет налога УСН"));
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета",             Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета",              Ложь);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",              Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",                  Ложь);
	Результат.Вставить("ИспользоватьПриВыводеЗаголовка",                 Истина);
	Результат.Вставить("ИспользоватьФиксированныйМакетЗаголовкаИПодвала", Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	НаборПоказателей.Добавить("БУ");
	НаборПоказателей.Добавить("НУ");
	НаборПоказателей.Добавить("ПР");
	НаборПоказателей.Добавить("ВР");
	
	Возврат НаборПоказателей;
	
КонецФункции

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт
	
	НачалоПериода = НачалоГода(ПараметрыОтчета.НачалоПериода);
	КонецПериода  = КонецКвартала(ПараметрыОтчета.КонецПериода);
	
	ЧастиЗаголовка = Новый Массив;
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		ЧастиЗаголовка.Добавить(
			НСтр("ru = 'Расчет торгового сбора, уменьшающего налог УСН за'"));
	Иначе
		ЭтоКонецГода = КонецПериода = КонецГода(КонецПериода);
		
		ЧастиЗаголовка.Добавить(НСтр("ru = 'Справка-расчет'"));
		ЧастиЗаголовка.Добавить(?(ЭтоКонецГода, НСтр("ru = 'налога УСН'"), НСтр("ru = 'авансового платежа по налогу УСН'")));
		ЧастиЗаголовка.Добавить(НСтр("ru = 'за'"));
	КонецЕсли;
	
	ЧастиЗаголовка.Добавить(
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода, Истина));
	
	ТекстЗаголовка = СтрСоединить(ЧастиЗаголовка, " ");
	
	Если ПараметрыОтчета.НалоговыйПериодРасширен Тогда
		СправкиРасчеты.ДополнитьСловоСсылкойНаПримечание(ТекстЗаголовка);
	КонецЕсли;
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		СправкиРасчеты.ДополнитьСловоСсылкойНаПримечание(ТекстЗаголовка);
	КонецЕсли;
	
	Возврат ТекстЗаголовка;
	
КонецФункции

Процедура ПриВыводеЗаголовка(ПараметрыОтчета, КомпоновщикНастроек, Результат) Экспорт
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	ОбластьЗаголовок        = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьОрганизация      = Макет.ПолучитьОбласть("Организация");
	
	//Организация
	ТекстОрганизация = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(ПараметрыОтчета.Организация, , ПараметрыОтчета.КонецПериода);
	ОбластьОрганизация.Параметры.НазваниеОрганизации = ТекстОрганизация;
	Результат.Вывести(ОбластьОрганизация);
	
	//Заголовок
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = "" + ПолучитьТекстЗаголовка(ПараметрыОтчета);
	Результат.Вывести(ОбластьЗаголовок);
	
	Результат.Область("R1:R" + Результат.ВысотаТаблицы).Имя = "Заголовок";
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		
		// Раздвинем заголовок
		Результат.Область("R1:R" + Результат.ВысотаТаблицы).ШиринаКолонки = 140;
		
		// Добавим отступ таблицы
		ОбластьЗаголовок.Параметры.ЗаголовокОтчета = "";
		Результат.Вывести(ОбластьЗаголовок);
		
	КонецЕсли;
	
КонецПроцедуры

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоКвартала(ПараметрыОтчета.НачалоПериода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецКвартала(ПараметрыОтчета.КонецПериода));
		
		ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоГода(ПараметрыОтчета.КонецПериода), ПараметрыОтчета.КонецПериода);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПредставлениеПериода", ПредставлениеПериода);
	КонецЕсли;
	
	ПредставлениеПериодаПревышения = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		НачалоГода(ПараметрыОтчета.ПериодПревышенияЛимитов),
		НачалоКвартала(ПараметрыОтчета.ПериодПревышенияЛимитов) - 1,
		Истина);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"НалоговаяБазаДоПревышенияПредставление",
		СтрШаблон(НСтр("ru = '3.1. Налоговая база за %1'"), ПредставлениеПериодаПревышения));
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"НалогДоПревышенияПредставление",
		СтрШаблон(НСтр("ru = '3.2. Налог за %1'"), ПредставлениеПериодаПревышения));
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПустаяДата", Дата(1, 1, 1));
	
	НачалоНалоговогоПериода = ИнтерфейсыВзаимодействияБРО.НачалоНалоговогоПериода(
		ПараметрыОтчета.Организация,
		ПараметрыОтчета.КонецПериода,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВДекабре, , ,
		Справочники.Организации.ДатаРегистрацииОрганизации(ПараметрыОтчета.Организация));
	
	КонецПервогоОтчетногоПериода =
		КонецКвартала(Макс(НачалоГода(ПараметрыОтчета.НачалоПериода), НачалоНалоговогоПериода));
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"КонецПервогоОтчетногоПериода", КонецПервогоОтчетногоПериода);
	
	ПовышеннаяСтавкаСПервогоОтчетногоПериода =
		(КонецКвартала(ПараметрыОтчета.ПериодПревышенияЛимитов) = КонецПервогоОтчетногоПериода);
	
	УпрощенныйЗачетВзносов = УчетСтраховыхВзносовИППовтИсп.УпрощенныйЗачетФиксированныхВзносов(
		ПараметрыОтчета.Организация,
		ПараметрыОтчета.КонецПериода)
		И (ПараметрыОтчета.КонецПериода > УчетСтраховыхВзносовИП.ДатаПереходаНаУпрощенныйЗачетВзносов());
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"УпрощенныйЗачетВзносов",
		УпрощенныйЗачетВзносов);
	
	// Заполняем параметры с примечаниями.
	НомерПримечания = 0;
	Если ПараметрыОтчета.НалоговыйПериодРасширен Тогда
		НомерПримечания = НомерПримечания + 1;
	КонецЕсли;
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		НомерПримечания = НомерПримечания + 1;
	КонецЕсли;
	
	// Заголовок поля "Налоговая ставка"
	НалоговаяСтавкаЗаголовок = НСтр("ru = '2. Налоговая ставка '");
	Если ПараметрыОтчета.ПрименяетсяПовышеннаяСтавкаНалога Тогда
		НомерПримечания = НомерПримечания + 1;
		СправкиРасчеты.ДополнитьСловоСсылкойНаПримечание(НалоговаяСтавкаЗаголовок, НомерПримечания);
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "НалоговаяСтавкаЗаголовок", НалоговаяСтавкаЗаголовок);
	
	// Заголовок таблицы "Расчет торгового сбора".
	ВычитаемыйТорговыйСборЗаголовок = СтрШаблон(
		НСтр("ru = 'Приложение. Расчет суммы торгового сбора, уменьшающей налог%1'"), ПредставлениеПериода);
	
	НомерПримечания = НомерПримечания + 1;
	
	СправкиРасчеты.ДополнитьСловоСсылкойНаПримечание(ВычитаемыйТорговыйСборЗаголовок, НомерПримечания);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "ВычитаемыйТорговыйСборЗаголовок", ВычитаемыйТорговыйСборЗаголовок);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
	ПараметрыОтчета.ПоказательНУ = Истина;
	
	ОтображатьТорговыйСбор = ПараметрыОтчета.УменьшатьНалогНаТорговыйСбор;
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетНалогаУСН" Тогда
		
		ОбъектДоходы = (ПараметрыОтчета.ОбъектНалогообложения = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы);
		Если ОбъектДоходы Тогда
			ИмяГруппировки = "Доходы";
		Иначе
			ИмяГруппировки = "ДоходыМинусРасходы";
		КонецЕсли;
		
		Если ОтображатьТорговыйСбор Тогда
			ИмяГруппировки = ИмяГруппировки + "ПлательщикТорговогоСбора";
		КонецЕсли;
		
		Если ПараметрыОтчета.ПрименяетсяПовышеннаяСтавкаНалога И Не ПовышеннаяСтавкаСПервогоОтчетногоПериода Тогда
			ИмяГруппировки = СтрШаблон("%1ПовышеннаяСтавка", ИмяГруппировки);
		КонецЕсли;
		
		Если УпрощенныйЗачетВзносов И ОбъектДоходы Тогда
			ИмяГруппировки = СтрШаблон("%1УпрощенныйЗачетВзносов", ИмяГруппировки);
		КонецЕсли;
		
		ГруппировкаОбъектНалогообложения = БухгалтерскиеОтчеты.НайтиПоИмени(
			КомпоновщикНастроек.Настройки.Структура, СтрШаблон("Объект%1", ИмяГруппировки));
		
		ГруппировкаОбъектНалогообложения.Использование = Истина;
		
	КонецЕсли;
	
	// Для плательщика торгового сбора на УСН-доходы выведем расчет суммы вычитаемого из налога сбора
	Если ОтображатьТорговыйСбор Или ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		
		// Заголовок таблицы
		ГруппировкаВычитаемыйСборЗаголовок = БухгалтерскиеОтчеты.НайтиПоИмени(
			КомпоновщикНастроек.Настройки.Структура, "ВычитаемыйТорговыйСборЗаголовок");
		
		ГруппировкаВычитаемыйСборЗаголовок.Использование = (ПараметрыОтчета.КлючТекущегоВарианта = "РасчетНалогаУСН");
		
		Если ПараметрыОтчета.ПрименяетсяПовышеннаяСтавкаНалога И Не ПовышеннаяСтавкаСПервогоОтчетногоПериода Тогда
			ИмяГруппировки = "ВычитаемыйТорговыйСборПовышеннаяСтавка";
		Иначе
			ИмяГруппировки = "ВычитаемыйТорговыйСбор";
		КонецЕсли;
		
		ГруппировкаВычитаемыйСбор = БухгалтерскиеОтчеты.НайтиПоИмени(КомпоновщикНастроек.Настройки.Структура, ИмяГруппировки);
		
		ГруппировкаВычитаемыйСбор.Использование = Истина;
		
	КонецЕсли;
	
	Если КонецКвартала(ПараметрыОтчета.КонецПериода) = КонецГода(ПараметрыОтчета.КонецПериода) Тогда
		Периодичность = Перечисления.Периодичность.Год;
	Иначе
		Периодичность = Перечисления.Периодичность.Квартал;
	КонецЕсли;
	КоэффициентСпецРежим = 1 - УчетПСН.ДоляДоходовПатентнойСистемыНалогообложения(
		ПараметрыОтчета.Организация,
		ПараметрыОтчета.КонецПериода,
		Периодичность,
		Истина);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ДоляДоходаУСН", КоэффициентСпецРежим);
	ЕдиныйТариф = УчетСтраховыхВзносовИП.ВзносыПоЕдиномуТарифуПодлежащиеУплате(
		ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
	ВзносыСДохода = УчетСтраховыхВзносовИП.ВзносыСДоходовПодлежащиеУплате(
		ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПоЕдиномуТарифуРассчитано", ЕдиныйТариф);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СДоходаЗаПредыдущийГодРассчитано",
		ВзносыСДохода.ЗаПредыдущийГод);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СДоходаЗаТекущийГодРассчитано",
		ВзносыСДохода.ЗаТекущийГод);
	
	УплаченныеСтраховыеВзносы = УчетСтраховыхВзносовИП.УплаченныеСтраховыеВзносыПоСроку(
		ПараметрыОтчета.Организация, НачалоГода(ПараметрыОтчета.НачалоПериода), КонецДня(ПараметрыОтчета.КонецПериода));
	ЗачтеноПоЗаявлениям = УчетСтраховыхВзносовИП.ЗарезервированныеСуммыВзносовЗаНалоговыйПериод(
		ПараметрыОтчета.Организация, НачалоГода(ПараметрыОтчета.НачалоПериода), КонецДня(ПараметрыОтчета.КонецПериода));
	УплаченныеСтраховыеВзносы = Макс(0, УплаченныеСтраховыеВзносы - ЗачтеноПоЗаявлениям);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ОплаченныеРасходыУказанные", УплаченныеСтраховыеВзносы);
	
	ОтражатьИспользованныеВзносы = УчетСтраховыхВзносовИП.ОтражатьИспользованныеВзносы(
		ПараметрыОтчета.Организация,
		НачалоГода(ПараметрыОтчета.НачалоПериода),
		ПараметрыОтчета.КонецПериода,
		Перечисления.СистемыНалогообложения.Упрощенная);
	Если ОтражатьИспользованныеВзносы И УпрощенныйЗачетВзносов
		И ПараметрыОтчета.КлючТекущегоВарианта = "РасчетНалогаУСН" Тогда
		ГруппировкаЗаголовокВзносы = БухгалтерскиеОтчеты.НайтиПоИмени(
			КомпоновщикНастроек.Настройки.Структура, "ЗаголовокВзносы");
		ГруппировкаЗаголовокВзносы.Использование = Истина;
		
		Если ОбъектДоходы Тогда
			ОбъектУСН = "Доход";
			БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
				КомпоновщикНастроек, "ЗаголовокВзносы", Нстр("ru = '* Взносы ИП за себя, использованные для уменьшения налога распределяются по остаточному принципу, начиная с единого тарифа и заканчивая взносом с дохода за текущий год'"));
			Если КоэффициентСпецРежим = 1 Тогда
				Совмещение = "";
			Иначе
				Совмещение = "Совмещение";
			КонецЕсли;
		Иначе
			ОбъектУСН = "Расход";
			БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
				КомпоновщикНастроек, "ЗаголовокВзносы", Нстр("ru = 'Страховые взносы за себя, включенные в расходы'"));
			СуммаВзносовУказанная = УчетСтраховыхВзносовИП.СкорректированнаяСуммаВзносовПодлежащихУплате(
				ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
			
			Если СуммаВзносовУказанная = Неопределено Тогда
				Совмещение = ?(КоэффициентСпецРежим = 1, "", "Совмещение");
			Иначе
				БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СуммаВзносовУказанная",
					СуммаВзносовУказанная);
				Совмещение = ?(КоэффициентСпецРежим = 1, "УказанаСумма", "УказанаСуммаСовмещение");
			КонецЕсли;
		КонецЕсли;
		
		ГруппировкаВзносы = БухгалтерскиеОтчеты.НайтиПоИмени(
			КомпоновщикНастроек.Настройки.Структура, СтрШаблон("ВзносыОбъект%1%2", ОбъектУСН, Совмещение));
		ГруппировкаВзносыЗаголовок = БухгалтерскиеОтчеты.НайтиПоИмени(
			КомпоновщикНастроек.Настройки.Структура, СтрШаблон("ВзносыОбъект%1%2Заголовок", ОбъектУСН, Совмещение));
		ГруппировкаВзносыПодвал = БухгалтерскиеОтчеты.НайтиПоИмени(
			КомпоновщикНастроек.Настройки.Структура, СтрШаблон("ВзносыОбъект%1%2Подвал", ОбъектУСН, Совмещение));
		
		ГруппировкаВзносы.Использование = Истина;
		ГруппировкаВзносыЗаголовок.Использование = Истина;
		ГруппировкаВзносыПодвал.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	// Добавим примечания, если требуются.
	СчетчикПримечаний = 0;
	
	Если ПараметрыОтчета.НалоговыйПериодРасширен Тогда
		
		СправкиРасчеты.ДобавитьПримечание(
			Результат,
			ПояснениеРасширенныйНалоговыйПериод(ПараметрыОтчета.ДатаНачалаДеятельности, ПараметрыОтчета.КонецПериода),
			СчетчикПримечаний);
		
	КонецЕсли;
	
	Если ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		
		СправкиРасчеты.ДобавитьПримечание(Результат, ПояснениеУменьшениеНалогаНаТорговыйСбор(), СчетчикПримечаний);
			
	КонецЕсли;
	
	Если ПараметрыОтчета.ПрименяетсяПовышеннаяСтавкаНалога Тогда
		
		ПараметрыОтчета.Вставить("СтавкаЗаПериод", СтавкаЗаПериод(ПараметрыОтчета.Организация, ПараметрыОтчета.НачалоПериода));
		
		СправкиРасчеты.ДобавитьПримечание(Результат, ПояснениеПовышеннаяСтавкаНалога(ПараметрыОтчета), СчетчикПримечаний);
		
	КонецЕсли;
	
	Если ПараметрыОтчета.УменьшатьНалогНаТорговыйСбор
		И Не ПараметрыОтчета.КлючТекущегоВарианта = "РасчетТорговогоСбораУменьшающегоНалог" Тогда
		
		СправкиРасчеты.ДобавитьПримечание(Результат, ПояснениеУменьшениеНалогаНаТорговыйСбор(), СчетчикПримечаний);
		
	КонецЕсли;
	
	// Установим параметры таблицы.
	Результат.ФиксацияСверху = 0;
	Результат.ФиксацияСлева  = 0;
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
КонецПроцедуры

Функция ДанныеПревышенияГраницыПримененияОсновнойСтавкиУСН(Организация, ОтчетныйПериод) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ПрименяетсяПовышеннаяСтавкаНалога", Ложь);
	Результат.Вставить("ПериодПревышенияЛимитов", Дата(1, 1, 1));
	Результат.Вставить("ПревышенЛимитДоходов", Ложь);
	Результат.Вставить("ПревышенЛимитРаботников", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(ОтчетныйПериод));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(ОтчетныйПериод));
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(ОтчетныйПериод));
	
	Запрос.Текст = "ВЫБРАТЬ
	|	РасчетНалогаУСН.Организация КАК Организация,
	|	РасчетНалогаУСН.Доходы,
	|	РасчетНалогаУСН.СредняяЧисленностьРаботников,
	|	НАЧАЛОПЕРИОДА(РасчетНалогаУСН.ПериодРасчета, КВАРТАЛ) КАК ПериодРасчета,
	|	НАЧАЛОПЕРИОДА(РасчетНалогаУСН.ПериодПревышенияЛимитов, КВАРТАЛ) КАК ПериодПревышенияЛимитов
	|ПОМЕСТИТЬ РасчетНалогаУСН
	|ИЗ
	|	РегистрСведений.РасчетНалогаУСН КАК РасчетНалогаУСН
	|ГДЕ
	|	РасчетНалогаУСН.Организация = &Организация
	|	И РасчетНалогаУСН.ПериодРасчета МЕЖДУ &НачалоГода И &КонецПериода
	|	И НЕ РасчетНалогаУСН.ДеятельностьНаТорговомСборе
	|	И РасчетНалогаУСН.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалогаЗаТекущийПериод.Организация,
	|	РасчетНалогаЗаТекущийПериод.ПериодПревышенияЛимитов,
	|	ЕСТЬNULL(РасчетыНалогаЗаНалоговыйПериод.Доходы, 0) КАК Доходы,
	|	ЕСТЬNULL(РасчетыНалогаЗаНалоговыйПериод.СредняяЧисленностьРаботников, 0) КАК СредняяЧисленностьРаботников
	|ИЗ
	|	РасчетНалогаУСН КАК РасчетНалогаЗаТекущийПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетНалогаУСН КАК РасчетыНалогаЗаНалоговыйПериод
	|		ПО РасчетНалогаЗаТекущийПериод.Организация = РасчетыНалогаЗаНалоговыйПериод.Организация
	|		И РасчетНалогаЗаТекущийПериод.ПериодПревышенияЛимитов = РасчетыНалогаЗаНалоговыйПериод.ПериодРасчета
	|ГДЕ
	|	РасчетНалогаЗаТекущийПериод.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ГраницаДоходов =
			КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаДоходовДляПримененияОсновнойСтавкиУСН(ОтчетныйПериод);
		ГраницаЧисленностиРаботников =
			КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаЧисленностиРаботниковДляПримененияОсновнойСтавкиУСН(ОтчетныйПериод);
		
		Результат.ПрименяетсяПовышеннаяСтавкаНалога = ЗначениеЗаполнено(Выборка.ПериодПревышенияЛимитов);
		Результат.ПериодПревышенияЛимитов = Выборка.ПериодПревышенияЛимитов;
		Результат.ПревышенЛимитДоходов = Выборка.Доходы > ГраницаДоходов;
		Результат.ПревышенЛимитРаботников = Выборка.СредняяЧисленностьРаботников > ГраницаЧисленностиРаботников;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПояснениеРасширенныйНалоговыйПериод(ДатаРегистрации, КонецПериодаОтчета)
	
	ШаблонПояснения = НСтр("ru = '%1 рассчитывается за %2 период с даты регистрации %3 по %4 (п.2 ст. 55 НК РФ)'");
	
	ЭтоНалоговыйПериод = КонецКвартала(КонецПериодаОтчета) = КонецГода(КонецПериодаОтчета);
	
	ФорматнаяСтрокаПериода = "ДФ=dd.MM.yyyy";
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПояснения,
		?(ЭтоНалоговыйПериод, НСтр("ru = 'Налог'"), НСтр("ru = 'Авансовый платеж'")),
		?(ЭтоНалоговыйПериод, НСтр("ru = 'налоговый'"), НСтр("ru = 'отчетный'")),
		Формат(ДатаРегистрации, ФорматнаяСтрокаПериода),
		Формат(КонецКвартала(КонецПериодаОтчета), ФорматнаяСтрокаПериода));
	
КонецФункции

Функция ПояснениеУменьшениеНалогаНаТорговыйСбор()
	
	Возврат НСтр("ru = 'Торговый сбор уменьшает только налог, рассчитанный по облагаемой торговым сбором деятельности (п. 8 ст. 346.21 НК РФ)'");
	
КонецФункции

Функция ПояснениеПовышеннаяСтавкаНалога(ПараметрыОтчета)
	
	НомерКвартала = ОбщегоНазначенияБПКлиентСервер.НомерКвартала(ПараметрыОтчета.ПериодПревышенияЛимитов);

	ГраницаДоходов = КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаДоходовДляПримененияОсновнойСтавкиУСН(
		ПараметрыОтчета.ПериодПревышенияЛимитов);
	
	ГраницаЧисленностиРаботников =
			КонтрольПраваПримененияСпецрежимаКлиентСервер.ГраницаЧисленностиРаботниковДляПримененияОсновнойСтавкиУСН(ПараметрыОтчета.ПериодПревышенияЛимитов);
	
	Если ПараметрыОтчета.ПрименяетсяПовышеннаяСтавкаНалога
		И ПараметрыОтчета.СтавкаЗаПериод = УчетУСН.СтавкиНалогаУСН(ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода).ОсновнаяСтавка Тогда
		Если НомерКвартала = 2 Тогда
			ПояснениеПовышеннаяСтавка = НСтр("ru = 'Во %1 квартале %2.
				|Доходы, полученные с начала %1 квартала, облагаются по ставке %3%%
				|Применяется основная ставка в связи со снижением налоговой базы в текущем периоде.'");
		Иначе
			ПояснениеПовышеннаяСтавка = НСтр("ru = 'В %1 квартале %2.
				|Доходы, полученные с начала %1 квартала, облагаются по ставке %3%%
				|Применяется основная ставка в связи со снижением налоговой базы в текущем периоде.'");
		КонецЕсли;
	Иначе
		Если НомерКвартала = 2 Тогда
			ПояснениеПовышеннаяСтавка = НСтр("ru = 'Во %1 квартале %2.
				|Доходы, полученные с начала %1 квартала, облагаются по повышенной ставке %3%%'");
		Иначе
			ПояснениеПовышеннаяСтавка = НСтр("ru = 'В %1 квартале %2.
				|Доходы, полученные с начала %1 квартала, облагаются по повышенной ставке %3%%'");
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыОтчета.ПревышенЛимитДоходов
		И ПараметрыОтчета.ПревышенЛимитРаботников Тогда
		ПояснениеПревышенныйПоказатель = СтрШаблон(
			НСтр("ru = 'сумма доходов превысила %1 млн. рублей и средняя численность наемных работников превысила %2 человек'"),
			ГраницаДоходов / 1000000, // в млн. рублей
			ГраницаЧисленностиРаботников);
	ИначеЕсли ПараметрыОтчета.ПревышенЛимитДоходов Тогда
		ПояснениеПревышенныйПоказатель =
			СтрШаблон(НСтр("ru = 'сумма доходов превысила %1 млн. рублей'"),
			ГраницаДоходов / 1000000); // в млн. рублей
	ИначеЕсли ПараметрыОтчета.ПревышенЛимитРаботников Тогда
		ПояснениеПревышенныйПоказатель = СтрШаблон(
			НСтр("ru = 'средняя численность наемных работников превысила %1 человек'"), ГраницаЧисленностиРаботников);
	КонецЕсли;
	
	ПояснениеПовышеннаяСтавка = СтрШаблон(
		ПояснениеПовышеннаяСтавка, НомерКвартала, ПояснениеПревышенныйПоказатель,
		ПараметрыОтчета.СтавкаЗаПериод);
		
	Возврат ПояснениеПовышеннаяСтавка;
	
КонецФункции

Функция СтавкаЗаПериод(Организация, ОтчетныйПериод)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетНалогаУСН.СтавкаНалогаУСН КАК СтавкаНалогаУСН
		|ИЗ
		|	РегистрСведений.РасчетНалогаУСН КАК РасчетНалогаУСН
		|ГДЕ
		|	РасчетНалогаУСН.Организация = &Организация
		|	И РасчетНалогаУСН.Активность
		|	И РасчетНалогаУСН.ПериодРасчета МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Запрос.УстановитьПараметр("КонецПериода",  КонецКвартала(ОтчетныйПериод));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(ОтчетныйПериод));
	Запрос.УстановитьПараметр("Организация",   Организация);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		Возврат Результат.СтавкаНалогаУСН;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли