
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.СпособЗаполнения = "ПоДаннымУчета" Тогда 
		Отчет.СпособЗаполнения        = 0;
		Отчет.Организация             = Параметры.Организация; 
		Отчет.Период                  = Параметры.Период;
	ИначеЕсли Параметры.СпособЗаполнения = "ПоБухгалтерскойОтчетности" Тогда
		Отчет.СпособЗаполнения        = 1;
		Отчет.Организация             = Параметры.Организация; 
		Отчет.Период                  = Параметры.Период;
		Отчет.БухгалтерскаяОтчетность = Параметры.БухгалтерскаяОтчетность;
	Иначе
		ДатаАктуальности = ОбщегоНазначения.ТекущаяДатаПользователя();
		Отчет.Период                  = КонецМесяца(ДобавитьМесяц(ДатаАктуальности, -1));
		Отчет.Организация             = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ОграничениеСнизуПериодаОтчета = РасчетСтоимостиЧистыхАктивовКлиентСервер.ОграничениеСнизуПериодаОтчета();
	Отчет.Период                      = Макс(Отчет.Период, ОграничениеСнизуПериодаОтчета);
	
	СформироватьПриОткрытии = Параметры.СформироватьПриОткрытии;
	
	УстановитьПредставлениеБухгалтерскойОтчетности();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
	УстановитьДоступностьКнопкиРасшифровать();
	УстановитьПредставлениеПериода();
	УстановитьСостояниеПоляТабличногоДокумента("НеАктуальность");
	
	Если СформироватьПриОткрытии Тогда
		СформироватьОтчетНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОбработкаОповещенияАктуализации(ЭтотОбъект, Отчет.Организация, Отчет.Период, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОграничениеСнизуПериодаОтчета = РасчетСтоимостиЧистыхАктивовКлиентСервер.ОграничениеСнизуПериодаОтчета();
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода",    НачалоГода(Отчет.Период));
	ПараметрыВыбора.Вставить("КонецПериода",     Отчет.Период);
	ПараметрыВыбора.Вставить("ОграничениеСнизу", ОграничениеСнизуПериодаОтчета);
	ПараметрыВыбора.Вставить("ВыборКварталов",   Истина);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбора, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отчет.Период = ВыбранноеЗначение.КонецПериода;
	УстановитьПредставлениеПериода(); 
	
	ПриИзмененииПериодаОрганизацииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииПериодаОрганизацииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияПриИзменении(Элемент)
	
	УстановитьВидимость();
	
	ПриИзмененииПериодаОрганизацииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура БухгалтерскаяОтчетностьПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПроверитьЗаполнениеНаКлиенте(0) Тогда
		Возврат;
	КонецЕсли;
	
	ПарамОтбора = Новый Структура;
	ИсточникБухОтчетности = РасчетСтоимостиЧистыхАктивовКлиентСервер.ИсточникБухгалтерскойОтчетности();
	ПарамОтбора.Вставить("ИсточникОтчета", ИсточникБухОтчетности);
	Организации = Новый СписокЗначений;
	Организации.Добавить(Отчет.Организация);
	ПарамОтбора.Вставить("Организация", Организации);
	ПарамОтбора.Вставить("ОтборКодИФНС", Ложь);
	ПарамОтбора.Вставить("ОтборПериод", Истина);
	ПарамОтбора.Вставить("ДатаНачалаПериодаОтчета", НачалоГода(Отчет.Период));
	ПарамОтбора.Вставить("ДатаКонцаПериодаОтчета", Отчет.Период);
	
	ФормаВыбораОтчета = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени(
	"ФормаВыбораОтчета", ПарамОтбора, Элемент);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораБухгалтерскойОтчетности", ЭтотОбъект);
	ФормаВыбораОтчета.ОписаниеОповещенияОЗакрытии = ОписаниеОповещения;
	ФормаВыбораОтчета.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаВыбораОтчета.Элементы.ФормаСоздатьОтчет.Видимость = Ложь;
	ФормаВыбораОтчета.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура БухгалтерскаяОтчетностьПредставлениеОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение( , Отчет.БухгалтерскаяОтчетность);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СформироватьОтчетНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочте(Команда)
	
	ОтправкаПочтовыхСообщенийКлиент.ОтправитьОтчет(ЭтотОбъект);
	
КонецПроцедуры

#Область Актуализация

&НаКлиенте
Процедура Актуализировать(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.Актуализировать(ЭтотОбъект, Отчет.Организация, Отчет.Период);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьАктуализацию(ЭтотОбъект, Отчет.Организация, Отчет.Период);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Расшифровать(Команда)

	ОткрытьРасшифровку();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьОтчетНаКлиенте()
	
	Если Не ПроверитьЗаполнениеНаКлиенте(Отчет.СпособЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СформироватьОтчетНаСервере();
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	УстановитьСостояниеПоляТабличногоДокумента("НеИспользовать");
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыОтчета = Отчеты.РасчетСтоимостиЧистыхАктивов.ПараметрыОтчета(РеквизитФормыВЗначение("Отчет"));
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
	"Отчеты.РасчетСтоимостиЧистыхАктивов.СформироватьОтчет", ПараметрыОтчета);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения = Неопределено Тогда
		УстановитьСостояниеПоляТабличногоДокумента("НеАктуальность");
		Возврат;
	КонецЕсли;
	
	Если РезультатВыполнения.Статус = "Ошибка" Тогда
		УстановитьСостояниеПоляТабличногоДокумента("НеАктуальность");
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(РезультатВыполнения.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	ДанныеОтчета = ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	УдалитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	
	Результат = ДанныеОтчета.Результат;
	ДанныеРасшифровки = ДанныеОтчета.ДанныеРасшифровки;
	УстановитьДоступностьКнопкиРасшифровать();
	
	Если ДанныеОтчета.ТребуетсяПроверитьАктуальностьДанных Тогда
		БухгалтерскийУчетКлиентПереопределяемый.ПослеФормированияОтчета(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНаКлиенте(СпособЗаполнения)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Отчет.Период) Тогда
		ТекстСообщения = НСтр("ru='Укажите месяц'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,
		"ПериодПредставление", ,Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Отчет.Организация) Тогда
		ТекстСообщения = НСтр("ru='Укажите организацию'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,
		"Организация", "Отчет", Отказ);
	КонецЕсли;
	
	Если СпособЗаполнения = 0 Тогда
		Возврат Не Отказ;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Отчет.БухгалтерскаяОтчетность) Тогда
		ТекстСообщения = НСтр("ru='Укажите экземпляр бухгалтерской отчетности'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,
		"БухгалтерскаяОтчетностьПредставление", ,Отказ);
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимость()

	Элементы.ГруппаБухгалтерскаяОтчетность.Видимость = Не Отчет.СпособЗаполнения = 0;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопкиРасшифровать()

	Элементы.Расшифровать.Доступность = ЗначениеЗаполнено(ДанныеРасшифровки);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеПоляТабличногоДокумента(Состояние)

	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, Состояние);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПредставлениеПериода()
	
	Если Не ЗначениеЗаполнено(Отчет.Период) Тогда
		ПериодПредставление = "";
	ИначеЕсли Месяц(Отчет.Период) % 3 = 0 Тогда
		ФорматДаты          = НСтр("ru='ФП=Истина'");
		ПериодПредставление = ПредставлениеПериода(НачалоГода(Отчет.Период),
		КонецМесяца(Отчет.Период), ФорматДаты);
	Иначе
		ФорматДаты          = НСтр("ru='ДФ=''ММММ гггг ""г.""'''");
		ПериодПредставление = Формат(Отчет.Период, ФорматДаты);
	КонецЕсли;
	
КонецПроцедуры

#Область БухгалтерскаяОтчетности

&НаКлиенте
Процедура ПриИзмененииПериодаОрганизацииНаКлиенте()
	
	Если ВозможноОпределениеБухгалтерскойОтчетности() Тогда
		ОпределитьБухгалтерскуюОтчетностьБРО();
	Иначе
		Отчет.БухгалтерскаяОтчетность = Неопределено;
		БухгалтерскаяОтчетностьПредставление = "";
	КонецЕсли;
	
	УстановитьСостояниеПоляТабличногоДокумента("НеАктуальность");
	
КонецПроцедуры

&НаКлиенте
Функция ВозможноОпределениеБухгалтерскойОтчетности()
	
	Возврат ЗначениеЗаполнено(Отчет.Период)
		И ЗначениеЗаполнено(Отчет.Организация)
		И ЗначениеЗаполнено(Отчет.СпособЗаполнения);
	
КонецФункции

&НаСервере
Процедура ОпределитьБухгалтерскуюОтчетностьБРО()
	
	Отчет.БухгалтерскаяОтчетность = Неопределено;
	
	МетаданныеДокумента = Метаданные.Документы.РегламентированныйОтчет;
	
	// Выполняем поиск среди сохранных в базе регламентированных отчетов.
	ПараметрыРеглОтчетов = Новый ТаблицаЗначений;
	ПараметрыРеглОтчетов.Колонки.Добавить("ИсточникОтчета", МетаданныеДокумента.Реквизиты.ИсточникОтчета.Тип);
	ПараметрыРеглОтчетов.Колонки.Добавить("Организация",    Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ПараметрыРеглОтчетов.Колонки.Добавить("ДатаОкончания",  ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ПараметрыРеглОтчетов.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	ЗаполнениеФинОтчетностиВБанки.ПодготовитьПараметрыПоискаРеглОтчетов(ПараметрыРеглОтчетов);
	
	ИсточникиБухОтчетности = Новый Массив;
	ИсточникБухОтчетности = РасчетСтоимостиЧистыхАктивовКлиентСервер.ИсточникБухгалтерскойОтчетности();
	ИсточникиБухОтчетности.Добавить(ИсточникБухОтчетности);
	
	Для каждого ИсточникБухОтчетности Из ИсточникиБухОтчетности Цикл
		ПараметрРеглОтчета = ПараметрыРеглОтчетов.Добавить();
		ПараметрРеглОтчета.ИсточникОтчета = ИсточникБухОтчетности;
		ПараметрРеглОтчета.Организация    = Отчет.Организация;
		ПараметрРеглОтчета.ДатаОкончания  = Отчет.Период;
	КонецЦикла;
	
	// Выберем все подходящие отчеты.
	ИнтерфейсыВзаимодействияБРО.ЗаполнитьСсылкиНаРеглОтчеты(ПараметрыРеглОтчетов);
	
	Для Каждого ПараметрыРеглОтчета Из ПараметрыРеглОтчетов Цикл
		
		Для Каждого РеглОтчет Из ПараметрыРеглОтчета.Документы Цикл
			
			Отчет.БухгалтерскаяОтчетность = РеглОтчет.Ссылка;
			Прервать;
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Отчет.БухгалтерскаяОтчетность) Тогда 
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПредставлениеБухгалтерскойОтчетности();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеБухгалтерскойОтчетности()
	
	Если ЗначениеЗаполнено(Отчет.БухгалтерскаяОтчетность) Тогда
		
		РеквизитыОтчетности = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Отчет.БухгалтерскаяОтчетность, "Вид,Комментарий");
		
		Если РеквизитыОтчетности.Вид = 0 Тогда
			ПредставлениеВида = НСтр("ru='Первичная'");
		Иначе
			ШаблонПредставленияВида = НСтр("ru='Корректирующая №%1'");
			ПредставлениеВида = СтрШаблон(ШаблонПредставленияВида, РеквизитыОтчетности.Вид);
		КонецЕсли;
		
		СтрокиПредставления = Новый Массив;
		СтрокиПредставления.Добавить(ПредставлениеВида);
		Если Не ПустаяСтрока(РеквизитыОтчетности.Комментарий) Тогда
			СтрокиПредставления.Добавить(РеквизитыОтчетности.Комментарий);
		КонецЕсли;
		БухгалтерскаяОтчетностьПредставление = СтрСоединить(СтрокиПредставления, ", ");
		
	Иначе
		БухгалтерскаяОтчетностьПредставление = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораБухгалтерскойОтчетности(РезультатВыбора, ДопПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(РезультатВыбора) Тогда
		Возврат;
	КонецЕсли;
	
	Отчет.БухгалтерскаяОтчетность = РезультатВыбора;
	УстановитьПредставлениеБухгалтерскойОтчетности();
	УстановитьСостояниеПоляТабличногоДокумента("НеАктуальность");
	
КонецПроцедуры

#КонецОбласти

#Область Актуализация

//@skip-check module-structure-form-event-regions
&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	
КонецПроцедуры

//@skip-check module-structure-form-event-regions
&НаКлиенте
Процедура ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыйПараметрыАктуализацииОтчета();
	ПараметрыАктуализации.Вставить("Организация",                       Отчет.Организация);
	ПараметрыАктуализации.Вставить("ДатаАктуальности",                  ДатаАктуальности);
	ПараметрыАктуализации.Вставить("ДатаОкончанияАктуализации",         Отчет.Период);
	
	ЗакрытиеМесяцаКлиент.ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		ЭтотОбъект,
		ПараметрыАктуализации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеМесяцаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "СформироватьОтчет" Тогда
		
		БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
		Активизировать();
		СформироватьОтчетНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(ЭтотОбъект, Отчет.Организация, Отчет.Период);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОткрытьРасшифровку()

	Если ЗначениеЗаполнено(ДанныеРасшифровки) 
		И ТипЗнч(ДанныеРасшифровки) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		ПоказатьЗначение( , ДанныеРасшифровки);
	Иначе
		// Расшифровка поддерживается только для способа "по бухгалтерской отчетности". При выборе
		// способа "по данным учета" расшифровка отсутствует. Поэтому доступность кнопки "Расшифровать"
		// определяется наличием данных расшифровки (см. УстановитьДоступностьКнопкиРасшифровать).
		// Данное предупреждение служит страховкой на случай некорректного управления доступностью кнопки.
		ТекстПредупреждения = НСтр("ru='Для данного отчета не предусмотрена расшифровка показателей'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
