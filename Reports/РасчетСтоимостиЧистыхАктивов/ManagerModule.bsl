#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает результат формирования отчета
//
// Параметры:
//  ПараметрыОтчета - см. ПараметрыОтчета
//
// Возвращаемое значение:
//   Структура:
//      * Результат - ТабличныйДокумент - табличный документ отчета
//      * ТребуетсяПроверитьАктуальностьДанных - Булево - признак необходимости
//                                   запуска проверки актуальности данных после
//                                   формирования отчета. Принимает значение Истина
//                                   при расчете по данным учета
//      * ДанныеРасшифровки - см. ДанныеРасшифровки
//
Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб          = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РасчетСтоимостиЧистыхАктивов_РасчетСтоимостиЧистыхАктивов";
	
	Макет = ПолучитьМакет("РасчетСтоимостиЧистыхАктивов");
	ПараметрыМакета = ПараметрыМакета(ПараметрыОтчета);
	Макет.Параметры.Заполнить(ПараметрыМакета);
	
	ТабличныйДокумент.Вывести(Макет);
	
	ДанныеОтчета = Новый Структура;
	ДанныеОтчета.Вставить("Результат", ТабличныйДокумент);
	// Актуальность данных проверяется только при расчете по данным учета
	ПроверитьАктуальность = ПараметрыОтчета.СпособЗаполнения = 0;
	ДанныеОтчета.Вставить("ТребуетсяПроверитьАктуальностьДанных", ПроверитьАктуальность);
	
	ДанныеРасшифровки = ДанныеРасшифровки(ПараметрыОтчета);
	ДанныеОтчета.Вставить("ДанныеРасшифровки", ДанныеРасшифровки);
	
	Возврат ДанныеОтчета;
	
КонецФункции

// Возвращает параметры формирования отчета
//
// Параметры:
//  Отчет  - ОтчетОбъект.РасчетСтоимостиЧистыхАктивов - объект отчета, для
//                 которого будут подготовлены параметры
//
// Возвращаемое значение:
//   Структура:
//      * СпособЗаполнения - Число - при заполнении по данным учета принимает
//                                   значение 0, по бухгалтерской отчетности - 1
//      * БухгалтерскаяОтчетность - ДокументСсылка.РегламентированныйОтчет - ссылка на
//                                  бухгалтерскую отчетность. Имеет заполненное значение
//                                  только при СпособЗаполнения = 1
//      * Организация - СправочникСсылка.Организации - организация отчета
//      * Период - Дата - дата отчета
//      * ДатаОтчетная04 - Дата - конец дня даты отчета
//      * ДатаОтчетная05 - Дата - конец прошлого года
//      * ДатаОтчетная06 - Дата - конец позапрошлого года
//
Функция ПараметрыОтчета(Отчет) Экспорт
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("СпособЗаполнения",            Отчет.СпособЗаполнения);
	Если Отчет.СпособЗаполнения = 1 Тогда
		ПараметрыОтчета.Вставить("БухгалтерскаяОтчетность", Отчет.БухгалтерскаяОтчетность);
	Иначе
		ПараметрыОтчета.Вставить("БухгалтерскаяОтчетность", Документы.РегламентированныйОтчет.ПустаяСсылка());
	КонецЕсли;
	ПараметрыОтчета.Вставить("Организация",                 Отчет.Организация);
	ПараметрыОтчета.Вставить("Период",                      Отчет.Период);
	
	ДатыПолученияОстатков = ДатыПолученияОстатков(ПараметрыОтчета);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтчета, ДатыПолученияОстатков, Истина);
	
	//@skip-check constructor-function-return-section
	Возврат ПараметрыОтчета;
	
КонецФункции

// Возвращает значения параметров для заполнения макета
//
// Параметры:
//  ПараметрыОтчета - см. ПараметрыОтчета
//
// Возвращаемое значение:
//   Структура
//
Функция ПараметрыМакета(ПараметрыОтчета) Экспорт
	
	ПараметрыМакета = Новый Структура;
	
	ЗаголовкиМакета = ЗаголовкиМакета(ПараметрыОтчета);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыМакета, ЗаголовкиМакета, Истина);
	
	Если ПараметрыОтчета.СпособЗаполнения = 0 Тогда
		ПараметрыПоУчету = ПараметрыПоДаннымУчета(ПараметрыОтчета);
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыМакета, ПараметрыПоУчету, Истина);
	Иначе
		ПараметрыПоОтчетности = ПараметрыПоБухгалтерскойОтчетности(ПараметрыОтчета);
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыМакета, ПараметрыПоОтчетности, Истина);
	КонецЕсли;
	
	ПараметрыИтогов = ПараметрыИтогов(ПараметрыМакета);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыМакета, ПараметрыИтогов, Истина);
	
	Возврат ПараметрыМакета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру, содержащую даты, предназначенные для расчета остатков
//
// Параметры:
//  ПараметрыОтчета  - см. ПараметрыОтчета
//
// Возвращаемое значение:
//   Структура:
//      * ДатаОтчетная04 - Дата - конец дня даты отчета
//      * ДатаОтчетная05 - Дата - конец прошлого года
//      * ДатаОтчетная06 - Дата - конец позапрошлого года
//
Функция ДатыПолученияОстатков(ПараметрыОтчета)
	
	ДатыПолученияОстатков = Новый Структура;
	
	ДатаНачалаПериодаОтчета = НачалоГода(ПараметрыОтчета.Период);
	ДатаКонцаПериодаОтчета = КонецДня(ПараметрыОтчета.Период);
	
	ДатыПолученияОстатков.Вставить("ДатаОтчетная04", ДатаКонцаПериодаОтчета);
	
	ДатаНачалаПрошлогоГода = НачалоГода(ДобавитьМесяц(ДатаНачалаПериодаОтчета, -12));
	ДатаКонцаПрошлогоГода = КонецГода(ДатаНачалаПрошлогоГода);
	
	ДатыПолученияОстатков.Вставить("ДатаОтчетная05", ДатаКонцаПрошлогоГода);
	
	ДатаНачалаПозапрошлогоГода = НачалоГода(ДобавитьМесяц(ДатаНачалаПрошлогоГода, -12));
	ДатаКонцаПозапрошлогоГода = КонецГода(ДатаНачалаПозапрошлогоГода);
	
	ДатыПолученияОстатков.Вставить("ДатаОтчетная06", ДатаКонцаПозапрошлогоГода); 
	
	Возврат ДатыПолученияОстатков;
	
КонецФункции

// Возвращает заголовки колонок показателей
//
// Параметры:
//  ПараметрыОтчета - см. ПараметрыОтчета
//
// Возвращаемое значение:
//   Структура:
//      * ЗаголовокПоказателей04 - Строка - заголовок даты отчета
//      * ЗаголовокПоказателей05 - Строка - заголовок конца прошлого года
//      * ЗаголовокПоказателей06 - Строка - заголовок конца позапрошлого года
//
Функция ЗаголовкиМакета(ПараметрыОтчета)
	
	ЗаголовкиМакета = Новый Структура;
	
	// Заголовки периодов показателей
	ШаблонДаты = НСтр("ru='На %1 г.'");
	ФорматДаты = НСтр("ru='ДФ=''дд ММММ гггг'''");
	ЗаголовкиМакета.Вставить("ЗаголовокПоказателей04", СтрШаблон(ШаблонДаты, Формат(ПараметрыОтчета.ДатаОтчетная04, ФорматДаты)));
	ЗаголовкиМакета.Вставить("ЗаголовокПоказателей05", СтрШаблон(ШаблонДаты, Формат(ПараметрыОтчета.ДатаОтчетная05, ФорматДаты)));
	ЗаголовкиМакета.Вставить("ЗаголовокПоказателей06", СтрШаблон(ШаблонДаты, Формат(ПараметрыОтчета.ДатаОтчетная06, ФорматДаты)));
	
	Возврат ЗаголовкиМакета;
	
КонецФункции

#Область ДанныеУчета

// Возвращает параметры макета, определяемые по данным учета. Рассчитывает
// показатели для каждой графы (см. ДобавитьПоказателиДанныхУчетаПоГрафе).
// Дополняет параметры представлением периода отчета, сведениями об организации
// и ее руководителе
//
// Параметры:
//  ПараметрыОтчета - см. ПараметрыОтчета
//
// Возвращаемое значение:
//   Структура
//
Функция ПараметрыПоДаннымУчета(ПараметрыОтчета)
	
	ПараметрыМакета = Новый Структура;
	
	Для Каждого Графа Из ГрафыРегламентированногоОтчета() Цикл
		ДобавитьПоказателиДанныхУчетаПоГрафе(ПараметрыМакета, Графа, ПараметрыОтчета);
	КонецЦикла;
	
	ПараметрыМакета.Вставить("ПериодРасчетаСтоимостиЧистыхАктивов", ПредставлениеПериодаПоДаннымУчета(ПараметрыОтчета));
	
	СведенияОбОрганизации = СведенияОбОрганизацииПоДаннымУчета(ПараметрыОтчета);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыМакета, СведенияОбОрганизации, Истина);
	
	Возврат ПараметрыМакета;
	
КонецФункции

// Рассчитывает и добавляет показатели баланса по графе. Подробнее о графах
// см. ГрафыРегламентированногоОтчета. Используя модуль заполнения отчетности,
// рассчитываются итоги разделов баланса и определяются доступные пользователю
// филиалы организации. Далее они (филиалы) используются вместе с организацией
// для получения корректирующих показателей. См. ДобавитьПоказателиКорректирующиеПоГрафе.
// Следует понимать, что в общем случае состав филиалов зависит от даты, то есть от графы
//
// Параметры:
//  ПараметрыМакета - Структура - параметры макета, в которые будут
//                                добавлены корректирующие показатели
//  Графа - Строка - номер графы. См. ГрафыРегламентированногоОтчета
//  ПараметрыОтчета - см. ПараметрыОтчета
//
Процедура ДобавитьПоказателиДанныхУчетаПоГрафе(ПараметрыМакета, Графа, ПараметрыОтчета)
	
	ПериодОтчета = ПараметрыОтчета["ДатаОтчетная" + Графа];
	Организация = ПараметрыОтчета.Организация;
	ВключаяМежотчетныйПериод = Не Графа = "04";
	ЗначенияПоказателей = ЗаполнениеБухгалтерскойОтчетности2025Основная.ПоказателиБаланс(ПериодОтчета,
	Организация, , ВключаяМежотчетныйПериод);
	ЗначенияИтогов = ЗаполнениеБухгалтерскойОтчетности2025Основная.ИтогиРазделовБаланса(ЗначенияПоказателей);
	
	ПараметрыМакета.Вставить("ВнеоборотныеАктивы" + Графа,               ЗначенияИтогов.ВнеоборотныеАктивы);
	ПараметрыМакета.Вставить("ОборотныеАктивы" + Графа,                  ЗначенияИтогов.ОборотныеАктивы);
	ПараметрыМакета.Вставить("Капитал" + Графа,                          ЗначенияИтогов.Капитал);
	ПараметрыМакета.Вставить("ДолгосрочныеОбязательства" + Графа,        ЗначенияИтогов.ДолгосрочныеОбязательства);
	ПараметрыМакета.Вставить("КраткосрочныеОбязательства" + Графа,       ЗначенияИтогов.КраткосрочныеОбязательства);
	
	ЗначенияПоказателей.Свернуть("Организация");
	ДополнительныеОрганизации = ЗначенияПоказателей.ВыгрузитьКолонку("Организация");
	
	ДобавитьПоказателиКорректирующиеПоГрафе(ПараметрыМакета, Графа, ПараметрыОтчета, ДополнительныеОрганизации);
	
КонецПроцедуры

// Возвращает представление периода расчета для вывода под заголовком
//
// Параметры:
//  ПараметрыОтчета - см. ПараметрыОтчета
//
// Возвращаемое значение:
//   Строка
//
Функция ПредставлениеПериодаПоДаннымУчета(ПараметрыОтчета)
	
	ШаблонИсточника = НСтр("ru='по данным учета на %1 г.'");
	ФорматДаты = НСтр("ru='ДФ=''дд ММММ гггг'''");
	ПредставлениеПериода = Формат(ПараметрыОтчета.Период, ФорматДаты);
	ПредставлениеБухОтчетности = СтрШаблон(ШаблонИсточника, ПредставлениеПериода);
	
	Возврат ПредставлениеБухОтчетности;
	
КонецФункции

// Возвращает сведения об организации и подписанте
//
// Параметры:
//  ПараметрыОтчета - см. ПараметрыОтчета
//
// Возвращаемое значение:
//   Структура
//
Функция СведенияОбОрганизацииПоДаннымУчета(ПараметрыОтчета)
	
	СведенияОбОрганизации = Новый Структура;
	
	ПереченьСведений = "НаимЮЛПол,ФИОРук,ДолжнРук,ФИО";
	Сведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
	ПараметрыОтчета.Организация, ПараметрыОтчета.Период, ПереченьСведений);
	
	Если РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(ПараметрыОтчета.Организация) Тогда
		СведенияОбОрганизации.Вставить("НаименованиеОрганизации", Сведения.НаимЮЛПол);
		СведенияОбОрганизации.Вставить("ФИОПодписанта",           Сведения.ФИОРук);
	Иначе
		СведенияОбОрганизации.Вставить("НаименованиеОрганизации", Сведения.ФИО);
		СведенияОбОрганизации.Вставить("ФИОПодписанта",           Сведения.ФИО);
	КонецЕсли;
	
	Возврат СведенияОбОрганизации;

КонецФункции

#КонецОбласти

#Область БухгалтерскаяОтчетность

// Возвращает параметры макета, определяемые по бухгалтерской отчетности. Из
// переданной в параметрах бухгалтерской отчетности извлекаются необходимые
// для расчета показатели баланса, сведения об организации и ее руководителе,
// а также дополнительные организации для заполнения (см. гиперссылка 
// "Организации для заполнения" отчета на форме заполнения бухгалтерского
// баланса). С учетом этих организаций рассчитываются корректирующие показатели
// для каждой графы отчета. Подробнее о графах см. ГрафыРегламентированногоОтчета.
// Возвращаемые параметры дополняются представлением периода отчета
//
// Параметры:
//  ПараметрыОтчета - см. ПараметрыОтчета
//
// Возвращаемое значение:
//   Структура
//
Функция ПараметрыПоБухгалтерскойОтчетности(ПараметрыОтчета)
	
	ПараметрыМакета = Новый Структура;
	
	БухОтчетность = ПараметрыОтчета.БухгалтерскаяОтчетность;
	ИзвлечениеДанных = БухгалтерскаяОтчетностьБРО.НовоеИзвлечениеПоказателейБухгалтерскойОтчетности();
	БухгалтерскаяОтчетностьБРО.ИнициализироватьИзвлечениеПоказателейБухгалтерскойОтчетности(ИзвлечениеДанных, БухОтчетность);
	
	ДополнительныеОрганизации = ИзвлечениеДанных.СохраненныеДанные.ГруппаОрганизаций.ВыгрузитьЗначения();
	ПроверитьДоступностьОрганизаций(ПараметрыОтчета, ДополнительныеОрганизации);
	
	Для Каждого КлючИЗначение Из КартаРегламентированногоОтчета() Цикл
		ЗначениеПоказателя = БухгалтерскаяОтчетностьБРО.ЗначениеПоказателяБухгалтерскойОтчетности(
		ИзвлечениеДанных, "Баланс", КлючИЗначение.Значение);
		ПараметрыМакета.Вставить(КлючИЗначение.Ключ, ЗначениеПоказателя);
	КонецЦикла;
	
	Для Каждого Графа Из ГрафыРегламентированногоОтчета() Цикл
		//@skip-check query-in-loop
		ДобавитьПоказателиКорректирующиеПоГрафе(ПараметрыМакета, Графа, ПараметрыОтчета, ДополнительныеОрганизации);
	КонецЦикла;
	
	ПараметрыМакета.Вставить("ПериодРасчетаСтоимостиЧистыхАктивов", ПредставлениеПериодаПоОтчетности(БухОтчетность));
	
	Возврат ПараметрыМакета;
	
КонецФункции

// Возвращает структуру, в которой ключ соответствует показателю расчета, а значение - идентификатору
// ячейки бухгалтерского баланса. Полученная структура используется для получения значений показателей
// из сохраненной бухгалтерской отчетности (ДокументСсылка.РегламентированныйОтчет). Вместе с показателями
// расчета из отчетности извлекаются сведения об организации и подписанте.
//
// Возвращаемое значение:
//   Структура
//
Функция КартаРегламентированногоОтчета()
	
	КартаПоказателей = Новый Структура;
	// Организация и подписант
	КартаПоказателей.Вставить("НаименованиеОрганизации", "НаименованиеОрганизацииВЗаголовке");
	КартаПоказателей.Вставить("ФИОПодписанта",           "ФИОПодписанта");
	КартаПоказателей.Вставить("ДолжностьПодписанта",     "ДолжностьПодписанта");
	
	КодыРазделов = БухгалтерскаяОтчетностьБРО.КодыРазделовБаланса();
	
	Для Каждого Графа Из ГрафыРегламентированногоОтчета() Цикл
		Для Каждого КодРаздела Из КодыРазделов Цикл
			КлючРаздела = КодРаздела.Ключ + Графа;
			ПолныйКод = КодРаздела.Значение + "00";
			ИдентификаторРаздела = ИдентификаторЯчейки(ПолныйКод, Графа);
			КартаПоказателей.Вставить(КлючРаздела, ИдентификаторРаздела);
		КонецЦикла;
	КонецЦикла;
	
	Возврат КартаПоказателей;
	
КонецФункции

// Возвращает идентификатор ячейки бухгалтерского баланса, по которому можно
// получить значение соответствующего показателя. Использует методы модуля
// БухгалтерскаяОтчетностьБРО
//
// Параметры:
//  КодСтроки  - Строка - код строки бухгалтерского баланса
//  Графа  - Строка - номер графы. См. ГрафыРегламентированногоОтчета
//
// Возвращаемое значение:
//   Строка
//
Функция ИдентификаторЯчейки(КодСтроки, Графа)
	
	ИдентификаторСтроки = БухгалтерскаяОтчетностьБРО.ИдентификаторФиксированнойСтрокиДляЯчеек(КодСтроки);
	ИдентификаторЯчейки = БухгалтерскаяОтчетностьБРО.ИдентификаторЯчейки(ИдентификаторСтроки, Графа);
	
	Возврат ИдентификаторЯчейки;
	
КонецФункции

// Проверяет наличие организаций, участвующих в расчете, данные по которым недоступны пользователю 
// из-за RLS, и вызывает исключение при их обнаружении.
//
// Параметры:
//  ПараметрыОтчета  - см. ПараметрыОтчета
//  ДополнительныеОрганизации  - Массив из СправочникСсылка.Организации
//
Процедура ПроверитьДоступностьОрганизаций(ПараметрыОтчета, ДополнительныеОрганизации)
	
	ОрганизацииОтчетности = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыОтчета.Организация);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОрганизацииОтчетности, ДополнительныеОрганизации, Истина);
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	НедоступныеОрганизации = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ОрганизацииОтчетности, ДоступныеОрганизации);
	Если НедоступныеОрганизации.Количество() > 0 Тогда
		ВыражениеИсключения = НСтр("ru='Ограничен доступ к данным некоторых организаций из бухгалтерской отчетности'");
		ВызватьИсключение ВыражениеИсключения;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает представление периода расчета для вывода под заголовком
//
// Параметры:
//  БухОтчетность  - ДокументСсылка.РегламентированныйОтчет - ссылка на документ, по
//                 данным которого производится расчет
//
// Возвращаемое значение:
//   Строка
//
Функция ПредставлениеПериодаПоОтчетности(БухОтчетность)
	
	ШаблонИсточника = НСтр("ru='по бухгалтерской отчетности за %1'");
	ПредставлениеПериода = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БухОтчетность, "ПредставлениеПериода");
	ПредставлениеБухОтчетности = СтрШаблон(ШаблонИсточника, ПредставлениеПериода);
	
	Возврат ПредставлениеБухОтчетности;
	
КонецФункции

#КонецОбласти

#Область КорректирующиеПоказатели

// Добавляет в структуру корректирующие стоимость активов и пассивов показатели
// на дату, определяемую графой. Подробнее о графах см. ГрафыРегламентированногоОтчета
//
// Параметры:
//  ПараметрыМакета - Структура - параметры макета, в которые будут
//                                добавлены корректирующие показатели
//  Графа - Строка - номер графы. См. ГрафыРегламентированногоОтчета
//  ПараметрыОтчета - см. ПараметрыОтчета
//  ДополнительныеОрганизации - Массив из СправочникСсылка.Организации
//
Процедура ДобавитьПоказателиКорректирующиеПоГрафе(ПараметрыМакета, Графа, ПараметрыОтчета, ДополнительныеОрганизации)
	
	ПериодОтчета = ПараметрыОтчета["ДатаОтчетная" + Графа];
	// В расчете на дату отчета исключаются движения межотчетного периода
	УточнениеПериода = 0;
	// Для остальных дат берутся все движения, включая межотчетный период
	Если Не Графа = "04" Тогда
		УточнениеПериода = БухгалтерскийУчет.ВерхняяГраницаУточненияПериода();
	КонецЕсли;
	КонецПериода = Новый МоментВремениСУточнениемПериода(ПериодОтчета, УточнениеПериода);
	Организация = ПараметрыОтчета.Организация;
	
	Корректировки = КорректирующиеПоказателиНаДату(КонецПериода, Организация, ДополнительныеОрганизации);
	
	ПараметрыМакета.Вставить("РасчетыПоВкладамВУставныйКапитал" + Графа, Корректировки.РасчетыПоВкладамВУставныйКапитал);
	ПараметрыМакета.Вставить("БезвозмездныеПоступления" + Графа,         Корректировки.БезвозмездныеПоступления);
	
КонецПроцедуры

// Возвращает значения корректирующих показателей, определенных как остатки
// регистра "Хозрасчетный" по счетах 75.01 и 98.02 на переданную дату.
// Значения возвращаются с инверсией зеака: умножением на -1 изменены знаки
// чисел, поэтому пригодны для прямого суммирования в расчетах.
//
// Параметры:
//  КонецПериода - МоментВремениСУточнениемПериода - момент времени для расчета показателей
//  Организация - СправочникСсылка.Организации - головная организация расчета показателей
//  ДополнительныеОрганизации - Массив из СправочникСсылка.Организации - филиалы головной
//                              организации, участвующие в расчете
//
// Возвращаемое значение:
//   Структура:
//      * РасчетыПоВкладамВУставныйКапитал - Число - дебетовое сальдо 75.01, умноженное на -1
//      * БезвозмездныеПоступления - Число - кредитовое сальдо 98.02, умноженное на -1
//
Функция КорректирующиеПоказателиНаДату(КонецПериода, Организация, ДополнительныеОрганизации)
	
	// Инициализация значений параметров запроса
	РасчетыПоВкладамВУставныйКапитал = БухгалтерскийУчетПовтИсп.СчетаВИерархии(
	ПланыСчетов.Хозрасчетный.РасчетыПоВкладамВУставныйКапитал); // 75.01
	
	БезвозмездныеПоступления = БухгалтерскийУчетПовтИсп.СчетаВИерархии(
	ПланыСчетов.Хозрасчетный.БезвозмездныеПоступления); // 98.02
	
	Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Организации, ДополнительныеОрганизации, Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("БезвозмездныеПоступления", БезвозмездныеПоступления);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("РасчетыПоВкладамВУставныйКапитал", РасчетыПоВкладамВУставныйКапитал); 
	Запрос.УстановитьПараметр("Организации", Организации);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОстатки.СуммаОстатокДт КАК РасчетыПоВкладамВУставныйКапитал,
		|	0 КАК БезвозмездныеПоступления
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет В (&РасчетыПоВкладамВУставныйКапитал),, Организация В
		|	(&Организации)) КАК ХозрасчетныйОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	ХозрасчетныйОстатки.СуммаОстатокКт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет В (&БезвозмездныеПоступления),, Организация В
		|	(&Организации)) КАК ХозрасчетныйОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	0
		|ИТОГИ
		|	СУММА(РасчетыПоВкладамВУставныйКапитал),
		|	СУММА(БезвозмездныеПоступления)
		|ПО
		|	ОБЩИЕ";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщийИтог.Следующий();
	
	ПоказателиРасчета = Новый Структура;
	ПоказателиРасчета.Вставить("РасчетыПоВкладамВУставныйКапитал", -ВыборкаОбщийИтог.РасчетыПоВкладамВУставныйКапитал);
	ПоказателиРасчета.Вставить("БезвозмездныеПоступления",         -ВыборкаОбщийИтог.БезвозмездныеПоступления);
	
	Возврат ПоказателиРасчета;
	
КонецФункции

#КонецОбласти

// Возвращает принимаемые к расчету активы и обязательства, а также стоимости
// чистых активов для каждой графы отчета.
// Подробнее о графах см. ГрафыРегламентированногоОтчета
//
// Параметры:
//  ПараметрыМакета  - Структура - структура параметров макета
//
// Возвращаемое значение:
//   Структура:
//      * ИтогоАктивы04 - Число - активы, принимаемые к расчету, на конец дня даты отчета
//      * ИтогоПассивы04 - Число - обязательства, принимаемые к расчету, на конец дня даты отчета
//      * ЧистыеАктивы04 - Число - стоимость чистых активов на конец дня даты отчета
//      * ИтогоАктивы05 - Число - активы, принимаемые к расчету, на конец прошлого года
//      * ИтогоПассивы05 - Число - обязательства, принимаемые к расчету, на конец прошлого года
//      * ЧистыеАктивы05 - Число - стоимость чистых активов на конец прошлого года
//      * ИтогоАктивы06 - Число - активы, принимаемые к расчету, на конец позапрошлого года
//      * ИтогоПассивы06 - Число - обязательства, принимаемые к расчету, на конец позапрошлого года
//      * ЧистыеАктивы06 - Число - стоимость чистых активов на конец позапрошлого года
//
Функция ПараметрыИтогов(ПараметрыМакета)
	
	ПараметрыИтогов = Новый Структура;
	
	Для Каждого Графа Из ГрафыРегламентированногоОтчета() Цикл
		
		ПараметрыИтогов.Вставить("ИтогоАктивы" + Графа,
		ПараметрыМакета["ВнеоборотныеАктивы" + Графа]
		+ ПараметрыМакета["ОборотныеАктивы" + Графа]
		+ ПараметрыМакета["РасчетыПоВкладамВУставныйКапитал" + Графа]);
		
		ПараметрыИтогов.Вставить("ИтогоПассивы" + Графа,
		ПараметрыМакета["ДолгосрочныеОбязательства" + Графа]
		+ ПараметрыМакета["КраткосрочныеОбязательства" + Графа]
		+ ПараметрыМакета["БезвозмездныеПоступления" + Графа]);
		
		ПараметрыИтогов.Вставить("ЧистыеАктивы" + Графа,
		ПараметрыИтогов["ИтогоАктивы" + Графа]
		- ПараметрыИтогов["ИтогоПассивы" + Графа]);
		
	КонецЦикла;
	
	//@skip-check constructor-function-return-section
	Возврат ПараметрыИтогов;
	
КонецФункции

// Функция возвращает массив номеров граф, используемых в алгоритме заполнения
// бухгалтерской отчетности.
// Для унификации наименований показателей на разные даты - дату отчета, конец
// прошлого года и конец позапрошлого года - используются одинаковые имена с
// разными постфиксами. Постфиксы "04", "05" и "06" обозначают соответственно
// дату отчета, конец прошлого года и конец позапрошлого года. Например,
// стоимости внеоборотных активов представляются показателями с именами
// "ВнеоборотныеАктивы04", "ВнеоборотныеАктивы05" и "ВнеоборотныеАктивы06".
// Эти постфиксы взяты из алгоритма заполнения бухгалтерской отчетности, где
// они соответствуют номерам граф таблицы баланса, поэтому для обозначения
// постфикса даты в имени показателя используется термин "Графа".
// Подробнее алгоритм заполнения бухгалтерской отчетности
// см. ЗаполнениеБухгалтерскойОтчетности2025Основная.ЗаполнитьОтчетБухОтчетностьФормаОтчета2025Кв1
//
// Возвращаемое значение:
//   Массив из Строка
//
Функция ГрафыРегламентированногоОтчета()
	
	Графы = Новый Массив;
	Графы.Добавить("04");
	Графы.Добавить("05");
	Графы.Добавить("06");
	
	Возврат Графы;
	
КонецФункции

// Возвращает данные расшифровки отчета. При расчете по бухгалтерской отчетности
// возвращается ссылка на документ-хранилище этой отчетности. При расчете по данных
// учета расшифровка не поддерживается - возвращается Неопределено.
// 
// Параметры:
//  ПараметрыОтчета - см. ПараметрыОтчета
// 
// Возвращаемое значение:
//  - ДокументСсылка.РегламентированныйОтчет
//  - Неопределено
//
Функция ДанныеРасшифровки(ПараметрыОтчета)

	Если ПараметрыОтчета.СпособЗаполнения = 1 Тогда
		Возврат ПараметрыОтчета.БухгалтерскаяОтчетность;
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

#КонецОбласти

#КонецЕсли
