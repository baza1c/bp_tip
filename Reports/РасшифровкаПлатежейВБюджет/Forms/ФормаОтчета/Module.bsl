
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ВидРасшифровки") Тогда
		ВызватьИсключение НСтр("ru = 'Отчет не предназначен для непосредственного использования.'");
	КонецЕсли;
	
	ВидОтчета = Параметры.ВидОтчета;
	
	ВсеВидыОтчета = Отчеты.РасшифровкаПлатежейВБюджет.ВидыОтчета();
	Если НЕ ВсеВидыОтчета.Свойство(ВидОтчета) Тогда
		ВызватьИсключение НСтр("ru = 'В параметрах открытия задан неподдерживаемый вид отчета.'");
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьТекстЗаголовка(ЭтотОбъект, ВидОтчета);
	
	Если ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОписаниеЗаданияФормированияОтчета, ОповещениеОЗавершении, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Отчет.РежимРасшифровки);
	ВариантМодифицирован = Ложь;
	ПользовательскиеНастройкиМодифицированы = НЕ Отчет.РежимРасшифровки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	БухгалтерскиеОтчетыКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "ЛичныйКабинетЕНС_ОбновлениеДанных"
		Или ИмяСобытия = "Запись_СведенияОбУплатеНалогов")
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("Организация")
		И Параметр.Организация = Отчет.Организация Тогда
		
		ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС();
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	ВыделенныеОбласти = Элементы.Результат.ПолучитьВыделенныеОбласти();
	Если ВыделенныеОбласти.Количество() > 0 Тогда
		ИнтервалОжидания = ?(ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая, 1, 0.2);
		ПодключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый", ИнтервалОжидания, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодсказкаИнтеграцияСЛичнымКабинетомЕНСОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ЕдиныйНалоговыйСчетИнтеграцияКлиентБП.ПодсказкаИнтеграцияСЛичнымКабинетомЕНСОбработкаНавигационнойСсылки(
		Отчет.Организация, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	ОчиститьСообщения();
	ОтказПроверкиЗаполнения = Ложь;
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	РезультатВыполнения.Свойство("ОтказПроверкиЗаполнения", ОтказПроверкиЗаполнения);
	
	Если ОтказПроверкиЗаполнения = Истина Тогда
		
		ПоказатьНастройки("");
		
	Иначе
		
		Если РезультатВыполнения.Статус = "Выполняется" Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);

		СкрытьНастройки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	СкрытьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	ОткрытьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПриИзмененииПериода(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НарастающимИтогом", Истина);
	ПараметрыФормы.Вставить("НачалоПериода", Отчет.НачалоПериода);
	ПараметрыФормы.Вставить("КонецПериода", Отчет.КонецПериода);
	ПараметрыФормы.Вставить("ВидПериода", ВидПериода);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаКвартал", ПараметрыФормы, Элементы.Период, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодОбработкаВыбора(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка,
		ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	
	ОбновитьТекстЗаголовка(ЭтотОбъект, ВидОтчета);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка,
		ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьПериод(Команда)
	
	Отчет.КонецПериода = КонецМесяца(ДобавитьМесяц(Отчет.КонецПериода, -3));
	ПриИзмененииПериода(Элементы.Период);
	
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьПериод(Команда)
	
	Отчет.КонецПериода = КонецМесяца(ДобавитьМесяц(Отчет.КонецПериода, 3));
	ПриИзмененииПериода(Элементы.Период);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка,
		ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбновитьТекстЗаголовка(ЭтотОбъект, ВидОтчета);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиент.ОрганизацияПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ОбработкаРасшифровкиСтандартногоОтчета(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	// Не будем обрабатывать нажатие на правую кнопку мыши.
	// Покажем стандартное контекстное меню ячейки табличного документа.
	Расшифровка = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодготовитьПараметрыОтчета()
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация"                      , Отчет.Организация);
	ПараметрыОтчета.Вставить("ВидОтчета"                        , ВидОтчета);
	ПараметрыОтчета.Вставить("НачалоПериода"                    , Отчет.НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода"                     , Отчет.КонецПериода);
	ПараметрыОтчета.Вставить("ВыводитьЗаголовок"                , Ложь);
	ПараметрыОтчета.Вставить("ВыводитьПодвал"                   , Ложь);
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	ПараметрыОтчета.Вставить("РежимРасшифровки"                 , Отчет.РежимРасшифровки);
	ПараметрыОтчета.Вставить("ДанныеРасшифровки"                , ДанныеРасшифровки);
	ПараметрыОтчета.Вставить("МакетОформления"                  , МакетОформления);
	ПараметрыОтчета.Вставить("СхемаКомпоновкиДанных"            , ПолучитьИзВременногоХранилища(СхемаКомпоновкиДанных));
	ПараметрыОтчета.Вставить("ИдентификаторОтчета"              , БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(ЭтотОбъект));
	ПараметрыОтчета.Вставить("НастройкиКомпоновкиДанных"        , Отчет.КомпоновщикНастроек.ПолучитьНастройки());
	ПараметрыОтчета.Вставить("ОтветственноеЛицо"                , ПредопределенноеЗначение("Перечисление.ОтветственныеЛицаОрганизаций.ОтветственныйЗаНалоговыеРегистры"));
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТекстЗаголовка(Форма, ВидОтчета)
	
	Отчет = Форма.Отчет;
	Форма.Период = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.НачалоПериода, Отчет.КонецПериода, Истина);
	
	НомерКвартала = Месяц(КонецКвартала(Отчет.КонецПериода))/3;
	
	Если ЭтоОтчетПоНалогуУСН(ВидОтчета) Тогда
		ЗаголовокОтчета = СтрШаблон(НСтр("ru = 'Налог УСН за %1'"), Форма.Период);
	ИначеЕсли ЭтоОтчетПоНДФЛ(ВидОтчета) Тогда
		ЗаголовокОтчета = СтрШаблон(НСтр("ru = 'НДФЛ за %1'"), Форма.Период);
	ИначеЕсли ЭтоОтчетПоТорговомуСбору(ВидОтчета) Тогда
		ЗаголовокОтчета = СтрШаблон(НСтр("ru = 'Уплаченный торговый сбор за %1'"), Форма.Период);
	ИначеЕсли ЭтоОтчетПоВзносам(ВидОтчета) Тогда
		ЗаголовокОтчета = СтрШаблон(НСтр("ru = 'Уплаченные страховые взносы за %1'"), Форма.Период);
	ИначеЕсли ЭтоОтчетПоВзносамПоСроку(ВидОтчета) Тогда
		ЗаголовокОтчета = СтрШаблон(НСтр("ru = 'Признанные по сроку страховые взносы за %1'"), Форма.Период);
	ИначеЕсли ЭтоОтчетПоВзносамИПнаОСН(ВидОтчета) Тогда
		ЗаголовокОтчета = СтрШаблон(НСтр("ru = 'Уплаченные страховые взносы за себя за %1'"), Форма.Период);
	ИначеЕсли ЭтоОтчетПоПодлежащимВзносамУСН(ВидОтчета) Тогда
		ЗаголовокОтчета =
			СтрШаблон(НСтр("ru = 'Страховые взносы за себя, включенные в расходы в %1'"), Форма.Период);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отчет.Организация) И Форма.ИспользуетсяНесколькоОрганизаций Тогда
		ЗаголовокОтчета = ЗаголовокОтчета + " " + БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Отчет.Организация);
	КонецЕсли;
	
	Форма.Заголовок = ЗаголовокОтчета;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоОтчетПоНалогуУСН(ВидОтчета)
	
	Возврат Отчеты.РасшифровкаПлатежейВБюджет.ЭтоОтчетПоНалогуУСН(ВидОтчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоОтчетПоНДФЛ(ВидОтчета)
	
	Возврат Отчеты.РасшифровкаПлатежейВБюджет.ЭтоОтчетПоНДФЛ(ВидОтчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоОтчетПоТорговомуСбору(ВидОтчета)
	
	Возврат Отчеты.РасшифровкаПлатежейВБюджет.ЭтоОтчетПоТорговомуСбору(ВидОтчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоОтчетПоВзносам(ВидОтчета)
	
	Возврат Отчеты.РасшифровкаПлатежейВБюджет.ЭтоОтчетПоВзносам(ВидОтчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоОтчетПоВзносамПоСроку(ВидОтчета)
	
	Возврат Отчеты.РасшифровкаПлатежейВБюджет.ЭтоОтчетПоВзносамПоСроку(ВидОтчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоОтчетПоВзносамИПнаОСН(ВидОтчета)
	
	Возврат Отчеты.РасшифровкаПлатежейВБюджет.ЭтоОтчетПоВзносамИПнаОСН(ВидОтчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоОтчетПоПодлежащимВзносамУСН(ВидОтчета)
	
	Возврат Отчеты.РасшифровкаПлатежейВБюджет.ЭтоОтчетПоПодлежащимВзносамУСН(ВидОтчета);
	
КонецФункции

&НаКлиенте
Функция ПолучитьЗапрещенныеПоля(Режим = "") Экспорт
	
	СписокПолей = Новый Массив;
	
	СписокПолей.Добавить("UserFields");
	СписокПолей.Добавить("DataParameters");
	СписокПолей.Добавить("SystemFields");
	СписокПолей.Добавить("Показатели");
	СписокПолей.Добавить("Параметры");
	СписокПолей.Добавить("Ресурсы");
	СписокПолей.Добавить("Группировки");
	СписокПолей.Добавить("Организация");
	СписокПолей.Добавить("Подразделение");
	
	Возврат Новый ФиксированныйМассив(СписокПолей);
	
КонецФункции

&НаСервере
Функция СформироватьОтчетНаСервере() Экспорт
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено, ОтказПроверкиЗаполнения", Истина, Истина);
	КонецЕсли;
	
	БухгалтерскиеОтчеты.ОтменитьВыполнениеЗадания(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета();
	
	ОписаниеЗаданияФормированияОтчета = БухгалтерскиеОтчеты.ЗапуститьПодготовкуОтчета(ЭтотОбъект, ПараметрыОтчета);
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	
	Возврат ОписаниеЗаданияФормированияОтчета;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения)

	БухгалтерскиеОтчеты.ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ЭтотОбъект);
	
	ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС();
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыВыбораЗначенияОтбора() Экспорт
	
	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Дата"              , Отчет.КонецПериода);
	СписокПараметров.Вставить("Организация"       , Отчет.Организация);
	
	Возврат СписокПараметров;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПериода(Элемент)
	
	ВыборПериодаКлиент.ПериодПриИзменении(Элемент, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	
	Если НЕ ПустаяСтрока(Период) Тогда
		Отчет.НачалоПериода = НачалоГода(Отчет.КонецПериода);
	КонецЕсли;
	
	ОбновитьТекстЗаголовка(ЭтотОбъект, ВидОтчета);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	ОбновитьТекстЗаголовка(ЭтотОбъект, ВидОтчета);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС()
	
	ЕдиныйНалоговыйСчетИнтеграцияВнутреннийБП.ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС(
		ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

#КонецОбласти
