#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//   Структура   - флаги, задающие необходимость дополнительных действий.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета",          Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета",           Истина);
	Результат.Вставить("ИспользоватьПередВыводомЭлементаРезультата",  Истина);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",           Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",               Истина);
	Результат.Вставить("ИспользоватьРасширенныеПараметрыРасшифровки", Истина);
	
	Возврат Результат;
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Схема        - СхемаКомпоновкиДанных - описание получаемых данных.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, 
		"Организация", 
		ПараметрыОтчета.Организация);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, 
		"НачалоПериода", 
		НачалоДня(ПараметрыОтчета.НачалоПериода));
	
	КонецПериода = ?(ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода), 
		КонецДня(ПараметрыОтчета.КонецПериода), 
		'39990101');
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, 
		"КонецПериода", 
		КонецПериода);
	
	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"РуководительДолжность",
		ОтветственныеЛица.РуководительДолжность);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"РуководительПредставление",
		ОтветственныеЛица.РуководительПредставление);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"ТекущаяДата",
		ОбщегоНазначенияБП.ТекущаяДатаНаСервере());
	
	ПростойУчетЕНС = ПараметрыОтчета.НачалоПериода >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	
	Если ПараметрыОтчета.ВариантОтчета = "РасчетыПоЕНС"
		И Не ПростойУчетЕНС Тогда
		
		ИмяОтчетаРасчетыПоНалогам = НСтр("ru='Расшифровка расчетов налогов по ЕНС'");
		ПериодОтчета = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
		ОстатокПоЕНСНаНачалоПериода = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(
			ПараметрыОтчета.Организация, ПараметрыОтчета.НачалоПериода);
		ОстатокПоЕНСНаКонецПериода = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(
			ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
		
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, 
			"ПериодОтчета", 
			ПериодОтчета);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, 
			"ОстатокПоЕНСНаНачалоПериода", 
			Формат(ОстатокПоЕНСНаНачалоПериода, "ЧДЦ=2; ЧН=0,00"));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, 
			"ОстатокПоЕНСНаКонецПериода", 
			Формат(ОстатокПоЕНСНаКонецПериода, "ЧДЦ=2; ЧН=0,00"));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, 
			"ИмяОтчетаРасчетыПоНалогам", 
			ИмяОтчетаРасчетыПоНалогам);
		
	КонецЕсли;
	
	Если ПараметрыОтчета.ВариантОтчета = "РасчетыПоНалогамНаЕНС"
		И ПростойУчетЕНС Тогда
		
		// Все обслуживаемые счета, кроме 68.10, для него есть отдельный запрос
		СчетаНалоговВзносов = ЕдиныйНалоговыйСчетПовтИсп.ОбслуживаемыеСчетаУчета(ПараметрыОтчета.КонецПериода);
		СчетаНалоговВзносов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			СчетаНалоговВзносов,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланыСчетов.Хозрасчетный.ПрочиеНалогиИСборы));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, 
			"СчетаНалоговВзносов", 
			СчетаНалоговВзносов);
		
		ПоказыватьСчетаУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
		Если Не ПоказыватьСчетаУчета Тогда
			СтруктураОтчета = КомпоновщикНастроек.Настройки.Структура;
			ГруппировкаНалог = БухгалтерскиеОтчеты.НайтиПоИмени(СтруктураОтчета, "Налог");
			ОтключаемыеПоля = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Новый ПолеКомпоновкиДанных("СчетНалога"));
			ОтключитьПоля(ГруппировкаНалог, ОтключаемыеПоля);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыОтчета.ВариантОтчета = "ВедомостьПоЕНС" 
		И ПростойУчетЕНС Тогда
		
		СчетаНалоговВзносов = ЕдиныйНалоговыйСчетПовтИсп.ОбслуживаемыеСчетаУчета(ПараметрыОтчета.КонецПериода);
		СчетаНалоговВзносов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			СчетаНалоговВзносов,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланыСчетов.Хозрасчетный.ПрочиеНалогиИСборы));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"СчетаНалоговВзносов",
			СчетаНалоговВзносов);
			
		ОстатокПоЕНСНаНачалоПериода = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(
			ПараметрыОтчета.Организация, НачалоДня(ПараметрыОтчета.НачалоПериода) - 1);
		ОстатокПоЕНСНаКонецПериода = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(
			ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
			
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, 
			"ОстатокПоЕНСНаНачалоПериода",
			ОстатокПоЕНСНаНачалоПериода);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, 
			"ОстатокПоЕНСНаКонецПериода",
			ОстатокПоЕНСНаКонецПериода);
			
	КонецЕсли;
	
КонецПроцедуры

// В процедуре можно уточнить особенности вывода данных в отчет.
//
// Параметры:
//  Контекст        - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  МакетКомпоновки - МакетКомпоновкиДанных - описание выводимых данных.
//
Процедура ПослеКомпоновкиМакета(Контекст, МакетКомпоновки) Экспорт
	
	Если Контекст.ВариантОтчета = "ВедомостьПоЕНС" Тогда 
		
		// Будем динамически определять необходимость вывода шапки таблицы по налогам
		Для Каждого Макет Из МакетКомпоновки.Макеты Цикл
			Если ТипЗнч(Макет.Макет) = Тип("МакетОбластиКомпоновкиДанных")
				И Макет.Макет.Количество() > 0 
				И Макет.Макет[0].Ячейки.Количество() > 0
				И Макет.Макет[0].Ячейки[0].Элементы.Количество() > 0 Тогда
				Элемент = Макет.Макет[0].Ячейки[0].Элементы[0];
				Если Элемент.Значение = "Наименование налога" Тогда
					Контекст.Вставить("МакетНалогиШапка", Макет.Имя);
					ПараметрНетНачислений = Макет.Параметры.Добавить(Тип("ПараметрОбластиВыражениеКомпоновкиДанных"));
					ПараметрНетНачислений.Имя       = "НетНачислений";
					ПараметрНетНачислений.Выражение = "ВЫБОР КОГДА ЕСТЬNULL(Сумма(НачислениеНалогов.Начислено), 0) = 0 ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ";
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// В процедуре можно уточнить особенности вывода в отчет отдельного элемента в структуре данных.
//
// Параметры:
//  Контекст          - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  МакетКомпоновки   - МакетКомпоновкиДанных - описание выводимых данных.
//  ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных - описание расшифровки для элемента в структуре данных.
//  ЭлементРезультата - ЭлементРезультатаКомпоновкиДанных - описание элемента в структуре данных.
//  ПропуститьЭлемент - Булево - если Истина, то не выводить эти данные в отчет.
//
Процедура ПередВыводомЭлементаРезультата(Контекст, МакетКомпоновки, ДанныеРасшифровки, ЭлементРезультата, ПропуститьЭлемент) Экспорт
	
	Если Контекст.ВариантОтчета = "ВедомостьПоЕНС" Тогда
		
		// Если не было начислений за период, то шапку таблицы по налогам не выводим
		Если Контекст.Свойство("МакетНалогиШапка")
			И ЭлементРезультата.Макет = Контекст.МакетНалогиШапка 
			И ЭлементРезультата.ЗначенияПараметров.Найти("НетНачислений") <> Неопределено Тогда
			ПропуститьЭлемент = ЭлементРезультата.ЗначенияПараметров.НетНачислений.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыРасшифровкиОтчета(Адрес, Расшифровка, ПараметрыРасшифровки) Экспорт
	
	ДанныеОбъекта = ПолучитьИзВременногоХранилища(Адрес);
	
	ОтчетОбъект       = ДанныеОбъекта.Объект;
	ДанныеРасшифровки = ДанныеОбъекта.ДанныеРасшифровки;
	
	ЭлементРасшифровки = ДанныеРасшифровки.Элементы[Расшифровка];
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") Тогда
		Возврат;
	КонецЕсли;
	
	ПоляРасшифровки = ЭлементРасшифровки.ПолучитьПоля();
	Если ПоляРасшифровки.Количество() > 0 Тогда
		Если ПоляРасшифровки[0].Значение = Null Тогда
			Возврат;
		ИначеЕсли ПоляРасшифровки[0].Поле = "Налог" Тогда
			Если ПоляРасшифровки[0].Значение = Справочники.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж Тогда
				
				СписокПунктовМеню = Новый СписокЗначений();
				СписокПунктовМеню.Добавить("РасшифровкаЗачетаАвансовНаЕНС", "РасшифровкаЗачетаАвансовНаЕНС");
				
				ПараметрыРасшифровки.Вставить("ОткрытьОбъект",     Ложь);
				ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
				ПараметрыРасшифровки.Вставить("ОткрытьФорму",      Истина);
				ПараметрыРасшифровки.Вставить("Форма",             "Отчет.СправкаРасчетЗачетАвансаПоЕдиномуНалоговомуСчету.ФормаОбъекта");
				
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("КлючВарианта",      "РасшифровкаЗачетаАвансовНаЕНС");
				ПараметрыФормы.Вставить("НачалоПериода",     ОтчетОбъект.НачалоПериода);
				ПараметрыФормы.Вставить("КонецПериода",      ОтчетОбъект.КонецПериода);
				ПараметрыФормы.Вставить("Организация",       ОтчетОбъект.Организация);
				ПараметрыФормы.Вставить("РежимРасшифровки",  Истина);
				ПараметрыФормы.Вставить("Налог",             Неопределено);
				ПараметрыФормы.Вставить("ПлатежныйДокумент", ПоляРасшифровки[1].Значение);
				ПараметрыФормы.Вставить("СрокУплаты",        Неопределено);
				
				ПараметрыРасшифровки.Вставить("ПараметрыФормы", ПараметрыФормы);
				
			Иначе
				
				СписокПунктовМеню = Новый СписокЗначений();
				СписокПунктовМеню.Добавить("РасшифровкаРасчетовНалоговПоЕНС", "РасшифровкаРасчетовНалоговПоЕНС");
				
				ПараметрыРасшифровки.Вставить("ОткрытьОбъект",     Ложь);
				ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
				ПараметрыРасшифровки.Вставить("ОткрытьФорму",      Истина);
				ПараметрыРасшифровки.Вставить("Форма",             "Отчет.СправкаРасчетЗачетАвансаПоЕдиномуНалоговомуСчету.ФормаОбъекта");
				
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("КлючВарианта",      "РасшифровкаРасчетовНалоговПоЕНС");
				ПараметрыФормы.Вставить("НачалоПериода",     ОтчетОбъект.НачалоПериода);
				ПараметрыФормы.Вставить("КонецПериода",      ОтчетОбъект.КонецПериода);
				ПараметрыФормы.Вставить("Организация",       ОтчетОбъект.Организация);
				ПараметрыФормы.Вставить("Налог",             ПоляРасшифровки[0].Значение);
				ПараметрыФормы.Вставить("ПлатежныйДокумент", Неопределено);
				ПараметрыФормы.Вставить("СрокУплаты",        ПоляРасшифровки[2].Значение);
				ПараметрыФормы.Вставить("РежимРасшифровки",  Истина);
				
				ПараметрыРасшифровки.Вставить("ПараметрыФормы", ПараметрыФормы);
				
			КонецЕсли;
		ИначеЕсли ПоляРасшифровки[0].Поле = "ЭтоРасшифровкаНачальноеСальдо"
			Или ПоляРасшифровки[0].Поле = "ЭтоРасшифровкаОплата"
			Или ПоляРасшифровки[0].Поле = "ЭтоРасшифровкаКонечноеСальдо" Тогда
			
			ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
			
			ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
			ДополнительныеСвойства.Вставить("Организация",   ОтчетОбъект.Организация);
			ДополнительныеСвойства.Вставить("НачалоПериода", ОтчетОбъект.НачалоПериода);
			ДополнительныеСвойства.Вставить("КонецПериода",  ОтчетОбъект.КонецПериода);
			ДополнительныеСвойства.Вставить("Счет",          ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("РежимРасшифровки",          Истина);
			ПараметрыФормы.Вставить("ВидРасшифровки",            2);
			ПараметрыФормы.Вставить("СформироватьПриОткрытии",   Истина);
			ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);

			СписокПунктовМеню = Новый СписокЗначений();
			СписокПунктовМеню.Добавить("ОборотноСальдоваяВедомостьПоСчету", НСтр("ru = 'ОСВ по счету 68.90'"));
			
			ПараметрыРасшифровки.Вставить("ОткрытьОбъект",     Ложь);
			ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
			ПараметрыРасшифровки.Вставить("ОткрытьФорму",      Истина);
			ПараметрыРасшифровки.Вставить("Форма",             "Отчет.ОборотноСальдоваяВедомостьПоСчету.Форма");
			ПараметрыРасшифровки.Вставить("ПараметрыФормы",    ПараметрыФормы);
			
		ИначеЕсли ПоляРасшифровки[0].Поле = "РасшифровкаНачисленоИтого" Тогда
			
			ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
			
			ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
			ДополнительныеСвойства.Вставить("Организация",          ОтчетОбъект.Организация);
			ДополнительныеСвойства.Вставить("НачалоПериода",        ОтчетОбъект.НачалоПериода);
			ДополнительныеСвойства.Вставить("КонецПериода",         ОтчетОбъект.КонецПериода);
			ДополнительныеСвойства.Вставить("Счет",                 ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет);
			ДополнительныеСвойства.Вставить("ПоСубсчетамКорСчетов", Истина);
			ДобавитьГруппировкиПоКорСчетам(ДополнительныеСвойства);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("РежимРасшифровки",          Истина);
			ПараметрыФормы.Вставить("ВидРасшифровки",            2);
			ПараметрыФормы.Вставить("СформироватьПриОткрытии",   Истина);
			ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
			
			СписокПунктовМеню = Новый СписокЗначений();
			СписокПунктовМеню.Добавить("АнализСчета", НСтр("ru = 'Анализ счета 68.90'"));
			
			ПараметрыРасшифровки.Вставить("ОткрытьОбъект",     Ложь);
			ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
			ПараметрыРасшифровки.Вставить("ОткрытьФорму",      Истина);
			ПараметрыРасшифровки.Вставить("Форма",             "Отчет.АнализСчета.Форма");
			ПараметрыРасшифровки.Вставить("ПараметрыФормы",    ПараметрыФормы);
			
		ИначеЕсли ПоляРасшифровки[0].Поле = "ЭтоРасшифровкаНалога" Тогда
			
			ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
			
			ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
			ДополнительныеСвойства.Вставить("Организация",          ОтчетОбъект.Организация);
			ДополнительныеСвойства.Вставить("НачалоПериода",        ОтчетОбъект.НачалоПериода);
			ДополнительныеСвойства.Вставить("КонецПериода",         ОтчетОбъект.КонецПериода);
			ДополнительныеСвойства.Вставить("Счет",                 ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет);
			ДополнительныеСвойства.Вставить("ПоСубсчетамКорСчетов", Истина);
			ДобавитьГруппировкиПоКорСчетам(ДополнительныеСвойства);
			
			ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
			ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "КорСчет", ПоляРасшифровки[2].Значение);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("РежимРасшифровки",          Истина);
			ПараметрыФормы.Вставить("ВидРасшифровки",            2);
			ПараметрыФормы.Вставить("СформироватьПриОткрытии",   Истина);
			ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
			
			СписокПунктовМеню = Новый СписокЗначений();
			СписокПунктовМеню.Добавить("АнализСчета", НСтр("ru = 'Анализ счета 68.90'"));
			
			ПараметрыРасшифровки.Вставить("ОткрытьОбъект",     Ложь);
			ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
			ПараметрыРасшифровки.Вставить("ОткрытьФорму",      Истина);
			ПараметрыРасшифровки.Вставить("Форма",             "Отчет.АнализСчета.Форма");
			ПараметрыРасшифровки.Вставить("ПараметрыФормы",    ПараметрыФормы);
			
		Иначе
			ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
			ПараметрыРасшифровки.Вставить("Значение",      ПоляРасшифровки[0].Значение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиБухгалтерскиеОтчеты

Функция ПолучитьТекстЗаголовка(Контекст) Экспорт
	
	Если Контекст.ВариантОтчета = "ВедомостьПоЕНС" Тогда
		Название = НСтр("ru='Ведомость по ЕНС'");
	ИначеЕсли Контекст.ВариантОтчета = "РасчетыПоНалогамНаЕНС" Тогда
		Название = НСтр("ru='Расчеты по налогам на ЕНС'");
	Иначе
		Название = НСтр("ru='Расчеты по ЕНС'");
	КонецЕсли;
	
	ЗаПериод = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		Контекст.НачалоПериода, 
		Контекст.КонецПериода);
	
	ЗаголовокОтчета = Название + ЗаПериод;
	Возврат ЗаголовокОтчета;
	
КонецФункции

Процедура ПослеВыводаРезультата(Контекст, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(Контекст.ИдентификаторОтчета, Результат);
	Результат.ФиксацияСверху = ВысотаЗаголовка(Контекст, Результат);
	
КонецПроцедуры

#КонецОбласти

Функция ОписаниеНалога(Налог, КодБК, КодПоОКТМО, РегистрацияВНалоговомОргане) Экспорт
	
	ОписаниеНалога = Новый Массив;
	
	Если Налог = Справочники.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж Тогда
		ОписаниеНалога.Добавить("КБК");
		ОписаниеНалога.Добавить(" ");
		ОписаниеНалога.Добавить(Строка(Справочники.ВидыНалоговИПлатежейВБюджет.КБК(Справочники.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж)));
		Возврат Новый ФорматированнаяСтрока(ОписаниеНалога);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодБК) Тогда
		ОписаниеНалога.Добавить("КБК");
		ОписаниеНалога.Добавить(" ");
		ОписаниеНалога.Добавить(Строка(КодБК));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		ОписаниеНалога.Добавить(Символы.ПС);
		ОписаниеНалога.Добавить(Строка(РегистрацияВНалоговомОргане));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодПоОКТМО) Тогда
		ОписаниеНалога.Добавить(Символы.ПС);
		ОписаниеНалога.Добавить("ОКТМО");
		ОписаниеНалога.Добавить(" ");
		ОписаниеНалога.Добавить(Строка(КодПоОКТМО))
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ОписаниеНалога);
	
КонецФункции

Функция ВысотаЗаголовка(Контекст, Результат)
	
	ВысотаЗаголовка = 0;
	
	ДанныеОбъекта = ПолучитьИзВременногоХранилища(Контекст.ДанныеРасшифровки);
	
	Если Не ЗначениеЗаполнено(ДанныеОбъекта) Тогда
		Возврат ВысотаЗаголовка;
	КонецЕсли;
	
	ОтчетОбъект       = ДанныеОбъекта.Объект;
	ДанныеРасшифровки = ДанныеОбъекта.ДанныеРасшифровки;
	
	// Начинаем перебирать первые ячейки таблицы, и если в ячейке обнаружится расшифровка,
	// то значит начались строки таблицы, их фиксировать уже не надо.
	Для НомерСтроки = 1 По Результат.ВысотаТаблицы Цикл
		
		Ячейка = Результат.Область(НомерСтроки, 1, НомерСтроки, 1);
		
		Если Ячейка.Расшифровка = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Расшифровка = ДанныеОбъекта.ДанныеРасшифровки.Элементы[Ячейка.Расшифровка];
		ПоляРасшифровки = Расшифровка.ПолучитьПоля();
		Если ПоляРасшифровки.Количество() > 0 Тогда
			Если ПоляРасшифровки[0].Значение = Null Тогда
				Продолжить;
			Иначе
				ВысотаЗаголовка = НомерСтроки - 1;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВысотаЗаголовка;
	
КонецФункции

Процедура ОтключитьПоля(Группировка, ОтключаемыеПоля)
	
	Если Группировка = Неопределено 
		Или Не ЗначениеЗаполнено(ОтключаемыеПоля) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Элемент Из Группировка.Выбор.Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ВыбранноеПолеКомпоновкиДанных") 
			И ОтключаемыеПоля.Найти(Элемент.Поле) <> Неопределено Тогда
			Элемент.Использование = Ложь;
		ИначеЕсли ТипЗнч(Элемент) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			ОтключитьПоля(Элемент, ОтключаемыеПоля);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьГруппировкуПоКорСчету(ПараметрыОтчета, Счет, ВидСубконто)
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	
	ПоСубконто = "";
	Представление = "";
	Если СвойстваСчета.УчетПоПодразделениям Тогда
		ПоСубконто = "-0";
	КонецЕсли;
	
	Для Сч = 1 По СвойстваСчета.КоличествоСубконто Цикл
		Если СвойстваСчета["ВидСубконто" + Сч] = ВидСубконто Тогда
			Знак = "+";
			Представление = СвойстваСчета["ВидСубконто" + Сч + "Наименование"];
		Иначе
			Знак = "-";
		КонецЕсли;
		ПоСубконто = ПоСубконто + Знак + Сч;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Представление) Тогда
		ГруппировкаКор = ПараметрыОтчета.ГруппировкаКор.Добавить();
		ГруппировкаКор.Использование = Истина;
		ГруппировкаКор.НомерСтроки   = ПараметрыОтчета.ГруппировкаКор.Количество();
		ГруппировкаКор.ПоСубконто    = ПоСубконто;
		ГруппировкаКор.ПоСубсчетам   = Истина;
		ГруппировкаКор.Представление = Представление;
		ГруппировкаКор.Счет          = Счет;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьГруппировкиПоКорСчетам(ДополнительныеСвойства)
	
	ПараметрыОтчета = Отчеты.АнализСчета.ПустыеПараметрыКомпоновкиОтчета();
	
	ДобавитьГруппировкуПоКорСчету(ПараметрыОтчета,
		ПланыСчетов.Хозрасчетный.ПрочиеНалогиИСборы,
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыНалогов);
	
	ДобавитьГруппировкуПоКорСчету(ПараметрыОтчета,
		ПланыСчетов.Хозрасчетный.ПрочиеДоходы,
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	
	ДобавитьГруппировкуПоКорСчету(ПараметрыОтчета,
		ПланыСчетов.Хозрасчетный.ПрочиеРасходы,
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);

	ЗначениеПараметра = ОбщегоНазначения.ТаблицаЗначенийВМассив(ПараметрыОтчета.ГруппировкаКор);
	ДополнительныеСвойства.Вставить("ГруппировкаКор", ЗначениеПараметра);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
