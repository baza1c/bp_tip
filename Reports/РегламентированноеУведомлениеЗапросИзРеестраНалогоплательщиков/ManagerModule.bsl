#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуПримененияФорматов() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуПримененияФорматов();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2026_1";
	Стр.КНД = "1110465";
	Стр.ВерсияФормата = "5.01";
	
	Возврат Результат;
КонецФункции

Функция ДанноеУведомлениеДоступноДляОрганизации() Экспорт 
	Возврат Истина;
КонецФункции

Функция ДанноеУведомлениеДоступноДляИП() Экспорт 
	Возврат Ложь;
КонецФункции

Функция ПолучитьОсновнуюФорму() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьФормуПоУмолчанию() Экспорт 
	Возврат "";
КонецФункции

Функция ПолучитьТаблицуФорм() Экспорт 
	Результат = Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюТаблицуФормУведомления();
	
	Стр = Результат.Добавить();
	Стр.ИмяФормы = "Форма2026_1";
	Стр.ОписаниеФормы = "В соответствии с приказом ФНС России от 13.11.2025 № ЕД-7-14/975@";
	
	Возврат Результат;
КонецФункции

Функция ЭлектронноеПредставление(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт
	Если ИмяФормы = "Форма2026_1" Тогда
		Возврат ЭлектронноеПредставление_Форма2026_1(Объект, УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция СформироватьСписокЛистов(Объект) Экспорт
	Если Объект.ИмяФормы = "Форма2026_1" Тогда 
		Возврат СформироватьСписокЛистовФорма2026_1(Объект);
	КонецЕсли;
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу(Объект, ИмяФормы, УникальныйИдентификатор) Экспорт 
	Если ИмяФормы = "Форма2026_1" Тогда 
		Данные = Объект.ДанныеУведомления.Получить();
		Возврат ПроверитьДокументСВыводомВТаблицу_Форма2026_1(Объект, 
			УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект), УникальныйИдентификатор);
	КонецЕсли;
КонецФункции

Функция ИдентификаторФайлаЭлектронногоПредставления(СведенияОтправки)
	Префикс = "UT_ZAPVIPEGRN";
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ИдентификаторФайлаЭлектронногоПредставления(Префикс, СведенияОтправки);
КонецФункции

Функция ПроверитьДокументСВыводомВТаблицу_Форма2026_1(Объект, Данные, УникальныйИдентификатор)
	ТаблицаОшибок = Новый СписокЗначений;
	УведомлениеОСпецрежимахНалогообложения.ПроверкаАктуальностиФормыПриВыгрузке(Объект.ИмяФормы, ТаблицаОшибок, ПолучитьТаблицуФорм());
	УведомлениеОСпецрежимахНалогообложения.ПроверкаДатВУведомлении(Данные, ТаблицаОшибок);
	УведомлениеОСпецрежимахНалогообложения.ПолнаяПроверкаЗаполненныхПоказателейНаСоотвествиеСписку(
		"СпискиВыбора2026_1", "СхемаВыгрузкиФорма2026_1",
		Данные.Объект.ИмяОтчета, ТаблицаОшибок, Данные);
	
	ЭтоЮЛ = РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Данные.Организация);
	Титульная = Данные.ДанныеУведомления.Титульная;
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаКодаНО(Титульная.КодНО, ТаблицаОшибок, "Титульная");
	Если Не ЗначениеЗаполнено(Титульная.ДАТА_ПОДПИСИ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указана дата подписи", "Титульная", "ДАТА_ПОДПИСИ"));
	КонецЕсли;
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаПодписантаНалоговойОтчетности(Данные, ТаблицаОшибок, "Титульная",
		ЭтоЮЛ Или Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2");
	Если Титульная.ПРИЗНАК_НП_ПОДВАЛ = "2" И Не ЗначениеЗаполнено(Титульная.НаимДок) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан документ представителя", "Титульная", "НаимДок"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ПРИЗНАК_НП_ПОДВАЛ) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак подписанта", "Титульная", "ПРИЗНАК_НП_ПОДВАЛ"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ПрЗаяв) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак заявителя", "Титульная", "ПрЗаяв"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Титульная.ПрВыписк) Тогда 
		ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
			"Не указан признак выписки", "Титульная", "ПрВыписк"));
	КонецЕсли;
	
	Если ЭтоЮЛ Тогда 
		УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИННКПП(Истина, Титульная, ТаблицаОшибок);
		Если Не ЗначениеЗаполнено(Титульная.НаимОрг) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указано наименование организации", "Титульная", "НаимОрг"));
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Титульная.ИНН) Тогда 
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(Титульная, ТаблицаОшибок, "Титульная", "ИНН", Истина, Ложь);
		Иначе
			Если ЗначениеЗаполнено(Титульная.ИдЕРН) Тогда
				Если Не СтрПодобнаПоРегулярномуВыражению(Титульная.ИдЕРН, "[0-9]{12}") Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Неправильно указан номер ЕРН", "Титульная", "ИдЕРН"));
				КонецЕсли;
			Иначе
				Если Не ЗначениеЗаполнено(Титульная.КодВидДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указан код документа", "Титульная", "КодВидДок"));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Титульная.СерНомДок) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указаны серия/номер документа", "Титульная", "СерНомДок"));
				КонецЕсли;
				Если Не ЗначениеЗаполнено(Титульная.ДатаРожд) Тогда 
					ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
						"Не указана дата рождения", "Титульная", "ДатаРожд"));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Титульная.ПрВыписк = "1" Тогда
		ЗапрВыпСеб = Данные.ДанныеУведомления.ЗапрВыпСеб;
		Если Не ЗначениеЗаполнено(ЗапрВыпСеб.ВидВыписк) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан вид выписки", "ЗапрВыпСеб", "ВидВыписк"));
		КонецЕсли;
		Если ЗапрВыпСеб.ВидВыписк = "3" Тогда 
			Если Не ЗначениеЗаполнено(ЗапрВыпСеб.ПрВыборСубРФ) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак выбора субъекта Российской Федерации", "ЗапрВыпСеб", "ПрВыборСубРФ"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ЗапрВыпСеб.ПрВыборОсн) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан признак выбора основания", "ЗапрВыпСеб", "ПрВыборОсн"));
			КонецЕсли;
			Если ЗапрВыпСеб.ПрВыборСубРФ = "2" Тогда 
				Для Каждого Стр Из Данные.ДанныеДопСтрокБД.МнгСтр1 Цикл 
					Если Не ЗначениеЗаполнено(Стр.Регион) Тогда 
						ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
							"Не указан код региона", "ЗапрВыпСеб", "Регион___" + (Данные.ДанныеДопСтрокБД.МнгСтр1.Индекс(Стр) + 1)));
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Если ЗапрВыпСеб.ПрВыборОсн = "2" Тогда 
				Для Каждого Стр Из Данные.ДанныеДопСтрокБД.МнгСтр2 Цикл 
					Если Не ЗначениеЗаполнено(Стр.Основание) Тогда 
						ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
							"Не указан код основания", "ЗапрВыпСеб", "Основание___" + (Данные.ДанныеДопСтрокБД.МнгСтр2.Индекс(Стр) + 1)));
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Титульная.ПрВыписк = "2" Тогда 
		ЗапрВыпТрЛицо = Данные.ДанныеУведомления.ЗапрВыпТрЛицо;
		Если Не ЗначениеЗаполнено(ЗапрВыпТрЛицо.ПрТрЛицо) Тогда 
			ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
				"Не указан признак третьего лица", "ЗапрВыпТрЛицо", "ПрТрЛицо"));
		КонецЕсли;
		
		Если ЗапрВыпТрЛицо.ПрТрЛицо = "1" Тогда 
			Если Не ЗначениеЗаполнено(ЗапрВыпТрЛицо.НаимОрг) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано наименование организации", "ЗапрВыпТрЛицо", "НаимОрг"));
			КонецЕсли;
			УведомлениеОСпецрежимахНалогообложенияСлужебный.ПроверкаИНН(
				ЗапрВыпТрЛицо, ТаблицаОшибок, "ЗапрВыпТрЛицо", "ИНН", Истина, Истина);
		ИначеЕсли ЗапрВыпТрЛицо.ПрТрЛицо = "2" Тогда 
			Если Не ЗначениеЗаполнено(ЗапрВыпТрЛицо.Фамилия) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указана фамилия", "ЗапрВыпТрЛицо", "Фамилия"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ЗапрВыпТрЛицо.Имя) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указано имя", "ЗапрВыпТрЛицо", "Имя"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ЗапрВыпТрЛицо.КодВидДок) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указан код документа", "ЗапрВыпТрЛицо", "КодВидДок"));
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ЗапрВыпТрЛицо.СерНомДок) Тогда 
				ТаблицаОшибок.Добавить(Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПустуюСтруктуруНавигацииПоОшибкам(
					"Не указаны серия/номер документа", "ЗапрВыпТрЛицо", "СерНомДок"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТаблицаОшибок;
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления_Форма2026_1(Объект, УникальныйИдентификатор)
	ОсновныеСведения = Документы.УведомлениеОСпецрежимахНалогообложения.НачальнаяИнициализацияОбщихРеквизитовВыгрузки(Объект);
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	Данные = Объект.ДанныеУведомления.Получить();
	ОсновныеСведения.Вставить("ИдЕРН", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Данные.ДанныеУведомления["Титульная"], "ИдЕРН"));
	ОсновныеСведения.Вставить("ПрВыписк", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Данные.ДанныеУведомления["Титульная"], "ПрВыписк"));
	ОсновныеСведения.Вставить("ВидВыписк", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Данные.ДанныеУведомления["ЗапрВыпСеб"], "ВидВыписк"));
	ОсновныеСведения.Вставить("ПрВыборСубРФ", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Данные.ДанныеУведомления["ЗапрВыпСеб"], "ПрВыборСубРФ"));
	ОсновныеСведения.Вставить("ПрВыборОсн", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Данные.ДанныеУведомления["ЗапрВыпСеб"], "ПрВыборОсн"));
	ОсновныеСведения.Вставить("ПрТрЛицо", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Данные.ДанныеУведомления["ЗапрВыпТрЛицо"], "ПрТрЛицо"));
	Возврат ОсновныеСведения;
КонецФункции

Функция ЭлектронноеПредставление_Форма2026_1(Объект, УникальныйИдентификатор)
	СведенияЭлектронногоПредставления = УведомлениеОСпецрежимахНалогообложения.СведенияЭлектронногоПредставления();
	
	ДанныеУведомления = УведомлениеОСпецрежимахНалогообложения.ДанныеУведомленияДляВыгрузки(Объект);
	Ошибки = ПроверитьДокументСВыводомВТаблицу_Форма2026_1(Объект, ДанныеУведомления, УникальныйИдентификатор);
	УведомлениеОСпецрежимахНалогообложения.СообщитьОшибкиПриПроверкеВыгрузки(Объект, Ошибки, ДанныеУведомления);
	
	ОсновныеСведения = ОсновныеСведенияЭлектронногоПредставления_Форма2026_1(Объект, УникальныйИдентификатор);
	СтруктураВыгрузки = Документы.УведомлениеОСпецрежимахНалогообложения.ИзвлечьСтруктуруXMLУведомления(Объект.ИмяОтчета, "СхемаВыгрузкиФорма2026_1");
	УведомлениеОСпецрежимахНалогообложения.ТиповоеЗаполнениеДанными(ДанныеУведомления, ОсновныеСведения, СтруктураВыгрузки);
	УведомлениеОСпецрежимахНалогообложения.ВыгрузитьДеревоВТаблицу(СтруктураВыгрузки, ОсновныеСведения, СведенияЭлектронногоПредставления);
	Возврат СведенияЭлектронногоПредставления;
КонецФункции

Функция СформироватьСписокЛистовФорма2026_1(Объект) Экспорт
	Листы = Новый СписокЗначений;
	СписокИспользуемыхМакетов = УведомлениеОСпецрежимахНалогообложения.ЗагрузитьМакетыИзАрхива(Объект, "", "ТабличныеДокументы");
	
	ПечатнаяФорма = УведомлениеОСпецрежимахНалогообложения.НовыйПустойЛист();
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	ИННКПП = УведомлениеОСпецрежимахНалогообложения.ТиповаяСтруктураИННКППДляПечати(Объект, СтруктураПараметров.ДанныеУведомления.Титульная);
	
	НомСтр = 0;
	УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
		СтруктураПараметров.ДанныеУведомления["Титульная"], НомСтр, 
		УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_Титульная_2026"),
		ПечатнаяФорма, ИННКПП);
	УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
	
	Если УведомлениеОСпецрежимахНалогообложенияКлиентСервер.СтраницаЗаполнена(СтруктураПараметров.ДанныеУведомления["ЗапрВыпСеб"]) Тогда 
		КоличествоСтраниц = Макс(1, Цел(СтруктураПараметров.ДанныеДопСтрокБД.МнгСтр1.Количество() / 91),
			Цел(СтруктураПараметров.ДанныеДопСтрокБД.МнгСтр2.Количество() / 26));
			
		Для Инд = 1 По КоличествоСтраниц Цикл 
			СтраницаПечати = ОбщегоНазначения.СкопироватьРекурсивно(СтруктураПараметров.ДанныеУведомления["ЗапрВыпСеб"]);
			
			МнгСтр = СтруктураПараметров.ДанныеДопСтрокБД.МнгСтр1;
			Для Ит = 1 По 91 Цикл 
				Позиция = 91*(Инд-1) + Ит - 1;
				СтраницаПечати.Вставить("Регион_" + Ит, ?(МнгСтр.Количество() > Позиция, МнгСтр[Позиция].Регион, ""))
			КонецЦикла;
			МнгСтр = СтруктураПараметров.ДанныеДопСтрокБД.МнгСтр2;
			Для Ит = 1 По 26 Цикл 
				Позиция = 26*(Инд-1) + Ит - 1;
				СтраницаПечати.Вставить("Основание_" + Ит, ?(МнгСтр.Количество() > Позиция, МнгСтр[Позиция].Основание, ""))
			КонецЦикла;
			
			УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
				СтраницаПечати, НомСтр, УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_ЗапрВыпСеб_2026"),
				ПечатнаяФорма, ИННКПП);
			УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
		КонецЦикла;
	КонецЕсли;
	
	Если УведомлениеОСпецрежимахНалогообложенияКлиентСервер.СтраницаЗаполнена(СтруктураПараметров.ДанныеУведомления["ЗапрВыпТрЛицо"]) Тогда 
		УведомлениеОСпецрежимахНалогообложения.НапечататьСтруктуру(Объект.ИмяОтчета,
			СтруктураПараметров.ДанныеУведомления["ЗапрВыпТрЛицо"], НомСтр, 
			УведомлениеОСпецрежимахНалогообложения.ПустойМакетИзСписка(Объект, СписокИспользуемыхМакетов, "Печать_ЗапрВыпТрЛицо_2026"),
			ПечатнаяФорма, ИННКПП);
		УведомлениеОСпецрежимахНалогообложения.ПоложитьПФВСписокЛистов(Объект, Листы, ПечатнаяФорма, НомСтр);
	КонецЕсли;
	
	Возврат Листы;
КонецФункции

#КонецОбласти

#КонецЕсли
