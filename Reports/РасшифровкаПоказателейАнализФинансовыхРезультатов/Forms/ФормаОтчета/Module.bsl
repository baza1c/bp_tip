
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не Параметры.Свойство("ВидРасшифровки") Тогда
		ВызватьИсключение НСтр("ru = 'Отчет не предназначен для непосредственного использования.'");
	КонецЕсли;

	Если Параметры.Свойство("АдресНастроек") Тогда
		ПараметрыРасшифровки = ПолучитьИзВременногоХранилища(Параметры.АдресНастроек);
	КонецЕсли;
	
	Если ПараметрыРасшифровки <> Неопределено Тогда
		
		Если ПараметрыРасшифровки.Свойство("ДеталиРасшифровки") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРасшифровки.ДеталиРасшифровки);
			Счет = УстановитьСчетПоПоказателю(Показатель);
		КонецЕсли;
		
	КонецЕсли;
	
	Отчет.ПоказыватьБыстрыеНастройки = Истина;
	
	БухгалтерскиеОтчетыВызовСервера.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

	БыстрыеНастройкиОтчетовСервер.ПриСозданииНаСервере(ЭтотОбъект);
	
	ВестиУчетПоПодразделениям = БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям();
	
	ПравоДоступаКДаннымБухгалтерии = УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии();

	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Отчет.РасшифровкаПоказателейАФР",
		"ФормаОтчета",
		НСтр("ru='Новости: Расшифровка показателей анализа финансовых результатов'"),
		"ПриОткрытии");
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	Если Отчет.Группировка.Количество() = 0 Тогда
		ЗаполнитьГруппировкиПоУмолчанию(ЭтотОбъект, ВестиУчетПоПодразделениям); 
	КонецЕсли;
	
	БухгалтерскиеОтчеты.ИнициализироватьРежимВыгрузкиБП(ЭтотОбъект);
	
	Если Отчет.ПоказыватьБыстрыеНастройки Тогда
		ПоказатьБыстрыеНастройкиНаСервере();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОписаниеЗаданияФормированияОтчета, ОповещениеОЗавершении, ПараметрыОжидания);

	ИначеЕсли Отчет.РежимРасшифровки И ПравоДоступаКДаннымБухгалтерии Тогда
		БухгалтерскийУчетКлиентПереопределяемый.ПослеФормированияОтчета(ЭтотОбъект);
	Иначе
		ПодключитьОбработчикОжидания("Подключаемый_СформироватьПриОткрытии", БухгалтерскиеОтчетыКлиент.ИнтервалЗапускаФормированияОтчетаПриОткрытии(), Истина);
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Отчет.РежимРасшифровки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	БухгалтерскиеОтчетыКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки)
	
	Если ИспользуютсяСтандартныеНастройки
		И Не (Параметры.Свойство("РежимРасшифровки") И Параметры.РежимРасшифровки) Тогда
		
		БыстрыеНастройкиОтчетовКлиентСервер.ПереключитьВидимостьБыстрыхНастроек(ЭтотОбъект);
		Возврат;
		
	КонецЕсли;
	
	Если Не КомпоновщикИнициализирован Тогда
		ПользовательскиеНастройки = ПоместитьВоВременноеХранилище(Настройки, УникальныйИдентификатор);
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервереВОтчетеРуководителю(ЭтотОбъект, Настройки);
	
	РежимРасшифровки = Параметры.Свойство("РежимРасшифровки") И Параметры.РежимРасшифровки;
	Если Не КомпоновщикИнициализирован И (ОбщегоНазначения.ЭтоВебКлиент() Или РежимРасшифровки) Тогда
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	Если ИнформационнаяБазаФайловая Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	КонецЕсли;
	
	БыстрыеНастройкиОтчетовКлиентСервер.ПереключитьВидимостьБыстрыхНастроек(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	ВестиУчетПоПодразделениям = БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям();
	УстановитьНастройкиПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОбработкаОповещенияАктуализации(ЭтотОбъект, Отчет.Организация,
		Отчет.КонецПериода, ИмяСобытия, Параметр, Источник);

	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.РассылкиОтчетов.Форма.НастройкаРассылкиБП" Тогда
		ОбработкаНастройкиРассылкиОтчета(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область Период

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	НастройкиИнициализированы = Ложь;
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)

	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	НастройкиИнициализированы = Ложь;
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
		
	ОбновитьСписокПоказателейНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область Показатель

&НаКлиенте
Процедура ПоказательПриИзменении(Элемент)
	
	ОтборПоПодразделению = Новый Структура("Поле", "Подразделение");
	ГруппировкаПодразделение = Отчет.Группировка.НайтиСтроки(ОтборПоПодразделению);
	Если ГруппировкаПодразделение.Количество() > 0 Тогда
		ГруппировкаПодразделениеВключена = ГруппировкаПодразделение[0].Использование;
	КонецЕсли;
	ЭтоРасчетныйПоказатель = ЭтоРасчетныйПоказатель(Показатель);
	НастройкиИнициализированы = Ложь;
	ПоляОтбора = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	ПолеПоказатель = Новый ПолеКомпоновкиДанных("Показатель");
	ПолеПоказательРасчетный = Новый ПолеКомпоновкиДанных("ПоказательРасчетный");
	
	Для Каждого ЭлементОтбора Из ПоляОтбора Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = ПолеПоказатель 
			Или ЭлементОтбора.ЛевоеЗначение = ПолеПоказательРасчетный Тогда
			
			Если ЭлементОтбора.ПравоеЗначение = Показатель Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭтоРасчетныйПоказатель Тогда
				ЭлементОтбора.ЛевоеЗначение = ПолеПоказательРасчетный;
			Иначе
				ЭлементОтбора.ЛевоеЗначение = ПолеПоказатель;
			КонецЕсли;
			
			ЭлементОтбора.Использование = Истина;
			ЭлементОтбора.ПравоеЗначение = Показатель;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");

		КонецЕсли;
	КонецЦикла;
			
	ОбновитьТекстЗаголовка(ЭтотОбъект);

	Если ЭтоРасчетныйПоказатель Тогда
		ТекущийВариант = "РасшифровкаРасчетныхПоказателей";
	Иначе
		ТекущийВариант = "РасшифровкаПоказателейАнализаФинансовыхРезультатов";
	КонецЕсли;
	
	Если КлючТекущегоВарианта <> ТекущийВариант Тогда
		
		ИзменитьТекущийВариант(ТекущийВариант);
		ИзменениеСхемыКомпоновкиДанныхДляНастроекНаСервере();
		
	КонецЕсли;
	
	Счет = УстановитьСчетПоПоказателю(Показатель);

	ЗаполнитьГруппировкиПоУмолчанию(ЭтотОбъект, ВестиУчетПоПодразделениям, ГруппировкаПодразделениеВключена);
		
КонецПроцедуры

#КонецОбласти

#Область Организация

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияБПКлиентСервер.ОбработкаОтменыВыбораОрганизации(
		ПолеОрганизация,
		Отчет.Организация,
		Отчет.ВключатьОбособленныеПодразделения);

	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	НастройкиИнициализированы = Ложь;
	
	БухгалтерскиеОтчетыКлиент.ОрганизацияПриИзменении(ЭтотОбъект, Элемент);
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
	
	ОбновитьСписокПоказателейНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, 
		СоответствиеОрганизаций, Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
КонецПроцедуры

#КонецОбласти

#Область Результата

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ОбработкаРасшифровкиСтандартногоОтчета(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	// Не будем обрабатывать нажатие на правую кнопку мыши.
	// Покажем стандартное контекстное меню ячейки табличного документа.
	Расшифровка = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		"Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриИзменении(Элемент)
	
	РучнаяКорректировка = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Отборы

&НаКлиенте
Процедура ОтборыПриИзменении(Элемент)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПриИзменении(ЭтотОбъект, Элемент, Ложь);
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);

КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломИзменения(Элемент, Отказ)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПравоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокПараметров = ПолучитьПараметрыВыбораЗначенияОтбора();
	БухгалтерскиеОтчетыКлиент.ОтборыПравоеЗначениеНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора,
		СтандартнаяОбработка, СписокПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область Оформление

&НаКлиенте
Процедура МакетОформленияПриИзменении(Элемент)
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Отчет.КомпоновщикНастроек.Настройки, "МакетОформления",
		МакетОформления);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьЗаголовокПриИзменении(Элемент)

	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПодвалПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовБыстрыхНастроек
&НаКлиенте
Процедура ВыводитьЗаголовокБыстрыеНастройкиПриИзменении(Элемент)
	
	ПриИзмененииНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПодвалБыстрыеНастройкиПриИзменении(Элемент)
	
	ПриИзмененииНастройки();

КонецПроцедуры 

&НаКлиенте
Процедура МакетОформленияБыстрыеНастройкиПриИзменении(Элемент)
		
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Отчет.КомпоновщикНастроек.Настройки,
		"МакетОформления", МакетОформления);
	
	ПриИзмененииНастройки();

КонецПроцедуры

&НаКлиенте
Процедура ОкруглениеСуммПриИзменении(Элемент)

	ПриИзмененииНастройки();

КонецПроцедуры

&НаКлиенте
Процедура ОтрицательноеКраснымПриИзменении(Элемент)
	
	ПриИзмененииНастройки();
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеПоделитьсяМнениемОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьОтзывПоЭлектроннойПочте", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыйПараметрыАктуализацииОтчета();
	ПараметрыАктуализации.Вставить("Организация",                       Отчет.Организация);
	ПараметрыАктуализации.Вставить("ВключатьОбособленныеПодразделения", Отчет.ВключатьОбособленныеПодразделения);
	ПараметрыАктуализации.Вставить("ДатаАктуальности",                  ДатаАктуальности);
	ПараметрыАктуализации.Вставить("ДатаОкончанияАктуализации",         Отчет.КонецПериода);

	ЗакрытиеМесяцаКлиент.ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		ЭтотОбъект,
		ПараметрыАктуализации);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Если Элементы.Результат.ОтображениеСостояния.Видимость Тогда
		
		ТекущийЭлемент = Элементы.СформироватьОтчет;
		ПоказатьПредупреждение( , НСтр("ru = 'Нажмите ""Сформировать"" для получения отчета'"), ,
								 НСтр("ru = 'Отчет не сформирован'"));
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Результат",     Результат);
	ПараметрыОтчета.Вставить("Организация",   Отчет.Организация);
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Отчет.ВключатьОбособленныеПодразделения);
	ПараметрыОтчета.Вставить("НачалоПериода", Отчет.НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода",  Отчет.КонецПериода); 
	ПараметрыОтчета.Вставить("РазрезПродаж",  Отчет.РазрезПродаж);
	ПараметрыОтчета.Вставить("РучнаяКорректировка", РучнаяКорректировка);
	ПараметрыОтчета.Вставить("РучнаяНастройка",
		БухгалтерскиеОтчетыКлиент.ЕстьОтличияНастроекОтЭталонныхБП(ПараметрыРежимаВыгрузки, Отчет));
	
	Закрыть(ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	Если КлючТекущегоВарианта = "АнализФинансовыхРезультатовЗаПериод" 
		И Отчет.КонецПериода > КонецГода(Отчет.НачалоПериода) Тогда 
		
		ТекущийЭлемент = Элементы.СформироватьОтчет;
		ПоказатьПредупреждение( , НСтр("ru = 'Выбор периода отчета возможен только в пределах одного календарного года'"),,
			НСтр("ru = 'Отчет не сформирован'"));
			
		Возврат;
		
	КонецЕсли;
	
	ЗапуститьФормированиеОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура Актуализировать(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.Актуализировать(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьАктуализацию(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	КнопкаДействия = ?(Элементы.Выгрузить.Видимость, Элементы.Выгрузить, Элементы.СформироватьОтчет);
	КнопкаДействия.КнопкаПоУмолчанию = Истина;
	
	СкрытьНастройки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	Если Не НастройкиИнициализированы Тогда
		ИзменениеСхемыКомпоновкиДанныхДляНастроекНаСервере();
	КонецЕсли;
	
	ОткрытьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочте(Команда)
	
	ОтправкаПочтовыхСообщенийКлиент.ОтправитьОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", Отчет.НачалоПериода, Отчет.КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРассылкуОтчета(Команда)
	
	ЗаполнитьНастройкиОтчетаДляРассылки();
	
	РассылкаОтчетовБПКлиент.НастроитьРассылкуИзОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБыстрыеНастройки(Команда)
	
	Отчет.ПоказыватьБыстрыеНастройки = Не Отчет.ПоказыватьБыстрыеНастройки;
	ПереключитьВидимостьБыстрыхНастроекНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаСнятьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Отчет.Группировка Цикл
		СтрокаТаблицы.Использование = Ложь;
	КонецЦикла;
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

#Область Группировка

&НаКлиенте
Процедура ГруппировкаУстановитьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Отчет.Группировка Цикл
		СтрокаТаблицы.Использование = Истина;
	КонецЦикла;
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкамиПоУмолчанию(ЗаполняемыеНастройки) Экспорт
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, "ПриОткрытии");

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Отчет    = Форма.Отчет;
	
	Отчет.НачалоПериода = НачалоМесяца(Отчет.НачалоПериода);

	Если НачалоМесяца(Отчет.НачалоПериода) = НачалоМесяца(Отчет.КонецПериода) Тогда
		Форма.ПериодСтрокой = Формат(Отчет.КонецПериода, "Л=ru_RU; ДФ='ММММ гггг'");
	Иначе 
		Форма.ПериодСтрокой = СтрШаблон("%1 - %2", Формат(Отчет.НачалоПериода, "Л=ru_RU; ДФ=ММММ"),
			Формат(Отчет.КонецПериода, "Л=ru_RU; ДФ='ММММ гггг'"));
	КонецЕсли;
	
	ОбновитьТекстЗаголовка(Форма);
	
	ОбновитьСписокПоказателей(Форма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьГруппировкиПоУмолчанию(Форма, ВестиУчетПоПодразделениям, ГруппировкаПодразделениеВключена = Ложь)
	
	Отчет = Форма.Отчет;
	Отчет.Группировка.Очистить();
	
	Если ВестиУчетПоПодразделениям Тогда
		НоваяГруппировка                = Отчет.Группировка.Добавить();
		НоваяГруппировка.Поле           = "Подразделение";
		НоваяГруппировка.Представление  = "Подразделение";
		НоваяГруппировка.Использование  = ГруппировкаПодразделениеВключена;
		НоваяГруппировка.ТипГруппировки = 0;
	КонецЕсли;
	
	СубконтоСчета = СубконтоСчета(Форма.Счет);
	КоличествоСубконто = СубконтоСчета.Количество();
	Для НомерСубконто = 1 По КоличествоСубконто Цикл
		НоваяГруппировка                = Отчет.Группировка.Добавить();
		НоваяГруппировка.Поле           = СтрШаблон("Субконто%1", НомерСубконто);
		НоваяГруппировка.Представление  = СубконтоСчета.Получить(НомерСубконто).Представление;
		НоваяГруппировка.Использование  = Истина;
		НоваяГруппировка.ТипГруппировки = 0;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиПоУмолчанию()
	
	БухгалтерскиеОтчетыВызовСервера.УстановитьНастройкиПоУмолчанию(ЭтотОбъект);
	ЗаполнитьГруппировкиПоУмолчанию(ЭтотОбъект, ВестиУчетПоПодразделениям);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
		
	ОбновитьСписокПоказателейНаСервере();

	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	НастройкиИнициализированы = Ложь;
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТекстЗаголовка(Форма)
	
	Отчет = Форма.Отчет;
	
	Название = СтрШаблон(НСтр("ru='%1'"), Форма.Показатель);
	
	ЗаголовокОтчета = Название + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		Отчет.НачалоПериода, Отчет.КонецПериода);
	
	Если ЗначениеЗаполнено(Отчет.Организация) И Форма.ИспользуетсяНесколькоОрганизаций Тогда
		
		ТекстОрганизации = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Отчет.Организация,
			Отчет.ВключатьОбособленныеПодразделения);
			
		ЗаголовокОтчета = СтрШаблон("%1 %2", ЗаголовокОтчета, ТекстОрганизации);
			
	КонецЕсли;
	
	Форма.Заголовок = ЗаголовокОтчета;

КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗапрещенныеПоля(Режим = "") Экспорт

	СписокПолей = Новый Массив;
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	
	Для НомерСубконто = СвойстваСчета.КоличествоСубконто + 1 По 3 Цикл
		СписокПолей.Добавить(СтрШаблон("Субконто%1", НомерСубконто));
	КонецЦикла;
	
	Для НомерСубконто = КоличествоКорСубконто + 1 По 3 Цикл
		СписокПолей.Добавить(СтрШаблон("КорСубконто%1", НомерСубконто));
	КонецЦикла;
	
	СписокПолей.Добавить("UserFields");
	СписокПолей.Добавить("DataParameters");
	СписокПолей.Добавить("SystemFields");
	СписокПолей.Добавить("Год");
	СписокПолей.Добавить("Месяц");
	СписокПолей.Добавить("Выручка");
	СписокПолей.Добавить("Договор");
	СписокПолей.Добавить("Документ");
	СписокПолей.Добавить("ДоговорПредставление");
	СписокПолей.Добавить("ВидСтроки");
	СписокПолей.Добавить("Порядок");
	СписокПолей.Добавить("КонтрагентПредставление");	
	СписокПолей.Добавить("НоменклатураПредставление");
	СписокПолей.Добавить("ВаловаяПрибыль");
	СписокПолей.Добавить("ПредставлениеПериода"); 
	СписокПолей.Добавить("Валюта");
	СписокПолей.Добавить("Показатель");
	СписокПолей.Добавить("ПоказательРасчетный");
	СписокПолей.Добавить("РеализуемыйАктив");
	СписокПолей.Добавить("Счет");
	СписокПолей.Добавить("КорСчет");
	СписокПолей.Добавить("СчетДт");
	СписокПолей.Добавить("СчетКт");
	СписокПолей.Добавить("ДокументТип");
	СписокПолей.Добавить("Дата");
	СписокПолей.Добавить("ПодразделениеКор");
	СписокПолей.Добавить("ВалютаКор");
	СписокПолей.Добавить("ВалютаДт");
	СписокПолей.Добавить("ВалютаКт");
	СписокПолей.Добавить("ПодразделениеДт");
	СписокПолей.Добавить("ПодразделениеКт"); 
	Если КлючТекущегоВарианта = "РасшифровкаРасчетныхПоказателей" Тогда
		СписокПолей.Добавить("Показатель");
	Иначе
		СписокПолей.Добавить("ПоказательФормулы"); 
	КонецЕсли;
	СписокПолей.Добавить("ПодразделениеКт");
	
	СписокПолей.Добавить("СубконтоКт1");
	СписокПолей.Добавить("СубконтоКт2");
	СписокПолей.Добавить("СубконтоКт3");
	СписокПолей.Добавить("СубконтоДт1");
	СписокПолей.Добавить("СубконтоДт2");
	СписокПолей.Добавить("СубконтоДт3");

	СписокПолей.Добавить("СчетРасчетный");
	СписокПолей.Добавить("КорСчетРасчетный");
	СписокПолей.Добавить("Номер");
	СписокПолей.Добавить("ЭлементФормулы");


	СписокПолей.Добавить("ИтоговаяСумма");
	СписокПолей.Добавить("ПрибыльУбытокОтПродаж");
	СписокПолей.Добавить("СебестоимостьПродаж");

	Если Не ВестиУчетПоПодразделениям Тогда
		
		СписокПолей.Добавить("Подразделение");
		
	КонецЕсли;
	
	СписокПолей.Добавить("Период");
	
	Если Режим = "Выбор" Тогда 
		БухгалтерскиеОтчетыКлиент.ДобавитьПоляРесурсовВЗапрещенныеПоля(ЭтотОбъект, СписокПолей);
	ИначеЕсли Режим = "Группировка" Или Режим = "Отбор" Или Режим = "Порядок" Тогда
		СписокПолей.Добавить("Контрагент");
		СписокПолей.Добавить("Номенклатура");
		СписокПолей.Добавить("НоменклатурнаяГруппа");
		БухгалтерскиеОтчетыКлиент.ДобавитьПоляРесурсовВЗапрещенныеПоля(ЭтотОбъект, СписокПолей);
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(СписокПолей);
	
КонецФункции

&НаСервере
Функция СформироватьОтчетНаСервере() Экспорт
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено, ОтказПроверкиЗаполнения", Истина, Истина);
	КонецЕсли;
	
	БухгалтерскиеОтчеты.ОтменитьВыполнениеЗадания(ЭтотОбъект);
	
	БухгалтерскиеОтчеты.ЗаписатьОперациюБизнесСтатистики(ЭтотОбъект, "СформироватьОтчет", НастройкиОтчетаДляСтатистики());
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	Настройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
	Настройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
	Настройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";
	Настройки.ДополнительныеСвойства.Вставить("ВыводитьЗаголовок"                   , ВыводитьЗаголовок);
	Настройки.ДополнительныеСвойства.Вставить("ВыводитьПодвал"                      , ВыводитьПодвал);
	Настройки.ДополнительныеСвойства.Вставить("МаксимальноеКоличествоКорСубконто"   , 0);
	
	// Быстрые настройки
	ЗаполняемыеСвойства = Новый Массив;
	ЗаполняемыеСвойства.Добавить("ОтрицательноеКрасным");
	ЗаполняемыеСвойства.Добавить("ОкруглениеСумм");
	ЗаполняемыеСвойства.Добавить("МакетОформления");
	Для Каждого Свойство Из ЗаполняемыеСвойства Цикл
		Если Свойство = "МакетОформления" Тогда
			БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Отчет.КомпоновщикНастроек.Настройки,
				"МакетОформления", МакетОформления);
		Иначе
			ПереключитьНастройкуОформления(Свойство);
		КонецЕсли;
	КонецЦикла;
			
	ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();
	
	Если БыстрыеНастройкиОтчетовСервер.ЭтоОкруглениеСуммДоТысяч(ОкруглениеСумм) Тогда
		ПараметрыОтчета.ВыводитьПримечанияОкругления = Истина;
	Иначе
		ПараметрыОтчета.ВыводитьПримечанияОкругления =
			БыстрыеНастройкиОтчетовСервер.РежимОкругленияСуммВТысячах(Отчет.КомпоновщикНастроек.Настройки);
	КонецЕсли;
		
	ОписаниеЗаданияФормированияОтчета = БухгалтерскиеОтчеты.ЗапуститьПодготовкуОтчета(ЭтотОбъект, ПараметрыОтчета);
	
	КнопкаДействия = ?(Элементы.Выгрузить.Видимость, Элементы.Выгрузить, Элементы.СформироватьОтчет);
	КнопкаДействия.КнопкаПоУмолчанию = Истина;
	
	Возврат ОписаниеЗаданияФормированияОтчета;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
	БухгалтерскийУчетКлиентПереопределяемый.ПослеФормированияОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения)

	БухгалтерскиеОтчеты.ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
			Результат, КэшВыделеннойОбласти);
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНастройки(ПоказыватьБыстрыеНастройки)

	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;

	Отчет.ПоказыватьБыстрыеНастройки = ПоказыватьБыстрыеНастройки;
	
	ПереключитьВидимостьБыстрыхНастроекНаКлиенте();

	
КонецПроцедуры

&НаСервере
Процедура ПоказатьБыстрыеНастройкиНаСервере()
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	ГруппыНастроек = Новый Структура;
	ГруппыНастроек.Вставить("Отбор", Элементы.БыстрыеОтборы);
	ГруппыНастроек.Вставить("Оформление", Элементы.БыстроеОформление);
	
	БыстрыеНастройкиОтчетовСервер.ПоказатьБыстрыеНастройкиНаСервере(ЭтотОбъект, ГруппыНастроек);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОчисткаОтбора(Элемент)
	
	Если Не СоответствиеПолейОтчетаИРеквизитов.Свойство(Элемент.Имя) Тогда
		Возврат;
	КонецЕсли;

	ИмяРеквизита = Элемент.Имя;
	ОчиститьОтборНаСервере(ИмяРеквизита);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьОтборПриИзменении(Элемент)
	
	Если Не СоответствиеПолейОтчетаИРеквизитов.Свойство(Элемент.Имя) Тогда
		Возврат;
	КонецЕсли;
	ИмяРеквизита = Элемент.Имя;
	УстановитьОтборПриИзмененииНаСервере(ИмяРеквизита);

	ПриИзмененииНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокПараметров = ПолучитьПараметрыВыбораЗначенияОтбора();
	БыстрыеНастройкиОтчетовКлиент.ОтборыПравоеЗначениеНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора,
		СтандартнаяОбработка, СписокПараметров);

КонецПроцедуры

&НаСервере
Процедура ОчиститьОтборНаСервере(ИмяРеквизита)
	
	БыстрыеНастройкиОтчетовСервер.ОчиститьОтборНаСервере(ЭтотОбъект, ИмяРеквизита);
	
КонецПроцедуры

&НаСервере
Процедура ПереключитьНастройкуОформления(ИмяНастройки)
	
	БыстрыеНастройкиОтчетовСервер.ПереключитьНастройкуОформления(ЭтотОбъект, ИмяНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНастройки()

	Если Отчет.ФормироватьОтчетПриИзмененииНастроек Тогда
		ЗапуститьФормированиеОтчета();
	Иначе
		Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыВыбораЗначенияОтбора()
	
	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Организация"       , Отчет.Организация);
	СписокПараметров.Вставить("Контрагент"        , Неопределено);
	СписокПараметров.Вставить("ДоговорКонтрагента", Неопределено);
	
	Возврат СписокПараметров;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_СформироватьПриОткрытии()
	
	ЗапуститьФормированиеОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета()
	
	ОчиститьСообщения();
	ОтказПроверкиЗаполнения = Ложь;
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	РезультатВыполнения.Свойство("ОтказПроверкиЗаполнения", ОтказПроверкиЗаполнения);
	
	Если ОтказПроверкиЗаполнения = Истина Тогда
		
		ПоказатьНастройки("");
		
	Иначе
		
		Если РезультатВыполнения.Статус = "Выполняется" Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);

		СкрытьНастройки(Отчет.ПоказыватьБыстрыеНастройки);
		
		Если ВыводитьПодвал Тогда
			ВысотаПустогоОтчета = 11;
		Иначе
			ВысотаПустогоОтчета = 5;
		КонецЕсли;
		
		Если Результат.ВысотаТаблицы <= ВысотаПустогоОтчета Тогда
			ОтображениеСостояния = Элементы.Результат.ОтображениеСостояния;
			ОтображениеСостояния.Видимость                      = Истина;
			ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
			ОтображениеСостояния.Текст                          = НСтр("ru = 'Нет данных по выбранному показателю...'");
		КонецЕсли;
		
	КонецЕсли;
	
	РучнаяКорректировка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	БухгалтерскиеОтчетыВызовСервера.ИнициализацияКомпоновщикаНастроек(ЭтотОбъект, Истина, КлючТекущегоВарианта);

	Если Отчет.Группировка.Количество() = 0 Тогда
		ЗаполнитьГруппировкиПоУмолчанию(ЭтотОбъект, ВестиУчетПоПодразделениям);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменениеСхемыКомпоновкиДанныхДляНастроекНаСервере() Экспорт
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();

	Отчеты.РасшифровкаПоказателейАнализФинансовыхРезультатов.НастроитьСхемуКомпоновкиДанных(СхемаКомпоновкиДанных, ПараметрыОтчета);
	
	КоличествоКорСубконто = ПараметрыОтчета.МаксимальноеКоличествоКорСубконто;
		
	Если (ЗначениеЗаполнено(ПараметрыОтчета.Аналитика) Или ПараметрыОтчета.ИспользуетсяАналитика)
		И ПараметрыОтчета.Свойство("ПолеОтбора") И ЗначениеЗаполнено(ПараметрыОтчета.ПолеОтбора) Тогда
		
		ПолеАналитика = Новый ПолеКомпоновкиДанных("Аналитика");
		Для Каждого ЭлементОтбора Из Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
			Если ЭлементОтбора.ЛевоеЗначение = ПолеАналитика Тогда
				ЭлементОтбора.ЛевоеЗначение = ПараметрыОтчета.ПолеОтбора;
			Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Отчет.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	Отчет.КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	НастройкиИнициализированы = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеМесяцаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "СформироватьОтчет" Тогда
		
		БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
		Активизировать();
		СформироватьОтчет("");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаНастройкиРассылкиОтчета(ВыбранноеЗначение)
	
	РассылкаОтчетовБП.ФормаОтчетаОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиОтчетаДляРассылки()
	
	// Для получения корректных настроек, необходимо предварительно инициализировать компоновщик настроек.
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	РассылкаОтчетовБП.ЗаполнитьНастройкиОтчетаДляРассылки(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПриИзмененииНаСервере(ИмяРеквизита)

	ГруппыНастроек = Новый Структура;
	ГруппыНастроек.Вставить("Отбор", Элементы.БыстрыеОтборы);
	
	БыстрыеНастройкиОтчетовСервер.УстановитьОтборПриИзмененииНаСервере(ЭтотОбъект, ГруппыНастроек, ИмяРеквизита);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыОтчетаНаСервере()
	
	Если Отчет.Группировка.Количество() = 0 Тогда
		ЗаполнитьГруппировкиПоУмолчанию(ЭтотОбъект, ВестиУчетПоПодразделениям);
	КонецЕсли;
	
	МенеджерОтчета = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяФормы);
	ПараметрыОтчета = МенеджерОтчета.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.НачалоПериода                     = Отчет.НачалоПериода;
	ПараметрыОтчета.КонецПериода                      = Отчет.КонецПериода;
	ПараметрыОтчета.Организация                       = Отчет.Организация;
	ПараметрыОтчета.РазрезПродаж                      = Отчет.РазрезПродаж;
	ПараметрыОтчета.ВключатьОбособленныеПодразделения = Отчет.ВключатьОбособленныеПодразделения;
	ПараметрыОтчета.РазмещениеДополнительныхПолей     = Ложь;
	ПараметрыОтчета.Группировка                       = Отчет.Группировка.Выгрузить();
	
	ПараметрыОтчета.ВыводитьЗаголовок                 = ВыводитьЗаголовок;
	ПараметрыОтчета.ВыводитьПодвал                    = ВыводитьПодвал;
	ПараметрыОтчета.МакетОформления                   = МакетОформления;
	ПараметрыОтчета.РежимРасшифровки                  = Отчет.РежимРасшифровки;
	ПараметрыОтчета.ДанныеРасшифровки                 = ДанныеРасшифровки;
	ПараметрыОтчета.КлючТекущегоВарианта              = КлючТекущегоВарианта;
	
	ПараметрыОтчета.СхемаКомпоновкиДанных             = ПолучитьИзВременногоХранилища(СхемаКомпоновкиДанных);
	ПараметрыОтчета.ИдентификаторОтчета               = БухгалтерскиеОтчеты.ИмяОтчета(ЭтотОбъект);
	ПараметрыОтчета.НастройкиКомпоновкиДанных         = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	
	ПараметрыОтчета.Показатель                        = Показатель;
	ПараметрыОтчета.Аналитика                         = Неопределено;
	ПараметрыОтчета.Договор                           = Договор;
	ПараметрыОтчета.Счет                              = Счет;
	ПараметрыОтчета.КорСчет                           = КорСчет;
	ПараметрыОтчета.ДанныеРасшифровки                 = ДанныеРасшифровки; 
	ПараметрыОтчета.Периодичность                 = Периодичность;

	ПараметрыОтчета.КлючТекущегоВарианта              = КлючТекущегоВарианта;
	
	ПараметрыОтчета.ОкруглениеСумм                    = ОкруглениеСумм;
	ПараметрыОтчета.ОтрицательноеКрасным              = ОтрицательноеКрасным;

	Если ДанныеДляРасчета = Неопределено Тогда
		ПараметрыОтчета.ДанныеДляРасчета                  = Новый Соответствие;
	Иначе
		ПараметрыОтчета.ДанныеДляРасчета                  = ДанныеДляРасчета;
	КонецЕсли;
	
	Если БыстрыеНастройкиОтчетовСервер.ЭтоОкруглениеСуммДоТысяч(ОкруглениеСумм) Тогда
		ПараметрыОтчета.ВыводитьПримечанияОкругления  = Истина;
	Иначе
		ПараметрыОтчета.ВыводитьПримечанияОкругления  =
			БыстрыеНастройкиОтчетовСервер.РежимОкругленияСуммВТысячах(ПараметрыОтчета.НастройкиКомпоновкиДанных);
	КонецЕсли;

	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Функция СохранитьТекущиеПараметрыОтчета(КлючВарианта)
	
	СохраняемыеНастройкиОтчета = Отчет.КомпоновщикНастроек.Настройки;
	СохраняемыеОтборы = СохраняемыеНастройкиОтчета.Отбор;

	СохранитьПользовательскиеНастройки(КлючВарианта);
	
	ТекущиеПараметрыОтчета = Новый Структура;
	ТекущиеПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Отчет.ВключатьОбособленныеПодразделения);
	ТекущиеПараметрыОтчета.Вставить("Группировка", Отчет.Группировка.Выгрузить());
	ТекущиеПараметрыОтчета.Вставить("НачалоПериода", Отчет.НачалоПериода);
	ТекущиеПараметрыОтчета.Вставить("КонецПериода", Отчет.КонецПериода);
	ТекущиеПараметрыОтчета.Вставить("РазрезПродаж", Отчет.РазрезПродаж);
	ТекущиеПараметрыОтчета.Вставить("Организация", Отчет.Организация);
	ТекущиеПараметрыОтчета.Вставить("ВыводитьЗаголовок", ВыводитьЗаголовок);
	ТекущиеПараметрыОтчета.Вставить("ВыводитьПодвал", ВыводитьПодвал);
	ТекущиеПараметрыОтчета.Вставить("МакетОформления", МакетОформления);
	ТекущиеПараметрыОтчета.Вставить("Отбор", СохраняемыеОтборы);
	
	Возврат ТекущиеПараметрыОтчета;
	
КонецФункции

&НаСервере
Процедура СохранитьПользовательскиеНастройки(КлючВарианта)
	
	СохраняемыеНастройкиОтчета = Отчет.КомпоновщикНастроек.Настройки;
	СохраняемыеОтборы = СохраняемыеНастройкиОтчета.Отбор.Элементы;
	Если СохраняемыеОтборы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ПользовательскиеНастройкиКомпоновкиДанных = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("НачалоПериода" , Отчет.НачалоПериода);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КонецПериода" , Отчет.КонецПериода);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КлючВарианта", КлючВарианта);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Группировка", Отчет.Группировка.Выгрузить());
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Организация", Отчет.Организация);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("ВыводитьЗаголовок", ВыводитьЗаголовок);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("РазрезПродаж", Отчет.РазрезПродаж);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("ВыводитьПодвал", ВыводитьПодвал);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения",
		Отчет.ВключатьОбособленныеПодразделения);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("МакетОформления", МакетОформления);
	
	Отбор = ПользовательскиеНастройкиКомпоновкиДанных.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	Отбор.ИдентификаторПользовательскойНастройки = "Отбор";

	Для Каждого СохраняемыйОтбор Из СохраняемыеОтборы Цикл
		ЭлементОтбора = Отбор.Элементы.Добавить(ТипЗнч(СохраняемыйОтбор));
		ЗаполнитьЗначенияСвойств(ЭлементОтбора, СохраняемыйОтбор);
	КонецЦикла;
	
	Порядок = ПользовательскиеНастройкиКомпоновкиДанных.Элементы.Добавить(Тип("ПорядокКомпоновкиДанных"));
	Порядок.ИдентификаторПользовательскойНастройки            = "Порядок";
	
	СохраняемыйПорядок = СохраняемыеНастройкиОтчета.Порядок.Элементы;
	Для Каждого ЗначениеПорядка Из СохраняемыйПорядок Цикл
		ЭлементПорядка = Порядок.Элементы.Добавить(ТипЗнч(ЗначениеПорядка));
		ЗаполнитьЗначенияСвойств(ЭлементПорядка, ЗначениеПорядка);
	КонецЦикла;

	ПользовательскиеНастройки = ПоместитьВоВременноеХранилище(ПользовательскиеНастройкиКомпоновкиДанных, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьТекущиеПараметрыОтчета(ТекущиеПараметрыОтчета)
	
	Если ТекущиеПараметрыОтчета = Неопределено
		Или ТипЗнч(ТекущиеПараметрыОтчета) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	ЗаполнитьЗначенияСвойств(ОтчетОбъект, ТекущиеПараметрыОтчета);
	ОтчетОбъект.Группировка.Очистить();
	ОтчетОбъект.Группировка.Загрузить(ТекущиеПараметрыОтчета.Группировка);
	
	СохраняемыеОтборы = ТекущиеПараметрыОтчета.Отбор;
	Отборы = ОтчетОбъект.КомпоновщикНастроек.Настройки.Отбор;
	Отборы.Элементы.Очистить();
	Для Каждого СохраняемыйОтбор Из СохраняемыеОтборы.Элементы Цикл
		ЭлементОтбора = Отборы.Элементы.Добавить(ТипЗнч(СохраняемыйОтбор));
		ЗаполнитьЗначенияСвойств(ЭлементОтбора, СохраняемыйОтбор);
	КонецЦикла;

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеПараметрыОтчета, "ВыводитьЗаголовок, ВыводитьПодвал, МакетОформления");
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, "ПолеОрганизация") Тогда
		ОбщегоНазначенияБПКлиентСервер.УстановитьЗначениеПолеОрганизация(
			ПолеОрганизация, ТекущиеПараметрыОтчета.Организация, Ложь);
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ОтчетОбъект, "Отчет");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокПоказателейНаСервере()
	
	ОбновитьСписокПоказателей(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьСписокПоказателей(Форма)
	
	Элементы = Форма.Элементы;
	Отчет = Форма.Отчет;
	
	Элементы.Показатель.СписокВыбора.Очистить();
	
	Показатели = Элементы.Показатель.СписокВыбора;
	Показатели.Добавить("Валовая прибыль", НСтр("ru='Валовая прибыль'"));
	Показатели.Добавить("Рентабельность продаж", НСтр("ru='Рентабельность продаж'"));
	Показатели.Добавить("Коммерческие расходы", НСтр("ru='Коммерческие расходы'"));
	Показатели.Добавить("Управленческие расходы", НСтр("ru='Управленческие расходы'"));
	Показатели.Добавить("Прибыль (убыток) от продаж", НСтр("ru='Прибыль (убыток) от продаж'"));
	Показатели.Добавить("Доходы от участия в других организациях", НСтр("ru='Доходы от участия в других организациях'"));
	Показатели.Добавить("Проценты к получению", НСтр("ru='Проценты к получению'"));
	Показатели.Добавить("Проценты к уплате", НСтр("ru='Проценты к уплате'"));
	Показатели.Добавить("Прочие доходы", НСтр("ru='Прочие доходы'"));
	Показатели.Добавить("Прочие расходы", НСтр("ru='Прочие расходы'"));
	Показатели.Добавить("Прибыль (убыток) до налогообложения", НСтр("ru='Прибыль (убыток) до налогообложения'"));
	
	// Налог на прибыль
	Если Не ЗначениеЗаполнено(Отчет.Организация) Тогда
		
		ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
		Для Каждого Организация Из ДоступныеОрганизации Цикл
			ДобавитьПоказательНалогНаПрибыль(Организация, Показатели, Форма);
		КонецЦикла;
		
	Иначе
		
		ДобавитьПоказательНалогНаПрибыль(Отчет.Организация, Показатели, Форма);
		
	КонецЕсли;
	
	Показатели.Добавить("Прочее", НСтр("ru='Прочее'"));
	Показатели.Добавить("Чистая прибыль", НСтр("ru='Чистая прибыль'"));

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьПоказательНалогНаПрибыль(Организация, Показатели, Форма)
	
	Отчет = Форма.Отчет;

	ОрганизацияПрименяетПБУ18 = РегламентированнаяОтчетностьПереопределяемый.ПрименяетсяПБУ18(
		Организация, КонецДня(Отчет.КонецПериода));
	ОрганизацияПрименяетУСН   = ЗаполнениеБухгалтерскойОтчетности.ПрименяетсяУСН(
		Организация, КонецДня(Отчет.КонецПериода));
	ОрганизацияПрименяетАУСН  = ЗаполнениеБухгалтерскойОтчетности.ПрименяетсяАУСН(
		Организация, КонецДня(Отчет.КонецПериода));
	ОрганизацияПрименяетНПД   = ЗаполнениеБухгалтерскойОтчетности.ПрименяетсяНПД(
		Организация, КонецДня(Отчет.КонецПериода));
	
	Если ОрганизацияПрименяетПБУ18 Тогда
		ПоказательНалогНаПрибыль = "Налог на прибыль";
		ПредсталениеПоказателяНалогНаПрибыль = НСтр("ru='Налог на прибыль'");
	ИначеЕсли ОрганизацияПрименяетУСН Или ОрганизацияПрименяетАУСН Тогда
		ПоказательНалогНаПрибыль = "Налог при УСН";
		ПредсталениеПоказателяНалогНаПрибыль = НСтр("ru='Налог при УСН'");
	ИначеЕсли ОрганизацияПрименяетНПД Тогда 
		ПоказательНалогНаПрибыль = "Налог на профессиональный доход";
		ПредсталениеПоказателяНалогНаПрибыль = НСтр("ru='Налог на профессиональный доход'");
	Иначе
		ПоказательНалогНаПрибыль = "Налог на прибыль";
		ПредсталениеПоказателяНалогНаПрибыль = НСтр("ru='Налог на прибыль'");
	КонецЕсли;
	
	Если Форма.Показатель =  "Налог на прибыль"
		Или Форма.Показатель = "Налог при УСН"
		Или Форма.Показатель = "Налог на профессиональный доход" Тогда
		Форма.Показатель = ПоказательНалогНаПрибыль;
	КонецЕсли;
	
	Если Показатели.НайтиПоЗначению(ПоказательНалогНаПрибыль) = Неопределено Тогда
		Показатели.Добавить(ПоказательНалогНаПрибыль, ПредсталениеПоказателяНалогНаПрибыль);
	КонецЕсли;
	
КонецПроцедуры

#Область БизнесСтатистика

&НаСервере
Функция НастройкиОтчетаДляСтатистики()
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();
	
	НастройкиДляСтатистики = БухгалтерскиеОтчеты.ПоказателиОтчетаРуководителяДляСтатистики(ПараметрыОтчета);
	
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет);
	
	Возврат ОбщегоНазначенияБП.ЗначениеВСтрокуJSON(НастройкиДляСтатистики, ПараметрыЗаписиJSON);
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура ИзменитьТекущийВариант(ТекущийВариант)
	
	ТекущиеПараметрыОтчета = СохранитьТекущиеПараметрыОтчета(ТекущийВариант);

	УстановитьТекущийВариант(ТекущийВариант);
	
	КомпоновщикИнициализирован = Ложь;
	
	ВосстановитьТекущиеПараметрыОтчета(ТекущиеПараметрыОтчета);
	
	СохранитьПользовательскиеНастройки(ТекущийВариант);

	РассылкаОтчетовБП.ПриИзмененииВариантаОтчета(ЭтотОбъект);
	
КонецПроцедуры

#Область ОбратнаяСвязь

&НаКлиенте
Процедура ОтправитьОтзывПоЭлектроннойПочте(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	БыстрыеНастройкиОтчетовКлиент.ОтправитьОтзывПоЭлектроннойПочте();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоРасчетныйПоказатель(Показатель)
	
	Возврат Отчеты.РасшифровкаПоказателейАнализФинансовыхРезультатов.ЭтоРасчетныйПоказатель(Показатель);
	
КонецФункции

&НаСервереБезКонтекста
Функция СубконтоСчета(Счет)
	
	Возврат Отчеты.РасшифровкаПоказателейАнализФинансовыхРезультатов.СубконтоСчета(Счет);
	
КонецФункции

&НаСервереБезКонтекста
Функция УстановитьСчетПоПоказателю(Показатель)
	
	Счет = Неопределено;
	Если Показатель = "Коммерческие расходы" Тогда
		Счет = ПланыСчетов.Хозрасчетный.КоммерческиеРасходы;
	ИначеЕсли Показатель = "Управленческие расходы" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы;
	ИначеЕсли Показатель = "Прочие доходы" 
		Или Показатель = "Доходы от участия в других организациях" 
		Или Показатель = "Проценты к получению" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
	ИначеЕсли Показатель = "Прочие расходы" 
		Или Показатель = "Проценты к уплате" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	ИначеЕсли Показатель = "Прочие расходы" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	ИначеЕсли Показатель = "Налог на прибыль"
		Или Показатель = "Налог при УСН"
		Или Показатель = "Налог на профессиональный доход" Тогда
		Счет = ПланыСчетов.Хозрасчетный.НалогНаПрибыль;
	ИначеЕсли Показатель = "Прочее" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеПрибылиИУбытки;
	КонецЕсли;
	
	Возврат Счет;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ПереключитьВидимостьБыстрыхНастроекНаКлиенте()
	
	БыстрыеНастройкиОтчетовКлиентСервер.ПереключитьВидимостьБыстрыхНастроек(ЭтотОбъект);
		
	Если Отчет.ПоказыватьБыстрыеНастройки Тогда
		ПоказатьБыстрыеНастройкиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти