#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Возвращает внешние наборы данных, которые используются при компоновке макета.
// 
// Параметры:
//   ПараметрыОтчета - Структура - Параметры исполнения отчета (См. ПустыеПараметрыКомпоновкиОтчета()).
//   МакетКомпоновки - МакетКомпоновкиДанных - сформированный макет компоновки данных.
// 
// Возвращаемое значение:
//   Структура - внешние наборы данных:
// *   РасчетныеПоказатели - ТаблицаЗначений - преобразованная таблица коэффициентов распределения.
//
Функция ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки) Экспорт
	
	ВнешниеИсточники = Новый Структура;

	Если ПараметрыОтчета.Свойство("Данные") Тогда
		Данные = ПараметрыОтчета.Данные;
	Иначе
		Данные = Отчеты.АнализФинансовыхРезультатов.ТаблицаПоказателейДляРасшифровки();
	КонецЕсли;

	ВнешниеИсточники.Вставить("РасчетныеПоказатели", Данные);
	
	Возврат ВнешниеИсточники;

КонецФункции

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//   Структура - флаги, задающие необходимость дополнительных действий.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета",          Истина);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",           Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",               Истина);
	Результат.Вставить("ИспользоватьРасширенныеПараметрыРасшифровки", Истина);
	Результат.Вставить("ИспользоватьВнешниеНаборыДанных",             Истина);
	
	Возврат Результат;
	
КонецФункции

// Формирует текст, выводимый в заголовке отчета.
//
// Параметры:
//   ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//
// Возвращаемое значение:
//   Строка - текст заголовка с учётом периода.
//
Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Аналитика) Тогда 
		Заголовок = СтрШаблон(НСтр("ru='%1: %2 %3'"), ПараметрыОтчета.Показатель, ПараметрыОтчета.Аналитика,
			БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода));
	Иначе
		Заголовок = СтрШаблон(НСтр("ru='%1%2'"), ПараметрыОтчета.Показатель,
			БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода));
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//   ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//   Схема - СхемаКомпоновкиДанных - описание получаемых данных.
//   КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт

	ЭтоРасчетныйПоказатель = ЭтоРасчетныйПоказатель(ПараметрыОтчета.Показатель);
	
	УстановитьЗначениеАналитики(ПараметрыОтчета);
	
	Если ЭтоРасчетныйПоказатель Тогда
		ПередКомпоновкойМакетаРасчетныеПоказатели(ПараметрыОтчета, Схема, КомпоновщикНастроек);
	Иначе
		ПередКомпоновкойМакетаПоказатели(ПараметрыОтчета, Схема, КомпоновщикНастроек);
	КонецЕсли;
	
	ПолеПоказатель = Новый ПолеКомпоновкиДанных("Показатель");
	ПолеПоказательРасчетный = Новый ПолеКомпоновкиДанных("ПоказательРасчетный");
	
	Подразделение = Неопределено;

	Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		Если ЭлементОтбора.ЛевоеЗначение = ПолеПоказатель
			Или ЭлементОтбора.ЛевоеЗначение = ПолеПоказательРасчетный Тогда
			
			Если ЭтоРасчетныйПоказатель Тогда
				ЭлементОтбора.ЛевоеЗначение = ПолеПоказательРасчетный;
			Иначе
				ЭлементОтбора.ЛевоеЗначение = ПолеПоказатель;
			КонецЕсли;
			
			ЭлементОтбора.ПравоеЗначение = ПараметрыОтчета.Показатель;
			
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") И ЭлементОтбора.Использование Тогда
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			Подразделение = ЭлементОтбора.ПравоеЗначение;
		КонецЕсли;
		
	КонецЦикла;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Подразделение", Подразделение);

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Организация", ПараметрыОтчета.Организация);
		
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", ПараметрыОтчета.НачалоПериода);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Показатель", ПараметрыОтчета.Показатель);

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
			"ПустоеПодразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	
	// Отбор по организации.
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
КонецПроцедуры

// В процедуре можно изменить табличный документ после вывода в него данных.
//
// Параметры:
//   ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//   Результат - ТабличныйДокумент - сформированный отчет.
//
Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	ПредставленияСубконто = ПредставленияСубконто();
	
	Если ПараметрыОтчета.Свойство("КорСчетаПоПоказателю") Тогда
		КорСчетаПоПоказателю = ПараметрыОтчета.КорСчетаПоПоказателю;
	Иначе
		КорСчетаПоПоказателю = ПолучитьКорСчетаПоПоказателю(ПараметрыОтчета);
	КонецЕсли;
	
	НачалоПоиска = Результат.Область("R1");

	Для Каждого СтрокаСчета Из КорСчетаПоПоказателю Цикл
		
		НачалоШапки = Результат.НайтиТекст("Субконто1", НачалоПоиска,,, Истина);
		Если НачалоШапки = Неопределено Тогда
			НачалоШапки = Результат.НайтиТекст("КорСубконто1", НачалоПоиска,,, Истина);
		КонецЕсли;
		
		Если НачалоШапки = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		НижняяСтрока = Результат.НайтиТекст("Период", НачалоПоиска,,, Истина);
		КонецШапки = Результат.НайтиТекст("Сумма", НачалоПоиска,,, Истина);
		ОбластьПоиска = Результат.Область(СтрШаблон("R%1C%2:R%3C%4", 
			Формат(НижняяСтрока.Верх, "ЧГ=0"),
			Формат(НижняяСтрока.Лево, "ЧГ=0"),
			Формат(НижняяСтрока.Верх, "ЧГ=0"),
			Формат(КонецШапки.Право, "ЧГ=0")));
			
					
		СписокСубконто = СубконтоСчета(СтрокаСчета.Счет);
		КоличествоСубконто = Мин(ПараметрыОтчета.Группировка.Итог("Использование"), СписокСубконто.Количество());
		
		СписокКорСубконто = СубконтоСчета(СтрокаСчета.КорСчет[0]);
		КоличествоКорСубконто = СписокКорСубконто.Количество();
		СчетчикВыведенныхСубконто = 1;
		Если КоличествоСубконто > 0 Тогда
			
			Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл
				
				НомерСубконто = НомерСубконтоПоПолю(ПолеВыбраннойГруппировки.Поле);
			
				Если ПолеВыбраннойГруппировки.Использование = Ложь Тогда
					Продолжить;
				КонецЕсли;
				
				ЯчейкаСубконто = Результат.НайтиТекст(СтрШаблон("Субконто%1", СчетчикВыведенныхСубконто), НачалоПоиска,,, Истина);
				Если ЯчейкаСубконто <> Неопределено И НомерСубконто <> 0 Тогда
					ЯчейкаСубконто.Текст = СписокСубконто[НомерСубконто].Представление;
				Иначе
					Продолжить;
				КонецЕсли;
				
				СчетчикВыведенныхСубконто = СчетчикВыведенныхСубконто + 1;
	
			КонецЦикла;
			
		КонецЕсли;
		
		Для НомерКорСубконто = 1 По КоличествоКорСубконто Цикл
			
			ЯчейкаКорСубконто = Результат.НайтиТекст(СтрШаблон("КорСубконто%1", НомерКорСубконто), , ОбластьПоиска,, Истина);
			Если ЯчейкаКорСубконто <> Неопределено Тогда
				ЯчейкаКорСубконто.Текст = СписокКорСубконто[НомерКорСубконто].Представление;
			КонецЕсли;
			
		КонецЦикла;
		
		НачалоПоиска = Результат.Область(СтрШаблон("R%1", Формат(НижняяСтрока.Верх + 1, "ЧГ=0")));
		
	КонецЦикла;
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	БыстрыеНастройкиОтчетовСервер.ВывестиПримечанияОкругления(ПараметрыОтчета, Результат);

КонецПроцедуры

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//   ОписаниеОтчета - СтрокаДереваЗначений - Настройки этого отчета,
//      уже сформированные при помощи функции ВариантыОтчетов.ОписаниеОтчета() и готовые к изменению.
//
Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "АнализФинансовыхРезультатовЗаПериод");
	
	ОписаниеВарианта.Размещение.Вставить(Метаданные.Подсистемы.Руководителю.Подсистемы.Анализ, "");
	
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
КонецПроцедуры

// Заполняет описание настроек отчета в коллекции Настройки.
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя, Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет параметры расшифровки ячейки отчета.
//
// Параметры:
//   Адрес - Строка - Адрес временного хранилища с данными расшифровки отчета.
//   Расшифровка - Произвольный - Значения полей расшифровки.
//   ПараметрыРасшифровки - Структура - Коллеккция параметров расшифроки, которую требуется заполнить. 
//      Подробнее см. БухгалтерскиеОтчетыВызовСервера.ПолучитьМассивПолейРасшифровки()
//
Процедура ЗаполнитьПараметрыРасшифровкиОтчета(Адрес, Расшифровка, ПараметрыРасшифровки) Экспорт
	
	// Инициализируем список пунктов меню
	СписокПунктовМеню = Новый СписокЗначений();
	
	// Заполниим соответствие полей которые мы хотим получить из данных расшифровки
	СоответствиеПолей = Новый Соответствие;
	ДанныеОтчета = ПолучитьИзВременногоХранилища(Адрес);
	ДеталиРасшифровки = Новый Структура;
	ЭтоРасчетныйПоказатель = Ложь;
	
	Если ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
		
		ЗначениеРасшифровки = ДанныеОтчета.ДанныеРасшифровки.Элементы[Расшифровка];
		ПоляРасшифровки = ЗначениеРасшифровки.ПолучитьПоля();
		
	Иначе
		
		ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
		ПараметрыРасшифровки.Вставить("Значение", Расшифровка);
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Ложь);
	
	Если ЭтоРасчетныйПоказатель(ДанныеОтчета.Объект.Показатель) Тогда 
		Показатель = ПоляРасшифровки.Найти("ПоказательФормулы");
	Иначе
		Показатель = ПоляРасшифровки.Найти("Показатель");
	КонецЕсли;
	
	Если Показатель = Неопределено Тогда 
		
		Для Каждого ПолеРасшифровки ИЗ ПоляРасшифровки Цикл
			Если ЗначениеЗаполнено(ПолеРасшифровки.Значение) Тогда
				ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
				ПараметрыРасшифровки.Вставить("Значение", ПолеРасшифровки.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Возврат;
		
	Иначе
		
		ЭтоРасчетныйПоказатель = ЭтоРасчетныйПоказатель(Показатель.Значение);
	
		Если ТипЗнч(ЗначениеРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
			Для Каждого ПолеРасшифровки ИЗ ПоляРасшифровки Цикл
				Если ЗначениеЗаполнено(ПолеРасшифровки.Значение) Тогда
					Ключ = СтрЗаменить(ПолеРасшифровки.Поле, "ПоказательФормулы", "Показатель");
					ДеталиРасшифровки.Вставить(Ключ, ПолеРасшифровки.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеОтчета = Неопределено Тогда
		ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
		Возврат;
	КонецЕсли;
	
	// Прежде всего интересны данные группировочных полей
	Для Каждого Группировка Из ДанныеОтчета.Объект.Группировка Цикл
		
		Если Группировка.Использование Тогда
			СоответствиеПолей.Вставить(Группировка.Поле);
		КонецЕсли;
		
	КонецЦикла;
		
	// Инициализация пользовательских настроек
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("РежимРасшифровки",          Истина);
	ДополнительныеСвойства.Вставить("Организация",               ДанныеОтчета.Объект.Организация);
	ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения",
				ДанныеОтчета.Объект.ВключатьОбособленныеПодразделения);
	ДополнительныеСвойства.Вставить("НачалоПериода",             ДанныеОтчета.Объект.НачалоПериода);
	ДополнительныеСвойства.Вставить("КонецПериода",              ДанныеОтчета.Объект.КонецПериода);

	ДополнительныеСвойства.Вставить("ВыводитьЗаголовок",         ДанныеОтчета.Объект.ВыводитьЗаголовок);
	ДополнительныеСвойства.Вставить("ВыводитьПодвал",            ДанныеОтчета.Объект.ВыводитьПодвал);
	ДополнительныеСвойства.Вставить("МакетОформления",           ДанныеОтчета.Объект.МакетОформления);
	ДополнительныеСвойства.Вставить("РазрезПродаж",              ДанныеОтчета.Объект.РазрезПродаж); 
	ДополнительныеСвойства.Вставить("Показатель",                Показатель.Значение);
	ДополнительныеСвойства.Вставить("ОчищатьТаблицуГруппировок", Истина);
	ДополнительныеСвойства.Вставить("ОкруглениеСумм",            ДанныеОтчета.Объект.ОкруглениеСумм);
	ДополнительныеСвойства.Вставить("ОтрицательноеКрасным",      ДанныеОтчета.Объект.ОтрицательноеКрасным);
	
	Если ЭтоАнализВыручки(Показатель.Значение) Тогда
		ДополнительныеСвойства.Вставить("КлючТекущегоВарианта",          "ВаловаяПрибыль");
		ДополнительныеСвойства.Вставить("ВыводитьДиаграмму",     Ложь);
	Иначе
		Если ЭтоРасчетныйПоказатель Тогда
			ДополнительныеСвойства.Вставить("КлючВарианта",         "РасшифровкаРасчетныхПоказателей");
		Иначе
			ДополнительныеСвойства.Вставить("КлючВарианта",         "РасшифровкаПоказателейАнализаФинансовыхРезультатов");
		КонецЕсли;
	КонецЕсли;
	ОтборПоЗначениямРасшифровки = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ОтборПоЗначениямРасшифровки.ИдентификаторПользовательскойНастройки = "Отбор";
	
	// Получаем структуру полей доступных в расшифровке
	Данные_Расшифровки = БухгалтерскиеОтчеты.ПолучитьДанныеРасшифровки(ДанныеОтчета.ДанныеРасшифровки, СоответствиеПолей, Расшифровка);
	
	// Формируем группировки нового отчета.
	Группировка = Новый Массив();

	Если ЭтоАнализВыручки(Показатель.Значение) Тогда
		
		РазрезПродаж = ДанныеОтчета.Объект.РазрезПродаж;
		
		Если РазрезПродаж = 1 Тогда
			
			СтрокаДляРасшифровки = СтрокаДляРасшифровки();
			СтрокаДляРасшифровки.Использование = 1;
			СтрокаДляРасшифровки.Поле = "Контрагент";
			СтрокаДляРасшифровки.Представление = "Покупатель";
			СтрокаДляРасшифровки.ТипГруппировки = 1;
			Группировка.Добавить(СтрокаДляРасшифровки);
			
			СтрокаДляРасшифровки = СтрокаДляРасшифровки();
			СтрокаДляРасшифровки.Использование = 1;
			СтрокаДляРасшифровки.Поле = "Договор";
			СтрокаДляРасшифровки.Представление = "Договор";
			СтрокаДляРасшифровки.ТипГруппировки = 0;
			Группировка.Добавить(СтрокаДляРасшифровки);
			
		ИначеЕсли РазрезПродаж = 2 Тогда
 
			СтрокаДляРасшифровки = СтрокаДляРасшифровки();
			СтрокаДляРасшифровки.Использование = 1;
			СтрокаДляРасшифровки.Поле = "Номенклатура";
			СтрокаДляРасшифровки.Представление = "Номенклатура";
			СтрокаДляРасшифровки.ТипГруппировки = 0;
			Группировка.Добавить(СтрокаДляРасшифровки);
			
		Иначе
			СтрокаДляРасшифровки = СтрокаДляРасшифровки();
			СтрокаДляРасшифровки.Использование = 1;
			СтрокаДляРасшифровки.Поле = "НоменклатурнаяГруппа";
			СтрокаДляРасшифровки.Представление = "Номенклатурная группа";
			СтрокаДляРасшифровки.ТипГруппировки = 0;
			Группировка.Добавить(СтрокаДляРасшифровки);
			
		КонецЕсли;
		
		СтрокаДляРасшифровки = СтрокаДляРасшифровки();
		СтрокаДляРасшифровки.Использование = 1;
		СтрокаДляРасшифровки.Поле = "Регистратор";
		СтрокаДляРасшифровки.Представление = "Документ";
		СтрокаДляРасшифровки.ТипГруппировки = 0;
		Группировка.Добавить(СтрокаДляРасшифровки);

	Иначе
		
		Для Каждого СтрокаГруппировки Из ДанныеОтчета.Объект.Группировка Цикл
			
			Если СтрокаГруппировки.Использование Тогда
				
				СтрокаДляРасшифровки = СтрокаДляРасшифровки();
				ЗаполнитьЗначенияСвойств(СтрокаДляРасшифровки, СтрокаГруппировки);
				Группировка.Добавить(СтрокаДляРасшифровки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

	ДополнительныеСвойства.Вставить("Группировка", Группировка);
	
	// Формируем отборы нового отчета.
	Для Каждого ЗначениеРасшифровки Из Данные_Расшифровки Цикл
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ОтборПоЗначениямРасшифровки, ЗначениеРасшифровки.Ключ, ЗначениеРасшифровки.Значение);
	КонецЦикла;
	
	// Формируем дополнительные поля.
	ДополнительныеПоля = Новый Массив();
	
	Если ЭтоАнализВыручки(Показатель.Значение) Тогда
		
		ДобавитьПоляДляРасшифровкиВыручки(ДополнительныеПоля);
		
		ДополнительныеСвойства.Вставить("ОчищатьДополнительныеПоля", Истина);
		ДополнительныеСвойства.Вставить("ДополнительныеПоля", ДополнительныеПоля);
	
	КонецЕсли;
	
	Если Не ПараметрыРасшифровки.Свойство("Значение") Тогда
		
		НастройкиРасшифровки = Новый Структура();

		Если ЭтоАнализВыручки(Показатель.Значение) Тогда
			СписокПунктовМеню.Добавить("ВаловаяПрибыль", 
				НСтр("ru='Анализ продаж'"));
			
			НастройкиРасшифровки.Вставить("ВаловаяПрибыль", ПользовательскиеНастройки);
		Иначе
			СписокПунктовМеню.Добавить("РасшифровкаПоказателейАнализФинансовыхРезультатов", 
				НСтр("ru='Расшифровка показателей анализа финансовых результатов'"));
			
			
			НастройкиРасшифровки.Вставить("РасшифровкаПоказателейАнализФинансовыхРезультатов", ПользовательскиеНастройки);
		КонецЕсли;
		ДанныеОтчета.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
		ДанныеОтчета.Вставить("ДеталиРасшифровки", ДеталиРасшифровки);
	
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(ДанныеОтчета, Адрес);
	
	ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
	
КонецПроцедуры

// Возвращает коллекцию вариантов настроек отчета.
// 
// Возвращаемое значение:
//  Массив - Варианты настроек
//
Функция ВариантыНастроек() Экспорт

	ВариантыНастроек = Новый Массив;
	
	ВариантыНастроек.Добавить(Новый Структура("Имя, Представление", "РасшифровкаПоказателейАнализаФинансовыхРезультатов", 
		НСтр("ru = 'Расшифровка показателей анализа финансовых результатов'")));
	ВариантыНастроек.Добавить(Новый Структура("Имя, Представление", "РасшифровкаРасчетныхПоказателей", 
		НСтр("ru = 'Расшифровка расчетных показателей'")));
	
	Возврат ВариантыНастроек;
	
КонецФункции

// Инициализирует представления полей субконто и особые наборы данных.
//
// Параметры:
//  Схема           - СхемаКомпоновкиДанных, Строка - обрабатываемая схема или адрес, где она лежит.
//  ПараметрыОтчета - Структура - структура параметров для формирования отчета.
//
Процедура НастроитьСхемуКомпоновкиДанных(Схема, ПараметрыОтчета) Экспорт
	
	УстановитьЗначениеАналитики(ПараметрыОтчета);

	ПараметрыАлгоритма = ПараметрыАлгоритма();
	
	КорСчетаПоПоказателю = ПолучитьКорСчетаПоПоказателю(ПараметрыОтчета);
	
	Если КорСчетаПоПоказателю = Неопределено Или КорСчетаПоПоказателю.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТипыКорСубконто = Новый Соответствие;
	
	МаксимальноеКоличествоКорСубконто = 0;
	
	Для Каждого СтрокаКорСчет Из КорСчетаПоПоказателю Цикл
		
		Счет = СтрокаКорСчет.Счет;
		МаксимальноеКоличествоКорСубконто = Макс(МаксимальноеКоличествоКорСубконто, СтрокаКорСчет.КоличествоКорСубконто);
		
		Для СчетчикСубконто = 1 По МаксимальноеКоличествоКорСубконто Цикл
			
			ОписаниеСубконто = ТипыКорСубконто.Получить(СчетчикСубконто);
			
			Если ОписаниеСубконто <> Неопределено Тогда
				МассивТиповКорСубконто = ОписаниеСубконто.ОписаниеТипов.Типы();
				ВидКорСубконто = ОписаниеСубконто.ВидКорСубконто;
			Иначе
				ОписаниеСубконто = Новый Структура;
				МассивТиповКорСубконто = Новый Массив;
				ВидКорСубконто = Новый Массив;
			КонецЕсли;
			
			Если ВидКорСубконто.Найти(СтрокаКорСчет["ВидСубконто" + СчетчикСубконто]) = Неопределено Тогда
				ВидКорСубконто.Добавить(СтрокаКорСчет["ВидСубконто" + СчетчикСубконто]);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаКорСчет["ВидСубконто" + СчетчикСубконто]) Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивТиповКорСубконто, 
					СтрокаКорСчет["ВидСубконто" + СчетчикСубконто].ТипЗначения.Типы());
			КонецЕсли;
			
			ОписаниеСубконто.Вставить("ВидКорСубконто", ВидКорСубконто);
			ОписаниеСубконто.Вставить("ОписаниеТипов", Новый ОписаниеТипов(МассивТиповКорСубконто));
			ТипыКорСубконто.Вставить(СчетчикСубконто, ОписаниеСубконто);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПараметрыОтчета.Вставить("МаксимальноеКоличествоКорСубконто", МаксимальноеКоличествоКорСубконто);

	// Изменим запрос под показатель
	ИзменитьЗапросПоПараметрам(ПараметрыОтчета, ПараметрыАлгоритма);

	Если ТипЗнч(Схема) = Тип("Строка") Тогда 
		
		ПоместитьВоВременноеХранилище(ПараметрыАлгоритма.Схема, Схема);
		
		СхемаКомпоновкиДанныхАдрес = Схема;
		СхемаКомпоновкиДанных = ПараметрыАлгоритма.Схема;
		
	Иначе
		
		Схема = ПараметрыАлгоритма.Схема;
		СхемаКомпоновкиДанныхАдрес = "";
		СхемаКомпоновкиДанных = ПараметрыАлгоритма.Схема;
		
	КонецЕсли;

	ОсновнойНабор = СхемаКомпоновкиДанных.НаборыДанных.ОсновнойНабор;

	ПараметрыОС      = Новый Структура("ИндексСубконто, ЗаголовокСубконто", 0, "");
	ПараметрыНМА     = Новый Структура("ИндексСубконто, ЗаголовокСубконто", 0, "");
	ПараметрыФизЛица = Новый Структура("ИндексСубконто, ЗаголовокСубконто", 0, "");
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	
	ПредставленияСубконто = ПредставленияСубконто();
	
	ПараметрыОтчета.Вставить("ПолеОтбора", Неопределено);
	АналитикаЗаполнена = АналитикаЗаполнена(ПараметрыОтчета);
	Для НомерСубконто = 1 По СвойстваСчета.КоличествоСубконто Цикл
		Если АналитикаЗаполнена
			И СвойстваСчета["ВидСубконто" + НомерСубконто].ТипЗначения.СодержитТип(ТипЗнч(ПараметрыОтчета.Аналитика)) Тогда
			ПараметрыОтчета.Вставить("ПолеОтбора", Новый ПолеКомпоновкиДанных("Субконто" + НомерСубконто));
		КонецЕсли;
	КонецЦикла;
	
	// Изменение представления и наложения ограничения типа значения.
	Для Индекс = 1 По СвойстваСчета.КоличествоСубконто Цикл

		Поле = ОсновнойНабор.Поля.Найти("Субконто" + Индекс);
		Если Поле = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Поле.ТипЗначения = СвойстваСчета["ВидСубконто" + Индекс + "ТипЗначения"];
		Поле.Заголовок   = ПредставленияСубконто.Получить(СвойстваСчета["ВидСубконто" + Индекс]);
		
		НаборПараметров = Неопределено;
		Если ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредств")
			И Поле.ТипЗначения.СодержитТип(БухгалтерскийУчетКлиентСерверПереопределяемый.ТипОсновныеСредства()) Тогда
			НаборПараметров = ПараметрыОС;
		ИначеЕсли ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА")
			И Поле.ТипЗначения.СодержитТип(Тип("СправочникСсылка.НематериальныеАктивы")) Тогда
			НаборПараметров = ПараметрыНМА;
		ИначеЕсли Поле.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ФизическиеЛица")) Тогда
			НаборПараметров = ПараметрыФизЛица;
		КонецЕсли;
		Если НаборПараметров <> Неопределено Тогда
			НаборПараметров.ИндексСубконто    = Индекс;
			НаборПараметров.ЗаголовокСубконто = Поле.Заголовок;
		КонецЕсли;

	КонецЦикла;

	// Присваиваем набор типов для кор.полей, которые будут использоваться в настройках пользователем.
	Для Индекс = 1 По МаксимальноеКоличествоКорСубконто Цикл
		
		Поле = ОсновнойНабор.Поля.Найти("КорСубконто" + Индекс);
		Если Поле = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеТипаКорСубконто = ТипыКорСубконто.Получить(Индекс);
		Если ОписаниеТипаКорСубконто = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Поле.ТипЗначения = ОписаниеТипаКорСубконто.ОписаниеТипов;
		
		Если ОписаниеТипаКорСубконто.ВидКорСубконто.Количество() = 1 Тогда
			Для Каждого СтрокаТипов Из КорСчетаПоПоказателю Цикл
				Если ЗначениеЗаполнено(СтрокаТипов["ВидСубконто" + Индекс]) Тогда
					Поле.Заголовок = ПредставленияСубконто.Получить(СтрокаТипов["ВидСубконто" + Индекс]);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Поле.Заголовок = СтрШаблон(НСтр("ru = 'Аналитика %1'"), Индекс);
		КонецЕсли;
		
	КонецЦикла;

	Если ПустаяСтрока(СхемаКомпоновкиДанныхАдрес) Тогда
		Схема = СхемаКомпоновкиДанных;
	Иначе
		Схема = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, СхемаКомпоновкиДанныхАдрес);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает набор параметров, которые необходимо сохранять в рассылке отчетов.
// Значения параметров используются при формировании отчета в рассылке.
//
// Возвращаемое значение:
//   Структура - структура настроек, сохраняемых в рассылке с неинициализированными значениями.
//
Функция НастройкиОтчетаСохраняемыеВРассылке() Экспорт
	
	КоллекцияНастроек = Новый Структура;
	КоллекцияНастроек.Вставить("Организация"                      , Справочники.Организации.ПустаяСсылка());
	КоллекцияНастроек.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	КоллекцияНастроек.Вставить("РазрезПродаж"                     , 0);
	КоллекцияНастроек.Вставить("РазмещениеДополнительныхПолей"    , 0);
	КоллекцияНастроек.Вставить("Группировка"                      , Неопределено);
	КоллекцияНастроек.Вставить("ВыводитьЗаголовок"                , Ложь);
	КоллекцияНастроек.Вставить("ВыводитьПодвал"                   , Ложь);
	КоллекцияНастроек.Вставить("МакетОформления"                  , Неопределено);
	КоллекцияНастроек.Вставить("НастройкиКомпоновкиДанных"        , Неопределено);
	КоллекцияНастроек.Вставить("ВыводитьПримечанияОкругления"     , Ложь);
	
	Возврат КоллекцияНастроек;
	
КонецФункции

// Возвращает структуру параметров, наличие которых требуется для успешного формирования отчета.
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Общая структура настроек.
	ПараметрыОтчета = БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.ИдентификаторОтчета = "АнализФинансовыхРезультатов";
	
	ОтчетОбъект = Отчеты.АнализФинансовыхРезультатов.Создать();
	ПараметрыОтчета.Вставить("Группировка", ОтчетОбъект.Группировка.ВыгрузитьКолонки());
	ПараметрыОтчета.Вставить("РазмещениеДополнительныхПолей",
		БухгалтерскиеОтчетыКлиентСервер.РазмещениеДополнительныхПолей().ВместеСВладельцем);
	
	ПараметрыОтчета.Вставить("КлючТекущегоВарианта", ""); 
	ПараметрыОтчета.Вставить("РазрезПродаж", 1); // по контрагентам
	ПараметрыОтчета.Вставить("ВыводитьПримечанияОкругления", Ложь);
	ПараметрыОтчета.Вставить("Показатель", "");
	ПараметрыОтчета.Вставить("Аналитика", Неопределено);
	ПараметрыОтчета.Вставить("Договор", Неопределено);
	ПараметрыОтчета.Вставить("Счет", Неопределено);
	ПараметрыОтчета.Вставить("КорСчет", Неопределено);
	ПараметрыОтчета.Вставить("Период", '00010101'); 
	ПараметрыОтчета.Вставить("Периодичность", 0);
	ПараметрыОтчета.Вставить("ДанныеДляРасчета", Неопределено);
	ПараметрыОтчета.Вставить("ВыводитьЗаголовок", Ложь);
	ПараметрыОтчета.Вставить("МакетОформления", Ложь);
	ПараметрыОтчета.Вставить("ОкруглениеСумм", "");
	ПараметрыОтчета.Вставить("ОтрицательноеКрасным", Ложь);
	ПараметрыОтчета.Вставить("ВыводитьПодвал", Ложь);
	ПараметрыОтчета.Вставить("МаксимальноеКоличествоКорСубконто", 0);

	Возврат ПараметрыОтчета;
	
КонецФункции

// Заполняет структуру настроек универсального формата из реквизитов формы.
//
// Параметры:
//   ПараметрыОтчета - Структура - см. ПустыеПараметрыКомпоновкиОтчета()
//   Форма - УправляемаяФорма - содержит основновной реквизит Отчет .
//
Процедура ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма) Экспорт
	
	БухгалтерскиеОтчеты.ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма);
	
	Отчет = Форма.Отчет;

	ПараметрыОтчета.РазрезПродаж = Отчет.РазрезПродаж;
	
КонецПроцедуры

// Возвращает параметры, необходимые для формирования отчета с расшифровкой по переданной организации и периоду.
//
// Параметры:
//   ИмяОтчета - Строка - имя отчета, для которого формируются параметры.
//   КлючВарианта - Строка - имя ключа варианта отчета
//   Организация - СправочникСсылка.Организации 
//   Дата - Дата - дата формирования отчета
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПараметрыДляОтчета(ИмяОтчета, КлючВарианта, Организация, Дата) Экспорт
	
	ПользовательскиеНастройкиКомпоновкиДанных = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("НачалоПериода" , НачалоГода(Дата));
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КонецПериода" , КонецМесяца(Дата));
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КлючВарианта", КлючВарианта);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Организация", Организация);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("РазрезПродаж", 1); // Контрагент

	НастройкиРасшифровки = Новый Структура;
	НастройкиРасшифровки.Вставить(ИмяОтчета, ПользовательскиеНастройкиКомпоновкиДанных);
	
	УсловияОтбора = Новый Структура();
	УсловияОтбора.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	ОбщиеНастройки = Новый Структура();
	ОбщиеНастройки.Вставить("Объект", УсловияОтбора);
	ОбщиеНастройки.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ОбщиеНастройки, Новый УникальныйИдентификатор);
	
	ЗаполнятьТиповыеНастройки = Новый Структура;
	ЗаполнятьТиповыеНастройки.Вставить("Отбор", Ложь);
	ЗаполнятьТиповыеНастройки.Вставить("Группировка", Истина);
	ЗаполнятьТиповыеНастройки.Вставить("ВыводимыеДанные", Истина);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ВидРасшифровки", 1);
	ПараметрыОтчета.Вставить("АдресНастроек", АдресХранилища);
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ИДРасшифровки", ИмяОтчета);
	ПараметрыОтчета.Вставить("РежимРасшифровки", Истина);
	ПараметрыОтчета.Вставить("ЗаполняемыеНастройки", ЗаполнятьТиповыеНастройки);
	ПараметрыОтчета.Вставить("КлючВарианта", КлючВарианта);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Возвращает субконто переданного счета.
//
// Параметры:
//   Счет - ПланыСчетовСсылка.Хозрасчетный - счет учета.
//
// Возвращаемое значение:
//   Соответствие - соответствие, где ключ - номер субконто, а Значение  параметров для формирования отчета.
//     * Ключ - число - порядковый номер субконто
//     * Значение - Структура: 
//        ** Представление - Строка - представление субконто.
//        ** Тип           - Тип - тип субконто.
//
Функция СубконтоСчета(Счет) Экспорт
	
	ПредставленияСубконто = ПредставленияСубконто();
	Результат = Новый Соответствие;
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);

	Для Каждого Субкотно Из Счет.ВидыСубконто Цикл
		ДанныеСубконто = Новый Структура;
		
		Заголовок = ПредставленияСубконто[Субкотно.ВидСубконто];
		Если ПустаяСтрока(Заголовок) Тогда
			Заголовок = СвойстваСчета["ВидСубконто" + Субкотно.НомерСтроки + "Наименование"];
		КонецЕсли;

		ДанныеСубконто.Вставить("Представление", Заголовок);
		ДанныеСубконто.Вставить("Тип", Субкотно.ВидСубконто.ТипЗначения);
		Результат.Вставить(Субкотно.НомерСтроки, ДанныеСубконто);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПоляДляРасшифровкиВыручки(ДополнительныеПоля)
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Истина);
	СтрокаДляРасшифровки.Вставить("Поле", "Количество");
	СтрокаДляРасшифровки.Вставить("Представление", "Количество");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Истина);
	СтрокаДляРасшифровки.Вставить("Поле", "Стоимость");
	СтрокаДляРасшифровки.Вставить("Представление", "Стоимость");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Истина);
	СтрокаДляРасшифровки.Вставить("Поле", "ВыручкаБезНДС");
	СтрокаДляРасшифровки.Вставить("Представление", "Выручка без НДС");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Истина);
	СтрокаДляРасшифровки.Вставить("Поле", "РентабельностьБезНДС");
	СтрокаДляРасшифровки.Вставить("Представление", "Рентабельность, %");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Ложь);
	СтрокаДляРасшифровки.Вставить("Поле", "СтоимостьЗаЕдиницу");
	СтрокаДляРасшифровки.Вставить("Представление", "Стоимость за единицу");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Ложь);
	СтрокаДляРасшифровки.Вставить("Поле", "ЦенаРеализацииБезНДС");
	СтрокаДляРасшифровки.Вставить("Представление", "Цена реализации");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
КонецПроцедуры

Функция НомерСубконтоПоПолю(Поле)
	
	Если СтрНайти(Поле, "Субконто1") <> 0 Тогда
		НомерСубконто = 1;
	ИначеЕсли СтрНайти(Поле, "Субконто2") <> 0 Тогда
		НомерСубконто = 2;
	ИначеЕсли СтрНайти(Поле, "Субконто3") <> 0 Тогда
		НомерСубконто = 3;
	Иначе
		НомерСубконто = 0;
	КонецЕсли;
	
	Возврат НомерСубконто;
	
КонецФункции

Процедура УстановитьЗначениеАналитики(ПараметрыОтчета)
	
	ПараметрыОтчета.Вставить("ИспользуетсяАналитика", Ложь);
	ПараметрыОтчета.Аналитика = Неопределено;
	
	Поля = Новый Массив;
	Поля.Добавить(Новый ПолеКомпоновкиДанных("Аналитика"));
	Поля.Добавить(Новый ПолеКомпоновкиДанных("Субконто1"));
	Поля.Добавить(Новый ПолеКомпоновкиДанных("Субконто2"));
	Поля.Добавить(Новый ПолеКомпоновкиДанных("Субконто3"));
	
	Для каждого ЭлементОтбора Из ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор.Элементы Цикл
		
		Если Поля.Найти(ЭлементОтбора.ЛевоеЗначение) <> Неопределено Тогда
			ПараметрыОтчета.Аналитика = ЭлементОтбора.ПравоеЗначение;
			ПараметрыОтчета.Вставить("ИспользуетсяАналитика", Истина);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьГруппировку(Поле, Группировка)
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Поле", Поле);
		ПараметрыЗаполнения.Вставить("ТипГруппировки", 0);
	Иначе
		ПараметрыЗаполнения = Поле;
	КонецЕсли;

	Если ТипЗнч(Группировка) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		НоваяГруппировка = Группировка.Добавить();
	ИначеЕсли ТипЗнч(Группировка) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		НоваяГруппировка = Группировка.Структура.Добавить();
	Иначе
		НоваяГруппировка = Группировка.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	КонецЕсли;
	
	ВыводитьОтбор = НоваяГруппировка.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВыводитьОтбор"));
	ВыводитьОтбор.Использование = Истина;
	ВыводитьОтбор.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить;
	
	БухгалтерскиеОтчетыВызовСервера.ЗаполнитьГруппировку(ПараметрыЗаполнения, НоваяГруппировка);
	
	Возврат НоваяГруппировка;
	
КонецФункции

Процедура ИзменитьЗапросПоПараметрам(ПараметрыОтчета, ПараметрыАлгоритма)
	
	ПоказателиОтчета = ПоказателиОтчета();
	
	Схема = ПараметрыАлгоритма.Схема;
	НаборДанных = Схема.НаборыДанных.ОсновнойНабор;
	ПараметрыОтбора = ПараметрыОтбораПоОрганизации(ПараметрыОтчета);
	
	СписокСубконто = СубконтоСчета(ПараметрыОтчета.Счет);
	СписокКорСубконто = СубконтоСчета(ПараметрыОтчета.КорСчет);
	Группировки = ПараметрыОтчета.Группировка;
	ТипОтчета = ПоказателиОтчета.Получить(ПараметрыОтчета.Показатель);
	Параметр = Неопределено;
	Выражение = "";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	
	Если ПараметрыОтчета.Показатель = ПоказательПрочее() Тогда
		
		ТекстЗапроса = ТекстЗапросаПрочее();
		
	ИначеЕсли ТипОтчета = 1 Тогда // Доходы
		
		ТекстЗапроса = ТекстЗапросаДоходы();

		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ПакетЗапросов = СхемаЗапроса.ПакетЗапросов[0];
		Параметр = ПакетЗапросов.Операторы[0].Источники[0].Источник.Параметры[2];
		
		Выражение = Строка(Параметр.Выражение);
		Для Каждого Субконто Из СписокСубконто Цикл 
			
			Поле = СтрШаблон("Субконто%1", Субконто.Ключ);
			Группировка = Группировки.Найти(Поле);
			
			Если Группировка = Неопределено Или Не Группировка.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Если АналитикаЗаполнена(ПараметрыОтчета)
				И Субконто.Значение.Тип.СодержитТип(ТипЗнч(ПараметрыОтчета.Аналитика)) Тогда
				Выражение = СтрЗаменить(Выражение, "&УсловиеСубконто", СтрШаблон("СубконтоКт%1 = &Субконто%1", Субконто.Ключ));
				Выражение = СтрЗаменить(Выражение, "&УсловиеКорСубконто", "Истина"); 
			ИначеЕсли Не АналитикаЗаполнена(ПараметрыОтчета)
				И ПараметрыОтчета.Показатель = ПоказательПроцентыКПолучению()
				И Субконто.Значение.Тип.СодержитТип(Тип("СправочникСсылка.ПрочиеДоходыИРасходы")) Тогда
				Выражение = СтрЗаменить(Выражение, "&УсловиеСубконто", СтрШаблон("СубконтоКт%1 В (&Субконто%1)", Субконто.Ключ));
				Выражение = СтрЗаменить(Выражение, "&УсловиеКорСубконто", "Истина");
			ИначеЕсли Не АналитикаЗаполнена(ПараметрыОтчета)
				И ПараметрыОтчета.Показатель = ПоказательПрочиеДоходы()
				И Субконто.Значение.Тип.СодержитТип(Тип("СправочникСсылка.ПрочиеДоходыИРасходы")) Тогда
				Выражение = СтрЗаменить(Выражение, "&УсловиеСубконто", СтрШаблон("Не СубконтоКт%1 В (&Субконто%1)", Субконто.Ключ));
				Выражение = СтрЗаменить(Выражение, "&УсловиеКорСубконто", "Истина");
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого КорСубконто Из СписокКорСубконто Цикл
			Если АналитикаЗаполнена(ПараметрыОтчета)
				И КорСубконто.Значение.Тип.СодержитТип(ТипЗнч(ПараметрыОтчета.Аналитика))  Тогда
				Выражение = СтрЗаменить(Выражение, "&УсловиеКорСубконто", СтрШаблон("СубконтоДт%1 = &КорСубконто%1", КорСубконто.Ключ));
				Выражение = СтрЗаменить(Выражение, "&УсловиеСубконто", "Истина");
			КонецЕсли;
		КонецЦикла;
		
		Выражение = СтрЗаменить(Выражение, "&УсловиеСубконто", "Истина");
		Выражение = СтрЗаменить(Выражение, "&УсловиеКорСубконто", "Истина");

	ИначеЕсли ТипОтчета = 2 Тогда // Расходы
		
		ТекстЗапроса = ТекстЗапросаРасходы();
		
		СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
		ПакетЗапросов = СхемаЗапроса.ПакетЗапросов[1];
		Параметр = ПакетЗапросов.Операторы[0].Источники[0].Источник.Параметры[2];
		
		Выражение = Строка(Параметр.Выражение);
		
		Для Каждого Субконто Из СписокСубконто Цикл
			Поле = СтрШаблон("Субконто%1", Субконто.Ключ);
			Группировка = Группировки.Найти(Поле);
			
			Если Группировка = Неопределено Или Не Группировка.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Если АналитикаЗаполнена(ПараметрыОтчета)
				И Субконто.Значение.Тип.СодержитТип(ТипЗнч(ПараметрыОтчета.Аналитика)) Тогда
				Выражение = СтрЗаменить(Выражение, "&УсловиеСубконто", СтрШаблон("СубконтоДт%1 = &Субконто%1", Субконто.Ключ));
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого КорСубконто Из СписокКорСубконто Цикл
			Если АналитикаЗаполнена(ПараметрыОтчета)
				И КорСубконто.Значение.Тип.СодержитТип(ТипЗнч(ПараметрыОтчета.Аналитика)) Тогда
				Выражение = СтрЗаменить(Выражение, "&УсловиеКорСубконто", СтрШаблон("СубконтоКт%1 = &КорСубконто%1", КорСубконто.Ключ));
			КонецЕсли;
		КонецЦикла;
		
		Выражение = СтрЗаменить(Выражение, "&УсловиеСубконто", "Истина");
		Выражение = СтрЗаменить(Выражение, "&УсловиеКорСубконто", "Истина");
		
	ИначеЕсли  ТипОтчета = 5 Тогда 
		
		ОрганизацияПрименяетПБУ18 = РегламентированнаяОтчетностьПереопределяемый.ПрименяетсяПБУ18(
			ПараметрыОтчета.Организация, КонецДня(ПараметрыОтчета.КонецПериода));
		
		Если ОрганизацияПрименяетПБУ18 Или ПараметрыОтчета.Показатель = ПоказательНалогНаПрибыль() Тогда
			ТекстЗапроса = ЗапросНалогНаПрибыльПриПБУ18();
		Иначе
			ТекстЗапроса = ЗапросНалогНаПрибыль();
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ТребуетсяУточнитьКорСчета(ПараметрыОтчета.Показатель) И Не ПустаяСтрока(Выражение) Тогда
		Если ТипОтчета = 1 Или ТипОтчета = 2 Тогда
			Выражение = СтрЗаменить(Выражение, "И КорСчет В (&КорСчет)", "");
		Иначе
			Выражение = СтрЗаменить(Выражение, "КорСчет В (&КорСчет)", "");
		КонецЕсли;
	КонецЕсли;
	
	Если Параметр <> Неопределено Тогда
		Параметр.Выражение = Новый ВыражениеСхемыЗапроса(Выражение);
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);

	НаборДанных.Запрос = ТекстЗапроса;

КонецПроцедуры

Функция ПараметрыАлгоритма()

	ПараметрыАлгоритма = Новый Структура;
	ПараметрыАлгоритма.Вставить("Схема", ПолучитьМакет("СхемаКомпоновкиДанных")); // Получаем схему компоновки - эталон

	Возврат ПараметрыАлгоритма;
	
КонецФункции

Функция ТребуетсяУточнитьКорСчета(Показатель)
	
	Возврат Показатель = ПоказательПрочее() Или Показатель = ПоказательНалогНаПрибыль();
	
КонецФункции

Функция АналитикаПроцентыКУплате()
	
	АналитикаПроцентыКУплате = Новый Массив;
	АналитикаПроцентыКУплате.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКПолучениюУплате);
	АналитикаПроцентыКУплате.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКУплате);
	АналитикаПроцентыКУплате.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыНачисленныеПоСт269);

	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ПрочиеДоходыИРасходы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПрочиеДоходыИРасходы КАК ПрочиеДоходыИРасходы
		|ГДЕ
		|	ПрочиеДоходыИРасходы.ВидПрочихДоходовИРасходов В(&АналитикаПроцентыКУплате)
		|";

	Запрос.УстановитьПараметр("АналитикаПроцентыКУплате", АналитикаПроцентыКУплате);
	РезультатЗапроса = Запрос.Выполнить();

	Результат = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");

	Возврат Результат;

КонецФункции

Функция ТекстЗапросаДоходы()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	&Показатель КАК Показатель,
	               |	ХозрасчетныйДвиженияССубконто.Период КАК Период,
	               |	ХозрасчетныйДвиженияССубконто.Организация КАК Организация,
	               |	ХозрасчетныйДвиженияССубконто.ПодразделениеДт КАК Подразделение,
	               |	ХозрасчетныйДвиженияССубконто.СчетКт КАК Счет,
	               |	ХозрасчетныйДвиженияССубконто.СчетДт КАК КорСчет,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК Субконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК Субконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК Субконто3,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК КорСубконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт2 КАК КорСубконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт3 КАК КорСубконто3,
	               |	ХозрасчетныйДвиженияССубконто.Регистратор КАК Документ,
	               |	ХозрасчетныйДвиженияССубконто.Сумма КАК Сумма,
	               |	ХозрасчетныйДвиженияССубконто.Содержание КАК Содержание
	               |{ВЫБРАТЬ
	               |	Период,
	               |	Организация.*,
	               |	Подразделение.*,
	               |	Документ.*,
	               |	Сумма,
	               |	Содержание,
	               |	Показатель}
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Счет В (&Счет)
	               |				И КорСчет В (&КорСчет)
	               |				И Сумма <> 0
	               |				И &УсловиеСубконто
	               |				И &УсловиеКорСубконто
	               |				И Активность,
	               |			,
	               |			) КАК ХозрасчетныйДвиженияССубконто
	               |{ГДЕ
	               |	ХозрасчетныйДвиженияССубконто.Регистратор.* КАК Документ,
	               |	ХозрасчетныйДвиженияССубконто.ПодразделениеДт.* КАК Подразделение,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт1.* КАК Субконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт2.* КАК Субконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт3.* КАК Субконто3,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт1.* КАК КорСубконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт2.* КАК КорСубконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт3.* КАК КорСубконто3,
	               |	ХозрасчетныйДвиженияССубконто.Содержание}
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаРасходы()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ХозрасчетныйОстатки.Счет КАК Счет,
	               |	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	               |	ХозрасчетныйОстатки.Организация КАК Организация,
	               |	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	               |ПОМЕСТИТЬ ОстаткиНаНачалоПериода
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(
	               |			&НачалоПериода,
	               |			Счет В (&Счет),
	               |			,
	               |			ИСТИНА) КАК ХозрасчетныйОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	&Показатель КАК Показатель,
	               |	ХозрасчетныйДвиженияССубконто.Период КАК Период,
	               |	ХозрасчетныйДвиженияССубконто.Организация КАК Организация,
	               |	ХозрасчетныйДвиженияССубконто.ПодразделениеДт КАК Подразделение,
	               |	ХозрасчетныйДвиженияССубконто.СчетДт КАК Счет,
	               |	ХозрасчетныйДвиженияССубконто.СчетКт КАК КорСчет,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Субконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт2 КАК Субконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт3 КАК Субконто3,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК КорСубконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК КорСубконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК КорСубконто3,
	               |	ХозрасчетныйДвиженияССубконто.Регистратор КАК Документ,
	               |	ХозрасчетныйДвиженияССубконто.Сумма КАК Сумма,
	               |	ХозрасчетныйДвиженияССубконто.Содержание КАК Содержание
	               |{ВЫБРАТЬ
	               |	Период,
	               |	Организация.*,
	               |	Подразделение.*,
	               |	Документ.*,
	               |	Сумма,
	               |	Содержание,
	               |	Показатель}
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			СчетДт В (&Счет)
	               |				И СчетКт В (&КорСчет)
	               |				И НЕ СчетДт В (&СчетаРасходыНаРеализацию)
	               |				И Сумма <> 0
	               |				И &УсловиеСубконто
	               |				И &УсловиеКорСубконто
	               |				И Активность,
	               |			,
	               |			) КАК ХозрасчетныйДвиженияССубконто
	               |{ГДЕ
	               |	ХозрасчетныйДвиженияССубконто.Организация.*,
	               |	ХозрасчетныйДвиженияССубконто.ПодразделениеДт.* КАК Подразделение,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт1.* КАК Субконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт2.* КАК Субконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт3.* КАК Субконто3,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт1.* КАК КорСубконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт2.* КАК КорСубконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт3.* КАК КорСубконто3,
	               |	ХозрасчетныйДвиженияССубконто.Регистратор.* КАК Документ,
	               |	ХозрасчетныйДвиженияССубконто.Содержание}
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Показатель,
	               |	ХозрасчетныйДвиженияССубконто.Период,
	               |	ХозрасчетныйДвиженияССубконто.Организация,
	               |	ХозрасчетныйДвиженияССубконто.ПодразделениеДт,
	               |	ХозрасчетныйДвиженияССубконто.СчетДт,
	               |	ХозрасчетныйДвиженияССубконто.СчетКт,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	               |	ХозрасчетныйДвиженияССубконто.Регистратор,
	               |	ХозрасчетныйДвиженияССубконто.Сумма,
	               |	ХозрасчетныйДвиженияССубконто.Содержание
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			СчетДт В (&Счет)
	               |				И СчетКт В (&КорСчет)
	               |				И Сумма <> 0
	               |				И &УсловиеСубконто
	               |				И &УсловиеКорСубконто
	               |				И Активность,
	               |			,
	               |			) КАК ХозрасчетныйДвиженияССубконто
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиНаНачалоПериода КАК ОстаткиНаНачалоПериода
	               |		ПО ХозрасчетныйДвиженияССубконто.Организация = ОстаткиНаНачалоПериода.Организация
	               |			И (ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) = ЕСТЬNULL(ОстаткиНаНачалоПериода.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)))
	               |			И ХозрасчетныйДвиженияССубконто.СубконтоКт1 = ОстаткиНаНачалоПериода.Субконто1
	               |{ГДЕ
	               |	ХозрасчетныйДвиженияССубконто.Организация.*,
	               |	ХозрасчетныйДвиженияССубконто.ПодразделениеДт.* КАК Подразделение,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт1.* КАК Субконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт2.* КАК Субконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоДт3.* КАК Субконто3,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт1.* КАК КорСубконто1,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт2.* КАК КорСубконто2,
	               |	ХозрасчетныйДвиженияССубконто.СубконтоКт3.* КАК КорСубконто3,
	               |	ХозрасчетныйДвиженияССубконто.Регистратор.* КАК Документ,
	               |	ХозрасчетныйДвиженияССубконто.Содержание}";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ЗапросНалогНаПрибыльПриПБУ18()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НалогНаПрибыль.Показатель КАК Показатель,
	|	НалогНаПрибыль.Период КАК Период,
	|	НалогНаПрибыль.Подразделение КАК Подразделение,
	|	НалогНаПрибыль.Организация КАК Организация,
	|	НалогНаПрибыль.Счет КАК Счет,
	|	НалогНаПрибыль.КорСчет КАК КорСчет,
	|	НалогНаПрибыль.Субконто1 КАК Субконто1,
	|	НалогНаПрибыль.Субконто2 КАК Субконто2,
	|	НалогНаПрибыль.Субконто3 КАК Субконто3,
	|	НалогНаПрибыль.КорСубконто1 КАК КорСубконто1,
	|	НалогНаПрибыль.КорСубконто2 КАК КорСубконто2,
	|	НалогНаПрибыль.КорСубконто3 КАК КорСубконто3,
	|	НалогНаПрибыль.Документ КАК Документ,
	|	СУММА(НалогНаПрибыль.Сумма) КАК Сумма,
	|	НалогНаПрибыль.Содержание КАК Содержание
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ) КАК Период,
	|		&Показатель КАК Показатель,
	|		ХозрасчетныйДвиженияССубконто.Регистратор КАК Документ,
	|		ХозрасчетныйДвиженияССубконто.ПодразделениеДт КАК Подразделение,
	|		ХозрасчетныйДвиженияССубконто.Организация КАК Организация,
	|		ХозрасчетныйДвиженияССубконто.СчетДт КАК Счет,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Субконто1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт2 КАК Субконто2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт3 КАК Субконто3,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК КорСубконто1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК КорСубконто2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК КорСубконто3,
	|		СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК Сумма,
	|		ХозрасчетныйДвиженияССубконто.Содержание КАК Содержание,
	|		ХозрасчетныйДвиженияССубконто.СчетКт КАК КорСчет
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				СчетДт В (&Счет)
	|					И СчетКТ В (&КорСчет),
	|				,
	|				) КАК ХозрасчетныйДвиженияССубконто
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ХозрасчетныйДвиженияССубконто.Организация,
	|		ХозрасчетныйДвиженияССубконто.ПодразделениеДт,
	|		ХозрасчетныйДвиженияССубконто.Содержание,
	|		ХозрасчетныйДвиженияССубконто.Регистратор,
	|		НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ),
	|		ХозрасчетныйДвиженияССубконто.СчетДт,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|		ХозрасчетныйДвиженияССубконто.СчетКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ),
	|		&Показатель,
	|		ХозрасчетныйДвиженияССубконто.Регистратор,
	|		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеКт, &ПустоеПодразделение),
	|		ХозрасчетныйДвиженияССубконто.Организация,
	|		ХозрасчетныйДвиженияССубконто.СчетКт,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|		СУММА(-ХозрасчетныйДвиженияССубконто.Сумма),
	|		ХозрасчетныйДвиженияССубконто.Содержание,
	|		ХозрасчетныйДвиженияССубконто.СчетДт
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				СчетКт В (&Счет)
	|					И СчетДТ В (&КорСчет),
	|				,
	|				) КАК ХозрасчетныйДвиженияССубконто
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ХозрасчетныйДвиженияССубконто.Организация,
	|		ХозрасчетныйДвиженияССубконто.Содержание,
	|		ХозрасчетныйДвиженияССубконто.Регистратор,
	|		НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ),
	|		ХозрасчетныйДвиженияССубконто.СчетДт,
	|		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеКт, &ПустоеПодразделение),
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|		ХозрасчетныйДвиженияССубконто.СчетКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ),
	|		&Показатель,
	|		ХозрасчетныйДвиженияССубконто.Регистратор,
	|		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	|		ХозрасчетныйДвиженияССубконто.Организация,
	|		ХозрасчетныйДвиженияССубконто.СчетДт,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|		СУММА(ХозрасчетныйДвиженияССубконто.Сумма),
	|		ХозрасчетныйДвиженияССубконто.Содержание,
	|		ХозрасчетныйДвиженияССубконто.СчетКт
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				СчетДт В (&Счет)
	|					И СчетКт В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль)),
	|				,
	|				) КАК ХозрасчетныйДвиженияССубконто
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ХозрасчетныйДвиженияССубконто.Организация,
	|		ХозрасчетныйДвиженияССубконто.ВалютаДт,
	|		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	|		ХозрасчетныйДвиженияССубконто.СчетДт,
	|		ХозрасчетныйДвиженияССубконто.Регистратор,
	|		ХозрасчетныйДвиженияССубконто.Содержание,
	|		НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ),
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|		ХозрасчетныйДвиженияССубконто.СчетКт
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ),
	|		&Показатель,
	|		ХозрасчетныйДвиженияССубконто.Регистратор,
	|		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	|		ХозрасчетныйДвиженияССубконто.Организация,
	|		ХозрасчетныйДвиженияССубконто.СчетДт,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|		СУММА(-ХозрасчетныйДвиженияССубконто.Сумма),
	|		ХозрасчетныйДвиженияССубконто.Содержание,
	|		ХозрасчетныйДвиженияССубконто.СчетКт
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				СчетКт В (&Счет)
	|					И СчетДт В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НераспределеннаяПрибыль)),
	|				,
	|				) КАК ХозрасчетныйДвиженияССубконто
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ХозрасчетныйДвиженияССубконто.Организация,
	|		ХозрасчетныйДвиженияССубконто.ВалютаДт,
	|		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	|		ХозрасчетныйДвиженияССубконто.СчетДт,
	|		ХозрасчетныйДвиженияССубконто.Регистратор,
	|		ХозрасчетныйДвиженияССубконто.Содержание,
	|		НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ),
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|		ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|		ХозрасчетныйДвиженияССубконто.СчетКт) КАК НалогНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	НалогНаПрибыль.КорСубконто1,
	|	НалогНаПрибыль.КорСубконто2,
	|	НалогНаПрибыль.Период,
	|	НалогНаПрибыль.КорСчет,
	|	НалогНаПрибыль.Содержание,
	|	НалогНаПрибыль.Показатель,
	|	НалогНаПрибыль.Субконто3,
	|	НалогНаПрибыль.КорСубконто3,
	|	НалогНаПрибыль.Счет,
	|	НалогНаПрибыль.Субконто2,
	|	НалогНаПрибыль.Организация,
	|	НалогНаПрибыль.Субконто1,
	|	НалогНаПрибыль.Документ,
	|	НалогНаПрибыль.Подразделение
	|
	|ИМЕЮЩИЕ
	|	СУММА(НалогНаПрибыль.Сумма) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НалогНаПрибыль.Период";

	Возврат ТекстЗапроса;

КонецФункции

Функция ЗапросНалогНаПрибыль()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ХозрасчетныйДвиженияССубконто.Период, МЕСЯЦ) КАК Период,
	|	&Показатель КАК Показатель,
	|	ХозрасчетныйДвиженияССубконто.ПодразделениеДт КАК Подразделение,
	|	-ХозрасчетныйДвиженияССубконто.Сумма КАК Сумма,
	|	ХозрасчетныйДвиженияССубконто.Организация КАК Организация,
	|	ХозрасчетныйДвиженияССубконто.СчетДт КАК Счет,
	|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Документ,
	|	ХозрасчетныйДвиженияССубконто.Содержание КАК Содержание,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Субконто1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт2 КАК Субконто2,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт3 КАК Субконто3,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК КорСубконто1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК КорСубконто2,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК КорСубконто3
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Счет В (&Счет)
	|				И КорСчет В (&КорСчет),
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаПрочее()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	&Показатель КАК Показатель,
	               |	Прочее.Период КАК Период,
	               |	Прочее.Организация КАК Организация,
	               |	Прочее.Подразделение КАК Подразделение,
	               |	Прочее.Документ КАК Документ,
	               |	Прочее.Содержание КАК Содержание,
	               |	Прочее.Субконто1 КАК Субконто1,
	               |	Прочее.КорСубконто1 КАК КорСубконто1,
	               |	Прочее.Счет КАК Счет,
	               |	Прочее.КорСчет КАК КорСчет,
	               |	СУММА(Прочее.Сумма) КАК Сумма
	               |{ВЫБРАТЬ
	               |	Организация.*,
	               |	Подразделение.*,
	               |	Документ.*,
	               |	Субконто1.*,
	               |	КорСубконто1.*,
	               |	Содержание}
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период КАК Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация КАК Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение) КАК Подразделение,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор КАК Документ,
	               |		ХозрасчетныйДвиженияССубконто.Содержание КАК Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК Субконто1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК КорСубконто1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт КАК Счет,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт КАК КорСчет,
	               |		СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК Сумма
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетКт В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки)
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.Содержание
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеКт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		-ХозрасчетныйДвиженияССубконто.Сумма
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетДт В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки)
	               |					И НЕ СчетКт В ИЕРАРХИИ (&ИсключаемыеСчетаНалоговНаПрибыль)
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеКт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.Сумма,
	               |		ХозрасчетныйДвиженияССубконто.Содержание
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		СУММА(ХозрасчетныйДвиженияССубконто.Сумма)
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетДт В (&СчетаПрочиеПрибылиИУбытки)
	               |					И СчетКт В ИЕРАРХИИ (&СчетаКорреспонденты)
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.Содержание
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		СУММА(-ХозрасчетныйДвиженияССубконто.Сумма)
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетДт В ИЕРАРХИИ (&СчетаКорреспонденты)
	               |					И СчетКт В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки)
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.Содержание
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		СУММА(ХозрасчетныйДвиженияССубконто.Сумма)
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетКт В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки)
	               |					И СчетДт В ИЕРАРХИИ (&РасчетыПоНалогам)
	               |					И СубконтоКт1 = &РасчетыСБюджетом
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.Содержание) КАК Прочее
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Прочее.Субконто1,
	               |	Прочее.КорСубконто1,
	               |	Прочее.Подразделение,
	               |	Прочее.Организация,
	               |	Прочее.Период,
	               |	Прочее.Документ,
	               |	Прочее.Содержание,
	               |	Прочее.Счет,
	               |	Прочее.КорСчет
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(Прочее.Сумма) <> 0
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период";

	Возврат ТекстЗапроса;

КонецФункции

Функция ПредставленияСубконто()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура, "Номенклатура");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций, "Работник организации");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства, "Основное средство");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты, "Контрагент");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы, "Нематериальный актив");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат, "Статья затрат");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета, "Банковский счет");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства, "Объект строительства");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы, "Номенклатурная группа");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы, "Прочий доход или расход");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрибылиИУбытки, "Прибыль или убыток");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры, "Договор");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов, "Расход будущих периодов");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоходыБудущихПериодов, "Доход будущих периодов");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ЦенныеБумаги, "Ценная бумага");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств, "Статья движения денежных средств");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет, "Вид платежа в бюджет (фонды)");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыАктивовИОбязательств, "Вид активов и обязательств");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НаправленияИспользованияПрибыли, "Направление использования прибыли");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.УровниБюджетов, "Уровень бюджетов");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС, "Ставка НДС");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады, "Склад");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыНаНИОКР, "Расход на НИОКР");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидНачисленийОплатыТрудаПоСтатье255НК, "Вид начислений оплаты труда");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Резервы, "Оценочное обязательство и резерв");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СпособыСтроительства, "Способ строительства");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбособленныеПодразделения, "Обособленное подразделение");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Комиссионеры, "Комиссионер");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии, "Партия");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами, "Документ расчетов с контрагентом");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Продавцы, "Продавец");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Покупатели, "Покупатель");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные, "Счет-фактура полученная");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыЦенностей, "Вид ценностей");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Бланкистрогойотчетности, "Бланк строгой отчетности");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыРасчетовПоСредствамФСС, "Вид расчетов по средствам ФСС");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРеализации, "Документ реализации");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации, "Партия материалов в эксплуатации");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыСтоимости, "Вид стоимости");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НомераГТД, "Номер ГТД");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтраныПроисхождения, "Страна происхождения");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФВыданные, "Счет-фактура выданная");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыАмортизационнойПремии, "Документ амортизационной премии");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РеализуемыеАктивы, "Реализуемый актив");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДенежныеДокументы, "Денежный документ");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СпособыУчетаНДС, "Способ учета НДС");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ЭлементыЗатрат, "Элемент затрат");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция, "Продукция");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Учредители, "Учредитель");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Билеты, "Билет");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ТранспортныеСредства, "Транспортное средство");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Убыткипрошлыхлет, "Убыток прошлых лет");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ИсполнительныеЛистыРаботников, "Исполнительный лист работника");
	Результат.Вставить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыНалогов, "Вид налога");
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьКорСчетаПоПоказателю(ПараметрыОтчета)
	
	ТипыКорСубконто = НовыйТипыКорСубконто();
	
	ПараметрыОтбора = ПараметрыОтбораПоОрганизации(ПараметрыОтчета);

	Если ПараметрыОтчета.Показатель = ПоказательПрочиеДоходы() Тогда
		Запрос = КорСубконтоПрочиеДоходы(ПараметрыОтчета, ПараметрыОтбора);
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательПрочиеРасходы() Тогда
		Запрос = КорСубконтоПрочиеРасходы(ПараметрыОтчета, ПараметрыОтбора);
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательДоходыОтУчастияВДругихОрганизациях() Тогда
		Запрос = КорСубконтоДоходыОтУчастияВДругихОрганизациях(ПараметрыОтчета, ПараметрыОтбора);
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательПроцентыКПолучению() Тогда
		Запрос = КорСубконтоДоходыПоКредитам(ПараметрыОтчета, ПараметрыОтбора);
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательПроцентыКУплате() Тогда
		Запрос = КорСубконтоРасходыПоКредитам(ПараметрыОтчета, ПараметрыОтбора);
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательКоммерческиеРасходы() Тогда
		Запрос = КорСубконтоКоммерческиеРасходы(ПараметрыОтчета, ПараметрыОтбора);
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательУправленческиеРасходы() Тогда
		Запрос = КорСубконтоУправленческиеРасходы(ПараметрыОтчета, ПараметрыОтбора);
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательНалогНаПрибыль()
		Или ПараметрыОтчета.Показатель = ПоказательНалогПриУСН() 
		Или ПараметрыОтчета.Показатель = ПоказательНалогНаПрофессиональныйДоход() Тогда
			
			Запрос = КорСубконтоНалогНаПрибыль(ПараметрыОтчета, ПараметрыОтбора);
			
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательПрочее() Тогда
		Запрос = КорСубконтоПрочее(ПараметрыОтчета, ПараметрыОтбора);
	КонецЕсли;
	
	Если Запрос = Неопределено Тогда
		
		Возврат ТипыКорСубконто;
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСубконто1 = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаСубконто1.Следующий() Цикл
		
		ВыборкаСубконто2 = ВыборкаСубконто1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСубконто2.Следующий() Цикл
			
			ВыборкаСубконто3 = ВыборкаСубконто2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаСубконто3.Следующий() Цикл
				
				ВыборкаНаименование = ВыборкаСубконто3.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаНаименование.Следующий() Цикл
					
					Выборка = ВыборкаНаименование.Выбрать();
					
					КорСчета    = Новый Массив;
					НоваяСтрока = ТипыКорСубконто.Добавить();
					
					Пока Выборка.Следующий() Цикл 
						КорСчета.Добавить(Выборка.КорСчет);
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, , "КорСчет");
					КонецЦикла;
					
					НоваяСтрока.КорСчет = КорСчета;
				КонецЦикла;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТипыКорСубконто;
	
КонецФункции

Функция ДобавитьДетальныеЗаписи(ГруппировкаРодитель, ГруппировкаОсновная, КоличествоКорСубконто, ИспользуютсяПодразделения, БезСубконто = Ложь)
	
	Если ТипЗнч(ГруппировкаРодитель) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		ДетальныеЗаписи = ГруппировкаРодитель.Структура.Добавить();
	Иначе
		ДетальныеЗаписи = ГруппировкаРодитель.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	КонецЕсли;
	
	ИмяГруппы = СтрШаблон("Детали%1", КоличествоКорСубконто);
	
	Если ИспользуютсяПодразделения Тогда
		ИмяГруппы = СтрШаблон("%1_П", ИмяГруппы);
	КонецЕсли;
	
	Если БезСубконто Тогда
		ИмяГруппы = СтрШаблон("%1_БезСубконто", ИмяГруппы);
	КонецЕсли;

	ДетальныеЗаписи.Имя = ИмяГруппы;
	
	ВыбранноеПоле = ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Заголовок = НСтр("ru='Период'");
	ВыбранноеПоле.Поле      = Новый ПолеКомпоновкиДанных("Период");
	
	Порядок = ДетальныеЗаписи.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	Порядок.Использование     = Истина;
	Порядок.Поле              =  Новый ПолеКомпоновкиДанных("Период");
	Порядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	Порядок.РежимОтображения  = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ТипЗаголовкаПолей = ДетальныеЗаписи.ПараметрыВывода.НайтиЗначениеПараметра(
		Новый ПараметрКомпоновкиДанных("ТипЗаголовкаПолей"));
	ТипЗаголовкаПолей.Использование = Истина;
	ТипЗаголовкаПолей.Значение = ТипЗаголовкаПолейКомпоновкиДанных.Полный;

	ВыбранноеПоле = ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Заголовок = НСтр("ru='Документ'");
	ВыбранноеПоле.Поле      = Новый ПолеКомпоновкиДанных("Документ");
	
	ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	ВыводитьОтбор = ГруппировкаОсновная.ПараметрыВывода.НайтиЗначениеПараметра(
		Новый ПараметрКомпоновкиДанных("ВыводитьОтбор"));
	ВыводитьОтбор.Использование = Истина;
	ВыводитьОтбор.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить;
	
	ВыводитьОтбор = ДетальныеЗаписи.ПараметрыВывода.НайтиЗначениеПараметра(
		Новый ПараметрКомпоновкиДанных("ВыводитьОтбор"));
	ВыводитьОтбор.Использование = Истина;
	ВыводитьОтбор.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить;

	Возврат ДетальныеЗаписи;
	
КонецФункции

Процедура ПередКомпоновкойМакетаПоказатели(ПараметрыОтчета, Схема, КомпоновщикНастроек)
	
	ПараметрыАлгоритма = ПараметрыАлгоритма();
	ИспользуютсяПодразделения = Ложь;
	ГруппировкаПодразделение = ПараметрыОтчета.Группировка.Найти("Подразделение");
	Если ГруппировкаПодразделение <> Неопределено Тогда
		ИспользуютсяПодразделения = ГруппировкаПодразделение.Использование;
	КонецЕсли;
	КорСчетаПоПоказателю = ПолучитьКорСчетаПоПоказателю(ПараметрыОтчета);
	
	ПараметрыОтчета.Вставить("КорСчетаПоПоказателю", КорСчетаПоПоказателю);

	// Изменим запрос под показатель
	ИзменитьЗапросПоПараметрам(ПараметрыОтчета, ПараметрыАлгоритма);
	Схема = ПараметрыАлгоритма.Схема;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Схема));

	// Установка параметров схемы компоновки
	
	Счета = Новый Массив;
	КорСчета = Новый Массив;
	
	ПрочиеДоходыИРасходы = АналитикаПроцентыКПолучению();
	АналитикаЗаполнена = АналитикаЗаполнена(ПараметрыОтчета);
	
	Для Каждого СтрокаСчета Из КорСчетаПоПоказателю Цикл
		
		Если Счета.Найти(СтрокаСчета.Счет) = Неопределено Тогда
			Счета.Добавить(СтрокаСчета.Счет);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КорСчета, СтрокаСчета.КорСчет);
		
		СписокСубконто = СубконтоСчета(СтрокаСчета.Счет);
		СписокКорСубконто = СубконтоСчета(СтрокаСчета.КорСчет[0]);
		КоличествоКорСубконто = СписокКорСубконто.Количество();
		КоличествоСубконто = Мин(ПараметрыОтчета.Группировка.Итог("Использование"), СписокСубконто.Количество());
		ПолеОтбора = Неопределено;
		Если КоличествоСубконто > 0 Тогда
			Для НомерСубконто = 1 По КоличествоСубконто Цикл
				Если АналитикаЗаполнена
					И СписокСубконто[НомерСубконто].Тип.СодержитТип(ТипЗнч(ПараметрыОтчета.Аналитика)) Тогда

					БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
						"Субконто" + НомерСубконто, ПараметрыОтчета.Аналитика);
					
				ИначеЕсли Не АналитикаЗаполнена
					И (ПараметрыОтчета.Показатель = ПоказательПроцентыКПолучению()
					Или ПараметрыОтчета.Показатель = ПоказательПрочиеДоходы())
					И СписокСубконто[НомерСубконто].Тип.СодержитТип(Тип("СправочникСсылка.ПрочиеДоходыИРасходы")) Тогда
			
					БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
						"Субконто" + НомерСубконто, ПрочиеДоходыИРасходы);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Для СчетчикСубконто = 1 По КоличествоКорСубконто Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаСчета[СтрШаблон("ВидСубконто%1", СчетчикСубконто)]) Тогда
				Продолжить;
			КонецЕсли;
			
			ТипЗначенияСубконто = СтрокаСчета[СтрШаблон("ВидСубконто%1", СчетчикСубконто)].ТипЗначения;
			
			Если АналитикаЗаполнена
					И ТипЗначенияСубконто.СодержитТип(ТипЗнч(ПараметрыОтчета.Аналитика)) Тогда
				
				БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
					СтрШаблон("КорСубконто%1", СчетчикСубконто), ПараметрыОтчета.Аналитика);
			
			КонецЕсли;
			
		КонецЦикла;

	КонецЦикла;

	ПредставленияСубконто = ПредставленияСубконто();
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"Счет", Счета);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"КорСчет", КорСчета);
	
	СчетаРасходыНаРеализацию = Новый Массив;
	СчетаРасходыНаРеализацию.Добавить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажу);
	СчетаРасходыНаРеализацию.Добавить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажуНеЕНВД);
	СчетаРасходыНаРеализацию.Добавить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажуЕНВД);

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"СчетаРасходыНаРеализацию", СчетаРасходыНаРеализацию);
	Если ПараметрыОтчета.Показатель = ПоказательПрочее() Тогда
		
		УстановитьПараметрыПрочее(КомпоновщикНастроек);
		
	КонецЕсли;
	
	// Формирование структуры отчета
	
	КомпоновщикНастроек.Настройки.Структура.Очистить();
		
	ПоляНабораДанных = Схема.НаборыДанных.ОсновнойНабор.Поля;
	
	ГруппировкаОсновная = КомпоновщикНастроек.Настройки;
	ГруппировкаОтчет = ГруппировкаОсновная;
	ГруппировкаРодитель = ГруппировкаОсновная;
	
	ВывестиЗаголовок = КорСчетаПоПоказателю.Количество() > 1;

	Для Каждого СтрокаСчета Из КорСчетаПоПоказателю Цикл
		
		СписокСубконто = СубконтоСчета(СтрокаСчета.Счет);
		КоличествоСубконто = Мин(ПараметрыОтчета.Группировка.Итог("Использование"), СписокСубконто.Количество());
		СписокКорСубконто = СубконтоСчета(СтрокаСчета.КорСчет[0]);
		КоличествоКорСубконто = СписокКорСубконто.Количество();
		
		ЭтоПерваяСтрока = Истина;
		Если КоличествоСубконто > 0 Тогда
			Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл
				
				НомерСубконто = НомерСубконтоПоПолю(ПолеВыбраннойГруппировки.Поле);
				
				Если ПолеВыбраннойГруппировки.Использование = Ложь Тогда
					Продолжить;
				КонецЕсли;
				
				Если ЭтоПерваяСтрока = Истина Тогда
					ЭтоПерваяСтрока = Ложь;
 					ГруппировкаРодитель = ДобавитьГруппировку(ПолеВыбраннойГруппировки.Поле, ГруппировкаОсновная);
					ГруппировкаОтчет = ГруппировкаРодитель;
					Если ИспользуютсяПодразделения Тогда
						
						ИмяГруппы = СтрШаблон("Группировка_П_С%1_КС%2:%3", КоличествоСубконто, КоличествоКорСубконто,
							НомерСубконто);
					Иначе
						ИмяГруппы = СтрШаблон("Группировка_С%1_КС%2:%3", КоличествоСубконто, КоличествоКорСубконто,
							НомерСубконто);
					КонецЕсли;
					
					ГруппировкаРодитель.Имя = ИмяГруппы;
					
					// Выведем заголовок
					Если ВывестиЗаголовок Тогда
						ЗаголовокТаблицы(ГруппировкаРодитель, СтрокаСчета.Наименование);
					КонецЕсли;
					
					// Установим отбор по кор. счетам
					ОтборПоКорСчетам(ГруппировкаРодитель, СтрокаСчета);
				Иначе
					ГруппировкаРодитель = ДобавитьГруппировку(ПолеВыбраннойГруппировки.Поле, ГруппировкаРодитель);
					
					Если ИспользуютсяПодразделения Тогда
						ИмяГруппы = СтрШаблон("Группировка_П_С%1_КС%2:%3", КоличествоСубконто, КоличествоКорСубконто,
						НомерСубконто);
					Иначе
						ИмяГруппы = СтрШаблон("Группировка_С%1_КС%2:%3", КоличествоСубконто, КоличествоКорСубконто,
						НомерСубконто);
					КонецЕсли;
					
					ГруппировкаРодитель.Имя = ИмяГруппы;
					
				КонецЕсли;
				
				НайденноеПоле = ПоляНабораДанных.Найти(ПолеВыбраннойГруппировки.Поле);
				
				Если НайденноеПоле <> Неопределено Тогда
					Если НомерСубконто <> 0 Тогда
						НайденноеПоле.Заголовок = СписокСубконто[НомерСубконто].Представление;
					КонецЕсли;
					НайденноеПоле.ВыражениеПредставления = 
						СтрШаблон("Выбор Когда Не ЗначениеЗаполнено(%1) Тогда ""%2"" Иначе %1 Конец", ПолеВыбраннойГруппировки.Поле, ПустоеЗначение());
				КонецЕсли;
				
			КонецЦикла;
		Иначе
			ГруппировкаРодитель = ГруппировкаОсновная;
		КонецЕсли;
		
		ДетальныеЗаписи = ДобавитьДетальныеЗаписи(ГруппировкаРодитель, ГруппировкаОтчет, КоличествоКорСубконто, ИспользуютсяПодразделения,
			КоличествоСубконто = 0);
		
		Если КоличествоСубконто = 0 Тогда
			
			// Выведем заголовок
			Если ВывестиЗаголовок Тогда
				ЗаголовокТаблицы(ДетальныеЗаписи, СтрокаСчета.Наименование);
			КонецЕсли;
			
			// Установим отбор по кор. счетам
			ОтборПоКорСчетам(ДетальныеЗаписи, СтрокаСчета);
			
		КонецЕсли;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаСчета.КорСчет[0]);

		Для СчетчикСубконто = 1 По КоличествоКорСубконто Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаСчета[СтрШаблон("ВидСубконто%1", СчетчикСубконто)]) Тогда
				Продолжить;
			КонецЕсли;
			ТипЗначенияСубконто = СтрокаСчета[СтрШаблон("ВидСубконто%1", СчетчикСубконто)].ТипЗначения;
			Если Не (АналитикаЗаполнена(ПараметрыОтчета)
				И ТипЗначенияСубконто.СодержитТип(ТипЗнч(ПараметрыОтчета.Аналитика))) Тогда

				ВыбранноеПоле = ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				
				Заголовок = ПредставленияСубконто[СтрокаСчета[СтрШаблон("ВидСубконто%1", СчетчикСубконто)]];
				Если ПустаяСтрока(Заголовок) Тогда 
					Заголовок =  СвойстваСчета["ВидСубконто" + СчетчикСубконто + "Наименование"];
				КонецЕсли;
				
				ВыбранноеПоле.Заголовок = Заголовок;
				ВыбранноеПоле.Поле      = Новый ПолеКомпоновкиДанных(СтрШаблон("КорСубконто%1", СчетчикСубконто));
				
				Порядок = ДетальныеЗаписи.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
				Порядок.Использование     = Истина;
				Порядок.Поле              = Новый ПолеКомпоновкиДанных(СтрШаблон("КорСубконто%1", СчетчикСубконто));
				Порядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
				Порядок.РежимОтображения  = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция АналитикаЗаполнена(ПараметрыОтчета)
	
	Возврат ЗначениеЗаполнено(ПараметрыОтчета.Аналитика) Или ПараметрыОтчета.ИспользуетсяАналитика;
	
КонецФункции

Процедура ПередКомпоновкойМакетаРасчетныеПоказатели(ПараметрыОтчета, Схема, КомпоновщикНастроек)
	
	Показатель = ПараметрыОтчета.Показатель;
	ЗначениеПоказателя = 0;
	
	ПараметрыОтбора = ПараметрыОтбораПоОрганизации(ПараметрыОтчета);

	Данные = Отчеты.АнализФинансовыхРезультатов.ДанныеРасчетаПоказателя(ПараметрыОтчета.Показатель, ПараметрыОтчета, ПараметрыОтбора);
	Если Показатель = ПоказательРентабельностьПродаж() Тогда
		ТаблицаПоказателиДляРасчета = Отчеты.АнализФинансовыхРезультатов.ТаблицаПоказателейДляРасшифровки();
		Для Каждого СтрокаДанные Из Данные Цикл
			Если СтрокаДанные.Показатель = ПоказательРентабельностьПродаж()
				И ПустаяСтрока(СтрокаДанные.ЭлементФормулы) Тогда
				ЗначениеПоказателя = СтрокаДанные.СуммаПоказателя;
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ТаблицаПоказателиДляРасчета.Добавить(), СтрокаДанные);
		КонецЦикла;
		Данные = ТаблицаПоказателиДляРасчета;
	Иначе
		ЗначениеПоказателя = Данные.Итог("СуммаПоказателя");
	КонецЕсли;
	
	ПараметрыОтчета.Вставить("Данные", Данные);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"РасшифровываемыйПоказатель", ПараметрыОтчета.Показатель);
	
	РежимОкругленияОтчета = ПараметрыОтчета.ОкруглениеСумм;
	Формула = ФормулаПоПоказателю(ПараметрыОтчета.Показатель, Данные, РежимОкругленияОтчета);

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"Формула", Формула);

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ЗначениеПоказателя", ЗначениеПоказателя);

КонецПроцедуры

Функция ФормулаПоПоказателю(Показатель, ИсходнаяТаблица, РежимОкругленияОтчета)
	
	Формула = "";
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПоказателиДляРасчета.ЭлементФормулы КАК ЭлементФормулы,
	               |	ПоказателиДляРасчета.СуммаПоказателя КАК СуммаПоказателя,
	               |	ПоказателиДляРасчета.НомерПоказателя КАК Номер
	               |ПОМЕСТИТЬ ПоказателиДляРасчета
	               |ИЗ
	               |	&ПоказателиДляРасчета КАК ПоказателиДляРасчета
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПоказателиДляРасчета.ЭлементФормулы КАК Показатель,
	               |	СУММА(ПоказателиДляРасчета.СуммаПоказателя) КАК СуммаПоказателя,
	               |	ПоказателиДляРасчета.Номер КАК Номер
	               |ИЗ
	               |	ПоказателиДляРасчета КАК ПоказателиДляРасчета
	               |ГДЕ
	               |	ПоказателиДляРасчета.Номер <> 0
	               |	И ПоказателиДляРасчета.СуммаПоказателя <> 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПоказателиДляРасчета.ЭлементФормулы,
	               |	ПоказателиДляРасчета.Номер
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Номер УБЫВ";
	Запрос.УстановитьПараметр("ПоказателиДляРасчета", ИсходнаяТаблица);
	
	РезультатЗапроса = Запрос.Выполнить();
	Показатели = РезультатЗапроса.Выгрузить();
	СоответствиеПоказателей = Новый Соответствие;
	МинимальноеКоличествоПоказателей = 2;
	
	Если Показатели.Количество() < МинимальноеКоличествоПоказателей Тогда 
		Возврат Формула;
	КонецЕсли;
	
	Для Каждого ЭлементФормулы Из Показатели Цикл
		
		Если РежимОкругленияОтчета = БыстрыеНастройкиОтчетовСервер.ОкруглениеСуммДоЦелых() Тогда
			Сумма = Окр(ЭлементФормулы.СуммаПоказателя, 0);
		ИначеЕсли РежимОкругленияОтчета = БыстрыеНастройкиОтчетовСервер.ОкруглениеСуммДоТысяч() 
			И ЭлементФормулы.СуммаПоказателя <> 0 Тогда
			Сумма = Окр(ЭлементФормулы.СуммаПоказателя / 1000, 0);
		Иначе
			Сумма = ЭлементФормулы.СуммаПоказателя;
		КонецЕсли;
		
		Если Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СоответствиеПоказателей.Вставить(ЭлементФормулы.Показатель, ЭлементФормулы.Номер);
	КонецЦикла;
 
	Если Показатель = ПоказательВаловаяПрибыль() Тогда
		
		Формула = СтрШаблон("(%1) - (%2)", 
			СоответствиеПоказателей.Получить(ПоказательВыручка()), 
			СоответствиеПоказателей.Получить(ПоказательСебестоимость()));
		
	ИначеЕсли Показатель = ПоказательРентабельностьПродаж() Тогда
		
		Формула = СтрШаблон("(%1) / (%2)",
			СоответствиеПоказателей.Получить(ПоказательПрибыльУбытокОтПродаж()),
			СоответствиеПоказателей.Получить(ПоказательВыручка()));
		
	ИначеЕсли Показатель = ПоказательЧистаяПрибыль() Тогда
		
		ПоказательПрибыльУбытокДоНалогообложения = СоответствиеПоказателей.Получить(ПоказательПрибыльУбытокДоНалогообложения());
		
		Если ПоказательПрибыльУбытокДоНалогообложения <> Неопределено Тогда
			
			Формула = СтрШаблон("(%1)", ПоказательПрибыльУбытокДоНалогообложения);
			
		КонецЕсли;
		
		Для Каждого ЭлементФормулы Из СоответствиеПоказателей Цикл
			
			Если ЭлементФормулы.Ключ = ПоказательПрибыльУбытокДоНалогообложения() Тогда
				Продолжить;
			ИначеЕсли ЭлементФормулы.Ключ = ПоказательНалогНаПрибыль()
				Или ЭлементФормулы.Ключ = ПоказательНалогПриУСН() 
				Или ЭлементФормулы.Ключ = ПоказательНалогНаПрофессиональныйДоход()
				Или ЭлементФормулы.Ключ = ПоказательПрочее() Тогда
				Формула = СтрШаблон("%1 - (%2)", Формула, ЭлементФормулы.Значение);
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Показатель = ПоказательПрибыльУбытокОтПродаж() Тогда
		
		ПоказательВаловаяПрибыль = СоответствиеПоказателей.Получить(ПоказательВаловаяПрибыль());
		
		Если ПоказательВаловаяПрибыль <> Неопределено Тогда
			
			Формула = СтрШаблон("(%1)", ПоказательВаловаяПрибыль);
			
		КонецЕсли;
		
		Для Каждого ЭлементФормулы Из СоответствиеПоказателей Цикл

			Если ЭлементФормулы.Ключ = ПоказательВаловаяПрибыль() Тогда
				Продолжить;
			ИначеЕсли ЭлементФормулы.Ключ = ПоказательКоммерческиеРасходы()
				Или ЭлементФормулы.Ключ = ПоказательУправленческиеРасходы() Тогда
				Формула = СтрШаблон("%1 - (%2)", Формула, ЭлементФормулы.Значение);
			КонецЕсли;
			
		КонецЦикла;
	
	ИначеЕсли Показатель = ПоказательПрибыльУбытокДоНалогообложения() Тогда
		
		ПоказательПрибыльУбытокОтПродаж = СоответствиеПоказателей.Получить(ПоказательПрибыльУбытокОтПродаж());
		
		Если ПоказательПрибыльУбытокОтПродаж <> Неопределено Тогда

			Формула = СтрШаблон("(%1)", ПоказательПрибыльУбытокОтПродаж);
		
		КонецЕсли;
		
		Для Каждого ЭлементФормулы Из СоответствиеПоказателей Цикл

			Если ЭлементФормулы.Ключ = ПоказательПрибыльУбытокОтПродаж() Тогда
				Продолжить;
			ИначеЕсли ЭлементФормулы.Ключ = ПоказательПрочиеДоходы()
				Или ЭлементФормулы.Ключ = ПоказательПроцентыКПолучению()
				Или ЭлементФормулы.Ключ = ПоказательДоходыОтУчастияВДругихОрганизациях() Тогда
				Формула = СтрШаблон("%1 + (%2)", Формула, ЭлементФормулы.Значение);
			ИначеЕсли ЭлементФормулы.Ключ = ПоказательКоммерческиеРасходы()
				Или ЭлементФормулы.Ключ = ПоказательУправленческиеРасходы()
				Или ЭлементФормулы.Ключ = ПоказательПрочиеРасходы()
				Или ЭлементФормулы.Ключ = ПоказательПроцентыКУплате() Тогда
				Формула = СтрШаблон("%1 - (%2)", Формула, ЭлементФормулы.Значение);
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли Показатель = ПоказательЧистаяПрибыль() Тогда
		
		ПоказательПрибыльУбытокДоНалогообложения = СоответствиеПоказателей.Получить(ПоказательПрибыльУбытокДоНалогообложения());
		
		Если ПоказательПрибыльУбытокДоНалогообложения <> Неопределено Тогда
			
			Формула = СтрШаблон("(%1)", ПоказательПрибыльУбытокДоНалогообложения);
			
		КонецЕсли;
		
		Для Каждого ЭлементФормулы Из СоответствиеПоказателей Цикл

			Если ЭлементФормулы.Ключ = ПоказательПрибыльУбытокДоНалогообложения() Тогда
				Продолжить;
			ИначеЕсли ЭлементФормулы.Ключ = ПоказательНалогНаПрибыль()
				Или ЭлементФормулы.Ключ = ПоказательНалогНаПрофессиональныйДоход()
				Или ЭлементФормулы.Ключ = ПоказательНалогПриУСН()
				Или ЭлементФормулы.Ключ = ПоказательПрочее() Тогда
				Формула = СтрШаблон("%1 - (%2)", Формула, ЭлементФормулы.Значение);
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат Формула;
	
КонецФункции

Функция СтрокаДляРасшифровки()
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", 0);
	СтрокаДляРасшифровки.Вставить("Поле", "");
	СтрокаДляРасшифровки.Вставить("Представление", "");
	СтрокаДляРасшифровки.Вставить("ТипГруппировки", 0);
	
	Возврат СтрокаДляРасшифровки;
	
КонецФункции

Функция ЭтоАнализВыручки(Показатель)
	
	Возврат Показатель = "Выручка" Или Показатель = "Себестоимость";
	
КонецФункции

Процедура ОтборПоКорСчетам(ГруппировкаРодитель, СтрокаКорСубконто)
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
		ГруппировкаРодитель.Отбор,
		"КорСчет",
		СтрокаКорСубконто.КорСчет,
		ВидСравненияКомпоновкиДанных.ВИерархии);

КонецПроцедуры

Процедура ЗаголовокТаблицы(ГруппировкаРодитель, Наименование)
	
	ПараметрИспользованияЗаголовка = ГруппировкаРодитель.ПараметрыВывода.Элементы.Найти("ВыводитьЗаголовок");
	ПараметрЗаголовка = ГруппировкаРодитель.ПараметрыВывода.Элементы.Найти("Заголовок");
	
	ПараметрИспользованияЗаголовка.Использование = Истина;
	ПараметрИспользованияЗаголовка.Значение = ТипВыводаТекстаКомпоновкиДанных.Выводить;
	ПараметрЗаголовка.Использование = Истина;
	ПараметрЗаголовка.Значение = Наименование;

КонецПроцедуры

#Область СтруктураКорСубконто

Функция НовыйТипыКорСубконто()
	
	ТаблицаТиповСубконто = Новый ТаблицаЗначений;
	ТаблицаТиповСубконто.Колонки.Добавить("Счет");
	ТаблицаТиповСубконто.Колонки.Добавить("КорСчет");
	ТаблицаТиповСубконто.Колонки.Добавить("Наименование");
	ТаблицаТиповСубконто.Колонки.Добавить("ВидСубконто1");
	ТаблицаТиповСубконто.Колонки.Добавить("ВидСубконто2");
	ТаблицаТиповСубконто.Колонки.Добавить("ВидСубконто3");
	ТаблицаТиповСубконто.Колонки.Добавить("Подразделение");
	ТаблицаТиповСубконто.Колонки.Добавить("Группировка");
	ТаблицаТиповСубконто.Колонки.Добавить("КоличествоКорСубконто");
	
	Возврат ТаблицаТиповСубконто;
	
КонецФункции

Процедура УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета)
	
	Запрос.УстановитьПараметр("НачалоПериода"      , ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода"       , КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("Организация"        , ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустоеЗначение", ПустоеЗначение());

	Подразделение = Неопределено;
	Для Каждого ЭлементОтбора Из ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") И ЭлементОтбора.Использование Тогда
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			Подразделение = ЭлементОтбора.ПравоеЗначение;
		КонецЕсли;
	КонецЦикла;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);

	Если АналитикаЗаполнена(ПараметрыОтчета) Тогда
		Запрос.УстановитьПараметр("Аналитика", ПараметрыОтчета.Аналитика);
	Иначе
		Запрос.УстановитьПараметр("Аналитика", Неопределено);
	КонецЕсли;
		
КонецПроцедуры

Функция СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы()
	
	СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы = Новый Массив;
	
	СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы.Добавить(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы.Добавить(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РеализуемыеАктивы);
	
	Возврат СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы;
	
КонецФункции

Функция СчетПрочиеДоходы()
	
	СчетПрочиеДоходы = Новый Массив;
	СчетПрочиеДоходы.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	СчетПрочиеДоходы = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетПрочиеДоходы);
	
	Возврат СчетПрочиеДоходы;
	
КонецФункции

Функция СчетаПрочиеПрибылиИУбытки()
	
	СчетаПрочиеПрибылиИУбытки = Новый Массив;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочиеПрибылиИУбытки,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеПрибылиИУбытки)); // 99.09
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПрочиеПрибылиИУбытки, 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиБезНалогаНаПрибыль)); // 99.01
	
	Возврат СчетаПрочиеПрибылиИУбытки;
	
КонецФункции

Функция ИсключаемыеСчетаНалоговНаПрибыль()
	
	ИсключаемыеСчетаНалоговНаПрибыль = Новый Массив;
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.НалогПриПСН);
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.НалогПриАУСН);
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.НалогНаПрофессиональныйДоход);
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.НалогНаПрибыль);
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.ЕНприУСН);
	
	Возврат ИсключаемыеСчетаНалоговНаПрибыль;
	
КонецФункции

Функция СчетаКорреспонденты()

	СчетаКорреспонденты = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаКорреспонденты, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль)); // 84
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаКорреспонденты, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрибылиИУбытки)); // 99
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаКорреспонденты, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Продажи)); // 90
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаКорреспонденты, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы)); //91
	
	Возврат СчетаКорреспонденты;
	
КонецФункции

Функция АналитикаПроцентыКПолучению()
	
	АналитикаПроцентыКПолучению = Новый Массив;
	АналитикаПроцентыКПолучению.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКПолучениюУплате);
	АналитикаПроцентыКПолучению.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКПолучению);
	АналитикаПроцентыКПолучению.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыПоГосударственнымЦеннымБумагам);
	АналитикаПроцентыКПолучению.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыПоГосударственнымЦеннымБумагамПоСтавке0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПрочиеДоходыИРасходы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПрочиеДоходыИРасходы КАК ПрочиеДоходыИРасходы
	|ГДЕ
	|	ПрочиеДоходыИРасходы.ВидПрочихДоходовИРасходов В(&АналитикаПроцентыКПолучению)";
	Запрос.УстановитьПараметр("АналитикаПроцентыКПолучению", АналитикаПроцентыКПолучению);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Результат = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция АналитикаДоходыОтУчастияВДругихОрганизациях()
		
	АналитикаДоходыОтУчастияВДругихОрганизациях = Новый Массив;
	
	АналитикаДоходыОтУчастияВДругихОрганизациях.Добавить(
		Перечисления.ВидыПрочихДоходовИРасходов.УчастиеВДругихОрганизациях);
	АналитикаДоходыОтУчастияВДругихОрганизациях.Добавить(
		Перечисления.ВидыПрочихДоходовИРасходов.ДолевоеУчастиеВРоссийскихОрганизациях);
	АналитикаДоходыОтУчастияВДругихОрганизациях.Добавить(
		Перечисления.ВидыПрочихДоходовИРасходов.ДолевоеУчастиеВИностранныхОрганизациях);
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПрочиеДоходыИРасходы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПрочиеДоходыИРасходы КАК ПрочиеДоходыИРасходы
	|ГДЕ
	|	ПрочиеДоходыИРасходы.ВидПрочихДоходовИРасходов В(&АналитикаДоходыОтУчастияВДругихОрганизациях)";
	Запрос.УстановитьПараметр("АналитикаДоходыОтУчастияВДругихОрганизациях", АналитикаДоходыОтУчастияВДругихОрганизациях);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Результат = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция КорСубконтоКоммерческиеРасходы(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ХозрасчетныйОстатки.Счет КАК Счет,
	               |	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	               |	ХозрасчетныйОстатки.Организация КАК Организация,
	               |	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	               |ПОМЕСТИТЬ ОстаткиНаНачалоПериода
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&Счет), , ИСТИНА) КАК ХозрасчетныйОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет КАК Счет,
	               |	ХозрасчетныйОбороты.КорСчет КАК КорСчет,
	               |	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОбороты.Организация КАК Организация,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборот) КАК СуммаОборот
	               |ПОМЕСТИТЬ КоммерческиеРасходы
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			Счет В (&Счет),
	               |			&СубконтоСтатьиЗатрат,
	               |			ВЫБОР
	               |				КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА Субконто1 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто1 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто2 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто3 = &Аналитика
	               |				ИНАЧЕ ИСТИНА
	               |			КОНЕЦ,
	               |			НЕ КорСчет В (&СчетаРасходыНаРеализацию),
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Организация,
	               |	ХозрасчетныйОбороты.КорСчет,
	               |	ХозрасчетныйОбороты.Счет
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.КорСчет,
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Организация,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборот)
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			,
	               |			,
	               |			ВЫБОР
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто1 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА Субконто1 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(Субконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА Субконто2 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(Субконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА Субконто3 = &Аналитика
	               |				ИНАЧЕ ИСТИНА
	               |			КОНЕЦ,
	               |			КорСчет В (&Счет),
	               |			&СубконтоСтатьиЗатрат) КАК ХозрасчетныйОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиНаНачалоПериода КАК ОстаткиНаНачалоПериода
	               |		ПО ХозрасчетныйОбороты.КорСчет = ОстаткиНаНачалоПериода.Счет
	               |			И ХозрасчетныйОбороты.КорСубконто1 = ОстаткиНаНачалоПериода.Субконто1
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Организация,
	               |	ХозрасчетныйОбороты.КорСчет,
	               |	ХозрасчетныйОбороты.Счет
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КоммерческиеРасходы.Счет КАК Счет,
	               |	КоммерческиеРасходы.КорСчет КАК КорСчет,
	               |	ВЫБОР
	               |		КОГДА КоммерческиеРасходы.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА КоммерческиеРасходы.КорСчет.Наименование
	               |		ИНАЧЕ КоммерческиеРасходы.КорСчет.Родитель.Наименование
	               |	КОНЕЦ КАК Наименование,
	               |	КоммерческиеРасходы.Подразделение КАК Подразделение,
	               |	КоммерческиеРасходы.Организация КАК Организация,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	               |	ВЫБОР
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 3
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 2
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК КоличествоКорСубконто
	               |ИЗ
	               |	КоммерческиеРасходы КАК КоммерческиеРасходы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	               |		ПО КоммерческиеРасходы.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	               |			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	               |		ПО КоммерческиеРасходы.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	               |			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	               |		ПО КоммерческиеРасходы.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	               |			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	               |ГДЕ
	               |	КоммерческиеРасходы.СуммаОборот <> 0
	               |ИТОГИ
	               |	МАКСИМУМ(КоличествоКорСубконто)
	               |ПО
	               |	ВидСубконто1,
	               |	ВидСубконто2,
	               |	ВидСубконто3,
	               |	Наименование";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета);

	Запрос.УстановитьПараметр("СубконтоСтатьиЗатрат", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("СчетаРасходыНаРеализацию", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажу)); 
	Запрос.УстановитьПараметр("Счет", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасходыНаПродажу));
		
	Возврат Запрос;
	
КонецФункции

Функция КорСубконтоУправленческиеРасходы(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ХозрасчетныйОстатки.Счет КАК Счет,
	               |	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	               |	ХозрасчетныйОстатки.Организация КАК Организация,
	               |	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	               |ПОМЕСТИТЬ ОстаткиНаНачалоПериода
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетУправленческиеРасходы), , ИСТИНА) КАК ХозрасчетныйОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет КАК Счет,
	               |	ХозрасчетныйОбороты.КорСчет КАК КорСчет,
	               |	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОбороты.Организация КАК Организация
	               |ПОМЕСТИТЬ УправленческиеРасходы
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			Счет В (&СчетУправленческиеРасходы),
	               |			&СубконтоСтатьиЗатрат,
	               |			ВЫБОР
	               |				КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА Субконто1 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто1 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто2 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто3 = &Аналитика
	               |				ИНАЧЕ ИСТИНА
	               |			КОНЕЦ,
	               |			НЕ КорСчет В (&ИсключаемыеСчетаУправленческиеРасходы),
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.КорСчет,
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Организация
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			,
	               |			&СубконтоСтатьиЗатрат,
	               |			ВЫБОР
	               |				КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА Субконто1 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто1 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто2 = &Аналитика
	               |				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |					ТОГДА КорСубконто3 = &Аналитика
	               |				ИНАЧЕ ИСТИНА
	               |			КОНЕЦ,
	               |			КорСчет В (&СчетУправленческиеРасходы),
	               |			) КАК ХозрасчетныйОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиНаНачалоПериода КАК ОстаткиНаНачалоПериода
	               |		ПО ХозрасчетныйОбороты.КорСчет = ОстаткиНаНачалоПериода.Счет
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	УправленческиеРасходы.Счет КАК Счет,
	               |	УправленческиеРасходы.КорСчет КАК КорСчет,
	               |	ВЫБОР
	               |		КОГДА УправленческиеРасходы.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА УправленческиеРасходы.КорСчет.Наименование
	               |		ИНАЧЕ УправленческиеРасходы.КорСчет.Родитель.Наименование
	               |	КОНЕЦ КАК Наименование,
	               |	УправленческиеРасходы.Подразделение КАК Подразделение,
	               |	УправленческиеРасходы.Организация КАК Организация,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	               |	ВЫБОР
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 3
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 2
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК КоличествоКорСубконто
	               |ИЗ
	               |	УправленческиеРасходы КАК УправленческиеРасходы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	               |		ПО УправленческиеРасходы.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	               |			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	               |		ПО УправленческиеРасходы.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	               |			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	               |		ПО УправленческиеРасходы.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	               |			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	               |ИТОГИ
	               |	МАКСИМУМ(КоличествоКорСубконто)
	               |ПО
	               |	ВидСубконто1,
	               |	ВидСубконто2,
	               |	ВидСубконто3,
	               |	Наименование";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета);

	Запрос.УстановитьПараметр("СубконтоСтатьиЗатрат", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ИсключаемыеСчетаУправленческиеРасходы", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходы));
	
	Запрос.УстановитьПараметр("СчетУправленческиеРасходы", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы));

	Возврат Запрос;
	
КонецФункции

Функция КорСубконтоПрочиеДоходы(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет КАК Счет,
	               |	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ КАК Аналитика,
	               |	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК Сумма,
	               |	ХозрасчетныйОбороты.Организация КАК Организация,
	               |	ХозрасчетныйОбороты.КорСчет КАК КорСчет
	               |ПОМЕСТИТЬ ПрочиеДоходы
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			Счет В ИЕРАРХИИ (&СчетПрочиеДоходы),
	               |			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
	               |			НЕ Субконто1 В (&ИсключаемаяАналитика)
	               |				И &УсловиеСубконто
	               |				И ВЫБОР
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто3 = &Аналитика
	               |					ИНАЧЕ ИСТИНА
	               |				КОНЕЦ,
	               |			,
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Организация,
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.КорСчет,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ,
	               |	ХозрасчетныйОбороты.Субконто2
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПрочиеДоходы.Счет КАК Счет,
	               |	ПрочиеДоходы.КорСчет КАК КорСчет,
	               |	ВЫБОР
	               |		КОГДА ПрочиеДоходы.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА ПрочиеДоходы.КорСчет.Наименование
	               |		ИНАЧЕ ПрочиеДоходы.КорСчет.Родитель.Наименование
	               |	КОНЕЦ КАК Наименование,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	               |	ПрочиеДоходы.Подразделение КАК Подразделение,
	               |	ВЫБОР
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 3
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 2
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК КоличествоКорСубконто
	               |ИЗ
	               |	ПрочиеДоходы КАК ПрочиеДоходы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	               |		ПО ПрочиеДоходы.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	               |			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	               |		ПО ПрочиеДоходы.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	               |			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	               |		ПО ПрочиеДоходы.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	               |			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	               |ГДЕ
	               |	ПрочиеДоходы.Сумма <> 0
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПрочиеДоходы.Счет,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто,
	               |	ВЫБОР
	               |		КОГДА ПрочиеДоходы.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА ПрочиеДоходы.КорСчет.Наименование
	               |		ИНАЧЕ ПрочиеДоходы.КорСчет.Родитель.Наименование
	               |	КОНЕЦ,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто,
	               |	ПрочиеДоходы.Подразделение,
	               |	ПрочиеДоходы.КорСчет
	               |ИТОГИ
	               |	МАКСИМУМ(КоличествоКорСубконто)
	               |ПО
	               |	ВидСубконто1,
	               |	ВидСубконто2,
	               |	ВидСубконто3,
	               |	Наименование";
	
	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета); 
	
	// Отбор по субконто
	Отбор = ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор;
	Запрос.УстановитьПараметр("УсловиеСубконто", Истина);
	УсловиеСубконто = ""; 
	КоличествоГруппировок = ПараметрыОтчета.Группировка.Итог("Использование");
	Если Отбор.Элементы.Количество() > 0 И КоличествоГруппировок > 0 Тогда
		
		УсловиеСубконто = УсловиеСубконтоПоОтбору(Запрос, Отбор);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(УсловиеСубконто) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСубконто", УсловиеСубконто);
	КонецЕсли;
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;

	ИсключаемаяАналитика = АналитикаДоходыОтУчастияВДругихОрганизациях();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключаемаяАналитика, АналитикаПроцентыКПолучению());
	
	Запрос.УстановитьПараметр("ИсключаемаяАналитика", ИсключаемаяАналитика);

	Запрос.УстановитьПараметр("СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы", СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы());

	Запрос.УстановитьПараметр("СчетПрочиеДоходы", СчетПрочиеДоходы());
	
	Возврат Запрос;
	
КонецФункции

Функция КорСубконтоПрочиеРасходы(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ХозрасчетныйОстатки.Счет КАК Счет,
	               |	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	               |	ХозрасчетныйОстатки.Организация КАК Организация,
	               |	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	               |ПОМЕСТИТЬ ОстаткиНаНачалоПериода
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетПрочиеРасходы), , ИСТИНА) КАК ХозрасчетныйОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет КАК Счет,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ КАК Аналитика,
	               |	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
	               |	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборотДт) КАК Сумма,
	               |	ХозрасчетныйОбороты.Организация КАК Организация,
	               |	ХозрасчетныйОбороты.КорСчет КАК КорСчет
	               |ПОМЕСТИТЬ ПрочиеРасходы
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			Счет В ИЕРАРХИИ (&СчетПрочиеРасходы),
	               |			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
	               |			НЕ Субконто1 В (&ИсключаемаяАналитика)
	               |				И &УсловиеСубконто
	               |				И ВЫБОР
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто3 = &Аналитика
	               |					ИНАЧЕ ИСТИНА
	               |				КОНЕЦ,
	               |			КорСчет <> &СчетНДС,
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.Организация,
	               |	ХозрасчетныйОбороты.Субконто2,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ,
	               |	ХозрасчетныйОбороты.КорСчет
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ,
	               |	ХозрасчетныйОбороты.Субконто2,
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборотДт),
	               |	ХозрасчетныйОбороты.Организация,
	               |	ХозрасчетныйОбороты.КорСчет
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			Счет В ИЕРАРХИИ (&СчетПрочиеРасходы),
	               |			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
	               |			НЕ Субконто1 В (&ИсключаемаяАналитика)
	               |				И &УсловиеСубконто
	               |				И ВЫБОР
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто3 = &Аналитика
	               |					ИНАЧЕ ИСТИНА
	               |				КОНЕЦ,
	               |			КорСчет <> &СчетНДС,
	               |			) КАК ХозрасчетныйОбороты
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиНаНачалоПериода КАК ОстаткиНаНачалоПериода
	               |		ПО ХозрасчетныйОбороты.КорСчет = ОстаткиНаНачалоПериода.Счет
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.Организация,
	               |	ХозрасчетныйОбороты.Субконто2,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ,
	               |	ХозрасчетныйОбороты.КорСчет
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПрочиеРасходы.Счет КАК Счет,
	               |	ПрочиеРасходы.КорСчет КАК КорСчет,
	               |	ВЫБОР
	               |		КОГДА ПрочиеРасходы.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА ПрочиеРасходы.КорСчет.Наименование
	               |		ИНАЧЕ ПрочиеРасходы.КорСчет.Родитель.Наименование
	               |	КОНЕЦ КАК Наименование,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	               |	ПрочиеРасходы.Подразделение КАК Подразделение,
	               |	ВЫБОР
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 3
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 2
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК КоличествоКорСубконто
	               |ИЗ
	               |	ПрочиеРасходы КАК ПрочиеРасходы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	               |		ПО ПрочиеРасходы.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	               |			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	               |		ПО ПрочиеРасходы.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	               |			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	               |		ПО ПрочиеРасходы.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	               |			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто,
	               |	ПрочиеРасходы.Счет,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто,
	               |	ВЫБОР
	               |		КОГДА ПрочиеРасходы.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА ПрочиеРасходы.КорСчет.Наименование
	               |		ИНАЧЕ ПрочиеРасходы.КорСчет.Родитель.Наименование
	               |	КОНЕЦ,
	               |	ПрочиеРасходы.Подразделение,
	               |	ПрочиеРасходы.КорСчет
	               |ИТОГИ
	               |	МАКСИМУМ(КоличествоКорСубконто)
	               |ПО
	               |	ВидСубконто1,
	               |	ВидСубконто2,
	               |	ВидСубконто3,
	               |	Наименование";
	
	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета); 
	
	// Отбор по субконто
	Отбор = ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор;
	Запрос.УстановитьПараметр("УсловиеСубконто", Истина);
	УсловиеСубконто = "";
	
	КоличествоГруппировок = ПараметрыОтчета.Группировка.Итог("Использование");
	Если Отбор.Элементы.Количество() > 0 И КоличествоГруппировок > 0 Тогда
		
		УсловиеСубконто = УсловиеСубконтоПоОтбору(Запрос, Отбор);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(УсловиеСубконто) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСубконто", УсловиеСубконто);
	КонецЕсли;
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("ИсключаемаяАналитика", АналитикаПроцентыКУплате());

	Запрос.УстановитьПараметр("СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы", СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы());
	
	СчетПрочиеРасходы = Новый Массив;
	СчетПрочиеРасходы.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасходы);
	СчетПрочиеРасходы = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетПрочиеРасходы);

	Запрос.УстановитьПараметр("СчетПрочиеРасходы", СчетПрочиеРасходы);
	Запрос.УстановитьПараметр("СчетНДС", ПланыСчетов.Хозрасчетный.НДС);

	Возврат Запрос;
	
КонецФункции

Функция УсловиеСубконтоПоОтбору(Запрос, Отбор)
	
	УсловиеСубконто = "";
	
	Для Каждого ЭлементОтбора Из Отбор.Элементы Цикл
		
		Если ЭлементОтбора.Использование = Ложь Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрНайти(ЭлементОтбора.ЛевоеЗначение, "Субконто") > 0 Тогда
					
				ВидСравненияСубконто = "=";
				Отрицание = "";
				ВСписке = Ложь;
				Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно Тогда
					ВидСравненияСубконто = ">=";
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
					ВидСравненияСубконто = "<="; 
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше Тогда
					ВидСравненияСубконто = ">";
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше Тогда
					ВидСравненияСубконто = "<";
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит Тогда
					ВидСравненияСубконто = "Подобно";
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит Тогда
					ВидСравненияСубконто = "Подобно";
					Отрицание = "НЕ";
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно Тогда
					ВидСравненияСубконто = "<>";
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
					ВидСравненияСубконто = "В";
					ВСписке = Истина; 
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке Тогда
					ВидСравненияСубконто = "В";
					ВСписке = Истина;
					Отрицание = "НЕ";
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии Тогда
					ВидСравненияСубконто = "В ИЕРАРХИИ";
					ВСписке = Истина;
				ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии
					Или ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
					ВидСравненияСубконто = "В ИЕРАРХИИ";
					ВСписке = Истина;
					Отрицание = "НЕ";
				КонецЕсли;
				
				Если УсловиеСубконто = "" Тогда
					Если ВСписке Тогда
						УсловиеСубконто = СтрШаблон(" %1 %2 %3 (&%2)", Отрицание, ЭлементОтбора.ЛевоеЗначение, ВидСравненияСубконто);
					Иначе
						УсловиеСубконто = СтрШаблон(" %1 %2 %3 &%2", Отрицание, ЭлементОтбора.ЛевоеЗначение, ВидСравненияСубконто);
					КонецЕсли;
				Иначе
					
					Если ВСписке Тогда
						УсловиеСубконто = СтрШаблон("
						| И %1 %2 %3 (&%2)", Отрицание, ЭлементОтбора.ЛевоеЗначение, ВидСравненияСубконто);
					Иначе
						УсловиеСубконто = СтрШаблон("
						| И  %1 %2 %3 &%2", Отрицание, ЭлементОтбора.ЛевоеЗначение, ВидСравненияСубконто);
					КонецЕсли;
				КонецЕсли;

				Запрос.УстановитьПараметр(СтрШаблон("%1", ЭлементОтбора.ЛевоеЗначение), ЭлементОтбора.ПравоеЗначение);
				
			КонецЕсли;
		КонецЦикла;
		
		Возврат УсловиеСубконто;
	
КонецФункции

Функция КорСубконтоДоходыОтУчастияВДругихОрганизациях(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет КАК Счет,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ КАК Аналитика,
	               |	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
	               |	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОбороты.Организация КАК Организация,
	               |	ХозрасчетныйОбороты.КорСчет КАК КорСчет,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборот) КАК СуммаОборот
	               |ПОМЕСТИТЬ ДоходыОтУчастияВДругихОрганизациях
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			Счет В ИЕРАРХИИ (&СчетПрочиеДоходы),
	               |			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
	               |			Субконто1 В (&АналитикаДоходыОтУчастияВДругихОрганизациях)
	               |				И ВЫБОР
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто3 = &Аналитика
	               |					ИНАЧЕ ИСТИНА
	               |				КОНЕЦ,
	               |			КорСчет В (&РасчетыПоПричитающимсяДивидендам),
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Организация,
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.Субконто2,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ,
	               |	ХозрасчетныйОбороты.КорСчет
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДоходыОтУчастияВДругихОрганизациях.Счет КАК Счет,
	               |	ДоходыОтУчастияВДругихОрганизациях.КорСчет КАК КорСчет,
	               |	ВЫБОР
	               |		КОГДА ДоходыОтУчастияВДругихОрганизациях.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА ДоходыОтУчастияВДругихОрганизациях.КорСчет.Наименование
	               |		ИНАЧЕ ДоходыОтУчастияВДругихОрганизациях.КорСчет.Родитель.Наименование
	               |	КОНЕЦ КАК Наименование,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	               |	ДоходыОтУчастияВДругихОрганизациях.Подразделение КАК Подразделение,
	               |	ВЫБОР
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 3
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 2
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК КоличествоКорСубконто
	               |ИЗ
	               |	ДоходыОтУчастияВДругихОрганизациях КАК ДоходыОтУчастияВДругихОрганизациях
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	               |		ПО ДоходыОтУчастияВДругихОрганизациях.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	               |			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	               |		ПО ДоходыОтУчастияВДругихОрганизациях.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	               |			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	               |		ПО ДоходыОтУчастияВДругихОрганизациях.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	               |			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйВидыСубконто.ВидСубконто,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто,
	               |	ВЫБОР
	               |		КОГДА ДоходыОтУчастияВДругихОрганизациях.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА ДоходыОтУчастияВДругихОрганизациях.КорСчет.Наименование
	               |		ИНАЧЕ ДоходыОтУчастияВДругихОрганизациях.КорСчет.Родитель.Наименование
	               |	КОНЕЦ,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто,
	               |	ДоходыОтУчастияВДругихОрганизациях.Счет,
	               |	ДоходыОтУчастияВДругихОрганизациях.Подразделение,
	               |	ДоходыОтУчастияВДругихОрганизациях.КорСчет
	               |ИТОГИ
	               |	МАКСИМУМ(КоличествоКорСубконто)
	               |ПО
	               |	ВидСубконто1,
	               |	ВидСубконто2,
	               |	ВидСубконто3,
	               |	Наименование";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;

	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета); 
	
	Запрос.УстановитьПараметр("АналитикаДоходыОтУчастияВДругихОрганизациях", 
		АналитикаДоходыОтУчастияВДругихОрганизациях());
		
	Запрос.УстановитьПараметр("СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы", 
		СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы());
		
	Запрос.УстановитьПараметр("СчетПрочиеДоходы", СчетПрочиеДоходы());
	
	Запрос.УстановитьПараметр("РасчетыПоПричитающимсяДивидендам", 
		ПланыСчетов.Хозрасчетный.РасчетыПоПричитающимсяДивидендам);
	
	Возврат Запрос;
	
КонецФункции

Функция КорСубконтоДоходыПоКредитам(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет КАК Счет,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ КАК Аналитика,
	               |	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК Сумма,
	               |	ХозрасчетныйОбороты.КорСчет КАК КорСчет,
	               |	ХозрасчетныйОбороты.Подразделение КАК Подразделение
	               |ПОМЕСТИТЬ ДоходыПоКредитам
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			Счет В ИЕРАРХИИ (&СчетПрочиеДоходы),
	               |			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
	               |			Субконто1 В (&АналитикаПроцентыКПолучению)
	               |				И ВЫБОР
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто3 = &Аналитика
	               |					ИНАЧЕ ИСТИНА
	               |				КОНЕЦ,
	               |			,
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.Субконто2,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ,
	               |	ХозрасчетныйОбороты.КорСчет,
	               |	ХозрасчетныйОбороты.Подразделение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДоходыПоКредитам.Счет КАК Счет,
	               |	ДоходыПоКредитам.КорСчет КАК КорСчет,
	               |	ВЫБОР
	               |		КОГДА ДоходыПоКредитам.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА ДоходыПоКредитам.КорСчет.Наименование
	               |		ИНАЧЕ ДоходыПоКредитам.КорСчет.Родитель.Наименование
	               |	КОНЕЦ КАК Наименование,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	               |	ДоходыПоКредитам.Подразделение КАК Подразделение,
	               |	ВЫБОР
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 3
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 2
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК КоличествоКорСубконто
	               |ИЗ
	               |	ДоходыПоКредитам КАК ДоходыПоКредитам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	               |		ПО ДоходыПоКредитам.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	               |			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	               |		ПО ДоходыПоКредитам.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	               |			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	               |		ПО ДоходыПоКредитам.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	               |			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	               |ИТОГИ
	               |	МАКСИМУМ(КоличествоКорСубконто)
	               |ПО
	               |	ВидСубконто1,
	               |	ВидСубконто2,
	               |	ВидСубконто3,
	               |	Наименование";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;

	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета);
	
	Запрос.УстановитьПараметр("АналитикаПроцентыКПолучению", АналитикаПроцентыКПолучению());

	Запрос.УстановитьПараметр("СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы", СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы());

	Запрос.УстановитьПараметр("СчетПрочиеДоходы", СчетПрочиеДоходы());
	
	Возврат Запрос;
	
КонецФункции

Функция КорСубконтоРасходыПоКредитам(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет КАК Счет,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ КАК Аналитика,
	               |	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
	               |	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
	               |	СУММА(ХозрасчетныйОбороты.СуммаОборотДт) КАК Сумма,
	               |	ХозрасчетныйОбороты.Организация КАК Организация,
	               |	ХозрасчетныйОбороты.КорСчет КАК КорСчет
	               |ПОМЕСТИТЬ РасходыПоКредитам
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоПериода,
	               |			&КонецПериода,
	               |			Авто,
	               |			Счет В ИЕРАРХИИ (&СчетПрочиеРасходы),
	               |			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
	               |			Субконто1 В (&АналитикаПроцентыКУплате)
	               |				И ВЫБОР
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(Субконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА Субконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто1 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто2 = &Аналитика
	               |					КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	               |						ТОГДА КорСубконто3 = &Аналитика
	               |					ИНАЧЕ ИСТИНА
	               |				КОНЕЦ,
	               |			,
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.Организация,
	               |	ХозрасчетныйОбороты.Субконто2,
	               |	ВЫБОР
	               |		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
	               |			ТОГДА &ПустоеЗначение
	               |		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
	               |	КОНЕЦ,
	               |	ХозрасчетныйОбороты.КорСчет
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РасходыПоКредитам.Счет КАК Счет,
	               |	РасходыПоКредитам.КорСчет КАК КорСчет,
	               |	ВЫБОР
	               |		КОГДА РасходыПоКредитам.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА РасходыПоКредитам.КорСчет.Наименование
	               |		ИНАЧЕ РасходыПоКредитам.КорСчет.Родитель.Наименование
	               |	КОНЕЦ КАК Наименование,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	               |	ВЫБОР
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 3
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 2
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК КоличествоКорСубконто
	               |ИЗ
	               |	РасходыПоКредитам КАК РасходыПоКредитам
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	               |		ПО РасходыПоКредитам.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	               |			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	               |		ПО РасходыПоКредитам.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	               |			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	               |		ПО РасходыПоКредитам.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	               |			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйВидыСубконто.ВидСубконто,
	               |	РасходыПоКредитам.Счет,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто,
	               |	ВЫБОР
	               |		КОГДА РасходыПоКредитам.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА РасходыПоКредитам.КорСчет.Наименование
	               |		ИНАЧЕ РасходыПоКредитам.КорСчет.Родитель.Наименование
	               |	КОНЕЦ,
	               |	РасходыПоКредитам.КорСчет
	               |ИТОГИ
	               |	МАКСИМУМ(КоличествоКорСубконто)
	               |ПО
	               |	ВидСубконто1,
	               |	ВидСубконто2,
	               |	ВидСубконто3,
	               |	Наименование";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета);
	
	Запрос.УстановитьПараметр("АналитикаПроцентыКУплате", АналитикаПроцентыКУплате());

	Запрос.УстановитьПараметр("СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы", СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы());
	
	СчетПрочиеРасходы = Новый Массив;
	СчетПрочиеРасходы.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасходы);
	СчетПрочиеРасходы = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетПрочиеРасходы);

	Запрос.УстановитьПараметр("СчетПрочиеРасходы", СчетПрочиеРасходы);
	Запрос.УстановитьПараметр("СчетНДС", ПланыСчетов.Хозрасчетный.НДС);

	Возврат Запрос;
	
КонецФункции

Функция КорСубконтоНалогНаПрибыль(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц КАК Период,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение) КАК Подразделение,
	|	ОборотыПоНалогуНаПрибыль.Валюта КАК Валюта,
	|	СУММА(ОборотыПоНалогуНаПрибыль.СуммаОборот) КАК Сумма,
	|	ОборотыПоНалогуНаПрибыль.Организация КАК Организация,
	|	ОборотыПоНалогуНаПрибыль.Счет КАК Счет,
	|	ОборотыПоНалогуНаПрибыль.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ НалогНаПрибыль
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетРасчетНалогНаПрибыль), , &ЭтоНалогНаПрибыль, КорСчет В ИЕРАРХИИ (&СчетНалогНаПрибыль), ) КАК ОборотыПоНалогуНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Счет,
	|	ОборотыПоНалогуНаПрибыль.КорСчет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.ПодразделениеДт, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.ВалютаДт,
	|	СУММА(ОборотыПоНалогуНаПрибыль.СуммаОборот),
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.СчетДт,
	|	ОборотыПоНалогуНаПрибыль.СчетКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В ИЕРАРХИИ (&СчетНалогНаПрибыль), , СчетКт В (&СчетНераспределеннаяПрибыльУбытки), , &ЭтоНалогНаПрибыль) КАК ОборотыПоНалогуНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.ВалютаДт,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.ПодразделениеДт, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.СчетДт,
	|	ОборотыПоНалогуНаПрибыль.СчетКт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОборотыПоОтложеннымНалоговымОбязательствам.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоОтложеннымНалоговымОбязательствам.ПодразделениеДт, &ПустоеПодразделение),
	|	ОборотыПоОтложеннымНалоговымОбязательствам.ВалютаДт,
	|	СУММА(ОборотыПоОтложеннымНалоговымОбязательствам.СуммаОборот),
	|	ОборотыПоОтложеннымНалоговымОбязательствам.Организация,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.СчетДт,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.СчетКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетНераспределеннаяПрибыльУбытки), , СчетКт В ИЕРАРХИИ (&СчетНалогНаПрибыль), , &ЭтоНалогНаПрибыль) КАК ОборотыПоОтложеннымНалоговымОбязательствам
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоОтложеннымНалоговымОбязательствам.ПериодМесяц,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.Организация,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.ВалютаДт,
	|	ЕСТЬNULL(ОборотыПоОтложеннымНалоговымОбязательствам.ПодразделениеДт, &ПустоеПодразделение),
	|	ОборотыПоОтложеннымНалоговымОбязательствам.СчетДт,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.СчетКт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.ПериодМесяц,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.Подразделение, &ПустоеПодразделение),
	|	NULL,
	|	-ХозрасчетныйОбороты.СуммаОборотДт,
	|	ХозрасчетныйОбороты.Организация,
	|	ХозрасчетныйОбороты.Счет,
	|	ХозрасчетныйОбороты.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Авто,
	|			Счет В (&СчетПрибылиИУбыткиНеЕНВД),
	|			,
	|			ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(Субконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	|					ТОГДА Субконто1 = &Аналитика
	|				КОГДА ТИПЗНАЧЕНИЯ(Субконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	|					ТОГДА Субконто2 = &Аналитика
	|				КОГДА ТИПЗНАЧЕНИЯ(Субконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	|					ТОГДА Субконто3 = &Аналитика
	|				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто1) = ТИПЗНАЧЕНИЯ(&Аналитика)
	|					ТОГДА КорСубконто1 = &Аналитика
	|				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто2) = ТИПЗНАЧЕНИЯ(&Аналитика)
	|					ТОГДА КорСубконто2 = &Аналитика
	|				КОГДА ТИПЗНАЧЕНИЯ(КорСубконто3) = ТИПЗНАЧЕНИЯ(&Аналитика)
	|					ТОГДА КорСубконто3 = &Аналитика
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ,
	|			КорСчет В ИЕРАРХИИ (&СчетРасчетыСБюджетом),
	|			) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	СУММА(ОборотыПоНалогуНаПрибыль.СуммаОборот),
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.Счет,
	|	ОборотыПоНалогуНаПрибыль.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В ИЕРАРХИИ (&СчетНалогНаПрибыльТекущийИОтложенный), , &ЭтоНалогНаПрибыль, , ) КАК ОборотыПоНалогуНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Счет,
	|	ОборотыПоНалогуНаПрибыль.КорСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалогНаПрибыль.Счет КАК Счет,
	|	НалогНаПрибыль.КорСчет КАК КорСчет,
	|	ВЫБОР
	|		КОГДА НалогНаПрибыль.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА НалогНаПрибыль.КорСчет.Наименование
	|		ИНАЧЕ НалогНаПрибыль.КорСчет.Родитель.Наименование
	|	КОНЕЦ КАК Наименование,
	|	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	|	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	|	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	|	ВЫБОР
	|		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	|			ТОГДА 3
	|		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	|			ТОГДА 2
	|		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоКорСубконто,
	|	НалогНаПрибыль.Организация КАК Организация
	|ИЗ
	|	НалогНаПрибыль КАК НалогНаПрибыль
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|		ПО НалогНаПрибыль.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	|			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	|		ПО НалогНаПрибыль.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	|			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	|		ПО НалогНаПрибыль.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	|			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйВидыСубконто.ВидСубконто,
	|	НалогНаПрибыль.Счет,
	|	ХозрасчетныйВидыСубконто1.ВидСубконто,
	|	ХозрасчетныйВидыСубконто2.ВидСубконто,
	|	ВЫБОР
	|		КОГДА НалогНаПрибыль.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА НалогНаПрибыль.КорСчет.Наименование
	|		ИНАЧЕ НалогНаПрибыль.КорСчет.Родитель.Наименование
	|	КОНЕЦ,
	|	НалогНаПрибыль.КорСчет,
	|	НалогНаПрибыль.Организация
	|ИТОГИ
	|	МАКСИМУМ(КоличествоКорСубконто)
	|ПО
	|	ВидСубконто1,
	|	ВидСубконто2,
	|	ВидСубконто3,
	|	Наименование";
		
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета);
	
	СчетРасчетыСБюджетом = Новый Массив;
	ЭтоНалогНаПрибыль = ПараметрыОтчета.Показатель = ПоказательНалогНаПрибыль();
	Если ЭтоНалогНаПрибыль Тогда
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетРасчетыСБюджетом,
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСБюджетом));
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательНалогПриУСН() Тогда
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетРасчетыСБюджетом, 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЕНприУСН));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетРасчетыСБюджетом, 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НалогПриАУСН));
	ИначеЕсли ПараметрыОтчета.Показатель = ПоказательНалогНаПрофессиональныйДоход() Тогда
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетРасчетыСБюджетом, 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НалогНаПрофессиональныйДоход));
	КонецЕсли;
	
	СчетНалогНаПрибыльТекущийИОтложенный = Новый Массив;
	СчетНалогНаПрибыльТекущийИОтложенный.Добавить(ПланыСчетов.Хозрасчетный.ТекущийНалогНаПрибыль); // 99.02.Т
	СчетНалогНаПрибыльТекущийИОтложенный.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныйНалогНаПрибыль); // 99.02.О
	
	Запрос.УстановитьПараметр("СчетНалогНаПрибыльТекущийИОтложенный", СчетНалогНаПрибыльТекущийИОтложенный); 

	Запрос.УстановитьПараметр("СчетРасчетыСБюджетом", СчетРасчетыСБюджетом);
	
	Запрос.УстановитьПараметр("СчетПрибылиИУбыткиНеЕНВД",
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиНеЕНВД));
	Запрос.УстановитьПараметр("СчетРасчетНалогНаПрибыль", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетНалогаНаПрибыль));
	Запрос.УстановитьПараметр("СчетНалогНаПрибыль", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрибылиИУбытки_НалогНаПрибыль));
	Запрос.УстановитьПараметр("СчетНераспределеннаяПрибыльУбытки",
		ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль); // 84 
	Запрос.УстановитьПараметр("ЭтоНалогНаПрибыль", ЭтоНалогНаПрибыль); 
	
	Возврат Запрос;

КонецФункции

Функция КорСубконтоПрочее(ПараметрыОтчета, ПараметрыОтбора)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Прочее.Организация КАК Организация,
	               |	Прочее.Подразделение КАК Подразделение,
	               |	Прочее.Счет КАК Счет,
	               |	Прочее.КорСчет КАК КорСчет
	               |ПОМЕСТИТЬ Прочее
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период КАК Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация КАК Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение) КАК Подразделение,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор КАК Документ,
	               |		ХозрасчетныйДвиженияССубконто.Содержание КАК Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК Субконто1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК КорСубконто1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт КАК Счет,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт КАК КорСчет,
	               |		СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК Сумма
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетКт В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки)
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.Содержание
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеКт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		-ХозрасчетныйДвиженияССубконто.Сумма
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетДт В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки)
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеКт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.Сумма,
	               |		ХозрасчетныйДвиженияССубконто.Содержание
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		СУММА(ХозрасчетныйДвиженияССубконто.Сумма)
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетДт В (&СчетаПрочиеПрибылиИУбытки)
	               |					И СчетКт В ИЕРАРХИИ (&СчетаКорреспонденты)
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.Содержание
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		СУММА(-ХозрасчетныйДвиженияССубконто.Сумма)
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетДт В ИЕРАРХИИ (&СчетаКорреспонденты)
	               |					И СчетКт В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки)
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.Содержание
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.Содержание,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		СУММА(ХозрасчетныйДвиженияССубконто.Сумма)
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |				&НачалоПериода,
	               |				&КонецПериода,
	               |				СчетКт В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки)
	               |					И СчетДт В ИЕРАРХИИ (&РасчетыПоНалогам)
	               |					И СубконтоКт1 = &РасчетыСБюджетом
	               |					И Активность,
	               |				,
	               |				) КАК ХозрасчетныйДвиженияССубконто
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ХозрасчетныйДвиженияССубконто.Период,
	               |		ХозрасчетныйДвиженияССубконто.Организация,
	               |		ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.ПодразделениеДт, &ПустоеПодразделение),
	               |		ХозрасчетныйДвиженияССубконто.Регистратор,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	               |		ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	               |		ХозрасчетныйДвиженияССубконто.СчетКт,
	               |		ХозрасчетныйДвиженияССубконто.СчетДт,
	               |		ХозрасчетныйДвиженияССубконто.Содержание) КАК Прочее
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Прочее.Подразделение,
	               |	Прочее.Организация,
	               |	Прочее.Счет,
	               |	Прочее.КорСчет
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(Прочее.Сумма) <> 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Прочее.Счет КАК Счет,
	               |	Прочее.КорСчет КАК КорСчет,
	               |	ВЫБОР
	               |		КОГДА Прочее.КорСчет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	               |			ТОГДА Прочее.КорСчет.Наименование
	               |		ИНАЧЕ Прочее.КорСчет.Родитель.Наименование
	               |	КОНЕЦ КАК Наименование,
	               |	Прочее.Подразделение КАК Подразделение,
	               |	Прочее.Организация КАК Организация,
	               |	ХозрасчетныйВидыСубконто.ВидСубконто КАК ВидСубконто1,
	               |	ХозрасчетныйВидыСубконто1.ВидСубконто КАК ВидСубконто2,
	               |	ХозрасчетныйВидыСубконто2.ВидСубконто КАК ВидСубконто3,
	               |	ВЫБОР
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто2.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 3
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто1.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 2
	               |		КОГДА НЕ ХозрасчетныйВидыСубконто.ВидСубконто ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК КоличествоКорСубконто
	               |ИЗ
	               |	Прочее КАК Прочее
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	               |		ПО Прочее.КорСчет = ХозрасчетныйВидыСубконто.Ссылка
	               |			И (ХозрасчетныйВидыСубконто.НомерСтроки = 1)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто1
	               |		ПО Прочее.КорСчет = ХозрасчетныйВидыСубконто1.Ссылка
	               |			И (ХозрасчетныйВидыСубконто1.НомерСтроки = 2)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто2
	               |		ПО Прочее.КорСчет = ХозрасчетныйВидыСубконто2.Ссылка
	               |			И (ХозрасчетныйВидыСубконто2.НомерСтроки = 3)
	               |ИТОГИ
	               |	МАКСИМУМ(КоличествоКорСубконто)
	               |ПО
	               |	ВидСубконто1,
	               |	ВидСубконто2,
	               |	ВидСубконто3,
	               |	Наименование";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	
	УстановитьОбщиеПараметры(Запрос, ПараметрыОтчета);

	Запрос.УстановитьПараметр("ИсключаемыеСчетаНалоговНаПрибыль", ИсключаемыеСчетаНалоговНаПрибыль());
	Запрос.УстановитьПараметр("СчетаПрочиеПрибылиИУбытки", СчетаПрочиеПрибылиИУбытки());
	Запрос.УстановитьПараметр("СчетаКорреспонденты", СчетаКорреспонденты());
	Запрос.УстановитьПараметр("РасчетыПоНалогам", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам));
	Запрос.УстановитьПараметр("РасчетыСБюджетом", Перечисления.ВидыПлатежейВГосБюджет.Налог);

	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область УстановкаПараметров

Процедура УстановитьПараметрыПрочее(КомпоновщикНастроек)
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"СчетаПрочиеПрибылиИУбытки", СчетаПрочиеПрибылиИУбытки());
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ИсключаемыеСчетаНалоговНаПрибыль", ИсключаемыеСчетаНалоговНаПрибыль());
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "СчетаКорреспонденты", СчетаКорреспонденты());
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "РасчетыПоНалогам", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам));
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"РасчетыСБюджетом", Перечисления.ВидыПлатежейВГосБюджет.Налог);
	
КонецПроцедуры

#КонецОбласти

#Область НазванияПоказателей

// Возвращает истина, если переданный показатель является расчетным.
// 
// Параметры:
//   Показатель - Строка - имя показателя.
// 
// Возвращаемое значение:
//   Булево
//
Функция ЭтоРасчетныйПоказатель(Показатель) Экспорт
	
	Возврат Показатель = ПоказательВаловаяПрибыль()
		Или Показатель = ПоказательРентабельностьПродаж()
		Или Показатель = ПоказательЧистаяПрибыль()
		Или Показатель = ПоказательПрибыльУбытокОтПродаж()
		Или Показатель = ПоказательПрибыльУбытокДоНалогообложения();
	
КонецФункции

Функция ПоказательВыручка()
	
	Возврат НСтр("ru='Выручка'");
	
КонецФункции

Функция ПоказательСебестоимость()
	
	Возврат НСтр("ru='Себестоимость'");
	
КонецФункции

Функция ПоказательПрочее()
	
	Возврат НСтр("ru='Прочее'");
	
КонецФункции

Функция ПоказательНалогНаПрибыль()
	
	Возврат НСтр("ru='Налог на прибыль'");
	
КонецФункции

Функция ПоказательДоходыОтУчастияВДругихОрганизациях()
	
	Возврат НСтр("ru='Доходы от участия в других организациях'");
	
КонецФункции

Функция ПоказательПроцентыКПолучению()
	
	Возврат НСтр("ru='Проценты к получению'");
	
КонецФункции

Функция ПоказательПрочиеДоходы()
	
	Возврат НСтр("ru='Прочие доходы'");
	
КонецФункции

Функция ПоказательКоммерческиеРасходы()
	
	Возврат НСтр("ru='Коммерческие расходы'");
	
КонецФункции

Функция ПоказательУправленческиеРасходы()
	
	Возврат НСтр("ru='Управленческие расходы'");
	
КонецФункции

Функция ПоказательПроцентыКУплате()
	
	Возврат НСтр("ru='Проценты к уплате'");
	
КонецФункции

Функция ПоказательПрочиеРасходы()
	
	Возврат НСтр("ru='Прочие расходы'");
	
КонецФункции

Функция ПоказательНалогПриУСН()
	
	Возврат НСтр("ru='Налог при УСН'");
	
КонецФункции

Функция ПоказательНалогНаПрофессиональныйДоход()
	
	Возврат НСтр("ru='Налог на профессиональный доход'");
	
КонецФункции

Функция ПоказательВаловаяПрибыль()
	
	Возврат НСтр("ru='Валовая прибыль'");
	
КонецФункции

Функция ПоказательРентабельностьПродаж()
	
	Возврат НСтр("ru='Рентабельность продаж'");
	
КонецФункции

Функция ПоказательПрибыльУбытокОтПродаж()
	
	Возврат НСтр("ru='Прибыль (убыток) от продаж'");
	
КонецФункции

Функция ПоказательПрибыльУбытокДоНалогообложения()
	
	Возврат НСтр("ru='Прибыль (убыток) до налогообложения'");
	
КонецФункции

Функция ПоказательЧистаяПрибыль()
	
	Возврат НСтр("ru='Чистая прибыль'");
	
КонецФункции

Функция ВидСтроки()
	
	ВидСтроки = Новый Структура;
	ВидСтроки.Вставить("Доход",               1);
	ВидСтроки.Вставить("Расход",              2);
	ВидСтроки.Вставить("РасчетныйПоказатель", 3);
	ВидСтроки.Вставить("НалогНаПрибыль",      5);
	ВидСтроки.Вставить("Информация",          6);
	
	Возврат ВидСтроки;
	
КонецФункции

Функция ПоказателиОтчета()
	
	ВидСтроки = ВидСтроки();
	
	ПоказателиОтчета = Новый Соответствие;
	ПоказателиОтчета.Вставить("Выручка", ВидСтроки.Доход);
	ПоказателиОтчета.Вставить("Себестоимость", ВидСтроки.Расход);
	ПоказателиОтчета.Вставить("Валовая прибыль", ВидСтроки.РасчетныйПоказатель);
	ПоказателиОтчета.Вставить("Рентабельность продаж", ВидСтроки.РасчетныйПоказатель);
	ПоказателиОтчета.Вставить("Коммерческие расходы", ВидСтроки.Расход);
	ПоказателиОтчета.Вставить("Управленческие расходы", ВидСтроки.Расход);
	ПоказателиОтчета.Вставить("Прибыль (убыток) от продаж", ВидСтроки.РасчетныйПоказатель);
	ПоказателиОтчета.Вставить("Доходы от участия в других организациях", ВидСтроки.Доход);
	ПоказателиОтчета.Вставить("Проценты к получению", ВидСтроки.Доход);
	ПоказателиОтчета.Вставить("Проценты к уплате", ВидСтроки.Расход);
	ПоказателиОтчета.Вставить("Прочие доходы", ВидСтроки.Доход);
	ПоказателиОтчета.Вставить("Прочие расходы", ВидСтроки.Расход);
	ПоказателиОтчета.Вставить("Налог на прибыль", ВидСтроки.НалогНаПрибыль);
	ПоказателиОтчета.Вставить("Налог при УСН", ВидСтроки.НалогНаПрибыль);
	ПоказателиОтчета.Вставить("Налог на профессиональный доход", ВидСтроки.НалогНаПрибыль);
	ПоказателиОтчета.Вставить("Прочее", ВидСтроки.Расход);
	
	Возврат ПоказателиОтчета;
	
КонецФункции

Функция ПустоеЗначение()

	Возврат НСтр("ru='<...>'");

КонецФункции

Процедура УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	СхемаЗапроса = СхемыЗапросов.Создать(ТекстЗапроса);
	
	Для Каждого ОписаниеЗапроса Из СхемаЗапроса.ПакетЗапросов Цикл
		
		Контекст = Новый Структура;
		Контекст.Вставить("Схема",  СхемаЗапроса);
		Контекст.Вставить("Запрос", ОписаниеЗапроса);
		
		Если ТипЗнч(ОписаниеЗапроса) <> Тип("ЗапросВыбораСхемыЗапроса") Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяВременнойТаблицы =  ОписаниеЗапроса.ТаблицаДляПомещения;
		ИндексКолонки = -1;
		
		Для Каждого ОператорТаблицы Из ОписаниеЗапроса.Операторы Цикл
			
			Для Каждого ТаблицыИсточники Из ОператорТаблицы.Источники Цикл
				
				Если ТипЗнч(ТаблицыИсточники.Источник) <> Тип("ТаблицаСхемыЗапроса")
					Или СтрНайти(ТаблицыИсточники.Источник.ИмяТаблицы, "Хозрасчетный") = 0
					Или СтрНайти(ТаблицыИсточники.Источник.ИмяТаблицы, "Хозрасчетный.ВидыСубконто") <> 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Контекст.Вставить("Таблица", ТаблицыИсточники.Источник);
				
				Если ЗначениеЗаполнено(ПараметрыОтбора.Организация)
					И Не ПустаяСтрока(ПараметрыОтбора.ОрганизацияФормулаСравнения) Тогда
					
					Если ПараметрыОтбора.ВключатьОбособленныеПодразделения Тогда 
						Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация.ГоловнаяОрганизация", "&Организация");
					Иначе
						Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация", "&Организация");
					КонецЕсли;
					СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ПараметрыОтбора.Подразделение)
					И Не ПустаяСтрока(ПараметрыОтбора.ПодразделениеФормулаСравнения) Тогда 
					
					Если СтрНайти(ТаблицыИсточники.Источник.ИмяТаблицы, "ОборотыДтКт") > 1 Тогда 
						Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(ПодразделениеДт, &ПустоеПодразделение)", "&Подразделение");
					Иначе
						Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(Подразделение, &ПустоеПодразделение)", "&Подразделение");
					КонецЕсли;
					
					СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецПроцедуры

Функция ПараметрыОтбораПоОрганизации(ПараметрыОтчета);
	
	Подразделение        = Неопределено;
	Организация          = Неопределено;
	ОрганизацияФормулаСравнения = "";
	ПодразделениеФормулаСравнения = "";
	
	Для Каждого ЭлементОтбора Из ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор.Элементы Цикл
		
		Если Не ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
			
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.Организации.ПустаяСсылка();
			КонецЕсли;
			
			Организация = ЭлементОтбора.ПравоеЗначение;
			ОрганизацияФормулаСравнения = ОтчетыРуководителя.ФормулаСравнения(ЭлементОтбора.ВидСравнения);
			
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") Тогда
			
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			
			Подразделение = ЭлементОтбора.ПравоеЗначение;
			ПодразделениеФормулаСравнения = ОтчетыРуководителя.ФормулаСравнения(ЭлементОтбора.ВидСравнения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Организация = Неопределено И ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда
		Организация = ПараметрыОтчета.Организация;
		ОрганизацияФормулаСравнения = ОтчетыРуководителя.ФормулаСравнения(ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация", Организация);
	ПараметрыОтбора.Вставить("ОрганизацияФормулаСравнения", ОрганизацияФормулаСравнения); 
	ПараметрыОтбора.Вставить("ВключатьОбособленныеПодразделения", ПараметрыОтчета.ВключатьОбособленныеПодразделения);
	ПараметрыОтбора.Вставить("Подразделение", Подразделение);
	ПараметрыОтбора.Вставить("ПодразделениеФормулаСравнения", ПодразделениеФормулаСравнения);
	
	Возврат ПараметрыОтбора;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
