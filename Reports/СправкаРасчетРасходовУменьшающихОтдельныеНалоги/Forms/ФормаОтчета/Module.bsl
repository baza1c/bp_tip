
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ВидПериода = Перечисления.ДоступныеПериодыОтчета.Квартал;
	
	ДатаРегистрацииОрганизации = Справочники.Организации.ДатаРегистрацииОрганизации(Отчет.Организация);
	
	ОбновитьОсобенностиПериодаИНалога();
	
	ЗапомнитьИсходныеЗначения(ЭтотОбъект);
	
	ОбновитьТекстЗаголовка();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОписаниеЗаданияФормированияОтчета, ОповещениеОЗавершении, ПараметрыОжидания);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИСформировать(ПараметрыОтчета) Экспорт
	
	Если ТипЗнч(ПараметрыОтчета) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Отчет, ПараметрыОтчета);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыОтчета);
		ЗапомнитьИсходныеЗначения(ЭтотОбъект);
	КонецЕсли;
	
	Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();
	Отчет.КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Очистить();
	
	ВыборПериодаКлиент.ВидПериодаПриИзменении(Элементы.Период,
		ВидПериода,
		Отчет.НачалоПериода,
		Отчет.КонецПериода,
		ЭтотОбъект.Период);
	
	ОткрытьИСформироватьНаСервере();
	
	ЭтотОбъект.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Отчет.РежимРасшифровки);
	ПользовательскиеНастройкиМодифицированы = НЕ Отчет.РежимРасшифровки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	БухгалтерскиеОтчетыКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки)
	
	Если ИспользуютсяСтандартныеНастройки Тогда
		Возврат;
	КонецЕсли;
	
	Если Не КомпоновщикИнициализирован Тогда
		ПользовательскиеНастройки = ПоместитьВоВременноеХранилище(Настройки, УникальныйИдентификатор);
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
	Если Не КомпоновщикИнициализирован И ОбщегоНазначения.ЭтоВебКлиент() Тогда
		ИнициализацияКомпоновщикаНастроек();
	КонецЕсли;
	
	Если Отчет.Организация <> ОрганизацияИсходноеЗначение Тогда
		ДатаРегистрацииОрганизации = Справочники.Организации.ДатаРегистрацииОрганизации(Отчет.Организация);
	КонецЕсли;
	
	ЗапомнитьИсходныеЗначения(ЭтотОбъект);
	
	ОбновитьОсобенностиПериодаИНалога();
	
	ОбновитьТекстЗаголовка();
	
	УправлениеФормой(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "ЛичныйКабинетЕНС_ОбновлениеДанных"
		Или ИмяСобытия = "Запись_СведенияОбУплатеНалогов")
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("Организация")
		И Параметр.Организация = Отчет.Организация Тогда
		
		ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС();
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	
	ПараметрыВыбора.Вставить("НачалоПериода", Отчет.НачалоПериода);
	ПараметрыВыбора.Вставить("КонецПериода",  Отчет.КонецПериода);
	
	Если Налог = "УСН" Тогда
		ПараметрыВыбора.Вставить("МинимальныйПериод", НачалоКвартала(ДатаРегистрацииОрганизации));
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаКвартал", ПараметрыВыбора, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отчет.КонецПериода = КонецКвартала(ТекущаяДата());
	Отчет.НачалоПериода = НачалоКвартала(Отчет.КонецПериода);
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ОрганизацияИсходноеЗначение = Отчет.Организация Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацияИзменилась = Истина;
	
	Налог = "";
	
	ОрганизацияПриИзмененииНаСервере();
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалогПриИзменении(Элемент)
	
	НалогПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаИнтеграцияСЛичнымКабинетомЕНСОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ЕдиныйНалоговыйСчетИнтеграцияКлиентБП.ПодсказкаИнтеграцияСЛичнымКабинетомЕНСОбработкаНавигационнойСсылки(
		Отчет.Организация, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);

КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломИзменения(Элемент, Отказ)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);	
	
КонецПроцедуры

&НаКлиенте
Процедура МакетОформленияПриИзменении(Элемент)
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Отчет.КомпоновщикНастроек.Настройки, "МакетОформления", МакетОформления);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьЗаголовокПриИзменении(Элемент)

	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПодвалПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыводитьЕдиницуИзмеренияПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УвеличитьПериод(Команда)
	
	Отчет.НачалоПериода = НачалоКвартала(ДобавитьМесяц(Отчет.НачалоПериода, 3));
	Отчет.КонецПериода  = КонецКвартала(Отчет.НачалоПериода);
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьПериод(Команда)
	
	Отчет.НачалоПериода = НачалоКвартала(ДобавитьМесяц(Отчет.НачалоПериода, -3));
	Отчет.КонецПериода  = КонецКвартала(Отчет.НачалоПериода);
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	ОчиститьСообщения();
	ОтказПроверкиЗаполнения = Ложь;
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	РезультатВыполнения.Свойство("ОтказПроверкиЗаполнения", ОтказПроверкиЗаполнения);
	
	Если ОтказПроверкиЗаполнения = Истина Тогда
		
		ПоказатьНастройки("");
		
	Иначе
		
		Если РезультатВыполнения.Статус = "Выполняется" Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);

		СкрытьНастройки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	СкрытьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализацияКомпоновщикаНастроек();
	КонецЕсли;
	
	ОткрытьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПоляТабличногоДокументаРезультат

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ОбработкаРасшифровкиСтандартногоОтчета(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	// Не будем обрабатывать нажатие на правую кнопку мыши.
	// Покажем стандартное контекстное меню ячейки табличного документа.
	Расшифровка = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		"Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыОтчетаНаСервере()
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация"                      , Отчет.Организация);
	ПараметрыОтчета.Вставить("НачалоПериода"                    , Отчет.НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода"                     , Отчет.КонецПериода);
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Отчет.ВключатьОбособленныеПодразделения);
	ПараметрыОтчета.Вставить("РежимРасшифровки"                 , Отчет.РежимРасшифровки);
	ПараметрыОтчета.Вставить("ДанныеРасшифровки"                , ДанныеРасшифровки);
	ПараметрыОтчета.Вставить("МакетОформления"                  , МакетОформления);
	ПараметрыОтчета.Вставить("СхемаКомпоновкиДанных"            , ПолучитьИзВременногоХранилища(СхемаКомпоновкиДанных));
	ПараметрыОтчета.Вставить("ИдентификаторОтчета"              , БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(ЭтотОбъект));
	ПараметрыОтчета.Вставить("НастройкиКомпоновкиДанных"        , Отчет.КомпоновщикНастроек.ПолучитьНастройки());
	ПараметрыОтчета.Вставить("ВыводитьЕдиницуИзмерения"         , ВыводитьЕдиницуИзмерения);
	ПараметрыОтчета.Вставить("ОтветственноеЛицо"                , Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаНалоговыеРегистры);
	ПараметрыОтчета.Вставить("Налог"                            , Налог);
	ПараметрыОтчета.Вставить("НалоговыйПериодУСНРасширен"       , НалоговыйПериодУСНРасширен);
	ПараметрыОтчета.Вставить("ДатаНачалаДеятельности"           , ДатаРегистрацииОрганизации);
	
	ПараметрыОтчета.Вставить("РежимРасшифровкиРасходовЕНВДПослеОтмены", РежимРасшифровкиРасходовЕНВДПослеОтмены);
	ПараметрыОтчета.Вставить("РежимРасшифровкиПомощникОплатыПатента", РежимРасшифровкиПомощникОплатыПатента);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Процедура ОбновитьТекстЗаголовка()
	
	ШаблонЗаголовка = Нстр("ru = 'Справка-расчет расходов, уменьшающих %1 %2 %3'");
	
	Если РежимРасшифровкиРасходовЕНВДПослеОтмены Или РежимРасшифровкиПомощникОплатыПатента Тогда
		ПредставлениеПериода = СтрШаблон(НСтр("ru = 'в %1 году'"), Формат(Год(Отчет.КонецПериода), "ЧГ=0"));
	Иначе
		НомерКвартала = ОбщегоНазначенияБПКлиентСервер.НомерКвартала(Отчет.КонецПериода);
		ПредставлениеПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 %2 квартале %3 г.'"),
				?(НомерКвартала = 2, НСтр("ru = 'во'"), НСтр("ru = 'в'")),
				Цел(Месяц(КонецКвартала(Отчет.КонецПериода))/3),
				Формат(Год(Отчет.КонецПериода), "ЧГ=0"));
	КонецЕсли;
	
	ПредставлениеОрганизации = "";
	
	Если ЗначениеЗаполнено(Отчет.Организация) И ИспользуетсяНесколькоОрганизаций Тогда
		ПредставлениеОрганизации = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(
			Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка,
		ПредставлениеДоступныхНалогов, ПредставлениеПериода, ПредставлениеОрганизации);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗапрещенныеПоля(Режим = "") Экспорт
	
	СписокПолей = Новый Массив;
	
	СписокПолей.Добавить("UserFields");
	СписокПолей.Добавить("DataParameters");
	СписокПолей.Добавить("SystemFields");
	СписокПолей.Добавить("Показатели");
	СписокПолей.Добавить("Параметры");
	СписокПолей.Добавить("Ресурсы");
	СписокПолей.Добавить("Группировки");
	СписокПолей.Добавить("Организация");
	СписокПолей.Добавить("ЭтоДобровольноеСтрахование");
	
	Возврат Новый ФиксированныйМассив(СписокПолей);
	
КонецФункции

&НаСервере
Процедура ОткрытьИСформироватьНаСервере()
	
	// Организация и период отчета перезаполнены из входящих параметров.
	// Надо обновить дату регистрации организации...
	ДатаРегистрацииОрганизации = Справочники.Организации.ДатаРегистрацииОрганизации(Отчет.Организация);
	// ...и обработать изменение периода.
	ПриИзмененииПериодаНаСервере();
	
	Если НЕ НалоговыйПериодУСНПропущен Тогда
		СформироватьОтчетНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере() Экспорт
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализацияКомпоновщикаНастроек();
	КонецЕсли;

	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено, ОтказПроверкиЗаполнения", Истина, Истина);
	КонецЕсли;
	
	БухгалтерскиеОтчеты.ОтменитьВыполнениеЗадания(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	Настройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
	Настройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
	Настройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";
	Настройки.ДополнительныеСвойства.Вставить("ВыводитьЗаголовок", ВыводитьЗаголовок);
	Настройки.ДополнительныеСвойства.Вставить("ВыводитьПодвал"   , ВыводитьПодвал);
	
	Отчет.ПоказательБУ = Истина;
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();
	
	ОписаниеЗаданияФормированияОтчета = БухгалтерскиеОтчеты.ЗапуститьПодготовкуОтчета(ЭтотОбъект, ПараметрыОтчета);
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	
	Возврат ОписаниеЗаданияФормированияОтчета;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗапомнитьИсходныеЗначения(Форма)
	
	Отчет = Форма.Отчет;
	
	Форма.ОрганизацияИсходноеЗначение = Отчет.Организация;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОсобенностиПериодаИНалога()
	
	Если Налог = "УСН" Тогда
		
		МинимальныйПериод = НачалоКвартала(ДатаРегистрацииОрганизации);
		
		Если НачалоКвартала(Отчет.НачалоПериода) < МинимальныйПериод Тогда
			Отчет.НачалоПериода = МинимальныйПериод;
			Отчет.КонецПериода  = КонецКвартала(Отчет.НачалоПериода);
		КонецЕсли;
		
	КонецЕсли;
	
	Если РежимРасшифровкиРасходовЕНВДПослеОтмены Тогда
		ПериодУменьшенияЕНВД = УчетЕНВД.ПериодУменьшенияЕНВДПослеОтмены(Отчет.Организация);
		Отчет.НачалоПериода = ПериодУменьшенияЕНВД.НачалоПериода;
		Отчет.КонецПериода  = ПериодУменьшенияЕНВД.КонецПериода;
	КонецЕсли;
	
	ОбновитьНалог();
	
	Период = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		Отчет.НачалоПериода, Отчет.КонецПериода, Истина);
	
	НалоговыйПериодУСНРасширен = (Налог = "УСН")
		И НачалоКвартала(Отчет.НачалоПериода) = НачалоГода(Отчет.НачалоПериода) // Период обзора расходов "расширяется" только в 1 квартале.
		И УчетУСН.НалоговыйПериодРасширен(Отчет.Организация, Отчет.КонецПериода, ДатаРегистрацииОрганизации);
	НалоговыйПериодУСНПропущен = (Налог = "УСН")
		И УчетУСН.НалоговыйПериодПропущен(Отчет.Организация, Отчет.КонецПериода, ДатаРегистрацииОрганизации);
	
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Заголовок = ПояснениеПереносНалоговогоПериода();
	
	ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС();
	
КонецПроцедуры

&НаСервере
Функция ПояснениеПереносНалоговогоПериода()
	
	Если Налог <> "УСН" Тогда
		Возврат "";
	КонецЕсли;
	
	ФорматнаяСтрокаПериода = "ДФ=dd.MM.yyyy";
	
	Если НалоговыйПериодУСНПропущен Тогда
		
		ГодОтчета = Год(Отчет.КонецПериода);
		
		ШаблонПодсказки = НСтр("ru = 'Налог УСН за %1 год уплачивать не нужно. Расходы с даты регистрации %2 по %3 учитываются при уменьшении налога за %4.'");
		
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПодсказки,
			Формат(ГодОтчета, "ЧГ=0"),
			Формат(ДатаРегистрацииОрганизации, ФорматнаяСтрокаПериода),
			Формат(КонецГода(ДатаРегистрацииОрганизации), ФорматнаяСтрокаПериода),
			Формат(Формат(КонецГода(ДатаРегистрацииОрганизации) + 1, "ДФ='к ''квартал'' гггг ''года'''"), "ЧГ=0"));
		
	ИначеЕсли НалоговыйПериодУСНРасширен Тогда
		
		ШаблонПояснения = НСтр("ru = 'Период с даты регистрации %1 по %2 включается в расчет за %3'");
		
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПояснения,
			Формат(ДатаРегистрацииОрганизации, ФорматнаяСтрокаПериода),
			Формат(КонецГода(ДатаРегистрацииОрганизации), ФорматнаяСтрокаПериода),
			ПредставлениеПериода(НачалоКвартала(Отчет.НачалоПериода), КонецКвартала(Отчет.КонецПериода), "ФП = Истина"));
		
	Иначе
		
		Возврат "";
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбновитьНалог()
	
	ПрименяетсяУСНДоходы = УчетнаяПолитика.ПрименяетсяУСНДоходы(Отчет.Организация, Отчет.КонецПериода);
	ПлательщикЕНВД = УчетнаяПолитика.ПлательщикЕНВД(Отчет.Организация, Отчет.КонецПериода);
	УменьшениеЕНВДПослеОтмены = УчетЕНВД.РасходыПериодаУменьшаютЕНВДПослеОтмены(Отчет.Организация, Отчет.КонецПериода);
	ПрименяетсяЕНВД = ПлательщикЕНВД Или УменьшениеЕНВДПослеОтмены;
	// Информация по патентной системе в справке-расчете отображается с 2021 года.
	ПрименяетсяПСН = Отчет.КонецПериода >= УчетПСНКлиентСервер.ДатаНачалаУменьшенияПСННаСтраховыеВзносы()
		И УчетнаяПолитика.ПрименяетсяУСНПатент(Отчет.Организация, Отчет.КонецПериода);
	
	КоличествоНалоговыхРежимов = Число(ПрименяетсяУСНДоходы) + Число(ПрименяетсяЕНВД) + Число(ПрименяетсяПСН);
	
	Элементы.Налог.Видимость = (КоличествоНалоговыхРежимов > 1);
	
	ПредставлениеДоступныхНалогов = ПредставлениеДоступныхНалогов(
		ПрименяетсяУСНДоходы, ПрименяетсяПСН, ПлательщикЕНВД, УменьшениеЕНВДПослеОтмены, РежимРасшифровкиПомощникОплатыПатента);
	
	Если (Налог = "ЕНВД" И Не ПрименяетсяЕНВД)
		Или (Налог = "ПСН" И Не ПрименяетсяПСН) Тогда
		Налог = "";
	КонецЕсли;
	
	ОбновитьСписокВыбораНалога(ПрименяетсяЕНВД, ПрименяетсяПСН);
	
	Если ЗначениеЗаполнено(Налог) Тогда // Налог уже установлен, инициализировать не требуется.
		Возврат;
	КонецЕсли;
	
	Если ПрименяетсяПСН И Не ПрименяетсяУСНДоходы Тогда
		Налог = "ПСН";
	ИначеЕсли ПрименяетсяЕНВД И Не ПрименяетсяУСНДоходы Тогда
		Налог = "ЕНВД";
	Иначе
		Налог = "УСН";
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеДоступныхНалогов(УСН, ПСН, ЕНВД, ВычетЕНВДПослеОтмены, РежимПомощникаОплатыПатента)
	
	НазванияНалогов = Новый Массив;
	
	Если РежимПомощникаОплатыПатента Тогда
		НазванияНалогов.Добавить(НСтр("ru = 'Патент'"));
	Иначе
		Если УСН Тогда
			НазванияНалогов.Добавить(НСтр("ru = 'УСН'"));
		КонецЕсли;
		
		Если ПСН Тогда
			НазванияНалогов.Добавить(НСтр("ru = 'Патент'"));
		КонецЕсли;
		
		Если ЕНВД Тогда
			НазванияНалогов.Добавить(НСтр("ru = 'ЕНВД'"));
		ИначеЕсли ВычетЕНВДПослеОтмены Тогда
			НазванияНалогов.Добавить(
			СтрШаблон(НСтр("ru = 'ЕНВД (за %1)'"),
				УчетЕНВДКлиентСервер.ПредставлениеПоследнегоНалоговогоПериодаЕНВД()));
		КонецЕсли;
	КонецЕсли;
	
	ПредставлениеНалогов = "";
	
	Если НазванияНалогов.Количество() = 1 Тогда
		
		Если УСН Тогда
			ПредставлениеНалогов = СтрШаблон(Нстр("ru = 'налог %1'"), НазванияНалогов[0]);
		Иначе
			ПредставлениеНалогов = НазванияНалогов[0];
		КонецЕсли;
		
	ИначеЕсли НазванияНалогов.Количество() = 2 Тогда
		
		Если УСН И ПСН Тогда
			ПредставлениеНалогов = СтрШаблон(Нстр("ru = 'налоги %1 и %2'"), НазванияНалогов[0], НазванияНалогов[1]);
		Иначе
			ПредставлениеНалогов = СтрШаблон(Нстр("ru = 'налог %1 и %2'"), НазванияНалогов[0], НазванияНалогов[1]);
		КонецЕсли;
	
	ИначеЕсли НазванияНалогов.Количество() = 3 Тогда
		
		ПредставлениеНалогов = СтрШаблон(Нстр("ru = 'налоги %1, %2 и %3'"),
			НазванияНалогов[0], НазванияНалогов[1], НазванияНалогов[2]);
		
	КонецЕсли;
	
	Возврат ПредставлениеНалогов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Отчет    = Форма.Отчет;
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаБыстрыеОтборы.Видимость = Не Форма.РежимРасшифровкиРасходовЕНВДПослеОтмены
		И Не Форма.РежимРасшифровкиПомощникОплатыПатента;
	
	ЕНВД = (Форма.Налог = "ЕНВД");
	
	Элементы.УменьшитьПериод.Доступность = ЕНВД
		ИЛИ (НачалоКвартала(Отчет.КонецПериода) > НачалоКвартала(Форма.ДатаРегистрацииОрганизации));
	
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость
		= Форма.НалоговыйПериодУСНРасширен ИЛИ Форма.НалоговыйПериодУСНПропущен;
	
	Элементы.ГруппаКоманднаяПанель.Видимость = ЕНВД ИЛИ НЕ Форма.НалоговыйПериодУСНПропущен;
	Элементы.Результат.Видимость             = ЕНВД ИЛИ НЕ Форма.НалоговыйПериодУСНПропущен;
	Элементы.НастройкиОтчета.Видимость       = ЕНВД ИЛИ НЕ Форма.НалоговыйПериодУСНПропущен;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения)

	БухгалтерскиеОтчеты.ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыВыбораЗначенияОтбора() Экспорт
	
	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Дата"              , Отчет.КонецПериода);
	СписокПараметров.Вставить("Номенклатура"      , Неопределено);
	СписокПараметров.Вставить("Склад"             , Неопределено);
	СписокПараметров.Вставить("Организация"       , Отчет.Организация);
	СписокПараметров.Вставить("Контрагент"        , Неопределено);
	СписокПараметров.Вставить("ДоговорКонтрагента", Неопределено);
	
	Возврат СписокПараметров;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияКомпоновщикаНастроек()
	
	БухгалтерскиеОтчетыВызовСервера.ИнициализацияКомпоновщикаНастроек(ЭтотОбъект, ОрганизацияИзменилась);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПериода()
	
	ПриИзмененииПериодаНаСервере();
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПериодаНаСервере()
	
	ОбновитьОсобенностиПериодаИНалога();
	
	ОбновитьТекстЗаголовка();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	Если КомпоновщикИнициализирован Тогда
		БухгалтерскиеОтчетыКлиентСервер.ОрганизацияПриИзменении(ЭтотОбъект, Отчет.Организация);
	КонецЕсли;
	
	ДатаРегистрацииОрганизации = Справочники.Организации.ДатаРегистрацииОрганизации(Отчет.Организация);
	
	ОбновитьОсобенностиПериодаИНалога();
	
	ЗапомнитьИсходныеЗначения(ЭтотОбъект);
	
	ОбновитьТекстЗаголовка();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура НалогПриИзмененииНаСервере()
	
	ОбновитьОсобенностиПериодаИНалога();
	
	УправлениеФормой(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

// Обновляет список выбора элемента "Налог" с учетом требуемых для отображения в отчете налоговых режимов.
//
// Параметры:
//  ПрименяетсяЕНВД - Булево - признак необходимости отображения в отчете информации по ЕНВД.
//  ПрименяетсяПСН - Булево - признак необходимости отображения в отчете информации по ПСН.
//
&НаСервере
Процедура ОбновитьСписокВыбораНалога(ОтображатьЕНВД, ОтображатьПСН)
	
	СписокВыбора = Элементы.Налог.СписокВыбора;
	
	СписокВыбора.Очистить();
	
	СписокВыбора.Добавить("УСН");
	
	Если ОтображатьЕНВД Тогда
		СписокВыбора.Добавить("ЕНВД");
	КонецЕсли;
	
	Если ОтображатьПСН Тогда
		СписокВыбора.Добавить("ПСН");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС()
	
	ЕдиныйНалоговыйСчетИнтеграцияВнутреннийБП.ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС(
		ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

#КонецОбласти
