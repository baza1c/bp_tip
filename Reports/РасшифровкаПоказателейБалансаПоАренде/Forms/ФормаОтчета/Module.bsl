&НаКлиенте
Перем СуммируемыеЯчейки;

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияАктуализации Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	ЗаполнитьЗначенияСвойств(Отчет, Параметры);

	ЭтотОбъект.ИмяОтчета = БухгалтерскиеОтчеты.ИмяОтчета(ЭтотОбъект);
	ЭтотОбъект.СинонимОтчета = Метаданные.Отчеты[ИмяОтчета].Синоним;
	//Для этого отчета период - месяц
	ЭтотОбъект.ДоступныйПериодМесяц = Перечисления.ДоступныеПериодыОтчета.Месяц;
	
	БухгалтерскиеОтчетыВызовСервера.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ЗаполнитьДатуРегистрацииОрганизации();
	УстановитьПериод(ЭтотОбъект);
	УправлениеФормой(ЭтаФорма);
	
	Если Параметры.СформироватьПриОткрытии = Истина Тогда // Если не передан, то значение Неопределено.
		
		СтандартнаяОбработка = Ложь;
		СформироватьОтчетНаСервере(); // фактически запустится в фоновом задании.
		
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Отчет.РасшифровкаПоказателейБалансаПоАренде",
		"ФормаОтчета",
		НСтр("ru = 'Новости: Расшифровка показателей баланса по аренде (лизингу)"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОписаниеЗаданияФормированияОтчета, ОповещениеОЗавершении, ПараметрыОжидания);
		
	ИначеЕсли Отчет.РежимРасшифровки Тогда
		
		БухгалтерскийУчетКлиентПереопределяемый.ПослеФормированияОтчета(ЭтотОбъект);
		
	КонецЕсли;

	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		ОбщегоНазначенияБПКлиентСервер.УстановитьЗначениеПолеОрганизация(
			ПолеОрганизация,
			Отчет.Организация,
			Отчет.ВключатьОбособленныеПодразделения);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Отчет.РежимРасшифровки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	БухгалтерскиеОтчетыКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки)

	Если ИспользуютсяСтандартныеНастройки Тогда
		Возврат;
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		ОбщегоНазначенияБПКлиентСервер.УстановитьЗначениеПолеОрганизация(
			ПолеОрганизация,
			Отчет.Организация,
			Отчет.ВключатьОбособленныеПодразделения);
	КонецЕсли;

	ЗаполнитьДатуРегистрацииОрганизации();
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	УстановитьПериод(ЭтотОбъект);
	УправлениеФормой(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(ОписаниеЗаданияФормированияОтчета) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОбработкаОповещенияАктуализации(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода, ИмяСобытия, Параметр, Источник);
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ВыборПериодаКлиент.ПериодПриИзменении(Элемент, Период, Отчет.НачалоПериода, Отчет.КонецПериода);
	УстановитьПериод(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(ОписаниеЗаданияФормированияОтчета) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиент.СброситьВидПериода(Отчет);
	УправлениеФормой(ЭтотОбъект);
	ОбновитьТекстЗаголовка(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода",    Отчет.НачалоПериода);
	ПараметрыВыбора.Вставить("КонецПериода",     Отчет.КонецПериода);
	ПараметрыВыбора.Вставить("ВидПериода",       ЭтотОбъект.ДоступныйПериодМесяц);
	ПараметрыВыбора.Вставить("ОграничениеСнизу", НачалоГода(ДатаРегистрацииОрганизации));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбора, Элементы.Период, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодОбработкаВыбора(
		Элемент, ВыбранноеЗначение, СтандартнаяОбработка,
		ЭтотОбъект.ДоступныйПериодМесяц, Период, Отчет.НачалоПериода, Отчет.КонецПериода);

КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияПриИзменении(Элемент, ПолеОрганизация,
		Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
		
	ОрганизацияПриИзмененииНаСервере();
	
	Если Не ЗначениеЗаполнено(ОписаниеЗаданияФормированияОтчета) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
		
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОбработкаВыбора(Элемент,
		ВыбранноеЗначение,
		СтандартнаяОбработка,
		СоответствиеОрганизаций,
		Отчет.Организация,
		Отчет.ВключатьОбособленныеПодразделения);
	
КонецПроцедуры

#Область СписокОтборы

// Важно: поле на форме должно иметь предопределенное имя "Отборы"
&НаКлиенте
Процедура ОтборыПриИзменении(Элемент)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПриИзменении(ЭтотОбъект, Элемент, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);

КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломИзменения(Элемент, Отказ)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПравоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ОтборыПравоеЗначениеНачалоВыбора(
		ЭтотОбъект,
		Элемент,
		ДанныеВыбора,
		СтандартнаяОбработка,
		ПараметрыВыбораЗначенияОтбора());
	
КонецПроцедуры

#КонецОбласти

#Область ФлагиОформления

&НаКлиенте
Процедура ВыводитьЗаголовокПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ОписаниеЗаданияФормированияОтчета) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПодвалПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ОписаниеЗаданияФормированияОтчета) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьЕдиницуИзмеренияПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ОписаниеЗаданияФормированияОтчета) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПоляТабличногоДокумента

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРасшифровки = ПараметрыРасшифровкиОтчета(ЭтотОбъект.ДанныеРасшифровки, Расшифровка);
	Если Не ЗначениеЗаполнено(ПараметрыРасшифровки) Тогда 
		Расшифровка = Неопределено;
		Возврат;
	КонецЕсли;
	
	ОбработатьРасшифровки(ПараметрыРасшифровки);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	// Не будем обрабатывать нажатие на правую кнопку мыши.
	// Покажем стандартное контекстное меню ячейки табличного документа.
	Расшифровка = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		ИмяОбработчикаВыделенаОбластьЯчеек());
	
КонецПроцедуры

#Область РасчетСуммыВыделенныхЯчеек

&НаКлиенте
Функция ИмяОбработчикаВыделенаОбластьЯчеек()
	Возврат "Подключаемый_ВыделенаОбластьЯчеек";
КонецФункции

&НаКлиенте
Процедура Подключаемый_ВыделенаОбластьЯчеек()
	
	ТребуетсяВызовСервера = БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		СуммаВыделенныхЯчеек,
		Результат,
		Элементы.Результат,
		СуммируемыеЯчейки);
	
	Если ТребуетсяВызовСервера Тогда
		РассчитатьСуммуВыделенныхЯчеекНаСервере(СуммируемыеЯчейки);
	КонецЕсли;
	
	ОтключитьОбработчикОжидания(ИмяОбработчикаВыделенаОбластьЯчеек());
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуВыделенныхЯчеекНаСервере(СуммируемыеЯчейки)
	
	// Процедура БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента() вносит изменения
	// в содержимое параметра СуммируемыеЯчейки. Поэтому параметр процедуры СуммируемыеЯчейки не должен передаваться по значению.
	СуммаВыделенныхЯчеек = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат,
		СуммируемыеЯчейки);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли; 
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	
	СкрытьНастройки(Команда);

КонецПроцедуры

&НаКлиенте
Процедура ВидПериода(Команда)
	
	БухгалтерскиеОтчетыКлиент.УстановитьВидПериодаПоИмениКоманды(Команда.Имя, ЭтотОбъект);
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#Область Настройки

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	БухгалтерскиеОтчетыКлиентСервер.ПоказатьНастройки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНастройки(Команда)
	
	БухгалтерскиеОтчетыКлиентСервер.СкрытьНастройки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);
	
КонецПроцедуры

#КонецОбласти

#Область КешКодаБухгалтерскиеОтчеты

&НаКлиенте
Функция ПараметрыВыбораЗначенияОтбора()
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("Дата",               Отчет.КонецПериода);
	ПараметрыВыбора.Вставить("Организация",        Отчет.Организация);
	ПараметрыВыбора.Вставить("Контрагент",         Неопределено);
	ПараметрыВыбора.Вставить("ДоговорКонтрагента", Неопределено);
	
	Возврат ПараметрыВыбора;
	
КонецФункции

&НаКлиенте
Функция ПолучитьЗапрещенныеПоля(Режим = "") Экспорт
	
	СписокПолей = Новый Массив;
	
	СписокПолей.Добавить("UserFields");
	СписокПолей.Добавить("DataParameters");
	СписокПолей.Добавить("SystemFields");
	СписокПолей.Добавить("Показатели");
	СписокПолей.Добавить("Параметры");
	СписокПолей.Добавить("Ресурсы");
	СписокПолей.Добавить("Группировки");
	СписокПолей.Добавить("Организация");
	
	Возврат Новый ФиксированныйМассив(СписокПолей);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьДатуРегистрацииОрганизации();
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	БухгалтерскиеОтчетыКлиентСервер.ОрганизацияПриИзменении(ЭтотОбъект, Отчет.Организация);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДатуРегистрацииОрганизации()

	ДатаРегистрацииОрганизации = Справочники.Организации.ДатаРегистрацииОрганизации(Отчет.Организация);

КонецПроцедуры

#Область ФормированиеОтчета

&НаСервере
Функция СформироватьОтчетНаСервере() Экспорт
	
	БухгалтерскиеОтчеты.ОтменитьВыполнениеЗадания(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");

	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	Настройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
	Настройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
	Настройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";
	
	Контекст = Отчеты[ИмяОтчета].НовыйКонтекстФормированияОтчета();
	ЗаполнитьКонтекст(Контекст);
	
	ОписаниеЗаданияФормированияОтчета = БухгалтерскиеОтчеты.ЗапуститьПодготовкуОтчета(ЭтотОбъект, Контекст);
	
	Возврат ОписаниеЗаданияФормированияОтчета;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
	БухгалтерскийУчетКлиентПереопределяемый.ПослеФормированияОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения)
	
	БухгалтерскиеОтчеты.ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ЭтотОбъект);
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьКонтекст(Контекст)
	
	Контекст.ИдентификаторОтчета       = ЭтотОбъект.ИмяОтчета;
	Контекст.СинонимОтчета             = ЭтотОбъект.СинонимОтчета;
	Контекст.СхемаКомпоновкиДанных     = ПолучитьИзВременногоХранилища(ЭтотОбъект.СхемаКомпоновкиДанных);
	Контекст.НастройкиКомпоновкиДанных = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	Контекст.ДанныеРасшифровки         = ЭтотОбъект.ДанныеРасшифровки;
	Контекст.РежимРасшифровки          = Отчет.РежимРасшифровки;
	
	Контекст.Организация                       = Отчет.Организация;
	Контекст.ВключатьОбособленныеПодразделения = Отчет.ВключатьОбособленныеПодразделения;
	Контекст.НачалоПериода                     = Отчет.НачалоПериода;
	Контекст.КонецПериода                      = БухгалтерскиеОтчеты.КонецПериодаСУчетомВидаПериода(КонецДня(Отчет.КонецПериода), Отчет.ВидПериода);
	Контекст.КонецПериодаДата                  = КонецДня(Отчет.КонецПериода);
	
	Контекст.ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.КонецПериода, Отчет.КонецПериода, Истина, Отчет.ВидПериода);
	
	Контекст.ВыводитьЕдиницуИзмерения = ВыводитьЕдиницуИзмерения; // Соответствует флагу "Единица измерения" в форме отчета на закладке "Оформление".
	Контекст.ВыводитьЗаголовок        = ВыводитьЗаголовок; // Соответствует флагу "Заголовок" в форме отчета на закладке "Оформление".
	Контекст.ВыводитьПодвал           = ВыводитьПодвал; // Соответствует флагу "Подписи" в форме отчета на закладке "Оформление".
	Контекст.ОтветственноеЛицо        = Перечисления.ОтветственныеЛицаОрганизаций.ОтветственныйЗаБухгалтерскиеРегистры;
	
	Контекст.ИспользуетсяВалютныйУчет = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
	Контекст.ЕстьДисконтирование      = УчетнаяПолитика.ПоддерживаетсяДисконтированиеОбязательств();
	Контекст.Организации              = ОбщегоНазначенияБП.СписокДоступныхОрганизаций(Контекст.Организация, Контекст.ВключатьОбособленныеПодразделения);

КонецПроцедуры

#КонецОбласти

#Область НастройкаФормы

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПериод(Форма)
	
	Отчет = Форма.Отчет;
		
	Форма.Период = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		Форма.ДоступныйПериодМесяц, Отчет.НачалоПериода, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТекстЗаголовка(Форма)
		
	Отчет = Форма.Отчет;
	
	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		
		ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 на %2 %3'"),
			Форма.СинонимОтчета,
			БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.КонецПериода, Отчет.КонецПериода, Истина, Отчет.ВидПериода),
			БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения));
		
	Иначе
		
		ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 на %2'"),
			Форма.СинонимОтчета,
			БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.КонецПериода, Отчет.КонецПериода, Истина, Отчет.ВидПериода));
		
	КонецЕсли;
	
	Форма.Заголовок = ЗаголовокОтчета;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Отчет    = Форма.Отчет;
	Элементы = Форма.Элементы;
	
	БухгалтерскиеОтчетыКлиентСервер.УправлениеВидимостьюВидПериода(Элементы.ГруппаВидПериода, Отчет);
	
КонецПроцедуры

#КонецОбласти

#Область РасшифровкаПолей

&НаСервере
Функция ПараметрыРасшифровкиОтчета(ДанныеРасшифровки, Расшифровка)
	
	Возврат Отчеты[ИмяОтчета].ПараметрыРасшифровкиОтчета(ДанныеРасшифровки, Расшифровка);

КонецФункции

&НаКлиенте
Процедура ОбработатьРасшифровки(ПараметрыРасшифровки)
	
	Если ПараметрыРасшифровки.Свойство("ОткрытьОбъект") И ПараметрыРасшифровки.ОткрытьОбъект Тогда
		ПоказатьЗначение( , ПараметрыРасшифровки.Значение);
	ИначеЕсли ПараметрыРасшифровки.Свойство("ОткрытьФормуПечати") И ПараметрыРасшифровки.ОткрытьФормуПечати Тогда 
		ОткрытьФорму("ОбщаяФорма.ПечатьДокументов", ПараметрыРасшифровки.ПараметрыОткрытияФормы, Неопределено, Строка(Новый УникальныйИдентификатор));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КомандыАктуализацияДанных

&НаКлиенте
Процедура Актуализировать(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.Актуализировать(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьАктуализацию(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	БухгалтерскийУчетКлиентПереопределяемый.ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
		
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыйПроцедурыАктуализацииДанных

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

#КонецОбласти

// Процедура показывает новости, требующие прочтения (важные и очень важные).
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(
		ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

#КонецОбласти