#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Описывает свойства отчета (манифест), декларирующий поддержанные в нем возможности подсистемы,
// включая реализованный программный интерфейс.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Манифест = Новый Структура;
	Манифест.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Манифест.Вставить("ИспользоватьПослеКомпоновкиМакета",  Истина);
	Манифест.Вставить("ИспользоватьВнешниеНаборыДанных",    Истина);
	Манифест.Вставить("ИспользоватьПриВыводеЗаголовка",     Истина);
	Манифест.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	СправкиРасчеты.УстановитьОтчетНеИспользуетНаборыСуммовыхПоказателей(Манифест);

	Возврат Манифест;

КонецФункции

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПередКомпоновкойМакета.
//
// Параметры:
//  Контекст  - См. НовыйКонтекстФормированияОтчета
//  Схема - СхемаКомпоновкиДанных - схема отчета.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - описание связи настроек и схемы отчета.
//
Процедура ПередКомпоновкойМакета(Контекст, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(Контекст.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "КонецПериода", Контекст.КонецПериодаДата);
	КонецЕсли;
	
	// получаем исходные данные по арендным обязательствам
	КонтекстРасчета = РасчетПоказателейБалансаПоАренднымОбязательствам.НовыйКонтекстРасчета();
	ЗаполнитьЗначенияСвойств(КонтекстРасчета, Контекст);
	ИсходныеДанные = РасчетПоказателейБалансаПоАренднымОбязательствам.ИсходныеДанныеПоАренднымОбязательствам(КонтекстРасчета);
	
	// переносим исходные данные в контекст
	Контекст.ЕстьРасчетыВВалюте = ИсходныеДанные.ЕстьРасчетыВВалюте;
	Контекст.ВнешниеНаборыДанных.Вставить("АрендныеОбязательства", ИсходныеДанные.АрендныеОбязательства);
	Контекст.ВнешниеНаборыДанных.Вставить("ГрафикиПлатежей",       ИсходныеДанные.ГрафикиПлатежей);
	
	// добавляем в контекст параметры для вывода группировок в табличный документ и оформления заголовков таблицы
	Контекст.ОписаниеШапкиТаблиц = ОписаниеШапкиТаблиц(Контекст);
	
	// в зависимости от параметров настраиваем структуру отчета
	ИзменитьСтруктуруОтчета(КомпоновщикНастроек, Контекст.ОписаниеШапкиТаблиц);
	
КонецПроцедуры

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПослеКомпоновкиМакета.
//
// Параметры:
//  Контекст - См. НовыйКонтекстФормированияОтчета
//  МакетКомпоновки - МакетКомпоновкиДанных - сформированный макет компоновки данных.
//
Процедура ПослеКомпоновкиМакета(Контекст, МакетКомпоновки) Экспорт
	
	ДобавитьМакетыТаблиц(Контекст.ОписаниеШапкиТаблиц, МакетКомпоновки);
	
	Для Каждого ОписаниеШапки Из Контекст.ОписаниеШапкиТаблиц Цикл
		ОписаниеШапки = ОписаниеШапки.Значение;
		Если ОписаниеШапки.Макеты <> Неопределено Тогда
			
			РазмещениеМакетовШапкиТаблицы = Новый Соответствие;
			Для Каждого Макет Из ОписаниеШапки.Макеты Цикл
				Если Макет.Значение <> ИмяИтогиГруппировки() Тогда
					РазмещениеМакетовШапкиТаблицы.Вставить(Макет.Ключ, Макет.Значение);
				КонецЕсли;
			КонецЦикла;
			
			Если ОписаниеШапки.Графы <> Неопределено Тогда
				СправкиРасчеты.НастроитьГрафыОтчета(МакетКомпоновки, ОписаниеШапки.Графы, РазмещениеМакетовШапкиТаблицы);
			КонецЕсли;
			
			ОбработатьМакетыТаблиц(МакетКомпоновки, ОписаниеШапки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьВнешниеНаборыДанных.
//
// Параметры:
//  Контекст - См. НовыйКонтекстФормированияОтчета
//  МакетКомпоновки - МакетКомпоновкиДанных - сформированный макет компоновки данных.
//
Функция ПолучитьВнешниеНаборыДанных(Контекст, МакетКомпоновки) Экспорт
	
	Возврат Контекст.ВнешниеНаборыДанных;
	
КонецФункции

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПослеВыводаРезультата.
//
// Параметры:
//  Контекст - См. НовыйКонтекстФормированияОтчета
//  Результат - ТабличныйДокумент - табличный документ с сформированным отчетом.
//
Процедура ПослеВыводаРезультата(Контекст, Результат) Экспорт
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(Контекст.ИдентификаторОтчета, Результат);

КонецПроцедуры

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПриВыводеЗаголовка.
//
// Параметры:
//  Контекст - См. НовыйКонтекстФормированияОтчета
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - описание связи настроек и схемы отчета.
//  Результат - ТабличныйДокумент - табличный документ с сформированным отчетом.
//
Процедура ПриВыводеЗаголовка(Контекст, КомпоновщикНастроек, Результат) Экспорт
	
	ВывестиШапкуОтчета(Результат, Контекст);
	
КонецПроцедуры 

// Возвращает параметры расшифровки ячейки отчета.
//
// Параметры:
//  Адрес - Строка - Адрес временного хранилища с данными расшифровки отчета.
//  ИдентификаторРасшифровки - ИдентификаторРасшифровкиКомпоновкиДанных
//
// Возвращаемое значение:
//  Структура - Содержит ключи (опционально):
//   * ОткрытьОбъект - Булево - Если Истина, то необходимо открыть указанный объект.
//   * ОткрытьФормуПечати - Булево - Если Истина, то необходимо открыть открыть печатную форму по параметрам.
//   * Значение - Произвольный - Значение расшифровывающего объекта.
//   * ПараметрыОткрытияФормы - Структура - параметры, необходимые для формирования печатной формы.
//
Функция ПараметрыРасшифровкиОтчета(Адрес, ИдентификаторРасшифровки) Экспорт
	
	ПараметрыРасшифровки = Новый Структура;
	
	// Заполним соответствие полей которые мы хотим получить из данных расшифровки
	СоответствиеПолей = Новый Соответствие;
	ДанныеОтчета = ПолучитьИзВременногоХранилища(Адрес);
	
	ЗначениеРасшифровки = ДанныеОтчета.ДанныеРасшифровки.Элементы[ИдентификаторРасшифровки];
	Если ТипЗнч(ЗначениеРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда 
		ПолеРасшифровки = ЗначениеРасшифровки.ПолучитьПоля()[0];
		
		Если СтрНайти(ПолеРасшифровки.Поле, "ОбязательствоГрафик") > 0 Тогда
			СоответствиеПолей.Вставить("ДокументАренды");
			СоответствиеПолей.Вставить("ДокументАрендыГрафик");
			
			// Получаем соответствие полей доступных в расшифровке
			ДанныеРасшифровки = БухгалтерскиеОтчеты.ПолучитьДанныеРасшифровки(ДанныеОтчета.ДанныеРасшифровки, СоответствиеПолей, ИдентификаторРасшифровки);
			
			Для Каждого ЭлементПоля Из СоответствиеПолей Цикл
				ДокументАренды = ДанныеРасшифровки[ЭлементПоля.Ключ];
				Если ДокументАренды <> Неопределено Тогда 
					ПараметрыРасшифровки.Вставить("ОткрытьФормуПечати", Истина);
					ПараметрыРасшифровки.Вставить("Значение", ДокументАренды);
					ПараметрыРасшифровки.Вставить("ПараметрыОткрытияФормы", ПараметрыОткрытияФормыПечатиСправкиПроцентов(ДанныеОтчета, ДокументАренды));
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ЗначениеЗаполнено(ПолеРасшифровки.Значение) И ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ПолеРасшифровки.Значение)) Тогда
			ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
			ПараметрыРасшифровки.Вставить("Значение",  ПолеРасшифровки.Значение);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыРасшифровки;
	
КонецФункции

// Конструктор коллекции, описывающий контекст и данные отчета
// При формировании отчета передается параметром согласно манифесту
//
// Возвращаемое значение:
//  Структура
//
Функция НовыйКонтекстФормированияОтчета() Экспорт 
	
	// Инициализация коллекции
	Контекст = Новый Структура;
	Контекст.Вставить("ИдентификаторОтчета");
	Контекст.Вставить("СинонимОтчета");
	
	Контекст.Вставить("СхемаКомпоновкиДанных");
	Контекст.Вставить("НастройкиКомпоновкиДанных");
	
	Контекст.Вставить("ДанныеРасшифровки");
	
	Контекст.Вставить("Организация");
	Контекст.Вставить("НачалоПериода");
	Контекст.Вставить("КонецПериода");
	Контекст.Вставить("КонецПериодаДата");
	Контекст.Вставить("ВнешниеНаборыДанных", Новый Структура);
	Контекст.Вставить("ЕстьРасчетыВВалюте", Ложь);
	
	Контекст.Вставить("ПредставлениеПериода");
	
	Контекст.Вставить("РежимРасшифровки",                  Ложь);
	Контекст.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	Контекст.Вставить("ВыводитьЕдиницуИзмерения",          Ложь); // Соответствует флагу "Единица измерения" в форме справки-расчета на закладке "Оформление".
	Контекст.Вставить("ВыводитьЗаголовок",                 Ложь); // Соответствует флагу "Заголовок" в форме справки-расчета на закладке "Оформление".
	Контекст.Вставить("ВыводитьПодвал",                    Ложь); // Соответствует флагу "Подписи" в форме справки-расчета на закладке "Оформление".
	Контекст.Вставить("ОтветственноеЛицо");
	Контекст.Вставить("ИспользуетсяВалютныйУчет", Ложь);
	Контекст.Вставить("ЕстьДисконтирование",      Ложь);
	Контекст.Вставить("ОписаниеШапкиТаблиц");
	Контекст.Вставить("Организации", Новый Массив);
	
	Возврат Контекст;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выводит шапку отчета.
//
// Параметры:
//  Результат - ТабличныйДокумент - табличный документ, в который выводится отчет
//  Контекст - См. НовыйКонтекстФормированияОтчета()
//
Процедура ВывестиШапкуОтчета(Результат, Контекст)
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	
	// Организация
	ОбластьВывода = Макет.ПолучитьОбласть("Организация");
	ОбластьВывода.Параметры.НазваниеОрганизации = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(
		Контекст.Организация,
		Контекст.ВключатьОбособленныеПодразделения,
		Контекст.КонецПериодаДата);
	Результат.Вывести(ОбластьВывода);
	
	// Название отчета
	ОбластьВывода = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьВывода.Параметры.ЗаголовокОтчета = ЗаголовокОтчета(Контекст);
	Результат.Вывести(ОбластьВывода);
	
	// Организация и название образуют заголовок
	ОбластьЗаголовка = Результат.Область(1, , Результат.ВысотаТаблицы);
	ОбластьЗаголовка.Имя = "Заголовок";
	
	// Единица измерения
	Если Контекст.Свойство("ВыводитьЕдиницуИзмерения")
		И Контекст.ВыводитьЕдиницуИзмерения Тогда
		// Единица измерения - рубль
		ОбластьВывода = Макет.ПолучитьОбласть("ОписаниеЕдиницыИзмерения");
		Результат.Вывести(ОбластьВывода);
	КонецЕсли; 
	
	// Отбор
	ТекстОтбор = Строка(Контекст.НастройкиКомпоновкиДанных.Отбор);
	
	Если Не ПустаяСтрока(ТекстОтбор) Тогда 
		ОбластьОписаниеНастроек = Макет.ПолучитьОбласть("ОписаниеНастроек");
		ОбластьОписаниеНастроек.Параметры.ИмяНастроекОтчета      = НСтр("ru = 'Отбор:'");
		ОбластьОписаниеНастроек.Параметры.ОписаниеНастроекОтчета = ТекстОтбор;
		
		Результат.Вывести(ОбластьОписаниеНастроек);
	КонецЕсли;
	
КонецПроцедуры

Функция ЗаголовокОтчета(Контекст)
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 на %2", Контекст.СинонимОтчета, Контекст.ПредставлениеПериода);

КонецФункции

Функция ПараметрыОткрытияФормыПечатиСправкиПроцентов(ДанныеОтчета, ДокументАренды)
	
	ДокументыАренды = Новый Массив;
	ДокументыАренды.Добавить(ДокументАренды);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ДополнитьКомплектВнешнимиПечатнымиФормами", Ложь);
	ДополнительныеПараметры.Вставить("ДатаРасчетаДолгосрочногоОбязательства", КонецМесяца(ДобавитьМесяц(ДанныеОтчета.Объект.КонецПериодаДата, 12)));
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ЗаголовокФормы", ДокументАренды);
	ПараметрыПечати.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ИменаМакетов", "НачислениеПроцентныхРасходов");
	ПараметрыОткрытия.Вставить("ИмяМенеджераПечати", "Обработка.ГрафикПлатежейПоДоговору");
	ПараметрыОткрытия.Вставить("ПараметрКоманды", ДокументыАренды);
	ПараметрыОткрытия.Вставить("ПараметрыПечати", ПараметрыПечати);
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

#Область ШапкиТаблиц

// Возвращает данные для оформления заголовков таблицы
// Параметры:
//  Контекст  - См. НовыйКонтекстФормированияОтчета
//
// Возвращаемое значение:
//  Соответствие:
//   Ключ - имя Таблицы в структуре настроек Схемы компоновки данных
//   Значение - См. НовыйШапкаТаблицы
//
Функция ОписаниеШапкиТаблиц(Контекст)
	
	ТекстыЗаголовковКолонок = ТекстыЗаголовковКолонок(Контекст);
	
	ОписаниеШапкиТаблиц = Новый Соответствие;
	
	ТекстГрафик          = НСтр("ru = 'По договорам с введенным графиком платежей'");
	ТекстРасходыРуб      = НСтр("ru = 'Расчеты в рублях '");
	ТекстРасходыВал      = НСтр("ru = 'Расчеты в валюте и в условных единицах'");
	ТекстБезГрафика      = НСтр("ru = 'По договорам без введенного графика платежей'");
	ТекстИтогоПоказатели = НСтр("ru = 'Итого показатели баланса по аренде'");
	
	ШрифтРаздела    = Новый Шрифт(, 11, Истина);
	ШрифтПодраздела = Новый Шрифт(, 11, Ложь);
	Отступ = 5;
	
	// таблица с графиком платежей в рублях
	// для заголовка раздела добавляем отступ сверху и снизу строки
	ЗаголовокРаздела = НовыйЗаголовокТаблицы();
	ЗаголовокРаздела.Текст = ТекстГрафик;
	ЗаголовокРаздела.ПараметрыОформления.Вставить("Шрифт", ШрифтРаздела);
	ЗаголовокРаздела.ПараметрыОформления.Вставить("ОтступСверху", Отступ);
	ЗаголовокРаздела.ПараметрыОформления.Вставить("ОтступСнизу",  Отступ);

	// для заголовка подраздела добавляем отступ только снизу строки
	ЗаголовокПодраздела = НовыйЗаголовокТаблицы();
	ЗаголовокПодраздела.Текст = ТекстРасходыРуб;
	ЗаголовокПодраздела.ПараметрыОформления.Вставить("Шрифт", ШрифтПодраздела);
	ЗаголовокПодраздела.ПараметрыОформления.Вставить("ОтступСнизу", Отступ);
	
	ШапкаТаблицы = НовыйШапкаТаблицы();
	ШапкаТаблицы.Графы               = ГрафыТаблицыГрафикРуб();
	ШапкаТаблицы.ЗаголовкиКолонок    = ЗаголовкиКолонокТаблицыГрафикРуб(ТекстыЗаголовковКолонок);
	ШапкаТаблицы.ЗаголовокРаздела    = ЗаголовокРаздела;
	ШапкаТаблицы.ЗаголовокПодраздела = ЗаголовокПодраздела;
	ШапкаТаблицы.Использование       = Контекст.ЕстьДисконтирование;
	ОписаниеШапкиТаблиц.Вставить("ГрафикРуб", ШапкаТаблицы);
	
	// таблица с графиком платежей в валюте и условных единицах
	ЗаголовокПодраздела = НовыйЗаголовокТаблицы();
	ЗаголовокПодраздела.Текст = ТекстРасходыВал;
	ЗаголовокПодраздела.ПараметрыОформления.Вставить("Шрифт", ШрифтПодраздела);
	ЗаголовокПодраздела.ПараметрыОформления.Вставить("ОтступСнизу", Отступ);
	
	ШапкаТаблицы = НовыйШапкаТаблицы();
	ШапкаТаблицы.Графы               = ГрафыТаблицыГрафикВал();
	ШапкаТаблицы.ЗаголовкиКолонок    = ЗаголовкиКолонокТаблицыГрафикВал(ТекстыЗаголовковКолонок);
	ШапкаТаблицы.ЗаголовокПодраздела = ЗаголовокПодраздела;
	ШапкаТаблицы.Использование       = Контекст.ЕстьДисконтирование И Контекст.ЕстьРасчетыВВалюте;
	ОписаниеШапкиТаблиц.Вставить("ГрафикВал", ШапкаТаблицы);
	
	// таблица без графика
	ЗаголовокРаздела = НовыйЗаголовокТаблицы();
	ЗаголовокРаздела.Текст = ТекстБезГрафика;
	ЗаголовокРаздела.ПараметрыОформления.Вставить("Шрифт", ШрифтРаздела);
	ЗаголовокРаздела.ПараметрыОформления.Вставить("ОтступСверху", Отступ);
	ЗаголовокРаздела.ПараметрыОформления.Вставить("ОтступСнизу",  Отступ);
	
	ШапкаТаблицы = НовыйШапкаТаблицы();
	ШапкаТаблицы.Графы                = ГрафыТаблицыБезГрафика();
	ШапкаТаблицы.ЗаголовкиКолонок     = ЗаголовкиКолонокТаблицыБезГрафика(ТекстыЗаголовковКолонок);
	// Если Не ЕстьДисконтирование, тогда выводится в табличный документ только Таблица компоновки данных "БезГрафика" и "ИтогоПоказатели"
	// поэтому смысла нет выводить заголовок раздела
	Если Контекст.ЕстьДисконтирование Тогда
		ШапкаТаблицы.ЗаголовокРаздела = ЗаголовокРаздела;
	КонецЕсли;
	ШапкаТаблицы.Использование        = Истина;
	ОписаниеШапкиТаблиц.Вставить("БезГрафика", ШапкаТаблицы);
	
	// таблица Итого
	ЗаголовокРаздела = НовыйЗаголовокТаблицы();
	ЗаголовокРаздела.Текст = ТекстИтогоПоказатели;
	ЗаголовокРаздела.ПараметрыОформления.Вставить("Шрифт", ШрифтРаздела);
	ЗаголовокРаздела.ПараметрыОформления.Вставить("ОтступСверху", Отступ);
	ЗаголовокРаздела.ПараметрыОформления.Вставить("ОтступСнизу",  Отступ);
	
	ШапкаТаблицы = НовыйШапкаТаблицы();
	ШапкаТаблицы.ЗаголовкиКолонок     = ЗаголовкиКолонокТаблицыИтогоПоказатели(ТекстыЗаголовковКолонок);
	ШапкаТаблицы.ЗаголовокРаздела     = ЗаголовокРаздела;
	ШапкаТаблицы.Использование        = Истина;
	ОписаниеШапкиТаблиц.Вставить("ИтогоПоказатели", ШапкаТаблицы);
	
	Возврат ОписаниеШапкиТаблиц;
	
КонецФункции

Функция НовыйШапкаТаблицы()

	Шапка = Новый Структура;
	Шапка.Вставить("Макеты");
	Шапка.Вставить("ЗаголовкиКолонок");
	Шапка.Вставить("Графы");
	Шапка.Вставить("ЗаголовокРаздела"); // см. НовыйЗаголовок()
	Шапка.Вставить("ЗаголовокПодраздела"); // см. НовыйЗаголовок()
	Шапка.Вставить("Использование", Ложь);
	
	Возврат Шапка;
	
КонецФункции

Функция НовыйЗаголовокТаблицы()

	Заголовок = Новый Структура;
	Заголовок.Вставить("Текст");
	Заголовок.Вставить("ПараметрыОформления", Новый Соответствие);

	Возврат Заголовок;
	
КонецФункции

#Область МакетыЗаголовков

Процедура ДобавитьМакетыТаблиц(ОписаниеШапкиТаблиц, МакетКомпоновки)
	
	Для Каждого Тело Из МакетКомпоновки.Тело Цикл
		
		// Обходим элементы, выводимые на верхнем уровне.
		// Макеты каждой таблицы храним списком, для дальнешей обработки заголовков и добавлением граф
		Если ТипЗнч(Тело) = Тип("ТаблицаМакетаКомпоновкиДанных") Тогда
			
			Макеты = Новый Соответствие;
			Если ЗначениеЗаполнено(Тело.МакетШапки) Тогда
				Макеты.Вставить(Тело.МакетШапки, ИмяГруппировка());
			КонецЕсли;
			Если ЗначениеЗаполнено(Тело.МакетИтоговСтрок) Тогда
				Макеты.Вставить(Тело.МакетИтоговСтрок, ИмяРесурсы());
			КонецЕсли;
			Если ЗначениеЗаполнено(Тело.Строки) Тогда
				ИмяМакетаПодвала = Тело.Строки[0].МакетПодвала.Макет;
				Если ЗначениеЗаполнено(ИмяМакетаПодвала) Тогда
					Макеты.Вставить(ИмяМакетаПодвала, ИмяИтогиГруппировки());
				КонецЕсли;
			КонецЕсли;
			
			ОписаниеШапки = ОписаниеШапкиТаблиц[Тело.Имя];
			Если ОписаниеШапки <> Неопределено Тогда
				ОписаниеШапки.Макеты = Макеты;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьМакетыТаблиц(МакетКомпоновки, ОписаниеШапки)
	
	Для Каждого ОписаниеМакетаОбласти Из ОписаниеШапки.Макеты Цикл
		
		ОписаниеМакета = МакетКомпоновки.Макеты.Найти(ОписаниеМакетаОбласти.Ключ);
		
		Если ОписаниеМакета = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// В табличном документе в шапке таблицы формируется первой строкой заголовок "Итого", группирующий поля ресурсов
		// Так как в шапке он не нужен, то удаляем первый элемент из МакетШапки и МакетИтоговСтрок,
		// чтоб не было смещения по колонкам группировки и ресурсов
		Если ОписаниеМакетаОбласти.Значение <> ИмяИтогиГруппировки() Тогда
			ОписаниеМакета.Макет.Удалить(0);
		КонецЕсли;
		
		//Центрируем заголовки ресурсов во всех ячейках шапки
		Если ОписаниеМакетаОбласти.Значение = ИмяРесурсы() Тогда
			Для Каждого Строка Из ОписаниеМакета.Макет Цикл
				Для Каждого Ячейка Из Строка.Ячейки Цикл
					Ячейка.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Центр);
					Ячейка.Оформление.УстановитьЗначениеПараметра("ВертикальноеПоложение",   ВертикальноеПоложение.Верх);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		ЭталоннаяСтрока = ОписаниеМакета.Макет[0];
		
		Если ОписаниеМакетаОбласти.Значение <> ИмяИтогиГруппировки() Тогда
			// Добавляем новые строки с заголовками Таблиц для вывода в табличный документ
			ДобавитьСтрокуНадТаблицей(ОписаниеМакетаОбласти, ОписаниеМакета, ОписаниеШапки.ЗаголовокПодраздела, ЭталоннаяСтрока);
			ДобавитьСтрокуНадТаблицей(ОписаниеМакетаОбласти, ОписаниеМакета, ОписаниеШапки.ЗаголовокРаздела, ЭталоннаяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьСтрокуНадТаблицей(ОписаниеМакетаОбласти, ОписаниеМакета, Заголовок, ЭталоннаяСтрока)
	
	Если Заголовок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = ОписаниеМакета.Макет.Вставить(0,Тип("СтрокаТаблицыОбластиКомпоновкиДанных"));
	
	НоваяСтрока.ИдентификаторТаблицы = ЭталоннаяСтрока.ИдентификаторТаблицы;
	Если ОписаниеМакетаОбласти.Значение <> ИмяРесурсы() Тогда
		
		НоваяЯчейка = НоваяСтрока.Ячейки.Добавить();
		Поле = НоваяЯчейка.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
		Поле.Значение = Заголовок.Текст;
		
		Для Каждого Параметр Из Заголовок.ПараметрыОформления Цикл
			НоваяЯчейка.Оформление.УстановитьЗначениеПараметра(
				Параметр.Ключ,
				Параметр.Значение);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОписаниеКолонок

Функция НовыйЗаголовкиКолонок()

	ЗаголовкиКолонок = Новый Структура;
	ЗаголовкиКолонок.Вставить("Группы"); //ГруппаВыбранныхПолейКомпоновкиДанных
	ЗаголовкиКолонок.Вставить("Поля"); //ВыбранноеПолеКомпоновкиДанных
	
	Возврат ЗаголовкиКолонок;
	
КонецФункции

Функция ТекстыЗаголовковКолонок(Контекст)
	
	НДСпоАвансам       = ПланыСчетов.Хозрасчетный.НДСпоАвансамИПредоплатамВыданным; // 76.ВА
	РасчетыПоАренде    = ПланыСчетов.Хозрасчетный.РасчетыПоАренде;    //76.07
	РасчетыПоАрендеВал = ПланыСчетов.Хозрасчетный.РасчетыПоАрендеВал; // 76.27
	РасчетыПоАрендеУЕ  = ПланыСчетов.Хозрасчетный.РасчетыПоАрендеУЕ;  // 76.37
	АрендныеПлатежи    = ПланыСчетов.Хозрасчетный.АрендныеПлатежи;    // 76.07.2
	АрендныеПлатежиВал = ПланыСчетов.Хозрасчетный.АрендныеПлатежиВал; // 76.27.2
	АрендныеПлатежиУЕ  = ПланыСчетов.Хозрасчетный.АрендныеПлатежиУЕ;  // 76.37.2
	
	Счета = Новый Массив;
	Счета.Добавить(НДСпоАвансам);
	Счета.Добавить(РасчетыПоАренде);
	Счета.Добавить(РасчетыПоАрендеВал);
	Счета.Добавить(РасчетыПоАрендеУЕ);
	Счета.Добавить(АрендныеПлатежи);
	Счета.Добавить(АрендныеПлатежиВал);
	Счета.Добавить(АрендныеПлатежиУЕ);
	
	СчетаСКодами = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Счета, "Код");
	
	Разделитель = ", ";
	
	СчетаАренды = Новый Массив;
	СчетаАренды.Добавить(СчетаСКодами[РасчетыПоАренде].Код);
	
	СчетаАрендныхПлатежей = Новый Массив;
	СчетаАрендныхПлатежей.Добавить(СчетаСКодами[АрендныеПлатежи].Код);

	Если Контекст.ИспользуетсяВалютныйУчет Тогда 
		СчетаАренды.Добавить(СчетаСКодами[РасчетыПоАрендеВал].Код);
		СчетаАренды.Добавить(СчетаСКодами[РасчетыПоАрендеУЕ].Код);
		СчетаАрендныхПлатежей.Добавить(СчетаСКодами[АрендныеПлатежиВал].Код);
		СчетаАрендныхПлатежей.Добавить(СчетаСКодами[АрендныеПлатежиУЕ].Код);
	КонецЕсли;
	
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("Период", Формат(КонецМесяца(ДобавитьМесяц(Контекст.КонецПериодаДата, 12)),"ДЛФ=D"));
	ПараметрыТекста.Вставить("СчетНДС", СчетаСКодами[НДСпоАвансам].Код);
	ПараметрыТекста.Вставить("СчетаАренды", СтрСоединить(СчетаАренды, Разделитель));
	ПараметрыТекста.Вставить("СчетаАрендныхПлатежей", СтрСоединить(СчетаАрендныхПлатежей, Разделитель));
	
	ТекстыЗаголовков = Новый Структура;
	ТекстыЗаголовков.Вставить("ОстатокПоАрендеДт",      СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
												НСтр("ru = 'Сальдо по дебету [СчетаАренды]'"), 
												ПараметрыТекста));
	ТекстыЗаголовков.Вставить("ОстатокПоАрендеКт",      СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
												НСтр("ru = 'Сальдо по кредиту [СчетаАренды]'"), 
												ПараметрыТекста));
	ТекстыЗаголовков.Вставить("ОстатокПлатежей",        СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
												НСтр("ru = 'Сальдо по дебету [СчетаАрендныхПлатежей]'"),
												ПараметрыТекста));
	ТекстыЗаголовков.Вставить("НДС",                    СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
												НСтр("ru = 'в т.ч. НДС - 
													|Сальдо по кредиту [СчетНДС]'"), 
												ПараметрыТекста));
	ТекстыЗаголовков.Вставить("РасчетноеОбязательство", СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
												НСтр("ru = 'Расчетное обязательство через 12 мес.
													|(по графику процентных расходов на дату,
													|следующую за [Период])'"),
												ПараметрыТекста));
	ТекстыЗаголовков.Вставить("АвансыПоАренде",         НСтр("ru = 'Авансы по аренде'"));
	ТекстыЗаголовков.Вставить("Долгосрочное",           НСтр("ru = 'Долгосрочное обязательство по аренде'"));
	ТекстыЗаголовков.Вставить("Краткосрочное",          НСтр("ru = 'Краткосрочное обязательство по аренде'"));
	ТекстыЗаголовков.Вставить("Обязательство",          НСтр("ru = 'Обязательство'"));
	ТекстыЗаголовков.Вставить("ОбязательствоРуб12",     НСтр("ru = 'В рублях'"));
	ТекстыЗаголовков.Вставить("ОбязательствоВал12",     НСтр("ru = 'В валюте'"));
	ТекстыЗаголовков.Вставить("Валюта",                 НСтр("ru = 'Вал.'"));
	ТекстыЗаголовков.Вставить("Курс",                   НСтр("ru = 'Курс валюты'"));
	
	Возврат ТекстыЗаголовков;
	
КонецФункции

Функция ЗаголовкиКолонокТаблицыГрафикРуб(ТекстыЗаголовков)
	
	ЗаголовкиКолонок = НовыйЗаголовкиКолонок();
	
	// графы колонок - см. ГрафыТаблицыГрафикРуб()
	Формулы = Новый Структура;
	Формулы.Вставить("Обязательство",              НСтр("ru = '(гр.5 - гр.4 + гр.7,
														|если результат больше нуля)'"));
	Формулы.Вставить("АвансыПоАренде",             НСтр("ru = '(гр.4 - гр.5 - гр.7,
														|если результат больше нуля)'"));
	Формулы.Вставить("Долгосрочное",  НСтр("ru = '(мин(гр. 8; гр.9))'"));
	Формулы.Вставить("Краткосрочное", НСтр("ru = '(гр.8 - гр.11)'"));
	
	ТекстыСФормулами = ТекстыСФормулами(ТекстыЗаголовков, Формулы);
	
	// поля
	Поля = Новый Структура;
	Поля.Вставить("ОстатокПоАрендеДт", ТекстыСФормулами.ОстатокПоАрендеДт);
	Поля.Вставить("ОстатокПоАрендеКт", ТекстыСФормулами.ОстатокПоАрендеКт);
	Поля.Вставить("ОстатокПлатежей",   ТекстыСФормулами.ОстатокПлатежей);
	Поля.Вставить("НДСсПлатежей",      ТекстыСФормулами.НДС);
	Поля.Вставить("ОбязательствоГрафикРуб12", ТекстыСФормулами.РасчетноеОбязательство);
	Поля.Вставить("Обязательство",     ТекстыСФормулами.Обязательство);
	Поля.Вставить("АвансыПоАренде",    ТекстыСФормулами.АвансыПоАренде);
	Поля.Вставить("ДолгосрочноеОбязательство",ТекстыСФормулами.Долгосрочное);
	Поля.Вставить("КраткосрочноеОбязательство", ТекстыСФормулами.Краткосрочное);

	ЗаголовкиКолонок.Поля = Поля;
	
	Возврат ЗаголовкиКолонок;

КонецФункции

Функция ЗаголовкиКолонокТаблицыГрафикВал(ТекстыЗаголовков)
	
	ЗаголовкиКолонок = НовыйЗаголовкиКолонок();
	
	// графы колонок - см. ГрафыТаблицыГрафикВал()
	Формулы = Новый Структура;
	Формулы.Вставить("Обязательство",      НСтр("ru = '(гр.5 - гр.4 + гр.7,
												|если результат больше нуля)'"));
	Формулы.Вставить("ОбязательствоРуб12", НСтр("ru = '(гр. 10 * гр.11)'"));
	Формулы.Вставить("АвансыПоАренде",     НСтр("ru = '(гр.4 - гр.5 - гр.7,
												|если результат больше нуля)'"));
	Формулы.Вставить("Долгосрочное",  НСтр("ru = '(мин(гр. 8; гр.12))'"));
	Формулы.Вставить("Краткосрочное", НСтр("ru = '(гр.8 - гр.14)'"));
	
	ТекстыСФормулами = ТекстыСФормулами(ТекстыЗаголовков, Формулы);
	
	// поля
	Поля = Новый Структура;
	Поля.Вставить("ОстатокПоАрендеДт", ТекстыСФормулами.ОстатокПоАрендеДт);
	Поля.Вставить("ОстатокПоАрендеКт", ТекстыСФормулами.ОстатокПоАрендеКт);
	Поля.Вставить("ОстатокПлатежей",   ТекстыСФормулами.ОстатокПлатежей);
	Поля.Вставить("НДСсПлатежей",      ТекстыСФормулами.НДС);
	Поля.Вставить("ВалютаГрафик",      ТекстыСФормулами.Валюта);
	Поля.Вставить("КурсГрафик",        ТекстыСФормулами.Курс);
	Поля.Вставить("ОбязательствоГрафик12",    ТекстыСФормулами.ОбязательствоВал12);
	Поля.Вставить("ОбязательствоГрафикРуб12", ТекстыСФормулами.ОбязательствоРуб12);
	Поля.Вставить("Обязательство",     ТекстыСФормулами.Обязательство);
	Поля.Вставить("АвансыПоАренде",    ТекстыСФормулами.АвансыПоАренде);
	Поля.Вставить("ДолгосрочноеОбязательство",ТекстыСФормулами.Долгосрочное);
	Поля.Вставить("КраткосрочноеОбязательство", ТекстыСФормулами.Краткосрочное);
	ЗаголовкиКолонок.Поля = Поля;
	
	// группы
	Группы = Новый Массив;
	
	// Расчетное обязательство
	ОписаниеПоляГруппы = Новый Структура;
	ОписаниеПоляГруппы.Вставить("Заголовок", ТекстыСФормулами.РасчетноеОбязательство);
	ОписаниеПоляГруппы.Вставить("ПолеГруппы", "ВалютаГрафик");
	Группы.Добавить(ОписаниеПоляГруппы);
	
	ЗаголовкиКолонок.Группы = Группы;

	Возврат ЗаголовкиКолонок;

КонецФункции

Функция ЗаголовкиКолонокТаблицыБезГрафика(ТекстыЗаголовков)
	
	ЗаголовкиКолонок = НовыйЗаголовкиКолонок();
	
	// графы колонок - см. ГрафыТаблицыБезГрафика()
	Формулы = Новый Структура;
	Формулы.Вставить("Обязательство",  НСтр("ru = '(гр.6 - гр.5 + гр.8,
											|если результат больше нуля)'"));
	Формулы.Вставить("АвансыПоАренде", НСтр("ru = '(гр.5 - гр.6 - гр.8,
											|если результат больше нуля)'"));
	Формулы.Вставить("Долгосрочное",   НСтр("ru = '(гр. 9, если 
											|в гр. 4 ""Да"")'"));
	Формулы.Вставить("Краткосрочное",  НСтр("ru = '(гр. 9, если 
											|в гр. 4 ""Нет"")'"));
	
	ТекстыСФормулами = ТекстыСФормулами(ТекстыЗаголовков, Формулы);

	// поля
	Поля = Новый Структура;
	Поля.Вставить("ОстатокПоАрендеДт", ТекстыСФормулами.ОстатокПоАрендеДт);
	Поля.Вставить("ОстатокПоАрендеКт", ТекстыСФормулами.ОстатокПоАрендеКт);
	Поля.Вставить("ОстатокПлатежей",   ТекстыСФормулами.ОстатокПлатежей);
	Поля.Вставить("НДСсПлатежей",      ТекстыСФормулами.НДС);
	Поля.Вставить("Обязательство",     ТекстыСФормулами.Обязательство);
	Поля.Вставить("АвансыПоАренде",    ТекстыСФормулами.АвансыПоАренде);
	Поля.Вставить("ДолгосрочноеОбязательство",  ТекстыСФормулами.Долгосрочное);
	Поля.Вставить("КраткосрочноеОбязательство", ТекстыСФормулами.Краткосрочное);

	ЗаголовкиКолонок.Поля = Поля;
		
	Возврат ЗаголовкиКолонок;
	
КонецФункции

Функция ЗаголовкиКолонокТаблицыИтогоПоказатели(ТекстыЗаголовков)
	
	ЗаголовкиКолонок = НовыйЗаголовкиКолонок();
	
	// поля
	Поля = Новый Структура;  
	Поля.Вставить("ОстатокПлатежей",   ТекстыЗаголовков.ОстатокПлатежей);
	Поля.Вставить("НДСсПлатежей",      ТекстыЗаголовков.НДС);
	Поля.Вставить("АвансыПоАренде",    ТекстыЗаголовков.АвансыПоАренде);
	Поля.Вставить("ДолгосрочноеОбязательство",  ТекстыЗаголовков.Долгосрочное);
	Поля.Вставить("КраткосрочноеОбязательство", ТекстыЗаголовков.Краткосрочное);

	ЗаголовкиКолонок.Поля = Поля;
	
	Возврат ЗаголовкиКолонок;

КонецФункции

// Дополняем тексты заголовков формулами
//
Функция ТекстыСФормулами(ТекстыЗаголовков, Формулы)
	
	Тексты = Новый Структура;
	Для Каждого ТекстЗаголовка Из ТекстыЗаголовков Цикл
		ЧастиЗаголовка = Новый Массив;
		ЧастиЗаголовка.Добавить(ТекстЗаголовка.Значение);
		Если Формулы.Свойство(ТекстЗаголовка.Ключ) Тогда
			ЧастиЗаголовка.Добавить(Формулы[ТекстЗаголовка.Ключ]);
		КонецЕсли;
		Тексты.Вставить(ТекстЗаголовка.Ключ, СтрСоединить(ЧастиЗаголовка, Символы.ПС));
	КонецЦикла;
	
	Возврат Тексты;
	
КонецФункции

Процедура ИзменитьСтруктуруОтчета(КомпоновщикНастроек, ОписаниеШапкиТаблиц)
	
	ТаблицыУдаления = Новый Массив;
	
	// Ключ ОписаниеШапкиТаблиц - имя Таблицы в структуре настроек Схемы компоновки данных
	Для Каждого Таблица Из КомпоновщикНастроек.Настройки.Структура Цикл
		
		Если ТипЗнч(Таблица) <> Тип("ТаблицаКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеШапки = ОписаниеШапкиТаблиц[Таблица.Имя];
		Если ОписаниеШапки = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		Таблица.Использование = ОписаниеШапки.Использование;
		
		Если Не Таблица.Использование Тогда
			ТаблицыУдаления.Добавить(Таблица);
			Продолжить;
		КонецЕсли;
		
		Если ОписаниеШапки.ЗаголовкиКолонок = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаголовкиПолей = ОписаниеШапки.ЗаголовкиКолонок.Поля;
		ЗаголовкиГрупп = ОписаниеШапки.ЗаголовкиКолонок.Группы;
		
		УстановитьЗаголовкиКолонок(Таблица.Строки, ЗаголовкиГрупп, ЗаголовкиПолей);
		
	КонецЦикла;
	
	// удалим из структуры настроек схемы компоновки неиспользуемые таблицы
	Для Каждого Таблица Из ТаблицыУдаления Цикл
		КомпоновщикНастроек.Настройки.Структура.Удалить(Таблица);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьЗаголовкиКолонок(ЭлементыТаблицы, ЗаголовкиГрупп, ЗаголовкиПолей)
	
	Для Каждого ЭлементТаблицы Из ЭлементыТаблицы Цикл
		
		Для Каждого Поле Из ЭлементТаблицы.Выбор.Элементы Цикл
			
			Если Не Поле.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(Поле) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
				Для Каждого ПолеЭлемента Из Поле.Элементы Цикл
					УстановитьЗаголовокПоля(ПолеЭлемента, ЗаголовкиПолей);
				КонецЦикла;
				
				УстановитьЗаголовокГруппы(Поле, ЗаголовкиГрупп);
			КонецЕсли;
			
			УстановитьЗаголовокПоля(Поле, ЗаголовкиПолей);
			
		КонецЦикла;
		
		// Используется рекурсия для случая:
		// Если в группировке и в детальных записях одинаковый набор полей компоновки данных объединен в группы
		// и эти поля должны находиться в колонках друг под другом, тогда таким группам устанавливается одинаковый заголовок.
		// В ином случае группа детальных записей пристыковывается справа в табличном документе.
		Если ЗначениеЗаполнено(ЭлементТаблицы.Структура)Тогда
			УстановитьЗаголовкиКолонок(ЭлементТаблицы.Структура, ЗаголовкиГрупп, ЗаголовкиПолей);
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

Процедура УстановитьЗаголовокГруппы(ПолеГруппы, ЗаголовкиГрупп)
	
	Если ЗаголовкиГрупп = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из ЗаголовкиГрупп Цикл
		Для Каждого ПолеЭлемента Из ПолеГруппы.Элементы Цикл
			Если Строка(ПолеЭлемента.Поле) = Элемент.ПолеГруппы Тогда
				ПолеГруппы.Заголовок = Элемент.Заголовок;
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьЗаголовокПоля(ПолеЭлемента, ИменаПолей)
	
	Если  ТипЗнч(ПолеЭлемента) = Тип("ВыбранноеПолеКомпоновкиДанных")Тогда
		ИмяПоля = СтрЗаменить(Строка(ПолеЭлемента.Поле), ".", "");
		Если ИменаПолей.Свойство(ИмяПоля) Тогда
			ПолеЭлемента.Заголовок = ИменаПолей[ИмяПоля];
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Графы

Функция ГрафыТаблицыГрафикРуб()
	
	Разделы = Новый Соответствие;
	
	// Заголовки группировок
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '1'"));  // Контрагент
	ГрафыРаздела.Добавить(НСтр("ru = '2'"));  // ДоговорКонтрагента
	ГрафыРаздела.Добавить(НСтр("ru = '3'"));  // ДокументАренды

	Разделы.Вставить(
		ИмяГруппировка(),
		ГрафыРаздела);
		
	//Заголовки ресурсов
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '4'"));  // ОстатокПоАрендеДт
	ГрафыРаздела.Добавить(НСтр("ru = '5'"));  // ОстатокПоАрендеКт
	ГрафыРаздела.Добавить(НСтр("ru = '6'"));  // ОстатокПлатежей
	ГрафыРаздела.Добавить(НСтр("ru = '7'"));  // НДСсПлатежей
	ГрафыРаздела.Добавить(НСтр("ru = '8'"));  // Обязательство
	ГрафыРаздела.Добавить(НСтр("ru = '9'"));  // ОбязательствоГрафикРуб12
	ГрафыРаздела.Добавить(НСтр("ru = '10'")); // АвансыПоАренде
	ГрафыРаздела.Добавить(НСтр("ru = '11'")); // ДолгосрочноеОбязательство
	ГрафыРаздела.Добавить(НСтр("ru = '12'")); // КраткосрочноеОбязательство
	
	Разделы.Вставить(
		ИмяРесурсы(),
		ГрафыРаздела);
		
	Возврат Разделы;
	
КонецФункции

Функция ГрафыТаблицыГрафикВал()
	
	Разделы = Новый Соответствие;
	
	// Заголовки группировок
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '1'"));  // Контрагент
	ГрафыРаздела.Добавить(НСтр("ru = '2'"));  // ДоговорКонтрагента
	ГрафыРаздела.Добавить(НСтр("ru = '3'"));  // ДокументАренды

	Разделы.Вставить(
		ИмяГруппировка(),
		ГрафыРаздела);
		
	//Заголовки ресурсов
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '4'"));  // ОстатокПоАрендеДт
	ГрафыРаздела.Добавить(НСтр("ru = '5'"));  // ОстатокПоАрендеКт
	ГрафыРаздела.Добавить(НСтр("ru = '6'"));  // ОстатокПлатежей
	ГрафыРаздела.Добавить(НСтр("ru = '7'"));  // НДСсПлатежей
	ГрафыРаздела.Добавить(НСтр("ru = '8'"));  // Обязательство
	ГрафыРаздела.Добавить(НСтр("ru = '9'"));  // ВалютаГрафик
	ГрафыРаздела.Добавить(НСтр("ru = '10'")); // КурсГрафик
	ГрафыРаздела.Добавить(НСтр("ru = '11'")); // ОбязательствоГрафик12
	ГрафыРаздела.Добавить(НСтр("ru = '12'")); // ОбязательствоГрафикРуб12
	ГрафыРаздела.Добавить(НСтр("ru = '13'")); // АвансыПоАренде
	ГрафыРаздела.Добавить(НСтр("ru = '14'")); // ДолгосрочноеОбязательство
	ГрафыРаздела.Добавить(НСтр("ru = '15'")); // КраткосрочноеОбязательство
	
	Разделы.Вставить(
		ИмяРесурсы(),
		ГрафыРаздела);
	
	Возврат Разделы;
	
КонецФункции

Функция ГрафыТаблицыБезГрафика()
	
	Разделы = Новый Соответствие;
	
	// Заголовки группировок
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '1'"));  // Контрагент
	ГрафыРаздела.Добавить(НСтр("ru = '2'"));  // ДоговорКонтрагента
	ГрафыРаздела.Добавить(НСтр("ru = '3'"));  // СрокДоговора
	ГрафыРаздела.Добавить(НСтр("ru = '4'"));  // Долгосрочный
	
	Разделы.Вставить(
		ИмяГруппировка(),
		ГрафыРаздела); 
	
	//Заголовки ресурсов
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '5'"));  // ОстатокПоАрендеДт
	ГрафыРаздела.Добавить(НСтр("ru = '6'"));  // ОстатокПоАрендеКт
	ГрафыРаздела.Добавить(НСтр("ru = '7'"));  // ОстатокПлатежей
	ГрафыРаздела.Добавить(НСтр("ru = '8'"));  // НДСсПлатежей
	ГрафыРаздела.Добавить(НСтр("ru = '9'"));  // Обязательство
	ГрафыРаздела.Добавить(НСтр("ru = '10'")); // АвансыПоАренде
	ГрафыРаздела.Добавить(НСтр("ru = '11'")); // ДолгосрочноеОбязательство
	ГрафыРаздела.Добавить(НСтр("ru = '12'")); // КраткосрочноеОбязательство

	Разделы.Вставить(
		ИмяРесурсы(),
		ГрафыРаздела);
	
	Возврат Разделы;
	
КонецФункции

// Используется для соответствия макетов таблиц в структуре настроек Схемы компоновки данных
//
Функция ИмяГруппировка()
	
	Возврат "Группировка";
	
КонецФункции

// Используется для соответствия макетов таблиц в структуре настроек Схемы компоновки данных
//
Функция ИмяРесурсы()
	
	Возврат "Ресурсы";
	
КонецФункции

// Используется для соответствия макетов таблиц в структуре настроек Схемы компоновки данных
//
Функция ИмяИтогиГруппировки()
	
	Возврат "ИтогиГруппировки";
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
