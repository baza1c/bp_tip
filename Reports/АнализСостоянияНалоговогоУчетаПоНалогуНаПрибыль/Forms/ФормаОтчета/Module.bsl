
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыВызовСервера.УстановитьНастройкиПоУмолчанию(ЭтотОбъект);
	
	ДанныеРасшифровки   = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
	КэшПараметровОтчета = ПоместитьВоВременноеХранилище(Новый Структура,   УникальныйИдентификатор);
	
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокОрганизаций(Элементы.ПолеОрганизация, СоответствиеОрганизаций);
	
	НастроитьПоОрганизацииНаСервере();
		
	ЗавершитьРаботуСОтчетом = Ложь;
	
	УправляющаяСтруктура = НоваяУправляющаяСтруктура();
	УправляющаяСтруктура.Главная = Истина;
	ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	БухгалтерскиеОтчетыКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы
	   И НомерТекущейСхемы <> -1
	   И НЕ ЗавершитьРаботуСОтчетом Тогда	
		
		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Завершить работу с отчетом?'"), РежимДиалогаВопрос.ДаНет);
	
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	БухгалтерскиеОтчетыКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки)
	
	Если ИспользуютсяСтандартныеНастройки Тогда
		Возврат;
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
	
	НастроитьПоОрганизацииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	НастроитьПериодНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	НастроитьПериодНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияПриИзменении(
		Элемент,
		ПолеОрганизация,
		Отчет.Организация,
		Отчет.ВключатьОбособленныеПодразделения);
		
	НастроитьПоОрганизацииНаКлиенте();
		
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, 
		СоответствиеОрганизаций, Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоДокументамПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Результат

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		"Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатСхемыВыбор(Элемент)
	
	Если Элемент.ТекущийЭлемент = Неопределено Тогда 		
		Возврат;		
	КонецЕсли;
	
	// собираем одинаковые элементы расшифровки
	Отчет.ИмяРасшифровки = СтрЗаменить(Элемент.ТекущийЭлемент.Имя, "_1", "");
	УправляющаяСтруктура = НоваяУправляющаяСтруктура();
	
	// Убытки прошлых лет расшифровываем иначе. Открываем "Справку-расчет списания убытков прошлых лет"
	Если Найти(Отчет.ИмяРасшифровки, "УбыткиПрошлыхЛет") > 0 Тогда
		
		ПараметрыОтчета = Новый Структура;		
		ПараметрыОтчета.Вставить("НачалоПериода", Отчет.НачалоПериода);
		ПараметрыОтчета.Вставить("КонецПериода", Отчет.КонецПериода);
		ПараметрыОтчета.Вставить("Организация", Отчет.Организация);
		ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
		
		ОткрытьФорму("Отчет.СправкаРасчетУбытковПрошлыхЛет.Форма.ФормаОтчета", ПараметрыОтчета); 
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	РезультатВыполнения = СформироватьСхемуНаСервере(Отчет.ИмяРасшифровки, УправляющаяСтруктура, Отказ);
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	ИначеЕсли Не Отказ И РезультатВыполнения.Статус = "Выполнено" Тогда
		ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОписаниеЗаданияФормированияОтчета, ОповещениеОЗавершении, ПараметрыОжидания);
		
		Если ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
			Если УправляющаяСтруктура.Главная ИЛИ УправляющаяСтруктура.ЭтоСхема Тогда
				Элементы.РезультатСхемы.Видимость      = Ложь;
				Элементы.ИндикаторВыполнения.Видимость = Истина;
				ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "ФормированиеОтчета");
			Иначе
				ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправляющаяСтруктура = НоваяУправляющаяСтруктура();
	РезультатВыполнения  = ОбработатьРасшифровкуНаСервере(Расшифровка, УправляющаяСтруктура);
	
	Если РезультатВыполнения.Статус = "Выполнено" Тогда
		Если РезультатВыполнения.СпособВыполнения = "Схема" Тогда 		
			ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);		
		ИначеЕсли РезультатВыполнения.СпособВыполнения = "ОткрытьОбъект" Тогда		
			ПоказатьЗначение( , УправляющаяСтруктура.ОткрытьОбъект);		
		ИначеЕсли ТипЗнч(Расшифровка) = Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
			БухгалтерскиеОтчетыКлиент.ОбработкаРасшифровкиСтандартногоОтчета(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
		КонецЕсли;
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОписаниеЗаданияФормированияОтчета, ОповещениеОЗавершении, ПараметрыОжидания);
		
		Если ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
			Если УправляющаяСтруктура.Главная
			 Или РезультатВыполнения.СпособВыполнения = "Схема" Тогда
				Элементы.РезультатСхемы.Видимость      = Ложь;
				Элементы.ИндикаторВыполнения.Видимость = Истина;
				ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "ФормированиеОтчета");
			Иначе
				ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	СправкиРасчетыКлиент.НачатьВыборИнтервала(
		ЭтотОбъект,
		Новый ОписаниеОповещения("ВыборПериода", ЭтотОбъект));
	
	// См. далее ВыборПериода()
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьВсюРасшифровку(Команда)
	
	УправляющаяСтруктура = НоваяУправляющаяСтруктура();
	СформироватьРасшифровку(УправляющаяСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТолькоОшибки(Команда)
	
	УправляющаяСтруктура = НоваяУправляющаяСтруктура();
	УправляющаяСтруктура.ТолькоОшибки = Истина;
	СформироватьРасшифровку(УправляющаяСтруктура);

КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	УправляющаяСтруктура = НоваяУправляющаяСтруктура();
	УправляющаяСтруктура.Главная = Истина;
	
	ОбработатьНазадНаСервере(УправляющаяСтруктура);
	ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСтруктураНалоговойБазы(Команда)
	
	ПоказатьСтруктуруНалоговойБазы();
	УправляющаяСтруктура = НоваяУправляющаяСтруктура();
	УправляющаяСтруктура.Главная = Истина;
	ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериода(РезультатВыбора, НеиспользуемыйОбязательныйПараметр) Экспорт // обработчик оповещения
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отчет.НачалоПериода = РезультатВыбора.НачалоПериода;
	Отчет.КонецПериода  = РезультатВыбора.КонецПериода;
	
	НастроитьПериодНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасшифровку(УправляющаяСтруктура)
	
	ОчиститьСообщения();
	Отказ = Ложь;
	
	РезультатВыполнения = ОбновитьТаблицуНаСервере(УправляющаяСтруктура, Отказ);
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	ИначеЕсли Не Отказ И РезультатВыполнения.Статус = "Выполнено" Тогда
		ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);	
	Иначе
		ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ОписаниеЗаданияФормированияОтчета, ОповещениеОЗавершении, ПараметрыОжидания);
		
		Если ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере(ПараметрыОтчета, Расшифровка = Ложь)
	
	РезультатВыполнения = Новый Структура("Статус", "Выполнено");
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	БухгалтерскиеОтчеты.ОтменитьВыполнениеЗадания(ЭтотОбъект);
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Если Расшифровка Тогда 
			Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.СформироватьРасшифровку(ПараметрыОтчета, АдресХранилища);
		Иначе
			Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.СформироватьОтчет(ПараметрыОтчета, АдресХранилища);
		КонецЕсли;
	Иначе
		ШаблонМетода = "Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.%1";
		Если Расшифровка Тогда 
			ИмяМетода = СтрШаблон(ШаблонМетода, "СформироватьРасшифровку");
		Иначе
			ИмяМетода = СтрШаблон(ШаблонМетода, "СформироватьОтчет");
		КонецЕсли;
		
		ПараметрыЗадания = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
		ПараметрыЗадания.ЗапуститьВФоне = Истина;
		ПараметрыЗадания.НаименованиеФоновогоЗадания =
			БухгалтерскиеОтчетыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтотОбъект);
		
		ОписаниеЗаданияФормированияОтчета = ДлительныеОперации.ВыполнитьПроцедуру(
			ПараметрыЗадания, ИмяМетода, ПараметрыОтчета, АдресХранилища);
		
		РезультатВыполнения.Статус = "Выполняется";
	КонецЕсли;
	
	Если РезультатВыполнения.Статус = "Выполнено" Тогда
		ЗагрузитьПодготовленныеДанныеНаСервере();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьПодготовленныеДанныеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере()

	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		
		Если РезультатВыполнения.Свойство("РезультатСхемы") Тогда
			
			// Результат СформироватьОтчет
			
			РезультатСхемы      = РезультатВыполнения.РезультатСхемы;
			КэшПараметровОтчета = ПоместитьВоВременноеХранилище(РезультатВыполнения.КэшПараметровОтчета, КэшПараметровОтчета);
			Результат           = РезультатВыполнения.Результат;
			
			ДополнитьКешОтображенныхСтраниц(РезультатСхемы, РезультатВыполнения.ИдентификаторСтраницы);
			
		Иначе
			
			// Результат СформироватьРасшифровку
			
			Результат = РезультатВыполнения.Результат;	
			
			ДополнитьКешОтображенныхСтраниц(Результат, РезультатВыполнения.ИдентификаторСтраницы);
			
			Если РезультатВыполнения.Свойство("ДанныеРасшифровки") Тогда
				ДанныеРасшифровки = РезультатВыполнения.ДанныеРасшифровки;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеЗаданияФормированияОтчета = Неопределено;
	
	Если Элементы.ИндикаторВыполнения.Видимость Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
		Элементы.РезультатСхемы.Видимость      = Истина;
		Элементы.ИндикаторВыполнения.Видимость = Ложь;
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыОтчета(ТолькоОшибки)
	
	НачалоНалоговогоПериода = НалоговыйУчет.НачалоНалоговогоПериода(Отчет.КонецПериода, Отчет.Организация);
	Если НачалоНалоговогоПериода = Неопределено Тогда
		НачалоНалоговогоПериода = Отчет.НачалоПериода;
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация",                       Отчет.Организация);
	ПараметрыОтчета.Вставить("НачалоПериода",                     Отчет.НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода",                      КонецДня(Отчет.КонецПериода));
	ПараметрыОтчета.Вставить("НачалоГода",                        НачалоНалоговогоПериода);
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Отчет.ВключатьОбособленныеПодразделения);
    ПараметрыОтчета.Вставить("ОрганизацияРасшифровки",            Отчет.ОрганизацияРасшифровки);
	ПараметрыОтчета.Вставить("РазбитьПоЛистам",                   Отчет.ИмяРасшифровки);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета",               БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(ЭтотОбъект));
	ПараметрыОтчета.Вставить("ДанныеРасшифровки",                 ДанныеРасшифровки);
	ПараметрыОтчета.Вставить("ИмяСхемы",                          ИмяСхемы);
	ПараметрыОтчета.Вставить("ТолькоОшибки",                      ТолькоОшибки);
	ПараметрыОтчета.Вставить("СуффиксСхемы",                      ДополнениеИмениСхемыВариантомИнтерфейса());
	
	ПараметрыОтчета.Вставить("ИдентификаторФормы",                УникальныйИдентификатор);
	
	ПараметрыОтчета.Вставить("ПредставлениеОрганизации",          ПредставлениеОрганизации);
	ПараметрыОтчета.Вставить("ПредставлениеПериода",              ПредставлениеПериодаОтчета);
	ПараметрыОтчета.Вставить("ИнформацияНалоговыйПериод",         "");
	
	ИнформацияНалоговыйПериод = НалоговыйУчет.ИнформацияНалоговыйПериод(Отчет.КонецПериода, Отчет.Организация);
	Если ИнформацияНалоговыйПериод <> Неопределено Тогда
		ПараметрыОтчета.ИнформацияНалоговыйПериод = ИнформацияНалоговыйПериод.Заголовок;
	КонецЕсли;
		
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Процедура ПоказатьСтруктуруНалоговойБазы()
	
	НомерТекущейСхемы = - 1;
	
	Если ПустаяСтрока(ИмяСхемы) Тогда
		ИмяСхемы = ИмяКарты(Отчет.НачалоПериода, Отчет.КонецПериода, Отчет.Организация);
	КонецЕсли;
    РезультатСхемы = Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.ПолучитьМакет(ИмяСхемы);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	// Очистим историю
	
	ПараметрыСеансаРаботыОтчета = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	ОчиститьКешОтображенныхСтраниц();
	КэшПараметровОтчета = ПоместитьВоВременноеХранилище(Новый Структура, КэшПараметровОтчета);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьНазадНаСервере(УправляющаяСтруктура)
	
	ЭтоРасшифровка = НомерТекущейСхемы = 100; 
	
	КэшПараметровОтчетаСтруктура = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	
	ЧислоСтраниц = КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Количество() - ?(ЭтоРасшифровка,1,2);
	НомерСхемы   = ?(ЧислоСтраниц < 0, 100, КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Получить(ЧислоСтраниц).Значение);
	
	РанееОтображеннаяСтраница = РанееОтображеннаяСтраница(НомерСхемы);
	Если РанееОтображеннаяСтраница = Неопределено Тогда
		ПоказатьСтруктуруНалоговойБазы();
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоРасшифровка Тогда			
		КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Удалить(ЧислоСтраниц + 1);			
	КонецЕсли;		
	
	НомерТекущейСхемы  = НомерСхемы;
	
	ЭтоСхема = (ТипЗнч(РанееОтображеннаяСтраница) = Тип("ГрафическаяСхема"));
	
	УправляющаяСтруктура.ЭтоСхема                = ЭтоСхема;
	УправляющаяСтруктура.ЭтоОтчетПоДокументам    = Ложь;
	УправляющаяСтруктура.ДоступностьПоДокументам = Ложь;
	УправляющаяСтруктура.Главная                 = Ложь;
	
	Результат.Очистить();
	
	Если ЭтоСхема Тогда
		РезультатСхемы = РанееОтображеннаяСтраница;
	Иначе
		Результат.Вывести(РанееОтображеннаяСтраница);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьСхемуНаСервере(Ссылка, УправляющаяСтруктура, Отказ)
	
	Если НЕ ПроверитьЗаполнение() Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	РезультатВыполнения = Новый Структура("Статус", "Выполнено");
 
	УправляющаяСтруктура.ЭтоСхема = Истина;
	
	ВременныйНомерТекущейСхемы = НомерТекущейСхемы;
	
	КэшПараметровОтчетаСтруктура = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета(УправляющаяСтруктура.ТолькоОшибки);
	
	Если ВременныйНомерТекущейСхемы = -1 Тогда		
		Если СтрНайти(Ссылка, "Схема") = 0 Тогда
			Отказ = Истина;
			Возврат РезультатВыполнения;			
		КонецЕсли;
		
		ВременныйНомерТекущейСхемы = Число(СтрЗаменить(Ссылка, "Схема", "")) + 
			?(Отчет.ВключатьОбособленныеПодразделения И НЕ ВременныйНомерТекущейСхемы > 20, 20, 0);
			
		УправляющаяСтруктура.ЭтоСхема = НЕ ВременныйНомерТекущейСхемы > 20;
		НомерТекущейСхемы = ВременныйНомерТекущейСхемы;
		
		ПараметрыОтчета.Вставить("НомерТекущейСхемы",   НомерТекущейСхемы);
		ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		ПараметрыОтчета.Вставить("Расшифровка",         ДанныеРасшифровки);
		
		РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета);
		
		ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
				
		Возврат РезультатВыполнения;
		
	ИначеЕсли НЕ КэшПараметровОтчетаСтруктура.Свойство("КэшМакетов") Тогда
		
		УправляющаяСтруктура.Главная = Истина;
		ТекстСообщения = НСтр("ru = 'Отчет обновлен!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		ПоказатьСтруктуруНалоговойБазы();
		
		ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
				
		Возврат РезультатВыполнения;
		
	ИначеЕсли КэшПараметровОтчетаСтруктура.КэшМакетов.Свойство("Макет_" + НомерТекущейСхемы) Тогда
		
		ПараметрыМакета = КэшПараметровОтчетаСтруктура.КэшМакетов["Макет_" + НомерТекущейСхемы];
		
		Если ПараметрыМакета.РасшифровкаМакета.Свойство(Ссылка) Тогда

			СоответствиеРасшифровки = ПараметрыМакета.РасшифровкаМакета[Ссылка];
			Если СоответствиеРасшифровки["ВидРасшифровки"] = "Схема" Тогда
				
				ВременныйНомерТекущейСхемы    = СоответствиеРасшифровки["НомерТекущейСхемы"];
				УправляющаяСтруктура.ЭтоСхема = НЕ ВременныйНомерТекущейСхемы > 20;
				НомерТекущейСхемы             = ВременныйНомерТекущейСхемы;
				
				ПараметрыОтчета.Вставить("НомерТекущейСхемы",   НомерТекущейСхемы);
				ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		        ПараметрыОтчета.Вставить("Расшифровка",         СоответствиеРасшифровки);
				
				РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета);
				
				ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
				
				Возврат РезультатВыполнения;
			Иначе
				ПоДокументам      = Ложь;
				НомерТекущейСхемы = 100;
				
				УправляющаяСтруктура.ЭтоСхема             = Ложь;
				УправляющаяСтруктура.ЭтоОтчетПоДокументам = Истина;
				
				СхемаКомпоновкиДанных = Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.ПолучитьМакет(
					СоответствиеРасшифровки["НомерТекущейСхемы"]);
				УправляющаяСтруктура.ДоступностьПоДокументам =
					СхемаКомпоновкиДанных.ВариантыНастроек.Найти("БезРегистратора") <> Неопределено;
				
				СоответствиеРасшифровки.Вставить("НеРазворачиватьПоРегистраторам", НЕ ПоДокументам);
				
				// закэшируем расшифровку для возможности переформирования
				КэшПараметровОтчетаСтруктура.Вставить("ТекущаяРасшифровка", СоответствиеРасшифровки);
				
				ПараметрыОтчета.Вставить("НомерТекущейСхемы",   НомерТекущейСхемы);
				ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		
				РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета, Истина);
				
				ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
				
				Возврат РезультатВыполнения;
			КонецЕсли;			
		КонецЕсли;				
	КонецЕсли;
	
	КэшПараметровОтчета = ПоместитьВоВременноеХранилище(КэшПараметровОтчетаСтруктура, КэшПараметровОтчета);
	
	ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Функция ОбновитьТаблицуНаСервере(УправляющаяСтруктура, Отказ)
	
	УправляющаяСтруктура.ЭтоСхема                = Ложь;
	УправляющаяСтруктура.ЭтоОтчетПоДокументам    = Истина;
	УправляющаяСтруктура.ДоступностьПоДокументам = Истина;
	
	РезультатВыполнения = Новый Структура("Статус", "Выполняется");
	
	КэшПараметровОтчетаСтруктура = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	
	Если КэшПараметровОтчетаСтруктура.Свойство("ИсторияОткрытий")
		И КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Количество() > 0 Тогда
		//Получим последний номер схемы из истории - это там Схема, которую мы расшифровали
		НомерТекущейСхемыРасшифровки = КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Получить(
			КэшПараметровОтчетаСтруктура.ИсторияОткрытий.Количество() - 1);
	Иначе
		Отказ = Ложь;
		Возврат РезультатВыполнения;	
	КонецЕсли;
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета(УправляющаяСтруктура.ТолькоОшибки);
	
	Если КэшПараметровОтчетаСтруктура.КэшМакетов.Свойство("Макет_" + НомерТекущейСхемыРасшифровки) Тогда
		
		НомерТекущейСхемы = 100;
		
		СоответствиеРасшифровки = КэшПараметровОтчетаСтруктура.ТекущаяРасшифровка;
		
		СхемаКомпоновкиДанных = Отчеты.АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль.ПолучитьМакет(
			СоответствиеРасшифровки["НомерТекущейСхемы"]);
		УправляющаяСтруктура.ДоступностьПоДокументам =
			СхемаКомпоновкиДанных.ВариантыНастроек.Найти("БезРегистратора") <> Неопределено;
		СоответствиеРасшифровки.Вставить("НеРазворачиватьПоРегистраторам", НЕ ПоДокументам);
		
		// закэшируем расшифровку для возможности переформирования
		КэшПараметровОтчетаСтруктура.Вставить("ТекущаяРасшифровка", СоответствиеРасшифровки);
		
		ПараметрыОтчета.Вставить("НомерТекущейСхемы",   НомерТекущейСхемы);
		ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		ПараметрыОтчета.Вставить("Расшифровка",         СоответствиеРасшифровки);
		
		// сформируем расшифровку
		РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета, Истина);
		
		КэшПараметровОтчета = ПоместитьВоВременноеХранилище(КэшПараметровОтчетаСтруктура, КэшПараметровОтчета);
		
		ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
		
	Иначе
		
		Отказ = Ложь;
		
	КонецЕсли;
	
	Возврат РезультатВыполнения;
		
КонецФункции

&НаСервере
Функция ОбработатьРасшифровкуНаСервере(Расшифровка, УправляющаяСтруктура, ОткрытоИзИстории = Ложь)
	
	КэшПараметровОтчетаСтруктура = ПолучитьИзВременногоХранилища(КэшПараметровОтчета);
	
	РезультатВыполнения = Новый Структура("Статус, СпособВыполнения", "Выполнено", "");
	Если ТипЗнч(Расшифровка) = Тип("Соответствие") Тогда
		НомерТекущейСхемы = Расшифровка["НомерТекущейСхемы"];
		
		ПараметрыОтчета = ПодготовитьПараметрыОтчета(УправляющаяСтруктура.ТолькоОшибки);
		ПараметрыОтчета.Вставить("НомерТекущейСхемы", НомерТекущейСхемы);
		ПараметрыОтчета.Вставить("КэшПараметровОтчета", КэшПараметровОтчетаСтруктура);
		ПараметрыОтчета.Вставить("Расшифровка", Расшифровка);
		
		РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета);
		
		УправляющаяСтруктура.ЭтоСхема = (Расшифровка["ВидРасшифровки"] = "Схема");
		РезультатВыполнения.Вставить("СпособВыполнения", "Схема");
		
		ОбновитьСтраницы(ЭтотОбъект, УправляющаяСтруктура);
	ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Расшифровка)) Тогда 
		
		УправляющаяСтруктура.ОткрытьОбъект = Расшифровка;
		РезультатВыполнения.СпособВыполнения = "ОткрытьОбъект";
		
	КонецЕсли;
	
	Возврат РезультатВыполнения;
		
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НоваяУправляющаяСтруктура()
	
	УправляющаяСтруктура = Новый Структура;
	УправляющаяСтруктура.Вставить("Главная",                 Ложь);
	УправляющаяСтруктура.Вставить("ТолькоОшибки",            Ложь);
	УправляющаяСтруктура.Вставить("ЭтоСхема",                Ложь);
	УправляющаяСтруктура.Вставить("ЭтоОтчетПоДокументам",    Ложь);
	УправляющаяСтруктура.Вставить("ДоступностьПоДокументам", Ложь);
	УправляющаяСтруктура.Вставить("ОткрытьОбъект");
	Возврат УправляющаяСтруктура;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСтраницы(Форма, УправляющаяСтруктура)
	
	Элементы = Форма.Элементы;
	ИнтерфейсСхемы = УправляющаяСтруктура.Главная Или УправляющаяСтруктура.ЭтоСхема;
	ИнтерфейсОтчетаПоДокументам = Не УправляющаяСтруктура.Главная И УправляющаяСтруктура.ЭтоОтчетПоДокументам;
	
	// Установим режим видимости объектов для карты и расшифровок
	Элементы.ГруппаБыстрыеОтборы.Видимость 			 = УправляющаяСтруктура.Главная;
	Элементы.Назад.Видимость = Не УправляющаяСтруктура.Главная;
	Элементы.КомандаСтруктураНалоговойБазы.Видимость = Не УправляющаяСтруктура.Главная;
	
	Элементы.Сформировать.Видимость                  = ИнтерфейсОтчетаПоДокументам;
	Элементы.СформироватьВсеДействия.Видимость       = ИнтерфейсОтчетаПоДокументам;
	Элементы.СформироватьТолькоОшибки.Видимость      = ИнтерфейсОтчетаПоДокументам;
	Элементы.ГруппаДополнительныеНастройки.Видимость = ИнтерфейсОтчетаПоДокументам;
	Элементы.ГруппаВыводВсеДействия.Видимость        = ИнтерфейсОтчетаПоДокументам;
	Элементы.РезультатСхемы.Видимость                = ИнтерфейсСхемы;
	Элементы.ИндикаторВыполнения.Видимость           = Ложь;
	Элементы.Результат.Видимость                     = Не ИнтерфейсСхемы;
	
	Элементы.ГруппаРаботаВТаблице.Видимость    = Не ИнтерфейсСхемы;
	Элементы.ПечатьСразу.Видимость             = Не ИнтерфейсСхемы; 
	Элементы.ПредварительныйПросмотр.Видимость = Не ИнтерфейсСхемы И Не ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	Элементы.ПечатьГрафическойСхемы.Видимость       = ИнтерфейсСхемы;
	Элементы.ПредварительныйПросмотрСхемы.Видимость = ИнтерфейсСхемы И Не ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	Элементы.ПредварительныйПросмотрВсеДействия.Видимость = ИнтерфейсОтчетаПоДокументам И Не ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	
	Если УправляющаяСтруктура.Главная Тогда
			
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");
		
	ИначеЕсли УправляющаяСтруктура.ЭтоСхема Тогда

		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ИндикаторВыполнения, "НеИспользовать");

	Иначе

		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");

	КонецЕсли;
	
	Элементы.ПоДокументам.Доступность = Не УправляющаяСтруктура.Главная И УправляющаяСтруктура.ДоступностьПоДокументам;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗавершитьРаботуСОтчетом = Истина;
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

#Область ЗаголовокФормы

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокФормы(Заголовок, ПредставлениеПериода, ПредставлениеОрганизации)
	
	Заголовок = СправкиРасчетыКлиентСервер.ТекстЗаголовка(
		НСтр("ru = 'Анализ учета по налогу на прибыль'"),
		ПредставлениеПериода,
		ПредставлениеОрганизации);
	
КонецПроцедуры

#КонецОбласти

#Область НастройкаПоПериоду

// Изменение периода иногда требует дорогого контекстного вызова сервера, но часто - не требует. 

&НаКлиенте
Процедура НастроитьПериодНаКлиенте(ЦелыйИнтервал = Ложь)
	
	Последствия = ОпределитьИнтервалОтчета(
		Отчет.НачалоПериода,
		Отчет.КонецПериода,
		ЦелыйИнтервал,
		Отчет.Организация,
		ИмяСхемы);
			
	НастроитьПериодНаКлиентеНаСервере(ЭтотОбъект, Последствия);
	
	Если Не ЗначениеЗаполнено(Последствия.ТребуетсяВызовСервера) Тогда
		
		// Отобразим информацию о налоговом периоде
		УдалосьНастроитьНаКлиенте = ИнформированиеНалоговыйПериодКлиент.НастроитьЭлементИнформацияНалоговыйПериод(
			Элементы,
			Последствия.ИнформацияНалоговыйПериод);
		
		Если Не УдалосьНастроитьНаКлиенте Тогда
			Последствия.ТребуетсяВызовСервера.Вставить("НастроитьЭлементИнформирования", Последствия.ИнформацияНалоговыйПериод);
		КонецЕсли;
		
	КонецЕсли;
				
	Если ЗначениеЗаполнено(Последствия.ТребуетсяВызовСервера) Тогда
		НастроитьПериодНаСервере(Последствия.ТребуетсяВызовСервера);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьПериодНаКлиентеНаСервере(Форма, ПоследствияИзменения)
	
	Форма.Отчет.НачалоПериода        = ПоследствияИзменения.НачалоПериода;
	Форма.ПредставлениеПериодаОтчета = ПоследствияИзменения.ПредставлениеПериода;
	
	УстановитьЗаголовокФормы(
		Форма.Заголовок,
		Форма.ПредставлениеПериодаОтчета,
		Форма.ПредставлениеОрганизации);

КонецПроцедуры

&НаСервере
Процедура НастроитьПериодНаСервере(ТребуетсяВызовСервера)
	
	Если ТребуетсяВызовСервера.Свойство("НастроитьЭлементИнформирования") Тогда
		ИнформированиеНалоговыйПериод.НастроитьЭлементИнформирования(
			Элементы,
			ТребуетсяВызовСервера.НастроитьЭлементИнформирования,
			Элементы.РезультатСхемы);
	КонецЕсли;
	
	Если ТребуетсяВызовСервера.Свойство("ИмяСхемы") Тогда
		ИмяСхемы = ТребуетсяВызовСервера.ИмяСхемы;
		ПоказатьСтруктуруНалоговойБазы();
	КонецЕсли;
	
КонецПроцедуры		

&НаСервереБезКонтекста
Функция ОпределитьИнтервалОтчета(Знач НачалоПериода, Знач КонецПериода, Знач ЦелыйИнтервал, Знач Организация, Знач ИмяСхемы)
	
	Последствия = Новый Структура;
	Последствия.Вставить("НачалоПериода",              НачалоПериода);
	Последствия.Вставить("ПредставлениеПериода",       "");
	Последствия.Вставить("ИнформацияНалоговыйПериод",  Неопределено);
	Последствия.Вставить("ТребуетсяВызовСервера",      Новый Структура);
	
	ОписаниеОтчета = СправкиРасчетыКлиентСервер.НовыйОписаниеФормыПериодОтчета();
	ОписаниеОтчета.ИмяОтчета     = "АнализСостоянияНалоговогоУчетаПоНалогуНаПрибыль";
	ОписаниеОтчета.ЦелыйИнтервал = ЦелыйИнтервал;
	
	СправкиРасчетыКлиентСервер.ОпределитьИнтервалОтчета(
		Последствия.ПредставлениеПериода,
		Последствия.ИнформацияНалоговыйПериод,
		Последствия.НачалоПериода,
		КонецПериода,
		ОписаниеОтчета,
		Организация,
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ДатаРегистрации"));
		
	НовоеИмяСхемы = ИмяКарты(
		НачалоПериода,
		КонецПериода,
		Организация);
		
	Если НовоеИмяСхемы <> ИмяСхемы Тогда
		Последствия.ТребуетсяВызовСервера.Вставить("ИмяСхемы", НовоеИмяСхемы);
		// Раз все равно вызывать сервер, то все сделаем на сервере
		Последствия.ТребуетсяВызовСервера.Вставить("НастроитьЭлементИнформирования", Последствия.ИнформацияНалоговыйПериод);
		Последствия.ИнформацияНалоговыйПериод = Неопределено;
	КонецЕсли;
	
	Возврат Последствия;
	
КонецФункции

#КонецОбласти

#Область НастройкаПоОрганизации

// Изменение организации иногда требует дорогого контекстного вызова сервера, но часто - не требует. 

&НаКлиенте
Процедура НастроитьПоОрганизацииНаКлиенте()
	
	Последствия = ПоследствияИзмененияОрганизации(
		Отчет.НачалоПериода,
		Отчет.КонецПериода,
		Ложь,
		Отчет.Организация,
		Отчет.ВключатьОбособленныеПодразделения,
		ИмяСхемы);
		
	НастроитьПоОрганизацииНаКлиентеНаСервере(ЭтотОбъект, Последствия);
	
	Если ЗначениеЗаполнено(Последствия.ТребуетсяВызовСервера) Тогда
		НастроитьПоОрганизацииНаСервере(Последствия.ТребуетсяВызовСервера);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьПоОрганизацииНаКлиентеНаСервере(Форма, ПоследствияИзменения)
	
	Форма.ПредставлениеОрганизации   = ПоследствияИзменения.ПредставлениеОрганизации;
	Форма.ДатаРегистрацииОрганизации = ПоследствияИзменения.ДатаРегистрацииОрганизации;
	
	НастроитьПериодНаКлиентеНаСервере(Форма, ПоследствияИзменения);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПоОрганизацииНаСервере(ТребуетсяВызовСервера = Неопределено)
	
	Если ТребуетсяВызовСервера = Неопределено Тогда
		
		// Первоначальная настройка, выполняемая на сервере
		
		Последствия = ПоследствияИзмененияОрганизации(
			Отчет.НачалоПериода,
			Отчет.КонецПериода,
			Истина,
			Отчет.Организация,
			Отчет.ВключатьОбособленныеПодразделения,
			ИмяСхемы);
			
		НастроитьПоОрганизацииНаКлиентеНаСервере(ЭтотОбъект, Последствия);
		
		// заодно настроим, раз уже на сервере
		Последствия.ТребуетсяВызовСервера.Вставить("НастроитьЭлементИнформирования", Последствия.ИнформацияНалоговыйПериод);
		
		ТребуетсяВызовСервера = Последствия.ТребуетсяВызовСервера;
		
	КонецЕсли;
	
	НастроитьПериодНаСервере(ТребуетсяВызовСервера);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоследствияИзмененияОрганизации(Знач НачалоПериода, Знач КонецПериода, Знач ЦелыйИнтервал,
	Знач Организация, Знач ВключатьОбособленныеПодразделения, Знач ИмяСхемы)
	
	Последствия = ОпределитьИнтервалОтчета(НачалоПериода, КонецПериода, ЦелыйИнтервал, Организация, ИмяСхемы);
	
	Последствия.Вставить(
		"ПредставлениеОрганизации",
		БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Организация, ВключатьОбособленныеПодразделения));
		
	Последствия.Вставить(
		"ДатаРегистрацииОрганизации",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ДатаРегистрации"));
		
	Возврат Последствия;
	
КонецФункции

#КонецОбласти

#Область ГрафическиеСхемы

&НаСервереБезКонтекста
Функция ИмяКарты(НачалоПериода, КонецПериода, Организация)
	
	Имя = "Карта";
	Если ЗначениеЗаполнено(Организация) 
		И ЗначениеЗаполнено(НачалоПериода)
		И ЗначениеЗаполнено(КонецПериода)
		И УчетнаяПолитика.ПлательщикЕНВДЗаПериод(Организация, НачалоПериода, КонецПериода) Тогда
		Имя = Имя + "ЕНВД";
	КонецЕсли;
	
	Возврат Имя + ДополнениеИмениСхемыВариантомИнтерфейса();
	
КонецФункции

&НаСервереБезКонтекста
Функция ДополнениеИмениСхемыВариантомИнтерфейса()

	НастройкиКлиента = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения");
	Если ТипЗнч(НастройкиКлиента) = Тип("НастройкиКлиентскогоПриложения") 
		И НастройкиКлиента.ВариантИнтерфейсаКлиентскогоПриложения = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2 Тогда
		Возврат "82";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область КешОтображенныхСтраниц

// В ходе сеанса работы с отчетом пользователь может многократно переходить между отображаемыми страницами (обычно - графическими схемами).
// Поэтому содержимое однажды отображенной страницы (графическую схему) сохраняем в кеше и при повторном переходе на нее отображаем из кеша -
// см. ДополнитьКешОтображенныхСтраниц и РанееОтображеннаяСтраница
// По окончании сеанса (например, смене периода или организации) кеш следует очистить с помощью ОчиститьКешОтображенныхСтраниц
//
// Эти методы фактически используются для кеширования графических схем.
// Для кеширования расшифровки используется устаревший механизм - см. КэшМакетов

&НаСервере
Процедура ДополнитьКешОтображенныхСтраниц(Страница, Идентификатор)
	
	ЗаписьАдреса = ЗаписьАдресаРанееОтображеннойСтраницы(Идентификатор);
	Если Не ПустаяСтрока(ЗаписьАдреса.Адрес) И ЭтоАдресВременногоХранилища(ЗаписьАдреса.Адрес) Тогда
		УдалитьИзВременногоХранилища(ЗаписьАдреса.Адрес);
	КонецЕсли;
	
	ЗаписьАдреса.Адрес = ПоместитьВоВременноеХранилище(Страница, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция РанееОтображеннаяСтраница(Идентификатор)
	
	ЗаписьАдреса = ЗаписьАдресаРанееОтображеннойСтраницы(Идентификатор);
	Если ПустаяСтрока(ЗаписьАдреса.Адрес) Или Не ЭтоАдресВременногоХранилища(ЗаписьАдреса.Адрес) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(ЗаписьАдреса.Адрес);
	
КонецФункции

&НаСервере
Процедура ОчиститьКешОтображенныхСтраниц()
	
	Для Каждого ОписаниеСтраницы Из АдресаОтображенныхСтраниц Цикл
		
		УдалитьЭлементКешаОтображенныхСтраниц(ОписаниеСтраницы);
		
	КонецЦикла;
	
	АдресаОтображенныхСтраниц.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементКешаОтображенныхСтраниц(ОписаниеСтраницы)

	Если ПустаяСтрока(ОписаниеСтраницы.Адрес) Или Не ЭтоАдресВременногоХранилища(ОписаниеСтраницы.Адрес) Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьИзВременногоХранилища(ОписаниеСтраницы.Адрес);

КонецПроцедуры
	
&НаСервере
Функция ЗаписьАдресаРанееОтображеннойСтраницы(Идентификатор)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Идентификатор", Идентификатор);
	
	НайденныеСтроки = АдресаОтображенныхСтраниц.НайтиСтроки(Отбор);
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		Запись = НайденныеСтроки[0];
	Иначе
		Запись = АдресаОтображенныхСтраниц.Добавить();
		Запись.Идентификатор = Идентификатор;
	КонецЕсли;
	
	Возврат Запись;
	
КонецФункции
	
#КонецОбласти

#КонецОбласти