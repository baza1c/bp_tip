#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция-конструктор для хранения настроек отчета в универсальном формате.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Общая структура настроек.
	ПараметрыОтчета = БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.ИдентификаторОтчета = "ЖурналУчетаОригиналовПервичныхДокументов";
	
	ОтчетОбъект = Отчеты.ЖурналУчетаОригиналовПервичныхДокументов.Создать();
	ПараметрыОтчета.Вставить("РазмещениеДополнительныхПолей",
		БухгалтерскиеОтчетыКлиентСервер.РазмещениеДополнительныхПолей().ВместеСВладельцем);
	ПараметрыОтчета.Вставить("ДополнительныеПоля", ОтчетОбъект.ДополнительныеПоля.ВыгрузитьКолонки());
	
	ПараметрыОтчета.Вставить("СостояниеОригиналовПервичногоДокумента", Неопределено);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Заполняет структуру настроек универсального формата из реквизитов формы.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПустыеПараметрыКомпоновкиОтчета()
//  Форма - УправляемаяФорма - содержит основновной реквизит Отчет .
//
Процедура ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма) Экспорт
	
	БухгалтерскиеОтчеты.ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма);
	ПараметрыОтчета.СостояниеОригиналовПервичногоДокумента = Форма.Отчет.СостояниеОригиналовПервичногоДокумента;
	
КонецПроцедуры

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета",  Ложь);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",      Истина);

	Возврат Результат;
	
КонецФункции

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт 
	
	Возврат НСтр("ru = 'Журнал учета оригиналов первичных документов'") + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоДня(ПараметрыОтчета.НачалоПериода));
	Иначе
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", Дата(1,1,1,0,0,1));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		КонецПериода = КонецДня(ПараметрыОтчета.КонецПериода);
	Иначе
		КонецПериода = КонецДня('2099-12-31');
	КонецЕсли;
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецПериода);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ВидКонтактнойИформацииТелефонКонтрагента", Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ВидКонтактнойИформацииТелефонЭлектроннаяПочта", Справочники.ВидыКонтактнойИнформации.EmailКонтрагенты);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ТипыДокументовСОригиналамиПервичныхДокументов",
		УчетОригиналовПервичныхДокументовБП.ТипыДокументовСОригиналамиПервичныхДокументов());
	
	Если ПараметрыОтчета.СостояниеОригиналовПервичногоДокумента = ИмяСостоянияДокументыПолучены() Тогда
		СписокСостоянийОригиналов = Новый Массив;
		СписокСостоянийОригиналов.Добавить(Справочники.СостоянияОригиналовПервичныхДокументов.ОригиналПолучен);
	ИначеЕсли ПараметрыОтчета.СостояниеОригиналовПервичногоДокумента = ИмяСостоянияДокументыНеПолучены() Тогда
		ВсеСостоянияОригинала = УчетОригиналовПервичныхДокументов.ВсеСостояния();
		СостояниеОригиналПолучен = Справочники.СостоянияОригиналовПервичныхДокументов.ОригиналПолучен;
		
		СписокСостоянийОригиналов = Новый Массив;
		СписокСостоянийОригиналов.Добавить(Справочники.СостоянияОригиналовПервичныхДокументов.ПустаяСсылка());
		Для Каждого СостояниеОригинала Из ВсеСостоянияОригинала Цикл
			Если СостояниеОригинала = СостояниеОригиналПолучен Тогда
				Продолжить;
			КонецЕсли;
			СписокСостоянийОригиналов.Добавить(СостояниеОригинала);
		КонецЦикла;
	Иначе
		СписокСостоянийОригиналов = УчетОригиналовПервичныхДокументов.ВсеСостояния();
		СписокСостоянийОригиналов.Добавить(Справочники.СостоянияОригиналовПервичныхДокументов.ПустаяСсылка());
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"СписокСостояний", СписокСостоянийОригиналов);
	
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
КонецПроцедуры

Функция ИмяСостоянияВсеДокументы() Экспорт
	Возврат "ВсеДокументы";
КонецФункции

Функция ИмяСостоянияДокументыПолучены() Экспорт
	Возврат "ДокументыПолучены";
КонецФункции

Функция ИмяСостоянияДокументыНеПолучены() Экспорт
	Возврат "ДокументыНеПолучены";
КонецФункции

#КонецОбласти

#КонецЕсли