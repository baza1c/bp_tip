#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийУчет, "");
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийИНалоговыйУчет, "");
	КонецЦикла;
	
КонецПроцедуры

//Процедура используется подсистемой варианты отчетов
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя, Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантыНастроек() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление",
		"НачислениеПроцентовПоКредитамЗаймам", "Начисление процентов по договорам займа"));
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Схема        - СхемаКомпоновкиДанных - описание получаемых данных.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "НачалоПериода", НачалоМесяца(ПараметрыОтчета.НачалоПериода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "ВалютаРегламентированногоУчета", ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	
	ОписаниеОтбораПоОрганизации = Новый Структура;
	ОписаниеОтбораПоОрганизации.Вставить("Организация", ПараметрыОтчета.Организация);
	ОписаниеОтбораПоОрганизации.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ОписаниеОтбораПоОрганизации, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПриВыводеЗаголовка(ПараметрыОтчета, КомпоновщикНастроек, Результат) Экспорт
	
	ПараметрыОтчета.НачалоПериода = НачалоМесяца(ПараметрыОтчета.НачалоПериода);
	ПараметрыОтчета.ПредставлениеПериода = ПредставлениеПериода(
		ПараметрыОтчета.НачалоПериода, КонецМесяца(ПараметрыОтчета.КонецПериода), "ДФ='MMMM yyyy ""г.""'");
	
	СправкиРасчеты.ВывестиШапкуОтчета(Результат, ПараметрыОтчета, Истина);
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	ОбластьОбщиеИтоги = Неопределено;
	ЯчейкаУдалитьИтоги = Неопределено;
	
	Индекс = Результат.ВысотаТаблицы;
	
	// Поиск области общих итогов.
	Пока Индекс <> 0 Цикл
		ОбластьОбщиеИтоги = Результат.Область("R" + Формат(Индекс, "ЧГ=0"));
		Если ОбластьОбщиеИтоги.Текст = НСтр("ru = 'Итого'") Тогда
			// Достигли нужной области.
			Прервать;
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;
	
	Если ОбластьОбщиеИтоги <> Неопределено Тогда
		
		ЯчейкаУдалитьИтоги = Результат.НайтиТекст("###УдалитьИтоги###", ОбластьОбщиеИтоги);
		
		Если ЯчейкаУдалитьИтоги <> Неопределено Тогда // В отчете есть рубли и другая валюта.
			Результат.УдалитьОбласть(ОбластьОбщиеИтоги, ТипСмещенияТабличногоДокумента.ПоВертикали);
		Иначе
			// Найдем колонку Начислено (руб), т.к. в отчете нет валюты.
			ЯчейкаУдалитьВал = Результат.НайтиТекст("###УдалитьВал###", ОбластьОбщиеИтоги);
			
			Если ЯчейкаУдалитьВал <> Неопределено Тогда
				Индекс = Результат.ВысотаТаблицы;
				Пока Индекс > 0 Цикл
					ИмяЯчейки = СтрШаблон("R%1C%2", Формат(Индекс, "ЧГ=0"), Формат(ЯчейкаУдалитьВал.Лево, "ЧГ=0"));
					Ячейка = Результат.Область(ИмяЯчейки);
					Если Ячейка.Текст = НСтр("ru = 'Начислено (руб.)'") Тогда // Достигли шапки, пронумеруем колонки вправо
						СтрокаНумерации = Ячейка.Верх + 3;
						КолонкаНумерации = Ячейка.Лево + 1;
						Результат.Область(СтрокаНумерации, КолонкаНумерации, СтрокаНумерации, КолонкаНумерации).Текст = "11";
						Результат.Область(СтрокаНумерации, КолонкаНумерации + 1, СтрокаНумерации, КолонкаНумерации + 1).Текст = "12";
					КонецЕсли;
					Индекс = Индекс - 1;
				КонецЦикла; 
				// Удаляем колонку "Начислено (руб.)".
				УдаляемаяОбласть = Результат.Область(1, ЯчейкаУдалитьВал.Лево, ЯчейкаУдалитьВал.Низ, ЯчейкаУдалитьВал.Право);
				Результат.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	СправкиРасчеты.ОформитьРезультатОтчета(Результат, ПараметрыОтчета);
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
КонецПроцедуры

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт
	
	Возврат СправкиРасчеты.ЗаголовокОтчета(ПараметрыОтчета);
	
КонецФункции

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Манифест = Новый Структура;
	Манифест.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Манифест.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Манифест.Вставить("ИспользоватьПослеКомпоновкиМакета",  Ложь);
	Манифест.Вставить("ИспользоватьВнешниеНаборыДанных",    Ложь);
	Манифест.Вставить("ИспользоватьПриВыводеЗаголовка",     Истина);
	Манифест.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Манифест.Вставить("ИспользоватьНалоговыйПериод",        Ложь);
	Манифест.Вставить("ИспользоватьПроизвольныйПериод",     Истина);
	
	СправкиРасчеты.УстановитьОтчетНеИспользуетНаборыСуммовыхПоказателей(Манифест);
	
	Возврат Манифест;
	
КонецФункции

#КонецОбласти

#КонецЕсли
