#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийУчет, "");
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты.Подсистемы.БухгалтерскийИНалоговыйУчет, "");
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(
			Метаданные.Подсистемы.БухгалтерияПредприятияПодсистемы.Подсистемы.ПростойИнтерфейс.Подсистемы.Отчеты.Подсистемы.СправкиРасчеты, "");
	КонецЦикла;
	
КонецПроцедуры

//Процедура используется подсистемой варианты отчетов
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя,Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантыНастроек() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление",
		"РасчетТуристическогоНалога", "Расчет туристического налога"));
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Схема        - СхемаКомпоновкиДанных - описание получаемых данных.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	ТекстЗапроса = Схема.НаборыДанных.СправкаРасчет.Запрос; 
	Если БухгалтерскийУчетПереопределяемый.ВестиУчетПоДоговорам() Тогда
		
		ТекстЗамены = "ВЫБОР
		|	КОГДА НЕ СчетНаОплатуПокупателюШапка.ДоговорКонтрагента ЕСТЬ NULL
		|    	ТОГДА СчетНаОплатуПокупателюШапка.ДоговорКонтрагента
		|	КОГДА НЕ НачислениеТуристическогоНалогаШапка.ДоговорКонтрагента ЕСТЬ NULL
		|    	ТОГДА НачислениеТуристическогоНалогаШапка.ДоговорКонтрагента
		|	КОГДА НЕ ОтчетКомиссионераОПродажахШапка.ДоговорКонтрагента ЕСТЬ NULL
		|    	ТОГДА ОтчетКомиссионераОПродажахШапка.ДоговорКонтрагента
        |	КОГДА НЕ РеализацияТоваровУслугШапка.ДоговорКонтрагента ЕСТЬ NULL
		|    	ТОГДА РеализацияТоваровУслугШапка.ДоговорКонтрагента
		|	ИНАЧЕ
		|		ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
		|	КОНЕЦ";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеДоговор", ТекстЗамены);
		
    Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеДоговор", "Неопределено");
	КонецЕсли;
	Схема.НаборыДанных.СправкаРасчет.Запрос = ТекстЗапроса;
	
	ПреобразоватьДатыПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);

	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", ПараметрыОтчета.НачалоПериода);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда  
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", ПараметрыОтчета.КонецПериода);
	КонецЕсли;
		
	ОписаниеОтбораПоОрганизации = Новый Структура;
	ОписаниеОтбораПоОрганизации.Вставить("Организация", ПараметрыОтчета.Организация);
	ОписаниеОтбораПоОрганизации.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ОписаниеОтбораПоОрганизации, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПриВыводеЗаголовка(Контекст, КомпоновщикНастроек, Результат) Экспорт
	
	ПреобразоватьДатыПериода(Контекст.НачалоПериода, Контекст.КонецПериода);
	
	Контекст.НачалоПериода = Контекст.НачалоПериода;
	Контекст.ПредставлениеПериода = ПредставлениеПериода(Контекст.НачалоПериода, Контекст.КонецПериода, "ДФ='MMMM yyyy ""г.""'");
	
	СправкиРасчеты.ВывестиШапкуОтчета(Результат, Контекст, Истина);
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(Контекст, Результат) Экспорт
	
	НомерКолонки = 8;
	
	КолонкаДоговор = Результат.НайтиТекст("###Договор###"); 
	Если КолонкаДоговор <> Неопределено Тогда
		Если БухгалтерскийУчетПереопределяемый.ВестиУчетПоДоговорам() Тогда
			КолонкаДоговор.Текст = "Договор";
		Иначе
			НомерКолонки = КолонкаДоговор.Лево;
			ОбластьНаУдаление = Результат.Область(1, НомерКолонки, Результат.ВысотаТаблицы, НомерКолонки);
			Результат.УдалитьОбласть(ОбластьНаУдаление, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
			НомерКолонкиПоказателя = НомерКолонки + 2;
			Результат.Область(1, НомерКолонкиПоказателя, Результат.ВысотаТаблицы, НомерКолонкиПоказателя).ШиринаКолонки = 17;
		КонецЕсли;
	КонецЕсли;
	
	КолонкаНалог = Результат.НайтиТекст("###Сумма налога###"); 
	Если КолонкаНалог <> Неопределено Тогда
		Если Контекст.КонецПериода < ТуристическийНалогСервер.ДатаПримененияЛьготыОплатаИзБюджета() Тогда
			КолонкаНалог.Текст = "Сумма налога";
		Иначе
			НомерКолонки = КолонкаНалог.Лево;
			ОбластьНаУдаление = Результат.Область(1, НомерКолонки, Результат.ВысотаТаблицы, НомерКолонки);  
			Результат.УдалитьОбласть(ОбластьНаУдаление, ТипСмещенияТабличногоДокумента.ПоГоризонтали); 	
		КонецЕсли;
	КонецЕсли;
	
	КолонкаНалог2026 = Результат.НайтиТекст("###Сумма налога2026###"); 
	Если КолонкаНалог2026 <> Неопределено Тогда
		Если Контекст.КонецПериода < ТуристическийНалогСервер.ДатаПримененияЛьготыОплатаИзБюджета() Тогда 
			НомерКолонки = КолонкаНалог2026.Лево;
			ОбластьНаУдаление = Результат.Область(1, НомерКолонки,Результат.ВысотаТаблицы, НомерКолонки + 2);  
			Результат.УдалитьОбласть(ОбластьНаУдаление, ТипСмещенияТабличногоДокумента.ПоГоризонтали); 	
		Иначе
			КолонкаНалог2026.Текст = "Сумма налога";
		КонецЕсли;
	КонецЕсли;
		
	КолонкаНомер11 = Результат.НайтиТекст("###Номер11###"); 
	Если КолонкаНомер11 <> Неопределено Тогда
		Если Контекст.КонецПериода < ТуристическийНалогСервер.ДатаПримененияЛьготыОплатаИзБюджета() Тогда 
			КолонкаНомер11.Текст = НомерКолонки; 
		Иначе
			КолонкаНомер11.Текст = НомерКолонки + 3; 
		КонецЕсли;
	КонецЕсли;

	СправкиРасчеты.ОформитьРезультатОтчета(Результат, Контекст);
	
	Результат.ФиксацияСверху = Результат.ФиксацияСверху + 2;
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ВывестиПояснение(Результат, Контекст);

КонецПроцедуры

Процедура ВывестиПояснение(Результат, Контекст)
	
	Если Контекст.КонецПериода < ТуристическийНалогСервер.ДатаПримененияЛьготыОплатаИзБюджета() Тогда
		Возврат;
	КонецЕсли;
	
	Текст = НСтр(
		"ru = 'Налоговая льгота (ст.418.5-1 НК РФ)
         |От уплаты налога освобождаются организации в отношении услуг по временному проживанию в составе:
         |    - услуг по санаторно-курортному лечению, если оно происходит по медицинским показаниям и оплачивается за счет бюджетных 
		 |ассигнований разного уровня (федерального бюджета, государственных внебюджетных фондов, бюджетов субъектов РФ, местных бюджетов);
         |    - услуг по организованному отдыху в средствах размещения, закрепленных на праве оперативного управления за учреждениями, 
		 |находящимися в ведении Управления делами Президента РФ и оплаченными за счет бюджетных ассигнований федерального бюджета'");
		
	СправкиРасчеты.ДобавитьПримечание(Результат, Текст);
	
	Текст = НСтр(
		"ru = 'Разница, возникшая при округлении итоговых сумм до рублей, согласно порядка заполнения декларации по туристическому налогу'");
		
	СправкиРасчеты.ДобавитьПримечание(Результат, Текст, 1);

	
КонецПроцедуры

Функция ПолучитьТекстЗаголовка(Контекст) Экспорт
	
	Возврат СправкиРасчеты.ЗаголовокОтчета(Контекст);
	
КонецФункции

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Манифест = Новый Структура;
	Манифест.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Манифест.Вставить("ИспользоватьРасширенныеПараметрыРасшифровки", Истина);
	Манифест.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Манифест.Вставить("ИспользоватьПослеКомпоновкиМакета",  Ложь);
	Манифест.Вставить("ИспользоватьВнешниеНаборыДанных",    Ложь);
	Манифест.Вставить("ИспользоватьПриВыводеЗаголовка",     Истина);
	Манифест.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Манифест.Вставить("ИспользоватьНалоговыйПериод",        Ложь);
	Манифест.Вставить("ИспользоватьПроизвольныйПериод",     Истина);   
	Манифест.Вставить("ИспользоватьРазличнуюПериодичность", Истина); 
    Манифест.Вставить("ПериодичностьОтчета",                Перечисления.ДоступныеПериодыОтчета.Месяц);

	СправкиРасчеты.УстановитьОтчетНеИспользуетНаборыСуммовыхПоказателей(Манифест);
	
	Возврат Манифест;
	
КонецФункции

Процедура ЗаполнитьПараметрыРасшифровкиОтчета(Адрес, Расшифровка, ПараметрыРасшифровки) Экспорт
	
	ДанныеОбъекта = ПолучитьИзВременногоХранилища(Адрес);
	
	ОтчетОбъект       = ДанныеОбъекта.Объект;
	ДанныеРасшифровки = ДанныеОбъекта.ДанныеРасшифровки;
	
	ЭлементРасшифровки = ДанныеРасшифровки.Элементы[Расшифровка];
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") Тогда
		Возврат;
	КонецЕсли;
	
	ПоляРасшифровки = ЭлементРасшифровки.ПолучитьПоля();
	Если ПоляРасшифровки.Количество() > 0 Тогда
		ЗначениеРасшифровки = ПоляРасшифровки[0].Значение;
		Если ЗначениеРасшифровки = Null
			Или ЗначениеРасшифровки = Неопределено
			Или ТипЗнч(ЗначениеРасшифровки) = Тип("Число")
			Или ТипЗнч(ЗначениеРасшифровки) = Тип("Строка")
			Или ТипЗнч(ЗначениеРасшифровки) = Тип("Булево") 
			Или ТипЗнч(ЗначениеРасшифровки) = Тип("ПеречислениеСсылка.СпособыРасчетаТуристическогоНалога") Тогда
			Возврат;
		Иначе
			ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
			ПараметрыРасшифровки.Вставить("Значение", ЗначениеРасшифровки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПериодичностьПоДате(КонецПериода) Экспорт
	
	Если КонецПериода >= ТуристическийНалогСервер.ДатаИзмененияПериодичностиРегОперации() Тогда
		Возврат Перечисления.ДоступныеПериодыОтчета.Месяц;
	Иначе
		Возврат Перечисления.ДоступныеПериодыОтчета.Квартал;
	КонецЕсли;
	
КонецФункции

Процедура ПреобразоватьДатыПериода(НачалоПериода, КонецПериода)
	
	Если КонецПериода >= ТуристическийНалогСервер.ДатаИзмененияПериодичностиРегОперации() Тогда
		НачалоПериода = НачалоМесяца(НачалоПериода);
	    КонецПериода = КонецМесяца(КонецПериода);
	Иначе
		НачалоПериода = НачалоКвартала(НачалоПериода);
	    КонецПериода = КонецКвартала(КонецПериода);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли