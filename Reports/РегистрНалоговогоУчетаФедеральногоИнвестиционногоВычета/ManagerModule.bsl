 #Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область БухгалтерскиеОтчеты

// Функция-конструктор для хранения настроек отчета в универсальном формате.
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Общая структура настроек.
	Контекст = БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета();
	
	ОтчетОбъект = Создать();
	// 0 = В одной колонке, 1 = В отдельных колонках
	Контекст.Вставить("РазмещениеДополнительныхПолей",
		БухгалтерскиеОтчетыКлиентСервер.РазмещениеДополнительныхПолей().ВместеСВладельцем);
	Контекст.Вставить("ДополнительныеПоля", ОтчетОбъект.ДополнительныеПоля.ВыгрузитьКолонки());
	Контекст.Вставить("ИдентификаторОтчета",    "");
	Контекст.Вставить("НазваниеРегистра",       НСтр("ru = 'Расчет федерального инвестиционного налогового вычета'"));
	Контекст.Вставить("ИнформацияНалоговыйПериод", "");
	Контекст.Вставить("ГрафыОтчета", Новый Массив); // из см. НовыйГрафаОтчета
	
	Возврат Контекст;
	
КонецФункции

// Заполняет структуру настроек универсального формата из реквизитов формы.
//
// Параметры:
//  Контекст - см. ПустыеПараметрыКомпоновкиОтчета
//  Форма - ФормаКлиентскогоПриложения - содержит основновной реквизит Отчет.
//
Процедура ЗаполнитьПараметрыИзФормы(Контекст, Форма) Экспорт
	
	БухгалтерскиеОтчеты.ЗаполнитьПараметрыИзФормы(Контекст, Форма);
	Контекст.ИдентификаторОтчета        = БухгалтерскиеОтчеты.ИмяОтчета(Форма);
		
КонецПроцедуры

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//   Структура   - флаги, задающие необходимость дополнительных действий.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета",  Истина);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Результат.Вставить("ИспользоватьПриВыводеЗаголовка",     Истина);
	
	Возврат Результат;
	
КонецФункции

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПриВыводеЗаголовка.
//
// Параметры:
//  Контекст            - см. ПустыеПараметрыКомпоновкиОтчета
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - описание связи настроек и схемы отчета.
//  Результат - ТабличныйДокумент - табличный документ с сформированным отчетом.
//
Процедура ПриВыводеЗаголовка(Контекст, КомпоновщикНастроек, Результат) Экспорт
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	ОбластьЗаголовок        = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьОрганизация      = Макет.ПолучитьОбласть("Организация");
	
	//Организация
	ТекстОрганизация = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Контекст.Организация,
		Контекст.ВключатьОбособленныеПодразделения);
	ОбластьОрганизация.Параметры.НазваниеОрганизации = ТекстОрганизация;
	Результат.Вывести(ОбластьОрганизация);
	
	//Заголовок
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = ПолучитьТекстЗаголовка(Контекст);
	Результат.Вывести(ОбластьЗаголовок);
	
	Результат.Область("R1:R" + Результат.ВысотаТаблицы).Имя = "Заголовок";
	
	Контекст.Вставить("ВысотаШапки",Результат.ВысотаТаблицы); 
	
КонецПроцедуры

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПередКомпоновкойМакета.
// В процедуре можно доработать компоновщик перед выводом в отчет.
// Изменения сохранены не будут.
// 
// Параметры:
//  Контекст - Структура
//  Схема - СхемаКомпоновкиДанных
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных
//
Процедура ПередКомпоновкойМакета(Контекст, Схема, КомпоновщикНастроек) Экспорт
	
	Если ЗначениеЗаполнено(Контекст.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоДня(Контекст.НачалоПериода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контекст.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецДня(Контекст.КонецПериода));
	КонецЕсли;
	
	ВсяОрганизация = Новый Массив;
	Если ЗначениеЗаполнено(Контекст.Организация) Тогда
		ВсяОрганизация = Справочники.Организации.ВсяОрганизация(Контекст.Организация);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"Организация",
			Контекст.Организация);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ВсяОрганизация",
			ВсяОрганизация);
	Иначе
			
		ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"Организация",
			ДоступныеОрганизации);
		
		Для Каждого ДоступнаяОрганизация Из ДоступныеОрганизации Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсяОрганизация, Справочники.Организации.ВсяОрганизация(ДоступнаяОрганизация), Истина);
		КонецЦикла;
		
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек,
			"ВсяОрганизация",
			ВсяОрганизация);
	КонецЕсли;

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"СчетУчетаИнвестиционныйВычет",
		ПланыСчетов.Хозрасчетный.ФедеральныйИнвестиционныйНалоговыйВычет);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"СубконтоСчетаИнвестиционныйВычет",
		ИнвестиционныйНалоговыйВычет.СубконтоСчетаИнвестиционныйВычет());
	
	СчетаОсновныеСредства = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	СчетаНематериальныеАктивы = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
	
	СчетаАмортизируемоеИмущество = Новый Массив(ОбщегоНазначения.СкопироватьРекурсивно(СчетаОсновныеСредства));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаАмортизируемоеИмущество, СчетаНематериальныеАктивы);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"СчетаАмортизируемогоИмущества",
		СчетаАмортизируемоеИмущество);
		
	// Размер вычета, установленный п. 3 Постановления Правительства Российской Федерации от 28 ноября 2024 г. N 1638
	// "О параметрах применения федерального инвестиционного налогового вычета", составляет 3 процента от суммы расходов.
	// В случае изменения этого размера и/или привязки его к условиям применения (периоду, виду расходов и пр.)
	// потребуется изменить временную таблицу, содержащую данные о проценте вычета: добавить недостающие разрезы и условия соединения.
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"ПроцентРасходов",
		ИнвестиционныйНалоговыйВычет.СтандартныйПроцентВычета());
	
	// Дополнительные данные.
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(Контекст, КомпоновщикНастроек);
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("ОрганизацияОсуществившаяРасходы");
	НеиспользуемыеПоля.Добавить("ОрганизацияПрименяющаяВычет");
	
	УточнитьНеиспользуемыеПоля(НеиспользуемыеПоля, Контекст, ВсяОрганизация);
	
	УдаляемыеПоля = Новый Массив;
	Если НеиспользуемыеПоля.Количество() > 0 Тогда
	
		Для Каждого ПолеВыбора Из КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл
			Если НеиспользуемыеПоля.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			Для НомерНеиспользуемогоПоля = 0 По НеиспользуемыеПоля.ВГраница() Цикл
				Если ПолеВыбора.Поле = Новый ПолеКомпоновкиДанных(НеиспользуемыеПоля[НомерНеиспользуемогоПоля]) Тогда
					УдаляемыеПоля.Добавить(ПолеВыбора);
					НеиспользуемыеПоля.Удалить(НомерНеиспользуемогоПоля);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Если УдаляемыеПоля.Количество() > 0 Тогда
			Для Каждого УдаляемоеПоле Из УдаляемыеПоля Цикл
				КомпоновщикНастроек.Настройки.Выбор.Элементы.Удалить(УдаляемоеПоле);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// В начале идут поля, относящиеся к группировке (реквизиты объекта), а затем поля-ресурсы.
	// Часть полей-ресурсов может отсутствовать: например, если не используется передача вычета, то нет
	// смысла выводить организацию-получателя.
	// Но важно знать состав полей, поскольку исходя из этого будет производиться нумерация колонок
	// и вывод заголовоков.
	// Нумерация полей группировки простая, по порядку, заголовки не меняются. Для них важно знать только
	// общее количество, поскольку от этого будут зависеть нумерация и заголовки ресурсов.
	ПоляИтога = Схема.ПоляИтога;
	ИменаПолейИтога = Новый Массив; // из Строка
	
	Для Каждого ПолеИтога Из ПоляИтога Цикл
		ИменаПолейИтога.Добавить(ПолеИтога.ПутьКДанным);
	КонецЦикла;
	
	ПоляГруппировки = Новый Массив;
	Для Каждого ВыбранноеПоле Из КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл
		Если ВыбранноеПоле.Поле = Неопределено Тогда
			// Это группа полей. Содержимое не анализируем, так как только ресурсы объединяем в группы.
			Продолжить;
		КонецЕсли;
		Если Не ВыбранноеПоле.Использование Тогда
			Продолжить;
		КонецЕсли;
		Если ИменаПолейИтога.Найти(Строка(ВыбранноеПоле.Поле)) = Неопределено Тогда
			ПоляГруппировки.Добавить(ВыбранноеПоле.Поле);
		КонецЕсли;
	КонецЦикла;
	
	КоличествоПолейГруппировки = ПоляГруппировки.Количество();
	
	ГрафыОтчета = Новый Массив; // из см. НовыйГрафаОтчета
	НомераКолонок = Новый Структура;
	НомераКолонок.Вставить("НачалоФормулы", Символы.ПС);
	НомерГрафы = КоличествоПолейГруппировки + 1;
	Для Каждого ВыбранноеПоле Из КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл
		Если ПоляГруппировки.Найти(ВыбранноеПоле.Поле) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если Не ВыбранноеПоле.Использование Тогда
			Продолжить;
		КонецЕсли;
		Если ВыбранноеПоле.Поле = Неопределено Тогда
			ВыбраннаяГруппа = ВыбранноеПоле;
			Для Каждого ВыбранноеПолеГруппы Из ВыбраннаяГруппа.Элементы Цикл
				ГрафаОтчета = НовыйГрафаОтчета();
				ГрафаОтчета.ВходитВГруппу = Истина;
				ШаблонПоля = НСтр("ru = '%1а'");
				Если ВыбраннаяГруппа.Элементы.Индекс(ВыбранноеПолеГруппы) = 1 Тогда
					ШаблонПоля = НСтр("ru = '%1б'");
				КонецЕсли;
				ГрафаОтчета.ИмяГрафы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПоля, НомерГрафы);
				ВыбранноеПолеГруппы.Заголовок = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
					ВыбранноеПолеГруппы.Заголовок,
					НомераКолонок);
				ГрафыОтчета.Добавить(ГрафаОтчета);
				НомераКолонок.Вставить(ВыбранноеПолеГруппы.Поле, НомерГрафы);
			КонецЦикла;
		Иначе
			ГрафаОтчета = НовыйГрафаОтчета();
			ГрафаОтчета.ИмяГрафы = НомерГрафы;
			ГрафыОтчета.Добавить(ГрафаОтчета);
			ВыбранноеПоле.Заголовок = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
				ВыбранноеПоле.Заголовок,
				НомераКолонок);
			НомераКолонок.Вставить(ВыбранноеПоле.Поле, НомерГрафы);
		КонецЕсли;
		НомерГрафы = НомерГрафы + 1;
	КонецЦикла;
	
	Контекст.ГрафыОтчета = ГрафыОтчета;
	
КонецПроцедуры

// В процедуре можно уточнить особенности вывода данных в отчет.
//
// Параметры:
//  Контекст - Структура
//  МакетКомпоновки - МакетКомпоновкиДанных - описание выводимых данных.
//
//@skip-check redundant-export-method
Процедура ПослеКомпоновкиМакета(Контекст, МакетКомпоновки) Экспорт
	
	ПараметрыПоиска = БухгалтерскиеОтчеты.ПараметрыПоискаВТелеМакетаКомпоновки();
	МакетТаблицы = БухгалтерскиеОтчеты.ПодобратьЭлементыИзТелаМакета(
		МакетКомпоновки,
		"ФедеральныйИнвестиционныйВычет",
		ПараметрыПоиска);
	
	// Удалим ячейки ресурсов "Итого"
	МакетИтого = МакетКомпоновки.Макеты[МакетТаблицы.МакетИтоговСтрок];
	ИндексСтрокиИтого = 0;
	МакетИтого.Макет.Удалить(МакетИтого.Макет.Получить(ИндексСтрокиИтого));
	
	// Удалим образовавшуюся лишнюю строку в группировке
	МакетШапкиГруппировок = МакетКомпоновки.Макеты[МакетТаблицы.МакетШапки];
	МакетШапкиГруппировок.Макет.Удалить(МакетШапкиГруппировок.Макет.Получить(МакетШапкиГруппировок.Макет.Количество() - 1));

	СправкиРасчеты.ДобавитьНомераКолонок(МакетШапкиГруппировок, Неопределено, Истина);
	
	ИменаГраф = Новый Массив;
	ИндексГрафы = 0;
	Для Каждого Графа Из Контекст.ГрафыОтчета Цикл
		ИменаГраф.Добавить(Графа.ИмяГрафы);
		ИндексГрафы = ИндексГрафы + 1;
		Если Графа.ВходитВГруппу Тогда
			ЯчейкаГруппыЗаголовка = МакетИтого.Макет[0].Ячейки[ИндексГрафы];
			ЯчейкаГруппыЗаголовка.Оформление.УстановитьЗначениеПараметра(
				"ГоризонтальноеПоложение",
				ГоризонтальноеПоложение.Центр);
			ЯчейкаГруппыЗаголовка.Оформление.УстановитьЗначениеПараметра(
				"Размещение",
				ТипРазмещенияТекстаКомпоновкиДанных.Переносить);
		КонецЕсли;
	КонецЦикла;
		
	СправкиРасчеты.ДобавитьНомераКолонок(МакетИтого, ИменаГраф);
	
КонецПроцедуры

// В процедуре можно изменить табличный документ после вывода в него данных.
// 
// Параметры:
//  Контекст - см. ПустыеПараметрыКомпоновкиОтчета
//  Результат - ТабличныйДокумент - сформированный отчет
// @skip-check redundant-export-method
Процедура ПослеВыводаРезультата(Контекст, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(Контекст.ИдентификаторОтчета, Результат);
	
	Результат.ФиксацияСверху = 0;
	
	Результат.ФиксацияСлева = 0;
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
КонецПроцедуры

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//   ОписаниеОтчета - СтрокаДереваЗначений - Настройки этого отчета,
//      уже сформированные при помощи функции ВариантыОтчетов.ОписаниеОтчета() и готовые к изменению.
//
Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(Метаданные.Подсистемы.Отчеты.Подсистемы.НалогНаПрибыль.Подсистемы.РегистрыПромежуточныхРасчетов, "");
		// Интерфейс 1С:Бухгалтерии 7.7
		ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, Вариант.Имя).Размещение.Вставить(Метаданные.Подсистемы.Интерфейс77НалоговыйУчет.Подсистемы.РегистрыПромежуточныхРасчетов, "");
	КонецЦикла;
	
КонецПроцедуры

//Процедура используется подсистемой варианты отчетов
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя,Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает коллекцию вариантов настроек отчета.
// 
// Возвращаемое значение:
//  Массив из Структура:
//    * Ключ - Строка - имя настройки
//    * Значение - Строка - представление настройки
//
Функция ВариантыНастроек() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление","ФедеральныйИнвестиционныйВычет", "2.06. Федеральный инвестиционный налоговый вычет"));
	
	Возврат Массив;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УточнитьНеиспользуемыеПоля(НеиспользуемыеПоля, Контекст, ВсяОрганизация)
	
	// Поле "Организация, применившая вычет" отображается, если:
	// 1) за отчетный период есть передача вычета
	//    или
	// 2) отчет сформирован без указания организации
	// Поле "Организация, осуществившая расходы" отображается, если:
	// 1) в отчетном периоде был получен вычет от другой организации
	//    или
	// 2) полученый вычет остался с прошлого периода
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВидОпераций.ЭтоПередача) КАК ОрганизацияПрименяющаяВычет,
	|	МАКСИМУМ(ВидОпераций.ЭтоПолучение) КАК ОрганизацияОсуществившаяРасходы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОперацияСИнвестиционнымВычетом.ВидОперации = &ВидОперацииПередача КАК ЭтоПередача,
	|		ОперацияСИнвестиционнымВычетом.ВидОперации = &ВидОперацииПолучение КАК ЭтоПолучение
	|	ИЗ
	|		Документ.ОперацияСИнвестиционнымВычетом КАК ОперацияСИнвестиционнымВычетом
	|	ГДЕ
	|		ОперацияСИнвестиционнымВычетом.Организация В(&ВсяОрганизация)
	|		И ОперацияСИнвестиционнымВычетом.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|		И ОперацияСИнвестиционнымВычетом.Проведен
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ОперацияСИнвестиционнымВычетом.ВидОперации = &ВидОперацииПередача,
	|		ОперацияСИнвестиционнымВычетом.ВидОперации = &ВидОперацииПолучение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЛОЖЬ,
	|		ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Документ.ОперацияСИнвестиционнымВычетом).ВидОперации = &ВидОперацииПолучение
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&НачалоПериода,
	|				Счет = &СчетУчетаИнвестиционныйВычет,
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыИнвестиционногоВычета),
	|				Организация В (&ВсяОрганизация)
	|					И Субконто1 ССЫЛКА Документ.ОперацияСИнвестиционнымВычетом) КАК ХозрасчетныйОстатки
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Документ.ОперацияСИнвестиционнымВычетом).ВидОперации = &ВидОперацииПолучение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИСТИНА,
	|		ЛОЖЬ
	|	ГДЕ
	|		НЕ &УстановленаОрганизация) КАК ВидОпераций";

	Запрос.УстановитьПараметр("ВсяОрганизация", ВсяОрганизация);
	Запрос.УстановитьПараметр("УстановленаОрганизация", ЗначениеЗаполнено(Контекст.Организация));
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Контекст.НачалоПериода));
	КонецПериода = КонецДня(Контекст.КонецПериода);
	Если Не ЗначениеЗаполнено(Контекст.КонецПериода) Тогда
		КонецПериода = Дата(3999, 12, 31);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Запрос.УстановитьПараметр("ВидОперацииПередача", Перечисления.ВидыОперацийСИнвестиционнымВычетом.Передача);
	Запрос.УстановитьПараметр("ВидОперацииПолучение", Перечисления.ВидыОперацийСИнвестиционнымВычетом.Получение);

	Запрос.УстановитьПараметр("СчетУчетаИнвестиционныйВычет", ПланыСчетов.Хозрасчетный.ФедеральныйИнвестиционныйНалоговыйВычет);

	Запрос.Текст = ТекстЗапроса;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ИндексПоля = НеиспользуемыеПоля.Найти(Колонка.Имя);
		Если Выборка[Колонка.Имя] = Истина И ИндексПоля <> Неопределено Тогда
			НеиспользуемыеПоля.Удалить(ИндексПоля);
		КонецЕсли;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция НовыйГрафаОтчета()
	ГрафаОтчета = Новый Структура;
	ГрафаОтчета.Вставить("ВходитВГруппу", Ложь);
	ГрафаОтчета.Вставить("ИмяГрафы", "");
	Возврат ГрафаОтчета;
КонецФункции

Функция ПолучитьТекстЗаголовка(Контекст)
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		Контекст.НачалоПериода,
		Контекст.КонецПериода,
		Истина);
		
	ОсновнойЗаголовок = СправкиРасчетыКлиентСервер.ТекстЗаголовка(НСтр("ru = 'Регистр налогового учета'"), ПредставлениеПериода);
	
	СправкиРасчеты.ДополнитьПериодОтчетаПримечанием(ПредставлениеПериода, Контекст);

	ЭлементыЗаголовка = Новый Массив;
	ЭлементыЗаголовка.Добавить(ОсновнойЗаголовок);
	ЭлементыЗаголовка.Добавить(Контекст.НазваниеРегистра);
	
	Возврат СтрСоединить(ЭлементыЗаголовка, Символы.ПС);
	
КонецФункции

#КонецОбласти

#КонецЕсли