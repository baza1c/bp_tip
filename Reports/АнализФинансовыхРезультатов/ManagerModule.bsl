#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает внешние наборы данных, которые используются при компоновке макета.
// 
// Параметры:
//   ПараметрыОтчета - Структура - Параметры исполнения отчета (См. ПустыеПараметрыКомпоновкиОтчета()).
//   МакетКомпоновки - МакетКомпоновкиДанных - сформированный макет компоновки данных.
// 
// Возвращаемое значение:
//   Структура - внешние наборы данных:
// *   ДанныеРаспределения - ТаблицаЗначений - преобразованная таблица коэффициентов распределения.
//
Функция ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки) Экспорт
	
	Подразделение        = Неопределено;
	Организация          = Неопределено;
	ОрганизацияФормулаСравнения = "";
	ПодразделениеФормулаСравнения = "";

	Для Каждого ЭлементОтбора Из ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор.Элементы Цикл
		
		Если Не ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация") Тогда
			
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.Организации.ПустаяСсылка();
			КонецЕсли;
			
			Организация = ЭлементОтбора.ПравоеЗначение;
			ОрганизацияФормулаСравнения = ОтчетыРуководителя.ФормулаСравнения(ЭлементОтбора.ВидСравнения);
				
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") Тогда
			
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			
			Подразделение = ЭлементОтбора.ПравоеЗначение;
			ПодразделениеФормулаСравнения = ОтчетыРуководителя.ФормулаСравнения(ЭлементОтбора.ВидСравнения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Организация) И Организация = Неопределено Тогда
		
		Организация = ПараметрыОтчета.Организация;
		ОрганизацияФормулаСравнения = ОтчетыРуководителя.ФормулаСравнения(ВидСравненияКомпоновкиДанных.Равно);
		
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация", Организация);
	ПараметрыОтбора.Вставить("ОрганизацияФормулаСравнения", ОрганизацияФормулаСравнения); 
	ПараметрыОтбора.Вставить("ВключатьОбособленныеПодразделения", ПараметрыОтчета.ВключатьОбособленныеПодразделения);
	ПараметрыОтбора.Вставить("Подразделение", Подразделение);
	ПараметрыОтбора.Вставить("ПодразделениеФормулаСравнения", ПодразделениеФормулаСравнения);

	ДанныеЗаТекущийПериод = ПодготовитьДанные(ПараметрыОтчета, ПараметрыОтбора, Ложь);
	
	ВнешниеИсточники = Новый Структура;
	ВнешниеИсточники.Вставить("ВыручкаСебестоимость", ДанныеЗаТекущийПериод.ВыручкаСебестоимость);
	ВнешниеИсточники.Вставить("ПрочиеПоказатели", ДанныеЗаТекущийПериод.ПрочиеПоказатели);
	Если ПараметрыОтчета.Периодичность = 0 Тогда 
		ДанныеЗаПрошлыйПериод = ПодготовитьДанные(ПараметрыОтчета, ПараметрыОтбора, Истина);
		ВнешниеИсточники.Вставить("ВыручкаСебестоимостьПрошлыйПериод", ДанныеЗаПрошлыйПериод.ВыручкаСебестоимость);
		ВнешниеИсточники.Вставить("ПрочиеПоказателиПрошлыйПериод", ДанныеЗаПрошлыйПериод.ПрочиеПоказатели);
	Иначе
		ВнешниеИсточники.Вставить("ВыручкаСебестоимостьПрошлыйПериод", ТаблицаРезультат());
		ВнешниеИсточники.Вставить("ПрочиеПоказателиПрошлыйПериод", ТаблицаРезультат());
	КонецЕсли;
	
	Возврат ВнешниеИсточники;

КонецФункции

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//   Структура - флаги, задающие необходимость дополнительных действий.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета",          Истина);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",           Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",               Истина);
	Результат.Вставить("ИспользоватьРасширенныеПараметрыРасшифровки", Истина);
	Результат.Вставить("ИспользоватьВнешниеНаборыДанных",             Истина);
	
	Возврат Результат;
	
КонецФункции

// Формирует текст, выводимый в заголовке отчета.
//
// Параметры:
//   ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//
// Возвращаемое значение:
//   Строка - текст заголовка с учётом периода.
//
Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Анализ финансовых результатов%1'"),
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, КонецДня(ПараметрыОтчета.КонецПериода)));
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//   ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//   Схема - СхемаКомпоновкиДанных - описание получаемых данных.
//   КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	// Установка параметров схемы компоновки
		
	РазрезПродаж = ПараметрыОтчета.РазрезПродаж;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ПоказательДоход", Новый ХранилищеЗначения(БиблиотекаКартинок.ПоказательДоход));
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ПоказательРасход", Новый ХранилищеЗначения(БиблиотекаКартинок.ПоказательРасход));
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ПоказательФормула", Новый ХранилищеЗначения(БиблиотекаКартинок.ПоказательФормула));
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"СуммаДоход", Новый ХранилищеЗначения(БиблиотекаКартинок.СуммаЗеленая));
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"СуммаРасход", Новый ХранилищеЗначения(БиблиотекаКартинок.СуммаКрасная));
	Если ПараметрыОтчета.Периодичность = 0 Тогда
		НачальныйПериод = Год(ПараметрыОтчета.НачалоПериода);
	Иначе
		НачальныйПериод = НачалоМесяца(ПараметрыОтчета.НачалоПериода);
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"НачальныйПериод", НачальныйПериод);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ВыручкаПоНоменклатуре", РазрезПродаж = 2);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"КартинкаИнформация", Новый ХранилищеЗначения(БиблиотекаКартинок.ИнформацияБЭД));
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"Периодичность", ПараметрыОтчета.Периодичность);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ПустоеЗначение", ОтчетыРуководителя.ПустоеЗначение());

	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"РаботыИУслуги", ОтчетыРуководителя.РаботыИУслуги());

	// Формирование структуры отчета
	
	Таблица = Неопределено;
	
	Для Каждого ЭлементСтруктуры Из КомпоновщикНастроек.Настройки.Структура Цикл
		Если ЭлементСтруктуры.Имя = "Таблица" Тогда
			Таблица = ЭлементСтруктуры;
		КонецЕсли;
	КонецЦикла;
		
	Если Таблица <> Неопределено Тогда
		
		Таблица.Колонки.Очистить();
		ГруппировкаПериод = Таблица.Колонки.Добавить();
		
		ЗаполнитьГруппировкуПериод(КомпоновщикНастроек, ПараметрыОтчета, ГруппировкаПериод);
		
		// Группировка
		Таблица.Строки.Очистить();
		Группировка = Таблица.Строки;
		
		Показатель = Новый ПолеКомпоновкиДанных("Показатель");
		Аналитика  = Новый ПолеКомпоновкиДанных("Аналитика");
		Порядок    = Новый ПолеКомпоновкиДанных("Порядок");
		ВидСтроки  = Новый ПолеКомпоновкиДанных("ВидСтроки");
		
		ЭтоПерваяГруппировка = Истина;
		Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл 
			
			Если Не ПолеВыбраннойГруппировки.Использование Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПолеВыбраннойГруппировки.Поле = "Показатель" Тогда
				
				Группировка = ДобавитьГруппировку(ПолеВыбраннойГруппировки, Группировка, ЭтоПерваяГруппировка);

				Если ЭтоПерваяГруппировка Тогда
					ЭлементОформления = Группировка.УсловноеОформление.Элементы.Добавить();
					КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "Отклонение");
					ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", "> 1000%");
					ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных .Недоступный;
					
					ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
						ЭлементОформления.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
				
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
						ГруппаОтбора,
						"Отклонение",
						1000,
						ВидСравненияКомпоновкиДанных.Больше);
				
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
						ГруппаОтбора,
						"Отклонение",
						-1000,
						ВидСравненияКомпоновкиДанных.Меньше);
 						
						ЭтоПерваяГруппировка = Ложь;
					Иначе
						Группировка.Имя = "Показатель";
				КонецЕсли;
		
				ПолеГруппировкиВидСтроки      = Группировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				ПолеГруппировкиВидСтроки.Поле = ВидСтроки;
				
				ПолеГруппировкиПорядок      = Группировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
				ПолеГруппировкиПорядок.Поле = Порядок;
				
				Группировка.Выбор.Элементы.Очистить();
				
				ЭлементыВыбора      = Группировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ЭлементыВыбора.Поле = Показатель;
				
				ЭлементыВыбора      = Группировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ЭлементыВыбора.Поле = Новый ПолеКомпоновкиДанных("Сумма");
				
				ЭлементыВыбора      = Группировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
				ЭлементыВыбора.Поле = Новый ПолеКомпоновкиДанных("Отклонение");
				ЭлементыВыбора.Заголовок = "Изменение";

				Группировка.Порядок.Элементы.Очистить();

				ДобавитьПорядок(Группировка, Порядок);
				
				ДобавитьОформлениеИтога(Группировка, 4);
				
				ДобавитьОформлениеИтога(Группировка, 5);
				
			ИначеЕсли ПолеВыбраннойГруппировки.Поле = "Аналитика" Тогда 
				
				// Аналитика продаж 
				
				Если РазрезПродаж = 1 Тогда
					
					ГруппировкаАналитика = ДобавитьГруппировку("Аналитика", Группировка, ЭтоПерваяГруппировка);
					
					ГруппировкаАналитика.Порядок.Элементы.Очистить();
					ДобавитьПорядок(ГруппировкаАналитика, Аналитика);

					ГруппировкаАналитика.Отбор.Элементы.Очистить();

					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаАналитика.Отбор, "Порядок", 3, 
						ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
					
					// Договор
					ГруппировкаАналитика = ДобавитьГруппировку("Договор", ГруппировкаАналитика, ЭтоПерваяГруппировка);
					
					ГруппировкаАналитика.Порядок.Элементы.Очистить();
					ДобавитьПорядок(ГруппировкаАналитика, Новый ПолеКомпоновкиДанных("Договор"));
					
					ГруппировкаАналитика.Отбор.Элементы.Очистить();
					
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаАналитика.Отбор, "Договор",,
						ВидСравненияКомпоновкиДанных.Заполнено);

				Иначе

					ГруппировкаАналитика = ДобавитьГруппировку("Аналитика", Группировка, ЭтоПерваяГруппировка);
					
					ГруппировкаАналитика.Имя = "Аналитика";
					ГруппировкаАналитика.Порядок.Элементы.Очистить();
					ДобавитьПорядок(ГруппировкаАналитика, Аналитика);
					
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаАналитика.Отбор, "Порядок", 3, 
						ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);

				КонецЕсли;
				
				// Аналитика
				ГруппировкаАналитика = ДобавитьГруппировку("Аналитика", Группировка, ЭтоПерваяГруппировка);

				ГруппировкаАналитика.Отбор.Элементы.Очистить();

				ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
					ГруппировкаАналитика.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
				
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаОтбора, "Порядок", 3, ВидСравненияКомпоновкиДанных.Больше);
				
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаОтбора, "Аналитика",, ВидСравненияКомпоновкиДанных.Заполнено);
				
				// РеализуемыйАктив
				
				ГруппировкаАналитика = ДобавитьГруппировку("РеализуемыйАктив", ГруппировкаАналитика, ЭтоПерваяГруппировка);
				
				ГруппировкаАналитика.Порядок.Элементы.Очистить();
				ДобавитьПорядок(ГруппировкаАналитика, Новый ПолеКомпоновкиДанных("РеализуемыйАктив"));
				
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппировкаАналитика.Отбор, "РеализуемыйАктив",,
					ВидСравненияКомпоновкиДанных.Заполнено);

			Иначе
				
				Если ГруппировкаАналитика <> Неопределено Тогда
				
					ГруппировкаАналитика = ДобавитьГруппировку(ПолеВыбраннойГруппировки, ГруппировкаАналитика, ЭтоПерваяГруппировка);
					
				КонецЕсли;
				
				Группировка = ДобавитьГруппировку(ПолеВыбраннойГруппировки, Группировка, ЭтоПерваяГруппировка); 
				
				Если ПолеВыбраннойГруппировки.Поле = "Организация"
					Или ПолеВыбраннойГруппировки.Поле = "Подразделение" Тогда
					
					Группировка.Выбор.Элементы.Очистить();
					ЭлементыВыбора      = Группировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
					ЭлементыВыбора.Поле = Новый ПолеКомпоновкиДанных(ПолеВыбраннойГруппировки.Поле);
					
					ЭлементыВыбора      = Группировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
					ЭлементыВыбора.Поле = Новый ПолеКомпоновкиДанных("ИтоговаяСумма");
					
				КонецЕсли;
				
				Если ЭтоПерваяГруппировка Тогда
					ЭлементОфрмления = Группировка.УсловноеОформление.Элементы.Добавить();
					КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОфрмления.Поля, "Отклонение");
					ЭлементОфрмления.Оформление.УстановитьЗначениеПараметра("Текст", "> 1000%");
					ЭлементОфрмления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных .Недоступный;
					
					ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
						ЭлементОфрмления.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
					
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
						ГруппаОтбора,
						"Отклонение",
						1000,
						ВидСравненияКомпоновкиДанных.Больше);
					
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
						ГруппаОтбора,
						"Отклонение",
						-1000,
						ВидСравненияКомпоновкиДанных.Меньше);
					
					ЭтоПерваяГруппировка = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Дополнительные данные.
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	// Отбор по организации.
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
КонецПроцедуры

// В процедуре можно изменить табличный документ после вывода в него данных.
//
// Параметры:
//   ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//   Результат - ТабличныйДокумент - сформированный отчет.
//
Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	// Отклонение
	УдаляемаяКолонка = Результат.НайтиТекст("Удалить", , , , Истина);
	Если УдаляемаяКолонка <> Неопределено Тогда
		Область = Результат.Область(СтрШаблон("R%1C%2:R%3C%4",
		УдаляемаяКолонка.Верх - 1, УдаляемаяКолонка.Лево - 1, УдаляемаяКолонка.Верх - 1, УдаляемаяКолонка.Лево));
		
		Область.Разъединить();
		
		Результат.УдалитьОбласть(Результат.Область(СтрШаблон("C%1", УдаляемаяКолонка.Лево)), ТипСмещенияТабличногоДокумента.ПоГоризонтали);
		
	КонецЕсли;

	КолонкаОтклонение = Результат.НайтиТекст("Изменение", , , , Истина);
	Если КолонкаОтклонение <> Неопределено Тогда
		ОтклонениеКолонка = КолонкаОтклонение.Лево - 1;
		ОтклонениеСтрока = КолонкаОтклонение.Верх - 1;
		
		Область = Результат.Область(СтрШаблон("R%1C%2:R%3C%4",
		ОтклонениеСтрока, ОтклонениеКолонка - 1, ОтклонениеСтрока, ОтклонениеКолонка));
		
		Область.Разъединить();
		
		ОбъединяемаяОбласть = Результат.Область(СтрШаблон("R%1C%2:R%3C%4", 
			ОтклонениеСтрока, ОтклонениеКолонка, ОтклонениеСтрока + 1, ОтклонениеКолонка + 1));
	
		ОбъединяемаяОбласть.Объединить();
		
		Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
		ОбъединяемаяОбласть.Обвести(Линия, Линия, Линия, Линия);
	КонецЕсли;

	КолонкаСумма = Результат.НайтиТекст("Сумма", , , , Истина);

	Пока КолонкаСумма <> Неопределено Цикл
		
		ОбъединяемаяОбласть = Результат.Область(СтрШаблон("R%1C%2:R%3C%4",
		КолонкаСумма.Верх - 1, КолонкаСумма.Лево, КолонкаСумма.Верх, КолонкаСумма.Лево));
		
		ОбъединяемаяОбласть.Объединить();
		
		КолонкаСумма = Результат.НайтиТекст("Сумма", , , , Истина);
		
	КонецЦикла;
	
	Результат.ФиксацияСлева = 4;
	
	Если ПараметрыОтчета.Периодичность = 0 Тогда
		Результат.ОриентацияСтраницы =  ОриентацияСтраницы.Портрет;
	Иначе
		Результат.ОриентацияСтраницы =  ОриентацияСтраницы.Ландшафт;
	КонецЕсли;
	
	// Свернем все группировки
	Уровень = Результат.КоличествоУровнейГруппировокСтрок() - 1;
	Пока Уровень >= 0 Цикл
		Результат.ПоказатьУровеньГруппировокСтрок(Уровень);
		Уровень = Уровень - 1;
	КонецЦикла;
	
	БыстрыеНастройкиОтчетовСервер.ВывестиПримечанияОкругления(ПараметрыОтчета, Результат);

КонецПроцедуры

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//   ОписаниеОтчета - СтрокаДереваЗначений - Настройки этого отчета,
//      уже сформированные при помощи функции ВариантыОтчетов.ОписаниеОтчета() и готовые к изменению.
//
Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "АнализФинансовыхРезультатовЗаПериод");
	
	ОписаниеВарианта.Размещение.Вставить(Метаданные.Подсистемы.Руководителю.Подсистемы.Анализ, "");
	
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
КонецПроцедуры

// Заполняет описание настроек отчета в коллекции Настройки.
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя, Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет параметры расшифровки ячейки отчета.
//
// Параметры:
//   Адрес - Строка - Адрес временного хранилища с данными расшифровки отчета.
//   Расшифровка - Произвольный - Значения полей расшифровки.
//   ПараметрыРасшифровки - Структура - Коллеккция параметров расшифроки, которую требуется заполнить. 
//      Подробнее см. БухгалтерскиеОтчетыВызовСервера.ПолучитьМассивПолейРасшифровки()
//
Процедура ЗаполнитьПараметрыРасшифровкиОтчета(Адрес, Расшифровка, ПараметрыРасшифровки) Экспорт

	// Инициализируем список пунктов меню
	СписокПунктовМеню = Новый СписокЗначений();
	
	// Заполниим соответствие полей которые мы хотим получить из данных расшифровки
	СоответствиеПолей = Новый Соответствие;
	ДанныеОтчета = ПолучитьИзВременногоХранилища(Адрес);
	ДеталиРасшифровки = Новый Структура;
	ДанныеДляРасчета = Неопределено;
	ЭтоРасчетныйПоказатель = Ложь;
	
	ЗначениеРасшифровки = ДанныеОтчета.ДанныеРасшифровки.Элементы[Расшифровка];
	ПоляРасшифровки = ЗначениеРасшифровки.ПолучитьПоля();
	
	ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Ложь);
	Показатель = ПоляРасшифровки.Найти("Показатель");
	Если Показатель = Неопределено Тогда
		Показатель = ПоляРасшифровки.Найти("ДоговорРасшифровка");
	КонецЕсли;
	
	Если Показатель = Неопределено Тогда
		Показатель = Новый Структура("Поле, Значение", "Показатель", ПоказательЧистаяПрибыль());
		ЭтоРасчетныйПоказатель = 
			Отчеты.РасшифровкаПоказателейАнализФинансовыхРезультатов.ЭтоРасчетныйПоказатель(ПоказательЧистаяПрибыль());
		ДеталиРасшифровки.Вставить(Показатель.Поле, Показатель.Значение);
	КонецЕсли;

	Если Показатель <> Неопределено 
		И (Показатель.Значение = "Всего доходов"
		Или Показатель.Значение = "Всего расходов"
		Или Показатель.Значение = "Изменение отложенных налоговых обязательств") Тогда 
		
		ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
		
		Сумма = ПоляРасшифровки.Найти("Сумма");
		Если Сумма <> Неопределено Тогда
			ПараметрыРасшифровки.Вставить("Значение", Сумма.Значение);
		Иначе
			ПараметрыРасшифровки.Вставить("Значение", Показатель.Значение);
		КонецЕсли;
	Иначе
		
		Если ТипЗнч(ЗначениеРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
			Для Каждого ПолеРасшифровки ИЗ ПоляРасшифровки Цикл
				Если ЗначениеЗаполнено(ПолеРасшифровки.Значение) Тогда 
					
					Если ПолеРасшифровки.Значение = "Розничные покупатели"
						Или ПолеРасшифровки.Поле = "Год"
						Или ПолеРасшифровки.Поле = "Месяц" Тогда
						ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
						
					ИначеЕсли ПоляРасшифровки.Количество() > 1
						И ЭтоРасшифровкаПоказателей(ПолеРасшифровки.Поле) Тогда
						ДеталиРасшифровки.Вставить(СтрЗаменить(ПолеРасшифровки.Поле, "Расшифровка", ""), ПолеРасшифровки.Значение);
						
						Если ПолеРасшифровки.Поле = "Показатель" Тогда
							ДанныеДляРасчета = ДанныеОтчета.Объект.ДанныеДляРасчета[ПолеРасшифровки.Значение];
							ЭтоРасчетныйПоказатель = 
							Отчеты.РасшифровкаПоказателейАнализФинансовыхРезультатов.ЭтоРасчетныйПоказатель(ПолеРасшифровки.Значение);
						КонецЕсли;
					Иначе
						ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
						ПараметрыРасшифровки.Вставить("Значение", ПолеРасшифровки.Значение);
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	Если ДанныеДляРасчета <> Неопределено 
		И ДеталиРасшифровки.Свойство("Период") Тогда
		ДанныеДляРасчетаРасшифровки = ДанныеДляРасчета[Год(ДеталиРасшифровки.Период)];
		Если Не ДеталиРасшифровки.Свойство("Аналитика") Или ДеталиРасшифровки.Аналитика.Пустая() Тогда
			ДеталиРасшифровки.Вставить("ДанныеДляРасчета", ДанныеДляРасчетаРасшифровки);
		Иначе
			Отбор = Новый Структура;
			Если Показатель <> Неопределено И ЭтоАнализВыручки(Показатель.Значение) Тогда
				Если ТипЗнч(ДеталиРасшифровки.Аналитика) = Тип("СправочникСсылка.Контрагенты") Тогда
					Отбор.Вставить("Контрагент", ДеталиРасшифровки.Аналитика);
				ИначеЕсли ТипЗнч(ДеталиРасшифровки.Аналитика) = Тип("СправочникСсылка.Номенклатура") Тогда
					Отбор.Вставить("Номенклатура", ДеталиРасшифровки.Аналитика);
				ИначеЕсли ТипЗнч(ДеталиРасшифровки.Аналитика) = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
					Отбор.Вставить("НоменклатурнаяГруппа", ДеталиРасшифровки.Аналитика);
				КонецЕсли;
			Иначе	
				Отбор.Вставить("Аналитика", ДеталиРасшифровки.Аналитика);
			КонецЕсли;
			НайденныеДанныеДляРасчетаРасшифровки = ДанныеДляРасчетаРасшифровки.ПоказателиДляРасчета.НайтиСтроки(Отбор);
			Если НайденныеДанныеДляРасчетаРасшифровки.Количество() > 0 Тогда
				НовыеДанныеДляРасчета = ТаблицаПоказателейДляРасшифровки();
				Для Каждого СтрокаТаблицы Из НайденныеДанныеДляРасчетаРасшифровки Цикл
					ЗаполнитьЗначенияСвойств(НовыеДанныеДляРасчета.Добавить(), СтрокаТаблицы);
				КонецЦикла;
			КонецЕсли;
			ДанныеДляРасчетаРасшифровкиСОтбором = Новый Структура;
			ДанныеДляРасчетаРасшифровкиСОтбором.Вставить("Организация", ДанныеДляРасчетаРасшифровки.Организация);
			ДанныеДляРасчетаРасшифровкиСОтбором.Вставить("ВключатьОбособленныеПодразделения",
				ДанныеОтчета.Объект.ВключатьОбособленныеПодразделения);
			ДанныеДляРасчетаРасшифровкиСОтбором.Вставить("Подразделение", ДанныеДляРасчетаРасшифровки.Подразделение);
			ДанныеДляРасчетаРасшифровкиСОтбором.Вставить("ПоказателиДляРасчета", НовыеДанныеДляРасчета);
			ДеталиРасшифровки.Вставить("ДанныеДляРасчета", ДанныеДляРасчетаРасшифровкиСОтбором);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеОтчета = Неопределено Тогда
		ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
		Возврат;
	КонецЕсли;
	
	// Прежде всего интересны данные группировочных полей
	Для Каждого Группировка Из ДанныеОтчета.Объект.Группировка Цикл
		
		Если Группировка.Использование Тогда
			
			СоответствиеПолей.Вставить(Группировка.Поле);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Инициализация пользовательских настроек
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("РежимРасшифровки",     Истина);
	ДополнительныеСвойства.Вставить("Организация",          ДанныеОтчета.Объект.Организация);
	ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения",
		ДанныеОтчета.Объект.ВключатьОбособленныеПодразделения);
	ДополнительныеСвойства.Вставить("НачалоПериода",        ДанныеОтчета.Объект.НачалоПериода);
	ДополнительныеСвойства.Вставить("КонецПериода",         ДанныеОтчета.Объект.КонецПериода);
	ДополнительныеСвойства.Вставить("ВыводитьЗаголовок",    ДанныеОтчета.Объект.ВыводитьЗаголовок);
	ДополнительныеСвойства.Вставить("ВыводитьПодвал",       ДанныеОтчета.Объект.ВыводитьПодвал);
	ДополнительныеСвойства.Вставить("МакетОформления",      ДанныеОтчета.Объект.МакетОформления);
	ДополнительныеСвойства.Вставить("ОкруглениеСумм",       ДанныеОтчета.Объект.ОкруглениеСумм);
	ДополнительныеСвойства.Вставить("ОтрицательноеКрасным", ДанныеОтчета.Объект.ОтрицательноеКрасным);
	
	Если ДеталиРасшифровки.Свойство("Период")Тогда
		
		Если ДанныеОтчета.Объект.Периодичность = 0 Тогда
			
			Если НачалоМесяца(ДеталиРасшифровки.Период) < НачалоМесяца(ДанныеОтчета.Объект.НачалоПериода) Тогда
				ДополнительныеСвойства.Вставить("НачалоПериода", НачалоМесяца(ДобавитьМесяц(ДанныеОтчета.Объект.НачалоПериода, -12)));
				ДополнительныеСвойства.Вставить("КонецПериода",  КонецМесяца(ДобавитьМесяц(ДанныеОтчета.Объект.КонецПериода, -12)));
			Иначе
				ДополнительныеСвойства.Вставить("НачалоПериода", ДанныеОтчета.Объект.НачалоПериода);
				ДополнительныеСвойства.Вставить("КонецПериода",  ДанныеОтчета.Объект.КонецПериода);
			КонецЕсли;
			
		Иначе
			
			ДополнительныеСвойства.Вставить("НачалоПериода", НачалоМесяца(ДеталиРасшифровки.Период));
			ДополнительныеСвойства.Вставить("КонецПериода", КонецМесяца(ДеталиРасшифровки.Период));
			
		КонецЕсли;
		
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ВыводитьЗаголовок",            ДанныеОтчета.Объект.ВыводитьЗаголовок);
	ДополнительныеСвойства.Вставить("ВыводитьПодвал",               ДанныеОтчета.Объект.ВыводитьПодвал);
	ДополнительныеСвойства.Вставить("МакетОформления",              ДанныеОтчета.Объект.МакетОформления);
	ДополнительныеСвойства.Вставить("Периодичность",                ДанныеОтчета.Объект.Периодичность);
	ДополнительныеСвойства.Вставить("РазрезПродаж",                 ДанныеОтчета.Объект.РазрезПродаж);
	ДополнительныеСвойства.Вставить("ОчищатьТаблицуГруппировок",    Истина);
	ДополнительныеСвойства.Вставить("ОчищатьДополнительныеПоля",    Истина);
	Если Показатель <> Неопределено И ЭтоАнализВыручки(Показатель.Значение) Тогда
		ДополнительныеСвойства.Вставить("КлючТекущегоВарианта",             "ВаловаяПрибыль");
		ДополнительныеСвойства.Вставить("ВыводитьДиаграмму",         Ложь);
	Иначе
		Если ЭтоРасчетныйПоказатель Тогда
			ДополнительныеСвойства.Вставить("КлючВарианта",             "РасшифровкаРасчетныхПоказателей");
		Иначе
			ДополнительныеСвойства.Вставить("КлючВарианта",             "РасшифровкаПоказателейАнализаФинансовыхРезультатов");
		КонецЕсли;
	КонецЕсли;
	ОтборПоЗначениямРасшифровки = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ОтборПоЗначениямРасшифровки.ИдентификаторПользовательскойНастройки = "Отбор";
	
	// Получаем структуру полей доступных в расшифровке
	Данные_Расшифровки = БухгалтерскиеОтчеты.ПолучитьДанныеРасшифровки(ДанныеОтчета.ДанныеРасшифровки, СоответствиеПолей, Расшифровка);
	
	// Формируем группировки нового отчета.
	Группировка = Новый Массив();
	ЕстьГруппировкаПоПодразделению = Ложь;
	ДоступныеПоляОтбора = Новый Массив;
	
	Если Показатель <> Неопределено Тогда
		
		Если ЭтоАнализВыручки(Показатель.Значение) Тогда
			
			РазрезПродаж = ДанныеОтчета.Объект.РазрезПродаж;
			
			Если РазрезПродаж = 1 Тогда
				
				СтрокаДляРасшифровки = СтрокаДляРасшифровки();
				СтрокаДляРасшифровки.Использование = Истина;
				СтрокаДляРасшифровки.Поле = "Контрагент";
				СтрокаДляРасшифровки.Представление = "Покупатель";
				СтрокаДляРасшифровки.ТипГруппировки = 1;
				Группировка.Добавить(СтрокаДляРасшифровки);
				
				СтрокаДляРасшифровки = СтрокаДляРасшифровки();
				СтрокаДляРасшифровки.Использование = Истина;
				СтрокаДляРасшифровки.Поле = "Договор";
				СтрокаДляРасшифровки.Представление = "Договор";
				СтрокаДляРасшифровки.ТипГруппировки = 0;
				Группировка.Добавить(СтрокаДляРасшифровки);
				
			ИначеЕсли РазрезПродаж = 2 Тогда
				
				СтрокаДляРасшифровки = СтрокаДляРасшифровки();
				СтрокаДляРасшифровки.Использование = Истина;
				СтрокаДляРасшифровки.Поле = "Номенклатура";
				СтрокаДляРасшифровки.Представление = "Номенклатура";
				СтрокаДляРасшифровки.ТипГруппировки = 0;
				Группировка.Добавить(СтрокаДляРасшифровки);
				
			Иначе
				СтрокаДляРасшифровки = СтрокаДляРасшифровки();
				СтрокаДляРасшифровки.Использование = Истина;
				СтрокаДляРасшифровки.Поле = "НоменклатурнаяГруппа";
				СтрокаДляРасшифровки.Представление = "Номенклатурная группа";
				СтрокаДляРасшифровки.ТипГруппировки = 0;
				Группировка.Добавить(СтрокаДляРасшифровки);
				
			КонецЕсли;
			
			СтрокаДляРасшифровки = СтрокаДляРасшифровки();
			СтрокаДляРасшифровки.Использование = Истина;
			СтрокаДляРасшифровки.Поле = "Регистратор";
			СтрокаДляРасшифровки.Представление = "Документ";
			СтрокаДляРасшифровки.ТипГруппировки = 0;
			Группировка.Добавить(СтрокаДляРасшифровки);
			
		Иначе
			
			Счет = УстановитьСчетПоПоказателю(Показатель.Значение);
			
			Если Счет <> Неопределено Тогда 
				СубконтоСчета = Отчеты.РасшифровкаПоказателейАнализФинансовыхРезультатов.СубконтоСчета(Счет);
				КоличествоСубконто = СубконтоСчета.Количество();
				Для НомерСубконто = 1 По КоличествоСубконто Цикл
					СтрокаДляРасшифровки = СтрокаДляРасшифровки();
					СтрокаДляРасшифровки.Поле           = СтрШаблон("Субконто%1", НомерСубконто);
					СтрокаДляРасшифровки.Представление  = СубконтоСчета.Получить(НомерСубконто).Представление;
					СтрокаДляРасшифровки.Использование  = Истина;
					СтрокаДляРасшифровки.ТипГруппировки = 0;
					
					ПолеОтбора = Новый Структура;
					ПолеОтбора.Вставить("ИмяПоля", СтрШаблон("Субконто%1", НомерСубконто));
					ПолеОтбора.Вставить("Тип", СубконтоСчета.Получить(НомерСубконто).Тип);

					ДоступныеПоляОтбора.Добавить(ПолеОтбора);
					
					Группировка.Добавить(СтрокаДляРасшифровки);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого СтрокаГруппировки Из ДанныеОтчета.Объект.Группировка Цикл
			Если СтрокаГруппировки.Использование И СтрокаГруппировки.Поле <> "Вид" Тогда
				Если СтрокаГруппировки.Поле = "Подразделение" Тогда
					ЕстьГруппировкаПоПодразделению = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЕстьГруппировкаПоПодразделению Тогда
		
		СтрокаДляРасшифровки = СтрокаДляРасшифровки();
		СтрокаДляРасшифровки.Использование = Истина;
		СтрокаДляРасшифровки.Поле = "Подразделение";
		СтрокаДляРасшифровки.Представление = "Подразделение";
		СтрокаДляРасшифровки.ТипГруппировки = 0;
		Группировка.Вставить(0, СтрокаДляРасшифровки);
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("Группировка", Группировка);

	Если Показатель <> Неопределено И ЭтоАнализВыручки(Показатель.Значение) Тогда
		ДополнительныеСвойства.Вставить("Группировка", Группировка);
	КонецЕсли;
	// Формируем отборы нового отчета.
	Для Каждого ЗначениеРасшифровки Из Данные_Расшифровки Цикл

		Если Показатель <> Неопределено И ЭтоАнализВыручки(Показатель.Значение) Тогда
			
			Если ЗначениеРасшифровки.Ключ = "Аналитика" Тогда
				
				Если ТипЗнч(ЗначениеРасшифровки.Значение) = Тип("СправочникСсылка.Контрагенты") Тогда
					Ключ = "Контрагент";
				ИначеЕсли ТипЗнч(ЗначениеРасшифровки.Значение) = Тип("СправочникСсылка.Номенклатура") Тогда
					Ключ = "Номенклатура";
				ИначеЕсли ТипЗнч(ЗначениеРасшифровки.Значение) = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
					Ключ = "НоменклатурнаяГруппа";
				КонецЕсли; 
				
			ИначеЕсли ЗначениеРасшифровки.Ключ = "Показатель" Тогда
				
				Продолжить;
				
			Иначе
				
				Ключ = ЗначениеРасшифровки.Ключ;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеРасшифровки.Значение = ОтчетыРуководителя.ПустоеЗначение() И Показатель <> Неопределено
			И Показатель.Поле = "Показатель" Тогда
			ЗначениеОтбора = ПустоеЗначениеОтбораПоПоказателю(Показатель.Значение);
		Иначе
			ЗначениеОтбора = ЗначениеРасшифровки.Значение;
		КонецЕсли;
		
		Если ЗначениеРасшифровки.Ключ <> "Показатель" Тогда
			
			Если ЗначениеРасшифровки.Ключ = "Аналитика" Тогда
				Для Каждого ПолеОтбора Из ДоступныеПоляОтбора Цикл 
					Если ПолеОтбора.Тип.СодержитТип(ТипЗнч(ЗначениеОтбора)) Тогда
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ОтборПоЗначениямРасшифровки, ПолеОтбора.ИмяПоля, ЗначениеОтбора);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ОтборПоЗначениямРасшифровки, Ключ, ЗначениеОтбора);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДеталиРасшифровки.Свойство("Договор") 
			И ЗначениеЗаполнено(ДеталиРасшифровки.Договор)
			И Не ДеталиРасшифровки.Договор.Пустая() Тогда
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ОтборПоЗначениямРасшифровки, "Договор", ДеталиРасшифровки.Договор);
		КонецЕсли;
		
	КонецЦикла;
	// Формируем дополнительные поля.
	ДополнительныеПоля = Новый Массив();
	
	Если Показатель <> Неопределено И Не ЭтоАнализВыручки(Показатель.Значение) Тогда
		Для Каждого ДополнительноеПоле Из ДанныеОтчета.Объект.ДополнительныеПоля Цикл
			Если ДополнительноеПоле.Использование Тогда
				
				СтрокаДляРасшифровки = Новый Структура("Использование, Поле, Представление");
				ЗаполнитьЗначенияСвойств(СтрокаДляРасшифровки, ДополнительноеПоле);
				ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
				
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли Показатель <> Неопределено И ЭтоАнализВыручки(Показатель.Значение) Тогда
		
		ДобавитьПоляДляРасшифровкиВыручки(ДополнительныеПоля);
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ДополнительныеПоля", ДополнительныеПоля);
	
	Если Не ПараметрыРасшифровки.Свойство("Значение") Тогда
		
		НастройкиРасшифровки = Новый Структура();

		Если Показатель <> Неопределено И ЭтоАнализВыручки(Показатель.Значение) Тогда
			СписокПунктовМеню.Добавить("ВаловаяПрибыль", 
				НСтр("ru='Анализ продаж'"));
			
			НастройкиРасшифровки.Вставить("ВаловаяПрибыль", ПользовательскиеНастройки);
		Иначе
			СписокПунктовМеню.Добавить("РасшифровкаПоказателейАнализФинансовыхРезультатов", 
				НСтр("ru='Расшифровка показателей анализа финансовых результатов'"));
			
			НастройкиРасшифровки.Вставить("РасшифровкаПоказателейАнализФинансовыхРезультатов", ПользовательскиеНастройки);
		КонецЕсли;
		ДанныеОтчета.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
		ДанныеОтчета.Вставить("ДеталиРасшифровки", ДеталиРасшифровки);
	
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(ДанныеОтчета, Адрес);
	
	ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
	
КонецПроцедуры

// Возвращает коллекцию вариантов настроек отчета.
//
Функция ВариантыНастроек() Экспорт

	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление", "АнализФинансовыхРезультатовЗаПериод", НСтр("ru = 'Анализ финансовых результатов за период'")));
	Массив.Добавить(Новый Структура("Имя, Представление", "АнализФинансовыхРезультатовВДинамике", НСтр("ru = 'Анализ финансовых результатов по месяцам'")));
	
	Возврат Массив;
	
КонецФункции

// Возвращает набор параметров, которые необходимо сохранять в рассылке отчетов.
// Значения параметров используются при формировании отчета в рассылке.
//
// Возвращаемое значение:
//   Структура - структура настроек, сохраняемых в рассылке с неинициализированными значениями.
//
Функция НастройкиОтчетаСохраняемыеВРассылке() Экспорт
	
	КоллекцияНастроек = Новый Структура;
	КоллекцияНастроек.Вставить("Организация"                      , Справочники.Организации.ПустаяСсылка());
	КоллекцияНастроек.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	КоллекцияНастроек.Вставить("Периодичность"                    , 0);
	КоллекцияНастроек.Вставить("РазрезПродаж"                     , 0);
	КоллекцияНастроек.Вставить("РазмещениеДополнительныхПолей"    , 0);
	КоллекцияНастроек.Вставить("Группировка"                      , Неопределено);
	КоллекцияНастроек.Вставить("ДополнительныеПоля"               , Неопределено);
	КоллекцияНастроек.Вставить("ВыводитьЗаголовок"                , Ложь);
	КоллекцияНастроек.Вставить("ВыводитьПодвал"                   , Ложь);
	КоллекцияНастроек.Вставить("МакетОформления"                  , Неопределено);
	КоллекцияНастроек.Вставить("НастройкиКомпоновкиДанных"        , Неопределено);
	КоллекцияНастроек.Вставить("ВыводитьПримечанияОкругления"     , Ложь);
	
	Возврат КоллекцияНастроек;
	
КонецФункции

// Возвращает структуру параметров, наличие которых требуется для успешного формирования отчета.
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Общая структура настроек.
	ПараметрыОтчета = БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.ИдентификаторОтчета = "АнализФинансовыхРезультатов";
	
	ОтчетОбъект = Отчеты.АнализФинансовыхРезультатов.Создать();
	ПараметрыОтчета.Вставить("Группировка", ОтчетОбъект.Группировка.ВыгрузитьКолонки());
	ПараметрыОтчета.Вставить("РазмещениеДополнительныхПолей",
		БухгалтерскиеОтчетыКлиентСервер.РазмещениеДополнительныхПолей().ВместеСВладельцем);
	ПараметрыОтчета.Вставить("ДополнительныеПоля", ОтчетОбъект.ДополнительныеПоля.ВыгрузитьКолонки());
	
	ПараметрыОтчета.Вставить("Периодичность",
		БухгалтерскиеОтчетыКлиентСервер.Периодичность().Период); // без детализации по периодам
	ПараметрыОтчета.Вставить("КлючТекущегоВарианта", ""); 
	ПараметрыОтчета.Вставить("РазрезПродаж", 1); // по контрагентам
	ПараметрыОтчета.Вставить("ВыводитьПримечанияОкругления", Ложь);
	ПараметрыОтчета.Вставить("ДанныеДляРасчета", Новый Соответствие);
	ПараметрыОтчета.Вставить("ВыводитьЗаголовок", Ложь);
	ПараметрыОтчета.Вставить("МакетОформления", Ложь);
	ПараметрыОтчета.Вставить("ОкруглениеСумм", "");
	ПараметрыОтчета.Вставить("ОтрицательноеКрасным", Ложь);
	ПараметрыОтчета.Вставить("ВыводитьПодвал", Ложь);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Заполняет структуру настроек универсального формата из реквизитов формы.
//
// Параметры:
//   ПараметрыОтчета - Структура - см. ПустыеПараметрыКомпоновкиОтчета()
//   Форма - УправляемаяФорма - содержит основновной реквизит Отчет.
//
Процедура ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма) Экспорт
	
	БухгалтерскиеОтчеты.ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, Форма);
	
	Отчет = Форма.Отчет;

	ПараметрыОтчета.Периодичность     = Отчет.Периодичность;
	ПараметрыОтчета.РазрезПродаж      = Отчет.РазрезПродаж;
	
КонецПроцедуры

// Возвращает параметры, необходимые для формирования отчета с расшифровкой по переданной
// организации и периоду.
//
// Параметры:
//   ИмяОтчета - Строка - имя отчета, для которого формируются параметры.
//   КлючВарианта - Строка - имя ключа варианта отчета
//   Организация - СправочникСсылка.Организации 
//   Дата - Дата - дата формирования отчета
//
Функция ПараметрыДляОтчета(ИмяОтчета, КлючВарианта, Организация, Дата) Экспорт
	
	ПользовательскиеНастройкиКомпоновкиДанных = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("НачалоПериода" , НачалоГода(Дата));
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КонецПериода" , КонецМесяца(Дата));
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КлючВарианта", КлючВарианта);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Организация", Организация);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("РазрезПродаж", 1); // Контрагент

	НастройкиРасшифровки = Новый Структура;
	НастройкиРасшифровки.Вставить(ИмяОтчета, ПользовательскиеНастройкиКомпоновкиДанных);
	
	УсловияОтбора = Новый Структура();
	УсловияОтбора.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	ОбщиеНастройки = Новый Структура();
	ОбщиеНастройки.Вставить("Объект", УсловияОтбора);
	ОбщиеНастройки.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ОбщиеНастройки, Новый УникальныйИдентификатор);
	
	ЗаполнятьТиповыеНастройки = Новый Структура;
	ЗаполнятьТиповыеНастройки.Вставить("Отбор", Ложь);
	ЗаполнятьТиповыеНастройки.Вставить("Группировка", Истина);
	ЗаполнятьТиповыеНастройки.Вставить("ВыводимыеДанные", Истина);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ВидРасшифровки", 1);
	ПараметрыОтчета.Вставить("АдресНастроек", АдресХранилища);
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ИДРасшифровки", ИмяОтчета);
	ПараметрыОтчета.Вставить("РежимРасшифровки", Истина);
	ПараметрыОтчета.Вставить("ЗаполняемыеНастройки", ЗаполнятьТиповыеНастройки);
	ПараметрыОтчета.Вставить("КлючВарианта", КлючВарианта);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Возвращает новую таблицу значений, которая используется для расшифровки показателей отчета.
//
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Показатель - Строка - имя расшифровываемого показателя.
//     * ЭлементФормулы - Строка - имя показателя, который используется в расчете.
//     * Аналитика - Строка
//                 - СправочникСсылка.Контрагенты
//                 - СправочникСсылка.Номенклатура
//                 - СправочникСсылка.НоменклатурныеГруппы
//                 - СправочникСсылка.ПрочиеДоходыИРасходы
//                 - СправочникСсылка.СтатьиЗатрат
//                 - СправочникСсылка.СтатьиДвиженияДенежныхСредств - разрез в котором 
//                      расшифровываются такие показатели как Выручка, Себестоимость и Валовая прибыль.
//     * Период - Дата - период данных.
//     * СуммаПоказателя - Число - значение показателя.
//
Функция ТаблицаПоказателейДляРасшифровки() Экспорт
	
	ТаблицаРезультат = Новый ТаблицаЗначений;
	ТаблицаРезультат.Колонки.Добавить("НомерПоказателя", ОбщегоНазначения.ОписаниеТипаЧисло(15));
	ТаблицаРезультат.Колонки.Добавить("Показатель", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРезультат.Колонки.Добавить("ЭлементФормулы", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРезультат.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	Типы = Новый Массив;
	Типы.Добавить(Тип("Строка"));
	Типы.Добавить(Тип("СправочникСсылка.Контрагенты"));
	Типы.Добавить(Тип("СправочникСсылка.Номенклатура"));
	Типы.Добавить(Тип("СправочникСсылка.НоменклатурныеГруппы"));
	Типы.Добавить(Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
	Типы.Добавить(Тип("СправочникСсылка.СтатьиЗатрат"));
	Типы.Добавить(Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств"));
	
	ТаблицаРезультат.Колонки.Добавить("Аналитика", Новый ОписаниеТипов(Типы, , , , Новый КвалификаторыСтроки(50)));

	ТаблицаРезультат.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ТаблицаРезультат.Колонки.Добавить("СуммаПоказателя", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Возврат ТаблицаРезультат;
	
КонецФункции

// Возвращает данные, необходимые для расшифровки расчетных показателей по переданным параметрам.
//
// Параметры:
//   Показатель - Строка - имя расчтного показателя.
//   ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() 
//                                 в Отчеты.РасшифровкаПоказателейАнализФинансовыхРезультатов.ФормаОтчета.
//
Функция ДанныеРасчетаПоказателя(Показатель, ПараметрыОтчета, ПараметрыОтбора) Экспорт
	
	Результат = ТаблицаПоказателейДляРасшифровки();
	
	СтруктураОтчета = СтруктураОтчета();
	
	ТаблицаВыручкаСебестоимость = ТаблицаВыручкаСебестоимость(ПараметрыОтчета, СтруктураОтчета, Ложь);
	// Валовая прибыль
	Если ЭтоАнализВыручки(Показатель) Или Показатель = СтруктураОтчета.ВаловаяПрибыль.Наименование Тогда
		
		Результат = ДанныеДляРасчетаВаловойПрибыли(ТаблицаВыручкаСебестоимость, ПараметрыОтчета);
		Возврат Результат;

	КонецЕсли;
	
	ТаблицаРасходов = ТаблицаРасходов(ПараметрыОтчета, СтруктураОтчета, ПараметрыОтбора, Ложь);
	
	// Прибыль (убыток) от продаж
	Если Показатель = СтруктураОтчета.ПрибыльУбытокОтПродаж.Наименование Тогда
		
		Результат = ДанныеДляРасчетаПрибылиУбыткуОтПродаж(ТаблицаВыручкаСебестоимость, ТаблицаРасходов, ПараметрыОтчета);
		
		Возврат Результат;
		
	Иначе
		
		ДанныеПоПрибыльУбытокОтПродаж = ДанныеДляРасчетаПрибылиУбыткуОтПродаж(ТаблицаВыручкаСебестоимость,
			ТаблицаРасходов, ПараметрыОтчета, Истина);
		
	КонецЕсли;
	
	// Рентабельность продаж
	Если Показатель = СтруктураОтчета.РентабельностьПродаж.Наименование Тогда
		
		Результат = ДанныеДляРасчетаРентабельностьПродаж(ТаблицаВыручкаСебестоимость,
			ДанныеПоПрибыльУбытокОтПродаж, ПараметрыОтчета);
		
		Возврат Результат;
		
	КонецЕсли;
	
	ТаблицаПрочихДоходовИРасходов = ТаблицаПрочихДоходовИРасходов(ПараметрыОтчета, СтруктураОтчета, ПараметрыОтбора, Ложь);
	
	// Прибыль (убыток) до налогообложения
	Если Показатель = СтруктураОтчета.ПрибыльУбытокДоНалогообложения.Наименование Тогда
		
		Результат = ДанныеДляРасчетаПрибылиУбыткуДоНалогообложения(ТаблицаВыручкаСебестоимость, ТаблицаРасходов,
			ТаблицаПрочихДоходовИРасходов, ПараметрыОтчета);
		
		Возврат Результат;

	Иначе
		
		ДанныеПоПрибыльУбытокДоНалогообложения = ДанныеДляРасчетаПрибылиУбыткуДоНалогообложения(ТаблицаВыручкаСебестоимость,
			ТаблицаРасходов, ТаблицаПрочихДоходовИРасходов, ПараметрыОтчета, Истина);
		
	КонецЕсли;
	
	ТаблицаНалогНаПрибыль = ТаблицаНалогНаПрибыль(ПараметрыОтчета, ПараметрыОтбора, СтруктураОтчета, Ложь);
	
	// Чистая прибыль
	Если Показатель = СтруктураОтчета.ЧистаяПрибыль.Наименование Тогда
		
		Результат = ДанныеДляРасчетаЧистойПрибыли(ДанныеПоПрибыльУбытокДоНалогообложения, ТаблицаНалогНаПрибыль, ПараметрыОтчета);
		
		Возврат Результат;
		
	КонецЕсли;

	Возврат Результат;
	
КонецФункции
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьГруппировкуПериод(КомпоновщикНастроек, ПараметрыОтчета, ГруппировкаПериод)
	
	Если ПараметрыОтчета.Периодичность = 0 Тогда
		ПолеПериода = "Год";
	Иначе
		ПолеПериода = "Месяц";
	КонецЕсли;

	ПолеГруппировки       = ГруппировкаПериод.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Поле  = Новый ПолеКомпоновкиДанных(ПолеПериода);
	ПолеГруппировки       = ГруппировкаПериод.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Поле  = Новый ПолеКомпоновкиДанных("ПредставлениеПериода");
	
	ГруппировкаПериод.Имя = ПолеПериода;
	ЭлементыВыбора        = ГруппировкаПериод.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ЭлементыВыбора.Поле   = Новый ПолеКомпоновкиДанных(ПолеПериода);
	
	ЭлементыВыбора      = ГруппировкаПериод.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ЭлементыВыбора.Поле = Новый ПолеКомпоновкиДанных("Сумма");

	ЭлементыВыбора           = ГруппировкаПериод.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ЭлементыВыбора.Поле      = Новый ПолеКомпоновкиДанных("Отклонение");
	ЭлементыВыбора.Заголовок = "Изменение";
	
	ЭлементПорядкаПериода = ГруппировкаПериод.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядкаПериода.Поле = Новый ПолеКомпоновкиДанных(ПолеПериода);
	
	Если ПараметрыОтчета.Периодичность = 0 Тогда
		ЭлементПорядкаПериода.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
	Иначе
		ЭлементПорядкаПериода.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	КонецЕсли;
	
	ЭлементПорядкаПериода.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементОформления = ГруппировкаПериод.УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, ПолеПериода);
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Центр);
	ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементОформления = ГруппировкаПериод.УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "Отклонение");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("МаксимальнаяШирина", 0.1);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", "Удалить");

	ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
		ЭлементОформления.Отбор, 
		ПолеПериода, 
		КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НачальныйПериод").Значение,
		ВидСравненияКомпоновкиДанных.Равно);
	
	ЭлементОформления = ГруппировкаПериод.УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "Отклонение");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("МинимальнаяШирина", 10);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("МаксимальнаяШирина", 10);

	ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;

	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
		ЭлементОформления.Отбор,
		ПолеПериода,
		КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("НачальныйПериод").Значение,
		ВидСравненияКомпоновкиДанных.Больше);
		
КонецПроцедуры

Функция ДобавитьГруппировку(Поле, Группировка, ЭтоПерваяГруппировка)
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Поле", Поле);
		ПараметрыЗаполнения.Вставить("ТипГруппировки", 0);
	Иначе
		ПараметрыЗаполнения = Поле;
	КонецЕсли;

	Если ТипЗнч(Группировка) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		НоваяГруппировка = Группировка.Добавить();
	Иначе
		НоваяГруппировка = Группировка.Структура.Добавить();
	КонецЕсли;
	
	Если ЭтоПерваяГруппировка Тогда
		НоваяГруппировка.Имя = "ПерваяГруппировка";
	КонецЕсли;
	
	ВыводитьОтбор = НоваяГруппировка.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВыводитьОтбор"));
	ВыводитьОтбор.Использование = Истина;
	ВыводитьОтбор.Значение = ТипВыводаТекстаКомпоновкиДанных.НеВыводить;
	
	БухгалтерскиеОтчетыВызовСервера.ЗаполнитьГруппировку(ПараметрыЗаполнения, НоваяГруппировка);
	
	Возврат НоваяГруппировка;
	
КонецФункции

Процедура ДобавитьОформлениеИтога(Группировка, ВидСтроки);
	
	ЭлементОформления = Группировка.УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "Показатель");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "Сумма");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "Отклонение");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВидСтроки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = ВидСтроки;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ВажнаяНадписьШрифт);
	ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
КонецПроцедуры

Процедура ДобавитьПорядок(Группировка, Поле, Направление = "Возр")
	
	ЭлементПорядка = Группировка.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	ЭлементПорядка.Поле              = Поле;
	ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных[Направление];
	ЭлементПорядка.РежимОтображения  = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
КонецПроцедуры

Функция ПодготовитьДанные(ПараметрыОтчета, ПараметрыОтбора, ПрошлыйПериод)
	
	СтруктураОтчета = СтруктураОтчета();
	ТаблицаВыручкаСебестоимость = ТаблицаВыручкаСебестоимость(ПараметрыОтчета, СтруктураОтчета, ПрошлыйПериод);
	ТаблицаРасходов = ТаблицаРасходов(ПараметрыОтчета, СтруктураОтчета, ПараметрыОтбора, ПрошлыйПериод);
	ТаблицаПрочихДоходовИРасходов = ТаблицаПрочихДоходовИРасходов(ПараметрыОтчета, СтруктураОтчета,
		ПараметрыОтбора, ПрошлыйПериод);
	ТаблицаНалогНаПрибыль = ТаблицаНалогНаПрибыль(ПараметрыОтчета, ПараметрыОтбора, СтруктураОтчета, ПрошлыйПериод);

	ТаблицыСДанными = Новый Структура;
	ТаблицыСДанными.Вставить("ТаблицаВыручкаСебестоимость", ТаблицаВыручкаСебестоимость);
	ТаблицыСДанными.Вставить("ТаблицаРасходов", ТаблицаРасходов);
	ТаблицыСДанными.Вставить("ТаблицаПрочихДоходовИРасходов", ТаблицаПрочихДоходовИРасходов);
	ТаблицыСДанными.Вставить("ТаблицаНалогНаПрибыль", ТаблицаНалогНаПрибыль);
	РасчетныеПоказатели = РасчетныеПоказатели(ТаблицыСДанными, СтруктураОтчета, ПараметрыОтчета);
	
	ПрочиеПоказатели = ТаблицаРезультат();
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.ОсновныеСредства"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ОбъектыСтроительства"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ЦенныеБумаги"));
	МассивТипов.Добавить(Тип("СправочникСсылка.НематериальныеАктивы"));
	
	ПрочиеПоказатели.Колонки.Добавить("РеализуемыйАктив", Новый ОписаниеТипов(МассивТипов));
	ПрочиеПоказатели.Колонки.Добавить("Выручка", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ПрочиеПоказатели.Колонки.Добавить("СебестоимостьПродаж", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ПрочиеПоказатели.Колонки.Добавить("ПрибыльУбытокОтПродаж", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));

	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаРасходов, ПрочиеПоказатели);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаПрочихДоходовИРасходов, ПрочиеПоказатели);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНалогНаПрибыль, ПрочиеПоказатели);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РасчетныеПоказатели, ПрочиеПоказатели);
	
	Данные = Новый Структура;
	Данные.Вставить("ВыручкаСебестоимость", ТаблицаВыручкаСебестоимость);
	Данные.Вставить("ПрочиеПоказатели", ПрочиеПоказатели);
	
	Возврат Данные;
	
КонецФункции

Функция ТаблицаВыручкаСебестоимость(ПараметрыОтчета, СтруктураОтчета, ПрошлыйПериод = Ложь)
	
	ТаблицаРезультат = ТаблицаРезультат();
	
	ТаблицаРезультат.Колонки.Добавить("Договор", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("Строка"));
	
	ТаблицаРезультат.Колонки.Добавить("Контрагент", Новый ОписаниеТипов(МассивТипов));
	ТаблицаРезультат.Колонки.Добавить("КонтрагентПредставление", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ТаблицаРезультат.Колонки.Добавить("НоменклатураПредставление", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ТаблицаРезультат.Колонки.Добавить("ДоговорПредставление", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ТаблицаРезультат.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаРезультат.Колонки.Добавить("НоменклатурнаяГруппа", Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ТаблицаРезультат.Колонки.Добавить("Розница", ОбщегоНазначения.ОписаниеТипаЧисло(1, 0));
	ТаблицаРезультат.Колонки.Добавить("ВаловаяПрибыль", ОбщегоНазначения.ОписаниеТипаЧисло(20, 7));
	
	ВыручкаПоКонтрагентам = ПараметрыОтчета.РазрезПродаж = 1;
	ВыручкаПоНоменклатуре = ПараметрыОтчета.РазрезПродаж = 2;
	ВыручкаПоНоменклатурнымГруппам = ПараметрыОтчета.РазрезПродаж = 3;
	
	НачалоПериода = ПараметрыОтчета.НачалоПериода;
	КонецПериода = КонецДня(ПараметрыОтчета.КонецПериода);

	Запрос = ОтчетыРуководителя.ВыручкаИСебестоимость(ПараметрыОтчета, ПрошлыйПериод);
	ТекстЗапроса = 
	
	"ВЫБРАТЬ
	|	ВыручкаИСебестоимость.Период КАК Период,
	|	ВыручкаИСебестоимость.Организация КАК Организация,
	|	ВыручкаИСебестоимость.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ВыручкаПоКонтрагентам
	|			ТОГДА ВыручкаИСебестоимость.Договор
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ КАК Договор,
	|	ВЫБОР
	|		КОГДА &ВыручкаПоКонтрагентам
	|			ТОГДА ВыручкаИСебестоимость.Контрагент
	|		КОГДА &ВыручкаПоНоменклатуре
	|			ТОГДА ВЫБОР
	|					КОГДА ВыручкаИСебестоимость.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|						ТОГДА &РаботыИУслуги
	|					ИНАЧЕ ВыручкаИСебестоимость.Номенклатура
	|				КОНЕЦ
	|		КОГДА &ВыручкаПоНоменклатурнымГруппам
	|			ТОГДА ВЫБОР
	|					КОГДА ВыручкаИСебестоимость.НоменклатурнаяГруппа = ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)
	|						ТОГДА &ПустоеЗначение
	|					ИНАЧЕ ВыручкаИСебестоимость.НоменклатурнаяГруппа
	|				КОНЕЦ
	|	КОНЕЦ КАК Аналитика,
	|	ВыручкаИСебестоимость.Розница КАК Розница,
	|	ВыручкаИСебестоимость.Выручка КАК Выручка,
	|	ВыручкаИСебестоимость.Себестоимость КАК Себестоимость,
	|	ВыручкаИСебестоимость.ВаловаяПрибыль КАК ВаловаяПрибыль,
	|	ВыручкаИСебестоимость.КонтрагентПредставление КАК КонтрагентПредставление,
	|	ВыручкаИСебестоимость.ДоговорПредставление КАК ДоговорПредставление,
	|	ВыручкаИСебестоимость.НоменклатураПредставление КАК НоменклатураПредставление,
	|	ВыручкаИСебестоимость.Контрагент КАК Контрагент,
	|	ВыручкаИСебестоимость.Номенклатура КАК Номенклатура,
	|	ВыручкаИСебестоимость.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ВыручкаИСебестоимость.Счет КАК Счет
	|ИЗ
	|	ВыручкаИСебестоимость КАК ВыручкаИСебестоимость";
			
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("ВыручкаПоКонтрагентам", ВыручкаПоКонтрагентам);
	Запрос.УстановитьПараметр("ВыручкаПоНоменклатуре", ВыручкаПоНоменклатуре);
	Запрос.УстановитьПараметр("ВыручкаПоНоменклатурнымГруппам", ВыручкаПоНоменклатурнымГруппам);

	Если ПрошлыйПериод Тогда
		НачалоПериода = ДобавитьМесяц(ПараметрыОтчета.НачалоПериода, -12);
		КонецПериода = ПолучитьКонецПериодаЗаПрошлыйГод(ПараметрыОтчета);
	КонецЕсли;
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода, Истина);
	ПредставлениеПериода = СтрШаблон(НСтр("ru='%1'"), СтрЗаменить(ПредставлениеПериода, " г.", ""));

	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		Если ПараметрыОтчета.Периодичность > 0 Тогда
			ПредставлениеПериода = СтрШаблон(НСтр("ru='%1'"), Формат(Выборка.Период, "ДФ='ММММ гггг'"));
		КонецЕсли;
		
		// Выручка
		Если Выборка.Выручка <> 0 Тогда
			СтруктураОтчетаВыручка = СтруктураОтчета.Выручка;
			СтрокаВыручка = ТаблицаРезультат.Добавить();
			ЗаполнитьСтроку(СтрокаВыручка, СтруктураОтчетаВыручка, ПредставлениеПериода);
			СтрокаВыручка.Сумма = Выборка.Выручка;
			ЗаполнитьЗначенияСвойств(СтрокаВыручка, Выборка);
		КонецЕсли;
	
		// Себестоимость
		СтруктураОтчетаСебестоимость = СтруктураОтчета.Себестоимость;
		СтрокаСебестоимость = ТаблицаРезультат.Добавить();
		ЗаполнитьСтроку(СтрокаСебестоимость, СтруктураОтчетаСебестоимость, ПредставлениеПериода);
		СтрокаСебестоимость.Сумма = -Выборка.Себестоимость;
		ЗаполнитьЗначенияСвойств(СтрокаСебестоимость, Выборка);
		
		// Валовая прибыль
		СтруктураОтчетаВаловаяПрибыль = СтруктураОтчета.ВаловаяПрибыль;
		СтрокаВаловаяПрибыль = ТаблицаРезультат.Добавить();
		ЗаполнитьСтроку(СтрокаВаловаяПрибыль, СтруктураОтчетаВаловаяПрибыль, ПредставлениеПериода);
		СтрокаВаловаяПрибыль.Сумма = Выборка.ВаловаяПрибыль;
		СтрокаВаловаяПрибыль.ВаловаяПрибыль = Выборка.ВаловаяПрибыль;
		ЗаполнитьЗначенияСвойств(СтрокаВаловаяПрибыль, Выборка);
		
	КонецЦикла;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ТаблицаРасходов(ПараметрыОтчета, СтруктураОтчета, ПараметрыОтбора, ПрошлыйПериод = Ложь)
		
	ТаблицаРезультат = ТаблицаРезультат();

	Запрос = Новый Запрос;
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&НаименованиеКоммерческиеРасходы КАК Показатель,
		|	ХозрасчетныйОбороты.ПериодМесяц КАК Период,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.КорСубконто1 = &ПустаяСтатьяЗатрат
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.КорСубконто1
		|	КОНЕЦ КАК Аналитика,
		|	ХозрасчетныйОбороты.КорСчет КАК КорСчет,
		|	ХозрасчетныйОбороты.ПодразделениеКор КАК Подразделение,
		|	ХозрасчетныйОбороты.Валюта КАК Валюта,
		|	-ХозрасчетныйОбороты.СуммаОборотДт КАК Сумма,
		|	ХозрасчетныйОбороты.Организация КАК Организация,
		|	&ПорядокКоммерческиеРасходы КАК Порядок,
		|	&ВидСтрокиКоммерческиеРасходы КАК ВидСтроки
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В ИЕРАРХИИ (&СчетРасходыНаРеализацию), , ИСТИНА, , &СубконтоСтатьиЗатрат) КАК ХозрасчетныйОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НаименованиеУправленческиеРасходы,
		|	ХозрасчетныйОбороты.ПериодМесяц,
		|	ХозрасчетныйОбороты.Счет,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОбороты.Субконто1 = &ПустаяСтатьяЗатрат
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ,
		|	ХозрасчетныйОбороты.КорСчет,
		|	ХозрасчетныйОбороты.ПодразделениеКор,
		|	ХозрасчетныйОбороты.Валюта,
		|	-ХозрасчетныйОбороты.СуммаОборотКт,
		|	ХозрасчетныйОбороты.Организация,
		|	&ПорядокУправленческиеРасходы,
		|	&ВидСтрокиУправленческиеРасходы
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, , &СубконтоСтатьиЗатрат, ИСТИНА, КорСчет В ИЕРАРХИИ (&СчетУправленческиеРасходы), ) КАК ХозрасчетныйОбороты";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	
	НачалоПериода = ПараметрыОтчета.НачалоПериода;
	КонецПериода = КонецДня(ПараметрыОтчета.КонецПериода);
	
	Если ПрошлыйПериод Тогда
		НачалоПериода = ДобавитьМесяц(ПараметрыОтчета.НачалоПериода, -12);
		КонецПериода = ПолучитьКонецПериодаЗаПрошлыйГод(ПараметрыОтчета);
	КонецЕсли;
	
	СтруктураОтчетаКоммерческиеРасходы = СтруктураОтчета.КоммерческиеРасходы;
	СтруктураОтчетаУправленческиеРасходы = СтруктураОтчета.УправленческиеРасходы;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода, Истина);
	ПредставлениеПериода = СтрШаблон(НСтр("ru='%1'"), СтрЗаменить(ПредставлениеПериода, " г.", ""));
	
	Запрос.УстановитьПараметр("ВидСтрокиКоммерческиеРасходы", СтруктураОтчетаКоммерческиеРасходы.ВидСтроки);
	Запрос.УстановитьПараметр("ВидСтрокиУправленческиеРасходы", СтруктураОтчетаУправленческиеРасходы.ВидСтроки);
	Запрос.УстановитьПараметр("СубконтоСтатьиЗатрат", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("СчетРасходыНаРеализацию", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажу));
	Запрос.УстановитьПараметр("СчетУправленческиеРасходы", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходы));
	Запрос.УстановитьПараметр("Организация", ПараметрыОтчета.Организация);
	Запрос.УстановитьПараметр("ПорядокКоммерческиеРасходы", СтруктураОтчетаКоммерческиеРасходы.Порядок);
	Запрос.УстановитьПараметр("ПорядокУправленческиеРасходы", СтруктураОтчетаУправленческиеРасходы.Порядок);
	Запрос.УстановитьПараметр("НаименованиеКоммерческиеРасходы", СтруктураОтчетаКоммерческиеРасходы.Наименование);
	Запрос.УстановитьПараметр("НаименованиеУправленческиеРасходы", СтруктураОтчетаУправленческиеРасходы.Наименование);
	Запрос.УстановитьПараметр("ПустаяСтатьяЗатрат", Справочники.СтатьиЗатрат.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустоеЗначение", ОтчетыРуководителя.ПустоеЗначение());
	Подразделение = Неопределено;
	Для Каждого ЭлементОтбора Из ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") И ЭлементОтбора.Использование Тогда
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			Подразделение = ЭлементОтбора.ПравоеЗначение;
		КонецЕсли;
	КонецЦикла;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ПараметрыОтчета.Свойство("Периодичность") И ПараметрыОтчета.Периодичность > 0 Тогда
			ПредставлениеПериода = СтрШаблон(НСтр("ru='%1'"), Формат(Выборка.Период, "ДФ='ММММ гггг'"));
		КонецЕсли;

		СтрокаКоммерческиеРасходы = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКоммерческиеРасходы, Выборка);
		СтрокаКоммерческиеРасходы.ПредставлениеПериода = ПредставлениеПериода;
		
	КонецЦикла;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ТаблицаПрочихДоходовИРасходов(ПараметрыОтчета, СтруктураОтчета, ПараметрыОтбора, ПрошлыйПериод = Ложь)
	
	ТаблицаРезультат = ТаблицаРезультат();
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.ОсновныеСредства"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ОбъектыСтроительства"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ЦенныеБумаги"));
	МассивТипов.Добавить(Тип("СправочникСсылка.НематериальныеАктивы"));
	
	ТаблицаРезультат.Колонки.Добавить("РеализуемыйАктив", Новый ОписаниеТипов(МассивТипов));

	АналитикаДоходыОтУчастияВДругихОрганизациях = Новый Массив; 
	АналитикаДоходыОтУчастияВДругихОрганизациях.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.УчастиеВДругихОрганизациях);
	АналитикаДоходыОтУчастияВДругихОрганизациях.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДолевоеУчастиеВРоссийскихОрганизациях);
	АналитикаДоходыОтУчастияВДругихОрганизациях.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДолевоеУчастиеВИностранныхОрганизациях);
	
	АналитикаПроцентыКПолучению = Новый Массив;
	АналитикаПроцентыКПолучению.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКПолучениюУплате);
	АналитикаПроцентыКПолучению.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыПоГосударственнымЦеннымБумагам);
	АналитикаПроцентыКПолучению.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыПоГосударственнымЦеннымБумагамПоСтавке0);
	
	АналитикаПроцентыКУплате = Новый Массив;
	АналитикаПроцентыКУплате.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКПолучениюУплате);
	АналитикаПроцентыКУплате.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыНачисленныеПоСт269);

	СчетаРасчетовПоКредитамВыданным = Новый Массив;
	СчетаРасчетовПоКредитамВыданным.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
	СчетаРасчетовПоКредитамВыданным = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаРасчетовПоКредитамВыданным);
	
	СчетаРасчетовПоКредитамПолученным = Новый Массив;
	СчетаРасчетовПоКредитамПолученным.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоКраткосрочнымКредитамИЗаймам);
	СчетаРасчетовПоКредитамПолученным.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоДолгосрочнымКредитамИЗаймам);
	СчетаРасчетовПоКредитамПолученным = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаРасчетовПоКредитамПолученным);
	
	СчетПрочиеДоходы = Новый Массив;
	СчетПрочиеДоходы.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	СчетПрочиеДоходы = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетПрочиеДоходы);

	СчетПрочиеРасходы = Новый Массив;
	СчетПрочиеРасходы.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасходы);

	СчетПрочиеРасходы = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетПрочиеРасходы);
	РасчетыПоПричитающимсяДивидендам = ПланыСчетов.Хозрасчетный.РасчетыПоПричитающимсяДивидендам;

	СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы = Новый Массив;
	СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы.Добавить(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы.Добавить(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РеализуемыеАктивы);
	Запрос = Новый Запрос;
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрочиеДоходыИРасходы.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаДоходыОтУчастияВДругихОрганизациях
		|ИЗ
		|	Справочник.ПрочиеДоходыИРасходы КАК ПрочиеДоходыИРасходы
		|ГДЕ
		|	ПрочиеДоходыИРасходы.ВидПрочихДоходовИРасходов В(&АналитикаДоходыОтУчастияВДругихОрганизациях)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрочиеДоходыИРасходы.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаПроцентыКПолучению
		|ИЗ
		|	Справочник.ПрочиеДоходыИРасходы КАК ПрочиеДоходыИРасходы
		|ГДЕ
		|	ПрочиеДоходыИРасходы.ВидПрочихДоходовИРасходов В(&АналитикаПроцентыКПолучению)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПрочиеДоходыИРасходы.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаПроцентыКУплате
		|ИЗ
		|	Справочник.ПрочиеДоходыИРасходы КАК ПрочиеДоходыИРасходы
		|ГДЕ
		|	ПрочиеДоходыИРасходы.ВидПрочихДоходовИРасходов В(&АналитикаПроцентыКУплате)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&НаименованиеДоходыОтУчастияВДругихОрганизациях КАК Показатель,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ КАК Аналитика,
		|	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
		|	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
		|	ХозрасчетныйОбороты.Валюта КАК Валюта,
		|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК Сумма,
		|	ХозрасчетныйОбороты.Организация КАК Организация,
		|	&ПорядокДоходыОтУчастияВДругихОрганизациях КАК Порядок,
		|	&ВидСтрокиДоходыОтУчастияВДругихОрганизациях КАК ВидСтроки,
		|	ХозрасчетныйОбороты.ПериодМесяц КАК Период
		|ПОМЕСТИТЬ ДоходыОтУчастияВДругихОрганизациях
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет В ИЕРАРХИИ (&СчетПрочиеДоходы),
		|			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
		|			Субконто1 В
		|				(ВЫБРАТЬ
		|					АналитикаДоходыОтУчастияВДругихОрганизациях.Ссылка КАК Ссылка
		|				ИЗ
		|					АналитикаДоходыОтУчастияВДругихОрганизациях КАК АналитикаДоходыОтУчастияВДругихОрганизациях),
		|			КорСчет В (&РасчетыПоПричитающимсяДивидендам),
		|			) КАК ХозрасчетныйОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Валюта,
		|	ХозрасчетныйОбороты.Организация,
		|	ХозрасчетныйОбороты.Подразделение,
		|	ХозрасчетныйОбороты.Счет,
		|	ХозрасчетныйОбороты.ПериодМесяц,
		|	ХозрасчетныйОбороты.Субконто2,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&НаименованиеПроцентыКПолучению КАК Показатель,
		|	ХозрасчетныйОбороты.ПериодМесяц КАК Период,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ КАК Аналитика,
		|	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
		|	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
		|	ХозрасчетныйОбороты.Валюта КАК Валюта,
		|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК Сумма,
		|	ХозрасчетныйОбороты.Организация КАК Организация,
		|	&ПорядокПроцентыКПолучению КАК Порядок,
		|	&ВидСтрокиПроцентыКПолучению КАК ВидСтроки
		|ПОМЕСТИТЬ ДоходыПоКредитам
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет В ИЕРАРХИИ (&СчетПрочиеДоходы),
		|			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
		|			Субконто1 В
		|				(ВЫБРАТЬ
		|					АналитикаПроцентыКПолучению.Ссылка КАК Ссылка
		|				ИЗ
		|					АналитикаПроцентыКПолучению КАК АналитикаПроцентыКПолучению),
		|			,
		|			) КАК ХозрасчетныйОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Валюта,
		|	ХозрасчетныйОбороты.Организация,
		|	ХозрасчетныйОбороты.Подразделение,
		|	ХозрасчетныйОбороты.Счет,
		|	ХозрасчетныйОбороты.Субконто2,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ,
		|	ХозрасчетныйОбороты.ПериодМесяц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&НаименованиеПроцентыКУплате КАК Показатель,
		|	ХозрасчетныйОбороты.ПериодМесяц КАК Период,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ КАК Аналитика,
		|	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
		|	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
		|	ХозрасчетныйОбороты.Валюта КАК Валюта,
		|	СУММА(ХозрасчетныйОбороты.СуммаОборотДт) КАК Сумма,
		|	ХозрасчетныйОбороты.Организация КАК Организация,
		|	&ПорядокПроцентыКУплате КАК Порядок,
		|	&ВидСтрокиПроцентыКУплате КАК ВидСтроки
		|ПОМЕСТИТЬ РасходыПоКредитам
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет В ИЕРАРХИИ (&СчетПрочиеРасходы),
		|			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
		|			Субконто1 В
		|				(ВЫБРАТЬ
		|					АналитикаПроцентыКУплате.Ссылка КАК Ссылка
		|				ИЗ
		|					АналитикаПроцентыКУплате КАК АналитикаПроцентыКУплате),
		|			,
		|			) КАК ХозрасчетныйОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Валюта,
		|	ХозрасчетныйОбороты.Подразделение,
		|	ХозрасчетныйОбороты.Счет,
		|	ХозрасчетныйОбороты.Организация,
		|	ХозрасчетныйОбороты.Субконто2,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ,
		|	ХозрасчетныйОбороты.ПериодМесяц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&НаименованиеПрочиеДоходы КАК Показатель,
		|	ХозрасчетныйОбороты.ПериодМесяц КАК Период,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ КАК Аналитика,
		|	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
		|	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
		|	ХозрасчетныйОбороты.Валюта КАК Валюта,
		|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК Сумма,
		|	ХозрасчетныйОбороты.Организация КАК Организация,
		|	&ПорядокПрочиеДоходы КАК Порядок,
		|	&ВидСтрокиПрочиеДоходы КАК ВидСтроки
		|ПОМЕСТИТЬ ПрочиеДоходы
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет В ИЕРАРХИИ (&СчетПрочиеДоходы),
		|			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
		|			НЕ Субконто1 В
		|					(ВЫБРАТЬ
		|						АналитикаДоходыОтУчастияВДругихОрганизациях.Ссылка КАК Ссылка
		|					ИЗ
		|						АналитикаДоходыОтУчастияВДругихОрганизациях КАК АналитикаДоходыОтУчастияВДругихОрганизациях
		|			
		|					ОБЪЕДИНИТЬ ВСЕ
		|			
		|					ВЫБРАТЬ
		|						АналитикаПроцентыКПолучению.Ссылка
		|					ИЗ
		|						АналитикаПроцентыКПолучению КАК АналитикаПроцентыКПолучению),
		|			,
		|			) КАК ХозрасчетныйОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Организация,
		|	ХозрасчетныйОбороты.Подразделение,
		|	ХозрасчетныйОбороты.Счет,
		|	ХозрасчетныйОбороты.Субконто2,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ,
		|	ХозрасчетныйОбороты.ПериодМесяц,
		|	ХозрасчетныйОбороты.Валюта
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НаименованиеПрочиеДоходы,
		|	ХозрасчетныйОбороты.ПериодМесяц,
		|	ХозрасчетныйОбороты.Счет,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ,
		|	ХозрасчетныйОбороты.Субконто2,
		|	ХозрасчетныйОбороты.Подразделение,
		|	ХозрасчетныйОбороты.Валюта,
		|	СУММА(-ХозрасчетныйОбороты.СуммаОборотДт),
		|	ХозрасчетныйОбороты.Организация,
		|	&ПорядокПрочиеДоходы,
		|	&ВидСтрокиПрочиеДоходы
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет В ИЕРАРХИИ (&СчетПрочиеРасходы),
		|			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
		|			НЕ Субконто1 В
		|					(ВЫБРАТЬ
		|						АналитикаДоходыОтУчастияВДругихОрганизациях.Ссылка КАК Ссылка
		|					ИЗ
		|						АналитикаДоходыОтУчастияВДругихОрганизациях КАК АналитикаДоходыОтУчастияВДругихОрганизациях
		|			
		|					ОБЪЕДИНИТЬ ВСЕ
		|			
		|					ВЫБРАТЬ
		|						АналитикаПроцентыКПолучению.Ссылка
		|					ИЗ
		|						АналитикаПроцентыКПолучению КАК АналитикаПроцентыКПолучению),
		|			КорСчет В (&СчетНДС),
		|			) КАК ХозрасчетныйОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Валюта,
		|	ХозрасчетныйОбороты.Организация,
		|	ХозрасчетныйОбороты.Подразделение,
		|	ХозрасчетныйОбороты.Счет,
		|	ХозрасчетныйОбороты.Субконто2,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ,
		|	ХозрасчетныйОбороты.ПериодМесяц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&НаименованиеПрочиеРасходы КАК Показатель,
		|	ХозрасчетныйОбороты.ПериодМесяц КАК Период,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ КАК Аналитика,
		|	ХозрасчетныйОбороты.Субконто2 КАК РеализуемыйАктив,
		|	ХозрасчетныйОбороты.Подразделение КАК Подразделение,
		|	ХозрасчетныйОбороты.Валюта КАК Валюта,
		|	СУММА(ХозрасчетныйОбороты.СуммаОборотДт) КАК Сумма,
		|	ХозрасчетныйОбороты.Организация КАК Организация,
		|	&ПорядокПрочиеРасходы КАК Порядок,
		|	&ВидСтрокиПрочиеРасходы КАК ВидСтроки
		|ПОМЕСТИТЬ ПрочиеРасходы
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет В ИЕРАРХИИ (&СчетПрочиеРасходы),
		|			&СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы,
		|			НЕ Субконто1 В
		|					(ВЫБРАТЬ
		|						АналитикаПроцентыКУплате.Ссылка КАК Ссылка
		|					ИЗ
		|						АналитикаПроцентыКУплате КАК АналитикаПроцентыКУплате),
		|			КорСчет <> &СчетНДС,
		|			) КАК ХозрасчетныйОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Валюта,
		|	ХозрасчетныйОбороты.Подразделение,
		|	ХозрасчетныйОбороты.Счет,
		|	ХозрасчетныйОбороты.Организация,
		|	ХозрасчетныйОбороты.Субконто2,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы)) = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ПустаяССылка)
		|			ТОГДА &ПустоеЗначение
		|		ИНАЧЕ ХозрасчетныйОбороты.Субконто1
		|	КОНЕЦ,
		|	ХозрасчетныйОбороты.ПериодМесяц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрочиеДоходы.Показатель КАК Показатель,
		|	ПрочиеДоходы.Период КАК Период,
		|	ПрочиеДоходы.Счет КАК Счет,
		|	ПрочиеДоходы.Аналитика КАК Аналитика,
		|	ПрочиеДоходы.РеализуемыйАктив КАК РеализуемыйАктив,
		|	ЕСТЬNULL(ПрочиеДоходы.Подразделение, &ПустоеПодразделение) КАК Подразделение,
		|	ПрочиеДоходы.Валюта КАК Валюта,
		|	ПрочиеДоходы.Сумма КАК Сумма,
		|	ПрочиеДоходы.Организация КАК Организация,
		|	ПрочиеДоходы.Порядок КАК Порядок,
		|	ПрочиеДоходы.ВидСтроки КАК ВидСтроки
		|ИЗ
		|	ПрочиеДоходы КАК ПрочиеДоходы
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПрочиеРасходы.Показатель,
		|	ПрочиеРасходы.Период,
		|	ПрочиеРасходы.Счет,
		|	ПрочиеРасходы.Аналитика,
		|	ПрочиеРасходы.РеализуемыйАктив,
		|	ЕСТЬNULL(ПрочиеРасходы.Подразделение, &ПустоеПодразделение),
		|	ПрочиеРасходы.Валюта,
		|	-ПрочиеРасходы.Сумма,
		|	ПрочиеРасходы.Организация,
		|	ПрочиеРасходы.Порядок,
		|	ПрочиеРасходы.ВидСтроки
		|ИЗ
		|	ПрочиеРасходы КАК ПрочиеРасходы
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасходыПоКредитам.Показатель,
		|	РасходыПоКредитам.Период,
		|	РасходыПоКредитам.Счет,
		|	РасходыПоКредитам.Аналитика,
		|	РасходыПоКредитам.РеализуемыйАктив,
		|	ЕСТЬNULL(РасходыПоКредитам.Подразделение, &ПустоеПодразделение),
		|	РасходыПоКредитам.Валюта,
		|	-РасходыПоКредитам.Сумма,
		|	РасходыПоКредитам.Организация,
		|	РасходыПоКредитам.Порядок,
		|	РасходыПоКредитам.ВидСтроки
		|ИЗ
		|	РасходыПоКредитам КАК РасходыПоКредитам
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоходыПоКредитам.Показатель,
		|	ДоходыПоКредитам.Период,
		|	ДоходыПоКредитам.Счет,
		|	ДоходыПоКредитам.Аналитика,
		|	ДоходыПоКредитам.РеализуемыйАктив,
		|	ЕСТЬNULL(ДоходыПоКредитам.Подразделение, &ПустоеПодразделение),
		|	ДоходыПоКредитам.Валюта,
		|	ДоходыПоКредитам.Сумма,
		|	ДоходыПоКредитам.Организация,
		|	ДоходыПоКредитам.Порядок,
		|	ДоходыПоКредитам.ВидСтроки
		|ИЗ
		|	ДоходыПоКредитам КАК ДоходыПоКредитам
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоходыОтУчастияВДругихОрганизациях.Показатель,
		|	ДоходыОтУчастияВДругихОрганизациях.Период,
		|	ДоходыОтУчастияВДругихОрганизациях.Счет,
		|	ДоходыОтУчастияВДругихОрганизациях.Аналитика,
		|	ДоходыОтУчастияВДругихОрганизациях.РеализуемыйАктив,
		|	ЕСТЬNULL(ДоходыОтУчастияВДругихОрганизациях.Подразделение, &ПустоеПодразделение),
		|	ДоходыОтУчастияВДругихОрганизациях.Валюта,
		|	ДоходыОтУчастияВДругихОрганизациях.Сумма,
		|	ДоходыОтУчастияВДругихОрганизациях.Организация,
		|	ДоходыОтУчастияВДругихОрганизациях.Порядок,
		|	ДоходыОтУчастияВДругихОрганизациях.ВидСтроки
		|ИЗ
		|	ДоходыОтУчастияВДругихОрганизациях КАК ДоходыОтУчастияВДругихОрганизациях";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;
	
	НачалоПериода = ПараметрыОтчета.НачалоПериода;
	КонецПериода = КонецДня(ПараметрыОтчета.КонецПериода);
	
	Если ПрошлыйПериод Тогда
		НачалоПериода = ДобавитьМесяц(ПараметрыОтчета.НачалоПериода, -12);
		КонецПериода = ПолучитьКонецПериодаЗаПрошлыйГод(ПараметрыОтчета);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		НачалоПериода, КонецПериода, Истина);
		
	ПредставлениеПериода = СтрШаблон(НСтр("ru='%1'"), СтрЗаменить(ПредставлениеПериода, " г.", ""));
	
	Запрос.УстановитьПараметр("СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы",
		СубконтоПрочиеДоходыИРасходыРеализуемыеАктивы);
	
	Запрос.УстановитьПараметр("СчетаРасчетовПоКредитамВыданным", СчетаРасчетовПоКредитамВыданным);
	Запрос.УстановитьПараметр("СчетаРасчетовПоКредитамПолученным", СчетаРасчетовПоКредитамПолученным);
	Запрос.УстановитьПараметр("СчетПрочиеДоходы", СчетПрочиеДоходы);
	Запрос.УстановитьПараметр("СчетНДС", ПланыСчетов.Хозрасчетный.НДС);
	Запрос.УстановитьПараметр("СчетПрочиеРасходы", СчетПрочиеРасходы);
	Запрос.УстановитьПараметр("РасчетыПоПричитающимсяДивидендам", РасчетыПоПричитающимсяДивидендам);
	Запрос.УстановитьПараметр("Организация", ПараметрыОтчета.Организация);
	
	Подразделение = Неопределено;
	Для Каждого ЭлементОтбора Из ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") И ЭлементОтбора.Использование Тогда
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			Подразделение = ЭлементОтбора.ПравоеЗначение;
		КонецЕсли;
	КонецЦикла;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);

	СтрокаДоходыОтУчастияВДругихОрганизациях = СтруктураОтчета.ДоходыОтУчастияВДругихОрганизациях;
	СтрокаПроцентыКПолучению = СтруктураОтчета.ПроцентыКПолучению;
	СтрокаПроцентыКУплате = СтруктураОтчета.ПроцентыКУплате;
	СтрокаПрочиеДоходы = СтруктураОтчета.ПрочиеДоходы;
	СтрокаПрочиеРасходы = СтруктураОтчета.ПрочиеРасходы;
	
	Запрос.УстановитьПараметр("НаименованиеДоходыОтУчастияВДругихОрганизациях", 
		СтрокаДоходыОтУчастияВДругихОрганизациях.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПроцентыКПолучению", СтрокаПроцентыКПолучению.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПроцентыКУплате", СтрокаПроцентыКУплате.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПрочиеДоходы", СтрокаПрочиеДоходы.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПрочиеРасходы", СтрокаПрочиеРасходы.Наименование);
	
	Запрос.УстановитьПараметр("ВидСтрокиДоходыОтУчастияВДругихОрганизациях", 
		СтрокаДоходыОтУчастияВДругихОрганизациях.ВидСтроки);
	Запрос.УстановитьПараметр("ВидСтрокиПроцентыКПолучению", СтрокаПроцентыКПолучению.ВидСтроки);
	Запрос.УстановитьПараметр("ВидСтрокиПроцентыКУплате", СтрокаПроцентыКУплате.ВидСтроки);
	Запрос.УстановитьПараметр("ВидСтрокиПрочиеДоходы", СтрокаПрочиеДоходы.ВидСтроки);
	Запрос.УстановитьПараметр("ВидСтрокиПрочиеРасходы", СтрокаПрочиеРасходы.ВидСтроки);
	
	Запрос.УстановитьПараметр("ПорядокДоходыОтУчастияВДругихОрганизациях", 
		СтрокаДоходыОтУчастияВДругихОрганизациях.Порядок);
	Запрос.УстановитьПараметр("ПорядокПроцентыКПолучению", СтрокаПроцентыКПолучению.Порядок);
	Запрос.УстановитьПараметр("ПорядокПроцентыКУплате", СтрокаПроцентыКУплате.Порядок);
	Запрос.УстановитьПараметр("ПорядокПрочиеДоходы", СтрокаПрочиеДоходы.Порядок);
	Запрос.УстановитьПараметр("ПорядокПрочиеРасходы", СтрокаПрочиеРасходы.Порядок);

	Запрос.УстановитьПараметр("АналитикаДоходыОтУчастияВДругихОрганизациях",
		АналитикаДоходыОтУчастияВДругихОрганизациях);
		
	Запрос.УстановитьПараметр("АналитикаПроцентыКПолучению", АналитикаПроцентыКПолучению);
	Запрос.УстановитьПараметр("АналитикаПроцентыКУплате", АналитикаПроцентыКУплате); 
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустоеЗначение", ОтчетыРуководителя.ПустоеЗначение());

	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ПараметрыОтчета.Свойство("Периодичность") И ПараметрыОтчета.Периодичность > 0 Тогда
			ПредставлениеПериода = СтрШаблон(НСтр("ru='%1'"), Формат(Выборка.Период, "ДФ='ММММ гггг'"));
		КонецЕсли;

		СтрокаПрочиеДоходыИРасходы = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПрочиеДоходыИРасходы, Выборка);
		СтрокаПрочиеДоходыИРасходы.ПредставлениеПериода = ПредставлениеПериода;
		
	КонецЦикла;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ТаблицаНалогНаПрибыль(ПараметрыОтчета, ПараметрыОтбора, СтруктураОтчета, ПрошлыйПериод = Ложь)
	
	ТаблицаРезультат = ТаблицаРезультат();
	
	Если Не ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда
		
		ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
		Для Каждого ДоступнаяОрганизация Из ДоступныеОрганизации Цикл
			ТаблицаНалогНаПрибыльПоОрганизации(ТаблицаРезультат, ПараметрыОтчета, ДоступнаяОрганизация,
				СтруктураОтчета, ПараметрыОтбора, ПрошлыйПериод);
		КонецЦикла;
	Иначе
		ТаблицаНалогНаПрибыльПоОрганизации(ТаблицаРезультат, ПараметрыОтчета, ПараметрыОтчета.Организация,
			СтруктураОтчета, ПараметрыОтбора, ПрошлыйПериод);
	КонецЕсли;
	
	ТаблицаРезультат = ОбработатьТаблицуРезультата(ТаблицаРезультат);
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ОбработатьТаблицуРезультата(ТаблицаИсточник)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаИсточник.Идентификатор КАК Идентификатор,
	               |	ТаблицаИсточник.Показатель КАК Показатель,
	               |	ТаблицаИсточник.Период КАК Период,
	               |	ТаблицаИсточник.ПредставлениеПериода КАК ПредставлениеПериода,
	               |	ТаблицаИсточник.Аналитика КАК Аналитика,
	               |	ТаблицаИсточник.Организация КАК Организация,
	               |	ТаблицаИсточник.Подразделение КАК Подразделение,
	               |	ТаблицаИсточник.Счет КАК Счет,
	               |	ТаблицаИсточник.Валюта КАК Валюта,
	               |	ТаблицаИсточник.Порядок КАК Порядок,
	               |	ТаблицаИсточник.ВидСтроки КАК ВидСтроки,
	               |	ТаблицаИсточник.Сумма КАК Сумма
	               |ПОМЕСТИТЬ ТаблицаИсточник
	               |ИЗ
	               |	&ТаблицаИсточник КАК ТаблицаИсточник
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаИсточник.Идентификатор КАК Идентификатор,
	               |	ТаблицаИсточник.Показатель КАК Показатель,
	               |	ТаблицаИсточник.Период КАК Период,
	               |	ТаблицаИсточник.ПредставлениеПериода КАК ПредставлениеПериода,
	               |	ТаблицаИсточник.Аналитика КАК Аналитика,
	               |	ТаблицаИсточник.Организация КАК Организация,
	               |	ТаблицаИсточник.Подразделение КАК Подразделение,
	               |	ТаблицаИсточник.Счет КАК Счет,
	               |	ТаблицаИсточник.Валюта КАК Валюта,
	               |	ТаблицаИсточник.Порядок КАК Порядок,
	               |	ТаблицаИсточник.ВидСтроки КАК ВидСтроки,
	               |	ТаблицаИсточник.Сумма КАК Сумма
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ТаблицаИсточник.Идентификатор КАК Идентификатор,
	               |		ТаблицаИсточник.Показатель КАК Показатель,
	               |		ТаблицаИсточник.Период КАК Период,
	               |		ТаблицаИсточник.ПредставлениеПериода КАК ПредставлениеПериода,
	               |		ВЫРАЗИТЬ(ТаблицаИсточник.Аналитика КАК СТРОКА(100)) КАК Аналитика,
	               |		ТаблицаИсточник.Организация КАК Организация,
	               |		ТаблицаИсточник.Подразделение КАК Подразделение,
	               |		ТаблицаИсточник.Счет КАК Счет,
	               |		ТаблицаИсточник.Валюта КАК Валюта,
	               |		ТаблицаИсточник.Порядок КАК Порядок,
	               |		ТаблицаИсточник.ВидСтроки КАК ВидСтроки,
	               |		СУММА(ТаблицаИсточник.Сумма) КАК Сумма
	               |	ИЗ
	               |		ТаблицаИсточник КАК ТаблицаИсточник
	               |	
	               |	СГРУППИРОВАТЬ ПО
	               |		ТаблицаИсточник.Валюта,
	               |		ТаблицаИсточник.Порядок,
	               |		ТаблицаИсточник.ВидСтроки,
	               |		ТаблицаИсточник.ПредставлениеПериода,
	               |		ТаблицаИсточник.Организация,
	               |		ТаблицаИсточник.Подразделение,
	               |		ТаблицаИсточник.Период,
	               |		ТаблицаИсточник.Показатель,
	               |		ТаблицаИсточник.Идентификатор,
	               |		ТаблицаИсточник.Счет,
	               |		ВЫРАЗИТЬ(ТаблицаИсточник.Аналитика КАК СТРОКА(100))) КАК ТаблицаИсточник
	               |ГДЕ
	               |	ТаблицаИсточник.Сумма <> 0";
	Запрос.УстановитьПараметр("ТаблицаИсточник", ТаблицаИсточник);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаИсточник;
	Иначе
		Возврат РезультатЗапроса.Выгрузить();
	КонецЕсли;
	
КонецФункции

Функция ЗапросНалогНаПрибыльПриПБУ18(ПараметрыОтчета, ПараметрыОтбора, СтруктураОтчета)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&НаименованиеНалогНаПрибыль КАК Показатель,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц КАК Период,
	|	"""" КАК Аналитика,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение) КАК Подразделение,
	|	ОборотыПоНалогуНаПрибыль.Валюта КАК Валюта,
	|	СУММА(ОборотыПоНалогуНаПрибыль.СуммаОборот) КАК Сумма,
	|	ОборотыПоНалогуНаПрибыль.Организация КАК Организация,
	|	&ПорядокНалогНаПрибыль КАК Порядок,
	|	&ВидСтрокиНалогНаПрибыль КАК ВидСтроки,
	|	ОборотыПоНалогуНаПрибыль.Счет КАК Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетРасчетНалогаНаПрибыль), , ИСТИНА, КорСчет В ИЕРАРХИИ (&СчетНалогНаПрибыль), ) КАК ОборотыПоНалогуНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеНалогНаПрибыль,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.ПодразделениеДт, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.ВалютаДт,
	|	СУММА(-ОборотыПоНалогуНаПрибыль.СуммаОборот),
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	&ПорядокНалогНаПрибыль,
	|	&ВидСтрокиНалогНаПрибыль,
	|	ОборотыПоНалогуНаПрибыль.СчетДт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В ИЕРАРХИИ (&СчетНалогНаПрибыль), , СчетКт В (&СчетНераспределеннаяПрибыльУбытки), , ИСТИНА) КАК ОборотыПоНалогуНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.ВалютаДт,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.ПодразделениеДт, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.СчетДт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеИзменениеОтложенныхНалоговыхОбязательств,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПоОтложеннымНалоговымОбязательствам.ПодразделениеДт, &ПустоеПодразделение),
	|	ОборотыПоОтложеннымНалоговымОбязательствам.ВалютаДт,
	|	СУММА(-ОборотыПоОтложеннымНалоговымОбязательствам.СуммаОборот),
	|	ОборотыПоОтложеннымНалоговымОбязательствам.Организация,
	|	&ПорядокИзменениеОтложенныхНалоговыхОбязательств,
	|	&ВидСтрокиИзменениеОтложенныхНалоговыхОбязательств,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.СчетДт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, СчетДт В (&СчетНераспределеннаяПрибыльУбытки), , СчетКт В ИЕРАРХИИ (&СчетНалогНаПрибыль), , ИСТИНА) КАК ОборотыПоОтложеннымНалоговымОбязательствам
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоОтложеннымНалоговымОбязательствам.ПериодМесяц,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.Организация,
	|	ОборотыПоОтложеннымНалоговымОбязательствам.ВалютаДт,
	|	ЕСТЬNULL(ОборотыПоОтложеннымНалоговымОбязательствам.ПодразделениеДт, &ПустоеПодразделение),
	|	ОборотыПоОтложеннымНалоговымОбязательствам.СчетДт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеНалогНаПрибыль,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	СУММА(-ОборотыПоНалогуНаПрибыль.СуммаОборот),
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	&ПорядокНалогНаПрибыль,
	|	&ВидСтрокиНалогНаПрибыль,
	|	ОборотыПоНалогуНаПрибыль.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетПрибылиИУбыткиНеЕНВД), , ИСТИНА, КорСчет В ИЕРАРХИИ (&СчетРасчетыСБюджетом), ) КАК ОборотыПоНалогуНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеНалогНаПрибыль,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	СУММА(-ОборотыПоНалогуНаПрибыль.СуммаОборот),
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	&ПорядокНалогНаПрибыль,
	|	&ВидСтрокиНалогНаПрибыль,
	|	ОборотыПоНалогуНаПрибыль.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетПрибылиИУбыткиНеЕНВД), , ИСТИНА, КорСчет В ИЕРАРХИИ (&СчетНалогНаПрибыльТекущийИОтложенный), ) КАК ОборотыПоНалогуНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеНалогНаПрибыль,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	СУММА(-ОборотыПоНалогуНаПрибыль.СуммаОборот),
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	&ПорядокНалогНаПрибыль,
	|	&ВидСтрокиНалогНаПрибыль,
	|	ОборотыПоНалогуНаПрибыль.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетНалогНаПрибыльТекущийИОтложенный), , ИСТИНА, , ) КАК ОборотыПоНалогуНаПрибыль
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПоНалогуНаПрибыль.Валюта,
	|	ОборотыПоНалогуНаПрибыль.Организация,
	|	ОборотыПоНалогуНаПрибыль.ПериодМесяц,
	|	ЕСТЬNULL(ОборотыПоНалогуНаПрибыль.Подразделение, &ПустоеПодразделение),
	|	ОборотыПоНалогуНаПрибыль.Счет";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("СчетПрибылиИУбыткиНеЕНВД", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиНеЕНВД)); // 99.01.1
	
	Запрос.УстановитьПараметр("СчетРасчетНалогаНаПрибыль", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетНалогаНаПрибыль)); // 68.04.2
	СчетНалогНаПрибыльТекущийИОтложенный = Новый Массив;
	СчетНалогНаПрибыльТекущийИОтложенный.Добавить(ПланыСчетов.Хозрасчетный.ТекущийНалогНаПрибыль); // 99.02.Т
	СчетНалогНаПрибыльТекущийИОтложенный.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныйНалогНаПрибыль); // 99.02.О
	
	Запрос.УстановитьПараметр("СчетНалогНаПрибыльТекущийИОтложенный", СчетНалогНаПрибыльТекущийИОтложенный); // 68.04.2

	Запрос.УстановитьПараметр("СчетНераспределеннаяПрибыльУбытки",
		ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль); // 84
	Запрос.УстановитьПараметр("СчетНалогНаПрибыль", 
		ПланыСчетов.Хозрасчетный.ПрибылиИУбытки_НалогНаПрибыль); // 99.02
		
	СтрокаНалогНаПрибыль = СтруктураОтчета.НалогНаПрибыль;
	СтрокаИзменениеОтложенныхНалоговыхОбязательств = СтруктураОтчета.ИзменениеОтложенныхНалоговыхОбязательств;

	Запрос.УстановитьПараметр("НаименованиеНалогНаПрибыль", СтрокаНалогНаПрибыль.Наименование);
	Запрос.УстановитьПараметр("НаименованиеИзменениеОтложенныхНалоговыхОбязательств",
		СтрокаИзменениеОтложенныхНалоговыхОбязательств.Наименование);

	
	Запрос.УстановитьПараметр("ВидСтрокиНалогНаПрибыль", СтрокаНалогНаПрибыль.ВидСтроки);
	Запрос.УстановитьПараметр("ВидСтрокиИзменениеОтложенныхНалоговыхОбязательств",
		СтрокаИзменениеОтложенныхНалоговыхОбязательств.ВидСтроки);
	
	Запрос.УстановитьПараметр("ПорядокНалогНаПрибыль", СтрокаНалогНаПрибыль.Порядок);
	Запрос.УстановитьПараметр("ПорядокИзменениеОтложенныхНалоговыхОбязательств",
	СтрокаИзменениеОтложенныхНалоговыхОбязательств.Порядок);
	
	Возврат Запрос;

КонецФункции

Функция ЗапросНалогНаПрибыль(СтруктураОтчета, ПараметрыОтбора)
	
	НалогНаПрибыль = СтруктураОтчета.НалогНаПрибыль;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&Показатель КАК Показатель,
	|	ХозрасчетныйОбороты.ПериодМесяц КАК Период,
	|	"""" КАК Аналитика,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.Подразделение, &ПустоеПодразделение) КАК Подразделение,
	|	ХозрасчетныйОбороты.Валюта КАК Валюта,
	|	-ХозрасчетныйОбороты.СуммаОборотДт КАК Сумма,
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	&ПорядокНалогНаПрибыль КАК Порядок,
	|	&ВидСтрокиНалогНаПрибыль КАК ВидСтроки,
	|	ХозрасчетныйОбороты.Счет КАК Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (&СчетПрибылиИУбыткиНеЕНВД), , ИСТИНА, КорСчет В (&СчетРасчетыСБюджетом), ) КАК ХозрасчетныйОбороты";
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("СчетПрибылиИУбыткиНеЕНВД", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиНеЕНВД));
	Запрос.УстановитьПараметр("ВидСтрокиНалогНаПрибыль", НалогНаПрибыль.ВидСтроки);
	Запрос.УстановитьПараметр("ПорядокНалогНаПрибыль", НалогНаПрибыль.Порядок);
	
	Возврат Запрос;

КонецФункции

Функция ДополнитьТекстЗапросаПрочее(Запрос, СтруктураОтчета, ПараметрыОтбора)
	
	ТекстЗапросаПрочее = 
	"ВЫБРАТЬ
	|	&НаименованиеПрочее КАК НаименованиеПрочее,
	|	ОборотыПрочее.ПериодМесяц КАК ПериодМесяц,
	|	"""" КАК Аналитика,
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение) КАК Подразделение,
	|	ОборотыПрочее.Валюта КАК Валюта,
	|	СУММА(ОборотыПрочее.СуммаОборотКт) КАК Сумма,
	|	ОборотыПрочее.Организация КАК Организация,
	|	&ПорядокПрочее КАК ПорядокПрочее,
	|	&ВидСтрокиПрочее КАК ВидСтрокиПрочее,
	|	ОборотыПрочее.Счет КАК Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки), , ИСТИНА, , ) КАК ОборотыПрочее
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПрочее.ПериодМесяц,
	|	ОборотыПрочее.Организация,
	|	ОборотыПрочее.Валюта,
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеПрочее,
	|	ОборотыПрочее.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.Валюта,
	|	СУММА(-ОборотыПрочее.СуммаОборотДт),
	|	ОборотыПрочее.Организация,
	|	&ПорядокПрочее,
	|	&ВидСтрокиПрочее,
	|	ОборотыПрочее.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки), , ИСТИНА, НЕ КорСчет В ИЕРАРХИИ (&ИсключаемыеСчетаНалоговНаПрибыль), ) КАК ОборотыПрочее
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПрочее.ПериодМесяц,
	|	ОборотыПрочее.Организация,
	|	ОборотыПрочее.Валюта,
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеПрочее,
	|	ОборотыПрочее.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.Валюта,
	|	СУММА(ОборотыПрочее.СуммаОборотДт),
	|	ОборотыПрочее.Организация,
	|	&ПорядокПрочее,
	|	&ВидСтрокиПрочее,
	|	ОборотыПрочее.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки), , ИСТИНА, КорСчет В ИЕРАРХИИ (&СчетаКорресонденты), ) КАК ОборотыПрочее
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПрочее.ПериодМесяц,
	|	ОборотыПрочее.Организация,
	|	ОборотыПрочее.Валюта,
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеПрочее,
	|	ОборотыПрочее.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.Валюта,
	|	СУММА(-ОборотыПрочее.СуммаОборотДт),
	|	ОборотыПрочее.Организация,
	|	&ПорядокПрочее,
	|	&ВидСтрокиПрочее,
	|	ОборотыПрочее.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В ИЕРАРХИИ (&СчетаКорресонденты), , ИСТИНА, КорСчет В ИЕРАРХИИ (&СчетаПрочиеПрибылиИУбытки), ) КАК ОборотыПрочее
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПрочее.ПериодМесяц,
	|	ОборотыПрочее.Организация,
	|	ОборотыПрочее.Валюта,
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.Счет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НаименованиеПрочее,
	|	ОборотыПрочее.ПериодМесяц,
	|	"""",
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.Валюта,
	|	СУММА(ОборотыПрочее.СуммаОборотКт),
	|	ОборотыПрочее.Организация,
	|	&ПорядокПрочее,
	|	&ВидСтрокиПрочее,
	|	ОборотыПрочее.Счет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В ИЕРАРХИИ (&СчетПрибылиИУбыткиБезНалогаНаПрибыль), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыПлатежейВГосБюджет), Субконто1 = &РасчетыСБюджетом, КорСчет В ИЕРАРХИИ (&РасчетыПоНалогам), ) КАК ОборотыПрочее
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыПрочее.ПериодМесяц,
	|	ОборотыПрочее.Организация,
	|	ОборотыПрочее.Валюта,
	|	ЕСТЬNULL(ОборотыПрочее.Подразделение, &ПустоеПодразделение),
	|	ОборотыПрочее.СуммаОборотКт,
	|	ОборотыПрочее.Счет";
	
	ТекстЗапроса = СтрШаблон("%1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|%2", Запрос.Текст, ТекстЗапросаПрочее);
	
	УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	Запрос.Текст = ТекстЗапроса;

	СчетаПрочиеПрибылиИУбытки = Новый Массив;
	СчетаПрочиеПрибылиИУбытки.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеПрибылиИУбытки); // 99.09
	СчетаПрочиеПрибылиИУбытки.Добавить(ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиБезНалогаНаПрибыль); // 99.01
	
	Запрос.УстановитьПараметр("СчетаПрочиеПрибылиИУбытки", СчетаПрочиеПрибылиИУбытки);
	
	ИсключаемыеСчетаНалоговНаПрибыль = Новый Массив;
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.НалогПриПСН);
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.НалогПриАУСН);
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.НалогНаПрофессиональныйДоход);
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.НалогНаПрибыль);
	ИсключаемыеСчетаНалоговНаПрибыль.Добавить(ПланыСчетов.Хозрасчетный.ЕНприУСН);
	
	Запрос.УстановитьПараметр("ИсключаемыеСчетаНалоговНаПрибыль", ИсключаемыеСчетаНалоговНаПрибыль);
	
	СчетаКорресонденты = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаКорресонденты, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль)); // 84
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаКорресонденты, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрибылиИУбытки)); // 99
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаКорресонденты, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Продажи));  // 90
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаКорресонденты, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы)); //91
		
	Запрос.УстановитьПараметр("СчетаКорресонденты", СчетаКорресонденты);
	
	СчетПрибылиИУбыткиБезНалогаНаПрибыль = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетПрибылиИУбыткиБезНалогаНаПрибыль, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиБезНалогаНаПрибыль)); //99.01
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетПрибылиИУбыткиБезНалогаНаПрибыль, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеПрибылиИУбытки)); //99.09

	Запрос.УстановитьПараметр("СчетПрибылиИУбыткиБезНалогаНаПрибыль", СчетПрибылиИУбыткиБезНалогаНаПрибыль);
	
	РасчетыПоНалогам = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСБюджетом);
	Запрос.УстановитьПараметр("РасчетыПоНалогам", РасчетыПоНалогам);
	Запрос.УстановитьПараметр("РасчетыСБюджетом", Перечисления.ВидыПлатежейВГосБюджет.Налог);
	
	СтрокаПрочее = СтруктураОтчета.Прочее;

	Запрос.УстановитьПараметр("НаименованиеПрочее", СтрокаПрочее.Наименование);
	
	Запрос.УстановитьПараметр("ВидСтрокиПрочее", СтрокаПрочее.ВидСтроки);

	Запрос.УстановитьПараметр("ПорядокПрочее", СтрокаПрочее.Порядок);

	Возврат Запрос;
	
КонецФункции 

Функция РасчетныеПоказатели(ТаблицыСДанными, СтруктураОтчета, ПараметрыОтчета)
	
	Если ПараметрыОтчета.ДанныеДляРасчета = Неопределено Тогда
		ПараметрыОтчета.ДанныеДляРасчета = Новый Соответствие;
	КонецЕсли;

	ТаблицаВыручкаСебестоимость = ТаблицыСДанными.ТаблицаВыручкаСебестоимость;
	ТаблицаРасходов = ТаблицыСДанными.ТаблицаРасходов;
	ТаблицаПрочихДоходовИРасходов = ТаблицыСДанными.ТаблицаПрочихДоходовИРасходов;
	ТаблицаНалогНаПрибыль = ТаблицыСДанными.ТаблицаНалогНаПрибыль;
	
	ТаблицаРезультат = ТаблицаРезультат();
	ТаблицаРезультат.Колонки.Добавить("Выручка", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаРезультат.Колонки.Добавить("СебестоимостьПродаж", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаРезультат.Колонки.Добавить("ПрибыльУбытокОтПродаж", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаВыручкаСебестоимость.Период КАК Период,
		|	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
		|	ТаблицаВыручкаСебестоимость.Организация КАК Организация,
		|	ТаблицаВыручкаСебестоимость.Подразделение КАК Подразделение,
		|	ТаблицаВыручкаСебестоимость.Сумма КАК Сумма,
		|	ТаблицаВыручкаСебестоимость.ВидСтроки КАК ВидСтроки,
		|	ТаблицаВыручкаСебестоимость.ПредставлениеПериода КАК ПредставлениеПериода
		|ПОМЕСТИТЬ ТаблицаВыручкаСебестоимость
		|ИЗ
		|	&ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаРасходов.Период КАК Период,
		|	ТаблицаРасходов.Показатель КАК Показатель,
		|	ТаблицаРасходов.Организация КАК Организация,
		|	ТаблицаРасходов.Подразделение КАК Подразделение,
		|	ТаблицаРасходов.Сумма КАК Сумма,
		|	ТаблицаРасходов.ВидСтроки КАК ВидСтроки,
		|	ТаблицаРасходов.ПредставлениеПериода КАК ПредставлениеПериода
		|ПОМЕСТИТЬ ТаблицаРасходов
		|ИЗ
		|	&ТаблицаРасходов КАК ТаблицаРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПрочихДоходовИРасходов.Период КАК Период,
		|	ТаблицаПрочихДоходовИРасходов.Показатель КАК Показатель,
		|	ТаблицаПрочихДоходовИРасходов.Организация КАК Организация,
		|	ТаблицаПрочихДоходовИРасходов.Подразделение КАК Подразделение,
		|	ТаблицаПрочихДоходовИРасходов.Сумма КАК Сумма,
		|	ТаблицаПрочихДоходовИРасходов.ВидСтроки КАК ВидСтроки,
		|	ТаблицаПрочихДоходовИРасходов.ПредставлениеПериода КАК ПредставлениеПериода
		|ПОМЕСТИТЬ ТаблицаПрочихДоходовИРасходов
		|ИЗ
		|	&ТаблицаПрочихДоходовИРасходов КАК ТаблицаПрочихДоходовИРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНалогНаПрибыль.Период КАК Период,
		|	ТаблицаНалогНаПрибыль.Показатель КАК Показатель,
		|	ТаблицаНалогНаПрибыль.Организация КАК Организация,
		|	ТаблицаНалогНаПрибыль.Подразделение КАК Подразделение,
		|	ТаблицаНалогНаПрибыль.Сумма КАК Сумма,
		|	ТаблицаНалогНаПрибыль.ВидСтроки КАК ВидСтроки,
		|	ТаблицаНалогНаПрибыль.ПредставлениеПериода КАК ПредставлениеПериода
		|ПОМЕСТИТЬ ТаблицаНалогНаПрибыль
		|ИЗ
		|	&ТаблицаНалогНаПрибыль КАК ТаблицаНалогНаПрибыль
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляРасчета.Период КАК Период,
		|	ДанныеДляРасчета.Организация КАК Организация,
		|	ДанныеДляРасчета.Подразделение КАК Подразделение,
		|	СУММА(ДанныеДляРасчета.ВаловаяПрибыль) КАК ВаловаяПрибыль,
		|	СУММА(ДанныеДляРасчета.КоммерческиеРасходы) КАК КоммерческиеРасходы,
		|	СУММА(ДанныеДляРасчета.УправленческиеРасходы) КАК УправленческиеРасходы,
		|	СУММА(ДанныеДляРасчета.ПрочиеДоходы) КАК ПрочиеДоходы,
		|	СУММА(ДанныеДляРасчета.ПрочиеРасходы) КАК ПрочиеРасходы,
		|	СУММА(ДанныеДляРасчета.ПроцентыКПолучению) КАК ПроцентыКПолучению,
		|	СУММА(ДанныеДляРасчета.ПроцентыКУплате) КАК ПроцентыКУплате,
		|	СУММА(ДанныеДляРасчета.ВсегоДоходов) КАК ВсегоДоходов,
		|	СУММА(ДанныеДляРасчета.ВсегоРасходов) КАК ВсегоРасходов,
		|	ДанныеДляРасчета.ДоходыОтУчастияВДругихОрганизациях КАК ДоходыОтУчастияВДругихОрганизациях,
		|	ДанныеДляРасчета.ПредставлениеПериода КАК ПредставлениеПериода,
		|	СУММА(ДанныеДляРасчета.НалогНаПрибыль) КАК НалогНаПрибыль,
		|	СУММА(ДанныеДляРасчета.Прочее) КАК Прочее,
		|	СУММА(ДанныеДляРасчета.СебестоимостьПродаж) КАК СебестоимостьПродаж,
		|	СУММА(ДанныеДляРасчета.Выручка) КАК Выручка
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаВыручкаСебестоимость.Период КАК Период,
		|		ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
		|		ТаблицаВыручкаСебестоимость.Организация КАК Организация,
		|		ТаблицаВыручкаСебестоимость.Подразделение КАК Подразделение,
		|		ВЫБОР
		|			КОГДА ТаблицаВыручкаСебестоимость.Показатель = &НаименованиеВаловаяПрибыль
		|				ТОГДА ТаблицаВыручкаСебестоимость.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ВаловаяПрибыль,
		|		0 КАК КоммерческиеРасходы,
		|		0 КАК УправленческиеРасходы,
		|		0 КАК ПрочиеДоходы,
		|		0 КАК ПрочиеРасходы,
		|		0 КАК ПроцентыКПолучению,
		|		0 КАК ПроцентыКУплате,
		|		0 КАК ВсегоДоходов,
		|		0 КАК ВсегоРасходов,
		|		0 КАК ДоходыОтУчастияВДругихОрганизациях,
		|		ТаблицаВыручкаСебестоимость.ПредставлениеПериода КАК ПредставлениеПериода,
		|		0 КАК НалогНаПрибыль,
		|		0 КАК Прочее,
		|		0 КАК СебестоимостьПродаж,
		|		ВЫБОР
		|			КОГДА ТаблицаВыручкаСебестоимость.Показатель = &НаименованиеВыручка
		|				ТОГДА ТаблицаВыручкаСебестоимость.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Выручка
		|	ИЗ
		|		ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаРасходов.Период,
		|		ТаблицаРасходов.Показатель,
		|		ТаблицаРасходов.Организация,
		|		ТаблицаРасходов.Подразделение,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаРасходов.Показатель = &НаименованиеКоммерческиеРасходы
		|				ТОГДА ТаблицаРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ТаблицаРасходов.Показатель = &НаименованиеУправленческиеРасходы
		|				ТОГДА ТаблицаРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ТаблицаРасходов.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаРасходов КАК ТаблицаРасходов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПрочихДоходовИРасходов.Период,
		|		ТаблицаПрочихДоходовИРасходов.Показатель,
		|		ТаблицаПрочихДоходовИРасходов.Организация,
		|		ТаблицаПрочихДоходовИРасходов.Подразделение,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаПрочихДоходовИРасходов.Показатель = &НаименованиеПрочиеДоходы
		|				ТОГДА ТаблицаПрочихДоходовИРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ТаблицаПрочихДоходовИРасходов.Показатель = &НаименованиеПрочиеРасходы
		|				ТОГДА ТаблицаПрочихДоходовИРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ТаблицаПрочихДоходовИРасходов.Показатель = &НаименованиеПроцентыКПолучению
		|				ТОГДА ТаблицаПрочихДоходовИРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ТаблицаПрочихДоходовИРасходов.Показатель = &НаименованиеПроцентыКУплате
		|				ТОГДА ТаблицаПрочихДоходовИРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		0,
		|		0,
		|		ТаблицаПрочихДоходовИРасходов.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаПрочихДоходовИРасходов КАК ТаблицаПрочихДоходовИРасходов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаВыручкаСебестоимость.Период,
		|		&НаименованиеВсегоДоходов,
		|		ТаблицаВыручкаСебестоимость.Организация,
		|		ТаблицаВыручкаСебестоимость.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаВыручкаСебестоимость.ВидСтроки = &ВидСтрокиДоход
		|				ТОГДА ТаблицаВыручкаСебестоимость.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		0,
		|		ТаблицаВыручкаСебестоимость.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаРасходов.Период,
		|		&НаименованиеВсегоРасходов,
		|		ТаблицаРасходов.Организация,
		|		ТаблицаРасходов.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаРасходов.ВидСтроки = &ВидСтрокиРасход
		|				ТОГДА ТаблицаРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		ТаблицаРасходов.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаРасходов КАК ТаблицаРасходов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПрочихДоходовИРасходов.Период,
		|		&НаименованиеВсегоДоходов,
		|		ТаблицаПрочихДоходовИРасходов.Организация,
		|		ТаблицаПрочихДоходовИРасходов.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаПрочихДоходовИРасходов.ВидСтроки = &ВидСтрокиДоход
		|				ТОГДА ТаблицаПрочихДоходовИРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		0,
		|		ТаблицаПрочихДоходовИРасходов.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаПрочихДоходовИРасходов КАК ТаблицаПрочихДоходовИРасходов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаВыручкаСебестоимость.Период,
		|		&НаименованиеВсегоРасходов,
		|		ТаблицаВыручкаСебестоимость.Организация,
		|		ТаблицаВыручкаСебестоимость.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаВыручкаСебестоимость.ВидСтроки = &ВидСтрокиРасход
		|				ТОГДА ТаблицаВыручкаСебестоимость.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		ТаблицаВыручкаСебестоимость.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПрочихДоходовИРасходов.Период,
		|		&НаименованиеВсегоРасходов,
		|		ТаблицаПрочихДоходовИРасходов.Организация,
		|		ТаблицаПрочихДоходовИРасходов.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаПрочихДоходовИРасходов.ВидСтроки = &ВидСтрокиРасход
		|				ТОГДА ТаблицаПрочихДоходовИРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		ТаблицаПрочихДоходовИРасходов.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаПрочихДоходовИРасходов КАК ТаблицаПрочихДоходовИРасходов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаНалогНаПрибыль.Период,
		|		&НаименованиеВсегоРасходов,
		|		ТаблицаНалогНаПрибыль.Организация,
		|		ТаблицаНалогНаПрибыль.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ТаблицаНалогНаПрибыль.Сумма,
		|		0,
		|		ТаблицаНалогНаПрибыль.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаНалогНаПрибыль КАК ТаблицаНалогНаПрибыль
		|	ГДЕ
		|		ТаблицаНалогНаПрибыль.ВидСтроки < &ВидСтрокиВсегоДоходов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПрочихДоходовИРасходов.Период,
		|		ТаблицаПрочихДоходовИРасходов.Показатель,
		|		ТаблицаПрочихДоходовИРасходов.Организация,
		|		ТаблицаПрочихДоходовИРасходов.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаПрочихДоходовИРасходов.Показатель = &НаименованиеДоходыОтУчастияВДругихОрганизациях
		|				ТОГДА ТаблицаПрочихДоходовИРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ТаблицаПрочихДоходовИРасходов.ПредставлениеПериода,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаПрочихДоходовИРасходов КАК ТаблицаПрочихДоходовИРасходов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаНалогНаПрибыль.Период,
		|		ТаблицаНалогНаПрибыль.Показатель,
		|		ТаблицаНалогНаПрибыль.Организация,
		|		ТаблицаНалогНаПрибыль.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ТаблицаНалогНаПрибыль.ПредставлениеПериода,
		|		ТаблицаНалогНаПрибыль.Сумма,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаНалогНаПрибыль КАК ТаблицаНалогНаПрибыль
		|	ГДЕ
		|		ТаблицаНалогНаПрибыль.ВидСтроки < &ВидСтрокиВсегоДоходов
		|		И ТаблицаНалогНаПрибыль.Показатель = &НаименованиеНалогНаПрибыль
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаНалогНаПрибыль.Период,
		|		ТаблицаНалогНаПрибыль.Показатель,
		|		ТаблицаНалогНаПрибыль.Организация,
		|		ТаблицаНалогНаПрибыль.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ТаблицаНалогНаПрибыль.ПредставлениеПериода,
		|		0,
		|		ТаблицаНалогНаПрибыль.Сумма,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаНалогНаПрибыль КАК ТаблицаНалогНаПрибыль
		|	ГДЕ
		|		ТаблицаНалогНаПрибыль.ВидСтроки = &ВидСтрокиРасход
		|		И ТаблицаНалогНаПрибыль.Показатель = &НаименованиеПрочее
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаВыручкаСебестоимость.Период,
		|		ТаблицаВыручкаСебестоимость.Показатель,
		|		ТаблицаВыручкаСебестоимость.Организация,
		|		ТаблицаВыручкаСебестоимость.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ТаблицаВыручкаСебестоимость.ПредставлениеПериода,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ТаблицаВыручкаСебестоимость.Показатель = &НаименованиеСебестоимость
		|				ТОГДА ТаблицаВыручкаСебестоимость.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0
		|	ИЗ
		|		ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаНалогНаПрибыль.Период,
		|		ТаблицаНалогНаПрибыль.Показатель,
		|		ТаблицаНалогНаПрибыль.Организация,
		|		ТаблицаНалогНаПрибыль.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ТаблицаНалогНаПрибыль.ПредставлениеПериода,
		|		ТаблицаНалогНаПрибыль.Сумма,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаНалогНаПрибыль КАК ТаблицаНалогНаПрибыль
		|	ГДЕ
		|		ТаблицаНалогНаПрибыль.ВидСтроки < &ВидСтрокиВсегоДоходов
		|		И ТаблицаНалогНаПрибыль.Показатель = &НаименованиеНалогПриУСН
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаНалогНаПрибыль.Период,
		|		ТаблицаНалогНаПрибыль.Показатель,
		|		ТаблицаНалогНаПрибыль.Организация,
		|		ТаблицаНалогНаПрибыль.Подразделение,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ТаблицаНалогНаПрибыль.ПредставлениеПериода,
		|		ТаблицаНалогНаПрибыль.Сумма,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаНалогНаПрибыль КАК ТаблицаНалогНаПрибыль
		|	ГДЕ
		|		ТаблицаНалогНаПрибыль.ВидСтроки < &ВидСтрокиВсегоДоходов
		|		И ТаблицаНалогНаПрибыль.Показатель = &НаименованиеНалогНаПрофессиональныйДоход) КАК ДанныеДляРасчета
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДляРасчета.Организация,
		|	ДанныеДляРасчета.ДоходыОтУчастияВДругихОрганизациях,
		|	ДанныеДляРасчета.ПредставлениеПериода,
		|	ДанныеДляРасчета.Подразделение,
		|	ДанныеДляРасчета.Период";
	
	Запрос.УстановитьПараметр("ТаблицаВыручкаСебестоимость", ТаблицаВыручкаСебестоимость);
	Запрос.УстановитьПараметр("ТаблицаРасходов", ТаблицаРасходов);
	Запрос.УстановитьПараметр("ТаблицаПрочихДоходовИРасходов", ТаблицаПрочихДоходовИРасходов);
	Запрос.УстановитьПараметр("ТаблицаНалогНаПрибыль", ТаблицаНалогНаПрибыль);
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	
	ВидСтроки = ВидСтроки();
	Запрос.УстановитьПараметр("ВидСтрокиДоход", ВидСтроки.Доход);
	Запрос.УстановитьПараметр("ВидСтрокиРасход", ВидСтроки.Расход);
	Запрос.УстановитьПараметр("ВидСтрокиВсегоДоходов", ВидСтроки.ВсегоДоходов);
	
	Выручка = СтруктураОтчета.Выручка;
	ПрибыльУбытокОтПродаж = СтруктураОтчета.ПрибыльУбытокОтПродаж;
	ПрибыльУбытокДоНалогообложения = СтруктураОтчета.ПрибыльУбытокДоНалогообложения;

	ВаловаяПрибыль = СтруктураОтчета.ВаловаяПрибыль;
	ВсегоДоходов = СтруктураОтчета.ВсегоДоходов;
	ВсегоРасходов = СтруктураОтчета.ВсегоРасходов;
	ДоходыОтУчастияВДругихОрганизациях = СтруктураОтчета.ДоходыОтУчастияВДругихОрганизациях;
	КоммерческиеРасходы = СтруктураОтчета.КоммерческиеРасходы;
	НалогНаПрибыль = СтруктураОтчета.НалогНаПрибыль;
	НалогПриУСН = СтруктураОтчета.НалогПриУСН;
	НалогНПД = СтруктураОтчета.НалогНаПрофессиональныйДоход;
	
	ПроцентыКПолучению = СтруктураОтчета.ПроцентыКПолучению;
	ПроцентыКУплате = СтруктураОтчета.ПроцентыКУплате;
	Прочее = СтруктураОтчета.Прочее;
	ПрочиеДоходы = СтруктураОтчета.ПрочиеДоходы;
	ПрочиеРасходы = СтруктураОтчета.ПрочиеРасходы;
	УправленческиеРасходы = СтруктураОтчета.УправленческиеРасходы;
	Себестоимость = СтруктураОтчета.Себестоимость;
	
	Запрос.УстановитьПараметр("НаименованиеВыручка", Выручка.Наименование);
	Запрос.УстановитьПараметр("НаименованиеВаловаяПрибыль", ВаловаяПрибыль.Наименование);
	Запрос.УстановитьПараметр("НаименованиеВсегоДоходов", ВсегоДоходов.Наименование);
	Запрос.УстановитьПараметр("НаименованиеВсегоРасходов", ВсегоРасходов.Наименование);
	Запрос.УстановитьПараметр("НаименованиеДоходыОтУчастияВДругихОрганизациях",
		ДоходыОтУчастияВДругихОрганизациях.Наименование);
	Запрос.УстановитьПараметр("НаименованиеКоммерческиеРасходы", КоммерческиеРасходы.Наименование);
	Запрос.УстановитьПараметр("НаименованиеНалогНаПрибыль", НалогНаПрибыль.Наименование);
	Запрос.УстановитьПараметр("НаименованиеНалогПриУСН", НалогПриУСН.Наименование);
	Запрос.УстановитьПараметр("НаименованиеНалогНаПрофессиональныйДоход", НалогНПД.Наименование);

	Запрос.УстановитьПараметр("НаименованиеПроцентыКПолучению", ПроцентыКПолучению.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПроцентыКУплате", ПроцентыКУплате.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПрочее", Прочее.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПрочиеДоходы", ПрочиеДоходы.Наименование);
	Запрос.УстановитьПараметр("НаименованиеПрочиеРасходы", ПрочиеРасходы.Наименование);
	Запрос.УстановитьПараметр("НаименованиеУправленческиеРасходы", УправленческиеРасходы.Наименование);
	Запрос.УстановитьПараметр("НаименованиеСебестоимость", Себестоимость.Наименование);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ПрибыльУбытокОтПродаж = СтруктураОтчета.ПрибыльУбытокОтПродаж;
	ПрибыльУбытокДоНалогообложения = СтруктураОтчета.ПрибыльУбытокДоНалогообложения;
	ЧистаяПрибыль = СтруктураОтчета.ЧистаяПрибыль;
	ВсегоДоходов = СтруктураОтчета.ВсегоДоходов;
	ВсегоРасходов = СтруктураОтчета.ВсегоРасходов;
	
	// Служебная таблица результата
	ТаблицаРезультатРентабельностьПродаж = ТаблицаРезультат();
	
	Пока Выборка.Следующий() Цикл
		
		// Прибыль (убыток) от продаж
		СтрокаПрибыльУбытокОтПродаж = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПрибыльУбытокОтПродаж, Выборка);
		СтрокаПрибыльУбытокОтПродаж.Сумма = Выборка.ВаловаяПрибыль + Выборка.КоммерческиеРасходы + Выборка.УправленческиеРасходы;
		
		// Себестоимость продаж
		СтрокаСебестоимостьПродаж = ТаблицаРезультатРентабельностьПродаж.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСебестоимостьПродаж, Выборка);
		СтрокаСебестоимостьПродаж.Показатель = "СебестоимостьПродаж";
		СтрокаСебестоимостьПродаж.Сумма = -Выборка.СебестоимостьПродаж;
		
		// Выручка
		СтрокаВыручка = ТаблицаРезультатРентабельностьПродаж.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВыручка, Выборка);
		СтрокаВыручка.Показатель = "Выручка";
		СтрокаВыручка.Сумма = Выборка.Выручка;
		
		// Прибыль (убыток) от продаж служебный
		СтрокаПрибыльУбытокОтПродажСлужебный = ТаблицаРезультатРентабельностьПродаж.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПрибыльУбытокОтПродажСлужебный, Выборка);
		СтрокаПрибыльУбытокОтПродажСлужебный.Показатель = "ПрибыльУбытокОтПродаж";
		СтрокаПрибыльУбытокОтПродажСлужебный.Сумма = СтрокаПрибыльУбытокОтПродаж.Сумма;
		
		СтрокаПрибыльУбытокОтПродаж.Сумма = Выборка.ВаловаяПрибыль + Выборка.КоммерческиеРасходы + Выборка.УправленческиеРасходы;

		ЗаполнитьСтроку(СтрокаПрибыльУбытокОтПродаж, ПрибыльУбытокОтПродаж);
		
		// Прибыль (убыток) до налогообложения
		СтрокаПрибыльУбытокДоНалогов = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПрибыльУбытокДоНалогов, Выборка);
		
		ЗаполнитьСтроку(СтрокаПрибыльУбытокДоНалогов, ПрибыльУбытокДоНалогообложения);

		СтрокаПрибыльУбытокДоНалогов.Сумма = Выборка.ВаловаяПрибыль
			+ Выборка.КоммерческиеРасходы
			+ Выборка.УправленческиеРасходы
			+ Выборка.ПрочиеРасходы
			+ Выборка.ПроцентыКУплате
			+ Выборка.ПрочиеДоходы
			+ Выборка.ПроцентыКПолучению
			+ Выборка.ДоходыОтУчастияВДругихОрганизациях;
		
		// Чистая прибыль
		СтрокаЧистаяПрибыль = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЧистаяПрибыль, Выборка);
		СтрокаЧистаяПрибыль.Сумма = СтрокаПрибыльУбытокДоНалогов.Сумма + Выборка.НалогНаПрибыль 
			+ Выборка.Прочее;
			
		ЗаполнитьСтроку(СтрокаЧистаяПрибыль, ЧистаяПрибыль);
		
		// Всего доходов
		СтрокаВсегоДоходов = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВсегоДоходов, Выборка);
		СтрокаВсегоДоходов.Сумма = Выборка.ВсегоДоходов;

		ЗаполнитьСтроку(СтрокаВсегоДоходов, ВсегоДоходов);
		
		// Всего расходов
		СтрокаВсегоРасходов = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВсегоРасходов, Выборка);
		СтрокаВсегоРасходов.Сумма = Выборка.ВсегоРасходов;
		
		ЗаполнитьСтроку(СтрокаВсегоРасходов, ВсегоРасходов);
		
	КонецЦикла;

	РассчитатьРентабельностьПродаж(ТаблицаРезультатРентабельностьПродаж, ТаблицаРезультат, СтруктураОтчета);

	Возврат ТаблицаРезультат;
	
КонецФункции

Функция РассчитатьРентабельностьПродаж(СлужебнаяТаблица, ТаблицаРезультат, СтруктураОтчета)
	
	РентабельностьПродаж = СтруктураОтчета.РентабельностьПродаж;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СлужебнаяТаблица.Период КАК Период,
		|	СлужебнаяТаблица.Показатель КАК Показатель,
		|	СлужебнаяТаблица.Организация КАК Организация,
		|	СлужебнаяТаблица.Подразделение КАК Подразделение,
		|	СлужебнаяТаблица.Сумма КАК Сумма,
		|	СлужебнаяТаблица.ВидСтроки КАК ВидСтроки,
		|	СлужебнаяТаблица.ПредставлениеПериода КАК ПредставлениеПериода
		|ПОМЕСТИТЬ СлужебнаяТаблица
		|ИЗ
		|	&СлужебнаяТаблица КАК СлужебнаяТаблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляРасчета.Период КАК Период,
		|	ДанныеДляРасчета.Показатель КАК Показатель,
		|	ДанныеДляРасчета.Организация КАК Организация,
		|	ДанныеДляРасчета.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА ДанныеДляРасчета.Выручка = 0
		|			ТОГДА 0
		|		ИНАЧЕ ДанныеДляРасчета.ПрибыльУбытокОтПродаж / ДанныеДляРасчета.Выручка * 100
		|	КОНЕЦ КАК Сумма,
		|	ДанныеДляРасчета.Выручка КАК Выручка,
		|	ДанныеДляРасчета.ПрибыльУбытокОтПродаж КАК ПрибыльУбытокОтПродаж,
		|	ДанныеДляРасчета.ПредставлениеПериода КАК ПредставлениеПериода
		|ИЗ
		|	(ВЫБРАТЬ
		|		СлужебнаяТаблица.Период КАК Период,
		|		&НаименованиеРентабельностьПродаж КАК Показатель,
		|		СлужебнаяТаблица.Организация КАК Организация,
		|		СлужебнаяТаблица.Подразделение КАК Подразделение,
		|		СУММА(ВЫБОР
		|				КОГДА СлужебнаяТаблица.Показатель = ""Выручка""
		|					ТОГДА СлужебнаяТаблица.Сумма
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК Выручка,
		|		СУММА(ВЫБОР
		|				КОГДА СлужебнаяТаблица.Показатель = ""ПрибыльУбытокОтПродаж""
		|					ТОГДА СлужебнаяТаблица.Сумма
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК ПрибыльУбытокОтПродаж,
		|		СлужебнаяТаблица.ПредставлениеПериода КАК ПредставлениеПериода,
		|		СлужебнаяТаблица.ВидСтроки КАК ВидСтроки
		|	ИЗ
		|		СлужебнаяТаблица КАК СлужебнаяТаблица
		|	
		|	СГРУППИРОВАТЬ ПО
		|		СлужебнаяТаблица.Организация,
		|		СлужебнаяТаблица.Период,
		|		СлужебнаяТаблица.Подразделение,
		|		СлужебнаяТаблица.ПредставлениеПериода,
		|		СлужебнаяТаблица.ВидСтроки) КАК ДанныеДляРасчета";
	
	Запрос.УстановитьПараметр("СлужебнаяТаблица", СлужебнаяТаблица);

	Запрос.УстановитьПараметр("НаименованиеРентабельностьПродаж", РентабельностьПродаж.Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл

		СтрокаРентабельностьПродаж = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРентабельностьПродаж, Выборка);
		ЗаполнитьСтроку(СтрокаРентабельностьПродаж, РентабельностьПродаж);
		
	КонецЦикла;

	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ТаблицаРезультат()
	
	ТаблицаРезультат = Новый ТаблицаЗначений;
	ТаблицаРезультат.Колонки.Добавить("Идентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРезультат.Колонки.Добавить("Показатель", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаРезультат.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ТаблицаРезультат.Колонки.Добавить("ПредставлениеПериода", ОбщегоНазначения.ОписаниеТипаСтрока(50));

	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Строка"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура")); 
	МассивТипов.Добавить(Тип("СправочникСсылка.НоменклатурныеГруппы"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
	МассивТипов.Добавить(Тип("СправочникСсылка.СтатьиЗатрат"));
	МассивТипов.Добавить(Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств"));
	
	ТаблицаРезультат.Колонки.Добавить("Аналитика", Новый ОписаниеТипов(МассивТипов, , , , Новый КвалификаторыСтроки(50)));
	ТаблицаРезультат.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	МассивТипов.Очистить();
	МассивТипов.Добавить(Тип("Строка"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаРезультат.Колонки.Добавить("Подразделение", Новый ОписаниеТипов(МассивТипов, , , , Новый КвалификаторыСтроки(5)));
	ТаблицаРезультат.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаРезультат.Колонки.Добавить("КорСчет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаРезультат.Колонки.Добавить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаРезультат.Колонки.Добавить("Порядок", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
	ТаблицаРезультат.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(20, 7));

	ТаблицаРезультат.Колонки.Добавить("ВидСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(1, 0));
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ВидСтроки()
	
	ВидСтроки = Новый Структура;
	ВидСтроки.Вставить("Доход",               1);
	ВидСтроки.Вставить("Расход",              2);
	ВидСтроки.Вставить("РасчетныйПоказатель", 3);
	ВидСтроки.Вставить("ВсегоДоходов",        4);
	ВидСтроки.Вставить("ВсегоРасходов",       5);
	ВидСтроки.Вставить("Информация",          6);
	
	Возврат ВидСтроки;
	
КонецФункции

Функция ПорядокСтрок()
	
	ПорядокСтрок = Новый Структура;
	ПорядокСтрок.Вставить("Выручка",                                  1);
	ПорядокСтрок.Вставить("Себестоимость",                            2);
	ПорядокСтрок.Вставить("ВаловаяПрибыль",                           3);
	ПорядокСтрок.Вставить("КоммерческиеРасходы",                      4);
	ПорядокСтрок.Вставить("УправленческиеРасходы",                    5);
	ПорядокСтрок.Вставить("ПрибыльУбытокОтПродаж",                    6);
	ПорядокСтрок.Вставить("РентабельностьПродаж",                     7);
	ПорядокСтрок.Вставить("ДоходыОтУчастияВДругихОрганизациях",       8);
	ПорядокСтрок.Вставить("ПроцентыКПолучению",                       9);
	ПорядокСтрок.Вставить("ПроцентыКУплате",                          10);
	ПорядокСтрок.Вставить("ПрочиеДоходы",                             11);
	ПорядокСтрок.Вставить("ПрочиеРасходы",                            12);
	ПорядокСтрок.Вставить("ПрибыльУбытокДоНалогообложения",           13);
	ПорядокСтрок.Вставить("НалогНаПрибыль",                           14);
	ПорядокСтрок.Вставить("НалогПриУСН",                              14);
	ПорядокСтрок.Вставить("НалогНаПрофессиональныйДоход",             14);
	ПорядокСтрок.Вставить("ИзменениеОтложенныхНалоговыхОбязательств", 15);
	ПорядокСтрок.Вставить("Прочее",                                   16);
	ПорядокСтрок.Вставить("ЧистаяПрибыль",                            17);
	ПорядокСтрок.Вставить("ВсегоДоходов",                             18);
	ПорядокСтрок.Вставить("ВсегоРасходов",                            19);
	
	Возврат ПорядокСтрок;
	
КонецФункции

Функция СтруктураОтчета()
	
	ВидСтроки = ВидСтроки();
	
	СтруктураОтчета = Новый Структура;
	НоваяСтрокаОтчета(СтруктураОтчета, "Выручка", "Выручка", ВидСтроки.Доход);
	НоваяСтрокаОтчета(СтруктураОтчета, "Себестоимость", "Себестоимость", ВидСтроки.Расход);
	НоваяСтрокаОтчета(СтруктураОтчета, "ВаловаяПрибыль", "Валовая прибыль", ВидСтроки.РасчетныйПоказатель);
	НоваяСтрокаОтчета(СтруктураОтчета, "РентабельностьПродаж", "Рентабельность продаж", ВидСтроки.РасчетныйПоказатель);
	НоваяСтрокаОтчета(СтруктураОтчета, "КоммерческиеРасходы", "Коммерческие расходы", ВидСтроки.Расход);
	НоваяСтрокаОтчета(СтруктураОтчета, "УправленческиеРасходы", "Управленческие расходы", ВидСтроки.Расход);
	НоваяСтрокаОтчета(СтруктураОтчета, "ПрибыльУбытокОтПродаж", "Прибыль (убыток) от продаж", ВидСтроки.РасчетныйПоказатель);
	НоваяСтрокаОтчета(СтруктураОтчета, "ДоходыОтУчастияВДругихОрганизациях",
		"Доходы от участия в других организациях", ВидСтроки.Доход);
	НоваяСтрокаОтчета(СтруктураОтчета, "ПроцентыКПолучению", "Проценты к получению", ВидСтроки.Доход);
	НоваяСтрокаОтчета(СтруктураОтчета, "ПроцентыКУплате", "Проценты к уплате", ВидСтроки.Расход);
	НоваяСтрокаОтчета(СтруктураОтчета, "ПрочиеДоходы", "Прочие доходы", ВидСтроки.Доход);
	НоваяСтрокаОтчета(СтруктураОтчета, "ПрочиеРасходы", "Прочие расходы", ВидСтроки.Расход);
	НоваяСтрокаОтчета(СтруктураОтчета, "ПрибыльУбытокДоНалогообложения", 
		"Прибыль (убыток) до налогообложения", ВидСтроки.РасчетныйПоказатель);
	НоваяСтрокаОтчета(СтруктураОтчета, "НалогНаПрибыль", "Налог на прибыль", ВидСтроки.Расход); 
	
	НоваяСтрокаОтчета(СтруктураОтчета, "НалогПриУСН", "Налог при УСН", ВидСтроки.Расход);
	НоваяСтрокаОтчета(СтруктураОтчета, "НалогНаПрофессиональныйДоход", "Налог на профессиональный доход", ВидСтроки.Расход);

	НоваяСтрокаОтчета(СтруктураОтчета, "ИзменениеОтложенныхНалоговыхОбязательств", 
		"Изменение отложенных налоговых обязательств", ВидСтроки.Информация);
	НоваяСтрокаОтчета(СтруктураОтчета, "Прочее", "Прочее", ВидСтроки.Расход);
	НоваяСтрокаОтчета(СтруктураОтчета, "ЧистаяПрибыль", "Чистая прибыль", ВидСтроки.РасчетныйПоказатель);
	НоваяСтрокаОтчета(СтруктураОтчета, "ВсегоДоходов", "Всего доходов", ВидСтроки.ВсегоДоходов);
	НоваяСтрокаОтчета(СтруктураОтчета, "ВсегоРасходов", "Всего расходов", ВидСтроки.ВсегоРасходов);
	
	Возврат СтруктураОтчета;
	
КонецФункции

Процедура НоваяСтрокаОтчета(СтруктураОтчета, Идентификатор, Наименование, ВидСтроки)
	
	ПорядокСтрок = ПорядокСтрок();
	
	СтрокаОтчета = Новый Структура;
	СтрокаОтчета.Вставить("Наименование", СтрШаблон(НСтр("ru='%1'"), Наименование));
	СтрокаОтчета.Вставить("Порядок", ПорядокСтрок[Идентификатор]);
	СтрокаОтчета.Вставить("ВидСтроки", ВидСтроки);
	
	СтруктураОтчета.Вставить(Идентификатор, СтрокаОтчета);
	
КонецПроцедуры

Процедура ЗаполнитьСтроку(СтрокаОтчета, ДанныеСтроки, ПредставлениеПериода = Неопределено)
	
	СтрокаОтчета.Показатель = ДанныеСтроки.Наименование;
	СтрокаОтчета.ВидСтроки = ДанныеСтроки.ВидСтроки;
	СтрокаОтчета.Порядок = ДанныеСтроки.Порядок;
	Если ПредставлениеПериода <> Неопределено Тогда
		СтрокаОтчета.ПредставлениеПериода = ПредставлениеПериода;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьКонецПериодаЗаПрошлыйГод(ПараметрыОтчета)
	
	Если КонецДня(ПараметрыОтчета.КонецПериода) = КонецМесяца(ПараметрыОтчета.КонецПериода) Тогда
		КонецПериода = КонецМесяца(ДобавитьМесяц(ПараметрыОтчета.КонецПериода, -12));
	Иначе
		КонецПериода = КонецДня(ДобавитьМесяц(ПараметрыОтчета.КонецПериода, -12));
	КонецЕсли;
	
	Возврат КонецПериода;
	
КонецФункции

Процедура ТаблицаНалогНаПрибыльПоОрганизации(ТаблицаРезультат, ПараметрыОтчета, Организация, СтруктураОтчета, ПараметрыОтбора, ПрошлыйПериод)
	
	ОрганизацияПрименяетПБУ18 = РегламентированнаяОтчетностьПереопределяемый.ПрименяетсяПБУ18(
		Организация, КонецДня(ПараметрыОтчета.КонецПериода));
	ОрганизацияПрименяетУСН   = ЗаполнениеБухгалтерскойОтчетности.ПрименяетсяУСН(
		Организация, КонецДня(ПараметрыОтчета.КонецПериода));
	ОрганизацияПрименяетАУСН  = ЗаполнениеБухгалтерскойОтчетности.ПрименяетсяАУСН(
		Организация, КонецДня(ПараметрыОтчета.КонецПериода));
	ОрганизацияПрименяетНПД   = ЗаполнениеБухгалтерскойОтчетности.ПрименяетсяНПД(
		Организация, КонецДня(ПараметрыОтчета.КонецПериода));
		
	СтрокаНалогНаПрибыль = СтруктураОтчета.НалогНаПрибыль;
	НаименованиеПоказателя = СтрокаНалогНаПрибыль.Наименование;
	
	ПараметрыОтбораОрганизации = Новый Структура;
	Для Каждого ЭлементОтбора Из ПараметрыОтбора Цикл
		ПараметрыОтбораОрганизации.Вставить(ЭлементОтбора.Ключ, ЭлементОтбора.Значение);
	КонецЦикла;

	Если Не ЗначениеЗаполнено(ПараметрыОтчета.Организация)
		Или ПараметрыОтчета.Организация.Пустая() Тогда
		ПараметрыОтбораОрганизации.Вставить("Организация", Организация);
		ПараметрыОтбораОрганизации.Вставить("ОрганизацияФормулаСравнения",
			ОтчетыРуководителя.ФормулаСравнения(ВидСравненияКомпоновкиДанных.Равно));
	КонецЕсли;
	
	Если ОрганизацияПрименяетПБУ18 Тогда
		Запрос = ЗапросНалогНаПрибыльПриПБУ18(ПараметрыОтчета, ПараметрыОтбораОрганизации, СтруктураОтчета);
		Запрос.УстановитьПараметр("СчетРасчетыСБюджетом", 
			БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСБюджетом));
	ИначеЕсли ОрганизацияПрименяетУСН Тогда 
		Запрос = ЗапросНалогНаПрибыль(СтруктураОтчета, ПараметрыОтбораОрганизации);
		Запрос.УстановитьПараметр("СчетРасчетыСБюджетом",
			БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ЕНприУСН));
		НаименованиеПоказателя = НСтр("ru='Налог при УСН'");
	ИначеЕсли ОрганизацияПрименяетАУСН Тогда 
		Запрос = ЗапросНалогНаПрибыль(СтруктураОтчета, ПараметрыОтбораОрганизации);
		Запрос.УстановитьПараметр("СчетРасчетыСБюджетом",
			БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НалогПриАУСН));
		НаименованиеПоказателя = НСтр("ru='Налог при УСН'");
	ИначеЕсли ОрганизацияПрименяетНПД Тогда
		Запрос = ЗапросНалогНаПрибыль(СтруктураОтчета, ПараметрыОтбораОрганизации);
		Запрос.УстановитьПараметр("СчетРасчетыСБюджетом", 
			БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НалогНаПрофессиональныйДоход));
		НаименованиеПоказателя = НСтр("ru='Налог на профессиональный доход'");
	Иначе
		Запрос = ЗапросНалогНаПрибыль(СтруктураОтчета, ПараметрыОтбораОрганизации);
		Запрос.УстановитьПараметр("СчетРасчетыСБюджетом", 
			БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетыСБюджетом));
		НаименованиеПоказателя = НСтр("ru='Налог на прибыль'");
	КонецЕсли;
	
	ДополнитьТекстЗапросаПрочее(Запрос, СтруктураОтчета, ПараметрыОтбораОрганизации);
	
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	Запрос.УстановитьПараметр("Показатель", НаименованиеПоказателя);
	
	Подразделение = Неопределено;
	Для Каждого ЭлементОтбора Из ПараметрыОтчета.НастройкиКомпоновкиДанных.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") И ЭлементОтбора.Использование Тогда
			Если ЭлементОтбора.ПравоеЗначение = Неопределено Тогда
				ЭлементОтбора.ПравоеЗначение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			Подразделение = ЭлементОтбора.ПравоеЗначение;
		КонецЕсли;
	КонецЦикла;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	НачалоПериода = ПараметрыОтчета.НачалоПериода;
	КонецПериода = КонецДня(ПараметрыОтчета.КонецПериода);
	
	Если ПрошлыйПериод Тогда
		НачалоПериода = ДобавитьМесяц(ПараметрыОтчета.НачалоПериода, -12);
		КонецПериода = ДобавитьМесяц(КонецДня(ПараметрыОтчета.КонецПериода), -12);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоПериода, КонецПериода, Истина);
	ПредставлениеПериода = СтрШаблон(НСтр("ru='%1'"), СтрЗаменить(ПредставлениеПериода, " г.", ""));

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		Если ПараметрыОтчета.Свойство("Периодичность") И ПараметрыОтчета.Периодичность > 0 Тогда
			ПредставлениеПериода = СтрШаблон(НСтр("ru='%1'"), Формат(Выборка.Период, "ДФ='ММММ гггг'"));
		КонецЕсли;

		СтрокаНалогНаПрибыль = ТаблицаРезультат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНалогНаПрибыль, Выборка);
		СтрокаНалогНаПрибыль.ПредставлениеПериода = ПредставлениеПериода; 
		СтрокаНалогНаПрибыль.Показатель = СтрШаблон(НСтр("ru='%1'"), Выборка.Показатель);

	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПоляДляРасшифровкиВыручки(ДополнительныеПоля)
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Истина);
	СтрокаДляРасшифровки.Вставить("Поле", "Количество");
	СтрокаДляРасшифровки.Вставить("Представление", "Количество");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Истина);
	СтрокаДляРасшифровки.Вставить("Поле", "Стоимость");
	СтрокаДляРасшифровки.Вставить("Представление", "Стоимость");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Истина);
	СтрокаДляРасшифровки.Вставить("Поле", "ВыручкаБезНДС");
	СтрокаДляРасшифровки.Вставить("Представление", "Выручка без НДС");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Истина);
	СтрокаДляРасшифровки.Вставить("Поле", "РентабельностьБезНДС");
	СтрокаДляРасшифровки.Вставить("Представление", "Рентабельность, %");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Ложь);
	СтрокаДляРасшифровки.Вставить("Поле", "СтоимостьЗаЕдиницу");
	СтрокаДляРасшифровки.Вставить("Представление", "Стоимость за единицу");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
	СтрокаДляРасшифровки = Новый Структура;
	СтрокаДляРасшифровки.Вставить("Использование", Ложь);
	СтрокаДляРасшифровки.Вставить("Поле", "ЦенаРеализацииБезНДС");
	СтрокаДляРасшифровки.Вставить("Представление", "Цена реализации");
	
	ДополнительныеПоля.Добавить(СтрокаДляРасшифровки);
	
КонецПроцедуры

Функция ЭтоРасшифровкаПоказателей(ИмяПоля)
	
	Возврат ИмяПоля = "СчетРасшифровка" 
		Или ИмяПоля = "АналитикаРасшифровка"
		Или ИмяПоля = "ДоговорРасшифровка"
		Или ИмяПоля = "КорСчет"
		Или ИмяПоля = "Показатель"
		Или ИмяПоля = "Период"
		Или ИмяПоля = "Сумма";
	
КонецФункции

Функция ДанныеДляРасчетаВаловойПрибыли(ТаблицаВыручкаСебестоимость, ПараметрыОтчета)
	
	Результат = ТаблицаПоказателейДляРасшифровки();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет КАК Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет КАК КорСчет,
	               |	ТаблицаВыручкаСебестоимость.Аналитика КАК Аналитика,
	               |	ТаблицаВыручкаСебестоимость.Сумма КАК Сумма,
	               |	ТаблицаВыручкаСебестоимость.Период КАК Период
	               |ПОМЕСТИТЬ ТаблицаВыручкаСебестоимость
	               |ИЗ
	               |	&ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |ГДЕ
	               |	(ТаблицаВыручкаСебестоимость.Аналитика = &Аналитика
	               |			ИЛИ &ВсяАналитика)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет КАК Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет КАК КорСчет,
	               |	СУММА(ТаблицаВыручкаСебестоимость.Сумма) КАК Сумма,
	               |	ТаблицаВыручкаСебестоимость.Период КАК Период
	               |ПОМЕСТИТЬ ВаловаяПрибыль
	               |ИЗ
	               |	ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |ГДЕ
	               |	ТаблицаВыручкаСебестоимость.Показатель = ""Валовая прибыль""
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаВыручкаСебестоимость.Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет,
	               |	ТаблицаВыручкаСебестоимость.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет КАК Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет КАК КорСчет,
	               |	СУММА(ТаблицаВыручкаСебестоимость.Сумма) КАК Сумма,
	               |	ТаблицаВыручкаСебестоимость.Период КАК Период
	               |ПОМЕСТИТЬ Выручка
	               |ИЗ
	               |	ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |ГДЕ
	               |	ТаблицаВыручкаСебестоимость.Показатель = ""Выручка""
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаВыручкаСебестоимость.Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет,
	               |	ТаблицаВыручкаСебестоимость.Показатель,
	               |	ТаблицаВыручкаСебестоимость.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет КАК Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет КАК КорСчет,
	               |	СУММА(ТаблицаВыручкаСебестоимость.Сумма) КАК Сумма,
	               |	ТаблицаВыручкаСебестоимость.Период КАК Период
	               |ПОМЕСТИТЬ Себестоимость
	               |ИЗ
	               |	ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |ГДЕ
	               |	ТаблицаВыручкаСебестоимость.Показатель = ""Себестоимость""
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаВыручкаСебестоимость.Показатель,
	               |	ТаблицаВыручкаСебестоимость.КорСчет,
	               |	ТаблицаВыручкаСебестоимость.Счет,
	               |	ТаблицаВыручкаСебестоимость.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВаловаяПрибыль.Показатель КАК Показатель,
	               |	Выручка.Показатель КАК ЭлементФормулы,
	               |	ЕСТЬNULL(Выручка.Сумма, 0) КАК СуммаПоказателя,
	               |	ВаловаяПрибыль.Счет КАК Счет,
	               |	ВаловаяПрибыль.КорСчет КАК КорСчет,
	               |	ВаловаяПрибыль.Период КАК Период
	               |ИЗ
	               |	ВаловаяПрибыль КАК ВаловаяПрибыль
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Выручка КАК Выручка
	               |		ПО ВаловаяПрибыль.Счет = Выручка.Счет
	               |			И ВаловаяПрибыль.КорСчет = Выручка.КорСчет
	               |			И ВаловаяПрибыль.Период = Выручка.Период
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВаловаяПрибыль.Показатель,
	               |	Себестоимость.Показатель,
	               |	ЕСТЬNULL(Себестоимость.Сумма, 0),
	               |	ВаловаяПрибыль.Счет,
	               |	ВаловаяПрибыль.КорСчет,
	               |	ВаловаяПрибыль.Период
	               |ИЗ
	               |	ВаловаяПрибыль КАК ВаловаяПрибыль
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Себестоимость КАК Себестоимость
	               |		ПО ВаловаяПрибыль.Счет = Себестоимость.Счет
	               |			И ВаловаяПрибыль.КорСчет = Себестоимость.КорСчет
	               |			И ВаловаяПрибыль.Период = Себестоимость.Период
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	СуммаПоказателя УБЫВ";
	
	Запрос.УстановитьПараметр("ТаблицаВыручкаСебестоимость", ТаблицаВыручкаСебестоимость);
	Запрос.УстановитьПараметр("Аналитика", ПараметрыОтчета.Аналитика);
	Запрос.УстановитьПараметр("ВсяАналитика", Не ЗначениеЗаполнено(ПараметрыОтчета.Аналитика));

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	СчетчикПоказателей = 0;
	СоответствиеПоказателей = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		
		Если СоответствиеПоказателей.Получить(Выборка.ЭлементФормулы) = Неопределено Тогда 
			СчетчикПоказателей = СчетчикПоказателей + 1;
			СоответствиеПоказателей.Вставить(Выборка.ЭлементФормулы, СчетчикПоказателей);
		КонецЕсли;
		
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.НомерПоказателя = СоответствиеПоказателей.Получить(Выборка.ЭлементФормулы);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеДляРасчетаПрибылиУбыткуОтПродаж(ТаблицаВыручкаСебестоимость, ТаблицаРасходов, ПараметрыОтчета, ТолькоПоказатель = Ложь)
	
	Результат = ТаблицаПоказателейДляРасшифровки();
	
	СтруктураОтчета = СтруктураОтчета();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Аналитика КАК Аналитика,
	               |	ТаблицаВыручкаСебестоимость.Счет КАК Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет КАК КорСчет,
	               |	ТаблицаВыручкаСебестоимость.Сумма КАК Сумма,
	               |	ТаблицаВыручкаСебестоимость.Период КАК Период
	               |ПОМЕСТИТЬ ТаблицаВыручкаСебестоимость
	               |ИЗ
	               |	&ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаРасходов.Показатель КАК Показатель,
	               |	ТаблицаРасходов.Аналитика КАК Аналитика,
	               |	ТаблицаРасходов.Счет КАК Счет,
	               |	ТаблицаРасходов.Сумма КАК Сумма,
	               |	ТаблицаРасходов.КорСчет КАК КорСчет,
	               |	ТаблицаРасходов.Период КАК Период
	               |ПОМЕСТИТЬ ТаблицаРасходов
	               |ИЗ
	               |	&ТаблицаРасходов КАК ТаблицаРасходов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет КАК Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет КАК КорСчет,
	               |	СУММА(ТаблицаВыручкаСебестоимость.Сумма) КАК Сумма,
	               |	ТаблицаВыручкаСебестоимость.Период КАК Период
	               |ПОМЕСТИТЬ ВаловаяПрибыль
	               |ИЗ
	               |	ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |ГДЕ
	               |	ТаблицаВыручкаСебестоимость.Показатель = &ВаловаяПрибыль
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаВыручкаСебестоимость.Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет,
	               |	ТаблицаВыручкаСебестоимость.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаРасходов.Показатель КАК Показатель,
	               |	ТаблицаРасходов.Счет КАК Счет,
	               |	СУММА(ТаблицаРасходов.Сумма) КАК Сумма,
	               |	ТаблицаРасходов.КорСчет КАК КорСчет,
	               |	ТаблицаРасходов.Период КАК Период
	               |ПОМЕСТИТЬ Расходы
	               |ИЗ
	               |	ТаблицаРасходов КАК ТаблицаРасходов
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаРасходов.Показатель,
	               |	ТаблицаРасходов.Счет,
	               |	ТаблицаРасходов.КорСчет,
	               |	ТаблицаРасходов.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВаловаяПрибыль.Период КАК Период,
	               |	&Показатель КАК Показатель,
	               |	ВЫБОР
	               |		КОГДА &ТолькоПоказатель
	               |			ТОГДА &ПрибыльУбытокОтПродаж
	               |		ИНАЧЕ ВаловаяПрибыль.Показатель
	               |	КОНЕЦ КАК ЭлементФормулы,
	               |	ВаловаяПрибыль.Счет КАК Счет,
	               |	ВаловаяПрибыль.КорСчет КАК КорСчет,
	               |	СУММА(ВаловаяПрибыль.Сумма) КАК СуммаПоказателя
	               |ИЗ
	               |	ВаловаяПрибыль КАК ВаловаяПрибыль
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫБОР
	               |		КОГДА &ТолькоПоказатель
	               |			ТОГДА &ПрибыльУбытокОтПродаж
	               |		ИНАЧЕ ВаловаяПрибыль.Показатель
	               |	КОНЕЦ,
	               |	ВаловаяПрибыль.Счет,
	               |	ВаловаяПрибыль.КорСчет,
	               |	ВаловаяПрибыль.Период
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	Расходы.Период,
	               |	&Показатель,
	               |	ВЫБОР
	               |		КОГДА &ТолькоПоказатель
	               |			ТОГДА &ПрибыльУбытокОтПродаж
	               |		ИНАЧЕ Расходы.Показатель
	               |	КОНЕЦ,
	               |	Расходы.Счет,
	               |	Расходы.КорСчет,
	               |	СУММА(Расходы.Сумма)
	               |ИЗ
	               |	Расходы КАК Расходы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫБОР
	               |		КОГДА &ТолькоПоказатель
	               |			ТОГДА &ПрибыльУбытокОтПродаж
	               |		ИНАЧЕ Расходы.Показатель
	               |	КОНЕЦ,
	               |	Расходы.Счет,
	               |	Расходы.КорСчет,
	               |	Расходы.Период
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	СуммаПоказателя УБЫВ";
	
	Запрос.УстановитьПараметр("ТаблицаВыручкаСебестоимость", ТаблицаВыручкаСебестоимость);
	Запрос.УстановитьПараметр("ТаблицаРасходов", ТаблицаРасходов);
	Запрос.УстановитьПараметр("Показатель", ПараметрыОтчета.Показатель);
	Запрос.УстановитьПараметр("ТолькоПоказатель", ТолькоПоказатель);   
	Запрос.УстановитьПараметр("ВаловаяПрибыль", СтруктураОтчета.ВаловаяПрибыль.Наименование);
	Запрос.УстановитьПараметр("ПрибыльУбытокОтПродаж", СтруктураОтчета.ПрибыльУбытокОтПродаж.Наименование);
	Запрос.УстановитьПараметр("УправленческиеРасходы", СтруктураОтчета.УправленческиеРасходы.Наименование);
	Запрос.УстановитьПараметр("КоммерческиеРасходы", СтруктураОтчета.КоммерческиеРасходы.Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	СчетчикПоказателей = 0;
	СоответствиеПоказателей = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		
		Если СоответствиеПоказателей.Получить(Выборка.ЭлементФормулы) = Неопределено Тогда 
			СчетчикПоказателей = СчетчикПоказателей + 1;
			СоответствиеПоказателей.Вставить(Выборка.ЭлементФормулы, СчетчикПоказателей);
		КонецЕсли;
		
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.НомерПоказателя = СоответствиеПоказателей.Получить(Выборка.ЭлементФормулы);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеДляРасчетаРентабельностьПродаж(ТаблицаВыручкаСебестоимость, ТаблицаПрибыльУбытокОтПродаж, ПараметрыОтчета)
	
	Результат = ТаблицаПоказателейДляРасшифровки();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Сумма КАК Сумма
	               |ПОМЕСТИТЬ ТаблицаВыручкаСебестоимость
	               |ИЗ
	               |	&ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПрибыльУбытокОтПродаж.ЭлементФормулы КАК Показатель,
	               |	ТаблицаПрибыльУбытокОтПродаж.СуммаПоказателя КАК Сумма
	               |ПОМЕСТИТЬ ТаблицаПрибыльУбытокОтПродаж
	               |ИЗ
	               |	&ТаблицаПрибыльУбытокОтПродаж КАК ТаблицаПрибыльУбытокОтПродаж
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	СУММА(ТаблицаВыручкаСебестоимость.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ Выручка
	               |ИЗ
	               |	ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |ГДЕ
	               |	ТаблицаВыручкаСебестоимость.Показатель = ""Выручка""
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаВыручкаСебестоимость.Показатель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПрибыльУбытокОтПродаж.Показатель КАК Показатель,
	               |	СУММА(ТаблицаПрибыльУбытокОтПродаж.Сумма) КАК Сумма
	               |ПОМЕСТИТЬ ПрибыльУбытокОтПродаж
	               |ИЗ
	               |	ТаблицаПрибыльУбытокОтПродаж КАК ТаблицаПрибыльУбытокОтПродаж
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаПрибыльУбытокОтПродаж.Показатель
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&Показатель КАК Показатель,
	               |	Выручка.Показатель КАК ЭлементФормулы,
	               |	СУММА(Выручка.Сумма) КАК СуммаПоказателя
	               |ИЗ
	               |	Выручка КАК Выручка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Выручка.Показатель
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Показатель,
	               |	ПрибыльУбытокОтПродаж.Показатель,
	               |	СУММА(ПрибыльУбытокОтПродаж.Сумма)
	               |ИЗ
	               |	ПрибыльУбытокОтПродаж КАК ПрибыльУбытокОтПродаж
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПрибыльУбытокОтПродаж.Показатель
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	СуммаПоказателя УБЫВ";
	
	Запрос.УстановитьПараметр("ТаблицаВыручкаСебестоимость", ТаблицаВыручкаСебестоимость);
	Запрос.УстановитьПараметр("ТаблицаПрибыльУбытокОтПродаж", ТаблицаПрибыльУбытокОтПродаж);
	Запрос.УстановитьПараметр("Показатель", ПараметрыОтчета.Показатель);
	
	РезультатЗапроса = Запрос.Выполнить();
	ДанныеПоказателей = РезультатЗапроса.Выгрузить();
	Выручка = 0;
	ПрибыльУбытокОтПродаж = 0;
	СчетчикПоказателей = 0;
	КоличествоПоказателей = ДанныеПоказателей.Количество();
	СоответствиеПоказателей = Новый Соответствие;
	Для Каждого СтрокаДанных Из ДанныеПоказателей Цикл
		Если СоответствиеПоказателей.Получить(СтрокаДанных.ЭлементФормулы) = Неопределено Тогда 
			
			Если КоличествоПоказателей > 1 Тогда
				СчетчикПоказателей = СчетчикПоказателей + 1;
			КонецЕсли;
			
			СоответствиеПоказателей.Вставить(СтрокаДанных.ЭлементФормулы, СчетчикПоказателей);
			
		КонецЕсли;
		
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.НомерПоказателя = СоответствиеПоказателей.Получить(СтрокаДанных.ЭлементФормулы);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
		
		Если СтрокаДанных.ЭлементФормулы = "Выручка" Тогда
			Выручка = СтрокаДанных.СуммаПоказателя;
		КонецЕсли;
		
		Если СтрокаДанных.ЭлементФормулы = "Прибыль (убыток) от продаж" Тогда
			ПрибыльУбытокОтПродаж = СтрокаДанных.СуммаПоказателя;
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаРентабельностьПродаж = Результат.Добавить();
	СтрокаРентабельностьПродаж.Показатель = "Рентабельность продаж";
	Если Выручка <> 0 И ПрибыльУбытокОтПродаж <> 0 Тогда
		СтрокаРентабельностьПродаж.СуммаПоказателя = Окр(ПрибыльУбытокОтПродаж / Выручка * 100, 2);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеДляРасчетаПрибылиУбыткуДоНалогообложения(ТаблицаВыручкаСебестоимость, ТаблицаРасходов,
		ТаблицаПрочихДоходовИРасходов, ПараметрыОтчета, ТолькоПоказатель = Ложь)
	
	Результат = ТаблицаПоказателейДляРасшифровки();
	
	СтруктураОтчета = СтруктураОтчета();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Аналитика КАК Аналитика,
	               |	ТаблицаВыручкаСебестоимость.Счет КАК Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет КАК КорСчет,
	               |	ТаблицаВыручкаСебестоимость.Сумма КАК Сумма,
	               |	ТаблицаВыручкаСебестоимость.Период КАК Период
	               |ПОМЕСТИТЬ ТаблицаВыручкаСебестоимость
	               |ИЗ
	               |	&ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаРасходов.Показатель КАК Показатель,
	               |	ТаблицаРасходов.Аналитика КАК Аналитика,
	               |	ТаблицаРасходов.Счет КАК Счет,
	               |	ТаблицаРасходов.КорСчет КАК КорСчет,
	               |	ТаблицаРасходов.Сумма КАК Сумма,
	               |	ТаблицаРасходов.Период КАК Период
	               |ПОМЕСТИТЬ ТаблицаРасходов
	               |ИЗ
	               |	&ТаблицаРасходов КАК ТаблицаРасходов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПрочихДоходовИРасходов.Показатель КАК Показатель,
	               |	ТаблицаПрочихДоходовИРасходов.Аналитика КАК Аналитика,
	               |	ТаблицаПрочихДоходовИРасходов.Счет КАК Счет,
	               |	ТаблицаПрочихДоходовИРасходов.КорСчет КАК КорСчет,
	               |	ТаблицаПрочихДоходовИРасходов.Сумма КАК Сумма,
	               |	ТаблицаПрочихДоходовИРасходов.Период КАК Период
	               |ПОМЕСТИТЬ ТаблицаПрочихДоходовИРасходов
	               |ИЗ
	               |	&ТаблицаПрочихДоходовИРасходов КАК ТаблицаПрочихДоходовИРасходов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаВыручкаСебестоимость.Показатель КАК Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет КАК Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет КАК КорСчет,
	               |	СУММА(ТаблицаВыручкаСебестоимость.Сумма) КАК Сумма,
	               |	ТаблицаВыручкаСебестоимость.Период КАК Период
	               |ПОМЕСТИТЬ ВаловаяПрибыль
	               |ИЗ
	               |	ТаблицаВыручкаСебестоимость КАК ТаблицаВыручкаСебестоимость
	               |ГДЕ
	               |	ТаблицаВыручкаСебестоимость.Показатель = &ВаловаяПрибыль
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаВыручкаСебестоимость.Показатель,
	               |	ТаблицаВыручкаСебестоимость.Счет,
	               |	ТаблицаВыручкаСебестоимость.КорСчет,
	               |	ТаблицаВыручкаСебестоимость.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаРасходов.Показатель КАК Показатель,
	               |	ТаблицаРасходов.Счет КАК Счет,
	               |	ТаблицаРасходов.КорСчет КАК КорСчет,
	               |	СУММА(ТаблицаРасходов.Сумма) КАК Сумма,
	               |	ТаблицаРасходов.Период КАК Период
	               |ПОМЕСТИТЬ Расходы
	               |ИЗ
	               |	ТаблицаРасходов КАК ТаблицаРасходов
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаРасходов.Показатель,
	               |	ТаблицаРасходов.Счет,
	               |	ТаблицаРасходов.КорСчет,
	               |	ТаблицаРасходов.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаПрочихДоходовИРасходов.Показатель КАК Показатель,
	               |	ТаблицаПрочихДоходовИРасходов.Счет КАК Счет,
	               |	ТаблицаПрочихДоходовИРасходов.КорСчет КАК КорСчет,
	               |	СУММА(ТаблицаПрочихДоходовИРасходов.Сумма) КАК Сумма,
	               |	ТаблицаПрочихДоходовИРасходов.Период КАК Период
	               |ПОМЕСТИТЬ ПрочиеДоходыИРасходы
	               |ИЗ
	               |	ТаблицаПрочихДоходовИРасходов КАК ТаблицаПрочихДоходовИРасходов
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТаблицаПрочихДоходовИРасходов.Показатель,
	               |	ТаблицаПрочихДоходовИРасходов.Счет,
	               |	ТаблицаПрочихДоходовИРасходов.КорСчет,
	               |	ТаблицаПрочихДоходовИРасходов.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&Показатель КАК Показатель,
	               |	ВЫБОР
	               |		КОГДА &ТолькоПоказатель
	               |			ТОГДА &ПрибыльУбытокДоНалогообложения
	               |		ИНАЧЕ &ПрибыльУбытокОтПродаж
	               |	КОНЕЦ КАК ЭлементФормулы,
	               |	ВаловаяПрибыль.Счет КАК Счет,
	               |	ВаловаяПрибыль.КорСчет КАК КорСчет,
	               |	СУММА(ВаловаяПрибыль.Сумма) КАК СуммаПоказателя,
	               |	ВаловаяПрибыль.Период КАК Период
	               |ИЗ
	               |	ВаловаяПрибыль КАК ВаловаяПрибыль
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВаловаяПрибыль.Счет,
	               |	ВаловаяПрибыль.КорСчет,
	               |	ВаловаяПрибыль.Период
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Показатель,
	               |	ВЫБОР
	               |		КОГДА &ТолькоПоказатель
	               |			ТОГДА &ПрибыльУбытокДоНалогообложения
	               |		ИНАЧЕ &ПрибыльУбытокОтПродаж
	               |	КОНЕЦ,
	               |	Расходы.Счет,
	               |	Расходы.КорСчет,
	               |	СУММА(Расходы.Сумма),
	               |	Расходы.Период
	               |ИЗ
	               |	Расходы КАК Расходы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Расходы.Счет,
	               |	Расходы.КорСчет,
	               |	Расходы.Период
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	&Показатель,
	               |	ВЫБОР
	               |		КОГДА &ТолькоПоказатель
	               |			ТОГДА &ПрибыльУбытокДоНалогообложения
	               |		ИНАЧЕ ПрочиеДоходыИРасходы.Показатель
	               |	КОНЕЦ,
	               |	ПрочиеДоходыИРасходы.Счет,
	               |	ПрочиеДоходыИРасходы.КорСчет,
	               |	СУММА(ПрочиеДоходыИРасходы.Сумма),
	               |	ПрочиеДоходыИРасходы.Период
	               |ИЗ
	               |	ПрочиеДоходыИРасходы КАК ПрочиеДоходыИРасходы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВЫБОР
	               |		КОГДА &ТолькоПоказатель
	               |			ТОГДА &ПрибыльУбытокДоНалогообложения
	               |		ИНАЧЕ ПрочиеДоходыИРасходы.Показатель
	               |	КОНЕЦ,
	               |	ПрочиеДоходыИРасходы.Счет,
	               |	ПрочиеДоходыИРасходы.КорСчет,
	               |	ПрочиеДоходыИРасходы.Период
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	СуммаПоказателя УБЫВ";
	
	Запрос.УстановитьПараметр("ТаблицаВыручкаСебестоимость", ТаблицаВыручкаСебестоимость);
	Запрос.УстановитьПараметр("ТаблицаРасходов", ТаблицаРасходов);
	Запрос.УстановитьПараметр("ТаблицаПрочихДоходовИРасходов", ТаблицаПрочихДоходовИРасходов);
	Запрос.УстановитьПараметр("Показатель", ПараметрыОтчета.Показатель);
	Запрос.УстановитьПараметр("ТолькоПоказатель", ТолькоПоказатель);
	Запрос.УстановитьПараметр("ВаловаяПрибыль", СтруктураОтчета.ВаловаяПрибыль.Наименование);
	Запрос.УстановитьПараметр("ПрибыльУбытокОтПродаж", СтруктураОтчета.ПрибыльУбытокОтПродаж.Наименование);
	Запрос.УстановитьПараметр("ПрибыльУбытокДоНалогообложения", СтруктураОтчета.ПрибыльУбытокДоНалогообложения.Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	ДанныеПоказателей = РезультатЗапроса.Выгрузить();
	СчетчикПоказателей = 0;
	КоличествоПоказателей = ДанныеПоказателей.Количество();
	СоответствиеПоказателей = Новый Соответствие;
	Для Каждого СтрокаДанных Из ДанныеПоказателей Цикл
		
		Если СоответствиеПоказателей.Получить(СтрокаДанных.ЭлементФормулы) = Неопределено Тогда 
			Если КоличествоПоказателей > 1 Тогда
				СчетчикПоказателей = СчетчикПоказателей + 1;
			КонецЕсли;
			СоответствиеПоказателей.Вставить(СтрокаДанных.ЭлементФормулы, СчетчикПоказателей);
		КонецЕсли;
		
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.НомерПоказателя = СоответствиеПоказателей.Получить(СтрокаДанных.ЭлементФормулы);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);

	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеДляРасчетаЧистойПрибыли(ДанныеПоПрибылиУбыткуДоНалогообложения, ТаблицаНалогНаПрибыль, ПараметрыОтчета)
	
	Результат = ТаблицаПоказателейДляРасшифровки();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.Счет КАК Счет,
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.СуммаПоказателя КАК Сумма,
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.ЭлементФормулы КАК ЭлементФормулы,
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.Период КАК Период
	               |ПОМЕСТИТЬ ДанныеПоПрибылиУбыткуДоНалогообложения
	               |ИЗ
	               |	&ДанныеПоПрибылиУбыткуДоНалогообложения КАК ДанныеПоПрибылиУбыткуДоНалогообложения
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаНалогНаПрибыль.Счет КАК Счет,
	               |	ТаблицаНалогНаПрибыль.Сумма КАК Сумма,
	               |	ТаблицаНалогНаПрибыль.Показатель КАК ЭлементФормулы,
	               |	ТаблицаНалогНаПрибыль.Период КАК Период
	               |ПОМЕСТИТЬ ТаблицаНалогНаПрибыль
	               |ИЗ
	               |	&ТаблицаНалогНаПрибыль КАК ТаблицаНалогНаПрибыль
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.Счет КАК Счет,
	               |	СУММА(ДанныеПоПрибылиУбыткуДоНалогообложения.Сумма) КАК Сумма,
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.ЭлементФормулы КАК ЭлементФормулы,
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.Период КАК Период
	               |ПОМЕСТИТЬ ПрибыльУбытокДоНалогообложения
	               |ИЗ
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения КАК ДанныеПоПрибылиУбыткуДоНалогообложения
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.Счет,
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.ЭлементФормулы,
	               |	ДанныеПоПрибылиУбыткуДоНалогообложения.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НалогНаПрибыль.Счет КАК Счет,
	               |	СУММА(НалогНаПрибыль.Сумма) КАК Сумма,
	               |	НалогНаПрибыль.ЭлементФормулы КАК ЭлементФормулы,
	               |	НалогНаПрибыль.Период КАК Период
	               |ПОМЕСТИТЬ НалогНаПрибыль
	               |ИЗ
	               |	ТаблицаНалогНаПрибыль КАК НалогНаПрибыль
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НалогНаПрибыль.Счет,
	               |	НалогНаПрибыль.ЭлементФормулы,
	               |	НалогНаПрибыль.Период
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ПрибыльУбытокДоНалогообложения.Период КАК Период,
	               |	&Показатель КАК Показатель,
	               |	ПрибыльУбытокДоНалогообложения.ЭлементФормулы КАК ЭлементФормулы,
	               |	ПрибыльУбытокДоНалогообложения.Счет КАК Счет,
	               |	СУММА(ПрибыльУбытокДоНалогообложения.Сумма) КАК СуммаПоказателя,
	               |	1 КАК Порядок
	               |ИЗ
	               |	ПрибыльУбытокДоНалогообложения КАК ПрибыльУбытокДоНалогообложения
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПрибыльУбытокДоНалогообложения.Счет,
	               |	ПрибыльУбытокДоНалогообложения.ЭлементФормулы,
	               |	ПрибыльУбытокДоНалогообложения.Период
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	НалогНаПрибыль.Период,
	               |	&Показатель,
	               |	НалогНаПрибыль.ЭлементФормулы,
	               |	НалогНаПрибыль.Счет,
	               |	СУММА(НалогНаПрибыль.Сумма),
	               |	ВЫБОР
	               |		КОГДА НалогНаПрибыль.ЭлементФормулы = ""Прочее""
	               |			ТОГДА 3
	               |		ИНАЧЕ 2
	               |	КОНЕЦ
	               |ИЗ
	               |	НалогНаПрибыль КАК НалогНаПрибыль
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НалогНаПрибыль.Счет,
	               |	НалогНаПрибыль.ЭлементФормулы,
	               |	НалогНаПрибыль.Период
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Порядок,
	               |	СуммаПоказателя УБЫВ";
	
	Запрос.УстановитьПараметр("ДанныеПоПрибылиУбыткуДоНалогообложения", ДанныеПоПрибылиУбыткуДоНалогообложения);
	Запрос.УстановитьПараметр("ТаблицаНалогНаПрибыль", ТаблицаНалогНаПрибыль);
	Запрос.УстановитьПараметр("Показатель", ПараметрыОтчета.Показатель);
	
	РезультатЗапроса = Запрос.Выполнить();
	ДанныеПоказателей = РезультатЗапроса.Выгрузить();
	СчетчикПоказателей = 0;
	КоличествоПоказателей = ДанныеПоказателей.Количество();
	СоответствиеПоказателей = Новый Соответствие;
	Для Каждого СтрокаДанных Из ДанныеПоказателей Цикл
		
		Если СоответствиеПоказателей.Получить(СтрокаДанных.ЭлементФормулы) = Неопределено Тогда 
			Если КоличествоПоказателей > 1 Тогда
				СчетчикПоказателей = СчетчикПоказателей + 1;
			КонецЕсли;

			СоответствиеПоказателей.Вставить(СтрокаДанных.ЭлементФормулы, СчетчикПоказателей);
		КонецЕсли;
		
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.НомерПоказателя = СоответствиеПоказателей.Получить(СтрокаДанных.ЭлементФормулы);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);

	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СтрокаДляРасшифровки()
	
	Возврат Новый Структура("Использование, Поле, Представление, ТипГруппировки");
	
КонецФункции

Функция ЭтоАнализВыручки(Показатель)
	
	Возврат Показатель = "Выручка" Или Показатель = "Себестоимость" Или Показатель = "Валовая прибыль";
	
КонецФункции

Функция ПустоеЗначениеОтбораПоПоказателю(Показатель)

	Если Показатель = "Коммерческие расходы" 
		Или Показатель = "Управленческие расходы" Тогда
		Возврат Справочники.СтатьиЗатрат.ПустаяСсылка();
	ИначеЕсли  Показатель = "Доходы от участия в других организациях" 
		Или Показатель = "Проценты к получению" 
		Или Показатель = "Проценты к уплате"
		Или Показатель = "Прочие доходы"
		Или Показатель = "Прочие расходы" Тогда
		Возврат Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПоказательЧистаяПрибыль()
	
	Возврат НСтр("ru='Чистая прибыль'");
	
КонецФункции

Функция УстановитьСчетПоПоказателю(Показатель)
	
	Счет = Неопределено;
	Если Показатель = "Коммерческие расходы" Тогда
		Счет = ПланыСчетов.Хозрасчетный.КоммерческиеРасходы;
	ИначеЕсли Показатель = "Управленческие расходы" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы;
	ИначеЕсли Показатель = "Прочие доходы" 
		Или Показатель = "Доходы от участия в других организациях" 
		Или Показатель = "Проценты к получению" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
	ИначеЕсли Показатель = "Прочие расходы" 
		Или Показатель = "Проценты к уплате" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	ИначеЕсли Показатель = "Прочие расходы" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	ИначеЕсли Показатель = "Налог на прибыль"
		Или Показатель = "Налог при УСН"
		Или Показатель = "Налог на профессиональный доход" Тогда
		Счет = ПланыСчетов.Хозрасчетный.НалогНаПрибыль;
	ИначеЕсли Показатель = "Прочее" Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеПрибылиИУбытки;
	КонецЕсли;
	
	Возврат Счет;
	
КонецФункции

Процедура УстановитьОтборПоОрганизации(ТекстЗапроса, ПараметрыОтбора);
	
	СхемаЗапроса = СхемыЗапросов.Создать(ТекстЗапроса);
	
	Для Каждого ОписаниеЗапроса Из СхемаЗапроса.ПакетЗапросов Цикл
		
		Контекст = Новый Структура;
		Контекст.Вставить("Схема",  СхемаЗапроса);
		Контекст.Вставить("Запрос", ОписаниеЗапроса);
		
		Если ТипЗнч(ОписаниеЗапроса) <> Тип("ЗапросВыбораСхемыЗапроса") Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяВременнойТаблицы =  ОписаниеЗапроса.ТаблицаДляПомещения;
		ИндексКолонки = -1;
		
		Для Каждого ОператорТаблицы Из ОписаниеЗапроса.Операторы Цикл
			
			Для Каждого ТаблицыИсточники Из ОператорТаблицы.Источники Цикл
				
				Если ТипЗнч(ТаблицыИсточники.Источник) <> Тип("ТаблицаСхемыЗапроса")
					Или СтрНайти(ТаблицыИсточники.Источник.ИмяТаблицы, "Хозрасчетный") = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Контекст.Вставить("Таблица", ТаблицыИсточники.Источник);
				
				Если ЗначениеЗаполнено(ПараметрыОтбора.Организация)
					И Не ПустаяСтрока(ПараметрыОтбора.ОрганизацияФормулаСравнения) Тогда
					
					Если ПараметрыОтбора.ВключатьОбособленныеПодразделения Тогда 
						Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация.ГоловнаяОрганизация", "&Организация");
					Иначе
						Условие = СтрШаблон(ПараметрыОтбора.ОрганизацияФормулаСравнения, "Организация", "&Организация");
					КонецЕсли;
					СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ПараметрыОтбора.Подразделение)
					И Не ПустаяСтрока(ПараметрыОтбора.ПодразделениеФормулаСравнения) Тогда 
					
					Если СтрНайти(ТаблицыИсточники.Источник.ИмяТаблицы, "ОборотыДтКт") > 1 Тогда 
						Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(ПодразделениеДт, &ПустоеПодразделение)", "&Подразделение");
					Иначе
						Условие = СтрШаблон(ПараметрыОтбора.ПодразделениеФормулаСравнения, "ЕСТЬNULL(Подразделение, &ПустоеПодразделение)", "&Подразделение");
					КонецЕсли;
					
					СхемыЗапросов.ДополнитьУсловиеРегистраБухгалтерии(Контекст, Условие);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
