#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияАктуализации Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	Если ЗначениеЗаполнено(Параметры.ВидРасхода) Тогда
		ЭтоРасшифровка = Истина;
		ВидРасхода = Параметры.ВидРасхода;
		Отчет.НачалоПериода = НачалоДня(Параметры.НачалоПериода);
		Отчет.КонецПериода = КонецДня(Параметры.КонецПериода);
		Отчет.Организация = Параметры.Организация;
	КонецЕсли;
	
	БухгалтерскиеОтчеты.ИнициализироватьРежимВыгрузкиБП(ЭтотОбъект);
	
	Элементы.Организация.Видимость = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтоРасшифровка Тогда
		СформироватьОтчетНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОбработкаОповещенияАктуализации(
		ЭтотОбъект, Отчет.Организация, КонецДня(Отчет.КонецПериода), ИмяСобытия, Параметр, Источник);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПолученнаяРасшифровка = ПолучитьВидРасшифровки(Расшифровка, ДанныеРасшифровки, ЭтоРасшифровка);
	
	Если Не ЗначениеЗаполнено(ПолученнаяРасшифровка) Или (ЭтоРасшифровка И Не ЭтоДокумент(ПолученнаяРасшифровка)) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоРасшифровка Тогда
		// Открываем отчет принятия расходов по документу
		ИмяФормыОтчета = "Отчет.РасходыУСН.Форма.ФормаРасходыПоДокументу";
		ПараметрыФормы = Новый Структура("ОбъектОтбора, Организация", ПолученнаяРасшифровка, Отчет.Организация);
	Иначе
		ИмяФормыОтчета = "Отчет.РасходыУСН.Форма.ФормаОтчета";
		ПараметрыФормы = ПараметрыРасшифровкиПоВидуРасхода(ПолученнаяРасшифровка);
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормыОтчета, ПараметрыФормы, ЭтотОбъект, Новый УникальныйИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	// Не будем обрабатывать нажатие на правую кнопку мыши.
	// Покажем стандартное контекстное меню ячейки табличного документа.
	Расшифровка = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		"Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", НачалоДня(Отчет.НачалоПериода), КонецДня(Отчет.КонецПериода));
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Актуализировать(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.Актуализировать(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьАктуализацию(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Если Элементы.Результат.ОтображениеСостояния.Видимость Тогда
		
		ТекущийЭлемент = Элементы.СформироватьОтчет;
		ПоказатьПредупреждение( , НСтр("ru = 'Нажмите ""Сформировать"" для получения отчета'"), ,
								 НСтр("ru = 'Отчет не сформирован'"));
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Результат",     Результат);
	ПараметрыОтчета.Вставить("Организация",   Отчет.Организация);
	ПараметрыОтчета.Вставить("НачалоПериода", НачалоДня(Отчет.НачалоПериода));
	ПараметрыОтчета.Вставить("КонецПериода",  КонецДня(Отчет.КонецПериода));
	
	Закрыть(ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочте(Команда)
	
	ОтправкаПочтовыхСообщенийКлиент.ОтправитьОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СформироватьОтчетНаКлиенте();
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьОтчетНаКлиенте()
	
	Если ЭтоРасшифровка Тогда
		ПараметрыРасшифровкиПоВидуРасхода = ПараметрыРасшифровкиПоВидуРасхода(ВидРасхода);
	Иначе
		ПараметрыРасшифровкиПоВидуРасхода = Неопределено;
	КонецЕсли;
	ЗапуститьФормированиеОтчета(ПараметрыРасшифровкиПоВидуРасхода);
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(
		ЭтотОбъект, Отчет.Организация, КонецДня(Отчет.КонецПериода));
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстЗаголовка()
	
	ПредставлениеПериодаОтчета =
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.НачалоПериода, Отчет.КонецПериода, Ложь);
	Если ЭтоРасшифровка Тогда
		ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Расходы УСН по виду %1 %2'"),
			Перечисления.ВидыРасходовУСН.ПредставлениеВидаРасходаУСН(ВидРасхода),
			ПредставлениеПериодаОтчета);
	Иначе
		ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Расходы УСН%1'"),
			ПредставлениеПериодаОтчета);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		ТекстОрганизация = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(
			Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
		ЗаголовокОтчета = ЗаголовокОтчета + " " + ТекстОрганизация;
	КонецЕсли;
	
	Заголовок = ЗаголовокОтчета;

КонецПроцедуры

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализацииАвтоматически(ЭтотОбъект);
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияКомпоновщикаНастроек()
	
	БухгалтерскиеОтчетыВызовСервера.ИнициализацияКомпоновщикаНастроек(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВидРасшифровки(Расшифровка, ДанныеРасшифровки, ЭтоРасшифровка)
	
	ДанныеРасшифровкиПолученные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	ЭлементРасшифровки = ДанныеРасшифровкиПолученные.ДанныеРасшифровки.Элементы.Получить(Расшифровка);
	ПоляРасшифровки = ЭлементРасшифровки.ПолучитьПоля();
	РодителиРасшифровки = ЭлементРасшифровки.ПолучитьРодителей();
	
	МассивРасшифровок = БухгалтерскиеОтчетыВызовСервера.ПолучитьМассивПолейРасшифровки(
		Расшифровка, ДанныеРасшифровкиПолученные.ДанныеРасшифровки);
	Если МассивРасшифровок.Количество() > 0 Тогда
		Возврат МассивРасшифровок[0].Значение;
	ИначеЕсли ЭтоРасшифровка Тогда
		Возврат Перечисления.ВидыРасходовУСН.ПустаяСсылка();
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УправлениеФормой()
	
	ОбновитьТекстЗаголовка();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета(ПараметрыОтчета)
	
	ОчиститьСообщения();
	РезультатВыполнения = СформироватьОтчетНаСервере(ПараметрыОтчета);
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда 
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере(ПараметрыОтчетаРасшифровки)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	ИдентификаторЗадания = Неопределено;
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализацияКомпоновщикаНастроек();
	КонецЕсли;
	
	ПараметрыОтчета = Отчеты.РасходыУСН.ПустыеПараметрыКомпоновкиОтчета();
	Отчеты.РасходыУСН.ЗаполнитьПараметрыИзФормы(ПараметрыОтчета, ЭтотОбъект);
	Если ЭтоРасшифровка Тогда
		ПараметрыОтчета.ИмяВарианта = "ВариантРасшифровка";
		ПараметрыОтчета.Вставить("ВидРасхода", ПараметрыОтчетаРасшифровки.ВидРасхода);
		НастройкиРасшифровки = ПараметрыОтчета.СхемаКомпоновкиДанных.ВариантыНастроек["ВариантРасшифровка"];
		ПараметрыОтчета.НастройкиКомпоновкиДанных = НастройкиРасшифровки.Настройки;
	Иначе
		ПараметрыОтчета.ИмяВарианта = "Основной";
	КонецЕсли;
	
	ОписаниеЗаданияФормированияОтчета = БухгалтерскиеОтчеты.ЗапуститьПодготовкуОтчета(ЭтотОбъект, ПараметрыОтчета);
	
	Возврат ОписаниеЗаданияФормированияОтчета;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполненияОперации)

	РезультатВыполнения = ПолучитьИзВременногоХранилища(РезультатВыполненияОперации.АдресРезультата);
	Результат           = РезультатВыполнения.Результат;
	ДанныеРасшифровки   = РезультатВыполнения.ДанныеРасшифровки;
	
	ИдентификаторЗадания = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеАктуализации()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьВыполнениеАктуализацииОтчета(ЭтотОбъект,
		Отчет.Организация, КонецДня(Отчет.КонецПериода));

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьЗавершениеАктуализации()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьЗавершениеАктуализации(ЭтотОбъект, Отчет.Организация, КонецДня(Отчет.КонецПериода));
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРасшифровкиПоВидуРасхода(Расшифровка)
	
	ПараметрыРасшифровки = Новый Структура;
	ПараметрыРасшифровки.Вставить("Организация", Отчет.Организация);
	ПараметрыРасшифровки.Вставить("НачалоПериода", НачалоДня(Отчет.НачалоПериода));
	ПараметрыРасшифровки.Вставить("КонецПериода", КонецДня(Отчет.КонецПериода));
	ПараметрыРасшифровки.Вставить("ВидРасхода", Расшифровка);
	
	Возврат ПараметрыРасшифровки;
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоДокумент(Расшифровка)
	
	Возврат ОбщегоНазначения.ЭтоДокумент(Расшифровка.Метаданные());
	
КонецФункции

#КонецОбласти