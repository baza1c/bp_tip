#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияАктуализации Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ОбъектОтбора = Параметры.ОбъектОтбора;
	Если Не ЗначениеЗаполнено(ОбъектОтбора) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Организация = Параметры.Организация;
	РеквизитыОбъектаОтбора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектОтбора, "Проведен, Организация");
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = РеквизитыОбъектаОтбора.Организация;
	КонецЕсли;
	ОбъектОтбораПроведен = РеквизитыОбъектаОтбора.Проведен;
	Если ТипЗнч(ОбъектОтбора) = Тип("ДокументСсылка.РегламентнаяОперация") Тогда
		ОбъектОтбораПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектОтбора, "Состояние")
			= Перечисления.ВидыСостоянийРегламентныхОпераций.Выполнено;
	КонецЕсли;
	ДатаПроверкиАктуальности = КонецДня(ТекущаяДатаСеанса());
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ОбъектОтбораПроведен Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Перед просмотром расходов УСН документ следует провести'"), 100);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(
		ЭтотОбъект, Организация, ДатаПроверкиАктуальности);
	
	СформироватьОтчетДлительнаяОперация();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОбработкаОповещенияАктуализации(
		ЭтотОбъект, Отчет.Организация, КонецДня(Отчет.КонецПериода), ИмяСобытия, Параметр, Источник);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТабличныйДокумент", ТабличныйДокументРасшифровкаПоДокументам(Расшифровка));
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Отчет.РасходыУСН.Форма.ФормаРасшифровкаПоДокументам",
		ПараметрыФормы,
		ЭтотОбъект,
		ЭтотОбъект.КлючУникальности);

КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		"Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Актуализировать(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.Актуализировать(
		ЭтотОбъект, Организация, ДатаПроверкиАктуальности);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьАктуализацию(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Если Элементы.Результат.ОтображениеСостояния.Видимость Тогда
		
		ТекущийЭлемент = Элементы.СформироватьОтчет;
		ПоказатьПредупреждение( , НСтр("ru = 'Нажмите ""Сформировать"" для получения отчета'"), ,
								 НСтр("ru = 'Отчет не сформирован'"));
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Результат",     Результат);
	ПараметрыОтчета.Вставить("Организация",   Отчет.Организация);
	ПараметрыОтчета.Вставить("НачалоПериода", Отчет.НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода",  Отчет.КонецПериода); 
	
	Закрыть(ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочте(Команда)
	
	ОтправкаПочтовыхСообщенийКлиент.ОтправитьОтчет(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СформироватьОтчетДлительнаяОперация();
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(
		ЭтотОбъект, Организация, ДатаПроверкиАктуальности);
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьОтчетДлительнаяОперация()
	
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("ОбъектОтбора", ОбъектОтбора);
	ПараметрыФункции.Вставить("Организация", Организация);
	
	ДлительнаяОперация = СформироватьОтчетНаСервереДлительнаяОперация(ПараметрыФункции, УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Отчет формируется...'");
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("СформироватьОтчетЗавершениеДлительнаяОперация", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеЗавершения, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьОтчетНаСервереДлительнаяОперация(ПараметрыФункции, УникальныйИдентификатор)
	
	ИмяФункции = "Отчеты.РасходыУСН.СформироватьРасходыПоДокументу";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование расходов по документу'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции, ПараметрыФункции);
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчетЗавершениеДлительнаяОперация(РезультатДлительнойОперации, ДопоплнительныеПараметры) Экспорт 
	
	// Прервали обработку
	Если РезультатДлительнойОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатДлительнойОперации.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, РезультатДлительнойОперации.КраткоеПредставлениеОшибки);
	Иначе
		ОбработатьРезультатНаСервере(РезультатДлительнойОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатНаСервере(РезультатДлительнойОперации)
	
	ДанныеДляОтчета = ПолучитьИзВременногоХранилища(РезультатДлительнойОперации.АдресРезультата);
	УдалитьИзВременногоХранилища(РезультатДлительнойОперации.АдресРезультата);
	
	Результат = ДанныеДляОтчета.ТабличныйДокумент;
	ТаблицаОплат.Загрузить(ДанныеДляОтчета.ТаблицаОплат);
	ТаблицаСписаний.Загрузить(ДанныеДляОтчета.ТаблицаСписаний);
	ТаблицаОплатПокупателей.Загрузить(ДанныеДляОтчета.ТаблицаОплатПокупателей);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстЗаголовка()
	
	ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Расходы УСН по документу: %1'"),
		ПредставлениеДокументаРасхода(ОбъектОтбора));
	
	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		ТекстОрганизация = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(
			Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
		ЗаголовокОтчета = ЗаголовокОтчета + " " + ТекстОрганизация;
	КонецЕсли;
	
	Заголовок = ЗаголовокОтчета;

КонецПроцедуры

&НаСервере
Функция ТабличныйДокументРасшифровкаПоДокументам(Расшифровка);
	
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("Расшифровка", Расшифровка);
	ПараметрыФормирования.Вставить("ТаблицаСписаний", ТаблицаСписаний.Выгрузить());
	ПараметрыФормирования.Вставить("ТаблицаОплат", ТаблицаОплат.Выгрузить());
	ПараметрыФормирования.Вставить("ТаблицаОплатПокупателей", ТаблицаОплатПокупателей.Выгрузить());

	Возврат Отчеты.РасходыУСН.ТабличныйДокументРасшифровкаПоДокументам(ПараметрыФормирования);
	
КонецФункции

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеДокументаРасхода(Документ)
	
	Возврат Отчеты.РасходыУСН.ПредставлениеДокументаРасхода(Документ);
	
КонецФункции

&НаСервере
Процедура УправлениеФормой()
	
	ОбновитьТекстЗаголовка();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеАктуализации()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьВыполнениеАктуализацииОтчета(ЭтотОбъект,
		Отчет.Организация, Отчет.КонецПериода);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьЗавершениеАктуализации()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьЗавершениеАктуализации(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

#КонецОбласти