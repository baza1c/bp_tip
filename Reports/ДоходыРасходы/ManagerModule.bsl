#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Результат.Вставить("ИспользоватьПослеКомпоновкиМакета",  Ложь);
	Результат.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	Результат.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Результат.Вставить("ИспользоватьРасширенныеПараметрыРасшифровки", Истина);
	
	Возврат Результат;
										
КонецФункции

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт 
	
	Возврат "Доходы и расходы" + БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет
// Изменения сохранены не будут
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
		
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Периодичность", ПараметрыОтчета.Периодичность);
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоДня(ПараметрыОтчета.НачалоПериода));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПараметрПериод", КонецДня(ПараметрыОтчета.КонецПериода));
	Иначе
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПараметрПериод", КонецДня(ТекущаяДатаСеанса()));
	КонецЕсли;
	
	Периодичность = БухгалтерскиеОтчетыКлиентСервер.ПолучитьЗначениеПериодичности(ПараметрыОтчета.Периодичность, ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
	СхемаЭталон = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	ТекстЗапроса = СхемаЭталон.НаборыДанных.Данные.Запрос;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Периодичность", Периодичность);
	
	ВыводитьДиаграмму = Неопределено;
	
	Если Не ПараметрыОтчета.Свойство("ВыводитьДиаграмму", ВыводитьДиаграмму) Тогда
		
		ВыводитьДиаграмму = Истина;
		
	КонецЕсли;
	
	Таблица   = Неопределено;
	Диаграмма = Неопределено;
	
	Для Каждого ЭлементСтруктуры Из КомпоновщикНастроек.Настройки.Структура Цикл
		Если ЭлементСтруктуры.Имя = "Таблица" Тогда
			Таблица = ЭлементСтруктуры;
		ИначеЕсли ЭлементСтруктуры.Имя = "Диаграмма" Тогда
			Диаграмма = ЭлементСтруктуры;
		КонецЕсли;
	КонецЦикла;
	
	Если Диаграмма <> Неопределено Тогда
		Диаграмма.Использование = ВыводитьДиаграмму;
		
		Диаграмма.Точки.Очистить();
		ГруппировкаПериод = Диаграмма.Точки.Добавить();
		ЗаполнитьГруппировкуПериод(ПараметрыОтчета, Периодичность, ГруппировкаПериод);
		
	КонецЕсли;
	
	Если Таблица <> Неопределено Тогда
		Таблица.Колонки.Очистить();
		ГруппировкаПериод = Таблица.Колонки.Добавить();
		
		ЗаполнитьГруппировкуПериод(ПараметрыОтчета, Периодичность, ГруппировкаПериод);
		
		ЕстьГруппирровкаПоВиду = Ложь;
		// Группировка
		Таблица.Строки.Очистить();
		Группировка = Таблица.Строки;
		Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл 
			Если ПолеВыбраннойГруппировки.Использование Тогда
				Если ТипЗнч(Группировка) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
					Группировка = Группировка.Добавить();
				Иначе
					Группировка = Группировка.Структура.Добавить();
				КонецЕсли;
				
				
				БухгалтерскиеОтчетыВызовСервера.ЗаполнитьГруппировку(ПолеВыбраннойГруппировки, Группировка);
				
				Если ПолеВыбраннойГруппировки.Поле = "Вид" Тогда
					ЕстьГруппирровкаПоВиду = Истина;
				КонецЕсли;

			КонецЕсли;
		КонецЦикла;
		
		// В конце всегда добавляем группировку по Виду
		Если Не ЕстьГруппирровкаПоВиду Тогда
			
			Если ТипЗнч(Группировка) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
				Группировка = Группировка.Добавить();
			Иначе
				Группировка = Группировка.Структура.Добавить();
			КонецЕсли;
			БухгалтерскиеОтчетыВызовСервера.ЗаполнитьГруппировку(Новый Структура("Поле, ТипГруппировки", "Вид", 3), Группировка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
	Для Каждого Рисунок Из Результат.Рисунки Цикл
		Попытка
			Если ТипЗнч(Рисунок.Объект) = Тип("Диаграмма") Тогда
				
				// немного расширим легенду влево
				Рисунок.Объект.ОбластьЛегенды.Лево = Рисунок.Объект.ОбластьЛегенды.Лево - 0.01;
				Рисунок.Объект.ОбластьПостроения.Право = Рисунок.Объект.ОбластьПостроения.Право - 0.01;
				
				Для Каждого Серия Из Рисунок.Объект.Серии Цикл
					Если Серия.Текст = "Доходы без НДС"
						Или Серия.Текст = "Расходы" Тогда
							Серия.Индикатор = Истина;
					КонецЕсли;
				КонецЦикла;
				
				
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	Результат.ФиксацияСлева = 0;
	БыстрыеНастройкиОтчетовСервер.ВывестиПримечанияОкругления(ПараметрыОтчета, Результат);
	
КонецПроцедуры

Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ДоходыРасходы").Размещение.Вставить(Метаданные.Подсистемы.Руководителю.Подсистемы.ОбщиеПоказатели, "");
	
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыРасшифровкиОтчета(Адрес, Расшифровка, ПараметрыРасшифровки) Экспорт
	
	// Инициализируем список мунктов меню
	СписокПунктовМеню = Новый СписокЗначений();
	
	// Заполним соответствие полей которые мы хотим получить из данных расшифровки
	СоответствиеПолей = Новый Соответствие;
	ДанныеОтчета = ПолучитьИзВременногоХранилища(Адрес);
	
	ЗначениеРасшифровки = ДанныеОтчета.ДанныеРасшифровки.Элементы[Расшифровка];
	Если ТипЗнч(ЗначениеРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
		Для Каждого ПолеРасшифровки ИЗ ЗначениеРасшифровки.ПолучитьПоля() Цикл
			Если ЗначениеЗаполнено(ПолеРасшифровки.Значение) Тогда
				ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
				ПараметрыРасшифровки.Вставить("Значение",  ПолеРасшифровки.Значение);
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Укажем что открывать объект сразу не нужно
	ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Ложь);
	
	Если ДанныеОтчета = Неопределено Тогда 
		ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
		Возврат;
	КонецЕсли;
	
	// Прежде всего интересны данные группировочных полей
	Для Каждого Группировка Из ДанныеОтчета.Объект.Группировка Цикл
		СоответствиеПолей.Вставить(Группировка.Поле);
	КонецЦикла;
	
	СоответствиеПолей.Вставить("Период");
	
	// Инициализация пользовательских настроек
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("РежимРасшифровки",                  Истина);
	ДополнительныеСвойства.Вставить("Организация",                       ДанныеОтчета.Объект.Организация);
	ДополнительныеСвойства.Вставить("НачалоПериода",                     ДанныеОтчета.Объект.НачалоПериода);
	ДополнительныеСвойства.Вставить("КонецПериода",                      ДанныеОтчета.Объект.КонецПериода);
	ДополнительныеСвойства.Вставить("ВыводитьЗаголовок",                 ДанныеОтчета.Объект.ВыводитьЗаголовок);
	ДополнительныеСвойства.Вставить("ВыводитьПодвал",                    ДанныеОтчета.Объект.ВыводитьПодвал);
	ДополнительныеСвойства.Вставить("МакетОформления",                   ДанныеОтчета.Объект.МакетОформления);
	ДополнительныеСвойства.Вставить("ВключатьОбособленныеПодразделения", ДанныеОтчета.Объект.ВключатьОбособленныеПодразделения);
	ДополнительныеСвойства.Вставить("ОчищатьТаблицуГруппировок",         Истина);
	ДополнительныеСвойства.Вставить("КлючВарианта",                      "ДоходыИРасходыПоДокументам");
	
	// Получаем соответствие полей доступных в расшифровке
	Данные_Расшифровки = БухгалтерскиеОтчеты.ПолучитьДанныеРасшифровки(ДанныеОтчета.ДанныеРасшифровки, СоответствиеПолей, Расшифровка);
	
	Период = Данные_Расшифровки.Получить("Период");
	
	Если ЗначениеЗаполнено(Период) Тогда
		
		Периодичность = БухгалтерскиеОтчетыКлиентСервер.ПолучитьЗначениеПериодичности(ДанныеОтчета.Объект.Периодичность, ДанныеОтчета.Объект.НачалоПериода, ДанныеОтчета.Объект.КонецПериода);
		ДополнительныеСвойства.Вставить("Периодичность", Периодичность);
		ДополнительныеСвойства.Вставить("КонецПериода",  КонецДня(БухгалтерскиеОтчетыКлиентСервер.КонецПериода(Период, Периодичность)));
		ДополнительныеСвойства.Вставить("НачалоПериода", НачалоДня(БухгалтерскиеОтчетыКлиентСервер.НачалоПериода(Период, Периодичность)));

	КонецЕсли;
	
	ОтборПоЗначениямРасшифровки = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ОтборПоЗначениямРасшифровки.ИдентификаторПользовательскойНастройки = "Отбор";
	
	Для Каждого ЗначениеРасшифровки Из Данные_Расшифровки Цикл
		Если ЗначениеРасшифровки.Ключ <> "Период"
			И ЗначениеРасшифровки.Ключ <> "Вид"
		Тогда
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ОтборПоЗначениямРасшифровки, ЗначениеРасшифровки.Ключ, ЗначениеРасшифровки.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Группировка = Новый Массив();
	ЕстьГруппировкаПоДокументу = Ложь;
	Для Каждого СтрокаГруппировки Из ДанныеОтчета.Объект.Группировка Цикл
		Если СтрокаГруппировки.Использование И СтрокаГруппировки.Поле <> "Вид" Тогда
			СтрокаДляРасшифровки = Новый Структура("Использование, Поле, Представление, ТипГруппировки");
			ЗаполнитьЗначенияСвойств(СтрокаДляРасшифровки, СтрокаГруппировки);
			Группировка.Добавить(СтрокаДляРасшифровки);
			
			Если СтрокаГруппировки.Поле = "Документ" Тогда
				
				ЕстьГруппировкаПоДокументу = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьГруппировкаПоДокументу Тогда
		
		СтрокаДляРасшифровки = Новый Структура();
		СтрокаДляРасшифровки.Вставить("Использование",  Истина);
		СтрокаДляРасшифровки.Вставить("Поле",           "Документ");
		СтрокаДляРасшифровки.Вставить("Представление",  "Документ");
		СтрокаДляРасшифровки.Вставить("ТипГруппировки", 0);
		
		Группировка.Добавить(СтрокаДляРасшифровки);
		
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("Группировка", Группировка);
	
	СписокПунктовМеню.Добавить("ДоходыИРасходыПоДокументам", "Доходы и расходы по документам");
	
	НастройкиРасшифровки = Новый Структура();
	НастройкиРасшифровки.Вставить("ДоходыИРасходыПоДокументам", ПользовательскиеНастройки);
	ДанныеОтчета.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	ПоместитьВоВременноеХранилище(ДанныеОтчета, Адрес);
	
	ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
	
КонецПроцедуры

//Процедура используется подсистемой варианты отчетов
//
Процедура НастройкиОтчета(Настройки) Экспорт
	
	ВариантыНастроек = ВариантыНастроек();
	Для Каждого Вариант Из ВариантыНастроек Цикл
		Настройки.ОписаниеВариантов.Вставить(Вариант.Имя,Вариант.Представление);
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантыНастроек() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Новый Структура("Имя, Представление","ДоходыРасходы", "Доходы и расходы"));
	
	Возврат Массив;
	
КонецФункции

// Возвращает набор параметров, которые необходимо сохранять в рассылке отчетов.
// Значения параметров используются при формировании отчета в рассылке.
//
// Возвращаемое значение:
//   Структура - структура настроек, сохраняемых в рассылке с неинициализированными значениями.
//
Функция НастройкиОтчетаСохраняемыеВРассылке() Экспорт
	
	КоллекцияНастроек = Новый Структура;
	КоллекцияНастроек.Вставить("Организация"                      , Справочники.Организации.ПустаяСсылка());
	КоллекцияНастроек.Вставить("Периодичность"                    , 0);
	КоллекцияНастроек.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	КоллекцияНастроек.Вставить("РазмещениеДополнительныхПолей"    , 0);
	КоллекцияНастроек.Вставить("Группировка"                      , Неопределено);
	КоллекцияНастроек.Вставить("ДополнительныеПоля"               , Неопределено);
	КоллекцияНастроек.Вставить("ВыводитьДиаграмму"                , Ложь);
	КоллекцияНастроек.Вставить("ВыводитьЗаголовок"                , Ложь);
	КоллекцияНастроек.Вставить("ВыводитьПодвал"                   , Ложь);
	КоллекцияНастроек.Вставить("МакетОформления"                  , Неопределено);
	КоллекцияНастроек.Вставить("НастройкиКомпоновкиДанных"        , Неопределено);
	
	Возврат КоллекцияНастроек;
	
КонецФункции

// Возвращает структуру параметров, наличие которых требуется для успешного формирования отчета.
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Часть параметров компоновки отчета используется так же и в рассылке отчета.
	ПараметрыОтчета = НастройкиОтчетаСохраняемыеВРассылке();
	
	// Дополним параметрами, влияющими на формирование отчета.
	ПараметрыОтчета.Вставить("ПериодОтчета"         , Неопределено);
	ПараметрыОтчета.Вставить("НачалоПериода"        , Дата(1,1,1));
	ПараметрыОтчета.Вставить("КонецПериода"         , Дата(1,1,1));
	ПараметрыОтчета.Вставить("РежимРасшифровки"     , Ложь);
	ПараметрыОтчета.Вставить("ДанныеРасшифровки"    , Неопределено);
	ПараметрыОтчета.Вставить("СхемаКомпоновкиДанных", Неопределено);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета"  , "");
	ПараметрыОтчета.Вставить("ВыводитьПримечанияОкругления", Ложь);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Формирует таблицу данных для монитора руководителя по организации за период
// Параметры
// 	Организация - СправочникСсылка.Организации - Организация по которой нужны данные
// 	ДатаКон - Дата - дата конца периода
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с данными для монитора руководителя
//
Функция ДоходыРасходыИПрибыльДляМонитораРуководителя(Организация, ДатаКон) Экспорт 
	
	НачалоГода            = НачалоГода(ДатаКон);
	НачалоМесяца          = НачалоМесяца(ДатаКон);
	НачалоПрошлогоМесяца  = ДобавитьМесяц(НачалоМесяца, -1);
	КонецПрошлогоМесяца   = КонецМесяца(НачалоПрошлогоМесяца);
	НачалоПрошлогоПериода = НачалоГода(НачалоПрошлогоМесяца);
	КонецПериода          = КонецДня(ДатаКон);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("НачалоГода",            НачалоГода);
	Запрос.УстановитьПараметр("НачалоМесяца",          НачалоМесяца);
	Запрос.УстановитьПараметр("НачалоПрошлогоМесяца",  НачалоПрошлогоМесяца);
	Запрос.УстановитьПараметр("НачалоПрошлогоПериода", НачалоПрошлогоПериода);
	Запрос.УстановитьПараметр("КонецПрошлогоМесяца",   КонецПрошлогоМесяца);
	Запрос.УстановитьПараметр("КонецПериода",          КонецПериода);
	Запрос.УстановитьПараметр("КонецПериодаГраница",   Новый Граница(КонецПериода));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ СчетаРасходов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	(Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродаж))
	|			ИЛИ Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи_Акцизы))
	|			ИЛИ Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи_ЭкспортныеПошлины))
	|			ИЛИ Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи_РасходыНаПродажу))
	|			ИЛИ Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи_УправленческиеРасходы)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ СчетаНДС
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи_НДС))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет
	|ПОМЕСТИТЬ СчетаДоходов
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Выручка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Расходы.СуммаОборотДт КАК Сумма,
	|	Расходы.Период КАК Период
	|ПОМЕСТИТЬ Расходы
	|{ВЫБРАТЬ
	|	Сумма,
	|	Период}
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПрошлогоПериода,
	|			&КонецПериодаГраница,
	|			Месяц,
	|			Счет В
	|				(ВЫБРАТЬ
	|					СчетаРасходов.Счет
	|				ИЗ
	|					СчетаРасходов КАК СчетаРасходов),
	|			,
	|			Организация = &Организация,
	|			,
	|			) КАК Расходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Доходы.СуммаОборотКт КАК Сумма,
	|	Доходы.Период КАК Период
	|ПОМЕСТИТЬ Доходы
	|{ВЫБРАТЬ
	|	Сумма,
	|	Период}
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПрошлогоПериода,
	|			&КонецПериодаГраница,
	|			Месяц,
	|			Счет В
	|				(ВЫБРАТЬ
	|					СчетаДоходов.Счет
	|				ИЗ
	|					СчетаДоходов КАК СчетаДоходов),
	|			,
	|			Организация = &Организация,
	|			,
	|			) КАК Доходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	-НДС.СуммаОборотДт,
	|	НДС.Период
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПрошлогоПериода,
	|			&КонецПериодаГраница,
	|			Месяц,
	|			Счет В
	|				(ВЫБРАТЬ
	|					СчетаНДС.Счет
	|				ИЗ
	|					СчетаНДС КАК СчетаНДС),
	|			,
	|			Организация = &Организация,
	|			,
	|			) КАК НДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеОтчета.ДоходыТекущийМесяц КАК ДоходыТекущийМесяц,
	|	ДанныеОтчета.РасходыТекущийМесяц КАК РасходыТекущийМесяц,
	|	ДанныеОтчета.ПрибыльУбытокТекущийМесяц КАК ПрибыльУбытокТекущийМесяц,
	|	ДанныеОтчета.ДоходыТекущийГод КАК ДоходыТекущийГод,
	|	ДанныеОтчета.РасходыТекущийГод КАК РасходыТекущийГод,
	|	ДанныеОтчета.ПрибыльУбытокТекущийГод КАК ПрибыльУбытокТекущийГод,
	|	ДанныеОтчета.ДоходыПрошлыйМесяц КАК ДоходыПрошлыйМесяц,
	|	ДанныеОтчета.РасходыПрошлыйМесяц КАК РасходыПрошлыйМесяц,
	|	ДанныеОтчета.ПрибыльУбытокПрошлыйМесяц КАК ПрибыльУбытокПрошлыйМесяц,
	|	ДанныеОтчета.ДоходыПрошлыйПериод КАК ДоходыПрошлыйПериод,
	|	ДанныеОтчета.РасходыПрошлыйПериод КАК РасходыПрошлыйПериод,
	|	ДанныеОтчета.ПрибыльУбытокПрошлыйПериод КАК ПрибыльУбытокПрошлыйПериод
	|ИЗ
	|	(ВЫБРАТЬ
	|		0 КАК ДоходыТекущийМесяц,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Расходы.Период = &НачалоМесяца
	|						ТОГДА Расходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0) КАК РасходыТекущийМесяц,
	|		0 КАК ПрибыльУбытокТекущийМесяц,
	|		0 КАК ДоходыТекущийГод,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Расходы.Период МЕЖДУ &НачалоГода И &КонецПериода
	|						ТОГДА Расходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0) КАК РасходыТекущийГод,
	|		0 КАК ПрибыльУбытокТекущийГод,
	|		0 КАК ДоходыПрошлыйМесяц,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Расходы.Период = &НачалоПрошлогоМесяца
	|						ТОГДА Расходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0) КАК РасходыПрошлыйМесяц,
	|		0 КАК ПрибыльУбытокПрошлыйМесяц,
	|		0 КАК ДоходыПрошлыйПериод,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Расходы.Период МЕЖДУ &НачалоПрошлогоПериода И &КонецПрошлогоМесяца
	|						ТОГДА Расходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0) КАК РасходыПрошлыйПериод,
	|		0 КАК ПрибыльУбытокПрошлыйПериод
	|	ИЗ
	|		Расходы КАК Расходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Доходы.Период = &НачалоМесяца
	|						ТОГДА Доходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Доходы.Период МЕЖДУ &НачалоГода И &КонецПериода
	|						ТОГДА Доходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Доходы.Период = &НачалоПрошлогоМесяца
	|						ТОГДА Доходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Доходы.Период МЕЖДУ &НачалоПрошлогоПериода И &КонецПрошлогоМесяца
	|						ТОГДА Доходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0
	|	ИЗ
	|		Доходы КАК Доходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Доходы.Период = &НачалоМесяца
	|						ТОГДА Доходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Доходы.Период МЕЖДУ &НачалоГода И &КонецПериода
	|						ТОГДА Доходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Доходы.Период = &НачалоПрошлогоМесяца
	|						ТОГДА Доходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Доходы.Период МЕЖДУ &НачалоПрошлогоПериода И &КонецПрошлогоМесяца
	|						ТОГДА Доходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0)
	|	ИЗ
	|		Доходы КАК Доходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Расходы.Период = &НачалоМесяца
	|						ТОГДА -Расходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Расходы.Период МЕЖДУ &НачалоГода И &КонецПериода
	|						ТОГДА -Расходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Расходы.Период = &НачалоПрошлогоМесяца
	|						ТОГДА -Расходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0),
	|		0,
	|		0,
	|		ЕСТЬNULL(СУММА(ВЫБОР
	|					КОГДА Расходы.Период МЕЖДУ &НачалоПрошлогоПериода И &КонецПрошлогоМесяца
	|						ТОГДА -Расходы.Сумма
	|					ИНАЧЕ 0
	|				КОНЕЦ), 0)
	|	ИЗ
	|		Расходы КАК Расходы) КАК ДанныеОтчета
	|ИТОГИ
	|	СУММА(ДоходыТекущийМесяц),
	|	СУММА(РасходыТекущийМесяц),
	|	СУММА(ПрибыльУбытокТекущийМесяц),
	|	СУММА(ДоходыТекущийГод),
	|	СУММА(РасходыТекущийГод),
	|	СУММА(ПрибыльУбытокТекущийГод),
	|	СУММА(ДоходыПрошлыйМесяц),
	|	СУММА(РасходыПрошлыйМесяц),
	|	СУММА(ПрибыльУбытокПрошлыйМесяц),
	|	СУММА(ДоходыПрошлыйПериод),
	|	СУММА(РасходыПрошлыйПериод),
	|	СУММА(ПрибыльУбытокПрошлыйПериод)
	|ПО
	|	ОБЩИЕ";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ДоходыТекущийМесяц  = 0;
	ДоходыПрошлыйМесяц  = 0;
	ДоходыТекущийГод    = 0;
	ДоходыПрошлыйПериод = 0;
	
	РасходыТекущийМесяц = 0;
	РасходыПрошлыйМесяц = 0;
	РасходыТекущийГод   = 0;
	РасходПрошлыйГод    = 0;
	
	ПрибыльУбытокТекущийМесяц = 0;
	ПрибыльУбытокПрошлыйМесяц = 0;
	ПрибыльУбытокТекущийГод   = 0;
	ПрибыльУбытокПрошлыйГод   = 0;

	Если Результат.Следующий() Тогда
		
		ДоходыТекущийМесяц  = Результат.ДоходыТекущийМесяц;
		ДоходыПрошлыйМесяц  = Результат.ДоходыПрошлыйМесяц;
		ДоходыТекущийГод    = Результат.ДоходыТекущийГод;
		ДоходыПрошлыйПериод = Результат.ДоходыПрошлыйПериод;
		
		РасходыТекущийМесяц  = Результат.РасходыТекущийМесяц;
		РасходыПрошлыйМесяц  = Результат.РасходыПрошлыйМесяц;
		РасходыТекущийГод    = Результат.РасходыТекущийГод;
		РасходыПрошлыйПериод = Результат.РасходыПрошлыйПериод;
		
		ПрибыльУбытокТекущийМесяц  = Результат.ПрибыльУбытокТекущийМесяц;
		ПрибыльУбытокПрошлыйМесяц  = Результат.ПрибыльУбытокПрошлыйМесяц;
		ПрибыльУбытокТекущийГод    = Результат.ПрибыльУбытокТекущийГод;
		ПрибыльУбытокПрошлыйПериод = Результат.ПрибыльУбытокПрошлыйПериод;
		
	КонецЕсли;
	
	ПредставлениеТекущегоМесяца            = МониторРуководителя.ПолучитьПредставлениеПериода(НачалоМесяца, КонецПериода);
	ПредставлениеТекущегоМесяцаСНачалаГода = МониторРуководителя.ПолучитьПредставлениеПериода(НачалоГода, КонецПериода);
	ПредставлениеПрошлогоМесяца            = МониторРуководителя.ПолучитьПредставлениеПериода(НачалоПрошлогоМесяца, КонецПрошлогоМесяца);
	ПредставлениеПрошлогоМесяцаСНачалаГода = МониторРуководителя.ПолучитьПредставлениеПериода(НачалоПрошлогоПериода, КонецПрошлогоМесяца);
	
	// Доходы.
	ТаблицаДоходы = МониторРуководителя.ТаблицаДанных();
	
	СтрокаТаблицы = ТаблицаДоходы.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеТекущегоМесяца;
	СтрокаТаблицы.Сумма         = ДоходыТекущийМесяц;
	СтрокаТаблицы.Порядок       = 1;
	
	СтрокаТаблицы = ТаблицаДоходы.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеТекущегоМесяцаСНачалаГода;
	СтрокаТаблицы.Сумма         = ДоходыТекущийГод;
	СтрокаТаблицы.Порядок       = 2;
	
	СтрокаТаблицы = ТаблицаДоходы.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеПрошлогоМесяца;
	СтрокаТаблицы.Сумма         = ДоходыПрошлыйМесяц;
	СтрокаТаблицы.Порядок       = 3;
	
	СтрокаТаблицы = ТаблицаДоходы.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеПрошлогоМесяцаСНачалаГода;
	СтрокаТаблицы.Сумма         = ДоходыПрошлыйПериод;
	СтрокаТаблицы.Порядок       = 4;
	
	// Расходы.
	ТаблицаРасходы = МониторРуководителя.ТаблицаДанных();
	
	СтрокаТаблицы = ТаблицаРасходы.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеТекущегоМесяца;
	СтрокаТаблицы.Сумма         = РасходыТекущийМесяц;
	СтрокаТаблицы.Порядок       = 1;
	
	СтрокаТаблицы = ТаблицаРасходы.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеТекущегоМесяцаСНачалаГода;
	СтрокаТаблицы.Сумма         = РасходыТекущийГод;
	СтрокаТаблицы.Порядок       = 2;
	
	СтрокаТаблицы = ТаблицаРасходы.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеПрошлогоМесяца;
	СтрокаТаблицы.Сумма         = РасходыПрошлыйМесяц;
	СтрокаТаблицы.Порядок       = 3;
	
	СтрокаТаблицы = ТаблицаРасходы.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеПрошлогоМесяцаСНачалаГода;
	СтрокаТаблицы.Сумма         = РасходыПрошлыйПериод;
	СтрокаТаблицы.Порядок       = 4;
	
	// ПрибыльУбыток.
	ТаблицаПрибыльУбыток = МониторРуководителя.ТаблицаДанных();
	
	СтрокаТаблицы = ТаблицаПрибыльУбыток.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеТекущегоМесяца;
	СтрокаТаблицы.Сумма         = ПрибыльУбытокТекущийМесяц;
	СтрокаТаблицы.Порядок       = 1;
	
	СтрокаТаблицы = ТаблицаПрибыльУбыток.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеТекущегоМесяцаСНачалаГода;
	СтрокаТаблицы.Сумма         = ПрибыльУбытокТекущийГод;
	СтрокаТаблицы.Порядок       = 2;
	
	СтрокаТаблицы = ТаблицаПрибыльУбыток.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеПрошлогоМесяца;
	СтрокаТаблицы.Сумма         = ПрибыльУбытокПрошлыйМесяц;
	СтрокаТаблицы.Порядок       = 3;
	
	СтрокаТаблицы = ТаблицаПрибыльУбыток.Добавить();
	СтрокаТаблицы.Представление = ПредставлениеПрошлогоМесяцаСНачалаГода;
	СтрокаТаблицы.Сумма         = ПрибыльУбытокПрошлыйПериод;
	СтрокаТаблицы.Порядок       = 4;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ТаблицаДоходы", ТаблицаДоходы);
	СтруктураДанных.Вставить("ТаблицаРасходы", ТаблицаРасходы);
	СтруктураДанных.Вставить("ТаблицаПрибыльУбыток", ТаблицаПрибыльУбыток);

	Возврат СтруктураДанных;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьГруппировкуПериод(ПараметрыОтчета, Периодичность, ГруппировкаПериод)
	
	ПолеГруппировки = ГруппировкаПериод.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование = Истина;
	ПолеГруппировки.Поле          = Новый ПолеКомпоновкиДанных("Период");
	ПолеГруппировки.ТипДополнения = БухгалтерскиеОтчетыВызовСервера.ПолучитьТипДополненияПоИнтервалу(Периодичность);
	ПолеГруппировки.НачалоПериода = НачалоДня(ПараметрыОтчета.НачалоПериода);
	ПолеГруппировки.КонецПериода  = КонецДня(ПараметрыОтчета.КонецПериода);
	
	ГруппировкаПериод.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ГруппировкаПериод.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли