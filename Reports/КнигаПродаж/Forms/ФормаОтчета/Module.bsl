
#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Отчет, Параметры);
	
	РежимРасшифровки = Параметры.РежимРасшифровки;
	
	Если РежимРасшифровки Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	// Установка настроек печати по умолчанию. Если настройки были изменены, они будут загружены при формировании отчета
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	Результат.АвтоМасштаб        = Истина;
	
	ЗаполнитьРеквизитыИзПараметровФормы(ЭтотОбъект);
	
	УчетНДСПереопределяемый.ФормаОтчетаПриСозданииНаСервере(ЭтотОбъект);
	
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокОрганизаций(Элементы.ПолеОрганизация, СоответствиеОрганизаций);
	
	УправлениеФормой(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереОтчет(ЭтотОбъект, Истина);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// Сверка счетов-фактур
	ЕстьБиблиотекаБСС = ОбщегоНазначения.ПодсистемаСуществует("СервисСверкиРасчетов");
	// Конец Сверка счетов-фактур
	
	ПравоДоступаКДаннымБухгалтерии = УправлениеДоступомБПВызовСервера.ПравоДоступаКДаннымБухгалтерии();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	БухгалтерскиеОтчетыКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОтчетПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		МодульСверкаДекларацийКлиент.ПриОткрытииОтчета(ЭтотОбъект, "КнигаПродаж");
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
	Если РежимРасшифровки Тогда
		ПодключитьОбработчикОжидания("Подключаемый_СформироватьПриОткрытии",
			БухгалтерскиеОтчетыКлиент.ИнтервалЗапускаФормированияОтчетаПриОткрытии(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	БухгалтерскиеОтчетыКлиент.ПередЗакрытием(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	БухгалтерскиеОтчетыКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ПолеОрганизация <> "" Тогда
		Если НЕ СоответствиеОрганизаций.Свойство(ПолеОрганизация) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", НСтр("ru = 'Организация'"), , ,);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПолеОрганизация", , Отказ);
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	
	БухгалтерскиеОтчетыВызовСервера.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки)
	
	Если ИспользуютсяСтандартныеНастройки Тогда
		Возврат;
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки, Истина);
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	ЗаполнитьРеквизитыИзПараметровФормы(ЭтотОбъект);
	
	УчетНДСКлиентСервер.ОтобразитьПоясненияКПериодуОтчета(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	УчетНДСКлиентСервер.ОтобразитьПоясненияКПериодуОтчета(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	УчетНДСКлиентСервер.ОтобразитьПоясненияКПериодуОтчета(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияПриИзменении(Элемент, ПолеОрганизация,
		Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	УчетНДСКлиентСервер.ОтобразитьПоясненияКПериодуОтчета(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		Если ЗначениеЗаполнено(ИдентификаторЗаданияСверки) Тогда
			МодульСверкаДекларацийКлиент.ОтменитьВыполнениеСверки(ЭтотОбъект);
		КонецЕсли;
		
		МодульСверкаДекларацийКлиент.ПроверкаИспользованияСверки(ЭтотОбъект, "КнигаПродаж");
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, 
		СоответствиеОрганизаций, Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбораЛистаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ЭлементСписка = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЭлементСписка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбораЛиста = ЭлементСписка.Представление;

	ПоказатьВыбранныйЛист(ЭлементСписка.Значение);

	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаИспользуетсяВРазделеОтчета = НомераРазделовДопЛистов.НайтиПоЗначению(ЭлементСписка.Значение) = Неопределено;
	ПроверкаКонтрагентовКлиентСервер.ВывестиНужнуюПанельПроверкиКонтрагентовВОтчете(ЭтотОбъект, ПроверкаИспользуетсяВРазделеОтчета);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДекларацийКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер");
		МодульСверкаДекларацийКлиентСервер.ОтобразитьСверкуНаФорме(ЭтотОбъект,
			ЭтотОбъект.НомераРазделовДопЛистов.НайтиПоЗначению(ЭлементСписка.Значение) <> Неопределено);
	КонецЕсли;
	// Конец Сверка счетов-фактур

КонецПроцедуры

&НаКлиенте
Процедура ФормироватьДополнительныеЛистыПриИзменении(Элемент)
	
	Если Не Отчет.ФормироватьДополнительныеЛисты И Отчет.ВыводитьТолькоДопЛисты Тогда
		Отчет.ВыводитьТолькоДопЛисты = Ложь;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС И ЗначениеЗаполнено(ИдентификаторЗаданияСверки) Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		МодульСверкаДекларацийКлиент.ОтменитьВыполнениеСверки(ЭтотОбъект);
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЛистыЗаТекущийПериодПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС И ЗначениеЗаполнено(ИдентификаторЗаданияСверки) Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		МодульСверкаДекларацийКлиент.ОтменитьВыполнениеСверки(ЭтотОбъект);
	КонецЕсли;
	// Конец Сверка счетов-фактур

КонецПроцедуры

&НаКлиенте
Процедура ВыводитьТолькоДопЛистыПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС И ЗначениеЗаполнено(ИдентификаторЗаданияСверки) Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		МодульСверкаДекларацийКлиент.ОтменитьВыполнениеСверки(ЭтотОбъект);
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьПродавцовПоАвансамПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентДляОтбораПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС И ЗначениеЗаполнено(ИдентификаторЗаданияСверки) Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		МодульСверкаДекларацийКлиент.ОтменитьВыполнениеСверки(ЭтотОбъект);
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппироватьПоКонтрагентамПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		"Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СформироватьОтчетНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетНаКлиенте()
	
	ОчиститьСообщения();
	ОтказПроверкиЗаполнения = Ложь;
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДекларацийКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер");
		МодульСверкаДекларацийКлиентСервер.ИнициализацияРеквизитовСверкиДекларации(ЭтотОбъект, "КнигаПродаж");
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	РезультатВыполнения.Свойство("ОтказПроверкиЗаполнения", ОтказПроверкиЗаполнения);
	
	Если ОтказПроверкиЗаполнения = Истина Тогда
		
		ПоказатьНастройки("");
		
	Иначе
		
		// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
		ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВОтчете(ЭтотОбъект);
		// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
		
		Если РезультатВыполнения.Статус = "Выполняется" Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект, АдресХранилища);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);

		СкрытьНастройки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	ОткрытьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	СкрытьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	БухгалтерскиеОтчетыКлиент.ОтчетСохранитьКак(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода,ВыборКварталов",
		Отчет.НачалоПериода, Отчет.КонецПериода, Истина);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц",
		ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Актуализировать(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.Актуализировать(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьАктуализацию(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыйПараметрыАктуализацииОтчета();
	ПараметрыАктуализации.Вставить("Организация",                       Отчет.Организация);
	ПараметрыАктуализации.Вставить("ВключатьОбособленныеПодразделения", Отчет.ВключатьОбособленныеПодразделения);
	ПараметрыАктуализации.Вставить("ДатаАктуальности",                  ДатаАктуальности);
	ПараметрыАктуализации.Вставить("ДатаОкончанияАктуализации",         Отчет.КонецПериода);

	ЗакрытиеМесяцаКлиент.ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		ЭтотОбъект,
		ПараметрыАктуализации);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("НалоговыйПериод",                Отчет.КонецПериода);
	ПараметрыОтчета.Вставить("Организация",                    Отчет.Организация);
	ПараметрыОтчета.Вставить("ФормироватьКнигуПродаж",         Истина);
	ПараметрыОтчета.Вставить("ФормироватьДопЛистыКнигиПродаж", Истина);
	
	ВыгружаемыеДанные = ВыгрузитьНаСервере(ПараметрыОтчета);
	
	УчетНДСКлиент.СохранитьФайлВыгрузкиНаКлиенте(ВыгружаемыеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_СформироватьПриОткрытии()
	
	СформироватьОтчетНаКлиенте();
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыОтчета(ЭтоПервоеФормированиеОтчета)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация"                       , Отчет.Организация);
	ПараметрыОтчета.Вставить("НачалоПериода"                     , Отчет.НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода"                      , Отчет.КонецПериода);
	ПараметрыОтчета.Вставить("ФормироватьДополнительныеЛисты"    , Отчет.ФормироватьДополнительныеЛисты);
	ПараметрыОтчета.Вставить("ДополнительныеЛистыЗаТекущийПериод", Отчет.ДополнительныеЛистыЗаТекущийПериод);
	ПараметрыОтчета.Вставить("ГруппироватьПоКонтрагентам"        , Отчет.ГруппироватьПоКонтрагентам);
	ПараметрыОтчета.Вставить("КонтрагентДляОтбора"               , Отчет.КонтрагентДляОтбора);
	ПараметрыОтчета.Вставить("ВыводитьТолькоДопЛисты"            , Отчет.ВыводитьТолькоДопЛисты);
	ПараметрыОтчета.Вставить("ВыводитьПродавцовПоАвансам"        , Отчет.ВыводитьПродавцовПоАвансам);
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения" , Отчет.ВключатьОбособленныеПодразделения);
	ПараметрыОтчета.Вставить("СформироватьОтчетПоСтандартнойФорме");
	ПараметрыОтчета.Вставить("ОтбиратьПоКонтрагенту", ЗначениеЗаполнено(Отчет.КонтрагентДляОтбора));
	ПараметрыОтчета.Вставить("СписокОрганизаций");
	ПараметрыОтчета.Вставить("ДатаФормированияДопЛиста");
	ПараметрыОтчета.Вставить("ЗаполнениеДокумента", Ложь);
	ПараметрыОтчета.Вставить("ЗаполнениеДекларации", Ложь);
	ПараметрыОтчета.Вставить("ЭтоКнигаПродаж", Истина);
	ПараметрыОтчета.Вставить("ФормироватьТабличныйДокумент", Истина);
		
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДеклараций = ОбщегоНазначения.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДС");
		МодульСверкаДеклараций.ДобавитьПараметрыДляСверкиДеклараций(ЭтотОбъект, ПараметрыОтчета, ЭтоПервоеФормированиеОтчета);
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ДобавитьПараметрыДляПроверкиКонтрагентов(ЭтотОбъект, ПараметрыОтчета, 
		ЭтоПервоеФормированиеОтчета);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТекстЗаголовка(Форма)
	
	Отчет = Форма.Отчет;
	
	ЗаголовокОтчета = НСтр("ru='Книга продаж'")
					+ БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(Отчет.НачалоПериода,
																				 Отчет.КонецПериода);

	Если ЗначениеЗаполнено(Отчет.Организация) И Форма.ИспользуетсяНесколькоОрганизаций Тогда
		ЗаголовокОтчета = ЗаголовокОтчета + " " + БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	КонецЕсли;
	
	Форма.Заголовок = ЗаголовокОтчета;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Отчет    = Форма.Отчет;
	Элементы = Форма.Элементы;

	Элементы.ДополнительныеЛистыЗаТекущийПериод.Доступность = Отчет.ФормироватьДополнительныеЛисты;
	Элементы.ВыводитьТолькоДопЛисты.Доступность             = Отчет.ФормироватьДополнительныеЛисты;

КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере()

	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено, ОтказПроверкиЗаполнения", Истина, Истина);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередФормированиемОтчета(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	БухгалтерскиеОтчеты.ОтменитьВыполнениеЗадания(ЭтотОбъект);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	ПараметрыЗадания = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ПараметрыЗадания.ЗапуститьНеВФоне = Истина;
	Иначе
		ПараметрыЗадания.ЗапуститьВФоне = Истина;
	КонецЕсли;
	ПараметрыЗадания.НаименованиеФоновогоЗадания =
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтотОбъект);
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета(Истина);
	
	ОписаниеЗаданияФормированияОтчета = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыЗадания, "УчетНДСПереопределяемый.ПодготовитьПараметрыКнигиПродаж", ПараметрыОтчета, АдресХранилища);
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	
	Возврат ОписаниеЗаданияФормированияОтчета;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, АдресРезультата) Экспорт
	
	Если РезультатВыполнения = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	// Сверка счетов-фактур
	Если РезультатВыполнения.Статус = "Выполнено" Тогда
		
		Если ЕстьБиблиотекаБСС Тогда
			МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
			МодульСверкаДекларацийКлиент.СверитьОтчет(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
	ЗагрузитьПодготовленныеДанныеНаСервере(АдресРезультата);
	
	БухгалтерскийУчетКлиентПереопределяемый.ПослеФормированияОтчета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(АдресРезультата)
	
	ДанныеОтчета = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ЗапомнитьРезультатФормированияОтчета(ЭтотОбъект, ДанныеОтчета, АдресРезультата);
	РазделыОтчета = ПроверкаКонтрагентов.РазделыОтчетаВФорме(ЭтотОбъект, ДанныеОтчета, Ложь);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДеклараций = ОбщегоНазначения.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДС");
		МодульСверкаДеклараций.ЗапомнитьРезультатФормированияОтчета(ЭтотОбъект, ДанныеОтчета);
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
	Элементы.СписокВыбораЛиста.СписокВыбора.Очистить();
	НомераРазделовДопЛистов.Очистить();
	
	Если РазделыОтчета.Количество() > 0 Тогда
		Для Каждого СтрокаРаздела Из РазделыОтчета Цикл
			Элементы.СписокВыбораЛиста.СписокВыбора.Добавить(СтрокаРаздела.НомерРаздела, СтрокаРаздела.НазваниеРаздела);
			
			// Запомним номера разделов, в которых расположены доп.листы.
			// Для них не выполняется проверка контрагентов и не показываются соответствующие элементы на форме.
			Если ТипЗнч(СтрокаРаздела.ДополнительныеПараметры) = Тип("Структура")
				И СтрокаРаздела.ДополнительныеПараметры.ЭтоДопЛист Тогда
				НомераРазделовДопЛистов.Добавить(СтрокаРаздела.НомерРаздела);
			КонецЕсли;
		КонецЦикла;

		СписокВыбораЛиста = РазделыОтчета[0].НазваниеРаздела;
		Если РазделыОтчета.Количество() = 1 Тогда
			Элементы.СписокВыбораЛиста.Видимость = Ложь;
		Иначе
			Элементы.СписокВыбораЛиста.Видимость = Истина;
		КонецЕсли;
		
		ПоказатьВыбранныйЛист(РазделыОтчета[0].НомерРаздела);
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	КонецЕсли;
	
	Если ДанныеОтчета.Свойство("СписокСообщений") Тогда
		Для Каждого Сообщение Из ДанныеОтчета.СписокСообщений Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ОписаниеЗаданияФормированияОтчета = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьВыбранныйЛист(НомерРаздела)

	Результат.Очистить();

	ТекущийНомерРаздела = НомерРаздела;

	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ВывестиРазделОтчета(ЭтотОбъект, Результат, ТекущийНомерРаздела);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
		
	Результат.ЧерноБелаяПечать = Истина;
	
	РассчитатьОбластьПечати();

КонецПроцедуры 

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыИзПараметровФормы(Форма)
	
	ПараметрыЗаполненияФормы = Неопределено;
	
	Если Форма.Параметры.Свойство("ПараметрыЗаполненияФормы",ПараметрыЗаполненияФормы) Тогда
	
		ЗаполнитьЗначенияСвойств(Форма.Отчет,ПараметрыЗаполненияФормы);			
	
	КонецЕсли; 		

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	ОбработатьВыборПериодаНаСервере();
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ПроверкаКонтрагентовКлиентСервер.СброситьАктуальностьОтчета(ЭтотОбъект);
	КонецЕсли;
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		Если ЗначениеЗаполнено(ИдентификаторЗаданияСверки) Тогда
			МодульСверкаДекларацийКлиент.ОтменитьВыполнениеСверки(ЭтотОбъект);
		КонецЕсли;
		
		МодульСверкаДекларацийКлиент.ПроверкаИспользованияСверки(ЭтотОбъект, "КнигаПродаж");
	КонецЕсли;
	// Конец Сверка счетов-фактур

КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборПериодаНаСервере()
	
	УчетНДСПереопределяемый.ФормаОтчетаОбработатьВыборПериода(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьОбластьПечати()

	ПерваяСтрока = 1;
	
	Результат.ОбластьПечати = Результат.Область(ПерваяСтрока,1,Результат.ВысотаТаблицы, Результат.ШиринаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(ЭтотОбъект, Отчет.Организация, Отчет.КонецПериода);

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеМесяцаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "СформироватьОтчет" Тогда
		
		БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
		Активизировать();
		СформироватьОтчет("");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыгрузитьНаСервере(ПараметрыОтчета)
	
	Результат = УчетНДС.СформироватьДокументыОтчетности(ПараметрыОтчета);
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЭлектронныеКниги = Результат.СозданныеДокументы;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЭлектронныеКниги, Результат.ПерезаполненныеДокументы);
	
	ВыгружаемыеДанные = Новый Массив;
	
	Для каждого ЭлектроннаяКнига Из ЭлектронныеКниги Цикл
		
		КнигаОбъект = ЭлектроннаяКнига.ПолучитьОбъект();
		ФайлыВыгрузки = КнигаОбъект.ВыгрузитьДокумент();
		ВыгружаемыеДанные.Добавить(ФайлыВыгрузки[0]);
		
	КонецЦикла;
	
	Возврат ВыгружаемыеДанные;
	
КонецФункции

#Область ПроверкаКонтрагентов

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ПроверитьКонтрагентов() Экспорт
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета(Ложь);
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВОтчете(ЭтотОбъект, ПараметрыОтчета);
	
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт

	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВОтчете(ЭтотОбъект, Результат, Неопределено, ТекущийНомерРаздела);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура ПереключательРежимаОтображенияПриИзменении(Элемент)
	ПереключитьРежимОтображенияОтчета();
	
	// Сверка счетов-фактур
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		МодульСверкаДекларацийКлиент.СохранитьРезультатыОтчета(ЭтотОбъект);
		ТекстКнигаПродажНажатиеНаСервере();
	КонецЕсли;
	// Конец Сверка счетов-фактур
	
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ПереключитьРежимОтображенияОтчета()
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПереключитьРежимОтображенияРазделаОтчета(ЭтотОбъект, Результат, ТекущийНомерРаздела);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область СверкаСчетовФактур

&НаКлиенте
Процедура НеПодключенаСверкаТекстОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПодключитьсяКСервисуСверки" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.ИнтеграцияССервисомСверкиРасчетов");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьБаннерНажатие(Элемент)
	Элементы.ГруппаНеПодключенаСверка.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьСверки(Команда)
	
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		МодульСверкаДекларацийКлиент.ИзменитьВидимостьСверки(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельСверки(Команда)
	
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДекларацийКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДСКлиент");
		МодульСверкаДекларацийКлиент.ЗакрытьПанельСверки(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстРасхожденияНажатие(Элемент)
	ТекстРасхожденияНажатияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстРасхожденияНажатияНаСервере()
	
	Если ЕстьБиблиотекаБСС Тогда
		ДопЛист = ЭтотОбъект.НомераРазделовДопЛистов.НайтиПоЗначению(ЭтотОбъект.ТекущийНомерРаздела) <> Неопределено;
		МодульСверкаДеклараций = ОбщегоНазначения.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДС");
		Если ДопЛист Тогда
			МодульСверкаДеклараций.ОткрытьДокументыСРасхождениями(ЭтотОбъект, ДопЛист);
		Иначе
			МодульСверкаДеклараций.ОткрытьДокументыСРасхождениями(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ТекстКнигаПродажНажатиеНаСервере()
	
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДеклараций = ОбщегоНазначения.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДС");
		МодульСверкаДеклараций.ОтобразитьВсюКнигу(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстКнигаПродажНажатие(Элемент)
	ТекстКнигаПродажНажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеОтраженоНажатиеНаСервере()
	
	Если ЕстьБиблиотекаБСС Тогда
		ДопЛист = ЭтотОбъект.НомераРазделовДопЛистов.НайтиПоЗначению(ЭтотОбъект.ТекущийНомерРаздела) <> Неопределено;
		МодульСверкаДеклараций = ОбщегоНазначения.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДС");
		Если ДопЛист Тогда
			МодульСверкаДеклараций.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНетУКонтрагентаДопЛист", ДопЛист);
		Иначе
			МодульСверкаДеклараций.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНетУКонтрагента", ДопЛист);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстНеОтраженоНажатие(Элемент)
	
	ТекстНеОтраженоНажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстНеСверялисьНажатиеНаСервере()
	
	Если ЕстьБиблиотекаБСС Тогда
		ДопЛист = ЭтотОбъект.НомераРазделовДопЛистов.НайтиПоЗначению(ЭтотОбъект.ТекущийНомерРаздела) <> Неопределено;
		МодульСверкаДеклараций = ОбщегоНазначения.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДС");
		Если ДопЛист Тогда
			МодульСверкаДеклараций.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНеСверилиДопЛист", ДопЛист);
		Иначе
			МодульСверкаДеклараций.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНеСверили", ДопЛист);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТекстНеСверялисьНажатие(Элемент)
	ТекстНеСверялисьНажатиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ТекстБезРасхожденийНажатиеНаСервере()
	
	Если ЕстьБиблиотекаБСС Тогда
		ДопЛист = ЭтотОбъект.НомераРазделовДопЛистов.НайтиПоЗначению(ЭтотОбъект.ТекущийНомерРаздела) <> Неопределено;
		МодульСверкаДеклараций = ОбщегоНазначения.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДС");
		Если ДопЛист Тогда
			МодульСверкаДеклараций.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаСверилиБезОшибокДопЛист", ДопЛист);
		Иначе
			МодульСверкаДеклараций.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаСверилиБезОшибок", ДопЛист);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстБезРасхожденийНажатие(Элемент)
	ТекстБезРасхожденийНажатиеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РазворачиватьРезультатыСверкиДекларацийСразуПриИзменении(Элемент)
	РазворачиватьРезультатыСверкиДекларацийСразуПриИзмененииНаСервере(РазворачиватьРезультатыСверкиДекларацийСразу);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура РазворачиватьРезультатыСверкиДекларацийСразуПриИзмененииНаСервере(РазворачиватьДанныеСверки)
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"РазворачиватьРезультатыСверкиДекларацийСразу",
		"КнигаПродаж",
		Не РазворачиватьДанныеСверки,,,Истина);
КонецПроцедуры

&НаСервере
Процедура ТекстНетВОрганизацииНажатиеНаСервере()
	Если ЕстьБиблиотекаБСС Тогда
		МодульСверкаДеклараций = ОбщегоНазначения.ОбщийМодуль("СервисСверкиРасчетовСверкаДекларацийНДС");
		МодульСверкаДеклараций.ОткрытьДокументыБезРасхождений(ЭтотОбъект, "ТаблицаНетВОрганизации");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекстНетВОрганизацииНажатие(Элемент)
	ТекстНетВОрганизацииНажатиеНаСервере();
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанельФормаОтчета(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, 
		Элементы.КоманднаяПанельОтчета.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, Элементы.ГруппаПечать.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКомандуСохранитьВМенюФормаОтчета(Элементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВПодменюМКПоделиться(Элементы, Элементы.Сохранить);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаКоманднаяПанельОтчета);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаБыстрыеОтборы);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаСписокЛистов);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаРасширенныйНалоговыйПериод);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Актуализация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.РазделыОтчета);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ПроверкаКонтрагента);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаНеПодключенаСверка);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаРезультатСверки);
	
	Элементы.НачалоПериода.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаПериод.РастягиватьПоГоризонтали = Истина;
	Элементы.ГруппаПериод.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.ПолеОрганизация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

