#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 12, 0);
	
КонецФункции

Функция СчетУчетаВнеоборотногоАктива(ВидОперации) Экспорт
	
	Если ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование Тогда 
		
		СчетУчетаВнеоборотногоАктива = ПланыСчетов.Хозрасчетный.ПриобретениеКомпонентовОсновныхСредств;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства Тогда
		
		СчетУчетаВнеоборотногоАктива = ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуОсновныхСредств.ПредметыАренды Тогда
		
		СчетУчетаВнеоборотногоАктива = ПланыСчетов.Хозрасчетный.ПриобретениеОсновныхСредств;
		
	Иначе
		
		Возврат ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		
	КонецЕсли;
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетУчетаВнеоборотногоАктива);
	Если СвойстваСчета.ЗапретитьИспользоватьВПроводках Тогда
		Возврат ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	КонецЕсли;
	
	Возврат СчетУчетаВнеоборотногоАктива;
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходовНУ
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Результат = Запрос.Выполнить();
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);

	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	ВедетсяУчетПрослеживаемыхТоваров  = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Реквизиты.Период);

	Запрос.УстановитьПараметр("СинонимОС", НСтр("ru = 'Основные средства'"));
	Запрос.УстановитьПараметр("ПлательщикНалогаНаПрибыль", УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ОтражатьВНалоговомУчетеУСН", УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ПлательщикНДС", 
		УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период)
		ИЛИ УчетнаяПолитика.ПлательщикНДСНаУСНСВычетами(Реквизиты.Организация, Реквизиты.Период));
	
	Запрос.УстановитьПараметр("ВестиУчетПоВидамДеятельностиИП",	УчетнаяПолитика.ВестиУчетПоВидамДеятельностиИП(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ОсновнаяНоменклатурнаяГруппа",	УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация, Реквизиты.Период));
	
	КритерииПримененияВычета = ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета();
	ЗаполнитьЗначенияСвойств(КритерииПримененияВычета, Реквизиты);
	КритерииПримененияВычета.Дата = Реквизиты.Период;
	КритерииПримененияВычета.МенеджерДокумента = Документы.ПринятиеКУчетуОС;

	Запрос.УстановитьПараметр("ПроцентИнвестиционногоВычета", ИнвестиционныйНалоговыйВычет.ПроцентИнвестиционногоВычета(КритерииПримененияВычета));
	
	НомераТаблиц = Новый Структура;

	Запрос.Текст =
		ТекстЗапросаТаблицаОС(НомераТаблиц)
		+ ТекстЗапросаСписаниеТоваров(НомераТаблиц)
		+ ТекстЗапросаОсновныеСредства(НомераТаблиц)
		+ ТекстЗапросаПринятиеКУчетуОбъектыСтроительства(НомераТаблиц)
		+ ТекстЗапросаПредметыАренды(НомераТаблиц)
		+ ТекстЗапросаПринятиеКУчетуПоРезультатамИнвентаризации(НомераТаблиц)
		+ ТекстЗапросаСобытияОС(НомераТаблиц)
		+ ТекстЗапросаСостоянияОС(НомераТаблиц)
		+ ТекстЗапросаПервоначальныеСведенияОСБУ(НомераТаблиц)
		+ ТекстЗапросаМестонахождениеОСБУ(НомераТаблиц)
		+ ТекстЗапросаНачислениеАмортизацииОСБУ(НомераТаблиц)
		+ ТекстЗапросаСчетаБухгалтерскогоУчетаОС(НомераТаблиц)
		+ ТекстЗапросаГрафикиАмортизацииОСБУ(НомераТаблиц)
		+ ТекстЗапросаСпособыОтраженияРасходовПоАмортизацииОСБУ(НомераТаблиц)
		+ ТекстЗапросаПараметрыАмортизацииОСБУ(НомераТаблиц)
		+ ТекстЗапросаПараметрыАмортизацииОСНУ(НомераТаблиц)
		+ ТекстЗапросаПервоначальныеСведенияОСНУ(НомераТаблиц)
		+ ТекстЗапросаПервоначальныеСведенияОСУСН(НомераТаблиц)
		+ ТекстЗапросаАмортизационнаяПремия(НомераТаблиц)
		+ ТекстЗапросаИнвестиционныйВычет(НомераТаблиц)
		+ ТекстЗапросаНачислениеАмортизацииОСНУ(НомераТаблиц)
		+ ТекстЗапросаНачислениеАмортизацииОССпециальныйКоэффициентНУ(НомераТаблиц)
		+ ТекстЗапросаНачислениеАмортизацииПриПринятииКУчету(НомераТаблиц)
		+ ТекстЗапросаВключениеВРасходыПриПринятииКУчетуНУ(НомераТаблиц)
		+ ТекстЗапросаСпособыОтраженияРасходовПоАренднымПлатежамОСНУ(НомераТаблиц)
		+ ТекстЗапросаОплатаОсновныхСредствДляУСН(НомераТаблиц)
		+ ТекстЗапросаНДС(НомераТаблиц)
		+ ТекстЗапросаПервоначальныеСведенияОСИП(НомераТаблиц)
		+ ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, 
			ВедетсяУчетПрослеживаемыхТоваров,
			?(Реквизиты.ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуОсновныхСредств.ПоРезультатамИнвентаризации, Истина, Ложь));

	Результат = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;

КонецФункции

Функция ПодготовитьТаблицыМатериаловПродукцииИП(ТаблицаСписанныеМПЗ, ТаблицаРеквизиты) Экспорт
	Перем СтруктураТаблиц;
	
	СтруктураТаблиц	= Новый Структура("ТаблицаМатериалов, ПолученоПродукции");
	
	Реквизиты	= ТаблицаРеквизиты[0];
	
	Если НЕ УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	Если Реквизиты.УказатьПервоначальнуюСтоимость Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	МассивТиповЗатрат	= Новый Массив;
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.СтатьиЗатрат"));
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.РасходыБудущихПериодов"));
	ОписаниеТиповЗатрат	= Новый ОписаниеТипов(МассивТиповЗатрат);
	
	ТаблицаМатериалов	= ТаблицаСписанныеМПЗ.Скопировать();
	ТаблицаМатериалов.Колонки.КорСчетСписания.Имя	= "СчетЗатрат";
	ТаблицаМатериалов.Колонки.Добавить("Продукция",				Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ТаблицаМатериалов.Колонки.Добавить("НоменклатурнаяГруппа",	Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ТаблицаМатериалов.Колонки.Добавить("СтатьяЗатрат",			ОписаниеТиповЗатрат);
	ТаблицаМатериалов.Колонки.Добавить("СчетДоходов",			Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаМатериалов.Колонки.Добавить("КоличествоПродукции",	ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	ОсновнаяНоменклатурнаяГруппа	= УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация, Реквизиты.Период);
	ТаблицаМатериалов.ЗаполнитьЗначения(ОсновнаяНоменклатурнаяГруппа, "НоменклатурнаяГруппа");
	
	ТаблицаМатериалов.ЗагрузитьКолонку(ТаблицаМатериалов.ВыгрузитьКолонку("КорСубконто1"), "Продукция");
	
	СтруктураТаблиц.ТаблицаМатериалов =
		УчетДоходовИРасходовПредпринимателя.ДополнитьТаблицуСписанияМатериалов(ТаблицаМатериалов, ТаблицаРеквизиты);
	
	Если СтруктураТаблиц.ТаблицаМатериалов <> Неопределено Тогда
		СтруктураТаблиц.ПолученоПродукции = СтруктураТаблиц.ТаблицаМатериалов.Скопировать();
		СтруктураТаблиц.ПолученоПродукции.Колонки.СчетЗатрат.Имя = "СчетУчетаПродукции";
		СтруктураТаблиц.ПолученоПродукции.Свернуть("СчетУчетаПродукции, Продукция, НоменклатурнаяГруппа", "КоличествоПродукции");
	КонецЕсли;
	
	Возврат СтруктураТаблиц;
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Акт о приеме-передаче ОС (ОС-1)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС1";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приеме-передаче ОС (ОС-1)'");
	КомандаПечати.Обработчик    = "УчетОСКлиент.ВыполнитьКомандуПечатиОС1";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Принятие к учету ОС""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
	//Инвентаризационные этикетки
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьЭтикеток) Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ИнвентаризационнаяЭтикетка";
		КомандаПечати.Представление = НСтр("ru = 'Инвентарная этикетка'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Инвентарная этикетка'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиИнвентаризационныхЭтикеток";
		КомандаПечати.СписокФорм    = "ФормаДокумента";
	КонецЕсли;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС1") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОС1",
			НСтр("ru = 'Акт о приеме-передаче объекта ОС (ОС-1)'"),
			ПечатьОС1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, "ОС1"),, "ОбщийМакет.ПФ_MXL_ОС1");
			
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС1а") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОС1а",
			НСтр("ru = 'Акт о приеме-передаче здания (сооружения) (ОС-1а)'"),
			ПечатьОС1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, "ОС1а"),, "ОбщийМакет.ПФ_MXL_ОС1а");
			
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС1б") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОС1б",
			НСтр("ru = 'Акт о приеме-передаче групп объектов ОС (ОС-1б)'"),
			ПечатьОС1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, "ОС1б"),, "ОбщийМакет.ПФ_MXL_ОС1б");
			
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "ВидОперации");
	
	Возврат Результат;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Функция ПодготовитьТаблицуПредметыАренды(ТаблицаПредметыАренды, ТаблицаРеквизиты, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаПредметыАренды) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаРезультат = ТаблицаПредметыАренды.Скопировать(); // Всегда 1 строка
	ТаблицаРезультат.Колонки.Добавить("СуммаБУ", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаРезультат.Колонки.Добавить("СуммаВР", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаРезультат.Колонки.Добавить("СуммаПР", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаРезультат.Колонки.Добавить("СуммаРуб", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Реквизиты = ТаблицаРеквизиты[0];
	
	// Заполняем остатки по данным БУ
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВнеоборотныеАктивы.Субконто1 КАК ОсновноеСредство,
		|	ВнеоборотныеАктивы.СуммаОстатокДт КАК СуммаБУ
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&Период,
		|			Счет = &СчетУчетаВнеоборотногоАктива,
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация
		|				И Субконто1 = &ПредметАренды) КАК ВнеоборотныеАктивы");
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПредметАренды", Реквизиты.ПредметАренды);
	Запрос.УстановитьПараметр("СчетУчетаВнеоборотногоАктива", Реквизиты.СчетУчетаВнеоборотногоАктива);
	ОстаткиБУ = Запрос.Выполнить().Выгрузить();
	
	СтрокаРезультат = ТаблицаРезультат.Найти(Реквизиты.ПредметАренды, "ОсновноеСредство");
	
	СтрокаОстаткаБУ = ОстаткиБУ.Найти(Реквизиты.ПредметАренды, "ОсновноеСредство");
	Если СтрокаОстаткаБУ <> Неопределено Тогда
		СтрокаРезультат.СуммаБУ = СтрокаОстаткаБУ.СуммаБУ;
	Иначе
		ТекстОшибки = СтрШаблон(
			НСтр("ru='Для основного средства ""%1"" не найдены остатки по счету %2.'"), 
			Реквизиты.ПредметАренды, 
			Реквизиты.СчетУчетаВнеоборотногоАктива);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Реквизиты.Регистратор, , , Отказ);
	КонецЕсли;
	
	// Проверяем, что данные принятия к учету соответствуют данным поступления в аренду
	
	ПараметрыПоступления = Новый Структура;
	ПараметрыПоступления.Вставить("ДатаСведений", Реквизиты.Период);
	ПараметрыПоступления.Вставить("Организация", Реквизиты.Организация);
	ПараметрыПоступления.Вставить("Контрагент", Реквизиты.Контрагент);
	ПараметрыПоступления.Вставить("ДоговорКонтрагента", Реквизиты.ДоговорКонтрагента);
	ПараметрыПоступления.Вставить("ПредметАренды", Реквизиты.ПредметАренды);
	ДанныеОПоступлении = РегистрыСведений.ДанныеПредметовАренды.ДанныеПредмета(ПараметрыПоступления);
	
	Если Не ДанныеОПоступлении.ЕстьДанныеОПоступлении Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru='Не найдены данные о поступлении основного средства ""%1"".'"), 
			Реквизиты.ПредметАренды);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Реквизиты.Регистратор, "ПредметАренды", , Отказ);
	Иначе
		
		Если ДанныеОПоступлении.СпособПоступления <> Реквизиты.СпособПоступления Тогда
			ТекстОшибки = СтрШаблон(
				НСтр("ru='Способ поступления отличается от указанного в документе ""%1"".'"), 
				ДанныеОПоступлении.ДокументПоступления);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Реквизиты.Регистратор, "СпособПоступления", , Отказ);
		КонецЕсли;
		
		ПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
		Если ПлательщикНалогаНаПрибыль 
			И ДанныеОПоступлении.СпособПоступления = Перечисления.СпособыПоступленияАктивов.ПоДоговоруЛизинга 
			И Реквизиты.СпособПоступления = Перечисления.СпособыПоступленияАктивов.ПоДоговоруЛизинга Тогда
			
			Если ДанныеОПоступлении.НалоговыйУчетИмуществаВедетЛизингополучатель 
				<> Реквизиты.НалоговыйУчетИмуществаВедетЛизингополучатель Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru='Порядок ведения налогового учета имущества отличается от указанного в документе ""%1"".'"), 
					ДанныеОПоступлении.ДокументПоступления);
				ОбщегоНазначения.СообщитьПользователю(
					ТекстОшибки, Реквизиты.Регистратор, "НалоговыйУчетИмуществаВедетЛизингополучатель", , Отказ);
			КонецЕсли;
			
			Если ДанныеОПоступлении.НалоговыйУчетИмуществаВедетЛизингополучатель 
				И Реквизиты.НалоговыйУчетИмуществаВедетЛизингополучатель 
				И ДанныеОПоступлении.РасходыЛизингодателя <> СтрокаРезультат.СуммаНУ Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru='Сумма расходов лизингодателя отличается от указанной в документе ""%1"".'"), 
					ДанныеОПоступлении.ДокументПоступления);
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Реквизиты.Регистратор, "ПервоначальнаяСтоимостьНУ", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		СтрокаРезультат.СуммаРуб = ДанныеОПоступлении.СуммаРуб;
		
	КонецЕсли;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

// Возвращает признак, что документ не может быть документом расхода по федеральному инвестиционному вычету.
// см. ИнвестиционныйНалоговыйВычет.МожноПрименятьНалоговыйВычет.
// Вызов функции универсален для менеджеров документов, описанных в параметре МенеджерДокумента
// функции ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета.
// 
// Параметры:
//  КритерииПримененияВычета - см. ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета
// 
// Возвращаемое значение:
//  Булево
//
Функция ЗапрещеноПрименятьНалоговыйВычет(КритерииПримененияВычета) Экспорт
	
	СписокДопустимыхОпераций = СписокОперацийПрименяетсяИнвестиционныйВычет();
	Если СписокДопустимыхОпераций.НайтиПоЗначению(КритерииПримененияВычета.ВидОперации) = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если КритерииПримененияВычета.ПорядокВключенияСтоимостиВСоставРасходовНУ <> Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации Тогда
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

// Заполняет новый реквизит "СтавкаНДС" в ТЧ "Оплата".
Процедура ЗаполнитьСтавкуНДС(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПринятиеКУчетуОСОплата.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.Оплата КАК ПринятиеКУчетуОСОплата
	|ГДЕ
	|	ПринятиеКУчетуОСОплата.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектовОбработано = ОбъектовОбработано + 1;
		НачатьТранзакцию();

		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПринятиеКУчетуОС");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		Блокировка.Заблокировать();
		
		ПринятиеКУчетуОС = Выборка.Ссылка.ПолучитьОбъект();
		
		Если ПринятиеКУчетуОС = Неопределено Тогда
			ОтменитьТранзакцию();
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаОплаты Из ПринятиеКУчетуОС.Оплата Цикл
			Если СтрокаОплаты.СтавкаНДС.Пустая() Тогда
				СтрокаОплаты.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ПринятиеКУчетуОС, Истина);
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Процедуре Документы.ПринятиеКУчетуОС.ЗаполнитьСтавкуНДС() не удалось обработать документ %1 по причине:
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				ПринятиеКУчетуОС.Ссылка,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПринятиеКУчетуОС,
				Выборка.Ссылка,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре Документы.ПринятиеКУчетуОС.ЗаполнитьСтавкуНДС() не удалось обработать документы: в %1 из %2 возникли ошибки'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ПроблемныхОбъектов,
			ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		Параметры.ОбработкаЗавершена = Истина;
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ПринятиеКУчетуОС,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Документы.ПринятиеКУчетуОС.ЗаполнитьСтавкуНДС() успешно завершена: обработано %1 документов'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

// Функция проверяет, заполнен ли новый реквизит "СтавкаНДС" в ТЧ "Оплата" (см. ЗаполнитьСтавкуНДС()).
// Определена в свойстве ПроцедураПроверки соответствующего обработчика обновления.
//
// Параметры:
//  Параметры - Структура - см. документацию к БСП
// 
// Возвращаемое значение:
//   - Булево - если Истина, то можно редактировать и записывать данные, Ложь - данные недоступны, пока не будут обработаны процедурой обновления
//
Функция СтавкаНДСЗаполнена(Параметры) Экспорт
	
	Документ = Неопределено;
	
	Если ТипЗнч(Параметры.Данные) = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
		Документ = Параметры.Данные;
	ИначеЕсли ТипЗнч(Параметры.Данные) = Тип("ДокументОбъект.ПринятиеКУчетуОС")
		 Или ТипЗнч(Параметры.Данные) = Тип("ДанныеФормыСтруктура") Тогда
		Документ = Параметры.Данные.Ссылка;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат Истина;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПринятиеКУчетуОСОплата.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.Оплата КАК ПринятиеКУчетуОСОплата
	|ГДЕ
	|	ПринятиеКУчетуОСОплата.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
	|	И ПринятиеКУчетуОСОплата.Ссылка = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодготовкаПараметровПроведенияДокумента

Функция ТекстЗапросаТаблицаОС(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ОсновноеСредство.ГруппаОС КАК ГруппаОС
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСписаниеТоваров(НомераТаблиц)

	НомераТаблиц.Вставить("СписаниеТоваров", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеТоваровТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	""Списание"" КАК ТипСписания,
	|	Реквизиты.Ссылка КАК ДокументРеализации,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	ЛОЖЬ КАК ДеятельностьНаПатенте,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.Склад.ТипСклада КАК ТипСклада,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	""Принят к учету объект ОС"" КАК Содержание,
	|	Реквизиты.СпособПоступления КАК СпособПоступления,
	|	Реквизиты.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ОС"" КАК ИмяСписка,
	|	&СинонимОС КАК СинонимСписка,
	|	ТаблицаОС.Ссылка.Дата КАК Период,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.Ссылка.СчетУчетаВнеоборотногоАктива КАК СчетУчета,
	|	ТаблицаОС.Ссылка.Номенклатура КАК Номенклатура,
	|	ТаблицаОС.Ссылка.Склад КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
	|	0 КАК Себестоимость,
	|	1 КАК Количество,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКомиссии,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетАвансовСКомитентом,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетРасчетовСКомитентом,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаРасчетовСКомитентом,
	|	0 КАК СуммаРасчетовСКомитентом,
	|	ТаблицаОС.Ссылка.СчетУчета КАК КорСчетСписания,
	|	ТаблицаОС.ОсновноеСредство КАК КорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства) КАК ВидКорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто3,
	|	ТаблицаОС.Ссылка.МестонахождениеОС КАК КорПодразделение
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаОсновныеСредства(НомераТаблиц)

	НомераТаблиц.Вставить("ОсновныеСредства", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ГруппаОС
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПринятиеКУчетуОбъектыСтроительства(НомераТаблиц)

	НомераТаблиц.Вставить("ПринятиеКУчетуОбъектыСтроительства", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПринятиеКУчетуОбъектыСтроительстваТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеКт,
	|	Реквизиты.МестонахождениеОС КАК ПодразделениеДт,
	|	Реквизиты.СчетУчета,
	|	Реквизиты.СчетУчетаВнеоборотногоАктива,
	|	Реквизиты.ОбъектСтроительства,
	|	Реквизиты.СпособПоступления,
	|	Реквизиты.ПервоначальнаяСтоимостьНУ
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.Ссылка.СтоимостьБУ КАК СуммаБУ,
	|	ТаблицаОС.Ссылка.СтоимостьНУ КАК СуммаНУ,
	|	ТаблицаОС.Ссылка.СтоимостьПР КАК СуммаПР,
	|	ТаблицаОС.Ссылка.СтоимостьВР КАК СуммаВР
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|	И (ТаблицаОС.Ссылка.СтоимостьБУ > 0
	|			ИЛИ ТаблицаОС.Ссылка.СтоимостьНУ > 0
	|			ИЛИ ТаблицаОС.Ссылка.СтоимостьПР <> 0
	|			ИЛИ ТаблицаОС.Ссылка.СтоимостьВР <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПринятиеКУчетуПоРезультатамИнвентаризации(НомераТаблиц)

	НомераТаблиц.Вставить("ПринятиеКУчетуПоРезультатамИнвентаризации", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПринятиеКУчетуПоРезультатамИнвентаризацииТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.МестонахождениеОС КАК ПодразделениеДт,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеКт,
	|	Реквизиты.СчетУчета КАК СчетУчета,
	|	Реквизиты.СтатьяДоходов КАК СтатьяДоходов,
	|	Реквизиты.ОбъектСтроительства КАК ОбъектСтроительства
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Ссылка.СтоимостьБУ КАК СуммаБУ,
	|	ТаблицаОС.Ссылка.СтоимостьНУ КАК СуммаНУ,
	|	ТаблицаОС.Ссылка.СтоимостьБУ - ТаблицаОС.Ссылка.СтоимостьНУ КАК СуммаПР
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПоРезультатамИнвентаризации)
	|	И (ТаблицаОС.Ссылка.СтоимостьБУ > 0
	|			ИЛИ ТаблицаОС.Ссылка.СтоимостьНУ > 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСобытияОС(НомераТаблиц)

	НомераТаблиц.Вставить("СобытияОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СобытияОСТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Номер,
	|	Реквизиты.Организация,
	|	Реквизиты.СобытиеОС КАК СобытиеОС
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство,
	|	0 КАК СуммаЗатратБУ,
	|	0 КАК СуммаЗатратНУ,
	|	0 КАК СуммаЗатратУСН
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСостоянияОС(НомераТаблиц)

	НомераТаблиц.Вставить("СостоянияОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Номер,
	|	Реквизиты.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК СостояниеОС,
	|	""ОС"" КАК ИмяСписка
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияОСБУ(НомераТаблиц)

	// Также может использоваться как таблица реквизитов
	НомераТаблиц.Вставить("ПервоначальныеСведенияОСБУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	&ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	Реквизиты.ПорядокПогашенияСтоимостиБУ КАК ПорядокПогашенияСтоимости,
	|	Реквизиты.СпособПоступления КАК СпособПоступления,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.СпособНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
	|	Реквизиты.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ КАК ПроцентАмортизационнойПремии,
	|	Реквизиты.ПараметрВыработкиБУ КАК ПараметрВыработки,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование) КАК ИспользоватьСписанныеТовары,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПредметыАренды) КАК ИспользоватьПредметыАренды,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|				ИЛИ Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПоРезультатамИнвентаризации)
	|			ТОГДА Реквизиты.СтоимостьБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|				ИЛИ Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПоРезультатамИнвентаризации)
	|			ТОГДА Реквизиты.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|			ТОГДА Реквизиты.СтоимостьПР
	|		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПоРезультатамИнвентаризации)
	|			ТОГДА Реквизиты.СтоимостьБУ - Реквизиты.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПР,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|			ТОГДА Реквизиты.СтоимостьВР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВР,
	|	Реквизиты.СтоимостьУСН КАК СуммаУСН
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаМестонахождениеОСБУ(НомераТаблиц)

	НомераТаблиц.Вставить("МестонахождениеОСБУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.МестонахождениеОС КАК Местонахождение,
	|	Реквизиты.МОЛБУ КАК МОЛ,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНачислениеАмортизацииОСБУ(НомераТаблиц)

	НомераТаблиц.Вставить("НачислениеАмортизацииОСБУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.НачислятьАмортизациюБУ КАК НачислятьАмортизацию
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСчетаБухгалтерскогоУчетаОС(НомераТаблиц)
	
	НомераТаблиц.Вставить("СчетаБухгалтерскогоУчетаОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СчетаБухгалтерскогоУчетаОСТаблица", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Ссылка.СчетУчета КАК СчетУчета,
	|	ТаблицаОС.Ссылка.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаГрафикиАмортизацииОСБУ(НомераТаблиц)

	НомераТаблиц.Вставить("ГрафикиАмортизацииОСБУ", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ГрафикиАмортизацииОСБУТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ГрафикАмортизацииПоГодуБУ КАК ГрафикАмортизации
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка.ГрафикАмортизацииПоГодуБУ <> ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСпособыОтраженияРасходовПоАмортизацииОСБУ(НомераТаблиц)

	НомераТаблиц.Вставить("СпособыОтраженияРасходовПоАмортизацииОСБУ", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СпособыОтраженияРасходовПоАмортизацииОСБУТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.СпособыОтраженияРасходовПоАмортизации
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ИнвентарныйНомер
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка.СпособыОтраженияРасходовПоАмортизации <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияРасходовПоАмортизации.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПараметрыАмортизацииОСБУ(НомераТаблиц)

	НомераТаблиц.Вставить("ПараметрыАмортизацииОСБУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ГодоваяНормаАмортизацииБУ КАК ГодоваяНормаАмортизации,
	|	Реквизиты.КоэффициентУскоренияБУ КАК КоэффициентУскорения,
	|	Реквизиты.ПредполагаемыйОбъемПродукцииБУ КАК ПредполагаемыйОбъемПродукции,
	|	Реквизиты.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	Реквизиты.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
	|	Реквизиты.СпособНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПараметрыАмортизацииОСНУ(НомераТаблиц)

	НомераТаблиц.Вставить("ПараметрыАмортизацииОСНУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка                       КАК Регистратор,
	|	Реквизиты.Дата                         КАК Период,
	|	Реквизиты.Организация                  КАК Организация,
	|	Реквизиты.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользования,
	|	&ПлательщикНалогаНаПрибыль             КАК ПлательщикНалогаНаПрибыль
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияОСНУ(НомераТаблиц)

	// Также может использоваться как таблица реквизитов
	НомераТаблиц.Вставить("ПервоначальныеСведенияОСНУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	&ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	Реквизиты.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользования,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	Реквизиты.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	Реквизиты.СпособПоступления КАК СпособПоступления,
	|	Реквизиты.СпособОтраженияРасходовПриВключенииВСтоимость,
	|	Реквизиты.СпособыОтраженияРасходовПоАмортизации
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияОСУСН(НомераТаблиц)

	// Также может использоваться как таблица реквизитов
	НомераТаблиц.Вставить("ПервоначальныеСведенияОСУСН", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СрокПолезногоИспользованияУСН,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовУСН,
	|	Реквизиты.ДатаПриобретения,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПредметыАренды) КАК ПредметАренды
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаАмортизационнаяПремия(НомераТаблиц)

	НомераТаблиц.Вставить("АмортизационнаяПремия", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|			ТОГДА Реквизиты.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроцентАмортизационнойПремии,
	|	Реквизиты.МестонахождениеОС КАК Подразделение
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаИнвестиционныйВычет(НомераТаблиц)

	НомераТаблиц.Вставить("ИнвестиционныйВычет", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.МестонахождениеОС КАК ПодразделениеОрганизации,
	|	Реквизиты.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПрименяетсяИнвестиционныйВычет
	|			ТОГДА &ПроцентИнвестиционногоВычета
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроцентИнвестиционногоВычета
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|	И ВЫБОР
	|			КОГДА Реквизиты.ПрименяетсяИнвестиционныйВычет
	|				ТОГДА &ПроцентИнвестиционногоВычета
	|			ИНАЧЕ 0
	|		КОНЕЦ > 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНачислениеАмортизацииОСНУ(НомераТаблиц)

	НомераТаблиц.Вставить("НачислениеАмортизацииОСНУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка                 КАК Регистратор,
	|	Реквизиты.Дата                   КАК Период,
	|	Реквизиты.Организация            КАК Организация,
	|	&ПлательщикНалогаНаПрибыль       КАК ПлательщикНалогаНаПрибыль,
	|	Реквизиты.НачислятьАмортизациюНУ КАК НачислятьАмортизацию
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции


Функция ТекстЗапросаНачислениеАмортизацииОССпециальныйКоэффициентНУ(НомераТаблиц)

	НомераТаблиц.Вставить("НачислениеАмортизацииОССпециальныйКоэффициентНУ", 		НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НачислениеАмортизацииОССпециальныйКоэффициентНУТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	&ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Регистратор,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.Ссылка.СпециальныйКоэффициентНУ КАК СпециальныйКоэффициент
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка.ПорядокВключенияСтоимостиВСоставРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|	И &ПлательщикНалогаНаПрибыль = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";


	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНачислениеАмортизацииПриПринятииКУчету(НомераТаблиц)

	НомераТаблиц.Вставить("НачислениеАмортизацииПриПринятииКУчету", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка                                КАК Регистратор,
	|	Реквизиты.Дата                                  КАК Период,
	|	Реквизиты.Организация                           КАК Организация,
	|	Реквизиты.МестонахождениеОС                     КАК Подразделение,
	|	Реквизиты.СпособНачисленияАмортизацииБУ         КАК СпособНачисленияАмортизации,
	|	Реквизиты.СпособыОтраженияРасходовПоАмортизации КАК СпособыОтраженияРасходовПоАмортизации,
	|	Реквизиты.СчетНачисленияАмортизации             КАК СчетНачисленияАмортизации,
	|	""Начисление амортизации""                      КАК Содержание,
	|	""ОсновныеСредства""                            КАК ИмяСубконто,
	|	""ОС""                                          КАК ИмяСписка
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаВключениеВРасходыПриПринятииКУчетуНУ(НомераТаблиц)

	НомераТаблиц.Вставить("ВключениеВРасходыПриПринятииКУчетуНУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка                                        КАК Регистратор,
	|	Реквизиты.Дата                                          КАК Период,
	|	Реквизиты.Организация                                   КАК Организация,
	|	&ПлательщикНалогаНаПрибыль                              КАК ПлательщикНалогаНаПрибыль,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ    КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	Реквизиты.СпособОтраженияРасходовПриВключенииВСтоимость КАК СпособОтраженияРасходовПриВключенииВСтоимость,
	|	Реквизиты.СпособыОтраженияРасходовПоАмортизации         КАК СпособыОтраженияРасходовПоАмортизации,
	|	Реквизиты.СчетУчета                                     КАК СчетУчета,
	|	Реквизиты.МестонахождениеОС		                        КАК Подразделение,
	|	Реквизиты.ПорядокПогашенияСтоимостиБУ                   КАК ПорядокПогашенияСтоимостиБУ,
	|	""Включение стоимости в состав расходов (НУ)""          КАК Содержание,
	|	""ОсновныеСредства""                                    КАК ИмяСубконто,
	|	""ОС""                                                  КАК ИмяСписка
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаОплатаОсновныхСредствДляУСН(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСпособыУчетаНДСОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОплатаОсновныхСредствДляУСН", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПринятиеКУчетуОСОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПринятиеКУчетуОСОС.СпособУчетаНДС КАК СпособУчетаНДС
	|ПОМЕСТИТЬ ВТ_СпсобыУчетаНДСОС
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ПринятиеКУчетуОСОС
	|ГДЕ
	|	ПринятиеКУчетуОСОС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПринятиеКУчетуОСОплата.Ссылка КАК Ссылка,
	|	ПринятиеКУчетуОСОплата.НомерСтроки КАК НомерСтроки,
	|	ПринятиеКУчетуОСОплата.ОсновноеСредство КАК ОсновноеСредство,
	|	ПринятиеКУчетуОСОплата.ДатаОплаты КАК ДатаОплаты,
	|	ПринятиеКУчетуОСОплата.СуммаОплаты КАК СуммаОплаты,
	|	ПринятиеКУчетуОСОплата.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(ВТ_СпсобыУчетаНДСОС.СпособУчетаНДС, ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.ПринимаетсяКВычету)) КАК СпособУчетаНДС
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.Оплата КАК ПринятиеКУчетуОСОплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпсобыУчетаНДСОС КАК ВТ_СпсобыУчетаНДСОС
	|		ПО ПринятиеКУчетуОСОплата.ОсновноеСредство = ВТ_СпсобыУчетаНДСОС.ОсновноеСредство
	|ГДЕ
	|	ПринятиеКУчетуОСОплата.Ссылка = &Ссылка
	|	И &ОтражатьВНалоговомУчетеУСН = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНДС(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТоварыНДС",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОбъектыСтроительстваНДС",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаСписанныеОбъектыСтроительстваНДС",    НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|			ТОГДА Реквизиты.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроцентАмортизационнойПремии,
	|	Реквизиты.ВидОперации КАК ВидОперации
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ОС"" КАК ИмяСписка,
	|	ТаблицаОС.НомерСтроки КАК НомерСтрокиДокумента,
	|	ТаблицаОС.Ссылка.Номенклатура КАК Номенклатура,
	|	1 КАК Количество,
	|	ТаблицаОС.Ссылка.СчетУчетаВнеоборотногоАктива КАК СчетУчета,
	|	ТаблицаОС.СпособУчетаНДС КАК НовыйСпособУчетаНДС
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование)
	|	И &ПлательщикНДС = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ОС"" КАК ИмяСписка,
	|	ТаблицаОС.НомерСтроки КАК НомерСтрокиДокумента,
	|	ТаблицаОС.Ссылка.ОбъектСтроительства КАК Номенклатура,
	|	1 КАК Количество,
	|	ТаблицаОС.Ссылка.СчетУчетаВнеоборотногоАктива КАК СчетУчета,
	|	ТаблицаОС.СпособУчетаНДС КАК НовыйСпособУчетаНДС
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|	И &ПлательщикНДС = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ОС"" КАК ИмяСписка,
	|	""Основные средства"" КАК СинонимСписка,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.Ссылка.СчетУчетаВнеоборотногоАктива КАК СчетУчета,
	|	ТаблицаОС.Ссылка.ОбъектСтроительства КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК Партия,
	|	1 КАК Количество,
	|	ТаблицаОС.Ссылка.СчетУчета КАК КорСчетСписания,
	|	1 КАК ВидКорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто3,
	|	ТаблицаОС.ОсновноеСредство КАК КорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	ТаблицаОС.Ссылка.МестонахождениеОС КАК КорПодразделение,
	|	ТаблицаОС.Ссылка.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|	И &ПлательщикНДС = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияОСИП(НомераТаблиц)

	НомераТаблиц.Вставить("ПервоначальныеСведенияОСИП",	НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
	|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
	|		ИНАЧЕ Реквизиты.Номенклатура.НоменклатурнаяГруппа
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	Реквизиты.Номенклатура КАК Номенклатура,
	|	Реквизиты.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеКомпонентовОсновныхСредств)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ Реквизиты.УказатьПервоначальнуюСтоимость
	|	КОНЕЦ КАК УказатьПервоначальнуюСтоимость,
	|	Реквизиты.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходовНУ,
	|	Реквизиты.НачислятьАмортизациюНУ КАК НачислятьАмортизацию,
	|	Реквизиты.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользования,
	|	Реквизиты.СпособНачисленияАмортизацииНУ КАК СпособНачисленияАмортизации,
	|	Реквизиты.КоэффициентАмортизацииНУ КАК КоэффициентАмортизации,
	|	Реквизиты.СпециальныйКоэффициентНУ КАК СпециальныйКоэффициент,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы) КАК СчетЗатрат,
	|	Реквизиты.СтатьяПрочихРасходов КАК СтатьяЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК ВидРасходовНУ,
	|	Реквизиты.СтатьяПрочихРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|	Реквизиты.СтатьяПрочихРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	Реквизиты.РеквизитыДокументаОплаты КАК РеквизитыДокументаОплаты
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, ВедетсяУчетПрослеживаемыхТоваров, ПоРезультатамИнвентаризации)
	
	Если НЕ ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОС",       Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТовары",   Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",        НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОС",           НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТовары",       НомераТаблиц.Количество());
	Если ПоРезультатамИнвентаризации Тогда
		НомераТаблиц.Вставить("ПрослеживаемыеОперации",    НомераТаблиц.Количество());
	Иначе
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации", Неопределено);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СУММА(ПринятиеКУчетуОССведенияПрослеживаемости.Количество) КАК Количество,
	|	ПринятиеКУчетуОССведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(ПринятиеКУчетуОССведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ПринятиеКУчетуОСОС.Ссылка КАК ДокументОперации,
	|	ПринятиеКУчетуОССведенияПрослеживаемости.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПринятиеКУчетуОС.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА ПринятиеКУчетуОС.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ПринятиеКУчетуОС.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|			ТОГДА ПринятиеКУчетуОСОС.ОсновноеСредство
	|		КОГДА ПринятиеКУчетуОС.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПоРезультатамИнвентаризации)
	|			ТОГДА ВЫБОР
	|					КОГДА ПринятиеКУчетуОССведенияПрослеживаемости.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ПринятиеКУчетуОСОС.ОсновноеСредство
	|				КОНЕЦ
	|	КОНЕЦ КАК Комплект,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	ПринятиеКУчетуОС.Организация КАК Организация,
	|	ПринятиеКУчетуОС.ВидОперации КАК ВидОперации,
	|	ПринятиеКУчетуОС.Номенклатура КАК Оборудование,
	|	ПринятиеКУчетуОС.ОбъектСтроительства КАК ОбъектСтроительства,
	|	ПринятиеКУчетуОС.Номер КАК Номер,
	|	ПринятиеКУчетуОССведенияПрослеживаемости.Номенклатура КАК Номенклатура,
	|	ПринятиеКУчетуОСОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА ПринятиеКУчетуОССведенияПрослеживаемости.Сумма = 0
	|			ТОГДА ПринятиеКУчетуОС.СтоимостьБУ
	|		ИНАЧЕ ПринятиеКУчетуОССведенияПрослеживаемости.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ПринятиеКУчетуОСОС.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТ_Прослеживаемость
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ПринятиеКУчетуОСОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.СведенияПрослеживаемости КАК ПринятиеКУчетуОССведенияПрослеживаемости
	|		ПО ПринятиеКУчетуОСОС.Ссылка = ПринятиеКУчетуОССведенияПрослеживаемости.Ссылка
	|			И ПринятиеКУчетуОСОС.ИдентификаторСтроки = ПринятиеКУчетуОССведенияПрослеживаемости.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|		ПО ПринятиеКУчетуОСОС.Ссылка = ПринятиеКУчетуОС.Ссылка
	|ГДЕ
	|	ПринятиеКУчетуОСОС.ПрослеживаемыйТовар
	|	И ПринятиеКУчетуОСОС.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПринятиеКУчетуОССведенияПрослеживаемости.РНПТ,
	|	ПринятиеКУчетуОСОС.Ссылка,
	|	ПринятиеКУчетуОССведенияПрослеживаемости.СтранаПроисхождения,
	|	ПринятиеКУчетуОС.Дата,
	|	ПринятиеКУчетуОС.Организация,
	|	ПринятиеКУчетуОС.ВидОперации,
	|	ПринятиеКУчетуОС.Номенклатура,
	|	ПринятиеКУчетуОС.ОбъектСтроительства,
	|	ПринятиеКУчетуОС.Номер,
	|	ВЫБОР
	|		КОГДА ПринятиеКУчетуОС.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ПринятиеКУчетуОС.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|			ТОГДА ПринятиеКУчетуОСОС.ОсновноеСредство
	|		КОГДА ПринятиеКУчетуОС.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПоРезультатамИнвентаризации)
	|			ТОГДА ВЫБОР
	|					КОГДА ПринятиеКУчетуОССведенияПрослеживаемости.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ПринятиеКУчетуОСОС.ОсновноеСредство
	|				КОНЕЦ
	|	КОНЕЦ,
	|	ПринятиеКУчетуОССведенияПрослеживаемости.Номенклатура,
	|	ПринятиеКУчетуОСОС.ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА ПринятиеКУчетуОССведенияПрослеживаемости.Сумма = 0
	|			ТОГДА ПринятиеКУчетуОС.СтоимостьБУ
	|		ИНАЧЕ ПринятиеКУчетуОССведенияПрослеживаемости.Сумма
	|	КОНЕЦ,
	|	ПринятиеКУчетуОСОС.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВТ_Прослеживаемость.Организация КАК Организация,
	|	ВТ_Прослеживаемость.Номенклатура КАК Комплектующие,
	|	ВТ_Прослеживаемость.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Прослеживаемость.Сумма КАК Сумма
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.РНПТ,
	|	ВТ_Прослеживаемость.СтранаПроисхождения,
	|	ВТ_Прослеживаемость.Организация,
	|	ВТ_Прослеживаемость.Номенклатура,
	|	ВТ_Прослеживаемость.ОсновноеСредство,
	|	ВТ_Прослеживаемость.Сумма
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Количество КАК Количество,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|			ТОГДА ВТ_Прослеживаемость.ОбъектСтроительства
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ОсновноеСредство,
	|	ВТ_Прослеживаемость.ВидЗапасов КАК ВидЗапасов,
	|	ВТ_Прослеживаемость.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДляВозврата,
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование)
	|			ТОГДА ВТ_Прослеживаемость.Оборудование
	|		КОГДА ВТ_Прослеживаемость.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|			ТОГДА ВТ_Прослеживаемость.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВТ_Прослеживаемость.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		КОГДА ВТ_Прослеживаемость.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства)
	|			ТОГДА ВТ_Прослеживаемость.Номенклатура
	|	КОНЕЦ КАК Комплектующие
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость";
	Если ПоРезультатамИнвентаризации Тогда
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		ТекстЗапроса = ТекстЗапроса + "ВЫБРАТЬ
		|	ВТ_Прослеживаемость.Количество КАК Количество,
		|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
		|	ВТ_Прослеживаемость.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	ВТ_Прослеживаемость.ДокументОперации КАК ДокументОперации,
		|	ВТ_Прослеживаемость.Сумма КАК СуммаБезНДС,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях) КАК ОтражениеВОтчетности,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
		|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт) КАК ТипДокументаВПрослеживаемости,
		|	НАЧАЛОПЕРИОДА(ВТ_Прослеживаемость.Дата, КВАРТАЛ) КАК ПериодОперации,
		|	ВТ_Прослеживаемость.Дата КАК ДатаДокумента,
		|	ВТ_Прослеживаемость.Номер КАК НомерДокумента,
		|	ВТ_Прослеживаемость.Организация КАК Организация,
		|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВозвратРанееУтраченногоТовара) КАК КодОперации,
		|	ВЫБОР
		|		КОГДА ВТ_Прослеживаемость.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА ВТ_Прослеживаемость.ОсновноеСредство
		|		ИНАЧЕ ВТ_Прослеживаемость.Номенклатура
		|	КОНЕЦ КАК Номенклатура,
		|	ЛОЖЬ КАК ПрослеживаемыйКомплект,
		|	ЛОЖЬ КАК ГрупповойОбъект,
		|	ВТ_Прослеживаемость.ИдентификаторСтроки КАК ИдентификаторСтроки 
		|ИЗ
		|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость";
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСпособыОтраженияРасходовПоАренднымПлатежамОСНУ(НомераТаблиц)

	НомераТаблиц.Вставить("СпособыОтраженияРасходовПоАренднымПлатежамОСНУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПредметыАренды)
	|		ИЛИ Реквизиты.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга) КАК ПоступлениеВАренду,
	|	Реквизиты.СпособОтраженияРасходовПоАренднымПлатежам КАК СпособОтраженияРасходовПоАренднымПлатежам
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПредметыАренды(НомераТаблиц)
	
	НомераТаблиц.Вставить("ПредметыАренды", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПредметыАрендыТаблица", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеКт,
	|	Реквизиты.МестонахождениеОС КАК ПодразделениеДт,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.СчетУчета КАК СчетУчета,
	|	Реквизиты.СчетУчетаВнеоборотногоАктива КАК СчетУчетаВнеоборотногоАктива,
	|	Реквизиты.ПредметАренды КАК ПредметАренды,
	|	Реквизиты.СпособПоступления КАК СпособПоступления,
	|	Реквизиты.НалоговыйУчетИмуществаВедетЛизингополучатель КАК НалоговыйУчетИмуществаВедетЛизингополучатель,
	|	ЛОЖЬ КАК ПереходНаФСБУ25
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Ссылка.СчетУчета КАК СчетУчета,
	|	ТаблицаОС.Ссылка.СчетУчетаВнеоборотногоАктива КАК СчетУчетаВнеоборотногоАктива,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.Ссылка.СпособПоступления = ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПоДоговоруЛизинга)
	|				И ТаблицаОС.Ссылка.НалоговыйУчетИмуществаВедетЛизингополучатель
	|			ТОГДА ТаблицаОС.Ссылка.ПервоначальнаяСтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУ
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуОсновныхСредств.ПредметыАренды)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

#КонецОбласти

//Процедура заполняет параметры организации-получателя формы ОС1
Процедура ЗаполнитьДанныеОрганизацииПолучателя(ПараметрыДокумента, ОбластьМакета) Экспорт

	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ПараметрыДокумента.Организация, ПараметрыДокумента.Дата);
	ОтветственныеЛицаОрганизации = ОтветственныеЛицаБП.ОтветственныеЛица(ПараметрыДокумента.Организация,
		ПараметрыДокумента.Дата, ПараметрыДокумента.ПодразделениеОрганизации);

	ПараметрыОрганизации = Новый Структура("ОрганизацияПолучатель, АдресПолучателя, РеквПолучателя, ДолжРукПолуч,
		|РукПолучателя, КодПоОКПОПолучателя, ГлавБухПолучателя");

	ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
	ПараметрыОрганизации.ОрганизацияПолучатель = ПредставлениеОрганизации;
	ПараметрыОрганизации.АдресПолучателя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации,
		"ЮридическийАдрес,Телефоны,");
	ПараметрыОрганизации.РеквПолучателя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации,
		"НомерСчета,Банк,БИК,КоррСчет,");
	ПараметрыОрганизации.КодПоОКПОПолучателя = СведенияОбОрганизации.КодПоОКПО;

	ПараметрыОрганизации.РукПолучателя = ОтветственныеЛицаОрганизации.РуководительПредставление;
	ПараметрыОрганизации.ДолжРукПолуч = ОтветственныеЛицаОрганизации.РуководительДолжностьПредставление;
	ПараметрыОрганизации.ГлавБухПолучателя = ОтветственныеЛицаОрганизации.ГлавныйБухгалтерПредставление;

	ОбластьМакета.Параметры.Заполнить(ПараметрыОрганизации);

КонецПроцедуры

Функция ПечатьОС1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПринятиеКУчетуОС_ОС1";

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС1");
	МакетОС1а = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС1а");
	МакетОС1б = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС1б");

	// Области
	ОбластьМакетаЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакетаЗаголовок_а = МакетОС1а.ПолучитьОбласть("Заголовок");

	Шапка1ОС1б     = МакетОС1б.ПолучитьОбласть("Шапка1");
	Шапка2ОС1б     = МакетОС1б.ПолучитьОбласть("Шапка2");
	Строка2ОС1б    = МакетОС1б.ПолучитьОбласть("Строка2");
	Строка2ПОС1б   = МакетОС1б.ПолучитьОбласть("Строка2П");
	Подвал2        = МакетОС1б.ПолучитьОбласть("Подвал2");
	Шапка3ОС1б     = МакетОС1б.ПолучитьОбласть("Шапка3");
	Строка3ОС1б    = МакетОС1б.ПолучитьОбласть("Строка3");
	Строка3ПОС1б   = МакетОС1б.ПолучитьОбласть("Строка3П");
	Подвал3        = МакетОС1б.ПолучитьОбласть("Подвал3");
	Шапка4ОС1б     = МакетОС1б.ПолучитьОбласть("Шапка4");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПринятиеКУчетуОСОсновныеСредства.Ссылка КАК Ссылка,
	|	ПринятиеКУчетуОСОсновныеСредства.Ссылка.Организация КАК Организация,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
	|	ПринятиеКУчетуОСОсновныеСредства.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПринятиеКУчетуОСОсновныеСредства.НомерСтроки КАК НомерСтроки,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.АмортизационнаяГруппа.Порядок + 1 КАК НомерГруппы,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.Изготовитель КАК Изготовитель,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.КодПоОКОФ.Код КАК КодОКОФ,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	МАКСИМУМ(ПервоначальныеСведенияОСБухгалтерскийУчет.Период) КАК МаксПериод
	|ПОМЕСТИТЬ ВТ_МаксПериодыСведенийОСБУ
	|ИЗ
	|	Документ.ПринятиеКУчетуОС.ОС КАК ПринятиеКУчетуОСОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ПО ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство
	|			И ПринятиеКУчетуОСОсновныеСредства.Ссылка.Организация = ПервоначальныеСведенияОСБухгалтерскийУчет.Организация
	|			И (ПервоначальныеСведенияОСБухгалтерскийУчет.Период <= КОНЕЦПЕРИОДА(ПринятиеКУчетуОСОсновныеСредства.Ссылка.Дата, МЕСЯЦ))
	|ГДЕ
	|	ПринятиеКУчетуОСОсновныеСредства.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПринятиеКУчетуОСОсновныеСредства.Ссылка,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство,
	|	ПринятиеКУчетуОСОсновныеСредства.ИнвентарныйНомер,
	|	ПринятиеКУчетуОСОсновныеСредства.Ссылка.Организация,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.АмортизационнаяГруппа.Порядок + 1,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.Изготовитель,
	|	ПринятиеКУчетуОСОсновныеСредства.НомерСтроки,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.ГруппаОС,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.НаименованиеПолное,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.ЗаводскойНомер,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.КодПоОКОФ.Код,
	|	ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство.ДатаВыпуска
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПринятиеКУчетуОСОсновныеСредства.Ссылка КАК Ссылка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПринятиеКУчетуОСОсновныеСредства.ОсновноеСредство) КАК КоличествоСтрок
	|ПОМЕСТИТЬ ВТ_ПринятиеКУчетуОС_КоличествоОС
	|ИЗ
	|	ВТ_МаксПериодыСведенийОСБУ КАК ПринятиеКУчетуОСОсновныеСредства
	|
	|СГРУППИРОВАТЬ ПО
	|	ПринятиеКУчетуОСОсновныеСредства.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументПринятиеКУчетуОС.Ссылка КАК Ссылка,
	|	ДокументПринятиеКУчетуОС.Номер КАК НомерАкта,
	|	ДокументПринятиеКУчетуОС.Дата КАК Дата,
	|	ДокументПринятиеКУчетуОС.Дата КАК ДатаВвода,
	|	ДокументПринятиеКУчетуОС.Дата КАК ДатаВводаПриПередаче,
	|	ДокументПринятиеКУчетуОС.КоэффициентУскоренияБУ КАК КоэффициентУскоренияБУ,
	|	ДокументПринятиеКУчетуОС.МОЛБУ КАК МОЛБУ,
	|	ДокументПринятиеКУчетуОС.ГодоваяНормаАмортизацииБУ КАК НормаАморт,
	|	ДокументПринятиеКУчетуОС.Ответственный КАК Ответственный,
	|	ДокументПринятиеКУчетуОС.МестонахождениеОС КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ДокументПринятиеКУчетуОС.МестонахождениеОС.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ДокументПринятиеКУчетуОС.МестонахождениеОС.Наименование
	|		ИНАЧЕ ДокументПринятиеКУчетуОС.МестонахождениеОС.НаименованиеПолное
	|	КОНЕЦ КАК ПодрПолучателя,
	|	ДокументПринятиеКУчетуОС.Склад КАК Склад,
	|	ДокументПринятиеКУчетуОС.СпособПоступления КАК СпособПоступления,
	|	ДокументПринятиеКУчетуОС.СрокПолезногоИспользованияБУ КАК СрокПолезнИспПриПост,
	|	ДокументПринятиеКУчетуОС.СтоимостьБУ КАК СтоимостьБУ,
	|	ДокументПринятиеКУчетуОС.СпособНачисленияАмортизацииБУ КАК СпособАмортизации,
	|	ДокументПринятиеКУчетуОС.Организация КАК Организация,
	|	ДокументПринятиеКУчетуОС.СчетУчета КАК СчетУчетаОС,
	|	ЕСТЬNULL(Сведения.ИнвентарныйНомер, ВТ_МаксПериодыСведенийОСБУ.ИнвентарныйНомер) КАК ИнвНомер,
	|	ЕСТЬNULL(Сведения.ПервоначальнаяСтоимость, 0) КАК НачСтоимость,
	|	ВТ_МаксПериодыСведенийОСБУ.НомерГруппы КАК НомерГруппы,
	|	ВТ_МаксПериодыСведенийОСБУ.Изготовитель КАК Изготовитель,
	|	ВТ_МаксПериодыСведенийОСБУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_МаксПериодыСведенийОСБУ.НомерСтроки КАК НомерСтроки,
	|	ВТ_МаксПериодыСведенийОСБУ.ГруппаОС КАК ГруппаОС,
	|	ВТ_МаксПериодыСведенийОСБУ.НаименованиеОС КАК НаименованиеОС,
	|	ВТ_МаксПериодыСведенийОСБУ.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ВТ_МаксПериодыСведенийОСБУ.ДатаВыпуска КАК ГодВыпуска,
	|	ВТ_МаксПериодыСведенийОСБУ.КодОКОФ КАК КодОКОФ,
	|	ЕСТЬNULL(ПринятиеКУчетуОС.КоличествоСтрок, 0) КАК КоличествоСтрок
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК ДокументПринятиеКУчетуОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПринятиеКУчетуОС_КоличествоОС КАК ПринятиеКУчетуОС
	|		ПО ДокументПринятиеКУчетуОС.Ссылка = ПринятиеКУчетуОС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МаксПериодыСведенийОСБУ КАК ВТ_МаксПериодыСведенийОСБУ
	|		ПО ДокументПринятиеКУчетуОС.Ссылка = ВТ_МаксПериодыСведенийОСБУ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК Сведения
	|		ПО (ВТ_МаксПериодыСведенийОСБУ.ОсновноеСредство = Сведения.ОсновноеСредство)
	|			И (ВТ_МаксПериодыСведенийОСБУ.Организация = Сведения.Организация)
	|			И (ВТ_МаксПериодыСведенийОСБУ.МаксПериод = Сведения.Период)
	|ГДЕ
	|	ДокументПринятиеКУчетуОС.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки";

	ВыборкаПоОС = Запрос.Выполнить().Выбрать();

	ПервыйДокумент = Истина;

	Пока ВыборкаПоОС.СледующийПоЗначениюПоля("Ссылка") Цикл

		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		Если ВыборкаПоОС.КоличествоСтрок <= 1 Тогда

			ВыборкаПоОС.Следующий();

			Если НЕ(ВыборкаПоОС.ГруппаОС = Перечисления.ГруппыОС.Здания ИЛИ
				ВыборкаПоОС.ГруппаОС = Перечисления.ГруппыОС.Сооружения) Тогда
				
				Если ИмяМакета <> "ОС1" Тогда
					Продолжить;
				КонецЕсли;
				
				ОбластьМакетаЗаголовок.Параметры.Заполнить(ВыборкаПоОС);
				
				ОбластьМакетаЗаголовок.Параметры.НомерАкта = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоОС.НомерАкта, Истина, Ложь);
				
				Если ПустаяСтрока(ВыборкаПоОС.НаименованиеОС) Тогда
					ОбластьМакетаЗаголовок.Параметры.НаименованиеОС = СокрЛП(ВыборкаПоОС.ОсновноеСредство);
				КонецЕсли;

				ЗаполнитьДанныеОрганизацииПолучателя(ВыборкаПоОС, ОбластьМакетаЗаголовок);
				
				ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаПоОС.Организация,ВыборкаПоОС.МОЛБУ,ВыборкаПоОС.Дата);
				ОбластьМакетаЗаголовок.Параметры.МОЛДолжность = ДанныеФизЛица.Должность;
				ОбластьМакетаЗаголовок.Параметры.МОЛРасшифровкаПодписи = ДанныеФизЛица.Представление;

				ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовок);

			Иначе
				
				Если ИмяМакета <> "ОС1а" Тогда
					Продолжить;
				КонецЕсли;
				
				ОбластьМакетаЗаголовок_а.Параметры.Заполнить(ВыборкаПоОС);
				
				ОбластьМакетаЗаголовок_а.Параметры.НомерАкта = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоОС.НомерАкта, Истина, Ложь);
				
				Если ПустаяСтрока(ВыборкаПоОС.НаименованиеОС) Тогда
					ОбластьМакетаЗаголовок_а.Параметры.НаименованиеОС = СокрЛП(ВыборкаПоОС.ОсновноеСредство);
				КонецЕсли;

				ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаПоОС.Организация,ВыборкаПоОС.МОЛБУ,ВыборкаПоОС.Дата);
				ОбластьМакетаЗаголовок_а.Параметры.МОЛДолжность = ДанныеФизЛица.Должность;
				ОбластьМакетаЗаголовок_а.Параметры.МОЛРасшифровкаПодписи = ДанныеФизЛица.Представление;
				
				ЗаполнитьДанныеОрганизацииПолучателя(ВыборкаПоОС, ОбластьМакетаЗаголовок_а);
				ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовок_а);

			КонецЕсли;

		Иначе // Объектов много - групповая печать.

			Если ИмяМакета <> "ОС1б" Тогда
				Продолжить;
			КонецЕсли;
			
			ОС1б_Страница1 = Новый ТабличныйДокумент();
			ОС1б_Страница2 = Новый ТабличныйДокумент();
			ОС1б_Страница3 = Новый ТабличныйДокумент();
			ОС1б_Страница4 = Новый ТабличныйДокумент();

			Шапка1ОС1б.Параметры.Заполнить(ВыборкаПоОС);
			Шапка1ОС1б.Параметры.НомерАкта = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоОС.НомерАкта, Истина, Ложь);
			ЗаполнитьДанныеОрганизацииПолучателя(ВыборкаПоОС, Шапка1ОС1б);
			ОС1б_Страница1.Вывести(Шапка1ОС1б);

			Шапка2ОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница2.Вывести(Шапка2ОС1б);

			Шапка3ОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница3.Вывести(Шапка3ОС1б);

			Шапка4ОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаПоОС.Организация,ВыборкаПоОС.МОЛБУ,ВыборкаПоОС.Дата);
			Шапка4ОС1б.Параметры.МОЛДолжность = ДанныеФизЛица.Должность;
			Шапка4ОС1б.Параметры.МОЛРасшифровкаПодписи = ДанныеФизЛица.Представление;
			ЗаполнитьДанныеОрганизацииПолучателя(ВыборкаПоОС, Шапка4ОС1б);
			ОС1б_Страница4.Вывести(Шапка4ОС1б);

			НомПП            = 0;
			ИтогНачСтоимость = 0;

			Пока ВыборкаПоОС.Следующий() Цикл

				ИтогНачСтоимость = ИтогНачСтоимость + ВыборкаПоОС.НачСтоимость;

				НомПП = НомПП + 1;
				Строка2ОС1б.Параметры.Нс = НомПП;

				Строка2ОС1б.Параметры.Заполнить(ВыборкаПоОС);
				Если ПустаяСтрока(ВыборкаПоОС.НаименованиеОС) Тогда
					Строка2ОС1б.Параметры.НаименованиеОС = СокрЛП(ВыборкаПоОС.ОсновноеСредство);
				КонецЕсли;
				ОС1б_Страница2.Вывести(Строка2ОС1б);

				Строка3ОС1б.Параметры.Заполнить(ВыборкаПоОС);
				ОС1б_Страница3.Вывести(Строка3ОС1б);

			КонецЦикла;

			Строка2ПОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница2.Вывести(Строка2ПОС1б);

			Подвал2.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница2.Вывести(Подвал2);

			Строка3ПОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница3.Вывести(Строка3ПОС1б);

			Подвал3.Параметры.Заполнить(ВыборкаПоОС);
			Подвал3.Параметры.ИтогНачСтоимость = ИтогНачСтоимость;
			ОС1б_Страница3.Вывести(Подвал3);

			ТабличныйДокумент.Вывести(ОС1б_Страница1);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабличныйДокумент.Вывести(ОС1б_Страница2);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабличныйДокумент.Вывести(ОС1б_Страница3);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабличныйДокумент.Вывести(ОС1б_Страница4);
		КонецЕсли;

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоОС.Ссылка);

	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

// Список операций, при которых возможно применение инвестиционного вычета.
// 
// Возвращаемое значение:
//  СписокЗначений из ПеречислениеСсылка.ВидыОперацийПринятияКУчетуОсновныхСредств
//
Функция СписокОперацийПрименяетсяИнвестиционныйВычет()
	
	СписокОпераций = Новый СписокЗначений;
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПринятияКУчетуОсновныхСредств.Оборудование);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПринятияКУчетуОсновныхСредств.ОбъектыСтроительства);
	Возврат СписокОпераций;
	
КонецФункции

#КонецОбласти

#КонецЕсли