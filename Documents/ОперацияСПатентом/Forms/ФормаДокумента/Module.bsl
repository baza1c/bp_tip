
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьСведенияНовогоПатента();
		ПодготовитьФормуНаСервере();
		ПоказатьПредупреждение = Истина;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.ОперацияСПатентом",
		"ФормаДокумента",
		НСтр("ru='Новости: Патент'"),
		ИдентификаторыСобытийПриОткрытии);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НалогПоПатенту > 0 И Не ЗначениеЗаполнено(Объект.КБК) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'КБК'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Объект.КБК", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ТекущийОбъект.ПометкаУдаления
		И Прав(ТекущийОбъект.КБК, 3) = "110" Тогда
		
		КатегорияПодчинения = Сред(ТекущийОбъект.КБК, 9, 3);
		КатегорииПодчинения = УсловияПримененияТребованийЗаконодательства.КатегорииПодчиненияПатентовПоВидамНалогов();
		Если КатегорииПодчинения.Найти(КатегорияПодчинения) = Неопределено Тогда
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ПоявиласьНоваяКатегорияПодчиненияПоВидуНалога", Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	РезультатВыполнения = КалендарьБухгалтера.ЗапуститьЗаполнениеВФоне(
		УникальныйИдентификатор, ТекущийОбъект.Организация, Ложь, Истина);
	ПараметрыЗаписи.Вставить("РезультатВыполненияЗаданияКалендаряБухгалтера", РезультатВыполнения);
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	СтруктураПараметров = Новый Структура("Организация, Ссылка");
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, Объект);
	
	Если ПараметрыЗаписи.Свойство("РезультатВыполненияЗаданияКалендаряБухгалтера") Тогда
		КалендарьБухгалтераКлиент.ОжидатьЗавершениеЗаполненияВФоне(
			ПараметрыЗаписи.РезультатВыполненияЗаданияКалендаряБухгалтера);
	КонецЕсли;
	
	Оповестить("ИзменениеПатента", СтруктураПараметров, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЗаявлениеНаПолучениеПатента" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОбработатьНажатиеНаЗаявлениеНаПолучениеПатента();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОчиститьЗаявлениеНаПолучениеПатента" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Объект.ЗаявлениеНаПолучениеПатента = Неопределено;
		ФайлПатента = Неопределено;
		
		Модифицированность = Истина;
		
		УстановитьПредставлениеЗаявленияНаПолучениеПатента();
		УстановитьПредставлениеПатента();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьФайлПатента" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОбработатьНажатиеНаПатент();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "УдалитьФайлПатента" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОбработатьОчисткуПатента();
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.ДатаОкончания)
		Или Объект.ДатаНачала > Объект.ДатаОкончания
		Или Год(Объект.ДатаОкончания) <> Год(Объект.ДатаНачала) Тогда
		
		Объект.ДатаОкончания = КонецГода(Объект.ДатаНачала);
		
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
	ОбновитьРасчетНалога();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачала)
		Или Объект.ДатаНачала > Объект.ДатаОкончания
		Или Год(Объект.ДатаОкончания) <> Год(Объект.ДатаНачала) Тогда
		
		Объект.ДатаНачала = НачалоГода(Объект.ДатаОкончания);
		
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
	ОбновитьРасчетНалога();
	
КонецПроцедуры

&НаКлиенте
Процедура ПотенциальноВозможныйГодовойДоходПриИзменении(Элемент)
	
	ОбновитьРасчетНалога();
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяСтавкаПриИзменении(Элемент)
	
	ОбновитьРасчетНалога();
	
КонецПроцедуры

&НаКлиенте
Процедура ПостановкаНаУчетВНалоговомОрганеПриИзменении(Элемент)
	
	УстановитьДоступностьНалоговогоОргана(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганПриИзменении(Элемент)
	
	НалоговыйОрганПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КБКНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыборКода("КБК", "КБК");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыФормы = Новый Структура("НачалоПериода, КонецПериода", Объект.ДатаНачала, Объект.ДатаОкончания);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыФормы, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДатаНачала    = РезультатВыбора.НачалоПериода;
	Объект.ДатаОкончания = РезультатВыбора.КонецПериода;
	
	ПериодПриИзмененииНаСервере();
	
	ОбновитьРасчетНалога();
	
КонецПроцедуры

&НаКлиенте
Процедура Оплатить(Команда)
	
	Если Не Объект.ПометкаУдаления И Не Объект.Проведен Или Модифицированность Тогда
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да,     "Сохранить");
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
	
		Оповещение = Новый ОписаниеОповещения("ВопросПередОплатойСледуетСохранитьЗавершение", ЭтотОбъект);
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Перед оплатой необходимо сохранить изменения'"),
			Кнопки, ,
			КодВозвратаДиалога.Да);
	Иначе
		ОплатитьПатент();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередОплатойСледуетСохранитьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроведен = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Если ДокументПроведен Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось сохранить изменения'"));
		Возврат;
	КонецЕсли;
	
	ОплатитьПатент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкаФормы

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Элементы.Оплатить.Видимость = ПравоДоступа("Использование", Метаданные.Обработки.ПомощникОплатыПатента);
	
	ФайлПатента = УчетПСН.ПрисоединенныйФайлПатента(Объект.ЗаявлениеНаПолучениеПатента);
	
	УстановитьСостояниеДокумента();
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьПредставлениеЗаявленияНаПолучениеПатента();
	
	УстановитьПредставлениеПатента();
	
	УстановитьДоступностьНалоговогоОргана(ЭтотОбъект);
	
	ПересчитатьСуммуНалога();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрОрганизацияФункциональныхОпцийФормы(
		ЭтотОбъект, Объект.Организация, Объект.Дата);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	УстановитьПредставлениеКоличестваДнейДействия(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеОплаты()
	
	ШаблонОплаты = НСтр("ru = 'Оплатить <b>%1 руб.</b> не позднее %2'");
	ШаблонПодсказкиОплаты = НСтр("ru = 'Сумма налога указана в строке 040 на оборотной стороне патента.
                                  |%1'");
	
	ДополнительнаяПодсказкаСрокаОплаты = "";
	Если Объект.ДатаНачала >= УчетПСНКлиентСервер.ДатаНачалаПримененияИзмененийСрокаУплатыНалога() Тогда
		ДополнительнаяПодсказкаСрокаОплаты =
			НСтр("ru = ', при этом если конец действия патента приходится на 31 декабря, то оплата производится не позднее 28 декабря'");
	КонецЕсли;
	
	ВыводитьПервыйПлатеж = Ложь;
	ВыводитьВторойПлатеж = Ложь;
	
	ОдинПлатеж = УчетПСНКлиентСервер.УплачиваетсяОдинПлатеж(Объект.ДатаНачала, Объект.ДатаОкончания);
	
	Если Объект.СуммаПервогоПлатежа > 0 И ЗначениеЗаполнено(Объект.ДатаПервогоПлатежа) Тогда
		ВыводитьПервыйПлатеж = Истина;
		Элементы.НадписьОплатаПервыйПлатеж.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			СтрШаблон(ШаблонОплаты, Объект.СуммаПервогоПлатежа, Формат(Объект.ДатаПервогоПлатежа, "ДЛФ=D")));
	КонецЕсли;
	
	Если Объект.СуммаВторогоПлатежа > 0 И ЗначениеЗаполнено(Объект.ДатаВторогоПлатежа) Тогда
		ВыводитьВторойПлатеж = Истина;
		Элементы.НадписьОплатаВторойПлатеж.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			СтрШаблон(ШаблонОплаты, Объект.СуммаВторогоПлатежа, Формат(Объект.ДатаВторогоПлатежа, "ДЛФ=D")));
	КонецЕсли;
	
	Если ОдинПлатеж Тогда
		ТекстПодсказкиОплаты = СтрШаблон(
			НСтр("ru = 'Патент сроком менее 6 месяцев, нужно оплатить до конца действия патента%1'"),
			ДополнительнаяПодсказкаСрокаОплаты);
	Иначе
		ТекстПодсказкиОплаты = СтрШаблон(
			НСтр("ru = 'Патент сроком от 6 до 12 месяцев, нужно оплатить:
                  |1/3 суммы - не позднее 90 дней после начала действия патента
                  |2/3 суммы - до конца действия патента%1'"),
			ДополнительнаяПодсказкаСрокаОплаты);
	КонецЕсли;
	
	Элементы.НалогПоПатентуРасширеннаяПодсказка.Заголовок = СтрШаблон(ШаблонПодсказкиОплаты, ТекстПодсказкиОплаты);
	
	Элементы.НадписьОплатаПервыйПлатеж.Видимость = ВыводитьПервыйПлатеж;
	Элементы.НадписьОплатаВторойПлатеж.Видимость = ВыводитьВторойПлатеж;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьНалоговогоОргана(Форма)
	
	Объект = Форма.Объект;
	
	ПостановкаНаУчетВНалоговомОргане = Объект.ПостановкаНаУчетВНалоговомОргане;
	
	ПостановкаВДругомНалоговомОргане = 
		ПостановкаНаУчетВНалоговомОргане = ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане");
	
	Форма.Элементы.ГруппаРеквизиты.Видимость = ПостановкаВДругомНалоговомОргане;
	Если Не ПостановкаВДругомНалоговомОргане Тогда
		Объект.КодПоОКТМО = "";
		Объект.НалоговыйОрган = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредставлениеКоличестваДнейДействия(Форма)
	
	Объект = Форма.Объект;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачала)
		Или Не ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		Форма.Элементы.НадписьКоличествоДнейДействия.Видимость = Ложь;
	Иначе
		Форма.Элементы.НадписьКоличествоДнейДействия.Видимость = Истина;
		
		КоличествоДнейДействия = УчетПСНКлиентСервер.КоличествоДней(Объект.ДатаНачала, Объект.ДатаОкончания);
		
		Форма.Элементы.НадписьКоличествоДнейДействия.Заголовок = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';%1 день;;%1 дня;%1 дней;%1 дня'"), КоличествоДнейДействия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеЗаявленияНаПолучениеПатента()
	
	Если ЗначениеЗаполнено(Объект.ЗаявлениеНаПолучениеПатента) Тогда
		
		ДанныеПредставления = ДанныеПредставленияЗаявления(Объект.ЗаявлениеНаПолучениеПатента);
		
		ПредставлениеЗаявления = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<a href=""ОткрытьЗаявлениеНаПолучениеПатента"">Заявление на получение патента от %1 (%2)</a> <a href=""ОчиститьЗаявлениеНаПолучениеПатента""><img src=""ОчиститьЗначениеМини""></a>'"),
			Формат(ДанныеПредставления.Дата, "ДЛФ=D"), ДанныеПредставления.СтатусЗаявления.Представление);
		
	Иначе
		
		ПредставлениеЗаявления = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<a href=""ОткрытьЗаявлениеНаПолучениеПатента"">%1</a>'"),
			ПредставлениеПустогоПоляЗаявленияИПатента());
		
	КонецЕсли;
	
	Элементы.ПредставлениеЗаявление.Заголовок = ПредставлениеЗаявления;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеПатента()
	
	Если ЗначениеЗаполнено(ФайлПатента) Тогда
		
		ПредставлениеФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлПатента, "Наименование");
		
		ПредставлениеПатента = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<a href=""ОткрытьФайлПатента"">%1</a> <a href=""УдалитьФайлПатента""><img src=""ОчиститьЗначениеМини""></a>'"),
			ПредставлениеФайла);
			
	Иначе
		
		ПредставлениеПатента = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<a href=""ОткрытьФайлПатента"">%1</a>'"),
			ПредставлениеПустогоПоляЗаявленияИПатента());
		
	КонецЕсли;
	
	Элементы.ПредставлениеПатента.Заголовок = ПредставлениеПатента;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеПредставленияЗаявления(Заявление)
	
	ДанныеПредставления = НовыеДанныеПредставленияЗаявления();
	
	ДатаПодписи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заявление, "ДатаПодписи");
	СтатусыЗаявления = ИнтерфейсыВзаимодействияБРО.СостояниеДокумента(Заявление);
	
	ДанныеПредставления.Дата = ДатаПодписи;
	ДанныеПредставления.СтатусЗаявления = СтатусыЗаявления;
	
	Возврат ДанныеПредставления;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыеДанныеПредставленияЗаявления()
	
	ДанныеПредставления = Новый Структура;
	ДанныеПредставления.Вставить("Дата", Дата(1, 1, 1));
	ДанныеПредставления.Вставить("СтатусЗаявления", Новый Структура);
	
	Возврат ДанныеПредставления;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеПустогоПоляЗаявленияИПатента()
	
	Возврат НСтр("ru = 'Выбрать'");
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура ЗаполнитьСведенияНовогоПатента()
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Объект.Организация = Параметры.Организация;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Патент) Тогда
		Объект.Патент = Параметры.Патент;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.РегистрацияВНалоговомОргане) Тогда
		
		РегистрацияВНалоговомОрганеИП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.Организация, "РегистрацияВНалоговомОргане");
		
		Если РегистрацияВНалоговомОрганеИП <> Параметры.РегистрацияВНалоговомОргане Тогда
			Объект.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане;
			Объект.НалоговыйОрган = Параметры.РегистрацияВНалоговомОргане;
			Объект.КодПоОКТМО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Параметры.РегистрацияВНалоговомОргане, "КодПоОКТМО");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ЗаявлениеНаПолучениеПатента) Тогда
		
		ЗаявлениеНаПолучениеПатента = Параметры.ЗаявлениеНаПолучениеПатента;
		
		Объект.ЗаявлениеНаПолучениеПатента = ЗаявлениеНаПолучениеПатента;
		
		РеквизитыУведомления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ЗаявлениеНаПолучениеПатента, "ДанныеУведомления, ИмяФормы");
		ДанныеУведомления = РеквизитыУведомления.ДанныеУведомления;
		ДанныеЗаявления = ДанныеУведомления.Получить();
		
		Если ДанныеЗаявления <> Неопределено Тогда
			
			ИмяТитульногоЛиста = СтрШаблон("%1_Титульная", РеквизитыУведомления.ИмяФормы);
			
			ДанныеТитульногоЛиста = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ДанныеЗаявления.ДанныеУведомления, ИмяТитульногоЛиста);
			
			Объект.ДатаНачала = ДанныеТитульногоЛиста.ДатаНачПат;
			Объект.ДатаОкончания = ДанныеТитульногоЛиста.ДатаКонПат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКода(ИмяКода, НазваниеМакета)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",      "Справочник");
	ПараметрыФормы.Вставить("НазваниеОбъекта", "Патенты");
	ПараметрыФормы.Вставить("НазваниеМакета",  НазваниеМакета);
	ПараметрыФормы.Вставить("ТекущийПериод",   Объект.ДатаНачала);
	ПараметрыФормы.Вставить("ТекущийКод",      Объект[ИмяКода]);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяКода", ИмяКода);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборКодаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы, , , , , ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКодаЗавершение(ВыбранныйКод, ДополнительныеПараметры) Экспорт
	
	ИмяКода = ДополнительныеПараметры.ИмяКода;
	
	Если ВыбранныйКод <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		Объект[ИмяКода] = ВыбранныйКод;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПериодПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	ПоказатьПредупреждение = Истина;
	ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура НалоговыйОрганПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.НалоговыйОрган) Тогда
		Объект.КодПоОКТМО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.НалоговыйОрган, "КодПоОКТМО");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРасчетНалога()
	
	НалогПоПатенту = УчетПСНКлиентСервер.НалогПоПатенту(
		Объект.ПотенциальноВозможныйГодовойДоход,
		Объект.ДатаНачала,
		Объект.ДатаОкончания,
		Объект.НалоговаяСтавка);
	
	ПересчитатьСуммыПлатежей();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСуммыПлатежей()
	
	РасчетПлатежей = УчетПСНКлиентСервер.РасчетПлатежейПоПатенту(НалогПоПатенту, Объект.ДатаНачала, Объект.ДатаОкончания);
	ЗаполнитьЗначенияСвойств(Объект, РасчетПлатежей);
	
	УстановитьПредставлениеОплаты();
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСуммуНалога()
	
	НалогПоПатенту = Объект.СуммаПервогоПлатежа + Объект.СуммаВторогоПлатежа;
	
	УстановитьПредставлениеОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьПатент()
	
	ДанныеПатента = ДанныеПатента(Объект.Ссылка);
	
	ОписаниеПараметровОплаты = ОписаниеОплатыПатента(ДанныеПатента);
	
	ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеПараметровОплаты);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеПатента(ДокументПатент)
	
	Возврат Документы.ОперацияСПатентом.ДанныеПатентаДляОплаты(ДокументПатент);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеОплатыПатента(ДанныеПатента)
	
	Возврат Документы.ОперацияСПатентом.ОписаниеОплатыПатента(ДанныеПатента);
	
КонецФункции

&НаКлиенте
Процедура ОбработатьНажатиеНаЗаявлениеНаПолучениеПатента()
	
	Если ЗначениеЗаполнено(Объект.ЗаявлениеНаПолучениеПатента) Тогда
		ОткрытьЗаявлениеНаПолучениеПатента();
	Иначе
		ОткрытьВыборЗаявленияНаПолучениеПатента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗаявлениеНаПолучениеПатента()
	
	РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФормуУведомленияОСпецрежимахПоСсылке(
		ЭтотОбъект, Объект.ЗаявлениеНаПолучениеПатента);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВыборЗаявленияНаПолучениеПатента()
	
	ПараметрыВыбораЗаявления = Новый Структура("Организация", Объект.Организация);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборЗаявленияЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.ПомощникЗаполненияЗаявленияНаПолучениеПатента.Форма.ВыборЗаявленияНаПолучениеПатента",
		ПараметрыВыбораЗаявления, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЗаявленияЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора) Тогда
		
		Объект.ЗаявлениеНаПолучениеПатента = РезультатВыбора;
		УстановитьПредставлениеЗаявленияНаПолучениеПатента();
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеНаПатент()
	
	Если ЗначениеЗаполнено(ФайлПатента) Тогда
		ОткрытьФайлПатента();
	Иначе
		ОткрытьВыборФайлаПатента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлПатента()
	
	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляОткрытия(
		ФайлПатента, Неопределено, УникальныйИдентификатор);
	
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВыборФайлаПатента()
	
	Если Не ЗначениеЗаполнено(Объект.ЗаявлениеНаПолучениеПатента) Тогда
		ТекстСообщения = НСтр("ru = 'Сначала необходимо выбрать заявление на получение патента'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПатентПредставление", "Объект");
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаПатентаЗавершение", ЭтотОбъект);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите файл патента'");
	ПараметрыЗагрузки.Диалог.Фильтр = НСтр("ru = 'Патент (*.pdf)|*.pdf|Все файлы (*.*)|*.*'");
	ПараметрыЗагрузки.Диалог.МножественныйВыбор = Ложь;
	
	ФайловаяСистемаКлиент.ЗагрузитьФайл(ОписаниеОповещения, ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаПатентаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(РезультатВыбора) Тогда
		Возврат;
	КонецЕсли;
	
	ПрикрепитьФайлПатента(РезультатВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ПрикрепитьФайлПатента(ПомещенныйФайл)
	
	СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПомещенныйФайл.Имя);
	
	ФайлПатента = УчетПСН.ПрикрепитьФайлПатентаКЗаявлениюНаПолучениеПатента(
		Объект.ЗаявлениеНаПолучениеПатента, ПомещенныйФайл.Хранение,
		СтруктураИмениФайла.ИмяБезРасширения, СтруктураИмениФайла.Расширение);
	
	УстановитьПредставлениеПатента();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьОчисткуПатента()
	
	ПометитьНаУдалениеПрикрепленныйФайлПатента(ФайлПатента);
	ФайлПатента = Неопределено;
	УстановитьПредставлениеПатента();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПометитьНаУдалениеПрикрепленныйФайлПатента(ПрикрепленныйФайл)
	
	Если Не ЗначениеЗаполнено(ПрикрепленныйФайл) Тогда
		Возврат;
	КонецЕсли;
	
	ПрикрепленныйФайлОбъект = ПрикрепленныйФайл.ПолучитьОбъект();
	ПрикрепленныйФайлОбъект.УстановитьПометкуУдаления(Истина);
	
КонецПроцедуры

#Область ПравоПримененияСпецрежима

&НаКлиенте
Процедура ПоказатьИнформациюОПравеПримененияСпецрежима()
	
	Если Не ЗначениеЗаполнено(Объект.ДатаОкончания)
		Или Не ПоказатьПредупреждение
		Или Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьИнформациюОПравеПримененияСпецрежимаНаСервере()
	
	ИнформацияОПравеПримененияПатента = КонтрольПраваПримененияСпецрежима.ИнформацияОПравеПримененияПатента(
		Объект.Организация,
		Объект.ДатаОкончания);
	
	СсылкаНаПояснение = ИнформацияОПравеПримененияПатента.СсылкаНаПояснение;
	
	ЧастиСтрокиИнформации = Новый Массив;
	ЧастиСтрокиИнформации.Добавить(ИнформацияОПравеПримененияПатента.ТекстИнформации);
	Если ИнформацияОПравеПримененияПатента.УтраченоПравоПрименения Тогда
		ДополнительныйТекст = НСтр("ru = '. Об утрате права на ПСН необходимо сообщить в ФНС.'");
		ЧастиСтрокиИнформации.Добавить(ДополнительныйТекст);
	КонецЕсли;
	ЧастиСтрокиИнформации.Добавить(" ");
	
	Если ЗначениеЗаполнено(ИнформацияОПравеПримененияПатента.СсылкаНаПояснение) Тогда
		СсылкаПодробнее = Новый ФорматированнаяСтрока(Нстр("ru = 'Подробнее'"), , , , СсылкаНаПояснение);
		ЧастиСтрокиИнформации.Добавить(СсылкаПодробнее);
	КонецЕсли;
	
	ТекстИнформации = Новый ФорматированнаяСтрока(ЧастиСтрокиИнформации);
	
	Элементы.ИнформацияОПравеПримененияСпецрежима.Видимость = ИнформацияОПравеПримененияПатента.Показать;
	Элементы.ИнформацияОПравеПримененияСпецрежима.ЦветФона = ИнформацияОПравеПримененияПатента.ЦветФонаГруппы;
	Элементы.ТекстИнформации.Заголовок = ТекстИнформации;
	
	Элементы.ТекстИнформацииРасширеннаяПодсказка.Заголовок = ИнформацияОПравеПримененияПатента.ПодсказкаТекстИнформации;
	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды 

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(
		ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВМККомандыФормы(Элементы, Элементы.Оплатить);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Организация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ВидДеятельностиПоПатенту);
	
	Элементы.НомерПатента.РастягиватьПоГоризонтали = Истина;
	Элементы.НомерПатента.Ширина = 0;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.НомерПатента);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ДатаВыдачи);
	
	Элементы.ГруппаПериодДействия.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаПериодДействия);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.НадписьКоличествоДнейДействия);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ВыбратьПериод);
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ПотенциальноВозможныйГодовойДоход);
	
	Элементы.НалогПоПатенту.РастягиватьПоГоризонтали = Истина;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаНалоговаяСтавка);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.КБК);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаОплата);
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаНалоговыйОрган);
	
	Элементы.ГруппаОтветственныйКомментарий.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПодвал(Элементы,
		Элементы.ГруппаОтветственныйКомментарий);
		
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

