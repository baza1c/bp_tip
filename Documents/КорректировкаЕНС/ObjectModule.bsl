#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	НачалоПростогоУчета = ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	Если Дата < НачалоПростогоУчета Тогда
		 Дата = НачалоПростогоУчета;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	НепроверяемыеРеквизиты.Добавить("Операции.Налог");
	НепроверяемыеРеквизиты.Добавить("Операции.ВидПлатежаВГосБюджет");
	НепроверяемыеРеквизиты.Добавить("Операции.Контрагент");
	
	Для каждого СтрокаОперации Из Операции Цикл
		
		Префикс = "Операции[%1].";
		Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Префикс, Формат(СтрокаОперации.НомерСтроки - 1, "ЧН=0; ЧГ="));
		
		ИмяСписка = НСтр("ru = 'Операции'");
		
		
		Если СтрокаОперации.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога
			Или СтрокаОперации.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ВозмещениеНалога Тогда
			Если Не ЗначениеЗаполнено(СтрокаОперации.Налог) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Налог'"),
					СтрокаОперации.НомерСтроки, ИмяСписка);
				Поле = Префикс + "Налог";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаОперации.ВидПлатежаВГосБюджет) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Вид платежа в гос бюджет'"),
					СтрокаОперации.НомерСтроки, ИмяСписка);
				Поле = Префикс + "ВидПлатежаВГосБюджет";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаОперации.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПереводСредствТретьемуЛицу
			Или УдалитьВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеСредствОтТретьегоЛица Тогда
			Если Не ЗначениеЗаполнено(СтрокаОперации.Контрагент) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Контрагент'"),
					СтрокаОперации.НомерСтроки, ИмяСписка);
				Поле = Префикс + "Контрагент";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	УвеличениеСальдо = 0;
	УменьшениеСальдо = 0;
	ВидыОперацийУвеличениеСальдоЕНС = Новый ФиксированныйМассив(Документы.КорректировкаЕНС.ВидыОперацийУвеличениеСальдоЕНС());
	
	Для Каждого Операция Из Операции Цикл
		
		СвойстваСчета = Документы.КорректировкаЕНС.СвойстваСчета(Операция.СчетУчета);
		
		Если Операция.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога 
			Или Операция.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ВозмещениеНалога Тогда
			Если Не СвойстваСчета.ЕстьУровниБюджетов Тогда
					Операция.УровеньБюджета = Неопределено;
			КонецЕсли;
			Если Не СвойстваСчета.ЕстьРегистрацияВНалоговомОргане Тогда
				Операция.РегистрацияВНалоговомОргане = Неопределено;
			КонецЕсли;
		Иначе
			Операция.Налог = Неопределено;
			Операция.ВидПлатежаВГосБюджет = Неопределено;
			Операция.УровеньБюджета = Неопределено;
			Операция.РегистрацияВНалоговомОргане = Неопределено;
			КонецЕсли;
		
		Если Не (Операция.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеСредствОтТретьегоЛица 
			Или Операция.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПереводСредствТретьемуЛицу) Тогда
			Операция.Контрагент = Неопределено;
		КонецЕсли;
		
		Если Не (Операция.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПрочееУвеличениеСальдо
			Или Операция.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПрочееУменьшениеСальдо) Тогда
			Операция.Субконто1 = Неопределено;
			Операция.Субконто2 = Неопределено;
			Операция.Субконто3 = Неопределено;
		КонецЕсли;
		
		ЭтоУвеличениеСальдоЕНС = ВидыОперацийУвеличениеСальдоЕНС.Найти(Операция.ВидОперации) <> Неопределено;
		Если ЭтоУвеличениеСальдоЕНС Тогда
			УвеличениеСальдо = УвеличениеСальдо + Операция.Сумма;
		Иначе
			УменьшениеСальдо = УменьшениеСальдо + Операция.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПараметрыПроведения = Документы.КорректировкаЕНС.ПодготовитьПараметрыПроведения(
		Ссылка,
		Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	Документы.КорректировкаЕНС.СформироватьПроводки(ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	НачалоПростогоУчета = ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	Если Дата < НачалоПростогоУчета Тогда
		 Дата = НачалоПростогоУчета;
	КонецЕсли;
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли