#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначенияЗаполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЗначенияЗаполнения", Новый Структура);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияЗаполнения);
	
	Если ЭтоПрочее Тогда
		БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
			ЭтотОбъект, ЭтотОбъект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И (Модифицированность ИЛИ ПеренестиВДокумент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
	Если ПеренестиВДокумент И НЕ Отказ Тогда
		Отказ = НЕ ПроверитьЗаполнениеНаКлиенте();
	КонецЕсли;
	
	Если Отказ Тогда
		ПеренестиВДокумент = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("СчетУчета");
	СтруктураРезультат.Вставить("ВидПлатежаВГосБюджет");
	СтруктураРезультат.Вставить("Контрагент");
	СтруктураРезультат.Вставить("Субконто1");
	СтруктураРезультат.Вставить("Субконто2");
	СтруктураРезультат.Вставить("Субконто3");
	СтруктураРезультат.Вставить("Подразделение");
	СтруктураРезультат.Вставить("УровеньБюджета");
	СтруктураРезультат.Вставить("РегистрацияВНалоговомОргане");
	СтруктураРезультат.Вставить("ЕстьВидыПлатежейВГосБюджет");
	СтруктураРезультат.Вставить("ЕстьУчетПоПодразделениям");
	СтруктураРезультат.Вставить("ЕстьУровниБюджетов");
	СтруктураРезультат.Вставить("ЕстьРегистрацияВНалоговомОргане");
	
	ЗаполнитьЗначенияСвойств(СтруктураРезультат, ЭтотОбъект);
	
	ОповеститьОВыборе(СтруктураРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийШапкиФормы

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	
	СвойстваСчетаУчета = СвойстваСчета(СчетУчета);
	Если ЭтоПрочее Тогда
		КоличествоСубконто = СвойстваСчетаУчета.КоличествоСубконто;
		БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
			ЭтотОбъект, ЭтотОбъект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	Иначе
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СвойстваСчетаУчета);
		Если ЕстьРегистрацияВНалоговомОргане
			И Не ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
			РегистрацияВНалоговомОргане = ОсновнаяРегистрацияВНалоговомОргане;
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто1ПриИзменении(Элемент)
	ПриИзмененииСубконто(1);
КонецПроцедуры

&НаКлиенте
Процедура Субконто1НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто2ПриИзменении(Элемент)
	ПриИзмененииСубконто(2);
КонецПроцедуры

&НаКлиенте
Процедура Субконто2НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Субконто3ПриИзменении(Элемент)
	ПриИзмененииСубконто(3);
КонецПроцедуры

&НаКлиенте
Процедура Субконто3НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.Контрагент.Видимость                  = Форма.ЭтоТретьиЛица;
	Элементы.СчетУчета.Видимость                   = Форма.ПользовательУправляетСчетамиУчета Или Форма.ЭтоПрочее;
	Элементы.ГруппаПрочее.Видимость                = Форма.ЭтоПрочее;
	Элементы.ВидПлатежаВГосБюджет.Видимость        = Форма.ЕстьВидыПлатежейВГосБюджет;
	Элементы.УровеньБюджета.Видимость              = Форма.ЕстьУровниБюджетов;
	Элементы.РегистрацияВНалоговомОргане.Видимость = Форма.ЕстьРегистрацияВНалоговомОргане;
	Элементы.Подразделение.Видимость               = Форма.ЕстьУчетПоПодразделениям;
	
	Если Форма.ЭтоНалог Тогда
		Элементы.СчетУчета.Заголовок     = "Счет налога";
		Элементы.Подразделение.Заголовок = "Подразделение";
	ИначеЕсли Форма.ЭтоРасходы Тогда
		Элементы.СчетУчета.Заголовок     = "Счет расходов";
		Элементы.Подразделение.Заголовок = "Подразделение расходов";
	ИначеЕсли Форма.ЭтоДоходы Тогда
		Элементы.СчетУчета.Заголовок     = "Счет доходов";
		Элементы.Подразделение.Заголовок = "Подразделение доходов";
	ИначеЕсли Форма.ЭтоТретьиЛица Тогда
		Элементы.СчетУчета.Заголовок     = "Счет расчетов";
		Элементы.Подразделение.Заголовок = "Подразделение расчетов";
	Иначе
		Элементы.СчетУчета.Заголовок     = "Счет учета";
		Элементы.Подразделение.Заголовок = "Подразделение";
	КонецЕсли;
	
	Если Форма.ЭтоПрочее Тогда
		БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
			Форма, Форма, ПараметрыУстановкиСвойствСубконто(Форма));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНаКлиенте()
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(СчетУчета) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Счет учета'"));
		Поле = "СчетУчета";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

&НаСервереБезКонтекста
Функция СвойстваСчета(Знач Счет)
	
	Возврат Документы.КорректировкаЕНС.СвойстваСчета(Счет);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконто(Форма)
	
	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"Субконто", "", "Субконто", "", "СчетУчета");
	
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Организация);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииСубконто(НомерСубконто)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСубконто(
		ЭтотОбъект, ЭтотОбъект, НомерСубконто, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		ЭтотОбъект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(
		ЭтотОбъект, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

#КонецОбласти
