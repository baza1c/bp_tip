#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПростогоУчета = ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	#Область СтандартныеПодсистемы
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	ЗаполнитьДобавленныеКолонкиТаблицыОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Организация", Объект.Организация);
	ПараметрыОповещения.Вставить("Период",      Объект.Дата);
	
	Оповестить("Запись_КорректировкаЕНС", ПараметрыОповещения, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если Объект.Дата < НачалоПростогоУчета Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru='Документ используется, начиная с %1'"),
			Формат(НачалоПростогоУчета, "ДЛФ=DD"));
		ПоказатьПредупреждение(, ТекстОшибки);
		Объект.Дата = НачалоПростогоУчета;
	КонецЕсли;
	
	ОсновнаяРегистрацияВНалоговомОргане = ОсновнаяРегистрацияВНалоговомОргане(Объект.Организация, Объект.Дата);
	ЗаполнитьРегистрациюВНалоговомОргане(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОсновнаяРегистрацияВНалоговомОргане = ОсновнаяРегистрацияВНалоговомОргане(Объект.Организация, Объект.Дата);
	ЗаполнитьРегистрациюВНалоговомОргане(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыОперации

&НаКлиенте
Процедура ОперацииПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ОперацииСчетУчета" Тогда
		ДанныеСтроки = Элементы.Операции.ТекущиеДанные;
		ОткрытьФормуРедактированияСтроки(ДанныеСтроки);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Не НоваяСтрока Тогда
		Возврат;
	КонецЕсли;
	Если Копирование Тогда
		Элемент.ТекущиеДанные.ИдентификаторСтроки = "";
		Возврат;
	КонецЕсли; 
	
	Элемент.ТекущиеДанные.АналитикаУчета = "<...>";
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииВидОперацииПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Операции.ТекущиеДанные;
	ЗаполнитьСвойстваВидаОперации(ДанныеСтроки);
	
	Если Не ДанныеСтроки.ЭтоНалог Тогда
		ДанныеСтроки.Налог = Неопределено;
		ДанныеСтроки.СрокУплаты = '0001-01-01';
	КонецЕсли;
	
	ОбработатьИзменениеОперацииНалог(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииНалогПриИзменении(Элемент)
	
	ОбработатьИзменениеОперацииНалог(Элементы.Операции.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииСчетУчетаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуРедактированияСтроки(Элементы.Операции.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	ОсновнаяРегистрацияВНалоговомОргане = ОсновнаяРегистрацияВНалоговомОргане(Объект.Организация, Объект.Дата);
	
	ВидыОперацийУвеличениеСальдоЕНС = Новый ФиксированныйМассив(Документы.КорректировкаЕНС.ВидыОперацийУвеличениеСальдоЕНС());
	
	ЗаполнитьДобавленныеКолонкиТаблицыОперации();
	
	УстановитьСостояниеДокумента();
	
	ОбновитьИнформационнуюПанель();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеОперацииНалог(ДанныеСтроки)
	
	ДанныеСчета = ДанныеСчета(ДанныеСтроки.ВидОперации, ДанныеСтроки.Налог, Объект.Дата);
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ДанныеСчета);
	
	Если Не ДанныеСтроки.ЕстьВидыПлатежейВГосБюджет Тогда
		ДанныеСтроки.ВидПлатежаВГосБюджет = Неопределено;
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеСтроки.ВидПлатежаВГосБюджет) Тогда
		ДанныеСтроки.ВидПлатежаВГосБюджет = ПредопределенноеЗначение("Перечисление.ВидыПлатежейВГосБюджет.Налог");
	КонецЕсли;
	Если Не ДанныеСтроки.ЕстьУровниБюджетов Тогда
		ДанныеСтроки.УровеньБюджета = Неопределено;
	Иначе
		ДанныеСтроки.УровеньБюджета = ДанныеСчета.УровеньБюджета;
	КонецЕсли;
	Если Не ДанныеСтроки.ЕстьРегистрацияВНалоговомОргане Тогда
		ДанныеСтроки.РегистрацияВНалоговомОргане = Неопределено;
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеСтроки.РегистрацияВНалоговомОргане) Тогда
		ДанныеСтроки.РегистрацияВНалоговомОргане = ОсновнаяРегистрацияВНалоговомОргане;
	КонецЕсли;
	
	ЗаполнитьКолонкуАналитикаУчета(ДанныеСтроки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеСчета(Знач ВидОперации, Знач Налог, Знач Дата)
	
	Возврат Документы.КорректировкаЕНС.ДанныеСчета(ВидОперации, Налог, Дата);
	
КонецФункции

&НаСервереБезКонтекста
Функция СвойстваСчета(Знач Счет)
	
	Возврат Документы.КорректировкаЕНС.СвойстваСчета(Счет);
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// ОперацииНалог
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОперацииНалог");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Операции.ЭтоНалог", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь); 
	
	// ОперацииСрокУплаты
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОперацииСрокУплаты");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Операции.ЭтоНалог", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	// ОперацииПредстоящийПлатеж
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОперацииПредстоящийПлатеж");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Операции.ПредстоящийПлатеж", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	
	Форма.Объект.УвеличениеСальдо = 0;
	Форма.Объект.УменьшениеСальдо = 0;
	Для Каждого Операция Из Форма.Объект.Операции Цикл
		ЭтоУвеличениеСальдоЕНС = Форма.ВидыОперацийУвеличениеСальдоЕНС.Найти(Операция.ВидОперации) <> Неопределено;
		Если ЭтоУвеличениеСальдоЕНС Тогда
			Форма.Объект.УвеличениеСальдо = Форма.Объект.УвеличениеСальдо + Операция.Сумма;
		Иначе
			Форма.Объект.УменьшениеСальдо = Форма.Объект.УменьшениеСальдо + Операция.Сумма;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоНалог(ВидОперации)
	
	Возврат ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.НачислениеНалога")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.ВозмещениеНалога");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоРасходы(ВидОперации)
	
	Возврат ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.НачислениеПени")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.НачислениеШтрафа")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.НачислениеПроцентов")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.НачислениеГосПошлины");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоДоходы(ВидОперации)
	
	Возврат ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.ПоступлениеПроцентовОтНалоговогоОргана");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоТретьиЛица(ВидОперации)
	
	Возврат ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.ПереводСредствТретьемуЛицу")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.ПоступлениеСредствОтТретьегоЛица");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПрочее(ВидОперации)
	
	Возврат ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.ПрочееУвеличениеСальдо")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийКорректировкиЕНС.ПрочееУменьшениеСальдо");
	
КонецФункции

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОсновнаяРегистрацияВНалоговомОргане(Организация, Дата)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Справочники.Организации.РегистрацияВНалоговомОрганеНаДату(Организация, Дата);
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуРедактированияСтроки(ДанныеСтроки)
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура();
	ЗначенияЗаполнения.Вставить("Дата");
	ЗначенияЗаполнения.Вставить("Организация");
	
	ЗначенияЗаполнения.Вставить("ТолькоПросмотр");
	ЗначенияЗаполнения.Вставить("ОсновнаяРегистрацияВНалоговомОргане");
	ЗначенияЗаполнения.Вставить("ПользовательУправляетСчетамиУчета");
	
	ЗначенияЗаполнения.Вставить("СчетУчета");
	ЗначенияЗаполнения.Вставить("ВидПлатежаВГосБюджет");
	ЗначенияЗаполнения.Вставить("Контрагент");
	ЗначенияЗаполнения.Вставить("Субконто1");
	ЗначенияЗаполнения.Вставить("Субконто2");
	ЗначенияЗаполнения.Вставить("Субконто3");
	ЗначенияЗаполнения.Вставить("Подразделение");
	ЗначенияЗаполнения.Вставить("УровеньБюджета");
	ЗначенияЗаполнения.Вставить("РегистрацияВНалоговомОргане");
	ЗначенияЗаполнения.Вставить("ЭтоНалог");
	ЗначенияЗаполнения.Вставить("ЭтоРасходы");
	ЗначенияЗаполнения.Вставить("ЭтоДоходы");
	ЗначенияЗаполнения.Вставить("ЭтоТретьиЛица");
	ЗначенияЗаполнения.Вставить("ЭтоПрочее");
	ЗначенияЗаполнения.Вставить("ЕстьУчетПоПодразделениям");
	ЗначенияЗаполнения.Вставить("ЕстьВидыПлатежейВГосБюджет");
	ЗначенияЗаполнения.Вставить("ЕстьУровниБюджетов");
	ЗначенияЗаполнения.Вставить("ЕстьРегистрацияВНалоговомОргане");
	ЗначенияЗаполнения.Вставить("КоличествоСубконто");
	
	ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, ДанныеСтроки);
	ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, ЭтотОбъект, 
		"ТолькоПросмотр,ОсновнаяРегистрацияВНалоговомОргане,ПользовательУправляетСчетамиУчета");
	ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, Объект, "Дата,Организация");
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуРедактированияСтрокиЗавершение", ЭтотОбъект);
	ОткрытьФорму("Документ.КорректировкаЕНС.Форма.РедактированиеСчетаУчета", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияСтрокиЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт 
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбраннаяСтрока = Элементы.Операции.ТекущиеДанные;
	ЗаполнитьЗначенияСвойств(ВыбраннаяСтрока, РезультатВыбора);
	ЗаполнитьКолонкуАналитикаУчета(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблицыОперации()
	
	Для Каждого ДанныеСтроки Из Объект.Операции Цикл
		ЗаполнитьСвойстваВидаОперации(ДанныеСтроки);
		СвойстваСчета = СвойстваСчета(ДанныеСтроки.СчетУчета);
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, СвойстваСчета);
		ЗаполнитьКолонкуАналитикаУчета(ДанныеСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСвойстваВидаОперации(ДанныеСтроки)
	
	ДанныеСтроки.ЭтоНалог      = ЭтоНалог(ДанныеСтроки.ВидОперации);
	ДанныеСтроки.ЭтоРасходы    = ЭтоРасходы(ДанныеСтроки.ВидОперации);
	ДанныеСтроки.ЭтоДоходы     = ЭтоДоходы(ДанныеСтроки.ВидОперации);
	ДанныеСтроки.ЭтоТретьиЛица = ЭтоТретьиЛица(ДанныеСтроки.ВидОперации);
	ДанныеСтроки.ЭтоПрочее     = ЭтоПрочее(ДанныеСтроки.ВидОперации);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКолонкуАналитикаУчета(ДанныеСтроки)
	
	ЗначенияАналитики = Новый Массив;
	
	ДобавитьЗначениеАналитики(ЗначенияАналитики, ДанныеСтроки.СчетУчета);
	Если ДанныеСтроки.ЕстьУчетПоПодразделениям Тогда
		ДобавитьЗначениеАналитики(ЗначенияАналитики, ДанныеСтроки.Подразделение);
	КонецЕсли;
	Если ДанныеСтроки.ЭтоТретьиЛица Тогда
		ДобавитьЗначениеАналитики(ЗначенияАналитики, ДанныеСтроки.Контрагент);
	КонецЕсли;
	Если ДанныеСтроки.ЕстьВидыПлатежейВГосБюджет Тогда
		ДобавитьЗначениеАналитики(ЗначенияАналитики, ДанныеСтроки.ВидПлатежаВГосБюджет);
	КонецЕсли;
	Если ДанныеСтроки.ЕстьУровниБюджетов Тогда
		ДобавитьЗначениеАналитики(ЗначенияАналитики, ДанныеСтроки.УровеньБюджета);
	КонецЕсли;
	Если ДанныеСтроки.ЕстьРегистрацияВНалоговомОргане Тогда
		ДобавитьЗначениеАналитики(ЗначенияАналитики, ДанныеСтроки.РегистрацияВНалоговомОргане);
	КонецЕсли;
	Если ДанныеСтроки.ЭтоПрочее Тогда
		Для Ном = 1 По ДанныеСтроки.КоличествоСубконто Цикл
			ДобавитьЗначениеАналитики(ЗначенияАналитики, ДанныеСтроки["Субконто" + Ном]);
		КонецЦикла;
	КонецЕсли;
	
	ДанныеСтроки.АналитикаУчета = СтрСоединить(ЗначенияАналитики, ", ");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьЗначениеАналитики(ВсеЗначения, ТекущееЗначение)
	Если ЗначениеЗаполнено(ТекущееЗначение) Тогда
		ВсеЗначения.Добавить(ТекущееЗначение);
	Иначе
		ВсеЗначения.Добавить("<...>");
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРегистрациюВНалоговомОргане(Форма)
	
	Для Каждого ДанныеСтроки Из Форма.Объект.Операции Цикл
		Если ДанныеСтроки.ЕстьРегистрацияВНалоговомОргане 
			И Не ЗначениеЗаполнено(ДанныеСтроки.РегистрацияВНалоговомОргане) Тогда
			ДанныеСтроки.РегистрацияВНалоговомОргане = Форма.ОсновнаяРегистрацияВНалоговомОргане;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформационнуюПанель()
	
	Элементы.ТекстИнформационнойПанели.Видимость = Объект.СозданАвтоматически;
	Если Объект.СозданАвтоматически Тогда
		Элементы.ТекстИнформационнойПанели.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru='Операции были загружены автоматически по данным Личного кабинета ЕНС'"));
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
