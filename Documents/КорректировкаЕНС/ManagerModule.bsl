#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 1, 0);
	
КонецФункции

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Документ_КорректировкаЕНС";
	Набор.Идентификатор = Новый УникальныйИдентификатор("e57b4531-9252-40de-9a9b-47564aee61ac");
	Набор.Используется  = Истина;
	
КонецПроцедуры

// При выполнении регламентной операции НачислениеНалогаАУСН удаляет корректировки ЕНС, которые могли быть ранее
// автоматически загружены из сервиса АУСН
//
// Параметры:
//  КонтекстРасчета - Структура - см. УчетАУСН.НовыйКонтекстРасчетаНалога
//
Процедура УдалитьАвтоматическиеКорректировкиАУСН(КонтекстРасчета) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КорректировкаЕНС.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.КорректировкаЕНС КАК КорректировкаЕНС
		|ГДЕ
		|	КорректировкаЕНС.Организация = &Организация
		|	И КорректировкаЕНС.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И КорректировкаЕНС.СозданАвтоматически
		|	И КорректировкаЕНС.Комментарий ПОДОБНО &Комментарий
		|	И НЕ КорректировкаЕНС.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ДатаОкончания", КонтекстРасчета.КонецНалоговогоПериода);
	Запрос.УстановитьПараметр("ДатаНачала", КонтекстРасчета.НачалоНалоговогоПериода);
	Запрос.УстановитьПараметр("Организация", КонтекстРасчета.Организация);
	Запрос.УстановитьПараметр("Комментарий", НСтр("ru = 'Загружен из сервиса АУСН'"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.УстановитьПометкуУдаления(Истина);
		Исключение
			ЗаписьЖурналаРегистрации(
				ИнтеграцияАУСНКлиентСервер.ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.КорректировкаЕНС,
				Выборка.Ссылка,
				СтрШаблон(
					НСтр("ru = 'Не удалось пометить на удаление документ %1'"), Выборка.Ссылка));	
		КонецПопытки;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

// Перенесем данные из реквизитов в табличную часть документа КорректировкаЕНС.
Процедура ПеренестиДанныеВТабличнуюЧастьОперации(ПараметрыОбработчика) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	КорректировкаЕНС.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаЕНС КАК КорректировкаЕНС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаЕНС.Операции КАК КорректировкаЕНСОперации
	|		ПО КорректировкаЕНС.Ссылка = КорректировкаЕНСОперации.Ссылка
	|ГДЕ
	|	КорректировкаЕНСОперации.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаЕНС.Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	ПараметрыОбработчика.ОбработкаЗавершена = Ложь;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеДокумента = Строка(Выборка.Ссылка);
		НачатьТранзакцию();
		
		Попытка
			
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ." + Выборка.Ссылка.Метаданные().Имя);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			// Если объект ранее был удален другими сеансами, пропускаем его.
			Если ДокументОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			ДокументОбъект.Операции.Очистить();
			НоваяСтрока = ДокументОбъект.Операции.Добавить();
			НоваяСтрока.ВидОперации = ДокументОбъект.УдалитьВидОперации;
			НоваяСтрока.Сумма = ДокументОбъект.УдалитьСуммаДокумента;
			НоваяСтрока.Контрагент = ДокументОбъект.УдалитьКонтрагент;
			НоваяСтрока.Налог = ДокументОбъект.УдалитьНалог;
			НоваяСтрока.СчетУчета = ДокументОбъект.УдалитьСчетУчета;
			НоваяСтрока.ВидПлатежаВГосБюджет = ДокументОбъект.УдалитьВидПлатежаВГосБюджет;
			НоваяСтрока.УровеньБюджета = ДокументОбъект.УдалитьУровеньБюджета;
			НоваяСтрока.РегистрацияВНалоговомОргане = ДокументОбъект.УдалитьРегистрацияВНалоговомОргане;
			НоваяСтрока.Подразделение = ДокументОбъект.УдалитьПодразделение;
			НоваяСтрока.Субконто1 = ДокументОбъект.УдалитьСубконто1;
			НоваяСтрока.Субконто2 = ДокументОбъект.УдалитьСубконто2;
			НоваяСтрока.Субконто3 = ДокументОбъект.УдалитьСубконто3;
			НоваяСтрока.Основание = ДокументОбъект.УдалитьОснование;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре %1 не удалось обработать документ ""%2"" по причине:
					|%3'"),
				"ЕдиныйНалоговыйСчет.ПеренестиДанныеВТабличнуюЧастьОперации()",
				ПредставлениеДокумента,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Ссылка.Метаданные(),
				Выборка.Ссылка,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗагрузкаСлужебные

Функция ДатаПоследнегоДокумента(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НАЧАЛОПЕРИОДА(КорректировкаЕНС.Дата, ДЕНЬ) КАК Дата
	|ИЗ
	|	Документ.КорректировкаЕНС КАК КорректировкаЕНС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаЕНС.Операции КАК КорректировкаЕНСОперации
	|		ПО КорректировкаЕНС.Ссылка = КорректировкаЕНСОперации.Ссылка
	|ГДЕ
	|	КорректировкаЕНС.Организация = &Организация
	|	И КорректировкаЕНС.Проведен
	|	И НЕ КорректировкаЕНСОперации.ПредстоящийПлатеж
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат НачалоДня(Выборка.Дата);
	КонецЕсли;
	
	Возврат '0001-01-01';
	
КонецФункции

Процедура ЗагрузитьИзДанныхЛичногоКабинета(Организация, ТаблицаОперацийЕНС, ЭтоОбновлениеДанныхЛК = Ложь, ЭтоЗагрузкаЗаПериод = Ложь, РезультатЗагрузки = Неопределено) Экспорт
	
	Если РезультатЗагрузки = Неопределено Тогда
		РезультатЗагрузки = НовыйРезультатОтразитьВУчете();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТаблицаОперацийЕНС) Тогда
		РезультатЗагрузки.СообщениеОбОшибке = НСтр("ru='Нет данных для отражения в учете.'");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаЕНС.Ссылка КАК Ссылка,
	|	НАЧАЛОПЕРИОДА(КорректировкаЕНС.Дата, ДЕНЬ) КАК Дата,
	|	ЛОЖЬ КАК ЕстьВДанныхЗагрузки
	|ИЗ
	|	Документ.КорректировкаЕНС КАК КорректировкаЕНС
	|ГДЕ
	|	КорректировкаЕНС.Организация = &Организация
	|	И КорректировкаЕНС.Дата МЕЖДУ &ПерваяДата И &ПоследняяДата
	|	И КорректировкаЕНС.СозданАвтоматически
	|	И НЕ КорректировкаЕНС.ПометкаУдаления";
	Запрос.УстановитьПараметр("Организация", Организация);
	ПерваяДата = ТаблицаОперацийЕНС[ТаблицаОперацийЕНС.Количество() - 1].ДатаОперации;
	Запрос.УстановитьПараметр("ПерваяДата", НачалоДня(ПерваяДата));
	ПоследняяДата = ТаблицаОперацийЕНС[0].ДатаОперации;
	Запрос.УстановитьПараметр("ПоследняяДата", КонецДня(ПоследняяДата));
	
	РанееЗагруженныеДокументы = Запрос.Выполнить().Выгрузить();
	РанееЗагруженныеДокументы.Индексы.Добавить("Дата");
	
	ДатаДокумента = '0001-01-01';
	ДокументОбъект = Неопределено;
	НачисленияЗаДату = Новый Массив;
	ОчищатьВсеОперации = ЭтоОбновлениеДанныхЛК Или ЭтоЗагрузкаЗаПериод;
	
	Для каждого СтрокаДанных Из ТаблицаОперацийЕНС Цикл
		
		ДатаСтроки = НачалоДня(СтрокаДанных.ДатаОперации);
		Если ДатаСтроки <> ДатаДокумента Тогда
			ЗаписатьЗагружаемыйДокумент(ДокументОбъект, НачисленияЗаДату, РезультатЗагрузки);
			// При вызове из формы ЛК прерываем загрузку при ошибке
			Если Не ЭтоОбновлениеДанныхЛК И Не РезультатЗагрузки.Успешно Тогда
				Возврат;
			КонецЕсли;
			СтрокаДокумента = РанееЗагруженныеДокументы.Найти(ДатаСтроки, "Дата");
			Если СтрокаДокумента <> Неопределено Тогда
				СтрокаДокумента.ЕстьВДанныхЗагрузки = Истина;
				ДокументОбъект = СтрокаДокумента.Ссылка.ПолучитьОбъект();
				Если ОчищатьВсеОперации Тогда
					ДокументОбъект.Операции.Очистить();
				КонецЕсли;
			Иначе
				ДокументОбъект = Документы.КорректировкаЕНС.СоздатьДокумент();
				ДокументОбъект.Дата = ДатаСтроки;
				ДокументОбъект.Организация = Организация;
			КонецЕсли;
			ДатаДокумента = НачалоДня(ДокументОбъект.Дата);
			НачисленияЗаДату = Новый Массив;
		КонецЕсли;
		
		Если Не ОчищатьВсеОперации Тогда
			ОтборПоИдентификатору = Новый Структура("ИдентификаторФНС", СтрокаДанных.ИдентификаторЗаписи);
			Для каждого УдаляемаяСтрока Из ДокументОбъект.Операции.НайтиСтроки(ОтборПоИдентификатору) Цикл
				ДокументОбъект.Операции.Удалить(УдаляемаяСтрока);
			КонецЦикла;
		КонецЕсли;
		
		НоваяСтрока = ДокументОбъект.Операции.Добавить();
		НоваяСтрока.Сумма = СтрокаДанных.Сумма;
		
		Если Не ЭтоОбновлениеДанныхЛК Тогда
			НовоеОснование = НовыйОснованиеНачисления();
			НовоеОснование.Сумма = СтрокаДанных.Сумма;
			НовоеОснование.ИдентификаторСтроки = СтрокаДанных.ИдентификаторСтроки;
			НачисленияЗаДату.Добавить(НовоеОснование);
		КонецЕсли;
		
		Если СтрокаДанных.ТипКНО = Справочники.ТипыКНО.Налог Тогда
			НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога;
		ИначеЕсли СтрокаДанных.ТипКНО = Справочники.ТипыКНО.Пеня Тогда
			НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеПени;
		ИначеЕсли СтрокаДанных.ТипКНО = Справочники.ТипыКНО.Штраф Тогда
			НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеШтрафа;
		ИначеЕсли СтрокаДанных.ТипКНО = Справочники.ТипыКНО.Госпошлина Тогда
			НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеГосПошлины;
		ИначеЕсли СтрокаДанных.ТипКНО = Справочники.ТипыКНО.Процент2200 Тогда
			НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеПроцентов;
		ИначеЕсли СтрокаДанных.ТипКНО = Справочники.ТипыКНО.Процент5000 Тогда
			НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеПроцентовОтНалоговогоОргана;
		КонецЕсли;
		
		НоваяСтрока.ИдентификаторФНС = СтрокаДанных.ИдентификаторЗаписи;
		НоваяСтрока.ПредстоящийПлатеж = СтрокаДанных.ПредстоящийПлатеж;
		
		Если НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога Тогда
			НалогПоКБК = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(СтрокаДанных.КБК, Ложь);
			НоваяСтрока.Налог = НалогПоКБК;
			Если Не ЗначениеЗаполнено(НоваяСтрока.Налог) Тогда
				НоваяСтрока.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы;
			КонецЕсли;
			НоваяСтрока.ВидПлатежаВГосБюджет = Перечисления.ВидыПлатежейВГосБюджет.Налог;
			НоваяСтрока.СрокУплаты = СтрокаДанных.СрокУплаты;
		КонецЕсли;
		ВидНалога = ?(ЗначениеЗаполнено(НоваяСтрока.Налог),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Налог, "ВидНалога"),
			Неопределено);
			
		ДанныеСчета = Документы.КорректировкаЕНС.ДанныеСчета(НоваяСтрока.ВидОперации, НоваяСтрока.Налог, ДатаДокумента);
		
		Если ВидНалога = Перечисления.ВидыНалогов.НДС
			И СтрокаДанных.НалоговыйАгент Тогда
			НоваяСтрока.СчетУчета = ПланыСчетов.Хозрасчетный.НДСНалоговогоАгента;
		Иначе
			НоваяСтрока.СчетУчета = ДанныеСчета.СчетУчета;
		КонецЕсли;
		НоваяСтрока.УровеньБюджета = ДанныеСчета.УровеньБюджета;
		
		Если ДанныеСчета.ЕстьРегистрацияВНалоговомОргане Тогда
			Если ЗначениеЗаполнено(СтрокаДанных.КПП) Тогда
				НоваяСтрока.РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.НайтиПоКПП(
					Организация, СтрокаДанных.КПП);
			ИначеЕсли ЗначениеЗаполнено(СтрокаДанных.ОКТМО) Тогда
				НоваяСтрока.РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.НайтиПоКодуТерритории(
					Организация, СтрокаДанных.ОКТМО);
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НоваяСтрока.СчетУчета) Тогда
			Если НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога Тогда
				НоваяСтрока.СчетУчета = ПланыСчетов.Хозрасчетный.ПрочиеНалогиИСборы;
			Иначе
				НоваяСтрока.СчетУчета = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
			КонецЕсли;
		КонецЕсли;
		
		ПредставлениеОснования = СтрШаблон(НСтр("ru='%1, №%2 от %3'"),
			Строка(СтрокаДанных.ТипОперации), 
			СтрокаДанных.ДокументИсточникНомер, 
			Формат(СтрокаДанных.ДокументИсточникДата, "ДЛФ=D"));
		НоваяСтрока.Основание = ПредставлениеОснования;
		
	КонецЦикла;
	
	ЗаписатьЗагружаемыйДокумент(ДокументОбъект, НачисленияЗаДату, РезультатЗагрузки);
	// При вызове из формы ЛК прерываем загрузку при ошибке
	Если Не ЭтоОбновлениеДанныхЛК И Не РезультатЗагрузки.Успешно Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьЛишниеДокументы(РанееЗагруженныеДокументы);
	
КонецПроцедуры

Процедура ЗаписатьЗагружаемыйДокумент(ДокументОбъект, НачисленияЗаДату, Результат);
	
	Если ДокументОбъект = Неопределено Тогда
		Результат.Успешно = Истина;
		Возврат;
	КонецЕсли;
	
	Попытка
		ДокументОбъект.СозданАвтоматически = Истина;
		ДокументОбъект.Комментарий = НСтр("ru='<Создан автоматически по данным Личного кабинета ЕНС>'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Результат.Успешно = Истина;
		Для каждого Начисление Из НачисленияЗаДату Цикл
			Начисление.Дата = ДокументОбъект.Дата;
			Начисление.Номер = ДокументОбъект.Номер;
			Начисление.Ссылка = ДокументОбъект.Ссылка;
			Результат.ОснованияНачислений.Добавить(Начисление);
		КонецЦикла;
	Исключение
		ЗаписьЖурналаРегистрации(ЕдиныйНалоговыйСчетИнтеграцияВнутренний.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.КорректировкаЕНС,
			ДокументОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Результат.Успешно = Ложь;
		Результат.СообщениеОбОшибке = СтрШаблон(НСтр("ru='Не удалось отразить операцию в учете:
				|%1'"), ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

Процедура УдалитьЛишниеДокументы(РанееЗагруженныеДокументы)
	
	ОтборНетВДанныхЗагрузки = Новый Структура("ЕстьВДанныхЗагрузки", Ложь);
	ДокументыКУдалению = РанееЗагруженныеДокументы.НайтиСтроки(ОтборНетВДанныхЗагрузки);
	Для каждого СтрокаКУдалению Из ДокументыКУдалению Цикл
		УдаляемыйДокумент = СтрокаКУдалению.Ссылка.ПолучитьОбъект();
		Если УдаляемыйДокумент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Попытка
			УдаляемыйДокумент.УстановитьПометкуУдаления(Истина);
		Исключение
			ЗаписьЖурналаРегистрации(ЕдиныйНалоговыйСчетИнтеграцияВнутренний.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.КорректировкаЕНС,
				УдаляемыйДокумент.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйРезультатОтразитьВУчете()
	
	Результат = Новый Структура;
	Результат.Вставить("Успешно", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	Результат.Вставить("ОснованияНачисления", Новый Массив);
	Возврат Результат;
	
КонецФункции

Функция НовыйОснованиеНачисления()
	
	Результат = Новый Структура;
	Результат.Вставить("Дата", '00010101');
	Результат.Вставить("Номер", "");
	Результат.Вставить("Сумма", 0);
	Результат.Вставить("Ссылка");
	Результат.Вставить("ИдентификаторСтроки", "");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	НомераТаблиц = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Ссылка.Дата КАК Период,
	|	Реквизиты.Ссылка.Организация КАК Организация,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Налог КАК Налог,
	|	Реквизиты.Сумма КАК СуммаДокумента,
	|	Реквизиты.СчетУчета КАК СчетУчета,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ВидПлатежаВГосБюджет КАК ВидПлатежаВГосБюджет,
	|	Реквизиты.УровеньБюджета КАК УровеньБюджета,
	|	Реквизиты.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Реквизиты.Основание КАК Основание,
	|	Реквизиты.Подразделение КАК Подразделение,
	|	Реквизиты.Субконто1 КАК Субконто1,
	|	Реквизиты.Субконто2 КАК Субконто2,
	|	Реквизиты.Субконто3 КАК Субконто3
	|ИЗ
	|	Документ.КорректировкаЕНС.Операции КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура СформироватьПроводки(ТаблицаРеквизиты, Движения, Отказ) Экспорт

	Для Каждого Реквизиты Из ТаблицаРеквизиты Цикл
		
		ВидОперации = Реквизиты.ВидОперации;
		СвойстваСчетаУчета = СвойстваСчета(Реквизиты.СчетУчета);
		ЕстьУровниБюджетов = СвойстваСчетаУчета.ЕстьУровниБюджетов;
		ЕстьРегистрацияВНалоговомОргане = СвойстваСчетаУчета.ЕстьРегистрацияВНалоговомОргане;
		ЕстьУчетПоПодразделениям = СвойстваСчетаУчета.ЕстьУчетПоПодразделениям;
		ЕстьНалоговыйУчет = СвойстваСчетаУчета.СвойстваСчета.НалоговыйУчет
			И УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
		КоличествоСубконто = СвойстваСчетаУчета.СвойстваСчета.КоличествоСубконто;
		
		ВсеСубконто = Новый Соответствие;
		Если ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеПени Тогда
			ВсеСубконто.Вставить(
				"ПрочиеДоходыИРасходы", // Для 91 счета
				Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("ПениПоНалогам"));
			ВсеСубконто.Вставить(
				"ПрибылиИУбытки", // Для 99 счета
				Перечисления.ПрибылиИУбытки.НалоговыеСанкции);
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеШтрафа Тогда
			ВсеСубконто.Вставить(
				"ПрочиеДоходыИРасходы", // Для 91 счета
				Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("ШтрафыПоНалогам"));
			ВсеСубконто.Вставить(
				"ПрибылиИУбытки", // Для 99 счета
				Перечисления.ПрибылиИУбытки.НалоговыеСанкции);
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеПроцентов Тогда
			ВсеСубконто.Вставить(
				"ПрочиеДоходыИРасходы", // Для 91 счета
				Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("ПроцентыПоНалогамКУплате"));
			ВсеСубконто.Вставить(
				"ПрибылиИУбытки", // Для 99 счета
				Перечисления.ПрибылиИУбытки.НалоговыеСанкции);
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеГосПошлины Тогда
			ВсеСубконто.Вставить(
				"ПрочиеДоходыИРасходы", 
				Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("ГосПошлиныПоСудебнымСпорам"));
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеПроцентовОтНалоговогоОргана Тогда
			ВсеСубконто.Вставить(
				"ПрочиеДоходыИРасходы", 
				Справочники.ПрочиеДоходыИРасходы.ПредопределенныйЭлемент("ПроцентыПоНалогамКПолучению"));
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПереводСредствТретьемуЛицу
			Или Реквизиты.ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеСредствОтТретьегоЛица Тогда
			ВсеСубконто.Вставить("Контрагенты", Реквизиты.Контрагент);
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога
			Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ВозмещениеНалога Тогда
			ВсеСубконто.Вставить("ВидыПлатежейВГосБюджет", Реквизиты.ВидПлатежаВГосБюджет);
			ВсеСубконто.Вставить("ВидыНалогов", Реквизиты.Налог);
			Если ЕстьУровниБюджетов Тогда
				ВсеСубконто.Вставить("УровниБюджетов", Реквизиты.УровеньБюджета);
			КонецЕсли;
			Если ЕстьРегистрацияВНалоговомОргане Тогда
				ВсеСубконто.Вставить("РегистрацияВНалоговомОргане", Реквизиты.РегистрацияВНалоговомОргане);
			КонецЕсли;
			
		ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПрочееУвеличениеСальдо
			Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПрочееУменьшениеСальдо Тогда
			Для НомерСубконто = 1 По КоличествоСубконто Цикл
			ВсеСубконто.Вставить(НомерСубконто, Реквизиты["Субконто" + НомерСубконто]);
		КонецЦикла;
			
		КонецЕсли;
		
		Проводка = Движения.Хозрасчетный.Добавить();
		Проводка.Период = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Если Не ПустаяСтрока(Реквизиты.Основание) Тогда
			Проводка.Содержание = СтрШаблон("Единый налоговый счет: %1. %2", 
				Строка(Реквизиты.ВидОперации),
				Реквизиты.Основание);
		Иначе
			Проводка.Содержание = СтрШаблон("Единый налоговый счет: %1", 
			Строка(Реквизиты.ВидОперации));
		КонецЕсли;
		
		Проводка.Сумма = Реквизиты.СуммаДокумента;
		
		ЭтоУвеличениеСальдоЕНС = ВидыОперацийУвеличениеСальдоЕНС().Найти(ВидОперации) <> Неопределено;
		
		Если ЭтоУвеличениеСальдоЕНС Тогда
			
			// Дт 68.90 Кт <СчетУчета>
			
			Проводка.СчетДт = ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет;
			
			Проводка.СчетКт = Реквизиты.СчетУчета;
			Для каждого КоррСубконто Из ВсеСубконто Цикл
				Если ЗначениеЗаполнено(КоррСубконто.Значение) Тогда
					БухгалтерскийУчет.УстановитьСубконто(
						Проводка.СчетКт, Проводка.СубконтоКт, КоррСубконто.Ключ, КоррСубконто.Значение);
				КонецЕсли;
			КонецЦикла;
			Если ЕстьУчетПоПодразделениям Тогда
				Проводка.ПодразделениеКт = Реквизиты.Подразделение;
			КонецЕсли;
			Если ЕстьНалоговыйУчет Тогда
				Проводка.СуммаНУКт = Реквизиты.СуммаДокумента;
			КонецЕсли;
			
		Иначе
			
			// Дт <СчетУчета> Кт 68.90
			
			Проводка.СчетДт = Реквизиты.СчетУчета;
			Для каждого КоррСубконто Из ВсеСубконто Цикл
				Если ЗначениеЗаполнено(КоррСубконто.Значение) Тогда
					БухгалтерскийУчет.УстановитьСубконто(
						Проводка.СчетДт, Проводка.СубконтоДт, КоррСубконто.Ключ, КоррСубконто.Значение);
				КонецЕсли;
			КонецЦикла;
			Если ЕстьУчетПоПодразделениям Тогда
				Проводка.ПодразделениеДт = Реквизиты.Подразделение;
			КонецЕсли;
			Если ЕстьНалоговыйУчет Тогда
				Проводка.СуммаНУДт = Реквизиты.СуммаДокумента;
			КонецЕсли;
			
			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет;
			
		КонецЕсли;
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Функция ДанныеСчета(ВидОперации, Налог, Дата) Экспорт
	
	Счет = Неопределено;
	УровеньБюджета = Неопределено;
	Если ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеНалога
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ВозмещениеНалога Тогда
		Счет = Справочники.ВидыНалоговИПлатежейВБюджет.СчетУчета(Налог, Дата);
		УровеньБюджета = Справочники.ВидыНалоговИПлатежейВБюджет.УровеньБюджета(Налог);
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеПени
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеШтрафа
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеПроцентов
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.НачислениеГосПошлины Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеРасходы; // 91.02
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеПроцентовОтНалоговогоОргана Тогда
		Счет = ПланыСчетов.Хозрасчетный.ПрочиеДоходы; // 91.01
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПереводСредствТретьемуЛицу
		Или ВидОперации = Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеСредствОтТретьегоЛица Тогда
		Счет = ПланыСчетов.Хозрасчетный.РасчетыПоНалогамСТретьимиЛицами; // 76.51
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("СчетУчета", Счет);
	Результат.Вставить("УровеньБюджета", УровеньБюджета);
	Свойства = СвойстваСчета(Счет);
	Результат.Вставить("СвойстваСчета", Свойства.СвойстваСчета);
	Результат.Вставить("ЕстьУровниБюджетов", Свойства.ЕстьУровниБюджетов);
	Результат.Вставить("ЕстьВидыПлатежейВГосБюджет", Свойства.ЕстьВидыПлатежейВГосБюджет);
	Результат.Вставить("ЕстьРегистрацияВНалоговомОргане", Свойства.ЕстьРегистрацияВНалоговомОргане);
	Результат.Вставить("ЕстьУчетПоПодразделениям", Свойства.ЕстьУчетПоПодразделениям);
	Результат.Вставить("КоличествоСубконто", Свойства.КоличествоСубконто);
	
	Возврат Результат;
	
КонецФункции

Функция СвойстваСчета(Счет) Экспорт
	
	Свойства = Новый Структура;
	Свойства.Вставить("СвойстваСчета", Неопределено);
	Свойства.Вставить("ЕстьУровниБюджетов", Ложь);
	Свойства.Вставить("ЕстьРегистрацияВНалоговомОргане", Ложь);
	Свойства.Вставить("ЕстьУчетПоПодразделениям", Ложь);
	Свойства.Вставить("ЕстьВидыПлатежейВГосБюджет", Ложь);
	Свойства.Вставить("КоличествоСубконто", 0);
	
	Если ЗначениеЗаполнено(Счет) Тогда
		Свойства.СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
		Для каждого Элемент Из Свойства.СвойстваСчета.ИдентификаторыСубконто Цикл
			Если Элемент.Ключ.ИмяПредопределенныхДанных = "УровниБюджетов" Тогда
				Свойства.ЕстьУровниБюджетов = Истина;
			ИначеЕсли Элемент.Ключ.ИмяПредопределенныхДанных = "РегистрацияВНалоговомОргане" Тогда
				Свойства.ЕстьРегистрацияВНалоговомОргане = Истина;
			ИначеЕсли Элемент.Ключ.ИмяПредопределенныхДанных = "ВидыПлатежейВГосБюджет" Тогда
				Свойства.ЕстьВидыПлатежейВГосБюджет = Истина;
			КонецЕсли;
		КонецЦикла;
		Свойства.ЕстьУчетПоПодразделениям = Свойства.СвойстваСчета.УчетПоПодразделениям;
		Свойства.КоличествоСубконто = Свойства.СвойстваСчета.КоличествоСубконто;
	КонецЕсли;
	
	Возврат Свойства;
	
КонецФункции

Функция ВидыОперацийУвеличениеСальдоЕНС() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.ВидыОперацийКорректировкиЕНС.ВозмещениеНалога);
	Результат.Добавить(Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеСредствОтТретьегоЛица);
	Результат.Добавить(Перечисления.ВидыОперацийКорректировкиЕНС.ПоступлениеПроцентовОтНалоговогоОргана);
	Результат.Добавить(Перечисления.ВидыОперацийКорректировкиЕНС.ПрочееУвеличениеСальдо);
	
	Возврат Результат;
	
КонецФункции

Процедура РассчитатьИзменениеСальдоЕНС(ДокументОбъект) Экспорт
	
	ДокументОбъект.УвеличениеСальдо = 0;
	ДокументОбъект.УменьшениеСальдо = 0;
	Для Каждого Операция Из ДокументОбъект.Операции Цикл
		ЭтоУвеличениеСальдоЕНС = Операция.ВидОперации <> Неопределено;
		Если ЭтоУвеличениеСальдоЕНС Тогда
			ДокументОбъект.УвеличениеСальдо = ДокументОбъект.УвеличениеСальдо + Операция.Сумма;
		Иначе
			ДокументОбъект.УменьшениеСальдо =ДокументОбъект.УменьшениеСальдо + Операция.Сумма;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли