#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	СтраныЕАЭС = Справочники.СтраныМира.СтраныЕАЭС(Истина);
	
	Для каждого Страна Из СтраныЕАЭС Цикл
		Элементы.ОтборСтранаРеализации.СписокВыбора.Добавить(Страна.Значение);
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Организация",
		Параметры.Организация,
		ВидСравненияКомпоновкиДанных.Равно);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"ВидОперации",
		Параметры.ВидОперации,
		ВидСравненияКомпоновкиДанных.Равно);
	
	ОтборПериод = Параметры.НачалоПериода;
	ОтборПериодИспользование = ЗначениеЗаполнено(ОтборПериод);
	ОтборПериодПредставление = Формат (ОтборПериод,"ДФ='ММММ гггг'") + НСтр("ru = ' г.'");
	
	ОтборСтранаРеализации = Справочники.СтраныМира.НайтиПоРеквизиту("КодАльфа2", Параметры.ТоргующаяСтранаКод);
	ОтборСтранаРеализацииИспользование = ЗначениеЗаполнено(ОтборСтранаРеализации);
	
	УстановитьВосстановленныеОтборы();
	
	Элементы.ПодразделениеОрганизации.Видимость = ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыбратьДанные(Команда)
	
	ВыбранныеДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодИспользованиеПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период")
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ПараметрыПериода = Новый Структура("НачалоПериода, КонецПериода, ВидПериода, ИмяФормы");
	ПараметрыПериода.НачалоПериода = ОтборПериод;
	ПараметрыПериода.КонецПериода = КонецМесяца(ОтборПериод);
	ПараметрыПериода.ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц");
	ПараметрыПериода.ИмяФормы = "ВыборСтандартногоПериодаМесяц";
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму(
			СтрШаблон("ОбщаяФорма.%1","ВыборСтандартногоПериодаМесяц"),
			ПараметрыПериода,
			ЭтотОбъект, , , ,
			Новый ОписаниеОповещения("ОтборПериодПредставлениеЗавершениеВыбора", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПредставлениеПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(ОтборПериодПредставление) Тогда
		ОтборПериодИспользование = Ложь;
		ОтборПериод = '00010101';
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	ОтборПериодИспользование = Ложь;
	ОтборПериод = '00010101';
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период");
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтранаРеализацииИспользованиеПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СтранаРеализации");
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтранаРеализацииПриИзменении(Элемент)
	ОтборСтранаРеализацииИспользование = ЗначениеЗаполнено(ОтборСтранаРеализации);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СтранаРеализации");
КонецПроцедуры

&НаКлиенте
Процедура ОтборПокупательИспользованиеПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Покупатель");
КонецПроцедуры

&НаКлиенте
Процедура ОтборПокупательПриИзменении(Элемент)
	ОтборПокупательИспользование = ЗначениеЗаполнено(ОтборПокупатель);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Покупатель");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбранныеДанные();
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СтранаРеализации");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Покупатель");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПредставлениеЗавершениеВыбора(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		
		ОтборПериодПредставление = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			РезультатВыбора.НачалоПериода, РезультатВыбора.КонецПериода, Истина);
		ОтборПериод = РезультатВыбора.НачалоПериода;
		
		ОтборПериодИспользование = ЗначениеЗаполнено(ОтборПериод);
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеДанные()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		РезультатВыбора = Новый Структура;
		
		РезультатВыбора.Вставить("Дата",                            ТекущиеДанные.Дата);
		РезультатВыбора.Вставить("СистемныйНомер",                  ТекущиеДанные.Номер);
		РезультатВыбора.Вставить("ДокументСсылка",                  ТекущиеДанные.Ссылка);
		РезультатВыбора.Вставить("КлючСтроки",                      ТекущиеДанные.КлючСтроки);
		РезультатВыбора.Вставить("Организация",                     ТекущиеДанные.Организация);
		РезультатВыбора.Вставить("Покупатель",                      ТекущиеДанные.Покупатель);
		РезультатВыбора.Вставить("СтранаРеализации",                ТекущиеДанные.СтранаРеализации);
		РезультатВыбора.Вставить("ВалютаДокумента",                 ТекущиеДанные.ВалютаДокумента);
		РезультатВыбора.Вставить("НомерДоговора",                   ТекущиеДанные.НомерДоговора);
		РезультатВыбора.Вставить("ДатаДоговора",                    ТекущиеДанные.ДатаДоговора);
		РезультатВыбора.Вставить("Договор",                         ТекущиеДанные.ДоговорКонтрагента);
		РезультатВыбора.Вставить("ДатаСчетаФактурыОтКомиссионера",  ТекущиеДанные.ДатаСчетаФактурыОтКомиссионера);
		РезультатВыбора.Вставить("НомерСчетаФактурыОтКомиссионера", ТекущиеДанные.НомерСчетаФактурыОтКомиссионера);
		РезультатВыбора.Вставить("ДатаСчетаФактурыВыданного",       ТекущиеДанные.ДатаСчетаФактурыВыданного);
		РезультатВыбора.Вставить("НомерСчетаФактурыВыданного",
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ТекущиеДанные.НомерСчетаФактурыВыданного, Истина, Ложь));
		
		Закрыть(РезультатВыбора);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

