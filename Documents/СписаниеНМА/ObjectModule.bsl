#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено Тогда
		Если ТипДанныхЗаполнения = Тип("Структура") Тогда
			ЗаполнитьПоСтруктуре(ДанныеЗаполнения);
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.СписаниеНМА.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ
	
	ПараметрыВыбытия = УчетНМА.ПодготовитьТаблицыСведенийПоВыбытиюНМА(
		ПараметрыПроведения.ТаблицаНематериальныеАктивы, 
		ПараметрыПроведения.СписаниеНематериальныхАктивовРеквизиты, Отказ);
		
	// Учет доходов и расходов ИП
	ТаблицыСписанияНМАИП	= УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыСписанияОСиНМА(
		ПараметрыПроведения.СписаниеОСиНМАИПТаблица,
		ПараметрыПроведения.СписаниеОСиНМАИПРеквизиты, Движения, Отказ);

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ

	// Алгоритмы формирования проводок этого документа рассчитывают суммы проводок налогового учета
	Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("СуммыНалоговогоУчетаЗаполнены", Истина);
	
	УчетНМА.СформироватьДвиженияСостоянияНМАОрганизаций(ПараметрыПроведения.ТаблицаНематериальныеАктивы,
		ПараметрыПроведения.СписаниеНематериальныхАктивовРеквизиты, Движения, Отказ);
	
	УчетНМА.СформироватьДвиженияВыбытиеНМА(ПараметрыПроведения.СписаниеНематериальныхАктивовРеквизиты, 
		ПараметрыВыбытия, Движения, Отказ);
		
	// Учет доходов и расходов ИП
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияСписаниеОСиНМА(
		ТаблицыСписанияНМАИП,
		ПараметрыПроведения.СписаниеОСиНМАИПРеквизиты, Движения, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения)

	Если ДанныеЗаполнения.Свойство("ДокументОснование")
		И ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.ИнвентаризацияНМА") Тогда
		ЗаполнитьПоДаннымИнвентаризации(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымИнвентаризации(ДанныеЗаполнения)
		
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);

	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, ДокументОснование);
	
	ПодразделениеОрганизации = АктуальноеПодразделениеНематериальногоАктива();
	
	Если Не ЗначениеЗаполнено(СчетСписания) Тогда
		СчетСписания = Метаданные().Реквизиты.СчетСписания.ЗначениеЗаполнения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетСписания) И Не ЗначениеЗаполнено(Субконто) Тогда
		ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетСписания);
		ЗначенияСубконтоПоУмолчанию = БухгалтерскийУчетКлиентСервер.ПредопределенныеЗначенияСубконтоПоУмолчанию();
		Субконто = ЗначенияСубконтоПоУмолчанию[ДанныеСчета.ВидСубконто1];
	КонецЕсли;
	
КонецПроцедуры

Функция АктуальноеПодразделениеНематериальногоАктива()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объекты",     НематериальныйАктив);
	Запрос.УстановитьПараметр("Период",      Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетаУчета",  УчетНМА.СчетаУчетаНМА().Баланс);
	
	Запрос.Текст = УчетНМА.ТекстЗапросаУчетныеДанные();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Подразделение;
	КонецЕсли;
	
	Возврат Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#КонецЕсли