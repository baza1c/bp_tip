#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() И Параметры.Свойство("ДанныеУчастника") Тогда
		ВзаимодействияФормыБП.УстановитьКонтактноеЛицо(ЭтотОбъект, Параметры.ДанныеУчастника);
	КонецЕсли;
	
	Если Параметры.Свойство("ЗначенияЗаполнения") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
		ЗначенияЗаполнения = Параметры.ЗначенияЗаполнения;
		Если ТипЗнч(ЗначенияЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(Объект, ЗначенияЗаполнения);
			
			Если ЗначенияЗаполнения.Свойство("ПредметВзаимодействия") 
				И ЗначениеЗаполнено(ЗначенияЗаполнения.ПредметВзаимодействия) Тогда
				Предмет = ЗначенияЗаполнения.ПредметВзаимодействия;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ВзаимодействияФормыБП.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзмененоСостояниеЛидов") Тогда
		ПараметрыЗаписи.Вставить("ИзмененоСостояниеЛидов", ТекущийОбъект.ДополнительныеСвойства.ИзмененоСостояниеЛидов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИзмененоСостояниеЛидов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыЗаписи, "ИзмененоСостояниеЛидов", Ложь);
	Если ИзмененоСостояниеЛидов Тогда
		ОповеститьОбИзменении(Тип("СправочникСсылка.КонтактныеЛица"));
		Оповестить("ИзмененоСостояниеЛидов", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.КонтактноеЛицо));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.Предмет = Предмет;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьВсеРеквизиты(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Автор", Объект.Ответственный);
	СтруктураЗаполнения.Вставить("Тема", Объект.Тема);
	СтруктураЗаполнения.Вставить("Описание", Объект.Описание);
	СтруктураЗаполнения.Вставить("Ответственный", Объект.Ответственный);
	СтруктураЗаполнения.Вставить("Контрагент", Объект.Контрагент);
	СтруктураЗаполнения.Вставить("КонтактноеЛицо", Объект.КонтактноеЛицо);
	СтруктураЗаполнения.Вставить("Предмет", Предмет);
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", СтруктураЗаполнения);
	
	ОткрытьФорму("Документ.ПрочееВзаимодействие.Форма.ФормаДокумента", ПараметрыФормы, ВладелецФормы);
	
	Модифицированность = Ложь;
	
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КонтактноеЛицо");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Контрагент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.КонтактноеЛицо", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КонтактноеЛицо");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.КонтактноеЛицо", ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

#КонецОбласти

