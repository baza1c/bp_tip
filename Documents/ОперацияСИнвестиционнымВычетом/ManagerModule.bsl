
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область СтандартныеПодсистемы_УправлениеДоступом

// СтандартныеПодисистемы.УправлениеДоступом

// Ограничение доступа к данным.
//
// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	| ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодисистемы.УправлениеДоступомм

#КонецОбласти

#Область СтандартныеПодсистемы_Свойства

// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Документ_ОперацияСИнвестиционнымВычетом";
	Набор.Идентификатор = Новый УникальныйИдентификатор("5a0b3246-259d-4e84-b957-677b73687ade");
	Набор.Используется  = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы_ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	// ПриОпределенииНастроекВерсионированияОбъектов
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СтандартныеПодсистемы_Печать

// СтандартныеПодсистемы.Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Операция с инвестиционным вычетом""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	ВидОперации = Неопределено; 

	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "ВидОперации");
	КонецЕсли;

	// Если документ копируется, то вид операции получаем из копируемого документа.
	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		Если Параметры.Свойство("ЗначениеКопирования")
			И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Параметры.ЗначениеКопирования, "ВидОперации");
		КонецЕсли;
	КонецЕсли;
	
	ЗначенияЗаполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЗначенияЗаполнения");
	
	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		ВидОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначенияЗаполнения, "ВидОперации");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Ключ", Неопределено))
		Или ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЗначениеКопирования", Неопределено)) Тогда

		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "ФормаДокумента";

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	НомераТаблиц = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = ТекстЗапросаРеквизиты();
	ТаблицаРеквизиты = Запрос.Выполнить().Выгрузить();
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Если Не УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	МожноИспользоватьОперациюСИнвестиционнымВычетом =
		ИнвестиционныйНалоговыйВычет.МожноИспользоватьОперациюСИнвестиционнымВычетом(
			Реквизиты.Организация,
			Реквизиты.Период,
			Реквизиты.Регистратор,
			Истина);
			
	Если Не МожноИспользоватьОперациюСИнвестиционнымВычетом Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ЭтоОбособленноеПодразделение",
		ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоОбособленноеПодразделение(Реквизиты.Организация));
	Запрос.УстановитьПараметр("ВидОперацииПередача", Перечисления.ВидыОперацийСИнвестиционнымВычетом.Передача);
	Запрос.УстановитьПараметр("ВидОперацииПолучение", Перечисления.ВидыОперацийСИнвестиционнымВычетом.Получение);
	Запрос.УстановитьПараметр("СодержаниеПроводкиПередача",
		НСтр("ru = 'Передача инвестиционного вычета'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Запрос.УстановитьПараметр("СодержаниеПроводкиПолучение",
		НСтр("ru = 'Получение права на применение инвестиционного вычета'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Запрос.УстановитьПараметр("СодержаниеПроводкиПередачаГоловнойОрганизации",
		НСтр("ru = 'Передача инвестиционного вычета головной организации'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Запрос.УстановитьПараметр("СодержаниеПроводкиСторноПередачиГоловнойОрганизации",
		НСтр("ru = 'Сторно передачи инвестиционного вычета головной организации'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстЗапросаПередачаВычета(НомераТаблиц));
	ТекстыЗапроса.Добавить(ТекстЗапросаПолучениеВычета(НомераТаблиц));
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
КонецФункции

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	ДополнительныеРеквизиты = Новый Структура;
	ДополнительныеРеквизиты.Вставить("Информация", "Контрагент");
	ДополнительныеРеквизиты.Вставить("СуммаДокумента", "СуммаВычета");
	
	Возврат ДополнительныеРеквизиты;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРеквизиты()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Дата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ОперацияСИнвестиционнымВычетом КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПередачаВычета(НомераТаблиц)
	НомераТаблиц.Вставить("ПередачаВычета", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ОбъектВычета КАК АмортизируемоеИмущество,
	|	ТаблицаДокумента.ДокументРасходов КАК ДокументВычета,
	|	&ЭтоОбособленноеПодразделение КАК ЭтоОбособленноеПодразделение,
	|	&СодержаниеПроводкиПередача КАК СодержаниеПроводки,
	|	&СодержаниеПроводкиСторноПередачиГоловнойОрганизации КАК СодержаниеПроводкиПередачаГоловнойОрганизации
	|ИЗ
	|	Документ.ОперацияСИнвестиционнымВычетом КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.ВидОперации = &ВидОперацииПередача";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаПолучениеВычета(НомераТаблиц)
	
	НомераТаблиц.Вставить("ПолучениеВычета", НомераТаблиц.Количество());
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.ОбъектВычета КАК АмортизируемоеИмущество,
		|	ТаблицаДокумента.Ссылка КАК ДокументВычета,
		|	ТаблицаДокумента.ВидРасходов КАК ВидРасходов,
		|	ТаблицаДокумента.СуммаВычета КАК Сумма,
		|	&ЭтоОбособленноеПодразделение КАК ЭтоОбособленноеПодразделение,
		|	&СодержаниеПроводкиПолучение КАК СодержаниеПроводки,
		|	&СодержаниеПроводкиПередачаГоловнойОрганизации КАК СодержаниеПроводкиПередачаГоловнойОрганизации
		|ИЗ
		|	Документ.ОперацияСИнвестиционнымВычетом КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|	И ТаблицаДокумента.ВидОперации = &ВидОперацииПолучение";;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли
