
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	
	Движения.Записать();

	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.ОперацияСИнвестиционнымВычетом.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПередачаВычета = ИнвестиционныйНалоговыйВычет.ПодготовитьТаблицуПередачаИнвестиционногоВычета(
		ПараметрыПроведения.ПередачаВычета,
		ПараметрыПроведения.Реквизиты);
	
	ИнвестиционныйНалоговыйВычет.СформироватьДвиженияПередачаПолучение(
		ПараметрыПроведения.ПолучениеВычета,
		ПараметрыПроведения.Реквизиты,
		Движения);
		
	ИнвестиционныйНалоговыйВычет.СформироватьДвиженияПередачаПолучение(
		ТаблицаПередачаВычета,
		ПараметрыПроведения.Реквизиты,
		Движения);

	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	ЗаполнениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	ПредставлениеПоляКонтрагент = "";
	Если ВидОперации = Перечисления.ВидыОперацийСИнвестиционнымВычетом.Передача Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СуммаВычета");
		МассивНепроверяемыхРеквизитов.Добавить("ВидРасходов");
		ПредставлениеПоляКонтрагент = НСтр("ru = 'Получатель вычета'");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийСИнвестиционнымВычетом.Получение Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОбъектВычета");
		МассивНепроверяемыхРеквизитов.Добавить("ДокументРасходов");
		ПредставлениеПоляКонтрагент = НСтр("ru = 'Собственник имущества'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидОперации) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		Если Не ЗначениеЗаполнено(Контрагент) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , ПредставлениеПоляКонтрагент);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, ЭтотОбъект, "Контрагент", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
