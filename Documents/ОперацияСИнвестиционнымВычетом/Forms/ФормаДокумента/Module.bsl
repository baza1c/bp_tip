
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ПроведениеСервер.УстановитьПризнакПроверкиРеквизитов(Отказ, ТекущийОбъект,  ПараметрыЗаписи);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	УстановитьСостояниеДокумента();
	УправлениеФормой();
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСИнвестиционнымВычетом.Получение Тогда
		Элементы.СтраницыИнформацияОВычете.ТекущаяСтраница = Элементы.СтраницаПолучение;
		Элементы.Контрагент.Видимость = Ложь;
		// При получении вычета от других организаций заполнение объекта не обязательно, поскольку объекта нет в учете организации и
		// для учета вычета имеющихся в документе реквизитов достаточно, но если пользователь хочет создавать объекты-заглушки, например,
		// это может понадобиться для вывода реквизитов справочника в пользовательские отчеты, можно вывести элемент на форму, заполнение
		// в проводках автоматизировано. Элемент, который предполагается для вывода на форму - ОбъектВычетаПолучениеНастройка.
		// По умолчанию он скрыт настройками пользовательской видимости. При этом другой пользователь, который аналогичным образом не настроил форму,
		// увидит, что в проводках заполнено субконто Амортизируемое имущество, а на форме его нет. Чтобы этого избежать,
		// в случае если объект заполнен, безусловно выводим на форму другой реквизит: ОбъектВычетаПолучениеЗаполненноеЗначение,
		// у которого пользовательская видимость установлена в значение ИСТИНА, а настраиваемый реквизит скрываем.
		Если ЗначениеЗаполнено(Объект.ОбъектВычета) Тогда
			Элементы.ОбъектВычетаПолучениеЗаполненноеЗначение.Видимость = Истина;
			Элементы.ОбъектВычетаПолучениеНастройка.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	НаименованиеДокумента = ?(Объект.ВидОперации = Перечисления.ВидыОперацийСИнвестиционнымВычетом.Передача,
		НСтр("ru = 'Передача инвестиционного вычета'"),
		НСтр("ru = 'Получение права на применение инвестиционного вычета'"));
	
	ТекстЗаголовка = ?(ЗначениеЗаполнено(Объект.Ссылка),
		СтрШаблон(НСтр("ru='%1 %2 от %3'"), НаименованиеДокумента, Объект.Номер, Объект.Дата),
		СтрШаблон(НСтр("ru='%1 (создание)'"), НаименованиеДокумента));
	
	Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

#КонецОбласти
