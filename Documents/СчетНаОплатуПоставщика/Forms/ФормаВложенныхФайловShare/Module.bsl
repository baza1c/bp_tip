
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВложенныеФайлы = ПолучитьИзВременногоХранилища(Параметры.АдресВложенныхФайлов).Получить();
	Для Каждого ДанныеФайла Из ВложенныеФайлы Цикл
		СтрокаСписка = СписокФайлов.Добавить();
		СтрокаСписка.ИмяФайла = ДанныеФайла.ИмяФайла;
		СтрокаСписка.РазмерФайла = ОбщегоНазначенияБПКлиентСервер.ПредставлениеРазмераФайла(
			ДанныеФайла.ДвоичныеДанныеФайла.Размер());
		СтрокаСписка.ИндексПиктограммы = ИнтеграцияShareКлиентСервер.ПолучитьИндексПиктограммыФайла(
			Данныефайла.РасширениеБезТочки);
		СтрокаСписка.АдресХранилищаФайла = ПоместитьВоВременноеХранилище(
			ДанныеФайла.ДвоичныеДанныеФайла, УникальныйИдентификатор);
	КонецЦикла;
	
	УдалитьИзВременногоХранилища(Параметры.АдресВложенныхФайлов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицы

&НаКлиенте
Процедура СписокФайловПриАктивизацииЯчейки(Элемент)
	
	// Уберем выделение строк, чтобы таблица выглядела как обычные декорации.
	Элементы.СписокФайлов.ВыделенныеСтроки.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрисоединитьКДокументу(Команда)
	
	ПрисоединитьКДокументуНаСервере();
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Файлы присоединены'"),,
		СтрШаблон(НСтр("ru = 'Файлы присоединены к документу %1'"), Параметры.Владелец),,
		СтатусОповещенияПользователя.Информация);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаДиск(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ВыборКаталогаЗавершение", ЭтотОбъект);
	ФайловаяСистемаКлиент.ВыбратьКаталог(Обработчик);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрисоединитьКДокументуНаСервере()
	
	Для Каждого СтрокаСписка Из СписокФайлов Цикл
		ИмяРасширение = ИмяРасширениеФайла(СтрокаСписка.ИмяФайла);
		
		ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
		ПараметрыФайла.ВладелецФайлов = Параметры.Владелец;
		ПараметрыФайла.ИмяБезРасширения = ИмяРасширение.ИмяБезРасширения;
		ПараметрыФайла.РасширениеБезТочки = ИмяРасширение.РасширениеБезТочки;
		
		РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, СтрокаСписка.АдресХранилищаФайла);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаЗавершение(ПутьККаталогу, ДополнительныеПараметры) Экспорт
	
	Для Каждого СтрокаСписка Из СписокФайлов Цикл
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(СтрокаСписка.АдресХранилищаФайла);
		ДвоичныеДанные.Записать(ПутьККаталогу + ПолучитьРазделительПути() + СтрокаСписка.ИмяФайла);
	КонецЦикла;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Файлы сохранены'"),,
		СтрШаблон(НСтр("ru = 'Файлы сохранены в папке %1'"), ПутьККаталогу),,
		СтатусОповещенияПользователя.Информация);
	
	Закрыть();
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяРасширениеФайла(ИмяФайла)
	
	ИмяРасширение = Новый Структура("ИмяБезРасширения, РасширениеБезТочки", "", "");
	
	ИмяРасширение.РасширениеБезТочки = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайла);
	Если ПустаяСтрока(ИмяРасширение.РасширениеБезТочки) Тогда
		ИмяРасширение.ИмяБезРасширения = ИмяФайла;
	Иначе
		ИмяРасширение.ИмяБезРасширения = Лев(ИмяФайла, СтрДлина(ИмяФайла)
			- СтрДлина(ИмяРасширение.РасширениеБезТочки) - 1);
	КонецЕсли;
	
	Возврат ИмяРасширение;
	
КонецФункции

#КонецОбласти