#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 22, 0);
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Функция ПодготовитьДанныеДляЗаполнения(СтруктураПараметров) Экспорт

	СчетаУчетаДенежныхСредствОрганизации = Новый Массив;
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.ЦифровойРубль);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизации);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.ОперационнаяКасса);
	СчетаУчетаДенежныхСредствОрганизации.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизацииВал);

	СубконтоЗаявленияОВвозе = Новый Массив;
	СубконтоЗаявленияОВвозе.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	СубконтоЗаявленияОВвозе.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	СубконтоЗаявленияОВвозе.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода",              КонецДня(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("КонецПериодаГраница",       Новый Граница(КонецДня(СтруктураПараметров.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СчетУчетаКосвенногоНалога", ПланыСчетов.Хозрасчетный.НДСТаможенныйСоюзКУплате);
	Запрос.УстановитьПараметр("СубконтоЗаявлениеОВвозе",   СубконтоЗаявленияОВвозе);
	Запрос.УстановитьПараметр("Организация",               СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("СчетаУчетаДенежныхСредств", СчетаУчетаДенежныхСредствОрганизации);
	Запрос.УстановитьПараметр("Основание",                 СтруктураПараметров.Основание);
	Запрос.УстановитьПараметр("ВерсияПостановления1137",   УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("КонтролироватьОплату",      УчетНДС.КонтролироватьОплатуНДСПоЗаявлениюОВвозе(СтруктураПараметров.Дата));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСПредъявленныйОбороты.СчетФактура КАК ЗаявлениеОВвозе
	|ПОМЕСТИТЬ ВсеЗаявленияОВвозе
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Обороты(
	|			,
	|			&КонецПериодаГраница,
	|			Регистратор,
	|			Организация = &Организация
	|				И ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныйСоюз)
	|				И &УсловиеОснованиеСчетФактура) КАК НДСПредъявленныйОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСПредъявленныйОбороты.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КнигаПокупок.СчетФактура КАК ЗаявлениеОВвозе
	|ПОМЕСТИТЬ КнигаПокупокВЧастиЗаявленийОВвозе
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК КнигаПокупок
	|ГДЕ
	|	КнигаПокупок.Организация = &Организация
	|	И КнигаПокупок.Период <= &КонецПериода
	|	И КнигаПокупок.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныйСоюз)
	|	И &УсловиеОснованиеКнигаПокупок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КнигаПокупок.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВсеЗаявленияОВвозе.ЗаявлениеОВвозе КАК ЗаявлениеОВвозе,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаЗаявления
	|ПОМЕСТИТЬ НеподтвержденныеЗаявленияОВвозе
	|ИЗ
	|	ВсеЗаявленияОВвозе КАК ВсеЗаявленияОВвозе
	|		ЛЕВОЕ СОЕДИНЕНИЕ КнигаПокупокВЧастиЗаявленийОВвозе КАК КнигаПокупокВЧастиЗаявленийОВвозе
	|		ПО (КнигаПокупокВЧастиЗаявленийОВвозе.ЗаявлениеОВвозе = ВсеЗаявленияОВвозе.ЗаявлениеОВвозе)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ВсеЗаявленияОВвозе.ЗаявлениеОВвозе = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	КнигаПокупокВЧастиЗаявленийОВвозе.ЗаявлениеОВвозе ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВсеЗаявленияОВвозе.ЗаявлениеОВвозе
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Субконто3 КАК ЗаявлениеОВвозе,
	|	ХозрасчетныйОбороты.Регистратор КАК ДокументОплаты
	|ПОМЕСТИТЬ ВТ_ДатыОплат
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			,
	|			&КонецПериода,
	|			Регистратор,
	|			Счет = &СчетУчетаКосвенногоНалога,
	|			&СубконтоЗаявлениеОВвозе,
	|			Организация = &Организация
	|				И &УсловиеОснованиеСубконто3,
	|			КорСчет В (&СчетаУчетаДенежныхСредств),
	|			) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборотДт > 0
	|	И &КонтролироватьОплату
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявленияОВвозе.ЗаявлениеОВвозе КАК ЗаявлениеОВвозе,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокументаОплаты,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокументаОплаты
	|ИЗ
	|	НеподтвержденныеЗаявленияОВвозе КАК ЗаявленияОВвозе
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыОплат КАК ДатыОплат
	|		ПО (ЗаявленияОВвозе.ЗаявлениеОВвозе = ДатыОплат.ЗаявлениеОВвозе)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (ДатыОплат.ДокументОплаты = ДанныеПервичныхДокументов.Документ)
	|ГДЕ
	|	(НЕ ДатыОплат.ДокументОплаты ЕСТЬ NULL
	|			ИЛИ НЕ &КонтролироватьОплату)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаявленияОВвозе.ДатаЗаявления,
	|	ДатаДокументаОплаты";
	
	Если СтруктураПараметров.Основание = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОснованиеСчетФактура",  "Истина");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОснованиеКнигаПокупок", "Истина");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОснованиеСубконто3",    "Истина");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОснованиеСчетФактура",  "СчетФактура = &Основание");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОснованиеКнигаПокупок", "КнигаПокупок.СчетФактура = &Основание");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОснованиеСубконто3",    "Субконто3 = &Основание");
	КонецЕсли; 
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Документ_ПодтверждениеОплатыНДСВБюджет";
	Набор.Идентификатор = Новый УникальныйИдентификатор("322d2362-272a-442c-9504-1c82d9144375");
	Набор.Используется  = ПолучитьФункциональнуюОпцию("ВедетсяУчетИмпортаТоваров");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();
	Результат = Запрос.Выполнить();

	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);
 
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", 
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	Запрос.УстановитьПараметр("ПравилаПостановления981", 
		УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Реквизиты.Период) >= 4);
	
	ПараметрыПроведения.Вставить("ТаблицаРеквизиты", Результат.Выгрузить());
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаОплатаПодтверждена(НомераТаблиц)
		+ ТекстЗапросаРегистрацияЗаявленияНалоговымОрганом(НомераТаблиц)
		+ ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
		
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ПодтверждениеОплатыНДСВБюджет КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса ;
	
КонецФункции

Функция ТекстЗапросаОплатаПодтверждена(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаОплатаПодтверждена", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПодтверждениеОплатыНДСВБюджетСостав.ЗаявлениеОВвозе,
	|	ПодтверждениеОплатыНДСВБюджетСостав.ДатаДокументаОплаты КАК ДатаДокументаОплаты,
	|	ПодтверждениеОплатыНДСВБюджетСостав.НомерДокументаОплаты КАК НомерДокументаОплаты
	|ИЗ
	|	Документ.ПодтверждениеОплатыНДСВБюджет.Состав КАК ПодтверждениеОплатыНДСВБюджетСостав
	|ГДЕ
	|	ПодтверждениеОплатыНДСВБюджетСостав.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаРегистрацияЗаявленияНалоговымОрганом(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаРегистрацияЗаявленияНалоговымОрганомПредварительная", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаЗаявленияОВвозеТоваровДляКомитента",                  НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаРегистрацияЗаявленияНалоговымОрганом",                НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПодтверждениеОплатыНДСВБюджетСостав.ЗаявлениеОВвозе КАК ЗаявлениеОВвозе,
	|	ПодтверждениеОплатыНДСВБюджетСостав.НомерОтметкиОРегистрации КАК НомерОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДСВБюджетСостав.ДатаОтметкиОРегистрации КАК ДатаОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДСВБюджет.Дата КАК Дата,
	|	ПодтверждениеОплатыНДСВБюджет.Ссылка КАК Ссылка,
	|	ПодтверждениеОплатыНДСВБюджет.Организация КАК Организация,
	|	МАКСИМУМ(ПодтверждениеОплатыНДСВБюджетСостав.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ПодтверждениеОплатыНДС
	|ИЗ
	|	Документ.ПодтверждениеОплатыНДСВБюджет КАК ПодтверждениеОплатыНДСВБюджет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодтверждениеОплатыНДСВБюджет.Состав КАК ПодтверждениеОплатыНДСВБюджетСостав
	|		ПО ПодтверждениеОплатыНДСВБюджет.Ссылка = ПодтверждениеОплатыНДСВБюджетСостав.Ссылка
	|ГДЕ
	|	ПодтверждениеОплатыНДСВБюджетСостав.НомерОтметкиОРегистрации <> """"
	|	И ПодтверждениеОплатыНДСВБюджетСостав.ДатаОтметкиОРегистрации <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ПодтверждениеОплатыНДСВБюджетСостав.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждениеОплатыНДСВБюджетСостав.ЗаявлениеОВвозе,
	|	ПодтверждениеОплатыНДСВБюджетСостав.НомерОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДСВБюджетСостав.ДатаОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДСВБюджет.Ссылка,
	|	ПодтверждениеОплатыНДСВБюджет.Организация,
	|	ПодтверждениеОплатыНДСВБюджет.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявлениеОВвозеТоваровТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЗаявленияОВВозеТоваровДляКомитента
	|ИЗ
	|	ПодтверждениеОплатыНДС КАК ПодтверждениеОплатыНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОВвозеТоваров.Товары КАК ЗаявлениеОВвозеТоваровТовары
	|		ПО ПодтверждениеОплатыНДС.ЗаявлениеОВвозе = ЗаявлениеОВвозеТоваровТовары.Ссылка
	|ГДЕ
	|	ЗаявлениеОВвозеТоваровТовары.СчетУчета.Забалансовый
	|	И ЗаявлениеОВвозеТоваровТовары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И ЗаявлениеОВвозеТоваровТовары.Ссылка.Дата >= ДАТАВРЕМЯ(2024, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтверждениеОплатыНДС.ЗаявлениеОВвозе КАК СчетФактура,
	|	ПодтверждениеОплатыНДС.НомерОтметкиОРегистрации КАК НомерСчетаФактуры,
	|	ПодтверждениеОплатыНДС.ДатаОтметкиОРегистрации КАК ДатаСчетаФактуры,
	|	ЗаявлениеОВвозеТоваровТовары.Ссылка.Контрагент КАК Контрагент,
	|	""19"" КАК КодВидаОперации,
	|	ПодтверждениеОплатыНДС.Дата КАК Период,
	|	ПодтверждениеОплатыНДС.Дата КАК ДатаВыставленияПолучения,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры) КАК ЧастьЖурнала,
	|	СУММА(ЗаявлениеОВвозеТоваровТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА &ПравилаПостановления981
	|				ТОГДА ЗаявлениеОВвозеТоваровТовары.НалоговаяБазаНДС
	|			ИНАЧЕ ЗаявлениеОВвозеТоваровТовары.НалоговаяБазаНДС + ЗаявлениеОВвозеТоваровТовары.СуммаНДС
	|		КОНЕЦ) КАК СуммаПоСчетуФактуре,
	|	ПодтверждениеОплатыНДС.Ссылка КАК Регистратор,
	|	ПодтверждениеОплатыНДС.Организация КАК Организация,
	|	"""" КАК КППКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК Продавец,
	|	НЕОПРЕДЕЛЕНО КАК КодСпособаВыставленияПолучения,
	|	НЕОПРЕДЕЛЕНО КАК НомерКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	&ВалютаРеглУчета КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА ЗаявлениеОВвозеТоваровТовары.СчетУчета.Забалансовый
	|					И ЗаявлениеОВвозеТоваровТовары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ЗаявлениеОВвозеТоваровТовары.СуммаНДС
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНДСКомиссия,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ЗаявленияОВВозеТоваровДляКомитента.Ссылка ЕСТЬ NULL
	|				ТОГДА ЗаявлениеОВвозеТоваровТовары.НалоговаяБазаНДС
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПоСчетуФактуреКомиссия,
	|	0 КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	0 КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	0 КАК СуммаНДСРазницаУвеличение,
	|	0 КАК СуммаНДСРазницаУменьшение,
	|	0 КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	0 КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	0 КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	0 КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	ЛОЖЬ КАК ПоСтавкеБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураНеВыставляется,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	0 КАК ИндексСтроки,
	|	НЕОПРЕДЕЛЕНО КАК Субкомиссионер,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураВыданныйПокупателю,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявлениеОВвозеТоваровТовары.СчетУчета.Забалансовый
	|					И ЗаявлениеОВвозеТоваровТовары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ""1""
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ) КАК КодВидаСделки,
	|	НЕОПРЕДЕЛЕНО КАК Посредник,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактурыПродавца,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактурыПродавца,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявлениеОВвозеТоваровТовары.СчетУчета.Забалансовый
	|					И ЗаявлениеОВвозеТоваровТовары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ""01""
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ) КАК КодВидаОперацииКомиссия,
	|	"""" КАК КодВидаОперацииНаУменьшение,
	|	ЛОЖЬ КАК ИсправлениеСобственнойОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйСчетФактура,
	|	"""" КАК ИННКонтрагента,
	|	ЛОЖЬ КАК Сторно,
	|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка) КАК СчетФактураПолученныйОтПродавца,
	|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка) КАК ИсправленныйСчетФактура,
	|	"""" КАК ИННПродавца,
	|	"""" КАК КПППродавца,
	|	"""" КАК ИННСубкомиссионера,
	|	"""" КАК КППСубкомиссионера,
	|	ПодтверждениеОплатыНДС.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ПодтверждениеОплатыНДС КАК ПодтверждениеОплатыНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОВвозеТоваров.Товары КАК ЗаявлениеОВвозеТоваровТовары
	|		ПО ПодтверждениеОплатыНДС.ЗаявлениеОВвозе = ЗаявлениеОВвозеТоваровТовары.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаявленияОВВозеТоваровДляКомитента КАК ЗаявленияОВВозеТоваровДляКомитента
	|		ПО ПодтверждениеОплатыНДС.ЗаявлениеОВвозе = ЗаявленияОВВозеТоваровДляКомитента.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждениеОплатыНДС.ЗаявлениеОВвозе,
	|	ПодтверждениеОплатыНДС.НомерОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДС.ДатаОтметкиОРегистрации,
	|	ЗаявлениеОВвозеТоваровТовары.Ссылка.Контрагент,
	|	ПодтверждениеОплатыНДС.Ссылка,
	|	ПодтверждениеОплатыНДС.Дата,
	|	ПодтверждениеОплатыНДС.Организация,
	|	ПодтверждениеОплатыНДС.НомерСтроки,
	|	ПодтверждениеОплатыНДС.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтверждениеОплатыНДС.ЗаявлениеОВвозе,
	|	ПодтверждениеОплатыНДС.НомерОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДС.ДатаОтметкиОРегистрации,
	|	ЗаявлениеОВвозеТоваровТовары.Контрагент,
	|	""01"",
	|	ПодтверждениеОплатыНДС.Дата,
	|	ПодтверждениеОплатыНДС.Дата,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ВыставленныеСчетаФактуры),
	|	СУММА(ЗаявлениеОВвозеТоваровТовары.СуммаНДС),
	|	СУММА(ЗаявлениеОВвозеТоваровТовары.НалоговаяБазаНДС),
	|	ПодтверждениеОплатыНДС.Ссылка,
	|	ПодтверждениеОплатыНДС.Организация,
	|	"""",
	|	ЗаявлениеОВвозеТоваров.Контрагент,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	&ВалютаРеглУчета,
	|	СУММА(ЗаявлениеОВвозеТоваровТовары.СуммаНДС),
	|	СУММА(ЗаявлениеОВвозеТоваровТовары.НалоговаяБазаНДС),
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗаявлениеОВвозеТоваровТовары.Контрагент,
	|	ПодтверждениеОплатыНДС.НомерОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДС.ДатаОтметкиОРегистрации,
	|	""01"",
	|	"""",
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	ЛОЖЬ,
	|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка),
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	ПодтверждениеОплатыНДС.НомерСтроки
	|ИЗ
	|	ПодтверждениеОплатыНДС КАК ПодтверждениеОплатыНДС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОВвозеТоваров КАК ЗаявлениеОВвозеТоваров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОВвозеТоваров.Товары КАК ЗаявлениеОВвозеТоваровТовары
	|			ПО ЗаявлениеОВвозеТоваров.Ссылка = ЗаявлениеОВвозеТоваровТовары.Ссылка
	|		ПО ПодтверждениеОплатыНДС.ЗаявлениеОВвозе = ЗаявлениеОВвозеТоваров.Ссылка
	|ГДЕ
	|	ЗаявлениеОВвозеТоваровТовары.СчетУчета.Забалансовый
	|	И ЗаявлениеОВвозеТоваровТовары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И ЗаявлениеОВвозеТоваров.Дата >= ДАТАВРЕМЯ(2024, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждениеОплатыНДС.ЗаявлениеОВвозе,
	|	ЗаявлениеОВвозеТоваровТовары.Контрагент,
	|	ПодтверждениеОплатыНДС.НомерОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДС.ДатаОтметкиОРегистрации,
	|	ЗаявлениеОВвозеТоваров.Контрагент,
	|	ПодтверждениеОплатыНДС.Ссылка,
	|	ПодтверждениеОплатыНДС.Дата,
	|	ПодтверждениеОплатыНДС.Организация,
	|	ПодтверждениеОплатыНДС.НомерСтроки,
	|	ПодтверждениеОплатыНДС.Дата,
	|	ЗаявлениеОВвозеТоваровТовары.Контрагент,
	|	ПодтверждениеОплатыНДС.НомерОтметкиОРегистрации,
	|	ПодтверждениеОплатыНДС.ДатаОтметкиОРегистрации
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПодтверждениеОплатыНДС.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоФормированиюРегламентнойОперации(НомераТаблиц)

	НомераТаблиц.Вставить("ДанныеРегламентнойОперации", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(Реквизиты.Дата, КВАРТАЛ) КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РегламентныеОперации.ПодтверждениеОплатыНДСВБюджет) КАК РегламентнаяОперация
	|ИЗ
	|	Документ.ПодтверждениеОплатыНДСВБюджет КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти

#Область ПечатьДокумента

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли