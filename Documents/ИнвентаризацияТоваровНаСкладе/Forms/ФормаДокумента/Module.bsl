
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ВестиСкладскойУчетБУ = БухгалтерскийУчет.ВедетсяУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары);
	Элементы.Склад.Видимость = ВестиСкладскойУчетБУ;
	
	Если Параметры.Ключ.Пустая() Тогда
		Параметры.Свойство("ЗаполнениеИзПомощника", ЗаполнениеИзПомощника);
		Если ЗаполнениеИзПомощника Тогда
			ЗаполнитьЗначенияСвойств(Объект, Параметры, "Организация, Дата");
			Если Параметры.Свойство("СкладПодраздела") И ВестиСкладскойУчетБУ Тогда
				Объект.Склад = Параметры.СкладПодраздела;
			КонецЕсли;
			Если Параметры.Свойство("СчетУчетаПодраздела") И ЗначениеЗаполнено(Параметры.СчетУчетаПодраздела) Тогда
				Объект.СчетУчета = Параметры.СчетУчетаПодраздела;
			КонецЕсли;
		КонецЕсли;
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	ИспользоватьПодключаемоеОборудование = МенеджерОборудованияБП.ИспользуетсяОборудование("СканерШтрихкода");
	
	УстановитьВидимостьСчетовУчета();
	
	УстановитьУсловноеОформление();
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Документ.ИнвентаризацияТоваровНаСкладе",
		"ФормаДокумента",
		НСтр("ru='Новости: Инвентаризация товаров'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере

	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
	УстановитьВидимостьГруппыКнопокТСД();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ТолькоПросмотр И ИспользоватьПодключаемоеОборудование Тогда
		// Попробуем подключить сканер штрихкода
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	Если ЗаполнениеИзПомощника Тогда
		Если ЗначениеЗаполнено(Объект.СчетУчета) Тогда
			НазваниеДействия = "ЗаполнитьПоСчету";
		Иначе
			НазваниеДействия = "Заполнить";
		КонецЕсли;
		ЗаполнитьПоАктуальнымОстаткам(НазваниеДействия, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтаФорма);
	// Конец ПодключаемоеОборудование
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				ТекущийКод = Параметр[0];
			Иначе
				ТекущийКод = Параметр[1][1];
			КонецЕсли;
			ДобавитьПоШтрихкодуНаСервере(ТекущийКод);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ДанныеСкопированыВБуферОбмена" Тогда
		
		УстановитьДоступностьКомандыВставки(ЭтотОбъект, Истина);
		
	КонецЕсли;
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда	
		ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыЭДОПриСоздании = ОбменСКонтрагентами.ПараметрыПриЧтенииНаСервере_ФормаДокумента();
	ПараметрыЭДОПриСоздании.Форма = ЭтотОбъект;
	ПараметрыЭДОПриСоздании.ДокументСсылка = Объект.Ссылка;
	ПараметрыЭДОПриСоздании.МестоРазмещенияКоманд = Элементы.КомандыЭДО;
	ПараметрыЭДОПриСоздании.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыЭДОПриСоздании.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	ОбменСКонтрагентами.ПриЧтенииНаСервере_ФормаДокумента(ПараметрыЭДОПриСоздании);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "ПроведениеИнвентаризацияТоваровНаСкладе";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	УстановитьСостояниеДокумента();
	
	ЗаполнитьДобавленныеКолонкиТаблиц();

	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Организация", Объект.Организация);
	Если ЗначениеЗаполнено(Объект.СчетУчета) Тогда
		ПараметрыОповещения.Вставить("РазделУчета", ПредопределенноеЗначение("Перечисление.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета"));
	Иначе
		ПараметрыОповещения.Вставить("РазделУчета", ПредопределенноеЗначение("Перечисление.РазделыУчетаДляИнвентаризации.Запасы"));
	КонецЕсли;
	ПараметрыОповещения.Вставить("СкладПодраздела", Объект.Склад);
	ПараметрыОповещения.Вставить("СчетУчетаПодраздела", Объект.СчетУчета);
	Оповестить("ОбновитьФормуПомощникаИнвентаризации", ПараметрыОповещения);

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами	
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтаФорма, ПараметрыЗаписи);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры


#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(
		Объект.Дата, ТекущаяДатаДокумента);
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		СкладПриИзмененииНаСервере();
	КонецЕсли;

	Если ЗначениеЗаполнено(Объект.Склад) И Объект.Товары.Количество() <> 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'Перезаполнить учетные количества и суммы? %ПредупреждениеОЗаписиДокумента%'");
		ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ПредупреждениеОЗаписиДокумента%", 
			?(Модифицированность, Символы.ПС + НСтр("ru='(Перед заполнением документ будет записан!)'"), 
			""));
			
		Оповещение = Новый ОписаниеОповещения("ВопросПерезаполнитьКоличествоСуммуПриИзмененииСкладаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Заголовок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственноеЛицоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ОтветственноеЛицо) И Объект.Товары.Количество() <> 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'Перезаполнить учетные количества и суммы? %ПредупреждениеОЗаписиДокумента%'");
		ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ПредупреждениеОЗаписиДокумента%",
			?(Модифицированность, Символы.ПС + НСтр("ru='(Перед заполнением документ будет записан!)'"),
			""));
			
		Оповещение = Новый ОписаниеОповещения("ВопросПерезаполнитьКоличествоСуммуПриИзмененииОтветственногоЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Заголовок);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НЕ ОтменаРедактирования Тогда
		Элемент.ТекущиеДанные.Отклонение = Элемент.ТекущиеДанные.Количество 
											- Элемент.ТекущиеДанные.КоличествоУчет;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)

	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ТекущиеДанные.Цена = 0;
	РассчитатьСуммуТабЧасти(ТекущиеДанные);
	
	ПараметрыЗаполненияСчетовУчета = НачатьЗаполнениеСчетовУчета("Товары.Номенклатура", Объект, ТекущиеДанные);
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	СтрокаТабличнойЧасти.Сумма = РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоУчетПриИзменении(Элемент)
	
	Элементы.Товары.ТекущиеДанные.СуммаУчет = Элементы.Товары.ТекущиеДанные.Цена 
											* Элементы.Товары.ТекущиеДанные.КоличествоУчет;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	СтрокаТабличнойЧасти.Цена = ?(СтрокаТабличнойЧасти.Количество = 0, 0, 
									СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Товары, Материалы");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Товары, Материалы");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыИнвентаризационнаяКомиссия

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НЕ ОтменаРедактирования Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		ПроверитьФлагиПредседателя(Элемент.ТекущиеДанные);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПослеУдаления(Элемент)
	
	Если Объект.ИнвентаризационнаяКомиссия.Количество() > 0 Тогда
		ПроверитьФлагиПредседателя(Неопределено);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Копирование Тогда
		Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные.ФизЛицо = Неопределено;
		Элементы.ИнвентаризационнаяКомиссия.ТекущиеДанные.Председатель = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтрокДоВыбора = Объект.ИнвентаризационнаяКомиссия.Количество();
	Для каждого ВыбранныйЭлемент Из ВыбранноеЗначение Цикл
		
		СтрокиФизЛица = Объект.ИнвентаризационнаяКомиссия.НайтиСтроки(Новый Структура("ФизЛицо", ВыбранныйЭлемент));
		Если СтрокиФизЛица.Количество() = 0 Тогда
			СтрокаФизЛица = Объект.ИнвентаризационнаяКомиссия.Добавить();
			СтрокаФизЛица.ФизЛицо = ВыбранныйЭлемент;
		Иначе
			СтрокаФизЛица = СтрокиФизЛица[0];
			ИндексСтроки = Объект.ИнвентаризационнаяКомиссия.Индекс(СтрокаФизЛица);
			ИмяПоля = "ИнвентаризационнаяКомиссия[" + Формат(ИндексСтроки, "ЧН=0; ЧГ=") + "].ФизЛицо"; 
			ТекстСообщения = НСтр("ru='Физическое лицо %1 уже добавлено в список комиссии'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ВыбранныйЭлемент);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли; 
		Элементы.ИнвентаризационнаяКомиссия.ТекущаяСтрока = СтрокаФизЛица;
	
	КонецЦикла;
	
	Если СтрокДоВыбора = 0 И Объект.ИнвентаризационнаяКомиссия.Количество() > 0 Тогда
		Объект.ИнвентаризационнаяКомиссия[0].Председатель = Истина;
	КонецЕсли; 
	
	Модифицированность = Истина;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияФизЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	ИнвентаризацияАктивовОбязательствКлиент.ИнвентаризационнаяКомиссияИсключитьДублиФизическихЛиц(
		Объект.ИнвентаризационнаяКомиссия,
		Элементы.ИнвентаризационнаяКомиссия,
		ВыбранноеЗначение,
		СтандартнаяОбработка);
		
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением табличная часть будет очищена. Заполнить?'");
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоОстаткамЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Заголовок);
	Иначе
		ЗаполнитьДокументНаКлиенте("Заполнить");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьУчетныеДанные(Команда)
	
	Если Объект.Товары.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Перезаполнить учетные количества и суммы? %ПредупреждениеОЗаписиДокумента%'");
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ПредупреждениеОЗаписиДокумента%", 
		?(Модифицированность, Символы.ПС + НСтр("ru='(Перед заполнением документ будет записан!)'"), 
		""));
	Оповещение = Новый ОписаниеОповещения("ВопросПерезаполнитьУчетныеДанныеЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборКомиссии(Команда)
	
	ПараметрыФормы = Новый Структура("ЗакрыватьприВыборе,МножественныйВыбор,РежимВыбора", Ложь, Истина, Истина);
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаСписка", ПараметрыФормы, Элементы.ИнвентаризационнаяКомиссия);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборТовары(Команда)
	
	ПараметрыПодбора = ПолучитьПараметрыПодбора("Товары");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора, 
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьФакт(Команда)
	
	ОчиститьФактНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКомиссиюИзПредыдущегоДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Организация'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Организация");
		Возврат;
	КонецЕсли;
	
	ТекстПредупреждения = ЗаполнитьИнвентаризационнуюКомиссию();
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(,ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткамНаСчете(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением табличная часть будет очищена. Заполнить?'");
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьПоОстаткамНаСчетеЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Заголовок);
	Иначе
		ОповещениеВыборСчета = Новый ОписаниеОповещения("ВыборСчетаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.ИнвентаризацияТоваровНаСкладе.Форма.ВыборСчета",,ЭтотОбъект,,,,ОповещениеВыборСчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();


	// ТоварыЦенаВРознице

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыЦенаВРознице");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТипСклада", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыЦена

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыЦена");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТипСклада", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыКоличество, ТоварыКоличествоУчет

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКоличество");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКоличествоУчет");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.Количество", ВидСравненияКомпоновкиДанных.Заполнено);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.КоличествоУчет", ВидСравненияКомпоновкиДанных.Заполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ИспользуетсяОтложенноеПроведение = ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Объект.Организация, Объект.Дата);

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	ЗаполнениеДокументов.ПриИзмененииЗначенияОрганизации(Объект, Пользователи.ТекущийПользователь());
	
	ИспользуетсяОтложенноеПроведение = ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Объект.Организация, Объект.Дата);
	
	Изменения = Новый Массив;
	Изменения.Добавить("Организация");
	Изменения.Добавить("ПодразделениеОрганизации");
	Изменения.Добавить("ДоговорКонтрагента");
	
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(НачатьЗаполнениеСчетовУчета(Изменения, Объект));
	
КонецПроцедуры

&НаСервере
Процедура СкладПриИзмененииНаСервере()

	ТипСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Склад, "ТипСклада");
	Объект.ОтветственноеЛицо = ОтветственныеЛицаБП.ОтветственноеЛицоНаСкладе(Объект.Склад, Объект.Дата);
	
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(НачатьЗаполнениеСчетовУчета("Склад", Объект));

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьФлагиПредседателя(СтрокаТЧ)

	СтрокаПредседателя = ?(СтрокаТЧ <> Неопределено И СтрокаТЧ.Председатель, СтрокаТЧ, Неопределено);
	
	Для Каждого СтрокаКомиссии Из Объект.ИнвентаризационнаяКомиссия Цикл
		
		Если СтрокаКомиссии.Председатель И СтрокаПредседателя = Неопределено Тогда
			СтрокаПредседателя = СтрокаКомиссии;
			Продолжить;
		КонецЕсли;	
		
		Если СтрокаКомиссии.Председатель И СтрокаКомиссии <> СтрокаПредседателя Тогда
			СтрокаКомиссии.Председатель = Ложь;
		КонецЕсли;	
		
	КонецЦикла;	

	Если СтрокаПредседателя = Неопределено И Объект.ИнвентаризационнаяКомиссия.Количество() > 0 Тогда
		Объект.ИнвентаризационнаяКомиссия[0].Председатель = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()

	Для каждого СтрокаТаблицы Из Объект.Товары Цикл
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТаблицы);
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТаблицы)

	СтрокаТаблицы.Отклонение = СтрокаТаблицы.Количество - СтрокаТаблицы.КоличествоУчет;

КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыПодбора(ИмяТаблицы)

	ПараметрыФормы = Новый Структура;
	
	ДатаРасчетов 	 = ?(НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДата()), Неопределено, Объект.Дата);
	ЗаголовокПодбора = НСтр("ru = 'Подбор номенклатуры в %1 (%2)'");
	
	Если ТипСклада = ПредопределенноеЗначение("Перечисление.ТипыСкладов.НеавтоматизированнаяТорговаяТочка") Тогда
		Параметрыформы.Вставить("ПоказыватьЦены", Истина);
	КонецЕсли;
	
	ПредставлениеТаблицы = НСтр("ru = 'Товары'");
	
	ПараметрыФормы.Вставить("ПоказыватьОстатки"    , Истина);
	ПараметрыФормы.Вставить("КомандаВыбратьОстаток", Истина);
	
	ЗаголовокПодбора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокПодбора, Объект.Ссылка, ПредставлениеТаблицы);
	
	ПараметрыФормы.Вставить("ЕстьЦена"      , Истина);
	ПараметрыФормы.Вставить("ЕстьКоличество", Истина);
	ПараметрыФормы.Вставить("ДатаРасчетов"  , ДатаРасчетов);
	ПараметрыФормы.Вставить("Валюта"        , ВалютаРегламентированногоУчета);
	ПараметрыФормы.Вставить("Организация"   , Объект.Организация);
	ПараметрыФормы.Вставить("Склад"         , Объект.Склад);
	ПараметрыФормы.Вставить("Заголовок"     , ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВидПодбора"    , ПолучитьВидПодбора(ИмяТаблицы));
	ПараметрыФормы.Вставить("ИмяТаблицы"    , ИмяТаблицы);
	ПараметрыФормы.Вставить("Услуги"        , ИмяТаблицы = "Услуги");
	
	Возврат ПараметрыФормы;

КонецФункции

&НаКлиенте
Функция ПолучитьВидПодбора(ИмяТаблицы)
	
	ВидПодбора = "";
	
	Если ТипСклада = ПредопределенноеЗначение("Перечисление.ТипыСкладов.НеавтоматизированнаяТорговаяТочка") Тогда
		ВидПодбора = "НТТ";
	КонецЕсли;
	
	Возврат ВидПодбора;	

КонецФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	ТекущаяДатаДокумента = Объект.Дата;
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	ИспользуетсяОтложенноеПроведение = ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Объект.Организация, Объект.Дата);
	
	УстановитьСостояниеДокумента();
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ТипСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Склад, "ТипСклада");

	Элементы.ДокументОснованиеВид.СписокВыбора.Очистить();
	Элементы.ДокументОснованиеВид.СписокВыбора.Добавить("Приказ");
	Элементы.ДокументОснованиеВид.СписокВыбора.Добавить("Постановление");
	Элементы.ДокументОснованиеВид.СписокВыбора.Добавить("Распоряжение");

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	// Проверка буфера обмена на наличие скопированных строк
	УстановитьДоступностьКомандыВставки(ЭтотОбъект, Не ОбщегоНазначения.ПустойБуферОбмена());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РассчитатьСуммуТабЧасти(Знач СтрокаТабличнойЧасти)
	
	ЦенаУчет = Неопределено;
	
	Если СтрокаТабличнойЧасти.КоличествоУчет <> 0 Тогда
		ЦенаУчет = СтрокаТабличнойЧасти.СуммаУчет / СтрокаТабличнойЧасти.КоличествоУчет;
	КонецЕсли;
	
	Если ЦенаУчет <> Неопределено И СтрокаТабличнойЧасти.Цена = Окр(ЦенаУчет, 2) Тогда
		Сумма = СтрокаТабличнойЧасти.СуммаУчет * СтрокаТабличнойЧасти.Количество / СтрокаТабличнойЧасти.КоличествоУчет;
	Иначе
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
		Сумма = СтрокаТабличнойЧасти.Сумма;
	КонецЕсли;
	
	Возврат Сумма;
	
КонецФункции

&НаКлиенте
Процедура ВопросПерезаполнитьУчетныеДанныеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДокументНаКлиенте("ПерезаполнитьУчетныеКоличества");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПерезаполнитьКоличествоСуммуПриИзмененииСкладаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Объект.СчетУчета = Неопределено;
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДокументНаКлиенте("ПерезаполнитьУчетныеКоличества");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПерезаполнитьКоличествоСуммуПриИзмененииОтветственногоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Объект.СчетУчета = Неопределено;
	Если ЗначениеЗаполнено(Объект.Склад)
	   И ЗначениеЗаполнено(Объект.ОтветственноеЛицо) Тогда
		МОЛСклада = ОтветственноеЛицоНаСкладе(Объект.Склад, Объект.Дата);
		Если Не ЗначениеЗаполнено(МОЛСклада)
		 Или МОЛСклада <> Объект.ОтветственноеЛицо Тогда
			Объект.Склад = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Склад)
		Или ЗначениеЗаполнено(Объект.ОтветственноеЛицо) Тогда
		Если Результат = КодВозвратаДиалога.Да Тогда
			ЗаполнитьДокументНаКлиенте("ПерезаполнитьУчетныеКоличества");
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиСтрокуТабличнойЧасти(ИмяТабличнойЧасти, СтруктураОтбора)

	СтрокаТабличнойЧасти = Неопределено;

	МассивНайденныхСтрок = Объект[ИмяТабличнойЧасти].НайтиСтроки(СтруктураОтбора);
	Если МассивНайденныхСтрок.Количество() > 0 Тогда
		// Нашли. Вернем первую найденную строку.
		СтрокаТабличнойЧасти = МассивНайденныхСтрок[0];
	КонецЕсли;

	Возврат СтрокаТабличнойЧасти;

КонецФункции

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервере
Функция ЗаполнитьИнвентаризационнуюКомиссию()
	
	Возврат ИнвентаризацияАктивовОбязательств.ЗаполнитьИнвентаризационнуюКомиссию(
		Объект,
		Новый Структура("Склад", Объект.Склад));
	
КонецФункции
#Область СчетаУчета

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив(); 
	ЭлементыСчетов.Добавить("ТоварыСчетУчета");
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НачатьЗаполнениеСчетовУчета(ПричиныИзменения, Объект = Неопределено, СтрокаСписка = Неопределено, КонтейнерОбъект = Неопределено, КонтейнерСтрокаСписка = Неопределено)

	// Код этой функции сформирован автоматически с помощью СчетаУчетаВДокументах.КодФункцииНачатьЗаполнениеСчетовУчета()

	ПараметрыЗаполнения = СчетаУчетаВДокументахКлиентСервер.НовыйПараметрыЗаполнения(
		"ИнвентаризацияТоваровНаСкладе",
		ПричиныИзменения,
		Объект,
		СтрокаСписка,
		КонтейнерОбъект,
		КонтейнерСтрокаСписка);

	// 1. Заполняемые реквизиты
	// Товары.Номенклатура
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Товары.Номенклатура") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СчетУчета");
	КонецЕсли;

	// Организация
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Организация") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СчетУчета");
	КонецЕсли;

	// Склад
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Склад") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СчетУчета");
	КонецЕсли;

	// Дата
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Дата") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СчетУчета");
	КонецЕсли;

	// 2. (если требуется) Передадим на сервер данные, необходимые для заполнения
	Если ПараметрыЗаполнения.Свойство("Контейнер") Тогда
		// Товары.Номенклатура
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Товары.Номенклатура") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Склад");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
		КонецЕсли;

		// Организация
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Организация") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Склад");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
		КонецЕсли;

		// Склад
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Склад") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Склад");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
		КонецЕсли;

		// Дата
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Дата") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Склад");
		КонецЕсли;

	КонецЕсли; // Нужно передавать на сервер данные заполнения
	
	Возврат ПараметрыЗаполнения;

КонецФункции

&НаСервере
Процедура ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИмяТаблицы)

	ЭтоВставкаИзБуфера = ВыбранноеЗначение.Свойство("ЭтоВставкаИзБуфера");
	СписокСвойств = Неопределено;
	Если ЭтоВставкаИзБуфера Тогда
		
		ТаблицаТоваров = ВыбранноеЗначение.Данные;
		СписокСвойств = ВыбранноеЗначение.СписокСвойств;
		
	Иначе
		
		ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
		
	КонецЕсли;
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	СтрокиДляЗаполненияСчетов = Новый Массив;

	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		СтрокаТабличнойЧасти = Неопределено;
		Если Не ЭтоВставкаИзБуфера Тогда
			
			СтруктураОтбора = Новый Структура("Номенклатура, Цена", СтрокаТовара.Номенклатура, СтрокаТовара.Цена);
			СтрокаТабличнойЧасти = НайтиСтрокуТабличнойЧасти(ИмяТаблицы, СтруктураОтбора);
			
		КонецЕсли;
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			// Нашли - увеличиваем количество.
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + СтрокаТовара.Количество;
			
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТабличнойЧасти);
			РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
		Иначе
			
			Если ЭтоВставкаИзБуфера
				И БухгалтерскийУчетПереопределяемый.НоменклатураЯвляетсяУслугой(СтрокаТовара.Номенклатура) Тогда
					
				Продолжить;
					
			КонецЕсли;
			СтрокаТабличнойЧасти = Объект[ИмяТаблицы].Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТовара, СписокСвойств);
			СтрокиДляЗаполненияСчетов.Добавить(СтрокаТабличнойЧасти);
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТабличнойЧасти);
			
		КонецЕсли;
		
	КонецЦикла;

	СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиДляЗаполненияСчетов, ИмяТаблицы, Объект, Документы.ИнвентаризацияТоваровНаСкладе);
	Если ЭтоВставкаИзБуфера Тогда
		
		ВыбранноеЗначение.КоличествоДобавленныхСтрок = СтрокиДляЗаполненияСчетов.Количество();
		
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкоду(Команда)
	ОписаниеОповещенияПоискПоШтрихкоду = Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.ШтрихкодыНоменклатуры.Форма.ФормаВводШтрихкода", , ЭтотОбъект,,,,ОписаниеОповещенияПоискПоШтрихкоду , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры


&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ДобавитьНоменклатуруНаСервере(Результат.Номенклатура);
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ДобавитьНоменклатуруНаСервере(Номенклатура)

	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		ПараметрыПоиска = Новый Структура("Номенклатура",Номенклатура);
		СтрокиТабличнойЧасти = Объект.Товары.НайтиСтроки(ПараметрыПоиска);
		
		Если СтрокиТабличнойЧасти.Количество() = 0 Тогда 
			СтрокиДляЗаполненияСчетов 			= Новый Массив;
			СтрокаТабличнойЧасти 				= Объект.Товары.Добавить();
			СтрокаТабличнойЧасти.Номенклатура 	= Номенклатура;
			СтрокаТабличнойЧасти.Количество 	= 1;
			СтрокаТабличнойЧасти.Цена 			= 0;
			СтрокиДляЗаполненияСчетов.Добавить(СтрокаТабличнойЧасти);
			СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиДляЗаполненияСчетов, "Товары", Объект, Документы.ИнвентаризацияТоваровНаСкладе);
		Иначе
			СтрокаТабличнойЧасти 				= СтрокиТабличнойЧасти[0]; 
			СтрокаТабличнойЧасти.Количество 	= СтрокаТабличнойЧасти.Количество + 1;
		КонецЕсли;
		
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТабличнойЧасти);
		СтрокаТабличнойЧасти.Сумма = РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
		
	КонецЕсли;

КонецПроцедуры 


&НаСервере
Функция ДобавитьПоШтрихкодуНаСервере(Штрихкод)
	ТаблицаНоменклатурыПоШтрихкоду = РегистрыСведений.ШтрихкодыНоменклатуры.НоменклатураПоШтрихкоду(Штрихкод);
	
	Если ТаблицаНоменклатурыПоШтрихкоду.Количество() = 1 Тогда
		ДобавитьНоменклатуруНаСервере(ТаблицаНоменклатурыПоШтрихкоду[0].Номенклатура);
	Иначе
		ДобавитьНоменклатуруНаСервере(Справочники.Номенклатура.ПустаяСсылка());
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура СкопироватьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КоличествоСтрок = Элементы[ИмяТаблицы].ВыделенныеСтроки.Количество();
	Если КоличествоСтрок = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	СкопироватьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОКопированииСтрокВБуферОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	КоличествоСтрок = ВставитьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОВставкеСтрокИзБуфераОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

#КонецОбласти

#Область КопированиеВставкаСтрокЧерезБуферОбмена

&НаСервере
Процедура СкопироватьСтрокиНаСервере(ИмяТаблицы)
	
	ОбщегоНазначения.СкопироватьСтрокиВБуферОбмена(Объект[ИмяТаблицы], 
		Элементы[ИмяТаблицы].ВыделенныеСтроки, Объект.Ссылка.Метаданные().Имя);

КонецПроцедуры

&НаСервере
Функция ВставитьСтрокиНаСервере(ИмяТаблицы)
	
	ПараметрыВставки = ОбработкаТабличныхЧастей.ПолучитьПараметрыВставкиДанныхИзБуфераОбмена(Объект.Ссылка, ИмяТаблицы);
	ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки);
	ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ПараметрыВставки, ПараметрыВставки.ИмяТаблицы);
	
	Возврат ПараметрыВставки.КоличествоДобавленныхСтрок;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИмяТекущейТабличнойЧасти()
	
	Возврат "Товары";
	
КонецФункции

&НаСервере
Процедура ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки)

	СписокСвойств = Новый Массив;
	СписокСвойств.Добавить("Номенклатура");
	СписокСвойств.Добавить("Количество");
	СписокСвойств.Добавить("КоличествоУчет");
	СписокСвойств.Добавить("Цена");
	СписокСвойств.Добавить("ЦенаВРознице");
	СписокСвойств.Добавить("Сумма");
	СписокСвойств.Добавить("СуммаУчет");
	Если ПараметрыВставки.ПоказыватьСчетаУчетаВДокументах Тогда
		
		СписокСвойств.Добавить("СчетУчета");
		
	КонецЕсли;
	
	ПараметрыВставки.СписокСвойств = ОбработкаТабличныхЧастей.ПолучитьСписокСвойствИмеющихсяВТаблицеДанных(
		ПараметрыВставки.Данные, СписокСвойств);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандыВставки(Форма, Доступность)

	Доступность = Не Форма.ТолькоПросмотр И Доступность;
	Элементы = Форма.Элементы;
	Элементы.ТоварыВставитьСтроки.Доступность					 = Доступность;
	Элементы.ТоварыКонтекстноеМенюВставитьСтроки.Доступность	 = Доступность;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтветственноеЛицоНаСкладе(Склад, Дата)
	
	Возврат ОтветственныеЛицаБППереопределяемый.ОтветственноеЛицоНаСкладе(Склад, Дата);
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеПоОстаткам

&НаКлиенте
Процедура ЗаполнитьПоОстаткамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДокументНаКлиенте("Заполнить");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокументНаКлиенте(НазваниеДействия)
	
	// Если не доступно закрытие месяца, то и не проверяем актуальность данных
	Если ИспользуетсяОтложенноеПроведение И ДоступноЗакрытиеМесяца() Тогда
		СтатусКорректировкиСтоимости = СтатусКорректировкиСтоимостиНоменклатуры(Объект.Организация, Объект.Дата);
		ОпределитьНеобходимостьАктуализацииСебестоимости(НазваниеДействия, СтатусКорректировкиСтоимости);
	Иначе
		ЗаполнитьПоАктуальнымОстаткам(НазваниеДействия, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтатусКорректировкиСтоимостиНоменклатуры(Знач Организация, Знач Период)
	
	Результат = Новый Структура;
	Результат.Вставить("ОперацияВыполненаЗаТекущийМесяц",    Ложь);
	Результат.Вставить("ОперацияВыполненаЗаПредыдущийМесяц", Ложь);

	// Определяем, нужно ли выполнять корректировку себестоимости.
	Результат.ОперацияВыполненаЗаТекущийМесяц = РаботаСПоследовательностями.ЕстьНеустаревшаяОперация(
		Организация,
		Период,
		Перечисления.ВидыРегламентныхОпераций.КорректировкаСтоимостиНоменклатуры);
	
	// Если себестоимость была неактуальна ещё в прошлом месяце, то текущий месяц не имеет значения.
	Если Результат.ОперацияВыполненаЗаТекущийМесяц
		ИЛИ ЗакрытиеМесяца.ЭтоПервыйМесяцВеденияУчета(НачалоМесяца(Период), Организация) Тогда
		Результат.ОперацияВыполненаЗаПредыдущийМесяц = Истина;
	Иначе
		Результат.ОперацияВыполненаЗаПредыдущийМесяц = РаботаСПоследовательностями.ЕстьНеустаревшаяОперация(
			Организация,
			ДобавитьМесяц(Период, -1),
			Перечисления.ВидыРегламентныхОпераций.КорректировкаСтоимостиНоменклатуры);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОпределитьНеобходимостьАктуализацииСебестоимости(НазваниеДействия, СтатусКорректировкиСтоимости)
	
	Если СтатусКорректировкиСтоимости.ОперацияВыполненаЗаТекущийМесяц Тогда
		ЗаполнитьПоАктуальнымОстаткам(НазваниеДействия, Ложь);
	Иначе
	
		Оповещение = Новый ОписаниеОповещения("ВопросОНеобходимостиАктуализацииСебестоимостиЗавершение", ЭтотОбъект, НазваниеДействия);
		ТекстВопроса = НСтр("ru = 'Данные учета не актуальны. Необходимо рассчитать себестоимость номенклатуры.'");
		
		Кнопки = Новый СписокЗначений;
		Если СтатусКорректировкиСтоимости.ОперацияВыполненаЗаПредыдущийМесяц Тогда
			// Предлагаем актуализировать "бесшовно" (в фоновом задании).
			Кнопки.Добавить("ВыполнитьКорректировку",  НСтр("ru = 'Выполнить корректировку стоимости'"));
			Кнопки.Добавить("НеАктуализировать", НСтр("ru = 'Не актуализировать себестоимость'"));
		Иначе
			// Предлагаем перейти в "Закрытие месяца".
			Кнопки.Добавить("ПерейтиВЗакрытие",  НСтр("ru = 'Перейти в ""Закрытие месяца""'"));
			Кнопки.Добавить("НеАктуализировать", НСтр("ru = 'Не актуализировать себестоимость'"));
		КонецЕсли;
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , "НеАктуализировать");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОНеобходимостиАктуализацииСебестоимостиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "ВыполнитьКорректировку" Тогда
		
		ЗаполнитьПоАктуальнымОстаткам(ДополнительныеПараметры, Истина);
		
	ИначеЕсли Результат = "ПерейтиВЗакрытие" Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Организация",       Объект.Организация);
		ПараметрыОткрытия.Вставить("ПериодРегистрации", Объект.Дата);
		
		ОткрытьФорму("Обработка.ЗакрытиеМесяца.Форма", ПараметрыОткрытия, ЭтотОбъект, Объект.Ссылка);
		
	Иначе
		
		ЗаполнитьПоАктуальнымОстаткам(ДополнительныеПараметры, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоАктуальнымОстаткам(НазваниеДействия, ВыполнитьКорректировкуСтоимости)
	
	// Запускаем фоновое задание.
	Если Не ЗапуститьЗаполнениеПоОстаткам(НазваниеДействия, ВыполнитьКорректировкуСтоимости) Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершенииЗадания = Новый ОписаниеОповещения("СообщитьРезультатЗаполнения", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);  
	ПараметрыОжидания.Заголовок            = НаименованиеФоновогоЗадания(ВыполнитьКорректировкуСтоимости);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения    = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОписаниеФоновогоЗадания, ОповещениеОЗавершенииЗадания, ПараметрыОжидания);
		
КонецПроцедуры

// Заполняет табличную часть данными регистра бухгалтерии, используя фоновое задание.
//
// Параметры
//  НазваниеДействия - Строка - Определяет способ заполнения документа:
//		"ПерезаполнитьУчетныеКоличества" - Заполняет данные в существующих строках документа;
//		"Заполнить" - Очищает старые строки в табличной части и заполняет ее новыми по данным регистра бухгалтерии.
//  ВыполнитьКорректировкуСтоимости - Булево - если Истина, то перед заполнением нужно выполнить регламентную операцию.
//
// Возвращаемое значение:
//   Булево      - Истина, фоновое задание запущено; иначе Ложь.
//
&НаСервере
Функция ЗапуститьЗаполнениеПоОстаткам(НазваниеДействия, ВыполнитьКорректировкуСтоимости)
	
	Если НазваниеДействия = "ПерезаполнитьУчетныеКоличества" Тогда
		Если Модифицированность Тогда
			Записать(Новый Структура("РежимЗаписиДокумента", РежимЗаписиДокумента.Запись));
		КонецЕсли;
	Иначе
		Объект.Товары.Очистить();
		Если НазваниеДействия = "Заполнить" Тогда
			Объект.СчетУчета = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	// Возможно, что фоновое задание было запущено раньше,
	// пользователь дал команду его отменить, однако задание не отменено.
	// В таком случае не следует запускать задание повторно - следует дождаться его выполнения.
	Если ОписаниеФоновогоЗадания <> Неопределено
	   И ЗакрытиеМесяца.ЗаданиеЕщеВыполняется(ОписаниеФоновогоЗадания.ИдентификаторЗадания) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыЗаполнения = Документы.ИнвентаризацияТоваровНаСкладе.НовыеПараметрыЗаполнения();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, Объект);
	
	ПараметрыЗаполнения.НазваниеДействия                 = НазваниеДействия;
	ПараметрыЗаполнения.ВыполнитьКорректировкуСтоимости  = ВыполнитьКорректировкуСтоимости;
	ПараметрыЗаполнения.ИспользуетсяОтложенноеПроведение = ИспользуетсяОтложенноеПроведение;
	
	Если ВыполнитьКорректировкуСтоимости Тогда
		
		// Ищем ранее созданную операцию.
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РегламентнаяОперация.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
		|ГДЕ
		|	РегламентнаяОперация.Организация = &Организация
		|	И РегламентнаяОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыРегламентныхОпераций.КорректировкаСтоимостиНоменклатуры)
		|	И НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) = &ПериодРегистрации
		|	И НЕ РегламентнаяОперация.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	РегламентнаяОперация.Проведен УБЫВ,
		|	РегламентнаяОперация.Состояние.Порядок УБЫВ");
		Запрос.УстановитьПараметр("Организация",       Объект.Организация);
		Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Объект.Дата));
		ВыборкаОперация = Запрос.Выполнить().Выбрать();
		ОперацияКорректировкаСтоимости = ?(ВыборкаОперация.Следующий(),
			ВыборкаОперация.Ссылка,
			Документы.РегламентнаяОперация.ПустаяСсылка());
		
		// Параметры обернем в структуру для их передачи в Обработки.ЗакрытиеМесяца.ВыполнитьРегламентнуюОперацию()
		// через механизм ДлительныеОперации.
		ВидОперации = Перечисления.ВидыРегламентныхОпераций.КорректировкаСтоимостиНоменклатуры;
		
		ПараметрыКорректировкиСтоимости = Обработки.ЗакрытиеМесяца.НовыеПараметрыВыполненияОперации();
		ПараметрыКорректировкиСтоимости.Организация = Объект.Организация;
		ПараметрыКорректировкиСтоимости.Период      = НачалоМесяца(Объект.Дата);
		ПараметрыКорректировкиСтоимости.ВидОперации = ВидОперации;
		ПараметрыКорректировкиСтоимости.Ссылка      = ОперацияКорректировкаСтоимости;
		ПараметрыКорректировкиСтоимости.ПропуститьПроверкуСтатусаПредыдущих = Истина;
		
		ПараметрыЗаполнения.ПараметрыКорректировкиСтоимости = ПараметрыКорректировкиСтоимости;
		
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания(ВыполнитьКорректировкуСтоимости);
	// Если на файловой базе уже запущено любое другое фоновое задание, то новое встанет в очередь, а не будет запущено в основном потоке.
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ОписаниеФоновогоЗадания = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "Документы.ИнвентаризацияТоваровНаСкладе.ЗаполнитьПоОстаткам", ПараметрыЗаполнения);
	ОписаниеФоновогоЗадания.Вставить("ОперацияКорректировкаСтоимости", ОперацияКорректировкаСтоимости);
		
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура СообщитьРезультатЗаполнения(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		
		Если Не ПустаяСтрока(РезультатЗадания.ПодробноеПредставлениеОшибки) Тогда
			Сообщить(РезультатЗадания.ПодробноеПредставлениеОшибки);
		КонецЕсли;
		ПоказатьПредупреждение(, РезультатЗадания.КраткоеПредставлениеОшибки, , НСтр("ru = 'Операция не выполнена'"));
		Возврат;
		
	КонецЕсли;
	
	Для Каждого Сообщение Из РезультатЗадания.Сообщения Цикл
		Сообщение.Сообщить();
	КонецЦикла;
	
	РезультатЗаполнения = ПрочитатьРезультатЗаполнения(РезультатЗадания.АдресРезультата);
	
	Если Не РезультатЗаполнения.ОткрытьФормуОшибки Тогда
		// ТЧ "Товары" заполнена.
		Возврат;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(РезультатЗаполнения.ОперацияКорректировкаСтоимости) Тогда
		
		ЗакрытиеМесяцаКлиент.ОткрытьФормуОшибок(РезультатЗаполнения.ОперацияКорректировкаСтоимости);
		
	Иначе
		
		ТекстПредупреждения = НСтр("ru = 'Операция не выполнена'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПрочитатьРезультатЗаполнения(АдресХранилища)
	
	РезультатЗаполнения = Документы.ИнвентаризацияТоваровНаСкладе.НовыйРезультатЗаполнения();
	РезультатЗаполнения.ОперацияКорректировкаСтоимости = ОписаниеФоновогоЗадания.ОперацияКорректировкаСтоимости;
	РезультатЗаполнения.ЗаданиеВыполнено = Истина;
	
	Обработки.ЗакрытиеМесяца.ПрочитатьРезультатЗаполнения(АдресХранилища, РезультатЗаполнения, Объект);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ОписаниеФоновогоЗадания = Неопределено;
	Модифицированность = Истина;
	
	Возврат РезультатЗаполнения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НаименованиеФоновогоЗадания(ВыполнитьКорректировкуСтоимости)
	
	Если ВыполнитьКорректировкуСтоимости Тогда
		
		НаименованиеЗадания = СтрШаблон(НСтр("ru = 'Формирование регламентной операции ""%1""'"),
			ПредопределенноеЗначение("Перечисление.ВидыРегламентныхОпераций.КорректировкаСтоимостиНоменклатуры"));
		
	Иначе
		
		НаименованиеЗадания = НСтр("ru = 'Заполнить по остаткам для инвентаризации товаров'");
		
	КонецЕсли;
	
	Возврат НаименованиеЗадания;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДоступноЗакрытиеМесяца()
	
	Возврат ПравоДоступа("Редактирование", Метаданные.Документы.РегламентнаяОперация);
		
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПоОстаткамНаСчетеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОповещениеВыборСчета = Новый ОписаниеОповещения("ВыборСчетаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.ИнвентаризацияТоваровНаСкладе.Форма.ВыборСчета",,ЭтотОбъект,,,,ОповещениеВыборСчета);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборСчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("ПланСчетовСсылка.Хозрасчетный")
		И ЗначениеЗаполнено(Результат) Тогда
		Объект.СчетУчета = Результат;
		ЗаполнитьПоАктуальнымОстаткам("ЗаполнитьПоСчету", Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти 

#Область БЭД

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДОНажатие(Элемент, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

#Область ИспользованиеТСД

&НаСервере
Процедура УстановитьВидимостьГруппыКнопокТСД()
	
	Элементы.ТоварыГруппаТСД.Видимость = МенеджерОборудованияБП.ИспользуетсяОборудование("ТерминалСбораДанных")
		И Не ОбщегоНазначения.ЭтоМобильныйКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеВТСД(Команда)
	
	#Если ВебКлиент Тогда
	ПоказатьПредупреждение(,НСтр("ru = 'Работа с торговым оборудованием не поддерживается в режиме веб-клиента.
					|Для использования терминала сбора данных запустите программу в режиме тонкого клиента.'"), 15);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Номенклатура не выгружена'"));
	Возврат;
	#КонецЕсли

	Если НЕ ТаблицаЗаполнена() Тогда
		ПоказатьПредупреждение(, "Табличная часть Товары не заполнена.
								|Отсутствуют данные для выгрузки в ТСД");
		Возврат;
	КонецЕсли;

	
	НоменклатураБезШтрихкода = Неопределено;
	ТаблицаТСД = ПолучитьТаблицуТоваровДляТСД(НоменклатураБезШтрихкода);
	Если ТаблицаТСД.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Отсуствуют данные для выгрузки в терминал");
		Возврат;
	КонецЕсли;
	
	Если НоменклатураБезШтрихкода.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр(
		"ru = 'Обнаружена номенклатура без штрихкода.
		|Номенклатура без штрихкода не будет выгружена в ТСД.
		|Продолжить выполнение?'");
		
		ДополнительныеПараметры = Новый Структура("ТаблицаТСД", ТаблицаТСД);
		Оповещение = Новый ОписаниеОповещения("ВыгрузитьДанныеВТСД_ПослеВопроса", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 15);
	Иначе
		ВыгрузитьДанныеВТСД_ВыполнитьВыгрузку(ТаблицаТСД);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеВТСД_ПослеВопроса(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыгрузитьДанныеВТСД_ВыполнитьВыгрузку(ДополнительныеПараметры.ТаблицаТСД);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Номенклатура не выгружена'"));
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеВТСД_ВыполнитьВыгрузку(ТаблицаТСД) Экспорт
	
	ОповещениеПослеВыгрузки = Новый ОписаниеОповещения("ПослеВыгрузкиВТСД", ЭтаФорма, Неопределено);
	МенеджерОборудованияКлиент.НачатьВыгрузкуДанныеВТСД(ОповещениеПослеВыгрузки, ЭтаФорма.УникальныйИдентификатор, ТаблицаТСД);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыгрузкиВТСД(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Результат Тогда
		ПоказатьОповещениеПользователя("Данные успешно выгружены", ,"Выгрузка данных  в ТСД"
			,БиблиотекаКартинок.ВыгрузитьВТСД,СтатусОповещенияПользователя.Информация);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ОписаниеОшибки);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСД(Команда)
	
	#Если ВебКлиент Тогда
	ПоказатьПредупреждение(,НСтр("ru = 'Работа с торговым оборудованием не поддерживается в режиме веб-клиента.
					|Для использования терминала сбора данных запустите программу в режиме тонкого клиента.'"), 15);
	Возврат;
	#КонецЕсли

	Если Не ТаблицаЗаполнена() Тогда
		ЗаполнитьДокументНаКлиенте("Заполнить");
	КонецЕсли;
	ОчиститьФактНаСервере();
	
	ЗаблокироватьДанныеФормыДляРедактирования();
	ЭтаФорма.Доступность = Ложь; // Блокируем интерфейс пользователя.
	СворачиватьДанные    = Истина; // Флаг определяет, будет ли сворачиваться загружаемая таблица (группировка по штрихкоду).
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьДанныеИзТСДЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьЗагрузкуДанныеИзТСД(Оповещение, УникальныйИдентификатор, СворачиватьДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзТСДЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЭтаФорма.Доступность = Истина; // Разблокировка интерфейса пользователя.
	
	Если Результат.Результат Тогда
		ТаблицаТСД = Результат.ТаблицаТоваров;
		ЗаполнитьДанныеИзТСДСервер(ТаблицаТСД);
		Элементы.Товары.Обновить();
		ПоказатьПредупреждение(, Нстр("ru = 'Данные загружены'"));
	Иначе
		ТекстСообщения = НСтр("ru = 'При выполнении операции произошла ошибка:""%1"".'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Результат.ОписаниеОшибки);
		ПоказатьПредупреждение(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуТоваровДляТСД(ТоварыБезШтрихкода = Неопределено)
	
	ОбъектФормы = РеквизитФормыВЗначение("Объект");
	
	ТоварыБезШтрихкода = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТоварыДокумента.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ТоварыДокумента.Количество КАК ЧИСЛО(15, 3)) КАК Количество,
	|	ВЫРАЗИТЬ(ТоварыДокумента.Цена КАК ЧИСЛО(15, 2)) КАК Цена
	|ПОМЕСТИТЬ ТоварыДокумента
	|ИЗ
	|	&ТоварыДокумента КАК ТоварыДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	ТоварыДокумента.Количество КАК Количество,
	|	ТоварыДокумента.Цена КАК Цена,
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК Штрихкод
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ТоварыДокумента.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура";
	
	Запрос.УстановитьПараметр("ТоварыДокумента", ОбъектФормы.Товары.Выгрузить());
	Выборка =  Запрос.Выполнить().Выбрать();
	
	ТаблицаДляТСД = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Штрихкод) Тогда
			НоваяСтрока = Новый Структура("Номенклатура, Цена, Количество, Штрихкод");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			ТаблицаДляТСД.Добавить(НоваяСтрока);
		Иначе
			СтрокаБезШтрихкода = Новый Структура("Номенклатура");
			ЗаполнитьЗначенияСвойств(СтрокаБезШтрихкода,Выборка);
			ТоварыБезШтрихкода.Добавить(СтрокаБезШтрихкода);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаДляТСД;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеИзТСДСервер(ТаблицаТСД)
	
	ТаблицаШтрихкодов = Новый ТаблицаЗначений;
	ТаблицаШтрихкодов.Колонки.Добавить("Штрихкод", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(128)));
	ТаблицаШтрихкодов.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(15,3)));
	Для Каждого ДанныеТСД Из ТаблицаТСД Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаШтрихкодов.Добавить(), ДанныеТСД);		 	
	КонецЦикла;
	
	ТаблицаТоваров = Объект.Товары.Выгрузить(,"Номенклатура, СчетУчета, КоличествоУчет, Цена, ЦенаВРознице, СуммаУчет");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Товары.СчетУчета КАК ПланСчетов.Хозрасчетный) КАК СчетУчета,
	|	ВЫРАЗИТЬ(Товары.КоличествоУчет КАК ЧИСЛО(15, 3)) КАК КоличествоУчет,
	|	ВЫРАЗИТЬ(Товары.Цена КАК ЧИСЛО(15, 2)) КАК Цена,
	|	ВЫРАЗИТЬ(Товары.ЦенаВРознице КАК ЧИСЛО(15, 2)) КАК ЦенаВРознице,
	|	ВЫРАЗИТЬ(Товары.СуммаУчет КАК ЧИСЛО(15, 2)) КАК СуммаУчет
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТСД.Штрихкод КАК Штрихкод,
	|	ТаблицаТСД.Количество КАК Количество
	|ПОМЕСТИТЬ ТаблицаТСД
	|ИЗ
	|	&ТаблицаТСД КАК ТаблицаТСД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.КоличествоУчет КАК КоличествоУчет,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.ЦенаВРознице КАК ЦенаВРознице,
	|	ТаблицаТовары.СуммаУчет КАК СуммаУчет,
	|	ЕСТЬNULL(ШтрихкодыНоменклатуры.Штрихкод, """") КАК Штрихкод
	|ПОМЕСТИТЬ НоменклатураШтрихкоды
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ТаблицаТовары.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураШтрихкоды.Номенклатура КАК Номенклатура,
	|	НоменклатураШтрихкоды.СчетУчета КАК СчетУчета,
	|	МАКСИМУМ(НоменклатураШтрихкоды.КоличествоУчет) КАК КоличествоУчет,
	|	СУММА(ЕСТЬNULL(ТаблицаТСД.Количество, 0)) КАК Количество,
	|	МАКСИМУМ(НоменклатураШтрихкоды.Цена) КАК Цена,
	|	МАКСИМУМ(НоменклатураШтрихкоды.ЦенаВРознице) КАК ЦенаВРознице,
	|	МАКСИМУМ(НоменклатураШтрихкоды.СуммаУчет) КАК СуммаУчет,
	|	СУММА(НоменклатураШтрихкоды.Цена * ТаблицаТСД.Количество) КАК Сумма
	|ИЗ
	|	НоменклатураШтрихкоды КАК НоменклатураШтрихкоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТСД КАК ТаблицаТСД
	|		ПО НоменклатураШтрихкоды.Штрихкод = ТаблицаТСД.Штрихкод
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураШтрихкоды.Номенклатура,
	|	НоменклатураШтрихкоды.СчетУчета";
	
	Запрос.УстановитьПараметр("Товары",ТаблицаТоваров);
	Запрос.УстановитьПараметр("ТаблицаТСД",ТаблицаШтрихкодов);
	Объект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция ТаблицаЗаполнена()
	
	Возврат Объект.Товары.Количество()>0;
	
КонецФункции

&НаСервере
Процедура ОчиститьФактНаСервере()
	
	ТаблицаТоваров = Объект.Товары.Выгрузить();
	ТаблицаТоваров.ЗаполнитьЗначения(0,"Количество");
	Объект.Товары.Загрузить(ТаблицаТоваров);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	АдаптироватьМКСписокФормы();
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, Элементы.ПодменюПечать.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	Предупреждение = МобильныйКлиентБПФормаОбъекта.СоздатьПредупреждениеОбОграничениях(ЭтотОбъект,
		"Ввести списание и оприходование товаров можно в версии для компьютера");
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Предупреждение);
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьНомерДатаФормаДокумента(Элементы, Элементы.ГруппаДатаНомер);
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаДатаНомер);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Организация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Склад);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ОтветственноеЛицо);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаПроведениеИнвентаризации);
		
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаСостояниеЭДО);
		
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаПодвал);
	
	Элементы.ГруппаТоварыПодвалПравая.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
	Элементы.ГруппаТоварыПодвалПравая.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаТоварыПодвал.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Авто;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаТоварыПодвал);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "Товары");
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыПодбор ="ТоварыПодборТовары";
	КомандыПоля.ИмяКомандыДобавитьСтроку = "ТоварыДобавить";
	КомандыПоля.КомандыТаблицыПрочие.Добавить("ТоварыПодменюЗаполнить");
	КомандыПоля.КомандыТаблицыПрочие.Добавить("ТоварыДобавитьПоШтрихкоду");
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаТовары;
	ОписаниеГруппыПоля.ПоказатьГруппуНового = Истина;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.КолонкаНомерСтроки = "ТоварыНомерСтроки";
	ОписаниеКолонокПоля.ВажныеКолонки = "ТоварыНоменклатура";
	ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = "ТоварыНоменклатура";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
		Элементы.Товары, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
		
	Элементы.ТоварыНоменклатура.Ширина = 0;
	Элементы.ТоварыНоменклатура.РастягиватьПоГоризонтали = Истина;
	Элементы.ТоварыНоменклатураКод.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.Переместить(Элементы.ТоварыНоменклатураКод, Элементы.Товары, Элементы.ТоварыКоличество);

	Элементы.ТоварыНоменклатураАртикул.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.Переместить(Элементы.ТоварыНоменклатураАртикул, Элементы.Товары, Элементы.ТоварыКоличество);
	
	Элементы.ТоварыКоличество.Формат = "ЧФ='Кол-во факт: Ч'";
	Элементы.ТоварыКоличествоУчет.Формат = "ЧФ='Кол-во учет: Ч'";
	Элементы.ТоварыОтклонение.Формат = "ЧФ='Кол-во отклонение: Ч'";
	Элементы.ТоварыЦена.Формат = "ЧФ='Цена: Ч'";
	Элементы.ТоварыЦенаВРознице.Формат = "ЧФ='Розн. цена: Ч'";
	Элементы.ТоварыСумма.Формат = "ЧФ='Сумма факт: Ч'";
	Элементы.ТоварыСуммаУчет.Формат = "ЧФ='Сумма учет: Ч'";
	
КонецПроцедуры

#КонецОбласти
