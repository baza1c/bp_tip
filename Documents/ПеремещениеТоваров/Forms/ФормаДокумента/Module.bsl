////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	СобытияФормИС.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка, Неопределено);

	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	// Активизировать первую непустую табличную часть
	СписокТабличныхЧастей = Новый СписокЗначений;
	СписокТабличныхЧастей.Добавить("Товары",			 "Товары");
	СписокТабличныхЧастей.Добавить("ТоварыНаКомиссии",	 "ТоварыНаКомиссии");
	СписокТабличныхЧастей.Добавить("ВозвратнаяТара",	 "ВозвратнаяТара");
	СписокТабличныхЧастей.Добавить("МатериалыЗаказчика", "МатериалыЗаказчика");
	
	АктивизироватьТабличнуюЧасть = ОбщегоНазначенияБПВызовСервера.ПолучитьПервуюНепустуюВидимуюТабличнуюЧасть(
		ЭтаФорма, СписокТабличныхЧастей);
	ОбщегоНазначенияБПВызовСервера.АктивизироватьЭлементФормы(ЭтаФорма, АктивизироватьТабличнуюЧасть);

	УстановитьВидимостьСчетовУчета();
	
	УстановитьУсловноеОформление();
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыЭДОПриСоздании = ОбменСКонтрагентами.ПараметрыПриЧтенииНаСервере_ФормаДокумента();
	ПараметрыЭДОПриСоздании.Форма = ЭтотОбъект;
	ПараметрыЭДОПриСоздании.ДокументСсылка = Объект.Ссылка;
	ПараметрыЭДОПриСоздании.МестоРазмещенияКоманд = Элементы.КомандыЭДО;
	ПараметрыЭДОПриСоздании.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыЭДОПриСоздании.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	ОбменСКонтрагентами.ПриЧтенииНаСервере_ФормаДокумента(ПараметрыЭДОПриСоздании);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	СобытияФормИС.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ПроведениеСервер.УстановитьПризнакПроверкиРеквизитов(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "ПроведениеПеремещениеТоваров";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УстановитьСостояниеДокумента();
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
	СобытияФормИС.ПослеЗаписиНаСервере(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ОбработанаТабличнаяЧастьТовары"
		И ТипЗнч(Параметр) = Тип("Структура") 
		И Параметр.Свойство("ИдентификаторВызывающейФормы")
		И Параметр.ИдентификаторВызывающейФормы = УникальныйИдентификатор Тогда
		ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметр);
		
	ИначеЕсли ИмяСобытия = "ДанныеСкопированыВБуферОбмена" Тогда
		
		УстановитьДоступностьКомандыВставки(ЭтотОбъект, Истина);
		
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;

	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
	
	СобытияФормИСКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник, Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормИСКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)

	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента);
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкладОтправительПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СкладОтправитель) Тогда
		СкладОтправительПриИзмененииНаСервере();
	Иначе
		ТипЦенСкладОтправитель = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
		ТипСкладаОтправитель   = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПолучательПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СкладПолучатель) Тогда
		СкладПолучательПриИзмененииНаСервере();
	Иначе
		ТипЦенСкладПолучатель = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
		ТипСкладаПолучатель   = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НДСвСтоимостиТоваровПриИзменении(Элемент)

	Если Объект.НДСвСтоимостиТоваров = ПредопределенноеЗначение("Перечисление.ДействиеНДСВСтоимостиТоваров.ВключитьВСтоимость") Тогда
		
		Изменения = СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(НачатьЗаполнениеСчетовУчета("НДСвСтоимостиТоваров", Объект));
		
		Если Изменения.Свойство("СчетСписанияНДС") Тогда
			БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
				ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
		КонецЕсли;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СчетСписанияНДСПриИзменении(Элемент)

	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(НачатьЗаполнениеСчетовУчета("СчетСписанияНДС", Объект));
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС1ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(1);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС2ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(2);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоСписанияНДС3ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(3);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <Товары>

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если Копирование Тогда
		Элемент.ТекущиеДанные.Себестоимость = 0;
		Элемент.ТекущиеДанные.ДокументОприходования = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	// Подготовим данные для передачи на сервер
	Контекст = Новый Структура("Дата, Организация, СкладОтправитель, СкладПолучатель, 
		|ТипСкладаОтправитель, ТипСкладаПолучатель, РазделятьПоСтавкамНДС");
	ЗаполнитьЗначенияСвойств(Контекст, ЭтаФорма);
	ЗаполнитьЗначенияСвойств(Контекст, Объект);

	КонтейнерСтрока = Новый Структура("Номенклатура");
	ЗаполнитьЗначенияСвойств(КонтейнерСтрока, ТекущиеДанные);
	
	ПараметрыЗаполненияСчетовУчета = НачатьЗаполнениеСчетовУчета(
		"Товары.Номенклатура",
		Объект,
		ТекущиеДанные,
		Контекст,
		КонтейнерСтрока);
		
	// Получим данные с сервера
	Изменения = ПолучитьДанныеТоварыНоменклатураПриИзменении(Контекст, КонтейнерСтрока, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	
	// Обработаем данные с сервера
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Изменения);
	Если Изменения.Свойство("ЦенаВРознице") Тогда
		ТекущиеДанные.СуммаВРознице = ТекущиеДанные.ЦенаВРознице * ТекущиеДанные.Количество;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)

	Элементы.Товары.ТекущиеДанные.Коэффициент = 1;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)

	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	Если Элементы.ТоварыЦенаВРознице.Видимость И СтрокаТабличнойЧасти.ЦенаВРознице <> 0 Тогда
		СтрокаТабличнойЧасти.СуммаВРознице = СтрокаТабличнойЧасти.ЦенаВРознице * СтрокаТабличнойЧасти.Количество;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаВРозницеПриИзменении(Элемент)

	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	СтрокаТабличнойЧасти.СуммаВРознице = СтрокаТабличнойЧасти.ЦенаВРознице * СтрокаТабличнойЧасти.Количество;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаВРозницеПриИзменении(Элемент)

	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;

	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Количество) Тогда
		СтрокаТабличнойЧасти.Цена = 0;
	Иначе
		СтрокаТабличнойЧасти.ЦенаВРознице = СтрокаТабличнойЧасти.СуммаВРознице / СтрокаТабличнойЧасти.Количество;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументОприходованияПриИзменении(Элемент)

	СтрокаТаблицы = Элементы.Товары.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументОприходования) Тогда
		СтрокаТаблицы.Себестоимость = 0;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументОприходованияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;

	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("КонецПериода"  , Объект.Дата);
	ПараметрыОбъекта.Вставить("Организация"   , Объект.Организация);
	ПараметрыОбъекта.Вставить("Склад"         , Объект.СкладОтправитель);
	ПараметрыОбъекта.Вставить("Номенклатура"  , ТекущиеДанные.Номенклатура);
	ПараметрыОбъекта.Вставить("СчетУчета"     , ТекущиеДанные.СчетУчета);
	ПараметрыОбъекта.Вставить("ТипыДокументов", "Метаданные.Документы.ПеремещениеТоваров.ТабличныеЧасти.Товары.Реквизиты.ДокументОприходования.Тип");

	ПараметрыФормы = Новый Структура("ПараметрыОбъекта", ПараметрыОбъекта);
	ОткрытьФорму("Документ.Партия.ФормаВыбора", ПараметрыФормы, Элемент);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <ТоварыНаКомиссии>

&НаКлиенте
Процедура ТоварыНаКомиссииНоменклатураПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ТоварыНаКомиссии.ТекущиеДанные;
	
	ДанныеОбъекта = Новый Структура("Организация, Дата, СкладОтправитель");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	ДанныеСтрокиТабличнойЧасти = Новый Структура("Номенклатура");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТабличнойЧасти, ТекущиеДанные);
	
	ПараметрыЗаполненияСчетовУчета = НачатьЗаполнениеСчетовУчета(
		"ТоварыНаКомиссии.Номенклатура",
		Объект,
		ТекущиеДанные,
		ДанныеОбъекта,
		ДанныеСтрокиТабличнойЧасти);
		
	Изменения = ПолучитьДанныеТоварыНаКомиссииНоменклатураПриИзменении(
		ДанныеОбъекта,
		ДанныеСтрокиТабличнойЧасти,
		ПараметрыЗаполненияСчетовУчета.КЗаполнению);

	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Изменения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНаКомиссииЕдиницаИзмеренияПриИзменении(Элемент)

	Элементы.ТоварыНаКомиссии.ТекущиеДанные.Коэффициент = 1;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <ВозвратнаяТара>

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураПриИзменении(Элемент)

	ПараметрыЗаполненияСчетов = НачатьЗаполнениеСчетовУчета(
		"ВозвратнаяТара.Номенклатура",
		Объект,
		Элементы.ВозвратнаяТара.ТекущиеДанные);
		
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетов);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ <МатериалыЗаказчика>

&НаКлиенте
Процедура МатериалыЗаказчикаНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыЗаказчика.ТекущиеДанные;
	
	// Подготовим данные для передачи на сервер
	ДанныеОбъекта = Новый Структура;
	
	ДанныеСтроки = Новый Структура("Номенклатура");
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ТекущиеДанные);
	
	ПараметрыЗаполненияСчетовУчета = НачатьЗаполнениеСчетовУчета(
		"МатериалыЗаказчика.Номенклатура",
		Объект,
		ТекущиеДанные,
		ДанныеОбъекта,
		ДанныеСтроки);
		
	Изменения = ПолучитьДанныеМатериалыЗаказчикаНоменклатураПриИзменении(
		ДанныеСтроки, 
		ДанныеОбъекта, 
		ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Изменения);
	
	КонтрагентНоменклатуры = ПолучитьЕдинственныхЗаказчиковМатериалов(ТекущиеДанные.Номенклатура);
	ТекущиеДанные.Контрагент = КонтрагентНоменклатуры.Получить(ТекущиеДанные.Номенклатура);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыЗаказчикаЕдиницаИзмеренияПриИзменении(Элемент)

	ТекущиеДанные = Элементы.МатериалыЗаказчика.ТекущиеДанные;
	
	ТекущиеДанные.Коэффициент = 1;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПодборТовары(Команда)

	ПараметрыПодбора = ПолучитьПараметрыПодбора("Товары");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборМатериалы(Команда)

	ПараметрыПодбора = ПолучитьПараметрыПодбора("Материалы");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборТоварыНаКомиссии(Команда)

	ПараметрыПодбора = ПолучитьПараметрыПодбора("ТоварыНаКомиссии");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборВозвратнаяТара(Команда)

	ПараметрыПодбора = ПолучитьПараметрыПодбора("ВозвратнаяТара");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборМатериалыЗаказчика(Команда)

	ПараметрыПодбора = ПолучитьПараметрыПодбора("МатериалыЗаказчика");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтотОбъект, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТовары(Команда)

	ПараметрыФормы = ПолучитьПараметрыОбработкиТабличнойЧастиТовары();
	
	Если ПараметрыФормы <> Неопределено Тогда
		ОткрытьФорму("Обработка.ИзменениеТаблицыТоваров.Форма.Форма", ПараметрыФормы,
			ЭтаФорма, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КоличествоСтрок = Элементы[ИмяТаблицы].ВыделенныеСтроки.Количество();
	Если КоличествоСтрок = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	СкопироватьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОКопированииСтрокВБуферОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	КоличествоСтрок = ВставитьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОВставкеСтрокИзБуфераОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВВЕТИС(Команда)
	
	ДополнительныеПараметры = Новый Структура("Основание", Объект.Ссылка);
	ОткрытьФорму("Документ.ИсходящаяТранспортнаяОперацияВЕТИС.ФормаОбъекта", ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыЗаполнитьПоОстаткам(Команда)
	
	ЗаполнитьМатериалыСотрудникаПоОстаткам();
	
КонецПроцедуры   

&НаКлиенте
Процедура МатериалыЗаказачикаЗаполнитьПоОстаткам(Команда)

	ЗаполнитьМатериалыЗаказчикаПоОстаткам();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();


	// ТоварыДокументОприходования

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыДокументОприходования");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ДокументОприходования", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Авто>'"));


	// ТоварыСебестоимость

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСебестоимость");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.Себестоимость", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Авто>'"));


	// ТоварыСебестоимость

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСебестоимость");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СпособОценкиМПЗПоСредней", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ДокументОприходования", ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"УчетВПродажныхЦенах", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// ТоварыНоменклатура, ТоварыКоличество, ТоварыНоменклатураЕдиницаИзмерения

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНоменклатура");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКоличество");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНоменклатураЕдиницаИзмерения");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"УчетВПродажныхЦенах", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТипСкладаПолучатель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыСчетУчета

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетУчета");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"УчетВПродажныхЦенах", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыСкладов.ОптовыйСклад);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыНовыйСчетУчета

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНовыйСчетУчета");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"УчетВПродажныхЦенах", ВидСравненияКомпоновкиДанных.Равно, Истина);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"ТипСкладаПолучатель", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыСкладов.ОптовыйСклад);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыСтавкаНДСВРознице

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСтавкаНДСВРознице");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"УчетВПродажныхЦенах", ВидСравненияКомпоновкиДанных.Равно, Ложь);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"РазделятьПоСтавкамНДС", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"ТипСкладаПолучатель", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыСуммаВРознице

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаВРознице");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе);

		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"УчетВПродажныхЦенах", ВидСравненияКомпоновкиДанных.Равно, Истина);
			
			ГруппаОтбора3 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора2.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

				ГруппаОтбора4 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора3.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

					ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора4,
						"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.ОптовыйСклад);

					ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора4,
						"ТипСкладаПолучатель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора3,
					"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыЦенаВРознице

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыЦенаВРознице");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе);

		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"УчетВПродажныхЦенах", ВидСравненияКомпоновкиДанных.Равно, Истина);
			
			ГруппаОтбора3 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора2.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

				ГруппаОтбора4 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора3.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

					ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора4,
						"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.ОптовыйСклад);

					ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора4,
						"ТипСкладаПолучатель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

				ГруппаОтбора4 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора3.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

					ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора4,
						"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

					ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора4,
						"ТипСкладаПолучатель", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыСебестоимость

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСебестоимость");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"УчетВПродажныхЦенах", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.РозничныйМагазин);

			ГруппаОтбора3 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора2.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора3,
					"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора3,
					"ТипСкладаПолучатель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыДокументОприходования

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыДокументОприходования");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"СпособОценкиМПЗПоСредней", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ТипСкладаОтправитель", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьСостояниеДокумента();
	
	ТекущаяДатаДокумента = Объект.Дата;
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПеремещениеТоваров")
		И Объект.ВидОперации = Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСотрудниками Тогда
		ЭтаФорма.Элементы.ВидОперации.Видимость = Ложь;
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		Объект.ВидОперации = Перечисления.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСкладами;
	КонецЕсли;
	
	РазделятьПоСтавкамНДС =
		ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ.ВидыСубконто.Найти(
			ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС, "ВидСубконто") <> Неопределено;
	
	Если ЗначениеЗаполнено(Объект.СкладОтправитель) Тогда
		СвойстваСкладаОтправителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.СкладОтправитель, "ТипСклада, ТипЦенРозничнойТорговли");
		ТипЦенСкладОтправитель = СвойстваСкладаОтправителя.ТипЦенРозничнойТорговли;
		ТипСкладаОтправитель   = СвойстваСкладаОтправителя.ТипСклада;
	Иначе
		ТипЦенСкладОтправитель    = Справочники.ТипыЦенНоменклатуры.ПустаяСсылка();
		ТипСкладаОтправитель      = Перечисления.ТипыСкладов.ОптовыйСклад;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.СкладПолучатель) Тогда
		СвойстваСкладаПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.СкладПолучатель, "ТипСклада, ТипЦенРозничнойТорговли");
		ТипЦенСкладПолучатель = СвойстваСкладаПолучателя.ТипЦенРозничнойТорговли;
		ТипСкладаПолучатель   = СвойстваСкладаПолучателя.ТипСклада;
	Иначе
		ТипЦенСкладПолучатель = Справочники.ТипыЦенНоменклатуры.ПустаяСсылка();
		ТипСкладаПолучатель   = Перечисления.ТипыСкладов.ОптовыйСклад;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
	// Проверка буфера обмена на наличие скопированных строк
	УстановитьДоступностьКомандыВставки(ЭтотОбъект, Не ОбщегоНазначения.ПустойБуферОбмена());
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоШапки(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект		= Форма.Объект;
	Элементы	= Форма.Элементы;
	
	Если Объект.НДСвСтоимостиТоваров = ПредопределенноеЗначение("Перечисление.ДействиеНДСВСтоимостиТоваров.ВключитьВСтоимость") Тогда
		Элементы.ГруппаСчетИАналитикаСписанияНДС.Доступность = Истина;
	Иначе
		Элементы.ГруппаСчетИАналитикаСписанияНДС.Доступность = Ложь;
	КонецЕсли; 
	
	ПередачаМеждуСкладами = (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСкладами"));
	ПередачаМеждуСотрудниками = (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСотрудниками"));
	
	Элементы.СкладОтправитель.Видимость         = ПередачаМеждуСкладами;
	Элементы.СкладПолучатель.Видимость          = ПередачаМеждуСкладами;
	Элементы.ГруппаМатериалыЗаказчика.Видимость = ПередачаМеждуСкладами
                                                И Форма.ВедетсяПроизводственнаяДеятельность	
												И (Форма.ТипСкладаПолучатель  = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад"))
												И (Форма.ТипСкладаОтправитель = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад"));
	Элементы.СотрудникОтправитель.Видимость     = ПередачаМеждуСотрудниками;
	Элементы.СотрудникПолучатель.Видимость      = ПередачаМеждуСотрудниками;
	Элементы.ГруппаМатериалы.Видимость          = ПередачаМеждуСотрудниками;
	
	Если ПередачаМеждуСотрудниками И Элементы.ГруппаСтраницы.ТекущаяСтраница <> Элементы.ГруппаМатериалы Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаМатериалы;
	КонецЕсли;	
	
	Элементы.ПодразделениеОтправитель.Доступность = ЗначениеЗаполнено(Объект.Организация);
	Элементы.ПодразделениеПолучатель.Доступность  = ЗначениеЗаполнено(Объект.Организация);

	Если НужноУбратьЗакладкиСтраниц(Объект) ИЛИ ПередачаМеждуСотрудниками Тогда
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	КонецЕсли;
	
	Элементы.ТоварыИзменитьТовары.Видимость = ПередачаМеждуСкладами;
	Элементы.ФормаВыгрузитьВВЕТИС.Видимость = ПередачаМеждуСкладами; 
	КомандаВыгрузитьВЕГАИС = Элементы.Найти("ФормаВыгрузитьВЕГАИС");
	Если КомандаВыгрузитьВЕГАИС <> Неопределено Тогда
		КомандаВыгрузитьВЕГАИС.Видимость = ПередачаМеждуСкладами;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НужноУбратьЗакладкиСтраниц(Объект)

	Возврат НЕ ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта("ИспользоватьВозвратнуюТару", Объект) 
		И НЕ ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта("ОсуществляетсяРеализацияТоваровУслугКомитентов", Объект) 
		И НЕ ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта("РаздельныйУчетНДСДо2014Года", Объект)
        И НЕ ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта("ВедетсяПроизводственнаяДеятельность", Объект);
		
КонецФункции

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма);
	
	УчетВПродажныхЦенах =
		УчетнаяПолитика.СпособОценкиТоваровВРознице(Объект.Организация, Объект.Дата) = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;
	СпособОценкиМПЗПоСредней =
		УчетнаяПолитика.СпособОценкиМПЗ(Объект.Организация, Объект.Дата) = Перечисления.СпособыОценки.ПоСредней;
		
	ВедетсяПроизводственнаяДеятельность = ОбщегоНазначенияБПВызовСервера
		.ПолучитьФункциональнуюОпциюОбъекта("ВедетсяПроизводственнаяДеятельность", Объект);
	
КонецПроцедуры

// Серверная обработка изменения реквизитов:

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	УстановитьФункциональныеОпцииФормы();
	
	УстановитьНДСВСтоимостиТоваров();
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры 

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	УстановитьФункциональныеОпцииФормы();
	
	Объект.ПодразделениеОтправитель	= ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(
		Объект.Организация, Объект.СкладОтправитель);
		
	Объект.ПодразделениеПолучатель	= ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(
		Объект.Организация, Объект.СкладПолучатель);
	
	УстановитьНДСВСтоимостиТоваров();
	
	Изменения = Новый Массив;
	Изменения.Добавить("Организация");
	Изменения.Добавить("НДСвСтоимостиТоваров");
	
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(НачатьЗаполнениеСчетовУчета(Изменения, Объект));
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры 

&НаСервере
Процедура СкладОтправительПриИзмененииНаСервере()
	
	СвойстваСкладаОтправителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Объект.СкладОтправитель, "ТипСклада, ТипЦенРозничнойТорговли");
	ТипЦенСкладОтправитель = СвойстваСкладаОтправителя.ТипЦенРозничнойТорговли;
	ТипСкладаОтправитель   = СвойстваСкладаОтправителя.ТипСклада;
	
	Объект.ПодразделениеОтправитель = ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(
		Объект.Организация, Объект.СкладОтправитель);
	
	УстановитьНДСВСтоимостиТоваров();
	
	ЗаполнитьСтрокиТабличнойЧастиТоварыНаСервере();
	
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(НачатьЗаполнениеСчетовУчета("СкладОтправитель", Объект));
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры 

&НаСервере
Процедура СкладПолучательПриИзмененииНаСервере()
	
	СвойстваСкладаПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Объект.СкладПолучатель, "ТипСклада, ТипЦенРозничнойТорговли");
	ТипЦенСкладПолучатель = СвойстваСкладаПолучателя.ТипЦенРозничнойТорговли;
	ТипСкладаПолучатель   = СвойстваСкладаПолучателя.ТипСклада;
	
	Объект.ПодразделениеПолучатель = ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(
		Объект.Организация, Объект.СкладПолучатель);
	
	УстановитьНДСВСтоимостиТоваров();
	
	ЗаполнитьСтрокиТабличнойЧастиТоварыНаСервере();
	
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(НачатьЗаполнениеСчетовУчета("СкладПолучатель", Объект));
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокиТабличнойЧастиТоварыНаСервере()
	
	Документы.ПеремещениеТоваров.ЗаполнитьСтрокиТабличнойЧастиТовары(Объект, ТипСкладаОтправитель, ТипСкладаПолучатель, РазделятьПоСтавкамНДС);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМатериалыСотрудникаПоОстаткам()
	
	Если Объект.Материалы.Количество() > 0 Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗаполнениемТаблицыМатериалыПоОстаткамЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='При заполнении табличная часть будет очищена.
			|Продолжить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	Иначе
		ЗаполнитьМатериалыСотрудникаПоОстаткамНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМатериалыЗаказчикаПоОстаткам()
	
	Если Объект.МатериалыЗаказчика.Количество() > 0 Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗаполнениемТаблицыМатериалыЗаказчикаПоОстаткамЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='При заполнении табличная часть будет очищена.
			|Продолжить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	Иначе
		ЗаполнитьМатериалыЗаказчикаПоОстаткамНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗаполнениемТаблицыМатериалыПоОстаткамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьМатериалыСотрудникаПоОстаткамНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗаполнениемТаблицыМатериалыЗаказчикаПоОстаткамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьМатериалыЗаказчикаПоОстаткамНаСервере(); 
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМатериалыСотрудникаПоОстаткамНаСервере()
	
	Объект.Материалы.Очистить();
	
	ПараметрыЗаполнения = Новый Структура("Дата, Организация, ПодразделениеОрганизации, Сотрудник, Ссылка");
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, Объект);
	ПараметрыЗаполнения.ПодразделениеОрганизации = Объект.ПодразделениеОтправитель;
	ПараметрыЗаполнения.Сотрудник                = Объект.СотрудникОтправитель;
	
	УчетМатериаловВЭксплуатации.ЗаполнитьПоОстаткамПереданныеСотрудникуМатериалы(ПараметрыЗаполнения, Объект.Материалы, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМатериалыЗаказчикаПоОстаткамНаСервере()

	Объект.МатериалыЗаказчика.Очистить();

	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация"  , Объект.Организация);
	Запрос.УстановитьПараметр("Склад"        , Объект.СкладОтправитель);
	Запрос.УстановитьПараметр("Подразделение", Объект.ПодразделениеОтправитель);
	Если Объект.Ссылка.Пустая() Тогда
		Запрос.УстановитьПараметр("Период", Новый Граница(Объект.Дата, ВидГраницы.Исключая));
	Иначе
		Запрос.УстановитьПараметр("Период",
			Новый Граница(Новый МоментВремени(Объект.Дата, Объект.Ссылка), ВидГраницы.Исключая));
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("СчетаУчета"  , БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОстатки.Субконто2 КАК Номенклатура,
	|	ХозрасчетныйОстатки.Субконто2.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	1 КАК Коэффициент,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество,
	|	ХозрасчетныйОстатки.Счет КАК СчетУчета
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет В (&СчетаУчета),
	|			&ВидыСубконто,
	|			Организация = &Организация
	|				И (Подразделение = &Подразделение
	|					ИЛИ Подразделение ЕСТЬ NULL)
	|				И Субконто3 = &Склад) КАК ХозрасчетныйОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Объект.МатериалыЗаказчика.Добавить(), Выборка);
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЕдинственныхЗаказчиковМатериалов(СписокНоменклатуры)

	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация"  , Объект.Организация);
	Запрос.УстановитьПараметр("Склад"        , Объект.СкладОтправитель);
	Запрос.УстановитьПараметр("Подразделение", Объект.ПодразделениеОтправитель);
	Если Объект.Ссылка.Пустая() Тогда
		Запрос.УстановитьПараметр("Период", Новый Граница(Объект.Дата, ВидГраницы.Исключая));
	Иначе
		Запрос.УстановитьПараметр("Период",
			Новый Граница(Новый МоментВремени(Объект.Дата, Объект.Ссылка), ВидГраницы.Исключая));
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидыСубконто"      , ВидыСубконто);
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.УстановитьПараметр("СчетаУчета"        , БухгалтерскийУчетПовтИсп.СчетаВИерархии(
		ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто2 КАК Номенклатура,
	|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ХозрасчетныйОстатки.Субконто1) КАК КоличествоКонтрагентов,
	|	СУММА(ХозрасчетныйОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет В (&СчетаУчета),
	|			&ВидыСубконто,
	|			Организация = &Организация
	|				И (Подразделение = &Подразделение
	|					ИЛИ Подразделение ЕСТЬ NULL)
	|				И Субконто3 = &Склад
	|				И Субконто2 В (&СписокНоменклатуры)) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Субконто2,
	|	ХозрасчетныйОстатки.Субконто1
	|
	|ИМЕЮЩИЕ
	|	СУММА(ХозрасчетныйОстатки.КоличествоОстаток) > 0
	|ИТОГИ
	|	СУММА(КоличествоКонтрагентов)
	|ПО
	|	Номенклатура";
	
	КонтрагентыНоменклатуры = Новый Соответствие;
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.КоличествоКонтрагентов = 1 Тогда
			Выборка = ВыборкаНоменклатура.Выбрать();
			Выборка.Следующий();
			КонтрагентыНоменклатуры.Вставить(Выборка.Номенклатура, Выборка.Контрагент);
		КонецЕсли;	
	КонецЦикла;	

	Возврат КонтрагентыНоменклатуры;	
	
КонецФункции	

// Пересчеты реквизитов в строках табличных частей

&НаСервереБезКонтекста
Функция ПолучитьДанныеТоварыНоменклатураПриИзменении(Знач Контекст, Знач ДанныеСтроки, Знач СчетаУчетаКЗаполнению)
	
	Результат = Новый Структура;
	
	ПараметрыДанныхОНоменклатуре = Новый Структура("Дата, Организация, Склад", Контекст.Дата, Контекст.Организация, );
	
	// СкладОтправитель
	ПараметрыДанныхОНоменклатуре.Склад	= Контекст.СкладОтправитель;
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		ДанныеСтроки.Номенклатура, ПараметрыДанныхОНоменклатуре, Ложь);
	
	Если СведенияОНоменклатуре <> Неопределено Тогда
		Результат.Вставить("ЕдиницаИзмерения", СведенияОНоменклатуре.ЕдиницаИзмерения);
		Результат.Вставить("Коэффициент",      СведенияОНоменклатуре.Коэффициент);
	КонецЕсли;
	
	// СкладПолучатель
	ПараметрыДанныхОНоменклатуре.Склад	= Контекст.СкладПолучатель;
	НовыеСведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		ДанныеСтроки.Номенклатура, ПараметрыДанныхОНоменклатуре, Ложь);
		
	ОпределенаЦена = Ложь;
	Если Контекст.ТипСкладаОтправитель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
		
		Если Контекст.РазделятьПоСтавкамНДС И СведенияОНоменклатуре <> Неопределено Тогда
			Результат.Вставить("СтавкаНДСВРознице", СведенияОНоменклатуре.СтавкаНДСВРознице);
		КонецЕсли;
		
		Если Контекст.ТипСкладаПолучатель = Перечисления.ТипыСкладов.ОптовыйСклад И СведенияОНоменклатуре <> Неопределено Тогда
			Результат.Вставить("ЦенаВРознице", СведенияОНоменклатуре.ЦенаВРознице);
		КонецЕсли;
		
	ИначеЕсли Контекст.ТипСкладаПолучатель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
		
		Если Контекст.РазделятьПоСтавкамНДС И НовыеСведенияОНоменклатуре <> Неопределено Тогда
			Результат.Вставить("СтавкаНДСВРознице", НовыеСведенияОНоменклатуре.СтавкаНДСВРознице);
		КонецЕсли;
		
		Если Контекст.ТипСкладаОтправитель = Перечисления.ТипыСкладов.ОптовыйСклад И НовыеСведенияОНоменклатуре <> Неопределено Тогда
			Результат.Вставить("ЦенаВРознице", НовыеСведенияОНоменклатуре.ЦенаВРознице);
		КонецЕсли;
		
	КонецЕсли;
	
	СчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.ПеремещениеТоваров,
		СчетаУчетаКЗаполнению,
		Контекст,
		"Товары",
		ДанныеСтроки);
		
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, СчетаУчета, Истина);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеТоварыНаКомиссииНоменклатураПриИзменении(Знач ДанныеОбъекта, Знач ДанныеСтроки, Знач СчетаУчетаКЗаполнению)

	Результат = Новый Структура;
	
	ПараметрыСведенийОНоменклатуре = Новый Структура;
	ПараметрыСведенийОНоменклатуре.Вставить("Дата",        ДанныеОбъекта.Дата);
	ПараметрыСведенийОНоменклатуре.Вставить("Организация", ДанныеОбъекта.Организация);
	ПараметрыСведенийОНоменклатуре.Вставить("Склад",       ДанныеОбъекта.СкладОтправитель);
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		ДанныеСтроки.Номенклатура, 
		ПараметрыСведенийОНоменклатуре, 
		Ложь, 
		Ложь);
	
	Если СведенияОНоменклатуре <> Неопределено Тогда
		Результат.Вставить("ЕдиницаИзмерения", СведенияОНоменклатуре.ЕдиницаИзмерения);
		Результат.Вставить("Коэффициент",      СведенияОНоменклатуре.Коэффициент);
	КонецЕсли;
	
	ЗаполненныеСчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.ПеремещениеТоваров,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"ТоварыНаКомиссии",
		ДанныеСтроки);
		
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ЗаполненныеСчетаУчета, Истина);
	
	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеМатериалыЗаказчикаНоменклатураПриИзменении(Знач ДанныеСтроки, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)

	Результат = Новый Структура;
	
	Если ЗначениеЗаполнено(ДанныеСтроки.Номенклатура) Тогда
		Результат.Вставить("ЕдиницаИзмерения", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСтроки.Номенклатура, "ЕдиницаИзмерения"));
		Результат.Вставить("Коэффициент",      1);
	КонецЕсли;
	
	ЗаполненныеСчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.ПеремещениеТоваров,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"МатериалыЗаказчика",
		ДанныеСтроки);
		
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ЗаполненныеСчетаУчета, Истина);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПолучитьПараметрыПодбора(ИмяТаблицы)
	
	ПараметрыФормы = Новый Структура;
	
	ДатаРасчетов 	 = ?(НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДата()), Неопределено, Объект.Дата);
	ЗаголовокПодбора = НСтр("ru = 'Подбор номенклатуры в %1 (%2)'");
	
	ПередачаМеждуСкладами = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПеремещениеТоваров.ПередачаМеждуСкладами");
	
	Валюта = ВалютаРегламентированногоУчета;
	
	ПараметрыФормы.Вставить("ПоказыватьОстатки"    , Истина);
	ПараметрыФормы.Вставить("КомандаВыбратьОстаток", Истина);
	
	Если ИмяТаблицы = "Товары" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Товары'");
	ИначеЕсли ИмяТаблицы = "Материалы" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Материалы'");
	ИначеЕсли ИмяТаблицы = "ТоварыНаКомиссии" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Товары на комиссии'");
	ИначеЕсли ИмяТаблицы = "ВозвратнаяТара" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Возвратная тара'");
	ИначеЕсли ИмяТаблицы = "МатериалыЗаказчика" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Материалы заказчика'");
	КонецЕсли;
	ЗаголовокПодбора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокПодбора, Объект.Ссылка, ПредставлениеТаблицы);
	
	Если ИмяТаблицы = "Товары" Тогда
		
		Если ТипСкладаОтправитель = ПредопределенноеЗначение("Перечисление.ТипыСкладов.НеавтоматизированнаяТорговаяТочка")
			И ТипСкладаПолучатель = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад") Тогда
			
			ПараметрыФормы.Вставить("ЕстьЦена",       Истина);
			ПараметрыФормы.Вставить("ПоказыватьЦены", ЗначениеЗаполнено(ТипЦенСкладОтправитель));
			ПараметрыФормы.Вставить("ДатаЦены",       Объект.Дата);
			ПараметрыФормы.Вставить("ТипЦен",         ТипЦенСкладОтправитель);
			
		ИначеЕсли ТипСкладаОтправитель = ПредопределенноеЗначение("Перечисление.ТипыСкладов.ОптовыйСклад")
			И ТипСкладаПолучатель = ПредопределенноеЗначение("Перечисление.ТипыСкладов.НеавтоматизированнаяТорговаяТочка") Тогда
			
			ПараметрыФормы.Вставить("ЕстьЦена",       Истина);
			ПараметрыФормы.Вставить("ПоказыватьЦены", ЗначениеЗаполнено(ТипЦенСкладПолучатель));
			ПараметрыФормы.Вставить("ДатаЦены",       Объект.Дата);
			ПараметрыФормы.Вставить("ТипЦен",         ТипЦенСкладПолучатель);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ПоказыватьЗабалансовыеОстатки", (ИмяТаблицы = "ТоварыНаКомиссии") ИЛИ (ИмяТаблицы = "МатериалыЗаказчика"));
	ПараметрыФормы.Вставить("ЕстьКоличество",      Истина);
	ПараметрыФормы.Вставить("ДатаРасчетов",        ДатаРасчетов);
	ПараметрыФормы.Вставить("Валюта",              Валюта);
	ПараметрыФормы.Вставить("Организация",         Объект.Организация);
	ПараметрыФормы.Вставить("Подразделение",       Объект.ПодразделениеОтправитель);
	ПараметрыФормы.Вставить("Склад",               ?(ПередачаМеждуСкладами, Объект.СкладОтправитель, Неопределено));
	ПараметрыФормы.Вставить("ПоказыватьОстатки",   ПередачаМеждуСкладами);
	ПараметрыФормы.Вставить("Заголовок",           ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВидПодбора",          ПолучитьВидПодбора(ИмяТаблицы));
	ПараметрыФормы.Вставить("ИмяТаблицы",          ИмяТаблицы);
	ПараметрыФормы.Вставить("Услуги",              Ложь);

	Возврат ПараметрыФормы;

КонецФункции

&НаКлиенте
Функция ПолучитьВидПодбора(ИмяТаблицы)

	ВидПодбора = "";

	Если ИмяТаблицы = "ТоварыНаКомиссии" Тогда
		 ВидПодбора = "КОМИТЕНТ";
	ИначеЕсли ИмяТаблицы = "МатериалыЗаказчика" Тогда
		 ВидПодбора = "МатериалыЗаказчика";
	ИначеЕсли ЗначениеЗаполнено(Объект.СкладОтправитель)
		И ТипСкладаОтправитель = ПредопределенноеЗначение("Перечисление.ТипыСкладов.НеавтоматизированнаяТорговаяТочка") Тогда
		ВидПодбора = "НТТ";
	КонецЕсли;

	Возврат ВидПодбора;

КонецФункции

&НаСервере
Процедура УстановитьНДСВСтоимостиТоваров()
	
	Объект.НДСвСтоимостиТоваров = Документы.ПеремещениеТоваров.НДСВСтоимостиТоваровПоСкладам(
		Объект.Организация, Объект.Дата, Объект.СкладОтправитель, Объект.СкладПолучатель);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТоварыВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаКлиенте
Функция ПолучитьПараметрыОбработкиТабличнойЧастиТовары()

	ПараметрыОбработки = Новый Структура;
	
	ПараметрыОбработки.Вставить("АдресХранилищаТовары",		ПоместитьТоварыВоВременноеХранилищеНаСервере());
	ПараметрыОбработки.Вставить("ЗаполнятьЦеныПоПокупке",	Ложь);
	ПараметрыОбработки.Вставить("ДокументСсылка",			Объект.Ссылка);
	ПараметрыОбработки.Вставить("ДокументДата",				Объект.Дата);
	ПараметрыОбработки.Вставить("ДокументОрганизация",		Объект.Организация);
	ПараметрыОбработки.Вставить("ДокументСклад",			Объект.СкладПолучатель);
	ПараметрыОбработки.Вставить("ИсключатьРеквизиты",		"Цена");
	
	Возврат ПараметрыОбработки;

КонецФункции 

&НаСервере
Процедура ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИмяТаблицы)

	ЭтоВставкаИзБуфера = ВыбранноеЗначение.Свойство("ЭтоВставкаИзБуфера");
	СписокСвойств = Неопределено;
	Если ЭтоВставкаИзБуфера Тогда
		
		ТаблицаТоваров = ВыбранноеЗначение.Данные;
		СписокСвойств = ВыбранноеЗначение.СписокСвойств;
		
		Если ИмяТаблицы = "МатериалыЗаказчика" Тогда
			СписокСвойств = "Номенклатура, КоличествоМест, ЕдиницаИзмерения, Коэффициент, Количество, Контрагент"; 
		КонецЕсли;	
		
	Иначе
		
		ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
		
	КонецЕсли;
	
	СписокНоменклатуры	= ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина);
	
	Если ИмяТаблицы = "МатериалыЗаказчика" Тогда
		Если ТаблицаТоваров.Колонки.Найти("Контрагент") = Неопределено Тогда
			ТаблицаТоваров.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		КонецЕсли;
		
		КонтрагентыНоменклатуры = ПолучитьЕдинственныхЗаказчиковМатериалов(СписокНоменклатуры);
		Для Каждого КонтрагентНоменклатуры Из КонтрагентыНоменклатуры Цикл
			ПоискНоменклатуры = Новый Структура("Номенклатура", КонтрагентНоменклатуры.Ключ);
			НайденныеСтроки = ТаблицаТоваров.НайтиСтроки(ПоискНоменклатуры);
			Для Каждого НайденныеСтроки Из НайденныеСтроки Цикл
				НайденныеСтроки.Контрагент = КонтрагентНоменклатуры.Значение;	
			КонецЦикла;
		КонецЦикла;	

	КонецЕсли;	
	
	ДобавленныеСтроки = Новый Массив;
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	ДанныеОбъекта.Склад	= Объект.СкладОтправитель;
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		СписокНоменклатуры, ДанныеОбъекта, Ложь, Ложь);
	
	ДанныеОбъекта.Склад	= Объект.СкладПолучатель;
	СоответствиеНовыхСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		СписокНоменклатуры, ДанныеОбъекта, Ложь, Ложь);
		
	ЕстьРеквизитЦена = ТаблицаТоваров.Колонки.Найти("Цена") <> Неопределено;
		
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТовара.Номенклатура);
		Если ЭтоВставкаИзБуфера 
			И СведенияОНоменклатуре <> Неопределено
			И ЗначениеЗаполнено(СведенияОНоменклатуре.Услуга)
			И СведенияОНоменклатуре.Услуга Тогда
			
			Продолжить;
			
		КонецЕсли;
		СтрокаТабличнойЧасти	= Объект[ИмяТаблицы].Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТовара, СписокСвойств);
		ДобавленныеСтроки.Добавить(СтрокаТабличнойЧасти);
		
		НовыеСведенияОНоменклатуре = СоответствиеНовыхСведенийОНоменклатуре.Получить(СтрокаТабличнойЧасти.Номенклатура);
		
		Если (ИмяТаблицы = "Товары" ИЛИ ИмяТаблицы = "ТоварыНаКомиссии" ИЛИ ИмяТаблицы = "МатериалыЗаказчика") 
			И СведенияОНоменклатуре <> Неопределено Тогда
			СтрокаТабличнойЧасти.ЕдиницаИзмерения	= СведенияОНоменклатуре.ЕдиницаИзмерения;
			СтрокаТабличнойЧасти.Коэффициент		= СведенияОНоменклатуре.Коэффициент;
		КонецЕсли;
		
		Если ИмяТаблицы = "Товары" Тогда
			
			Если ТипСкладаОтправитель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
				
				Если РазделятьПоСтавкамНДС И СведенияОНоменклатуре <> Неопределено Тогда
					СтрокаТабличнойЧасти.СтавкаНДСВРознице = СведенияОНоменклатуре.СтавкаНДСВРознице;
				КонецЕсли;
				
				Если ТипСкладаПолучатель = Перечисления.ТипыСкладов.ОптовыйСклад  Тогда
					ЦенаВРознице = ?(ЕстьРеквизитЦена, СтрокаТовара.Цена, НовыеСведенияОНоменклатуре.ЦенаВРознице);
					
					СтрокаТабличнойЧасти.ЦенаВРознице	= ЦенаВРознице;
					СтрокаТабличнойЧасти.СуммаВРознице	= ЦенаВРознице * СтрокаТабличнойЧасти.Количество;
				КонецЕсли;
				
			ИначеЕсли ТипСкладаПолучатель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
				
				Если РазделятьПоСтавкамНДС И НовыеСведенияОНоменклатуре <> Неопределено Тогда
					СтрокаТабличнойЧасти.СтавкаНДСВРознице = НовыеСведенияОНоменклатуре.СтавкаНДСВРознице;
				КонецЕсли;
				
				Если ТипСкладаОтправитель = Перечисления.ТипыСкладов.ОптовыйСклад Тогда
					ЦенаВРознице = ?(ЕстьРеквизитЦена, СтрокаТовара.Цена, НовыеСведенияОНоменклатуре.ЦенаВРознице);
					
					СтрокаТабличнойЧасти.ЦенаВРознице	= ЦенаВРознице;
					СтрокаТабличнойЧасти.СуммаВРознице	= ЦенаВРознице * СтрокаТабличнойЧасти.Количество;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СчетаУчетаВДокументах.ЗаполнитьСтроки(ДобавленныеСтроки, ИмяТаблицы, Объект, Документы.ПеремещениеТоваров);
	
	Если ЭтоВставкаИзБуфера Тогда
		
		ВыбранноеЗначение.КоличествоДобавленныхСтрок = ДобавленныеСтроки.Количество();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметры)
	
	СтрокиДляДополнительнойОбработки = Обработки.ИзменениеТаблицыТоваров.ЗагрузитьТабличнуюЧастьВДанныеФормы(Объект.Товары, Параметры);
	
	СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиДляДополнительнойОбработки, "Товары", Объект, Документы.ПеремещениеТоваров, Истина);

	Если РазделятьПоСтавкамНДС 
		И (ТипСкладаОтправитель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка 
		Или ТипСкладаПолучатель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка) Тогда
	
		СписокНоменклатуры	= ОбщегоНазначения.ВыгрузитьКолонку(СтрокиДляДополнительнойОбработки, "Номенклатура", Истина);
		ДанныеОбъекта = Новый Структура("Дата, Организация, Склад");
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
		
		// Для склада-отправителя
		ДанныеОбъекта.Склад	= Объект.СкладОтправитель;
		СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
			СписокНоменклатуры, ДанныеОбъекта, Ложь, Ложь);
			
		// Для склада-получателя
		ДанныеОбъекта.Склад	= Объект.СкладПолучатель;
		СоответствиеНовыхСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
			СписокНоменклатуры, ДанныеОбъекта, Ложь, Ложь);
	
		Для Каждого СтрокаТабличнойЧасти ИЗ СтрокиДляДополнительнойОбработки Цикл
				
			Если ТипСкладаОтправитель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
				СведенияОНоменклатуре	= СоответствиеСведенийОНоменклатуре.Получить(СтрокаТабличнойЧасти.Номенклатура);
				Если СведенияОНоменклатуре <> Неопределено Тогда
					СтрокаТабличнойЧасти.СтавкаНДСВРознице = СведенияОНоменклатуре.СтавкаНДСВРознице;
				КонецЕсли;
			ИначеЕсли ТипСкладаПолучатель = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
				НовыеСведенияОНоменклатуре	= СоответствиеНовыхСведенийОНоменклатуре.Получить(СтрокаТабличнойЧасти.Номенклатура);
				Если НовыеСведенияОНоменклатуре <> Неопределено Тогда
					СтрокаТабличнойЧасти.СтавкаНДСВРознице = НовыеСведенияОНоменклатуре.СтавкаНДСВРознице;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконто(Форма)
	
	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"СубконтоСписанияНДС", "", "СубконтоСписанияНДС", "", "СчетСписанияНДС");
	
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Объект.Организация);
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Объект, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСубконто(НомерСубконто)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСубконто(
		ЭтотОбъект, Объект, НомерСубконто, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
КонецПроцедуры

#Область СчетаУчета

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив(); 
	ЭлементыСчетов.Добавить("ТоварыСчетУчета");
	ЭлементыСчетов.Добавить("ТоварыНовыйСчетУчета");
	ЭлементыСчетов.Добавить("ТоварыСпособУчетаНДС");
	ЭлементыСчетов.Добавить("ТоварыНаКомиссииСчетУчета");
	ЭлементыСчетов.Добавить("ТоварыНаКомиссииНовыйСчетУчета");
	ЭлементыСчетов.Добавить("ВозвратнаяТараСчетУчета");
	ЭлементыСчетов.Добавить("ВозвратнаяТараНовыйСчетУчета");
	ЭлементыСчетов.Добавить("МатериалыЗаказчикаСчетУчета");
	
	ЭлементыСчетов.Добавить("ГруппаСчетИАналитикаСписанияНДС"); // Содержит имена счета и субконто
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НачатьЗаполнениеСчетовУчета(ПричиныИзменения, Объект = Неопределено, СтрокаСписка = Неопределено, КонтейнерОбъект = Неопределено, КонтейнерСтрокаСписка = Неопределено)

	// Код этой функции сформирован автоматически с помощью СчетаУчетаВДокументах.КодФункцииНачатьЗаполнениеСчетовУчета()

	ПараметрыЗаполнения = СчетаУчетаВДокументахКлиентСервер.НовыйПараметрыЗаполнения(
		"ПеремещениеТоваров",
		ПричиныИзменения,
		Объект,
		СтрокаСписка,
		КонтейнерОбъект,
		КонтейнерСтрокаСписка);

	// 1. Заполняемые реквизиты
	// НДСвСтоимостиТоваров
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("НДСвСтоимостиТоваров") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СчетСписанияНДС");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС1");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС2");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС3");
	КонецЕсли;

	// Организация
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Организация") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СчетСписанияНДС");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС1");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС2");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС3");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.НовыйСчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СпособУчетаНДС");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ВозвратнаяТара.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ВозвратнаяТара.НовыйСчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ТоварыНаКомиссии.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ТоварыНаКомиссии.НовыйСчетУчета"); 
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "МатериалыЗаказчика.СчетУчета");
	КонецЕсли;

	// Дата
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Дата") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СчетСписанияНДС");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС1");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС2");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС3");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.НовыйСчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СпособУчетаНДС");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ВозвратнаяТара.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ВозвратнаяТара.НовыйСчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ТоварыНаКомиссии.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ТоварыНаКомиссии.НовыйСчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "МатериалыЗаказчика.СчетУчета");
 	КонецЕсли;

	// СчетСписанияНДС
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("СчетСписанияНДС") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС1");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС2");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "СубконтоСписанияНДС3");
	КонецЕсли;

	// Товары.Номенклатура
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Товары.Номенклатура") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.НовыйСчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СпособУчетаНДС");
	КонецЕсли;
	
	// СкладОтправитель
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("СкладОтправитель") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ВозвратнаяТара.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ТоварыНаКомиссии.СчетУчета");
	КонецЕсли;

	// СкладПолучатель
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("СкладПолучатель") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.НовыйСчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "Товары.СпособУчетаНДС");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ВозвратнаяТара.НовыйСчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ТоварыНаКомиссии.НовыйСчетУчета");
	КонецЕсли;

	// ВозвратнаяТара.Номенклатура
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("ВозвратнаяТара.Номенклатура") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ВозвратнаяТара.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ВозвратнаяТара.НовыйСчетУчета");
	КонецЕсли;

	// ТоварыНаКомиссии.Номенклатура
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("ТоварыНаКомиссии.Номенклатура") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ТоварыНаКомиссии.СчетУчета");
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "ТоварыНаКомиссии.НовыйСчетУчета");
	КонецЕсли;

	// МатериалыЗаказчика.Номенклатура
	Если ПараметрыЗаполнения.ПричиныИзменения.Найти("МатериалыЗаказчика.Номенклатура") <> Неопределено Тогда
		СчетаУчетаВДокументахКлиентСервер.НачатьЗаполнениеРеквизита(ПараметрыЗаполнения, "МатериалыЗаказчика.СчетУчета");
	КонецЕсли;
	
	// 2. (если требуется) Передадим на сервер данные, необходимые для заполнения
	Если ПараметрыЗаполнения.Свойство("Контейнер") Тогда
		// НДСвСтоимостиТоваров
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("НДСвСтоимостиТоваров") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "НДСвСтоимостиТоваров");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СчетСписанияНДС");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС1");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС2");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС3");
		КонецЕсли;

		// Организация
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Организация") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СчетСписанияНДС");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "НДСвСтоимостиТоваров");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС1");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС2");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС3");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладОтправитель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладПолучатель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СпособУчетаНДС");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
            СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
            СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
		КонецЕсли;

		// Дата
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Дата") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СчетСписанияНДС");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "НДСвСтоимостиТоваров");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС1");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС2");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС3");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладОтправитель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладПолучатель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СпособУчетаНДС");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
            СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
            СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
		КонецЕсли;

		// СчетСписанияНДС
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("СчетСписанияНДС") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СчетСписанияНДС");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС1");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "НДСвСтоимостиТоваров");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС2");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СубконтоСписанияНДС3");
		КонецЕсли;

		// Товары.Номенклатура
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("Товары.Номенклатура") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладОтправитель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладПолучатель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СпособУчетаНДС");
		КонецЕсли;

		// СкладОтправитель
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("СкладОтправитель") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладОтправитель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
		КонецЕсли;

		// СкладПолучатель
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("СкладПолучатель") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладПолучатель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СпособУчетаНДС");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
		КонецЕсли;

		// ВозвратнаяТара.Номенклатура
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("ВозвратнаяТара.Номенклатура") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладОтправитель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладПолучатель");
		КонецЕсли;

		// ТоварыНаКомиссии.Номенклатура
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("ТоварыНаКомиссии.Номенклатура") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладОтправитель");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "НовыйСчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "СкладПолучатель");
		КонецЕсли;  
		
		// МатериалыЗаказчика.Номенклатура
		Если ПараметрыЗаполнения.ПричиныИзменения.Найти("МатериалыЗаказчика.Номенклатура") <> Неопределено Тогда
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "Номенклатура");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Строка", "СчетУчета");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Организация");
			СчетаУчетаВДокументахКлиентСервер.ДополнитьДанныеЗаполнения(ПараметрыЗаполнения, "Объект", "Дата");
		КонецЕсли;

	КонецЕсли; // Нужно передавать на сервер данные заполнения
	
	Возврат ПараметрыЗаполнения;

КонецФункции

#КонецОбласти

#Область КопированиеВставкаСтрокЧерезБуферОбмена

&НаСервере
Процедура СкопироватьСтрокиНаСервере(ИмяТаблицы)
	
	ОбщегоНазначения.СкопироватьСтрокиВБуферОбмена(Объект[ИмяТаблицы], 
		Элементы[ИмяТаблицы].ВыделенныеСтроки, Объект.Ссылка.Метаданные().Имя);

КонецПроцедуры

&НаСервере
Функция ВставитьСтрокиНаСервере(ИмяТаблицы)
	
	ПараметрыВставки = ОбработкаТабличныхЧастей.ПолучитьПараметрыВставкиДанныхИзБуфераОбмена(Объект.Ссылка, ИмяТаблицы);
	ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки);
	ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ПараметрыВставки, ПараметрыВставки.ИмяТаблицы);
	
	Возврат ПараметрыВставки.КоличествоДобавленныхСтрок;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИмяТекущейТабличнойЧасти()
	
	ИмяТекущейСтраницы = Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя;
	ИмяТаблицы = "";
	Если ИмяТекущейСтраницы = "ГруппаТовары" Тогда
		ИмяТаблицы = "Товары";
	ИначеЕсли ИмяТекущейСтраницы = "ГруппаМатериалыЗаказчика" Тогда	
		ИмяТаблицы = "МатериалыЗаказчика";
	КонецЕсли;
	
	Возврат ИмяТаблицы;
	
КонецФункции

&НаСервере
Процедура ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки)

	СписокСвойств = Новый Массив;
	СписокСвойств.Добавить("Номенклатура");
	СписокСвойств.Добавить("КоличествоМест");
	СписокСвойств.Добавить("ЕдиницаИзмерения");
	СписокСвойств.Добавить("Коэффициент");
	СписокСвойств.Добавить("Количество");
	СписокСвойств.Добавить("Цена");
	СписокСвойств.Добавить("ЦенаВРознице");
	СписокСвойств.Добавить("СтавкаНДСВРознице");
	
	Если ПараметрыВставки.ПоказыватьСчетаУчетаВДокументах Тогда
		
		СписокСвойств.Добавить("СчетУчета");
		Если ПараметрыВставки.ИсточникИДокументОдногоВида Тогда
			
			СписокСвойств.Добавить("НовыйСчетУчета");
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыВставки.СписокСвойств = ОбработкаТабличныхЧастей.ПолучитьСписокСвойствИмеющихсяВТаблицеДанных(
		ПараметрыВставки.Данные, СписокСвойств);
	
КонецПроцедуры
	
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандыВставки(Форма, Доступность)

	Доступность = Не Форма.ТолькоПросмотр И Доступность;
	Элементы = Форма.Элементы;
	Элементы.ТоварыВставитьСтроки.Доступность					 = Доступность;
	Элементы.ТоварыКонтекстноеМенюВставитьСтроки.Доступность	 = Доступность;

	Элементы.МатериалыЗаказчикаВставитьСтроки.Доступность				 = Доступность;
	Элементы.МатериалыЗаказчикаКонтекстноеМенюВставитьСтроки.Доступность = Доступность;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами	
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтаФорма, ПараметрыЗаписи);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

КонецПроцедуры

#КонецОбласти

#Область БЭД

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДОНажатие(Элемент, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти