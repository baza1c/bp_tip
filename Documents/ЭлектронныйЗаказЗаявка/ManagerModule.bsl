#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ЭлектронныйЗаказЗаявка.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Для использования в процедуре ДобавитьКомандыСозданияНаОсновании других модулей менеджеров объектов.
// Добавляет в список команд создания на основании этот объект.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено - описание добавленной команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды")
		И ПравоДоступа("Добавление", Метаданные.Документы.ЭлектроннаяТранспортнаяНакладная) Тогда
		МодульСозданиеНаОсновании = ОбщегоНазначения.ОбщийМодуль("СозданиеНаОсновании");
		КомандаСозданияНаОсновании = МодульСозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(КомандыСозданияНаОсновании, Метаданные.Документы.ЭлектроннаяТранспортнаяНакладная);
		КомандаСозданияНаОсновании.ОтображениеКнопки = ОтображениеКнопки.КартинкаИТекст;
		Возврат КомандаСозданияНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("ТитулГрузоотправителяЗаказНомер");
	Поля.Добавить("ТитулГрузоотправителяФункция");
	Поля.Добавить("ЭтоВходящий");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Номер = Данные.Номер;
	Направление = "";
	
	Если ЗначениеЗаполнено(Данные.ТитулГрузоотправителяФункция) Тогда
		ФункцияДокумента = НРег(Данные.ТитулГрузоотправителяФункция);
	Иначе
		ФункцияДокумента = "заказ (заявка)";
	КонецЕсли;
	Если ЗначениеЗаполнено(Данные.ТитулГрузоотправителяЗаказНомер) Тогда
		Номер = Данные.ТитулГрузоотправителяЗаказНомер;
	КонецЕсли;
	Если ЗначениеЗаполнено(Данные.ЭтоВходящий) Тогда
		Направление = ?(Данные.ЭтоВходящий, " " + НСтр("ru = 'входящий'"), "");
	КонецЕсли;
	
	Дата = Формат(Данные.Дата, "ДФ=dd.MM.yyyy");
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Электронный %1%2 № %3 от %4'"), ФункцияДокумента, Направление, Номер, Дата);
	СтандартнаяОбработка = Ложь;
		
КонецПроцедуры

#КонецОбласти