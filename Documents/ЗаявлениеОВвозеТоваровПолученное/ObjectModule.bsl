#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ДокументыОтгрузки.Количество() = 0 Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Список", "Заполнение",,,"ДокументыОтгрузки");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДокументыОтгрузки", "Объект", Отказ);
	КонецЕсли;
	
	ИнформацияОДублях = ИнформацияОДубляхЗаявленийОВвозеТоваровПолученных(, Контрагент);
	
	Если ИнформацияОДублях.ЕстьДубли Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Список", "Корректность", "ДокументыОтгрузки",,"ДокументыОтгрузки", ИнформацияОДублях.СообщениеПользователю);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДокументыОтгрузки", "Объект", Отказ);
	КонецЕсли;
	
	МассивДокументов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки.Проведен КАК Проведен,
	|	ИСТИНА КАК ПроверятьТипДокументаОснования,
	|	ТИПЗНАЧЕНИЯ(ВЫРАЗИТЬ(ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки КАК Документ.СчетФактураВыданный).ДокументОснование) = ТИП(Документ.ОтчетКомиссионераОПродажах) КАК ДопустимыйТипДокументаОснования,
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.НомерСтроки КАК НомерСтроки,
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки КАК ДокументОтгрузки
	|ИЗ
	|	Документ.ЗаявлениеОВвозеТоваровПолученное.ДокументыОтгрузки КАК ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|ГДЕ
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки ССЫЛКА Документ.СчетФактураВыданный
	|	И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки.Проведен,
	|	ЛОЖЬ,
	|	ИСТИНА,
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.НомерСтроки,
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки
	|ИЗ
	|	Документ.ЗаявлениеОВвозеТоваровПолученное.ДокументыОтгрузки КАК ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|ГДЕ
	|	НЕ ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки ССЫЛКА Документ.СчетФактураВыданный
	|	И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка = &Ссылка";
	
	Запрос.Параметры.Вставить("Ссылка",Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ Выборка.Проведен Тогда
			
			Текст = СтрШаблон(НСтр("ru = 'В строке %1 должен быть указан проведенный документ.'"), Выборка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				,
				"ДокументыОтгрузки["+Выборка.НомерСтроки+"].ДокументОтгрузки",
				,
				Отказ);
				
		КонецЕсли;
		
		Если Выборка.ПроверятьТипДокументаОснования
			И НЕ Выборка.ДопустимыйТипДокументаОснования Тогда
			
			Текст = СтрШаблон(НСтр("ru = 'В строке %1 может быть указан только счет-фактура выданный на отчет комиссионера.'"),
				Выборка.НомерСтроки);
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				,
				"ДокументыОтгрузки["+Выборка.НомерСтроки+"].ДокументОтгрузки",
				,
				Отказ);
				
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ДокументОтгрузки)
			И МассивДокументов.Найти(Выборка.ДокументОтгрузки) = Неопределено Тогда
			
			МассивДокументов.Добавить(Выборка.ДокументОтгрузки);
			
			Отбор = Новый Структура ("ДокументОтгрузки", Выборка.ДокументОтгрузки);
			
			РезультатПоиска = ДокументыОтгрузки.НайтиСтроки(Отбор);
			КоличествоНайденныхСтрок = РезультатПоиска.Количество();
			
			Если КоличествоНайденныхСтрок > 1 Тогда
				
				Индекс = 1;
				
				Пока Индекс < КоличествоНайденныхСтрок Цикл
					
					Текст = СтрШаблон(НСтр("ru = 'В строке %1 повторно указан документ %2.'"),
						РезультатПоиска[Индекс].НомерСтроки,
						РезультатПоиска[Индекс].ДокументОтгрузки);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						Текст,
						,
						"ДокументыОтгрузки["+Формат(РезультатПоиска[Индекс].НомерСтроки - 1, "ЧГ=")+"].ДокументОтгрузки",
						,
						Отказ);
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЭтоУчастникЕАЭС = Истина;
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		
		Если ДанныеЗаполнения.ПометкаУдаления Тогда
			ТекстСообщения = НСтр("ru = 'Заявление о ввозе товаров (полученное) нельзя выписать на основании документа, помеченного на удаление.'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Товары.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Невозможно создать заявление о ввозе товаров (полученное).
						|В документе основании отсутствует информация о товарах.'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		Если ТипДанныхЗаполнения = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			И ЗначениеЗаполнено(ДанныеЗаполнения.СтранаРеализации) Тогда
			ЭтоУчастникЕАЭС = УчетНДСВызовСервера.ЭтоУчастникЕАЭС(ДанныеЗаполнения.СтранаРеализации);
		Иначе
			ЭтоУчастникЕАЭС = УчетНДСВызовСервера.ЭтоУчастникЕАЭС(ДанныеЗаполнения.Контрагент);
		КонецЕсли;
	
		Если НЕ ЭтоУчастникЕАЭС Тогда
			ТекстСообщения = НСтр("ru = 'Невозможно создать заявление о ввозе товаров (полученное).
							|В документе основании нет реализации товаров в страну ЕАЭС.'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения, ДанныеЗаполнения.Контрагент);
		
	КонецЕсли;
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("ЕстьТовары") Тогда
		
			Если НЕ ДанныеЗаполнения.ЕстьТовары Тогда
				ТекстСообщения = НСтр("ru = 'Невозможно создать заявление о ввозе товаров (полученное).
							|В документе основании отсутствует информация о товарах.'");
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Контрагент") Тогда

			ЭтоУчастникЕАЭС = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(ДанныеЗаполнения.Контрагент);
		
		КонецЕсли;
		
		Если НЕ ЭтоУчастникЕАЭС Тогда
			ТекстСообщения = НСтр("ru = 'Невозможно создать заявление о ввозе товаров (полученное).
							|В документе основании нет реализации товаров в страну ЕАЭС.'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Ссылка")
			И ДанныеЗаполнения.Свойство("Контрагент") Тогда
			
			ПометкаУдаленияДокументаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Ссылка,
				"ПометкаУдаления");
			
			Если ПометкаУдаленияДокументаОснования = Истина Тогда
				ТекстСообщения = НСтр("ru = 'Заявление о ввозе товаров (полученное) нельзя выписать на основании документа, помеченного на удаление.'");
				ВызватьИсключение ТекстСообщения;
			Иначе
				ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения.Ссылка, ДанныеЗаполнения.Контрагент);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(Ссылка, Контрагент)
	
	ИнформацияОДублях = ИнформацияОДубляхЗаявленийОВвозеТоваровПолученных(Ссылка, Контрагент);
	
	Если ИнформацияОДублях.ЕстьДубли Тогда
		ВызватьИсключение ИнформацияОДублях.СообщениеПользователю;
	КонецЕсли;
	
	ИсключаемыеПоля = "Номер, Дата, Проведен, ПометкаУдаления, Комментарий";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Ссылка, , ИсключаемыеПоля);
	
	ДокументОтгрузки = ДокументыОтгрузки.Добавить();
	ДокументОтгрузки.ДокументОтгрузки = Ссылка;
	
	ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
	
КонецПроцедуры

Функция ИнформацияОДубляхЗаявленийОВвозеТоваровПолученных(ДокументОтгрузки = Неопределено, Контрагент = Неопределено)
	
	Результат = Новый Структура("ЕстьДубли, СообщениеПользователю");
	
	ЕстьДубли = Ложь;
	СообщенияПользователю = Новый Массив;
	
	СуществующиеЗаявления = НайтиЗаявленияОВвозеТоваровПолученныхПоДокументуОтгрузки(ДокументОтгрузки, Контрагент);
	Для Каждого СтрокаТЗ Из СуществующиеЗаявления Цикл
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='На основании документа %1 по контрагенту %2 уже введено Заявление о ввозе (полученное) %3 от %4'"),
			СтрокаТЗ.ДокументОтгрузки,
			Контрагент,
			СтрокаТЗ.НомерОтметкиРегистрацииЗаявления,
			СтрокаТЗ.ДатаНомерОтметкиРегистрацииЗаявления);
		СообщенияПользователю.Добавить(ТекстСообщения);
		ЕстьДубли = Истина;
	КонецЦикла;
	
	СообщениеПользователю = СтрСоединить(СообщенияПользователю, ". ");
	
	Результат.ЕстьДубли             = ЕстьДубли;
	Результат.СообщениеПользователю = СообщениеПользователю;
	
	Возврат Результат;
	
КонецФункции

Функция НайтиЗаявленияОВвозеТоваровПолученныхПоДокументуОтгрузки(ДокументОтгрузки, Контрагент)
	
	Если ЗначениеЗаполнено(ДокументОтгрузки) Тогда
		Отгрузки = Новый Массив;
		Отгрузки.Добавить(ДокументОтгрузки);
		
	Иначе
		Отгрузки = ДокументыОтгрузки.ВыгрузитьКолонку("ДокументОтгрузки");
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отгрузки", Отгрузки);
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка.Дата КАК ДатаНомерОтметкиРегистрацииЗаявления,
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка.НомерВходящегоДокумента КАК НомерОтметкиРегистрацииЗаявления,
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки КАК ДокументОтгрузки
	|ИЗ
	|	Документ.ЗаявлениеОВвозеТоваровПолученное.ДокументыОтгрузки КАК ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|ГДЕ
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки В(&Отгрузки)
	|	И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка <> &ТекущийДокумент
	|	И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка.Проведен
	|	И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка.Контрагент = &Контрагент";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли