#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	
	Провести = Параметры.ЗначенияЗаполнения.Провести;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Провести Тогда
		ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	ПроверкаЗаполненияОтметкиОРегистрацииЗаявления(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыОповещения.Вставить("Представление", СтрШаблон(НСтр("ru='%1 от %2'"), Объект.НомерВходящегоДокумента, Формат(Объект.Дата,"ДЛФ=Д")));
	
	ОповеститьОВыборе(ПараметрыОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ПроверкаЗаполненияОтметкиОРегистрацииЗаявления(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.НомерВходящегоДокумента) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
			НСтр("ru = 'Номер отметки о регистрации заявления о ввозе товаров покупателя из ЕАЭС в налоговом органе своей страны'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Номер", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение",
			НСтр("ru = 'Дата отметки о регистрации заявления о ввозе товаров покупателя из ЕАЭС в налоговом органе своей страны'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Дата", , Отказ);
	КонецЕсли;

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отказ = Ложь;
	ПроверкаЗаполненияОтметкиОРегистрацииЗаявления(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Провести Тогда
		ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		Записать(ПараметрыЗаписи);
	Иначе
		Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеРеквизиты(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Организация", Объект.Организация);
	СтруктураЗаполнения.Вставить("Контрагент", Объект.Контрагент);
	СтруктураЗаполнения.Вставить("НомерВходящегоДокумента", Объект.НомерВходящегоДокумента);
	СтруктураЗаполнения.Вставить("Дата", Объект.Дата);
	СтруктураЗаполнения.Вставить("Ссылка", Объект.ДокументыОтгрузки[0].ДокументОтгрузки);
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", СтруктураЗаполнения);
	ПараметрыФормы.Вставить("ДанныеМодифицированы", Модифицированность);

	ОткрытьФорму("Документ.ЗаявлениеОВвозеТоваровПолученное.ФормаОбъекта", ПараметрыФормы, ВладелецФормы);
	
	Модифицированность = Ложь;
	
	Закрыть();

КонецПроцедуры

#КонецОбласти
