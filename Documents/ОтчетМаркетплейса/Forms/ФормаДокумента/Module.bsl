
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет Тогда
		ЭтотОбъект.Заголовок = НСтр("ru = 'Отчет о продажах Яндекс.Маркет '") + Формат(Объект.Дата, "ДФ='ММММ ггг'");
	ИначеЕсли Объект.Маркетплейс = Перечисления.ВидыМаркетплейсов.Мегамаркет Тогда
		ЭтотОбъект.Заголовок = НСтр("ru = 'Отчет о продажах Мегамаркет '") + Формат(Объект.Дата, "ДФ='ММММ ггг'");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Магазин) Тогда
		Элементы.Магазин.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Отчет о продажах маркетплейса создается автоматически, при загрузке. Ручное заполнение не предусмотрено.'"));
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект, "ГруппаКомментарийОтветственный", "Ответственный");
	
	ОбновитьПредупрежденияДляОтчетовМаркетплейсов();
	ИнтеграцияЯндексМаркет.ПодготовитьНесозданныеДокументыОтчетаМаркетплейса(ЭтотОбъект);
	ОбменСМаркетплейс.ПодготовитьСопоставлениеНоменклатурыПоДокументу(ЭтотОбъект);
	
	ЗаполнитьДеревоДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЕстьНесозданныеДокументы Тогда
		ПодключитьОбработчикОжидания("ПоказатьНеСозданныеДокументы", 0.1, Истина);
	Иначе
		ОбменСМаркетплейсКлиент.ПодключитьОбработчикСопоставленияНоменклатуры(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_РеализацияТоваровУслуг"
		ИЛИ ИмяСобытия = "Запись_ВозвратТоваровОтПокупателя"
		ИЛИ ИмяСобытия = "Запись_РеализацияОтгруженныхТоваров"
		ИЛИ ИмяСобытия = "Запись_ОтчетОРозничныхПродажах" Тогда
		
		ОбновитьДеревоДокументов();
		Оповестить("ИзмененыДокументыМаркетплейсов");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ОбновитьПредупрежденияДляОтчетовМаркетплейсов();
		Оповестить("ИзмененыДокументыМаркетплейсов");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоДокументовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ТекущиеДанные = Элементы.ДеревоДокументов.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Документ);
		Если ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.РеализацияОтгруженныхТоваров") Тогда
			ОткрытьФорму("Документ.РеализацияОтгруженныхТоваров.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.ФормаОбъекта", ПараметрыФормы, , , , , );
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область РедактированиеКомментарияВЗакрытомПериоде

&НаКлиенте
Процедура Подключаемый_РедактироватьКомментарий(Команда) 
	
	КомментарииДокументовЗакрытогоПериодаКлиент.ИзменитьКомментарий(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПровестиДокументы(Команда)
	
	Если ВыполняетсяПроведениеДокументов Тогда
		Возврат;
	КонецЕсли;
	
	ВыполняетсяПроведениеДокументов = Истина;
	ПрогрессЗагрузки = 0;
	
	ФоновоеЗадание = ПровестиДокументыФоновоеЗадание();
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.Заголовок = НСтр("ru = 'Проведение документов'");
	НастройкиОжидания.ВыводитьОкноОжидания = Истина;
	
	Обработчик = Новый ОписаниеОповещения("ПослеПроведенияСозданныхДокументов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНоменклатуру(Команда)
	
	ОбменСМаркетплейсКлиент.ПоказатьСопоставлениеНоменклатуры(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоДокументов()
	
	Если Объект.Маркетплейс = Перечисления.ВидыМаркетплейсов.Мегамаркет Тогда

		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОтчетОРозничныхПродажах.Ссылка КАК Документ,
		|	ОтчетОРозничныхПродажах.СуммаДокумента КАК Сумма,
		|	ВЫБОР
		|		КОГДА ОтчетОРозничныхПродажах.Проведен
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Картинка,
		|	""Проданные товары"" КАК РазделОтчета,
		|	"""" КАК Отступ,
		|	1 КАК Порядок,
		|	НЕОПРЕДЕЛЕНО КАК Контрагент,
		|	ЛОЖЬ КАК ЕстьПредупреждения
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
		|ГДЕ
		|	ОтчетОРозничныхПродажах.ОтчетМаркетплейса = &ОтчетМаркетплейса
		|	И НЕ ОтчетОРозничныхПродажах.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	РазделОтчета";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РеализацияТоваровУслуг.Ссылка КАК Документ,
		|	РеализацияТоваровУслуг.СуммаДокумента КАК Сумма,
		|	ВЫБОР
		|		КОГДА РеализацияТоваровУслуг.Проведен
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Картинка,
		|	""Товары, переданные в доставку"" КАК РазделОтчета,
		|	"""" КАК Отступ,
		|	1 КАК Порядок,
		|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
		|	НЕ Предупреждения.Документ ЕСТЬ NULL КАК ЕстьПредупреждения
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
		|		ПО РеализацияТоваровУслуг.Ссылка = Предупреждения.Документ
		|ГДЕ
		|	РеализацияТоваровУслуг.ОтчетМаркетплейса = &ОтчетМаркетплейса
		|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияОтгруженныхТоваров.Ссылка,
		|	РеализацияОтгруженныхТоваров.СуммаДокумента,
		|	ВЫБОР
		|		КОГДА РеализацияОтгруженныхТоваров.Проведен
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	""Доставленные товары"",
		|	"""",
		|	2,
		|	РеализацияОтгруженныхТоваров.Контрагент,
		|	НЕ Предупреждения.Документ ЕСТЬ NULL
		|ИЗ
		|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
		|		ПО РеализацияОтгруженныхТоваров.Ссылка = Предупреждения.Документ
		|ГДЕ
		|	РеализацияОтгруженныхТоваров.ОтчетМаркетплейса = &ОтчетМаркетплейса
		|	И НЕ РеализацияОтгруженныхТоваров.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателя.Ссылка,
		|	ВозвратТоваровОтПокупателя.СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ВозвратТоваровОтПокупателя.Проведен
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	""Невыкупленные товары"",
		|	"""",
		|	3,
		|	ВозвратТоваровОтПокупателя.Контрагент,
		|	НЕ Предупреждения.Документ ЕСТЬ NULL
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
		|		ПО ВозвратТоваровОтПокупателя.Ссылка = Предупреждения.Документ
		|ГДЕ
		|	ВозвратТоваровОтПокупателя.ОтчетМаркетплейса = &ОтчетМаркетплейса
		|	И ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ОтгруженныеТовары)
		|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровОтПокупателя.Ссылка,
		|	ВозвратТоваровОтПокупателя.СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ВозвратТоваровОтПокупателя.Проведен
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	""Возвращённые товары"",
		|	"""",
		|	4,
		|	ВозвратТоваровОтПокупателя.Контрагент,
		|	НЕ Предупреждения.Документ ЕСТЬ NULL
		|ИЗ
		|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов КАК Предупреждения
		|		ПО ВозвратТоваровОтПокупателя.Ссылка = Предупреждения.Документ
		|ГДЕ
		|	ВозвратТоваровОтПокупателя.ОтчетМаркетплейса = &ОтчетМаркетплейса
		|	И ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.Товары)
		|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	РазделОтчета";

	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ОтчетМаркетплейса", Объект.Ссылка);
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ЗначениеВРеквизитФормы(Дерево, "ДеревоДокументов");
	
	УстановитьУсловноеОформление();
	УправлениеВидимостью(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//Оформление группы
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ДеревоДокументов.РазделОтчета"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовКонтрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовСумма");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовОтступ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ИтогиФонГруппы);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,Истина,Ложь));
	
	//Оформление элемента
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.Заполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовКонтрагент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Маркетплейс", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Строки с предупреждениями при загрузке
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовКонтрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовСумма");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.ЕстьПредупреждения", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ДокументыМаркетплейсовСПредупреждениямиПриЗагрузке);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДеревоДокументов()
	
	ЗаполнитьДеревоДокументов();
	Для Каждого СтрокаДерева Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		Элементы.ДеревоДокументов.Развернуть(СтрокаДерева.ПолучитьИдентификатор() ,Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПровестиДокументыФоновоеЗадание()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проведение документов'");
	
	ДокументыДляПроведения = Новый Массив;
	Для Каждого СтрокаДокумента Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		Если ЗначениеЗаполнено(СтрокаДокумента.Документ) Тогда
			ДокументыДляПроведения.Добавить(СтрокаДокумента.Документ);
		Иначе
			Для Каждого ПодстрокаДокумента Из СтрокаДокумента.ПолучитьЭлементы() Цикл
				Если ЗначениеЗаполнено(ПодстрокаДокумента.Документ) Тогда
					ДокументыДляПроведения.Добавить(ПодстрокаДокумента.Документ);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ЭлектронноеВзаимодействиеБП.ПровестиСозданныеДокументы",
		ДокументыДляПроведения);
	
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаКлиенте
Процедура ПослеПроведенияСозданныхДокументов(Задание, Контекст) Экспорт
	
	ВыполняетсяПроведениеДокументов = Ложь;
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДеревоДокументов();
	Оповестить("ИзмененыДокументыМаркетплейсов");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УправлениеВидимостью(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ДеревоДокументовПровестиДокументы.Видимость = Не Форма.ТребуетсяСопоставлениеНоменклатуры;
	Элементы.ФормаСопоставитьНоменклатуру.Видимость = Форма.ТребуетсяСопоставлениеНоменклатуры;
	
КонецПроцедуры

#Область ОтложенноеСопоставлениеНоменклатуры

&НаКлиенте
Процедура Подключаемый_ПоказатьСопоставлениеНоменклатуры()
	
	ОбменСМаркетплейсКлиент.ПоказатьСопоставлениеНоменклатуры(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьПредупрежденияДляОтчетовМаркетплейсов()
	
	РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов.ОбновитьДляОтчетовМаркетплейсов();
	
КонецПроцедуры

#КонецОбласти

#Область ОтложенноеСвязываниеДокументов

&НаКлиенте
Процедура ПоказатьНеСозданныеДокументы()
	
	ПараметрыФормы = ОбменСМаркетплейсКлиент.НовыйПараметрыДереваСвязанныхДокументов();
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ДанныеНесозданныхДокументов.ПараметрыДокумента);
	ПараметрыФормы.ДеревоДокументов = ДанныеНесозданныхДокументов.АдресХранилищаДереваСвязанныхДокументов;
	ПараметрыФормы.Период = ДанныеНесозданныхДокументов.ПараметрыДокумента.ПериодОтчета;
	ПараметрыФормы.ОтложенноеСвязываниеДокументов = Истина;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПоказатьНеСозданныеДокументыЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ЗагрузкаОтчетаЯндексМаркет.Форма.ФормаСписокДокументов", ПараметрыФормы, ЭтотОбъект, , , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНеСозданныеДокументыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = СоздатьРанееНесвязанныеДокументы(ДанныеНесозданныхДокументов, РезультатЗакрытия);
	Если ДлительнаяОперация = Неопределено Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		ОбновитьДеревоДокументов();
		ОбменСМаркетплейсКлиент.ПодключитьОбработчикСопоставленияНоменклатуры(ЭтотОбъект);
		Оповестить("ИзмененыДокументыМаркетплейсов");
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"СоздатьРанееНесвязанныеДокументыЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьРанееНесвязанныеДокументы(ДанныеНесозданныхДокументов, АдресХранилищаДереваДокументов)
	
	Предупреждения = РегистрыСведений.ПредупрежденияПриЗагрузкеОтчетовМаркетплейсов;
	ПараметрыДокумента = ДанныеНесозданныхДокументов.ПараметрыДокумента;
	
	Если Не ЗначениеЗаполнено(АдресХранилищаДереваДокументов) Тогда // отказ пользователя
		Предупреждения.УдалитьПредупреждениеДляДокумента(ПараметрыДокумента.ОтчетМаркетплейса);
		Предупреждения.ОбновитьДляОтчетовМаркетплейсов();
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеМаркетплейса = ПолучитьИзВременногоХранилища(ДанныеНесозданныхДокументов.АдресХранилищаДанныеФайла);
	ДеревоДокументов = ПолучитьИзВременногоХранилища(АдресХранилищаДереваДокументов);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание связанных документов'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ИнтеграцияЯндексМаркет.СоздатьРанееНесвязанныеДокументы",
		ПараметрыДокумента,
		ДанныеМаркетплейса,
		ДеревоДокументов);
		
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура СоздатьРанееНесвязанныеДокументыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ОповеститьОбИзменении(Объект.Ссылка);
	ОбновитьДеревоДокументов();
	ОбменСМаркетплейсКлиент.ПодключитьОбработчикСопоставленияНоменклатуры(ЭтотОбъект);
	Оповестить("ИзмененыДокументыМаркетплейсов");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
