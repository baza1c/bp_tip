#Область ОписаниеПеременных

// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
&НаКлиенте
Перем ОбменСБанкамиДепозиты_ПараметрыПредложений;
// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ИспользуетсяПодсистемаЗарплата = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии")
		ИЛИ ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		
	ИспользоватьВалютныйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет");
	ИспользуетсяСогласованиеПлатежей = ПолучитьФункциональнуюОпцию("ИспользоватьСогласованиеПлатежей");
	УстановитьИспользованиеСогласованияПлатежей(ЭтотОбъект);
	
	ТипИнформации = "Контрагент";
	НастроитьВнешнийВидОтбораКонтрагент();
	
	ВходящийОтборПоОрганизации = Ложь;
	СтруктураОтбора = Неопределено;
	
	Если Параметры.Свойство("Отбор", СтруктураОтбора) И ЗначениеЗаполнено(СтруктураОтбора) Тогда
		
		ВходящийОтборПоОрганизации = СтруктураОтбора.Свойство("Организация", ОтборОрганизация);
		СтруктураОтбора.Свойство("СчетОрганизации",      ОтборСчетОрганизации);
		СтруктураОтбора.Свойство("Контрагент",           ОтборКонтрагент);
		СтруктураОтбора.Свойство("ДатаОтбора",           ОтборДатаОтбора);
		СтруктураОтбора.Свойство("БанкСчетаОрганизации", ОтборБанкСчетаОрганизации);
		
		Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда
			Если ТипЗнч(ОтборКонтрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				Если Элементы.ТипИнформации.СписокВыбора.НайтиПоЗначению("Сотрудник") <> Неопределено Тогда
					ТипИнформации = "Сотрудник";
				Иначе
					ТипИнформации = "Контрагент";
					ОтборКонтрагент = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ОтборОрганизацияИспользование          = ЗначениеЗаполнено(ОтборОрганизация);
		ОтборСчетОрганизацииИспользование      = ЗначениеЗаполнено(ОтборСчетОрганизации);
		ОтборКонтрагентИспользование           = ЗначениеЗаполнено(ОтборКонтрагент);
		ОтборДатаОтбораИспользование           = ЗначениеЗаполнено(ОтборДатаОтбора);
		ОтборБанкСчетаОрганизацииИспользование = ЗначениеЗаполнено(ОтборБанкСчетаОрганизации);
		
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
	Если НЕ ВходящийОтборПоОрганизации И ОтборОрганизация <> ОсновнаяОрганизация Тогда
		ОтборОрганизация                  = ОсновнаяОрганизация;
		УстановитьСчетОрганизации(ОтборСчетОрганизации, ОтборОрганизация);
		ОтборОрганизацияИспользование     = ЗначениеЗаполнено(ОтборОрганизация);
		ОтборСчетОрганизацииИспользование = ЗначениеЗаполнено(ОтборСчетОрганизации);
	КонецЕсли;
	
	УстановитьВосстановленныеОтборы();
	УстановитьОграничениеТипаОтбораИнформации(ЭтотОбъект);
	
	ИнтеграцияСБанкамиПодключена = ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВИнформационнойБазеВключена();
	Если ИнтеграцияСБанкамиПодключена Тогда
		ПроверкаСостоянияИнтеграцииВФонеПриОткрытии = СостояниеИнтеграцииСБанкомВФоне(УникальныйИдентификатор);
	КонецЕсли;
	
	ЕстьРасчетныеСчетаБезИнтеграции = НЕ ИнтеграцияСБанкамиПодключена
		ИЛИ Справочники.НастройкиИнтеграцииСБанками.ЕстьРасчетныеСчетаБезИнтеграции();
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Документы.ПлатежноеПоручение);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	Элементы.КнопкаОтправитьВБанк.Видимость                    = МожноРедактировать И ЕстьРасчетныеСчетаБезИнтеграции;
	Элементы.ФормаОбновитьСписокИПолучитьСтатусы.Видимость     = МожноРедактировать И ЕстьРасчетныеСчетаБезИнтеграции;
	Элементы.ГруппаЗагрузить.Видимость                         = МожноРедактировать;
	Элементы.УплатаНалоговИВзносов.Видимость                   = МожноРедактировать;
	Элементы.ОплатаПоставщикам.Видимость                       = МожноРедактировать;
	
	Элементы.СчетВРежимеИнтеграции.Видимость = ИнтеграцияСБанкамиПодключена;
	
	ОбщегоНазначенияБП.УстановитьВидимостьКолонокДополнительнойИнформации(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	Если ПолучитьФункциональнуюОпцию("ВестиУчетИндивидуальногоПредпринимателя") Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_УчетСтраховыхВзносовИП");
	КонецЕсли;
	ИспользуетсяЭП = ЭлектронноеВзаимодействиеБП.НастроенОбменДиректБанк();
	Если ИспользуетсяЭП Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяЭП");
	КонецЕсли;
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.ПлатежноеПоручение",
		"ФормаСписка",
		НСтр("ru = 'Новости: Платежные поручения'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ТарификацияБП.РазместитьИнформациюОбОграниченииПоКоличествуОбъектов(ЭтотОбъект);
	
	АдресХранилищаНастройкиДинСпискаДляРеестра = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	// ЭлектронноеВзаимодействие.ОбменСБанками
	ПараметрыСозданияФормыСписка = ОбменСБанкамиКлиентСервер.ПараметрыСозданияФормыСписка();
	ПараметрыСозданияФормыСписка.ГруппаКоманд.Родитель            = Элементы.ГруппаВажныеКоманды;
	ПараметрыСозданияФормыСписка.ДинамическийСписок.Изменить      = Ложь;
	ПараметрыСозданияФормыСписка.СписокДокументов.ДобавитьКолонки = Ложь;
	ПараметрыСозданияФормыСписка.ПерепискаСБанками.ДобавитьСсылку = Ложь;
	ПараметрыСозданияФормыСписка.Реклама.Добавить                 = Ложь;
	ОбменСБанками.ПриСозданииФормыСпискаНаСервере(ЭтотОбъект, ПараметрыСозданияФормыСписка);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ИзменитьПараметрыВыбораБанковскогоСчета();
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ПриСозданииНаСервере(ЭтотОбъект,
		"ГруппаГлобальныеКоманды", , Истина, "СписокКонтекстноеМенюИзменитьВыделенные");
	
	Если ОбменСБанкамиДепозиты.ДоступенОбменДепозитами() И Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
		ДополнительныеПараметры = ОбменСБанкамиДепозиты.ПараметрыДепозитногоКалькулятора();
		ДополнительныеПараметры.ИмяГруппыРазмещения = "ГруппаДепозитыРасчеты";
		ДополнительныеПараметры.ПутьКРеквизитуОрганизация = "ОтборОрганизация";
		ДополнительныеПараметры.ИспользоватьКомандуДепозиты = Ложь;
		ДополнительныеПараметры.ВыводитьЗаголовокПанелиРазмещения = Ложь;
		ДополнительныеПараметры.ИмяЭлементаРеквизитаОрганизация = "ОтборОрганизация";
		ОбменСБанкамиДепозиты.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
		// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	Иначе
		Элементы.ПоказатьПанельИнформации.Видимость = Ложь;
	КонецЕсли;
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ТипЗнч(Настройки["ОтборКонтрагент"]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Если Элементы.ТипИнформации.СписокВыбора.НайтиПоЗначению("Сотрудник") <> Неопределено Тогда
			ТипИнформации = "Сотрудник";
		Иначе
			ТипИнформации                = "Контрагент";
			ОтборКонтрагент              = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
			ОтборКонтрагентИспользование = Ложь;
		КонецЕсли;
	Иначе
		ТипИнформации = "Контрагент";
	КонецЕсли;
	
	УстановитьОграничениеТипаОтбораИнформации(ЭтотОбъект);
	
	СтруктураОтбора = Неопределено;
	Если Параметры.Свойство("Отбор", СтруктураОтбора) И ЗначениеЗаполнено(СтруктураОтбора) Тогда
		СтруктураОтбора.Свойство("Организация",           ОтборОрганизация);
		СтруктураОтбора.Свойство("СчетОрганизации",       ОтборСчетОрганизации);
		СтруктураОтбора.Свойство("Контрагент",            ОтборКонтрагент);
		СтруктураОтбора.Свойство("ДатаОтбора",            ОтборДатаОтбора);
		СтруктураОтбора.Свойство("БанкСчетаОрганизации",  ОтборБанкСчетаОрганизации);

		ОтборОрганизацияИспользование          = ЗначениеЗаполнено(ОтборОрганизация);
		ОтборСчетОрганизацииИспользование      = ЗначениеЗаполнено(ОтборСчетОрганизации);
		ОтборКонтрагентИспользование           = ЗначениеЗаполнено(ОтборКонтрагент);
		ОтборДатаОтбораИспользование           = ЗначениеЗаполнено(ОтборДатаОтбора);
		ОтборБанкСчетаОрганизацииИспользование = ЗначениеЗаполнено(ОтборБанкСчетаОрганизации);
		
		Параметры.Отбор = Неопределено;
	Иначе
		Если ОтборОрганизация <> ОсновнаяОрганизация Тогда
			ОтборОрганизация = ОсновнаяОрганизация;
			
			// Предварительно сбросим сохраненный банковский счет, т.к. он не принадлежит текущей организации
			// и может быть проблема при RLS.
			ОтборСчетОрганизации = Справочники.БанковскиеСчета.ПустаяСсылка();
						
			УстановитьСчетОрганизации(ОтборСчетОрганизации, ОтборОрганизация);
			ОтборОрганизацияИспользование     = ЗначениеЗаполнено(ОтборОрганизация);
			ОтборСчетОрганизацииИспользование = ЗначениеЗаполнено(ОтборСчетОрганизации);
			
		ИначеЕсли НЕ ОтборОрганизацияИспользование Тогда
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		КонецЕсли;
		
		ОтборБанкСчетаОрганизации = Справочники.Банки.ПустаяСсылка();
		ОтборБанкСчетаОрганизацииИспользование = ЗначениеЗаполнено(ОтборБанкСчетаОрганизации);
	КонецЕсли;
	
	УстановитьВосстановленныеОтборы();
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		МобильныйКлиентБПФормаОбъектаКлиентСервер.ОбновитьНадписьФиксированногоОтбора(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РеквизитыОрганизацииЗаполнены = ОбщегоНазначенияБПКлиент.ПроверитьНаличиеОрганизаций();
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор, "Организация");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор, "СчетОрганизации");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор, "ДатаОтбора");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор, "Контрагент");
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор, "БанкСчетаОрганизации");

	Если РеквизитыОрганизацииЗаполнены Тогда
		
		// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
		ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
		// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
		
	КонецЕсли;
	
	Если ПроверкаСостоянияИнтеграцииВФонеПриОткрытии <> Неопределено Тогда
		
		ОжидатьЗавершенияФоновогоЗаданияПолученияСостоянияИнтеграцииСБанком(ПроверкаСостоянияИнтеграцииВФонеПриОткрытии);
		
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	ОбменСБанкамиДепозитыКлиент.ПриОткрытии(ЭтотОбъект);
	НастроитьОтображениеКомандыПоказатьПанельИнформации(Элементы.ГруппаДепозитыРасчеты.Видимость);
	//Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОсновнаяОрганизация = Параметр;
		Если ОсновнаяОрганизация <> ОтборОрганизация Тогда
			ОтборОрганизация                  = ОсновнаяОрганизация;
			УстановитьСчетОрганизации(ОтборСчетОрганизации, ОтборОрганизация);
			ОтборОрганизацияИспользование     = ЗначениеЗаполнено(ОтборОрганизация);
			ОтборСчетОрганизацииИспользование = ЗначениеЗаполнено(ОтборСчетОрганизации);
			
			УстановитьВосстановленныеОтборы(Истина);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОбновитьСостояниеОбменСБанками" Тогда
		Элементы.Список.Обновить();
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" И Параметр.Владелец = ОтборОрганизация Тогда
		УстановитьВидимостьБанковскогоСчета();
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьОбменСБанками" Тогда
		ИспользуетсяДиректБанк = ИспользуетсяДиректБанк();
	ИначеЕсли ИмяСобытия = "ИзменилосьСостояниеДокументаИнтеграцииСБанком" Тогда
		ОтключитьОбработчикОжидания("Подключаемый_ЗапуститьПроверкуСостояниеИнтеграцииВФоне");
		Результат = СостояниеИнтеграцииСБанкомВФоне(УникальныйИдентификатор);
		ОжидатьЗавершенияФоновогоЗаданияПолученияСостоянияИнтеграцииСБанком(Результат);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	// ЭлектронноеВзаимодействие.ОбменСБанками
	ОбменСБанкамиКлиент.ОбработатьОповещениеФормыСписка(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
	
	КомментарииДокументовЗакрытогоПериодаКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	// ЭлектронноеВзаимодействие.ОбменСБанками
	ОбменСБанкамиДепозитыКлиент.ОбработатьВыборУсловия(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора,
		ОбменСБанкамиДепозиты_ПараметрыПредложений);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// ЭлектронноеВзаимодействие.ОбменСБанками
	Если Не ЗавершениеРаботы Тогда
		ОбменСБанкамиДепозитыКлиент.ПередЗакрытием(ЭтотОбъект);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	УстановитьСчетОрганизации(ОтборСчетОрганизации, ОтборОрганизация);
	ОтборОрганизацияИспользование     = ЗначениеЗаполнено(ОтборОрганизация);
	ОтборСчетОрганизацииИспользование = ЗначениеЗаполнено(ОтборСчетОрганизации);
	
	УстановитьВосстановленныеОтборы(Истина);
	
	// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	
	ОбменСБанкамиДепозитыКлиент.ОбновитьПредложенияПриИзмененииОрганизации(ЭтотОбъект,
		ОбменСБанкамиДепозиты_ПараметрыПредложений);
	
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоМобильныйКлиент() Тогда
		МобильныйКлиентБПФормаОбъектаКлиентСервер.ОбновитьНадписьФиксированногоОтбора(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетОрганизацииПриИзменении(Элемент)
	
	ОтборСчетОрганизацииИспользование = ЗначениеЗаполнено(ОтборСчетОрганизации);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СчетОрганизации");
	УстановитьИспользованиеСогласованияПлатежей(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаОтбораПриИзменении(Элемент)
	
	ОтборДатаОтбораИспользование = ЗначениеЗаполнено(ОтборДатаОтбора);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ДатаОтбора");
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИнформацииПриИзменении(Элемент)
	
	УстановитьОграничениеТипаОтбораИнформации(ЭтотОбъект);
	Если ТипИнформации = "Сотрудник" Тогда
		ОтборКонтрагент = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	Иначе
		ОтборКонтрагент = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
	КонецЕсли;
	ОтборКонтрагентИспользование = Ложь;
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
	
КонецПроцедуры

&НаКлиенте
Процедура ТипИнформацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	
	ОтборКонтрагентИспользование = ЗначениеЗаполнено(ОтборКонтрагент);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
	
КонецПроцедуры

&НаКлиенте
Процедура БанкСчетаОрганизацииПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "БанкСчетаОрганизации");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетОрганизацииИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СчетОрганизации");
	УстановитьИспользованиеСогласованияПлатежей(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБанкСчетаОрганизацииИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "БанкСчетаОрганизации");

КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	Если ОтборОрганизацияИспользование Тогда
		Если НЕ ОтборСчетОрганизацииИспользование И ЗначениеЗаполнено(ОтборСчетОрганизации) Тогда
			ОтборСчетОрганизацииИспользование = Истина;
			ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СчетОрганизации");
		КонецЕсли;
	ИначеЕсли ОтборСчетОрганизацииИспользование Тогда
		ОтборСчетОрганизацииИспользование = Ложь;
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СчетОрганизации");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ДатаОтбора");
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыПриИзменении(Элемент)
	
	ОбменСБанкамиДепозитыКлиент.ДепозитыПриИзменении(ЭтотОбъект, Элемент, ОбменСБанкамиДепозиты_ПараметрыПредложений);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	ОбменСБанкамиДепозитыКлиент.ОбработкаВыбора(ЭтотОбъект, ОбменСБанкамиДепозиты_ПараметрыПредложений,
		Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ОбменСБанкамиДепозитыКлиент.ЗаполнитьДанныеВыбора(ЭтотОбъект, ОбменСБанкамиДепозиты_ПараметрыПредложений, 
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	КлючеваяОперация = "СозданиеФормыПлатежноеПоручение";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	КлючеваяОперация = "ОткрытиеФормыПлатежноеПоручение";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ОтборыСписков.СброситьИспользованиеПользовательскихОтборовВНастройке(Настройки);
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомндФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВБанк(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РежимПоУмолчанию", "ГруппаВыгрузка");
	Если ОтборОрганизацияИспользование И ЗначениеЗаполнено(ОтборОрганизация) Тогда
		СтруктураПараметров.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	Если ОтборСчетОрганизацииИспользование И ЗначениеЗаполнено(ОтборСчетОрганизации) Тогда
		СтруктураПараметров.Вставить("БанковскийСчет", ОтборСчетОрганизации);
	КонецЕсли;
	
	ОткрытьФорму("Обработка.КлиентБанк.Форма", СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура УплатаНалоговИВзносов(Команда)
	
	СтруктураПараметров = Новый Структура;
	Если ОтборОрганизацияИспользование И ЗначениеЗаполнено(ОтборОрганизация) Тогда
		СтруктураПараметров.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	Если ОтборСчетОрганизацииИспользование И ЗначениеЗаполнено(ОтборСчетОрганизации) Тогда
		СтруктураПараметров.Вставить("СчетОрганизации", ОтборСчетОрганизации);
	КонецЕсли;
	
	СтруктураПараметров.Вставить("СпособОплаты", ПредопределенноеЗначение("Перечисление.СпособыУплатыНалогов.БанковскийПеревод"));
	ОткрытьФорму("Обработка.ФормированиеПлатежныхПорученийНаУплатуНалогов.Форма", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикам(Команда)
	
	СтруктураПараметров = Новый Структура;
	Если ОтборОрганизацияИспользование И ЗначениеЗаполнено(ОтборОрганизация) Тогда
		СтруктураПараметров.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	Если ОтборСчетОрганизацииИспользование И ЗначениеЗаполнено(ОтборСчетОрганизации) Тогда
		СтруктураПараметров.Вставить("СчетОрганизации", ОтборСчетОрганизации);
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ОплатаПоставщикам.Форма", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокИПолучитьСтатусы(Команда)
	
	// Получаем статусы документов, если настроен DirectBank
	Если ИспользуетсяДиректБанк Тогда
		ПроизвестиОбменСБанком();
	Иначе
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСчетНаОплатуXMLИзФайла(Команда)
	
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьПлатежноеПоручениеИзФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСчетНаОплатуXMLИзЭлектроннойПочты(Команда)
	
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьПлатежноеПоручениеИзЭлектроннойПочты();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстСостояниеЗагрузкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИнтеграцияСБанкамиФормыКлиент.ОбработкаНавигационнойСсылкиБаннера(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СогласованиеПлатежей(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	Если ОтборОрганизацияИспользование И ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ПараметрыОткрытия.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	Если ОтборСчетОрганизацииИспользование И ЗначениеЗаполнено(ОтборСчетОрганизации) Тогда
		ПараметрыОткрытия.Вставить("БанковскийСчет", ОтборСчетОрганизации);
	КонецЕсли;
	
	ОткрытьФорму("Обработка.СогласованиеПлатежей.Форма.Форма", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПанельИнформации(Команда)
	
	ИзменитьВидимостьПанелиИнформации();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельИнформации(Команда)
	
	ИзменитьВидимостьПанелиИнформации();
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыВыполнитьКоманду(Команда)
	
	ОбменСБанкамиДепозитыКлиент.ВыполнитьКоманду(ЭтотОбъект, ОбменСБанкамиДепозиты_ПараметрыПредложений, Команда);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

&НаКлиенте
Процедура Подключаемый_РедактироватьКомментарий(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		КомментарииДокументовЗакрытогоПериодаКлиент.ИзменитьКомментарийВСписке(ТекущаяСтрока.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Организация
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Организация");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборОрганизацияИспользование", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборОрганизация", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// СчетОрганизации
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетОрганизации");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборСчетОрганизацииИспользование", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтборСчетОрганизации", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Список
	
	СписокВыделения = Неопределено;
	Если Параметры.Свойство("СписокВыделения", СписокВыделения) И ЗначениеЗаполнено(СписокВыделения) Тогда
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Список.Ссылка", ВидСравненияКомпоновкиДанных.ВСписке, СписокВыделения.Ссылка);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(Новый Шрифт(),,, Истина));
		
	КонецЕсли;
	
	Если ОтображатьСтатусыДокументов Тогда
		РаботаСБанкамиБП.УстановитьЦветовоеВыделениеСостоянийПлатежек(УсловноеОформление);
	КонецЕсли;
	
	Если ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВИнформационнойБазеВключена() Тогда
		// Номер
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Номер");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Список.СчетВРежимеИнтеграции", ВидСравненияКомпоновкиДанных.Равно, Истина);
		СписокСостояний = Новый СписокЗначений;
		СписокСостояний.Добавить(Перечисления.СостоянияБанковскихДокументов.НаПодписи);
		СписокСостояний.Добавить(Перечисления.СостоянияБанковскихДокументов.Оплачено);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Список.Состояние", ВидСравненияКомпоновкиДанных.НеВСписке, СписокСостояний);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "");
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВосстановленныеОтборы(ТолькоОрганизацияИСчет = Ложь)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СчетОрганизации");
	
	Если НЕ ТолькоОрганизацияИСчет Тогда
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ДатаОтбора");
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "БанкСчетаОрганизации");
	КонецЕсли;
	
	УстановитьВидимостьБанковскогоСчета();   
	УстановитьИспользованиеСогласованияПлатежей(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьБанковскогоСчета()
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ИспользоватьНесколькоБанковскихСчетовОрганизации =
			Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(ОтборОрганизация);
	Иначе
		ИспользоватьНесколькоБанковскихСчетовОрганизации = Истина;
	КонецЕсли;
	
	УстановленОтборПоБанку = ЗначениеЗаполнено(ОтборБанкСчетаОрганизации);
	Элементы.ОтборБанкСчетаОрганизации.Видимость = УстановленОтборПоБанку;
	Элементы.ОтборБанкСчетаОрганизацииИспользование.Видимость = УстановленОтборПоБанку;

	Элементы.ОтборСчетОрганизации.Видимость              = Не УстановленОтборПоБанку 
		И ИспользоватьНесколькоБанковскихСчетовОрганизации;
	Элементы.ОтборСчетОрганизацииИспользование.Видимость = Не УстановленОтборПоБанку
		И ИспользоватьНесколькоБанковскихСчетовОрганизации;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыБанковскогоСчета(БанковскийСчет)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БанковскийСчет, "Владелец, Банк");
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьСчетОрганизации(СчетОрганизации, Знач Организация)
	
	Если Организация = Неопределено Тогда
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	УчетДенежныхСредствБП.УстановитьБанковскийСчет(СчетОрганизации, Организация,
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуетсяДиректБанк()
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьОбменСБанками");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьИспользованиеСогласованияПлатежей(Форма)
	
	ДоступностьСогласованиеПлатежей = Форма.ИспользуетсяСогласованиеПлатежей;  

	Элементы = Форма.Элементы;   
	Элементы.СогласованиеПлатежей.Видимость = Ложь;  
	
	Если Форма.ИспользуетсяСогласованиеПлатежей И Форма.ИспользоватьВалютныйУчет Тогда
		Если Форма.ОтборСчетОрганизацииИспользование И ЗначениеЗаполнено(Форма.ОтборСчетОрганизации) Тогда
			ДоступностьСогласованиеПлатежей = ДоступностьСогласованияПлатежейПоБанковскомуСчету(Форма.ОтборСчетОрганизации);
		КонецЕсли;
	КонецЕсли; 
	
	Элементы.СогласованиеПлатежей.Видимость = ДоступностьСогласованиеПлатежей;
		
КонецФункции

&НаСервереБезКонтекста
Функция ДоступностьСогласованияПлатежейПоБанковскомуСчету(БанковскийСчет)
	
	ДоступностьСогласованиеПлатежей = 
		УчетДенежныхСредствБП.ДоступностьСогласованияПлатежейПоБанковскомуСчету(БанковскийСчет);
	
	Возврат ДоступностьСогласованиеПлатежей;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОграничениеТипаОтбораИнформации(Форма)
	
	Если Форма.ТипИнформации = "Сотрудник" Тогда
		Форма.Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица");
	Иначе
		Форма.Элементы.ОтборКонтрагент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Контрагенты");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВнешнийВидОтбораКонтрагент()
	
	ТипыОтбора = Элементы.ТипИнформации.СписокВыбора;
	
	Если НЕ ИспользуетсяПодсистемаЗарплата Тогда
		ЭлементОтборСотрудник = ТипыОтбора.НайтиПоЗначению("Сотрудник");
		Если ЭлементОтборСотрудник <> Неопределено Тогда
			ТипыОтбора.Удалить(ЭлементОтборСотрудник);
		КонецЕсли;
	КонецЕсли;
	
	ЕдинственныйТипОтбора = ТипыОтбора.Количество() = 1;
	
	Элементы.ТипИнформации.Видимость = НЕ ЕдинственныйТипОтбора;
	Элементы.ОтборКонтрагентИспользование.ПоложениеЗаголовка = ?(ЕдинственныйТипОтбора,
		ПоложениеЗаголовкаЭлементаФормы.Лево, ПоложениеЗаголовкаЭлементаФормы.Нет);
	
	Если ЕдинственныйТипОтбора Тогда
		// Установим название отбора из единственного доступного типа
		Элементы.ОтборКонтрагентИспользование.Заголовок = СтрЗаменить(ТипыОтбора[0].Представление, ":", "");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизвестиОбменСБанком()
	
	Если ЗначениеЗаполнено(ОтборСчетОрганизации) Тогда
		ПараметрыБанковскогоСчета = ПараметрыБанковскогоСчета(ОтборСчетОрганизации);
		ОбменСБанкамиКлиент.СинхронизироватьСБанком(ПараметрыБанковскогоСчета.Владелец, ПараметрыБанковскогоСчета.Банк);
	Иначе
		ОбменСБанкамиКлиент.СинхронизироватьСБанком();
	КонецЕсли;
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_УчетСтраховыхВзносовИП");
	Если ИспользуетсяЭП Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяЭП");
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

&НаСервере
Процедура НастройкиДинамическогоСписка()
	
	Отчеты.РеестрДокументов.НастройкиДинамическогоСписка(ЭтотОбъект);
	
КонецПроцедуры

#Область ИнтеграцияСБанками

&НаКлиенте
Процедура Подключаемый_ЗапуститьПроверкуСостояниеИнтеграцииВФоне() Экспорт
	
	Результат = СостояниеИнтеграцииСБанкомВФоне(УникальныйИдентификатор);
	ОжидатьЗавершенияФоновогоЗаданияПолученияСостоянияИнтеграцииСБанком(Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СостояниеИнтеграцииСБанкомВФоне(УникальныйИдентификатор)
	
	ПараметрыПроцедуры = Новый Структура;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Проверка состояния интеграции с банками.'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ДокументыИнтеграцииСБанком.ДанныеБаннераИнтеграцииСБанкомВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ОжидатьЗавершенияФоновогоЗаданияПолученияСостоянияИнтеграцииСБанком(Результат)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПослеПолученияИнформацииОВнешнейКомпоненте = Новый ОписаниеОповещения(
		"ОбработатьРезультатПроверкиСостоянияИнтеграцииСБанком", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ПослеПолученияИнформацииОВнешнейКомпоненте, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПроверкиСостоянияИнтеграцииСБанком(Результат, ДополнительныеПараметры) Экспорт
	
	ПоказатьБаннерСостоянияИнтеграцииСБанком(Результат);
	Если ИнтервалПроверкиСостоянияИнтеграцииСБанком > 0 Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЗапуститьПроверкуСостояниеИнтеграцииВФоне", ИнтервалПроверкиСостоянияИнтеграцииСБанком, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьБаннерСостоянияИнтеграцииСБанком(Результат)
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ДанныеБаннера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ИнтеграцияСБанкамиФормы.ПоказатьБаннерСостоянияОтправки(ЭтотОбъект, ДанныеБаннера);
	Иначе
		Элементы.ГруппаБаннерСостоянияОтправки.Видимость = Ложь;
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	Если УправлениеПечатьюБПКлиентСервер.ЭтоИмяКомандыРеестрДокументов(Команда.Имя) Тогда
		НастройкиДинамическогоСписка();
	КонецЕсли;
	
	ПроверяемыеКомандыНесогласованныхПлатежей
		= УчетДенежныхСредствКлиент.ПроверяемыеКомандыЭДОНесогласованныхПлатежей();
	ПроверяемыеКомандыОтклоненныхПлатежей
		= УчетДенежныхСредствКлиент.ПроверяемыеКомандыЭДООтклоненныхПлатежей();
	
	Если ИспользуетсяСогласованиеПлатежей Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Если ТекущиеДанные.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отклонено")
				И ПроверяемыеКомандыОтклоненныхПлатежей.Найти(Команда.Имя) <> Неопределено Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Платеж отклонен'"));
				Возврат;
			КонецЕсли;
			
			Если ТекущиеДанные.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Подготовлено")
				И ПроверяемыеКомандыНесогласованныхПлатежей.Найти(Команда.Имя) <> Неопределено Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Платеж не согласован'"));
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ДиректБанк

&НаКлиенте
Процедура Подключаемый_ВыполнитьСинхронизациюДиректБанк(Команда)

	ПроизвестиОбменСБанком();

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьСобытиеДиректБанк(
		Параметр1 = Неопределено,
		Параметр2 = Неопределено,
		Параметр3 = Неопределено)
	
	ОбменСБанкамиКлиент.ОбработатьСобытиеНаФормеСписка(Параметр1, Параметр2, Параметр3)
	
 КонецПроцедуры

#КонецОбласти

// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыПроверитьАктуальностьПредложений() Экспорт
	
	ОбменСБанкамиДепозитыКлиент.ПроверитьАктуальностьИОбновитьДанныеДепозитныхПродуктов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменСБанкамиДепозитыОбновитьУсловияПродукта() Экспорт
	
	ОбменСБанкамиДепозитыКлиент.ПроверитьОбновитьРасчетПоУсловиямТекущегоПродукта(
		ЭтотОбъект, ОбменСБанкамиДепозиты_ПараметрыПредложений);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменСБанкамиДепозитыВыполнитьИнициализацию() Экспорт
	
	РезультатИнициализации = ОбменСБанкамиДепозитыЗавершитьИнициализацию();
	ОбменСБанкамиДепозитыКлиент.УстановитьПредложенияБанковПослеИнициализации(ЭтотОбъект,
		ОбменСБанкамиДепозиты_ПараметрыПредложений, РезультатИнициализации);
	ЕстьПредложенияБанков = РезультатИнициализации.ЕстьПредложенияБанков;
	НастроитьОтображениеКомандыПоказатьПанельИнформации(ЕстьПредложенияБанков);
	
КонецПроцедуры

&НаСервере
Функция ОбменСБанкамиДепозитыЗавершитьИнициализацию()
	Возврат ОбменСБанкамиДепозиты.ПроверитьПродуктыБанковИЗавершитьИнициализацию(ЭтотОбъект);
КонецФункции

&НаКлиенте
Процедура ИзменитьВидимостьПанелиИнформации()
	
	ИмяКоманды = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПоказатьРасчетДепозитов");
	ОбменСБанкамиДепозитыКлиент.ВыполнитьКоманду(ЭтотОбъект,
		ОбменСБанкамиДепозиты_ПараметрыПредложений, Неопределено, ИмяКоманды);
		
	Если Не Элементы.ГруппаДепозитыРасчеты.Видимость Тогда
		ОбменСБанкамиДепозитыВызовСервера.ОбработатьЗакрытиеПанелиРасчетаДепозитов();
	КонецЕсли;
	
	НастроитьОтображениеКомандыПоказатьПанельИнформации(Элементы.ГруппаДепозитыРасчеты.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтображениеКомандыПоказатьПанельИнформации(РасчетыВидимость)
	
	Если РасчетыВидимость Тогда
		Элементы.ПоказатьПанельИнформации.Картинка = БиблиотекаКартинок.СтрелкаВправо;
	Иначе
		Элементы.ПоказатьПанельИнформации.Картинка = БиблиотекаКартинок.СтрелкаВлево;
	КонецЕсли;
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Элементы.Список);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаСервере
Процедура ИзменитьПараметрыВыбораБанковскогоСчета()
	
	Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	ДоступенПлатежОбменСЦифровымКошельком = ЦифровойРубль.ДоступенПлатежОбменСЦифровымКошельком(Период);
	
	Если ДоступенПлатежОбменСЦифровымКошельком Тогда
		Возврат;
	КонецЕсли;
	
	МассивПараметров = Новый Массив();
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЦифровойСчет", Ложь));
	
	Элементы.ОтборСчетОрганизации.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено));
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияПлатежныеПоручения());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстБаннераОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	АдаптироватьМКОтборыФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.КомандаСоздать = "ФормаСоздать";
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
	МобильныйКлиентБПФормаСписка.ПеренестиКомандыВМККомандыФормы(ЭтотОбъект, Элементы.ГруппаВажныеКоманды.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаСписка.ПеренестиЭлементВМККомандыФормы(ЭтотОбъект, Элементы.ПодменюОтчеты);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьОтбор(Команда)
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойОтбораМК(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКОтборыФормы()
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОтбораМК(ЭтотОбъект, "Список", "Организация");
	
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораОрганизация(ЭтотОбъект,
		Элементы.ОтборОрганизация,
		"",
		НСтр("ru = 'Все организации'"));
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораБыстрыеОтборы(ЭтотОбъект,
		Элементы.ОтборДатаОтбора,
		НСтр("ru = 'Дата'"),
		"");
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораБыстрыеОтборы(ЭтотОбъект,
		Элементы.ОтборСчетОрганизации,
		НСтр("ru = 'Банковский счет'"),
		НСтр("ru = 'Все банковские счета'"));
	
	ТипыОтбора = Элементы.ТипИнформации.СписокВыбора;
	ЕдинственныйТипОтбора = ТипыОтбора.Количество() = 1;
	
	Если Не ЕдинственныйТипОтбора Тогда
		МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКБыстрыеОтборы(ЭтотОбъект, Элементы.ТипИнформации);
		Элементы.ТипИнформации.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
		Элементы.ТипИнформации.ШрифтЗаголовка = ШрифтыСтиля.ШрифтТекстаЗаголовкаМК;
		Элементы.ТипИнформации.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаЗаголовкаМК;
		Элементы.ТипИнформации.Заголовок = НСтр("ru = 'Вид контрагента'");
		Для Каждого ТипОтбора Из ТипыОтбора Цикл
			ТипОтбора.Представление = СтрЗаменить(ТипОтбора.Представление,":","");
		КонецЦикла;
	КонецЕсли;
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораБыстрыеОтборы(ЭтотОбъект,
		Элементы.ОтборКонтрагент,
		НСтр("ru = 'Контрагент'"),
		НСтр("ru = 'Все контрагенты'"));
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКБыстрыеОтборы(ЭтотОбъект, Элементы.ГруппаДополнительныеОтборы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы, Элементы.ПодменюПечать, Элементы.Список.КонтекстноеМеню);
	
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементыФормы(Элементы,
		Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы,
		Элементы.Список.КонтекстноеМеню);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормы(ЭтотОбъект);
	
	//В реквизитах установить "Использовать всегда" для источника КолонкаСостояниеДокумента
	МобильныйКлиентБПФормаСписка.ДобавитьКолонкаСостояниеДокумента(ЭтотОбъект, "СтандартнаяКартинка", "Список");
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СостояниеДокумента(ЭтотОбъект, Элементы.ЕстьФайлы);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СостояниеДокумента(ЭтотОбъект, Элементы.СчетВРежимеИнтеграции);
	
	Элементы.Дата.РастягиватьПоГоризонтали = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, Элементы.Дата);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СуммаДокумента(ЭтотОбъект, Элементы.СуммаДокумента);
	
	Элементы.Контрагент.РастягиватьПоГоризонтали = Истина;
	Элементы.Контрагент.АвтоВысотаЯчейки = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Контрагент);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.ВидОперации, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Состояние, 2);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.НазначениеПлатежа, 3);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Прочее(ЭтотОбъект, Элементы.Организация);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
КонецПроцедуры

#КонецОбласти
