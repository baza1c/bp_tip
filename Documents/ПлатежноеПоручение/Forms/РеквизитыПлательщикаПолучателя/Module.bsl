
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПлательщик = Параметры.ЭтоПлательщик;
	Дата = Параметры.Объект.Дата;
	
	ОрганизацияИП = ?(ЭтоПлательщик,
		(Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо =
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Объект.Организация, "ЮридическоеФизическоеЛицо")),
		Ложь);
	
	ПереводМеждуСчетами         = УчетДенежныхСредствКлиентСервер.СписаниеСредствНаДругойСчет(Параметры.Объект.ВидОперации);
	ПеречислениеФизическомуЛицу = УчетДенежныхСредствКлиентСервер.РасчетыСФизическимиЛицами(Параметры.Объект.ВидОперации);
	ПеречислениеНаЛичныйСчет    = ПеречислениеФизическомуЛицу И НЕ ЗначениеЗаполнено(Параметры.Объект.Банк);
	
	АвтоЗначенияРеквизитов = УчетДенежныхСредствБП.СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя(
		Параметры.Объект.Организация,
		Параметры.Объект.СчетОрганизации,
		?(ПереводМеждуСчетами, Параметры.Объект.Организация,
			?(ПеречислениеФизическомуЛицу И НЕ ПеречислениеНаЛичныйСчет, Параметры.Объект.Банк, Параметры.Объект.Контрагент)),
		Параметры.Объект.СчетКонтрагента,
		Истина,
		Дата);
	
	Если ЭтоПлательщик Тогда
		ВсегдаУказыватьКПП     = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Объект.СчетОрганизации, "ВсегдаУказыватьКПП");
		ТекстНаименования      = Параметры.Объект.ТекстПлательщика;
		УказаниеКППОбязательно = Параметры.Объект.ПеречислениеВБюджет ИЛИ НЕ ОрганизацияИП И ВсегдаУказыватьКПП;
		
		ИНН = Параметры.Объект.ИННПлательщика;
		КПП = Параметры.Объект.КПППлательщика;
		
		ИННОбъекта = АвтоЗначенияРеквизитов.ИННПлательщика;
		Если УказаниеКППОбязательно Тогда
			Если НЕ ПустаяСтрока(КПП) Тогда
				КППОбъекта = КПП;
			ИначеЕсли НЕ ПустаяСтрока(АвтоЗначенияРеквизитов.КПППлательщика) Тогда
				КППОбъекта = АвтоЗначенияРеквизитов.КПППлательщика;
			ИначеЕсли ЭтоПлательщик И ОрганизацияИП Тогда
				КППОбъекта = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ПереводМеждуСчетами Тогда
			ПолучательФизЛицо = ОрганизацияИП;
		ИначеЕсли ПеречислениеФизическомуЛицу Тогда
			Если ПеречислениеНаЛичныйСчет Тогда
				ПолучательФизЛицо = Истина;
			ИначеЕсли ЗначениеЗаполнено(Параметры.Объект.Банк) Тогда
				СтранаРегистрацииПолучателя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Объект.Банк, "СтранаРегистрации");
				ПолучательНерезидент        = СтранаРегистрацииПолучателя <> Справочники.СтраныМира.Россия;
			КонецЕсли;
		Иначе
			Если Параметры.Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДивидендов
				И ТипЗнч(Параметры.Объект.Контрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				ПолучательФизЛицо = Истина;
				СтранаРегистрацииПолучателя = Справочники.СтраныМира.Россия;
			Иначе
				ПолучательФизЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо =
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
						?(ПеречислениеФизическомуЛицу, Параметры.Объект.Банк, Параметры.Объект.Контрагент), "ЮридическоеФизическоеЛицо");
						
				СтранаРегистрацииПолучателя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Объект.Контрагент, "СтранаРегистрации");
			КонецЕсли;
			
			ПолучательНерезидент = СтранаРегистрацииПолучателя <> Справочники.СтраныМира.Россия;
		КонецЕсли;
		
		ТекстНаименования      = Параметры.Объект.ТекстПолучателя;
		УказаниеКППОбязательно = НЕ ПолучательФизЛицо И НЕ ПолучательНерезидент
			И (Параметры.Объект.ПеречислениеВБюджет
			ИЛИ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Объект.СчетКонтрагента, "ВсегдаУказыватьКПП"));
		
		ИНН = Параметры.Объект.ИННПолучателя;
		КПП = Параметры.Объект.КПППолучателя;
		
		ИННОбъекта = АвтоЗначенияРеквизитов.ИННПолучателя;
		Если УказаниеКППОбязательно И НЕ ПустаяСтрока(КПП) Тогда
			КППОбъекта = КПП;
		Иначе
			КППОбъекта = АвтоЗначенияРеквизитов.КПППолучателя
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЭтоПлательщик Тогда
		Элементы.ИНН.Заголовок = НСтр("ru = 'ИНН получателя'");
		Элементы.КПП.Заголовок = НСтр("ru = 'КПП получателя'");
		Элементы.ТекстНаименования.Заголовок = НСтр("ru = 'Наименование получателя'");
	КонецЕсли;
	
	ПлательщикаПолучателя = ?(ЭтоПлательщик, НСтр("ru = 'плательщика'"), НСтр("ru = 'получателя'"));
	
	ТекстИННВладельца = НСтр("ru = '%1 - ИНН, указанный для %2'");
	ТекстИННВладельца = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстИННВладельца, ИННОбъекта, ПлательщикаПолучателя);
	Элементы.ИНН.СписокВыбора.Добавить(ИННОбъекта, ТекстИННвладельца);
	
	Если ЭтоПлательщик И Не ОрганизацияИП Тогда
		КБК = Параметры.Объект.КодБК;
		ЭтоУплатаЕНП = ПлатежиВБюджетКлиентСервер.ЭтоЕдиныйНалоговыйПлатеж(КБК);
		
		ЗаполнитьСписокКПП(СписокКПП, Параметры.Объект.Организация, КППОбъекта, Дата, ЭтоУплатаЕНП);
	Иначе
		ТекстКППвладельца = НСтр("ru = '%1 - КПП, указанный для %2 (основной)'");
		ТекстКППвладельца = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстКППвладельца, КППОбъекта, ПлательщикаПолучателя);
		СписокКПП.Добавить(КППОбъекта, ТекстКППвладельца);
	КонецЕсли;
	
	Для Каждого ЭлементСписка Из СписокКПП Цикл
		Элементы.КПП.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	УстановитьДоступность();
	
	Заголовок = ?(ЭтоПлательщик, НСтр("ru = 'Реквизиты плательщика'"), НСтр("ru = 'Реквизиты получателя'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Изменить значения реквизитов в документе?'");
		Оповещение   = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "Неопределено" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИзСпискаЗавершение", ЭтотОбъект);
		ПоказатьВыборИзСписка(ОповещениеВыбора, СписокКПП, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомндФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОбработкаВыбораРеквизитов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступность()
	
	Если ОрганизацияИП И НЕ ВсегдаУказыватьКПП Тогда
		Элементы.КПП.АвтоОтметкаНезаполненного = Ложь;
	Иначе
		Элементы.КПП.АвтоОтметкаНезаполненного = УказаниеКППОбязательно;
	КонецЕсли;
	
	Если НЕ ЭтоПлательщик И ПолучательФизЛицо Тогда
		Элементы.КПП.АвтоОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокКПП(СписокКПП, ВладелецСчета, КППВыбранный, Период, ЭтоУплатаЕНП)
	
	Владелец      = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(ВладелецСчета);
	КППОсновной   = Справочники.Организации.КППНаДату(Владелец, Период, Истина);
	КППКрупнейший = Справочники.Организации.КППНаДату(Владелец, Период, Ложь);
	Если КППКрупнейший = КППОсновной Тогда
		КППКрупнейший = "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец",                          Владелец);
	Запрос.УстановитьПараметр("КППВладельца",                      КППОсновной);
	Запрос.УстановитьПараметр("КППВыбранный",                      КППВыбранный);
	Запрос.УстановитьПараметр("КППКрупнейший",                     КППКрупнейший);
	Запрос.УстановитьПараметр("ИспользуетсяРасширенныйФункционал", ЗначениеЗаполнено(КППКрупнейший));
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.КПП КАК КПП,
	|	ВложенныйЗапрос.Наименование КАК Наименование,
	|	ВложенныйЗапрос.Код КАК Код,
	|	ВложенныйЗапрос.Приортитет КАК Приортитет
	|ИЗ
	|	(ВЫБРАТЬ
	|		Спр.Наименование КАК Наименование,
	|		Спр.КПП КАК КПП,
	|		Спр.Код КАК Код,
	|		0 КАК Приортитет
	|	ИЗ
	|		Справочник.РегистрацииВНалоговомОргане КАК Спр
	|	ГДЕ
	|		Спр.Владелец = &Владелец
	|		И Спр.КПП = &КППВыбранный
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Спр.Наименование,
	|		Спр.КПП,
	|		Спр.Код,
	|		1
	|	ИЗ
	|		Справочник.РегистрацииВНалоговомОргане КАК Спр
	|	ГДЕ
	|		Спр.Владелец = &Владелец
	|		И Спр.КПП = &КППВладельца
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Спр.Наименование,
	|		Спр.КПП,
	|		Спр.Код,
	|		2
	|	ИЗ
	|		Справочник.РегистрацииВНалоговомОргане КАК Спр
	|	ГДЕ
	|		Спр.Владелец = &Владелец
	|		И Спр.КПП = &КППКрупнейший
	|		И &ИспользуетсяРасширенныйФункционал
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Спр.Наименование,
	|		Спр.КПП,
	|		Спр.Код,
	|		3
	|	ИЗ
	|		Справочник.РегистрацииВНалоговомОргане КАК Спр
	|	ГДЕ
	|		Спр.Владелец = &Владелец
	|		И Спр.КПП <> &КППВладельца
	|		И Спр.КПП <> &КППВыбранный
	|		И НЕ Спр.ПометкаУдаления) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приортитет,
	|	КПП";
	
	ОсновнойКППВыведен   = Ложь;
	КрупнейшийКППВыведен = Не ЗначениеЗаполнено(КППКрупнейший);
	КППЕНП = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЭтоУплатаЕНП И СписокКПП.Количество() = 0 Тогда
			СписокКПП.Добавить(КППЕНП,
				СтрШаблон(НСтр("ru = '%1 - КПП, указываемый для Единого налогового платежа'"), КППЕНП));
		КонецЕсли;
		
		Если Выборка.КПП = КППОсновной Тогда
			Если Не ОсновнойКППВыведен Тогда
				ТекстШаблонаКППвладельца = НСтр("ru = '%1 - КПП, указанный для плательщика (основной)'");
				КППвладельцаПредставление = СтрШаблон(ТекстШаблонаКППвладельца, Выборка.КПП);
				СписокКПП.Добавить(Выборка.КПП, КППвладельцаПредставление);
				ОсновнойКППВыведен = Истина;
				Если ЭтоУплатаЕНП Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.КПП = КППКрупнейший Тогда
			Если Не КрупнейшийКППВыведен Тогда
				ТекстШаблонаКППвладельца = НСтр("ru = '%1 - КПП, присвоенный в качестве крупнейшего налогоплательщика'");
				КППвладельцаПредставление = СтрШаблон(ТекстШаблонаКППвладельца, Выборка.КПП);
				СписокКПП.Добавить(Выборка.КПП, КППвладельцаПредставление);
				КрупнейшийКППВыведен = Истина;
				Если ЭтоУплатаЕНП Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоУплатаЕНП
			И ОсновнойКППВыведен
			И КрупнейшийКППВыведен Тогда
			Возврат;
		ИначеЕсли Не ЭтоУплатаЕНП Тогда
			ТекстШаблонаКПП = НСтр("ru = '%1 - КПП в налоговом органе ""%2""'");
			КПППредставление = СтрШаблон(ТекстШаблонаКПП, Выборка.КПП, Выборка.Наименование);
			СписокКПП.Добавить(Выборка.КПП, КПППредставление);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьОшибкиЗаполненияРеквизитов()
	
	ОчиститьСообщения();
	ЕстьОшибки = Ложь;
	
	ЭтоЮрЛицо     = ?(ЭтоПлательщик, Не ОрганизацияИП, Не ПолучательФизЛицо);
	ЭтоПолучатель = Не ЭтоПлательщик;
	
	ИННПроверен = Ложь;
	Если ЭтоПолучатель Тогда
		Если (ПолучательФизЛицо Или ПолучательНерезидент) И Не ПлатежиВБюджетКлиентСервер.РеквизитЗаполнен(ИНН) Тогда
			// Для получателя - физ.лица (и нерезидента) можно указывать "0" или пустой ИНН, если ИНН неизвестен или отсутствует.
			ИННПроверен = Истина;
		ИначеЕсли ПолучательНерезидент И СтрДлина(СокрЛП(ИНН)) = 5 Тогда
			// Для нерезидента может указываться КИО, который состоит из 5 знаков.
			ИННПроверен = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ИННПроверен И ПустаяСтрока(ИНН) Тогда
		Если ЭтоПлательщик Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""ИНН плательщика"" не заполнено'"),, "ИНН",, ЕстьОшибки);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Поле ""ИНН получателя"" не заполнено'"),, "ИНН",, ЕстьОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ИННПроверен И Не ЕстьОшибки Тогда
		РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ИНН, ЭтоЮрЛицо);
		Если РезультатПроверки.СоответствуетТребованиям Тогда
			ИННПроверен = Истина;
		КонецЕсли;
		
		Если Не ИННПроверен И ЭтоПлательщик Тогда
			// Возможно это уплата по исполнительному листу ("101" - "19") и при уплате налога за третье лицо.
			// Дополнительная проверка на соответствие полю "101" (Статус плательщика) будет в обработке проведения.
			РезультатПроверкиФизЛицо = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(ИНН, НЕ ЭтоЮрЛицо);
			Если ИНН = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение() Или РезультатПроверкиФизЛицо.СоответствуетТребованиям Тогда
				ИННПроверен = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ИННПроверен И Не РезультатПроверки.СоответствуетТребованиям Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатПроверки.ОписаниеОшибки,, "ИНН",, ЕстьОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если УказаниеКППОбязательно Тогда
		Если ПустаяСтрока(КПП) Тогда
			Если ЭтоПлательщик Тогда
				ТекстСообщения = НСтр("ru = 'Поле ""КПП плательщика"" не заполнено'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Поле ""КПП получателя"" не заполнено'");
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "КПП",, ЕстьОшибки);
		ИначеЕсли ОрганизацияИП И КПП = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение() Тогда
			// Для плательщика - ИП указывается "0".
		Иначе
			ЭтоКонтрагент = НЕ ЭтоПлательщик;
			РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямКПП(КПП, ЭтоЮрЛицо, Ложь, ЭтоКонтрагент);
			Если НЕ РезультатПроверки.СоответствуетТребованиям Тогда
				Если НЕ (ЭтоПлательщик И КПП = ПлатежиВБюджетКлиентСервер.НезаполненноеЗначение()) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатПроверки.ОписаниеОшибки,, "КПП",, ЕстьОшибки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстНаименования) Тогда
		Если ЭтоПлательщик Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Наименование плательщика"" не заполнено'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Поле ""Наименование получателя"" не заполнено'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ТекстНаименования",, ЕстьОшибки);
	КонецЕсли;
	
	Возврат ЕстьОшибки;
	
КонецФункции

&НаКлиенте
Процедура ОповеститьОВыбранныхРеквизитахДокумента()
	
	СтруктураРезультата = Новый Структура;
	Если ЭтоПлательщик Тогда
		СтруктураРезультата.Вставить("ИННПлательщика",   ИНН);
		СтруктураРезультата.Вставить("КПППлательщика",   КПП);
		СтруктураРезультата.Вставить("ТекстПлательщика", ТекстНаименования);
		ОповеститьОВыборе(СтруктураВозврата("РеквизитыПлательщика", СтруктураРезультата));
	Иначе
		СтруктураРезультата.Вставить("ИННПолучателя",   ИНН);
		СтруктураРезультата.Вставить("КПППолучателя",   КПП);
		СтруктураРезультата.Вставить("ТекстПолучателя", ТекстНаименования);
		ОповеститьОВыборе(СтруктураВозврата("РеквизитыПолучателя", СтруктураРезультата));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураВозврата(ВидРеквизитов, Реквизиты)
	
	Возврат Новый Структура(ВидРеквизитов, Реквизиты);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораРеквизитов()
	
	Если ЕстьОшибкиЗаполненияРеквизитов() Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	ОповеститьОВыбранныхРеквизитахДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИзСпискаЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		КПП = ВыбранныйЭлемент.Значение;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОбработкаВыбораРеквизитов();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
