&НаКлиенте
Перем СтрокаПоискаПоКБК;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	РеквизитыОрганизацииСсылка = ПроверкаРеквизитовОрганизации.СтрокаСообщенияНеЗаполненБанковскийСчет();
	
	ЗаполнитьСписокОчередностьПлатежа();
	
	Если Параметры.Свойство("ВидОперации") Тогда
		Объект.ВидОперации = Параметры.ВидОперации;
	КонецЕсли;
	
	ПлатежноеПоручениеФормы.ЗаполнитьПравилоУплатыНалоговИзПараметровФормы(ЭтотОбъект, Параметры);
	ПлатежноеПоручениеФормы.ЗаполнитьРегистрациюВНалоговомОрганеИзПараметровФормы(ЭтотОбъект, Параметры);
	ПлатежноеПоручениеФормы.ЗаполнитьСведенияОГосударственномОрганеИзПараметров(ЭтотОбъект, Параметры);
	
	Если Параметры.Ключ.Пустая() Тогда
		ВидПлатежаПоУмолчанию();
		Если НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			УстановитьОчередностьПлатежа();
			ПлатежноеПоручениеФормыКлиентСервер.ОбновитьПодсказкуОчередностьПлатежа(ЭтотОбъект, Объект.ОчередностьПлатежа);
		КонецЕсли;
		
		ПроверитьУстановитьСчетОрганизацииПоУмолчанию();
		ПодготовитьФормуНаСервере();
		КодВидаДоходаПоУмолчанию(ЭтотОбъект);
	КонецЕсли;
	
	ДлительнаяОперацияПроверкиПлатежныхРеквизитов = ПлатежноеПоручениеФормы.ПроверитьПлатежныеРеквизитыВФоне(ЭтотОбъект);
	
	// Уведомим об изменениях в правилах платежей в бюджет
	НомерСчетаПолучателя = Объект.СчетКонтрагента.НомерСчета;
	
	// Если платежка не соответствует правилам платежей в бюджет,
	// то предложим пользователю варианты выхода из случая
	Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Если НЕ Объект.ПеречислениеВБюджет
			И (ПлатежиВБюджетКлиентСервер.ЭтоСчетПлатежаВБюджет(НомерСчетаПолучателя, Объект.Дата)
			ИЛИ УчетДенежныхСредствБП.ПлатежГосОргану(Объект.ВидОперации, Объект.Контрагент))Тогда
			ИзменитьПеречислениеВБюджет();
		Иначе
			ПлатежноеПоручениеФормы.НайтиОшибкиПлатежаВБюджет(ЭтотОбъект, Параметры.ЗначениеКопирования.Дата);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ИспользоватьНесколькоБанковскихСчетовОрганизации =
			Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Объект.Организация);
	Иначе
		ИспользоватьНесколькоБанковскихСчетовОрганизации = Истина;
	КонецЕсли;
	
	НастроитьСписокВыбораКодВидаДохода(ЭтотОбъект);
	УстановитьПредставлениеКодаУдержанияПоИспЛисту(ЭтотОбъект, Объект.КодВидаДохода);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.ПлатежноеПоручение",
		"ФормаДокумента",
		НСтр("ru = 'Новости: Платежное поручение'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	РаспознанныйДокументОснование = Параметры.РаспознанныйДокументОснование;
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_ПредупреждениеОбОшибке", 0.5, Истина);
	
	ЗапуститьПроверкуФоновогоЗадания();
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	Если ПоказатьИндексыСПАРК Тогда
		СПАРКРискиКлиент.ПриОткрытии(ЭтотОбъект, Неопределено);
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НадежностьБанковКлиент.ПодключитьОбработчикПоказатьИнформациюНадежностьБанков(ЭтотОбъект);
	
	ЗапуститьОбновлениеБаннераСостоянияОтправки();
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("РегистрСведенийКлючЗаписи.РеквизитыУплатыНалоговИПлатежейВБюджет") Тогда
		
		Если Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога") Тогда
			
			Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога");
			ВидОперацииОбработатьИзменение();
			
			// СПАРК Риски
			ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеИндексыСПАРК", 0.1, Истина);
			// Конец СПАРК Риски
			
		КонецЕсли;
		
		ИзменитьПеречислениеВБюджет(ВыбранноеЗначение);
		
		УправлениеФормой(ЭтотОбъект);
		
		УведомитьПользователяПлатежиВБюджет();
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ПравилаРегулярныхПлатежей") Тогда
		
		НадписьСледующийПлатеж = НадписьСледующийПлатеж(ВыбранноеЗначение, ПериодСобытияРегулярногоПлатежа);
		
		Если УстановитьПравилоПоДокументу(Объект.Организация, Объект.Ссылка, ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа) Тогда
			ОбновитьЗадачиБухгалтера();
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		РеквизитыПлательщика = Неопределено;
		РеквизитыПолучателя  = Неопределено;
		Если ВыбранноеЗначение.Свойство("РеквизитыПлательщика", РеквизитыПлательщика) Тогда
			ИзмененКПП = РеквизитыПлательщика.КПППлательщика <> Объект.КПППлательщика;
			Если ИзмененКПП
				Или РеквизитыПлательщика.ИННПлательщика <> Объект.ИННПлательщика
				Или СокрЛП(РеквизитыПлательщика.ТекстПлательщика) <> СокрЛП(Объект.ТекстПлательщика) Тогда
				Модифицированность = Истина;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Объект, РеквизитыПлательщика);
			Если ИзмененКПП
				И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога") Тогда
				РеквизитыГосОргана = Неопределено;
				ОбработатьИзменениеКПППлательщика(РеквизитыГосОргана);
				Если РеквизитыГосОргана <> Неопределено И РеквизитыГосОргана.ТребуетсяПерезаполнитьКонтрагента Тогда
					// Выбран КПП организации, который относится к другому налоговому органу.
					// Запросим изменение получателя платежа.
					Оповещение = Новый ОписаниеОповещения("ЗаменитьКонтрагентаНалоговыйОрган", ЭтотОбъект, РеквизитыГосОргана);
					ТекстВопроса = СтрШаблон(НСтр("ru = 'Выбранный КПП относится к другому налоговому органу.
						|Заменить получателя на %1?'"), РеквизитыГосОргана.Ссылка);
					ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
				КонецЕсли;
			ИначеЕсли Модифицированность Тогда
				НадписьРеквизитыПлательщика = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
			КонецЕсли;
		ИначеЕсли ВыбранноеЗначение.Свойство("РеквизитыПолучателя", РеквизитыПолучателя) Тогда
			ЗаполнитьЗначенияСвойств(Объект, РеквизитыПолучателя);
			НадписьРеквизитыПолучателя = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
			Модифицированность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ТипЗнч(НовыйОбъект) = Тип("СправочникСсылка.ПравилаРегулярныхПлатежей") Тогда
		
		ПравилоРегулярногоПлатежа = НовыйОбъект;
		НадписьСледующийПлатеж    = НадписьСледующийПлатеж(НовыйОбъект, ПериодСобытияРегулярногоПлатежа);
		
		Если УстановитьПравилоПоДокументу(Объект.Организация, Объект.Ссылка, ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа) Тогда
			ОбновитьЗадачиБухгалтера();
			Оповестить("ДобавитьБлокИнформационнойПанели", "ПериодическиеПлатежи");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьФорму" И ТипЗнч(Источник) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		
		Прочитать();
		
		// СПАРК Риски
		ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеИндексыСПАРК", 0.1, Истина);
		// Конец СПАРК Риски
		
	ИначеЕсли ИмяСобытия = "ИзменениеНастроекПлатежныхПорученийИТребований" Тогда
		НастройкиПечатиПриИзмененииНаСервере();
		Модифицированность = Истина;
	ИначеЕсли ИмяСобытия = "Запись_Контрагенты" Тогда
		Если Источник = Объект.Контрагент Тогда
			УстановитьРеквизитыПолучателяНаСервере(Истина);
			Модифицированность = Истина;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_Организации" Тогда
		Если Источник = Объект.Организация Тогда
			ОбработатьИзмененияВОрганизацииНаСервере();
			Модифицированность = Истина;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОбновитьСостояниеОбменСБанками" Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПолучитьСостояниеОбмена(Объект.Ссылка));
		УстановитьСвойстваПоляСостояние(ЭтотОбъект);    
		УстановитьСвойстваИнформацииСогласования(); 
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" Тогда
		Если Параметр.Владелец = Объект.Организация Тогда
			УстановитьРеквизитыПлательщикаНаСервере();
			СчетОрганизацииПриИзмененииНаСервере();
			Модифицированность = Истина;
		ИначеЕсли Параметр.Владелец = Объект.Контрагент Или Параметр.Владелец = Объект.Банк Тогда
			УстановитьРеквизитыПолучателяНаСервере(Истина, Ложь);
			СчетКонтрагентаПриИзмененииНаСервере();
			Модифицированность = Истина;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = РаботаСДоговорамиКонтрагентовБПКлиент.ИмяСобытияЗаписьДоговора()
		И Объект.ДоговорКонтрагента = Источник Тогда
		ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоПолучен()
		И Объект.ДоговорКонтрагента = Источник Тогда
		СтатусСамозанятого = РезультатПроверкиСтатусаСамозанятого(
			Параметр.ПлательщикНПД, Параметр.НаДату, Параметр.ДатаРегистрации);
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоОшибка()
		И Объект.ДоговорКонтрагента = Источник Тогда
		СтатусСамозанятого = РезультатПроверкиСтатусаСамозанятого(Ложь, Неопределено, , Параметр);
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	СПАРКРискиКлиент.ОбработкаОповещения(ЭтотОбъект, Неопределено, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ЗаполнитьСписокОчередностьПлатежа();
	ЗаполнитьПравилоРегулярногоПлатежаНаСервере(Объект.Ссылка);
	
	ПрочитатьСтатусСамозанятого();
	
	ПодготовитьФормуНаСервере();
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект, "ГруппаКомментарий", "Ответственный");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПроверитьПередЗаписью(Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Оповещение   = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "ПроведениеПлатежноеПоручение";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("ПропуститьПроверку")
		И ПараметрыЗаписи.ПропуститьПроверку Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Объект.ПеречислениеВБюджет Тогда
		ПлатежноеПоручениеФормыКлиентСервер.СброситьПризнакПлатежаВБюджет(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = ПроверкаРеквизитовПередЗаписьюНаСервере();
	
	СообщатьОДубляхУИН = НЕ СчетПоГосконтракту;
	
	ТекстВопроса = ПлатежноеПоручениеФормыКлиент.ТекстВопросаОбОшибкахЗаполнения(РезультатПроверки, СообщатьОДубляхУИН);
	
	Если ПустаяСтрока(ТекстВопроса) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина; // Примем решение позже, в зависимости от ответа пользователя
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ВопросЗаписатьСОшибкамиЗавершение", ЭтотОбъект, ПараметрыЗаписи),
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПроверкаРеквизитовОрганизации.ПередЗаписьюНаСервере(
			Объект.Организация,
			Объект.СчетОрганизации,
			ИспользоватьНесколькоБанковскихСчетовОрганизации,
			Отказ,
			ПараметрыЗаписи);
			
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Состояние", Состояние);
	
	ВыполнениеЗадачБухгалтера.УстановитьСвойстваПлатежаПриРегистрации(
		ТекущийОбъект,
		ПравилоУплатыНалогов,
		ПериодСобытияКалендаря,
		,
		РегистрацияВНалоговомОргане);
	
	// Если задано правило регулярного платежа, добавим запись в служебный регистр.
	Если ЗначениеЗаполнено(ПравилоРегулярногоПлатежа) Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ПравилоРегулярногоПлатежа", ПравилоРегулярногоПлатежа);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ПериодСобытияРегулярногоПлатежа", ПериодСобытияРегулярногоПлатежа);
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("СостояниеИнтеграцииСБанкомДо", РегистрыСведений.ДокументыИнтеграцииСБанком.СостояниеИнтеграции(ТекущийОбъект.Ссылка));
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	Если ЗначениеЗаполнено(РаспознанныйДокументОснование) Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("РаспознанныйДокументОснование", РаспознанныйДокументОснование);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ХранилищеОбщихНастроек.Сохранить("ПлатежноеПоручение_ВидПлатежа",, Объект.ВидПлатежа);
	
	ПлатежноеПоручениеФормы.УстановитьСостояниеДокумента(ЭтотОбъект);
	
	Если УчетДенежныхСредствКлиентСервер.РасчетыСФизическимиЛицами(ТекущийОбъект.ВидОперации)
		И НЕ ТекущийОбъект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ЛичныеСредстваПредпринимателя Тогда
		РегистрыСведений.ОсновныеВариантыПеречисленияСотруднику.УстановитьОсновнойВариантПеречисления(ТекущийОбъект.Организация,
			ТекущийОбъект.Контрагент, ТекущийОбъект.ВидОперации, ТекущийОбъект.СчетКонтрагента, ТекущийОбъект.НазначениеПлатежа);
	КонецЕсли;
	
	ПлатежноеПоручениеФормы.ПроверитьИзменитьНастройкуПлатежаВБюджет(ЭтотОбъект);
	
	РазблокироватьДанныеФормыДляРедактирования();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПолучитьСостояниеОбмена(Объект.Ссылка)); 
	
	УстановитьСвойстваИнформацииСогласования();
	
	ПараметрыЗаписи.Вставить("СостояниеИнтеграцииСБанкомПосле", РегистрыСведений.ДокументыИнтеграцииСБанком.СостояниеИнтеграции(ТекущийОбъект.Ссылка));
	Если ПараметрыЗаписи.СостояниеИнтеграцииСБанкомПосле = Перечисления.СостоянияДокументовИнтеграцииСБанком.ПодготовленоКОтправке Тогда
		РегистрыСведений.ДокументыИнтеграцииСБанком.ОтправитьОповещениеОбИзмененииПоДокументамВФоне(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("ПравилоРегулярногоПлатежаОбновлено",
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущийОбъект.ДополнительныеСвойства, "ПравилоРегулярногоПлатежаОбновлено", Ложь));
		
	Если ТекущийОбъект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику Тогда
		РаботаСДоговорамиКонтрагентовБП.ЗаписатьСпособЗаполненияСтавкиНДСПоДокументу(ТекущийОбъект.ДоговорКонтрагента,
			ТекущийОбъект.Ссылка, ТекущийОбъект.СуммаНДС <> 0, ТекущийОбъект.СтавкаНДС, Истина);
	КонецЕсли;
	
	ОбработатьИПоказатьСостоянияОтправки();
	
	БанкИКассаФормы.ПоказатьБаннерПредупреждениеОЗаполненииРеквизитовПлатежаНПД(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Оповестить("ОбновитьФорму", ВладелецФормы, Объект.Ссылка);
	
	ДокументОплачен = Состояние = ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Оплачено");
	Если Объект.ПеречислениеВБюджет Тогда
		ПлатежноеПоручениеФормыКлиент.ОповеститьОЗаписиПлатежногоПорученияНаУплатуНалога(
			Объект, ДокументОплачен);
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику") Тогда
		ПлатежноеПоручениеФормыКлиент.ОповеститьОЗаписиПлатежногоПорученияНаВыплатуЗарплатыРаботнику(
			Объект, ДокументОплачен);
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП") Тогда
		ПлатежноеПоручениеФормыКлиент.ОповеститьОЗаписиПлатежногоПорученияНаВыплатуЗарплаты(
			Объект, ДокументОплачен);
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда") Тогда
		ПлатежноеПоручениеФормыКлиент.ОповеститьОЗаписиПлатежногоПорученияНаВыплатуПоДоговоруПодряда(
			Объект, ДокументОплачен);
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда") Тогда
		ПлатежноеПоручениеФормыКлиент.ОповеститьОЗаписиПлатежногоПорученияНаВыплатуПоДоговорамПодряда(
			Объект, ДокументОплачен);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПравилоРегулярногоПлатежа) Тогда
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ЗадачиБухгалтера"));
	КонецЕсли;
	
	НадписьСледующийПлатеж = НадписьСледующийПлатеж(ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа);
	
	Если ПараметрыЗаписи.Свойство("ПравилоРегулярногоПлатежаОбновлено")
		И ПараметрыЗаписи.ПравилоРегулярногоПлатежаОбновлено Тогда
		
		ОбновитьЗадачиБухгалтера();
		
	КонецЕсли;
	
	Если ПараметрыЗаписи.СостояниеИнтеграцииСБанкомДо <> ПараметрыЗаписи.СостояниеИнтеграцииСБанкомПосле Тогда
		Оповестить("ИзменилосьСостояниеДокументаИнтеграцииСБанком", Неопределено, Объект.Ссылка);
	КонецЕсли;
	
	УстановитьСвойстваГруппыСогласованияПлатежей(ЭтотОбъект); 
	
	ЗапуститьОбновлениеБаннераСостоянияОтправки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВидОперации) И ВидОперацииДоИзменения <> Объект.ВидОперации Тогда
		
		ВидОперацииПриИзмененииСервер();
		
		ЗапуститьПроверкуФоновогоЗадания();
		
		УведомитьПользователяПлатежиВБюджет();
		
		// СПАРК Риски
		ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеИндексыСПАРК", 0.1, Истина);
		// Конец СПАРК Риски
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	СписаниеСРасчетногоСчетаФормыКлиент.ВидОперацииАвтоПодбор(Объект.ВидОперации, ДанныеВыбора, Ожидание, СтандартнаяОбработка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	Перем ПериодПлатежа;
	
	УстановитьОчередностьПлатежаПо345ФЗ(ЭтотОбъект);
	ИзменитьПодсказкуОчередностьПлатежа();
	
	ИзменилсяМесяц = НачалоМесяца(Объект.Дата) <> НачалоМесяца(ПредыдущаяДата);
	РеквизитыПерезаполнены = Ложь;
	ПлатежГосОргану = ПлатежГосОргану(Объект.ВидОперации, Объект.Контрагент);
	Если ПлатежГосОргану Тогда
		Если ПлатежиВБюджетКлиентСервер.ПрименяетсяОКТМО8Символов(Объект.Дата) Тогда
			ПлатежиВБюджетКлиентСервер.ПривестиКодТерриторииКФорматуПриказа58н(Объект.КодОКАТО, Объект.Дата);
			НадписьРеквизитыПлатежейВБюджет = ПлатежноеПоручениеФормыКлиентСервер.НадписьРеквизитыПлатежейВБюджет(ЭтотОбъект);
		КонецЕсли;
		
		Если ПлатежиВБюджетКлиентСервер.РеквизитЗаполнен(Объект.ПоказательПериода) Тогда
			ПоказателиПериода = ПлатежиВБюджетКлиентСервер.РазобратьНалоговыйПериод(Объект.ПоказательПериода);
			ПериодПлатежа = ПоказателиПериода.Дата;
		КонецЕсли;
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетПриказ107н(ПредыдущаяДата)
			<> ПлатежиВБюджетКлиентСервер.ДействуетПриказ107н(Объект.Дата) Тогда
			Объект.ПеречислениеВБюджет = Истина;
			ИзменениеПериодаПримененияПравилПлатежаВБюджет();
		ИначеЕсли ПлатежиВБюджетКлиентСервер.ДействуетПриказ2017_90н(ПредыдущаяДата)
			<> ПлатежиВБюджетКлиентСервер.ДействуетПриказ2017_90н(Объект.Дата)
			ИЛИ ЗначениеЗаполнено(ПериодПлатежа)
			И ПлатежиВБюджетКлиентСервер.ДействуетПриказ2017_90н(ПериодПлатежа)
			<> ПлатежиВБюджетКлиентСервер.ДействуетПриказ2017_90н(Объект.Дата) Тогда
			НалогПриИзмененииНаСервере(РеквизитыПерезаполнены, "");
		ИначеЕсли ИзменилсяМесяц Тогда
			ИзменитьТекстПредупрежденияЕНП(ЭтотОбъект);
			ИзменитьПеречислениеВБюджет(,, Истина);
			УправлениеФормой(ЭтотОбъект);
			РеквизитыПерезаполнены = Истина;
		КонецЕсли;
		
		УведомитьПользователяПлатежиВБюджет();
	КонецЕсли;
	
	Если НачалоДня(Объект.Дата) <> НачалоДня(ПредыдущаяДата) ИЛИ ИзменилсяМесяц Тогда
		ИзменитьПлатежныеРеквизитыПриИзмененииДаты(РеквизитыПерезаполнены, ИзменилсяМесяц);
	КонецЕсли;
	
	УчетДенежныхСредствКлиентСервер.ИсправитьВДокументеСтатьюЕдиныйНалоговыйПлатеж(
		Объект.СтатьяДвиженияДенежныхСредств,
		Объект.ВидОперации,
		Объект.Налог,
		Объект.Дата,
		ПредыдущаяДата);
	
	ЗапуститьПроверкуФоновогоЗадания();
	ДатаПриИзмененииНаСервере();
	
	ДатаСледующегоПлатежа = ДатаСледующегоПлатежа(ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа);
	
	Если ЗначениеЗаполнено(ДатаСледующегоПлатежа) И Объект.Дата >= ДатаСледующегоПлатежа Тогда
		
		ТекстВопроса = НСтр(
			"ru = 'Дата документа больше срока следующего платежа.
			|Перейти к редактированию расписания (документ будет записан)?'");
		Оповещение   = Новый ОписаниеОповещения("ВопросРедактированиеПравилаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
	ПредыдущаяДата = Объект.Дата;
	
	НастроитьСписокВыбораКодВидаДохода(ЭтотОбъект);
	КодВидаДоходаПоУмолчанию(ЭтотОбъект, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		
		ОрганизацияПриИзмененииНаСервере();
		
		ЗапуститьПроверкуФоновогоЗадания();
		
		УведомитьПользователяПлатежиВБюджет();
		
		// СПАРК Риски
		ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеИндексыСПАРК", 0.1, Истина);
		// Конец СПАРК Риски
		
	Иначе
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетОрганизацииПриИзменении(Элемент)
	
	СчетОрганизацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НалогПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Налог) Тогда
		Объект.ПоказательПериода = "";
		АктуализированыРеквизитыПлатежаВБюджет = Ложь;
		НалогПриИзмененииНаСервере(АктуализированыРеквизитыПлатежаВБюджет, СтрокаПоискаПоКБК);
		
		Если АктуализированыРеквизитыПлатежаВБюджет Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Коды бюджетной классификации успешно обновлены'"),
				"e1cib/data/Справочник.ВидыНалоговИПлатежейВБюджет.Форма.ФормаСписка",
				НСтр("ru = 'Налоги и взносы'"),
				БиблиотекаКартинок.Информация32);
		КонецЕсли;
		
		ЗапуститьПроверкуФоновогоЗадания();
		
		УведомитьПользователяПлатежиВБюджет();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалогАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если СтрДлина(Текст) > 13 Тогда
		
		СтрокаПоискаПоКБК = СтрЗаменить(ПараметрыПолученияДанных.СтрокаПоиска, " ", "");
		
		Если СтрДлина(СтрокаПоискаПоКБК) <= 20 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаПоискаПоКБК) Тогда
			
			// Строка поиска похожа на КБК. Код включает код подвида доходов.
			ДанныеВыбора = ДанныеВыбораНалогаПоКБК(СтрокаПоискаПоКБК);
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалогОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если СтрДлина(Текст) > 13 Тогда
		
		СтрокаПоискаПоКБК = СтрЗаменить(ПараметрыПолученияДанных.СтрокаПоиска, " ", "");
		
		Если СтрДлина(СтрокаПоискаПоКБК) <= 20 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаПоискаПоКБК) Тогда
			
			// Строка поиска похожа на КБК. Код включает код подвида доходов.
			ДанныеВыбора = ДанныеВыбораНалогаПоКБК(СтрокаПоискаПоКБК);
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидНалоговогоОбязательстваПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Налог) И ЗначениеЗаполнено(Объект.ВидНалоговогоОбязательства) Тогда
		
		ВидаНалоговогоОбязательстваОбработатьИзменение(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НалогоплательщикПриИзменении(Элемент)
	
	НалогоплательщикПриИзмененииНеСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	СотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПриИзмененииНаСервере(Истина, Истина);
	
	ЗапуститьПроверкуФоновогоЗадания();
	
	УведомитьПользователяПлатежиВБюджет();
	
	// Если пользователь не изменил вид операции, но выбрал государственный орган,
	// то предложим ему изменить вид операции
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику") 
		И ЭтоГосударственныйОрганУплатыНалога(Объект.Контрагент) Тогда
		
		ТекстВопроса = СтрШаблон(НСтр("ru='Получатель ""%1"" является государственным органом.
			|
			|Изменить вид операции на ""Уплата налога""?'"), Объект.Контрагент);
			
		КнопкиОтвета = Новый СписокЗначений;
		КнопкиОтвета.Добавить(КодВозвратаДиалога.Да, "Да, это уплата налога");
		КнопкиОтвета.Добавить(КодВозвратаДиалога.Отмена, "Нет, это другой платеж");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросЭтоУплатаНалогаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, КнопкиОтвета);
		
	КонецЕсли;
	
	// СПАРК Риски
	ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеИндексыСПАРК", 0.1, Истина);
	// Конец СПАРК Риски
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКонтрагентаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
		
		СчетКонтрагентаПриИзмененииНаСервере(ПустаяСтрока(Объект.ИННПолучателя));
		
		ЗапуститьПроверкуФоновогоЗадания();
		
		УведомитьПользователяПлатежиВБюджет();
		
	Иначе
		
		СчетПолучателяБИКБанка = "";
		СчетПолучателяВБанкеРФ = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ДоговорПриИзмененииНаСервере(Ложь);
	
	УправлениеФормой(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(Объект.ДоговорКонтрагента),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(
		Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, ПредлагатьНовыйДоговор, СтандартнаяОбработка);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьРеквизитыПолучателяНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПлатежноеПоручениеФормыКлиент.ОткрытьФормуРедактированияРеквизитовПолучателя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	Объект.СуммаНДС = ПересчитатьСуммуНДС(Объект.СуммаДокумента, Объект.СтавкаНДС);
	
	Если Объект.СуммаПроцентов > Объект.СуммаДокумента Тогда
		Объект.СуммаПроцентов = Объект.СуммаДокумента;
	КонецЕсли;
	
	СформироватьНазначениеПлатежаНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	Объект.СуммаНДС = ПересчитатьСуммуНДС(Объект.СуммаДокумента, Объект.СтавкаНДС);
	СформироватьНазначениеПлатежаНаСервере(Истина);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаНДСПриИзменении(Элемент)
	
	СформироватьНазначениеПлатежаНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьРеквизитыПлатежейВБюджетНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПлатежноеПоручениеФормыКлиент.ОткрытьФормуРедактированияРеквизитовНаУплатуНалога(
		ЭтотОбъект, ОповещениеОбработкаРедактированияРеквизитовПлатежаВБюджетЗавершение());
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеПлатежаПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.НазначениеПлатежа) Тогда
		НазначениеПлатежаБылоИзмененоВручную = Ложь;
	Иначе
		НазначениеПлатежаБылоИзмененоВручную = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчередностьПлатежаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСледующийПлатежНажатие(Элемент, СтандартнаяОбработка)
	
	Если Объект.Организация.Пустая() Тогда
		// Организация, по которой создается регулярный платеж, задается в форме документа и не может быть пустой.
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Организация'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Организация", "Объект");
	
	ИначеЕсли Объект.Ссылка.Пустая()  Тогда
		// Если редактируем правило по новому документу - запишем документ
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
				|Выполнение команды ""Расписание повторения платежа"" возможно только после записи данных.
				|Данные будут записаны.'");
					
		Оповещение = Новый ОписаниеОповещения("ВопросРедактированиеПравилаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
	Иначе
		
		ОткрытьПравилоРегулярногоПлатежа();
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКонтрагентаExtendedTooltipОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "e1cib/app/Документ.ПлатежноеПоручение.Форма.ФормаПроверкиПлатежныхРеквизитов" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ПлатежныеРеквизиты", ПлатежныеРеквизиты());
		ПараметрыФормы.Вставить("Контрагент",         Объект.Контрагент);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СчетКонтрагентаExtendedTooltipОбработкаНавигационнойСсылкиЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.ПлатежноеПоручение.Форма.ФормаПроверкиПлатежныхРеквизитов",
			ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,,, ОписаниеОповещения);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "e1cib/app/Обработка.ИнтернетПоддержкаПользователей.Форма.ОбщаяАвторизация" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПлатежныеРеквизиты", ЭтотОбъект);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РеестрВыплатСамозанятымПриИзменении(Элемент)
	
	РеестрВыплатСамозанятымПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежнаяВедомостьОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		Элемент = ПредопределенноеЗначение("Документ.ВедомостьНаВыплатуЗарплаты.ПустаяСсылка");
	Иначе
		Элемент = ПредопределенноеЗначение("Документ.ВедомостьНаВыплатуЗарплатыВКассу.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежнаяВедомостьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",  Объект.Организация);
		ПараметрыФормы.Вставить("ВидМестаВыплаты", ПредопределенноеЗначение("Перечисление.ВидыМестВыплатыЗарплаты.БанковскийСчет"));
		ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплаты.ФормаВыбора",
			Новый Структура("ПараметрыОтбораСписка, ТекущаяСтрока", ПараметрыФормы, Объект.ПлатежнаяВедомость), Элемент);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ОткрытьФорму("Документ.ВедомостьНаВыплатуЗарплатыВБанк.ФормаВыбора",
			Новый Структура("Отбор, ТекущаяСтрока", ПараметрыФормы, Объект.ПлатежнаяВедомость), Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежнаяВедомостьПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Контрагент) И ЗначениеЗаполнено(Объект.ПлатежнаяВедомость) Тогда
		Объект.СуммаДокумента = СуммаВыплаченнойЗарплатыРаботнику(
			Объект.Ссылка, Объект.Контрагент, Объект.ПлатежнаяВедомость, УчетЗарплатыИКадровВоВнешнейПрограмме);
	Иначе
		Объект.СуммаДокумента = 0;
	КонецЕсли;
	ПлатежнаяВедомостьПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительныйЛистПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Сотрудник)
		И ЗначениеЗаполнено(Объект.Контрагент)
		И ЗначениеЗаполнено(Объект.ИсполнительныйЛист) Тогда
		
		Если Объект.СуммаДокумента = 0 Тогда 
			Объект.СуммаДокумента = СуммаУдержанияРаботника(
				Объект.Ссылка, Объект.Организация, Объект.Контрагент, Объект.Сотрудник, Объект.ИсполнительныйЛист);
		КонецЕсли;
	КонецЕсли;
	ИсполнительныйЛистПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречислениеНаЛичныйСчетПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
	Если ПеречислениеНаЛичныйСчет Тогда
		Объект.Банк = "";
	КонецЕсли;
	
	КонтрагентПриИзмененииНаСервере(Ложь);
	
	НастроитьСписокВыбораКодВидаДохода(ЭтотОбъект);
	КодВидаДоходаПоУмолчанию(ЭтотОбъект);
	
	ЗапуститьПроверкуФоновогоЗадания();
	УведомитьПользователяПлатежиВБюджет();
	
КонецПроцедуры

&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	
	КонтрагентПриИзмененииНаСервере();
	
	ЗапуститьПроверкуФоновогоЗадания();
	
	УведомитьПользователяПлатежиВБюджет();
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПроверкаРеквизитовОрганизацииКлиент.РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Объект.Организация, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПроцентовПриИзменении(Элемент)
	
	Если Объект.СуммаПроцентов > Объект.СуммаДокумента Тогда
		Объект.СуммаПроцентов = Объект.СуммаДокумента;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстСостояниеЗагрузкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИнтеграцияСБанкамиФормыКлиент.ОбработкаНавигационнойСсылкиБаннера(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПредупреждениеОЗаполненииРеквизитовПлатежаНПДОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	БанкИКассаФормыКлиент.ОбработатьСсылкуБаннераПредупреждениеОЗаполненииРеквизитовПлатежаНПД(
		Объект.Организация, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипКонтрагентаПриИзменении(Элемент)
	
	ТипКонтрагентаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипКонтрагентаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПриИзменении(Элемент)
	
	СостояниеПриИзмененииНаСервере();
	
	// СПАРК Риски
	ИндексыСПАРКРиски = Неопределено;
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеИндексыСПАРК", 0.1, Истина);
	// Конец СПАРК Риски
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьРеквизитыПлательщикаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПлатежноеПоручениеФормыКлиент.ОткрытьФормуРедактированияРеквизитовПлательщика(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьДокументСписанияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Параметры.Ключ) ИЛИ Модифицированность Тогда
		ОбъектЗаписан = Записать();
		
		Если НЕ ОбъектЗаписан Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументСписания) Тогда
		ПоказатьЗначение(, ДокументСписания);
	ИначеЕсли НЕ ТолькоПросмотр Тогда
		Если Объект.ПометкаУдаления Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Нельзя вводить оплату на основании документа, помеченного на удаление!'"));
			Возврат;
		КонецЕсли;
		
		ПараметрыСписания = Новый Структура("Основание", Параметры.Ключ);
		ОткрытьФорму("Документ.СписаниеСРасчетногоСчета.ФормаОбъекта", ПараметрыСписания, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчередностьПлатежаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗаголовокПричиныОтказаБанкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
						
	СтандартнаяОбработка = Ложь;
	ПоказатьПредупреждение(,ПричинаОтклоненияБанком);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстПредупрежденияСчетОрганизацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	БанковскиеСчетаФормыКлиент.ОткрытьСписокСчетовОрганизации(Объект.Организация, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИндексыСПАРКРискиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СПАРКРискиКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержаниеПоИсполнительномуЛистуПриИзменении(Элемент)
	
	Если ПустаяСтрока(УдержаниеПоИсполнительномуЛисту)
		Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(КодИзПредставления(УдержаниеПоИсполнительномуЛисту)) Тогда
		Объект.КодВидаДохода = "";
		Возврат;
	КонецЕсли;
	
	ЭлементСписка = Элементы.УдержаниеПоИсполнительномуЛисту.СписокВыбора.НайтиПоЗначению(
		УдержаниеПоИсполнительномуЛисту);
	Если ЭлементСписка <> Неопределено Тогда
		Объект.КодВидаДохода = КодИзПредставления(УдержаниеПоИсполнительномуЛисту);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдержаниеПоИсполнительномуЛистуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если УчетДенежныхСредствКлиентСервер.ВступилиВСилуИзменения353ФЗ(Объект.Дата) Тогда
		Период = '20220101';
	Иначе
		Период = '20210101';
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",      "Документ");
	ПараметрыФормы.Вставить("НазваниеОбъекта", "ПлатежноеПоручение");
	ПараметрыФормы.Вставить("НазваниеМакета",  "КодВидаДохода");
	ПараметрыФормы.Вставить("ТекущийПериод",   Период);
	ПараметрыФормы.Вставить("ТекущийКод",      Объект.КодВидаДохода);
	ПараметрыФормы.Вставить("ОформлениеФормы", ОформлениеФормыВыбораКодаВидаДохода());
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборКодаЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы, , , , , ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВалютнойОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьКодИзПеречняКВВО();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки =
		ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ИмяСобытияВызовСервиса() Тогда
		СтандартнаяОбработка = Ложь;
		ИННСамозанятого = РаботаСДоговорамиКонтрагентовБПВызовСервера.ИННВладельца(Объект.ДоговорКонтрагента);
		ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБПКлиент.ДатаПолученияСтатуса(Объект.Дата);
		Если ИнтеграцияСтатусСамозанятогоКлиент.НачатьПолучениеСтатусаСамозанятого(
			Объект.ДоговорКонтрагента, ЭтотОбъект, ИННСамозанятого, ДатаПолученияСтатуса) Тогда
			Элемент.Заголовок = ИнтеграцияСтатусСамозанятогоФормыБПКлиент.ЗаголовокПодсказкиПолучениеСтатусаВПроцессе();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОплатыПоРееструПриИзменении(Элемент)
	
	Объект.Контрагент = Неопределено;
	ВидОплатыПоРееструПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	ПараметрыЗаписи.Вставить("Закрыть",     Истина);
	
	Если Записать(ПараметрыЗаписи) Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДосьеКонтрагента(Команда)
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПараметрыФормы = Новый Структура("Контрагент", Объект.Контрагент);
		ОткрытьФорму("Отчет.ДосьеКонтрагента.Форма", ПараметрыФормы);
	Иначе
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,,НСтр("ru = 'Получатель'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Объект.Контрагент");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочиеПлатежиВБюджет(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РежимВыбора", Истина);
	
	СписокОрганизаций   = Новый СписокЗначений;
	СписокОрганизаций.Добавить(Объект.Организация);
	СписокОрганизаций.Добавить(ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	
	СтруктураОтбора     = Новый Структура("Организация", СписокОрганизаций);
	СтруктураПараметров.Вставить("Отбор", СтруктураОтбора);
	
	ОткрытьФорму("РегистрСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.Форма.ФормаВыбора", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПлатежногоПоручения(Команда)
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.СчетОрганизации) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Банковский счет'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "СчетОрганизации", "Объект", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Счет получателя'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "СчетКонтрагента", "Объект", Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// См. Документы.ПлатежноеПоручение.НовыеПараметрыНазначенияПлатежа();
	ПараметрыНазначенияПлатежа = Новый Структура(
		"Организация, Налогоплательщик, Дата, ВидОперации,
		|Налог, ВидНалоговогоОбязательства,
		|Контрагент, ПеречислениеВБюджет, ПлатежнаяВедомость, СтатусСоставителя, РеестрВыплатСамозанятым,
		|СчетКонтрагента, ДоговорКонтрагента, КПППлательщика, РеестрВыплатСамозанятым,
		|КодБК, ДокументОснование, ПоказательПериода,
		|НачислениеДивидендов,
		|ПеречислениеПоИсполнительномуЛисту, Сотрудник, ИсполнительныйЛист, КодВалютнойОперации");
	ЗаполнитьЗначенияСвойств(ПараметрыНазначенияПлатежа, Объект);
	
	АвтоТекстНазначения = АвтоТекстНазначенияПлатежа(ПараметрыНазначенияПлатежа, ШаблонНазначенияПлатежаВБюджет);
	
	ПараметрыФормы = Новый Структура(
		"БанковскийСчетОрганизации, БанковскийСчетКонтрагента, АвтоТекстНазначения, ОткрытИзПлатежки",
		Объект.СчетОрганизации, Объект.СчетКонтрагента, АвтоТекстНазначения, Истина);
	
	ОткрытьФорму("ОбщаяФорма.НастройкиПлатежныхПорученийТребований", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СогласованиеПлатежей

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваГруппыСогласованияПлатежей(Форма)
	
	Если Не Форма.ИспользуетсяСогласованиеПлатежей Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Элементы.ГруппаСогласование.Видимость = ЗначениеЗаполнено(Форма.Состояние);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваИнформацииСогласования()
	
	Если Не ЭтотОбъект.ИспользуетсяСогласованиеПлатежей Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Утвердил) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПользователя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ЭтотОбъект.Утвердил, "Наименование, ИдентификаторПользователяИБ");
	Если Не ЗначениеЗаполнено(РеквизитыПользователя.ИдентификаторПользователяИБ) Тогда
		Элементы.НадписьСогласовано.Заголовок = "";
	Иначе
		ФИО = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(РеквизитыПользователя.Наименование);
		
		Элементы.НадписьСогласовано.Заголовок = СтрШаблон("%1 %2",
			?(ЭтотОбъект.Согласовано, НСтр("ru = 'Согласовано:'"), НСтр("ru = 'Не согласовано:'")), ФИО);
	КонецЕсли;
	
	Элементы.НадписьДокументСписания.Видимость =
		Не ПодключенаИнтеграцияСБанком И Согласовано Или ЗначениеЗаполнено(ДокументСписания);
	
КонецПроцедуры

#КонецОбласти

#Область ОшибкиПлатежейВБюджет

&НаКлиенте
Процедура Подключаемый_ПредупреждениеОбОшибке()
	
	УведомитьПользователяПлатежиВБюджет();
	
КонецПроцедуры

&НаКлиенте
Процедура УведомитьПользователяПлатежиВБюджет()
	
	ОписаниеОповещения = ОповещениеОбработкаРедактированияРеквизитовПлатежаВБюджетЗавершение();
	ПлатежноеПоручениеФормыКлиент.УведомитьПользователяПлатежиВБюджет(ЭтотОбъект, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Функция ОповещениеОбработкаРедактированияРеквизитовПлатежаВБюджетЗавершение()
	
	Возврат Новый ОписаниеОповещения("ОбработкаРедактированияРеквизитовПлатежаВБюджетЗавершение", ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область ОбластьРаботаСРегулярнымиПлатежами

&НаСервереБезКонтекста
Функция ДатаСледующегоПлатежа(ПравилоРегулярногоПлатежа, ПериодСобытия)
	
	Если ЗначениеЗаполнено(ПравилоРегулярногоПлатежа) 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПравилоРегулярногоПлатежа, "Выполняется") Тогда
		
		Возврат Справочники.ПравилаРегулярныхПлатежей.ДатаБлижайшегоПлатежа(ПравилоРегулярногоПлатежа, ПериодСобытия);
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьПравилоРегулярногоПлатежа()
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРегулярныхПлатежей = Новый Структура;
	ПараметрыРегулярныхПлатежей.Вставить("ДатаДокумента", Объект.Дата);
	ПараметрыРегулярныхПлатежей.Вставить("ПериодСобытия", ПериодСобытияРегулярногоПлатежа);
	
	Если ЗначениеЗаполнено(ПравилоРегулярногоПлатежа) Тогда
		
		ПараметрыРегулярныхПлатежей.Вставить("Ключ", ПравилоРегулярногоПлатежа);
		
	Иначе
		ЗначенияЗаполнения = Новый Структура;
		
		ЗначенияЗаполнения.Вставить("Организация",      Объект.Организация);
		ЗначенияЗаполнения.Вставить("Периодичность",    ПредопределенноеЗначение("Перечисление.Периодичность.Месяц"));
		ЗначенияЗаполнения.Вставить("НачалоВыполнения", ДобавитьМесяц(Объект.Дата, 1));
		ЗначенияЗаполнения.Вставить("Выполняется",      Истина);
		
		ПараметрыРегулярныхПлатежей.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ПравилаРегулярныхПлатежей.ФормаОбъекта", ПараметрыРегулярныхПлатежей, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПравилоРегулярногоПлатежаНаСервере(ПлатежноеПоручение)
	
	ЗаписьПоПлатежномуПоручению = РегистрыСведений.РегулярныеПлатежи.ЗаписьПравилаПоПлатежномуПоручению(ПлатежноеПоручение);
	
	Если ЗаписьПоПлатежномуПоручению <> Неопределено Тогда
		ПравилоРегулярногоПлатежа       = ЗаписьПоПлатежномуПоручению.Правило;
		ПериодСобытияРегулярногоПлатежа = ЗаписьПоПлатежномуПоручению.ПериодСобытия;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьПравилоПоДокументу(Организация, ПлатежноеПоручение, ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа)
	
	Возврат РегистрыСведений.РегулярныеПлатежи.ДобавитьЗаписьПравила(Организация, ПлатежноеПоручение, ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НадписьСледующийПлатеж(ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа)
	
	ДатаСледующегоПлатежа = ДатаСледующегоПлатежа(ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа);
	
	Если ЗначениеЗаполнено(ДатаСледующегоПлатежа) Тогда
		
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Следующий платеж %1'"),
			Формат(ДатаСледующегоПлатежа, "ДЛФ=D"));
	
	Иначе
		
		Возврат НСтр("ru = 'Повторять платеж?'");
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбластьЗадачиБухгалтера

&НаКлиенте
Процедура ОбновитьЗадачиБухгалтера()
	
	ДлительнаяОперация = ОбновитьЗадачиБухгалтераНаСервере(Объект.Организация, УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьЗадачиБухгалтераЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбновитьЗадачиБухгалтераНаСервере(Организация, УникальныйИдентификаторФормы)
	
	// Обеспечиваем актуальность в дальнейшем.
	КалендарьБухгалтера.ЗапланироватьОбновлениеЗадачБухгалтера();
	
	РезультатВыполнения = КалендарьБухгалтера.ЗапуститьЗаполнениеВФоне(УникальныйИдентификаторФормы, Организация, Ложь, Ложь, Истина);
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьЗадачиБухгалтераЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если ЗначениеЗаполнено(Результат) И Результат Тогда
		
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ЗадачиБухгалтера"));
		Оповестить("ПериодическиеПлатежи_Изменение");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ПлатежноеПоручениеФормы.УстановитьФункциональныеОпцииФормы(ЭтотОбъект);
	
	// Установка реквизитов формы
	УчетЗарплатыИКадровВоВнешнейПрограмме = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
	ВедетсяУчетРасчетовПоЗарплатеСводно   = ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно");
	ИспользуетсяСогласованиеПлатежей      = ПолучитьФункциональнуюОпцию("ИспользоватьСогласованиеПлатежей");
	ВидОперацииДоИзменения                = Объект.ВидОперации;
	ЕстьПравоПросмотраДосье               = ПравоДоступа("Просмотр", Метаданные.Отчеты.ДосьеКонтрагента);
	ОсновнойБанковскийСчетОрганизацииЗаполнен = ПроверкаРеквизитовОрганизации.ОсновнойБанковскийСчетОрганизацииЗаполнен(Объект.Организация);
	
	ПодключенаИнтеграцияСБанком = ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВключена(Объект.СчетОрганизации);
	
	БанкИКассаФормы.ПоказатьБаннерПредупреждениеОЗаполненииРеквизитовПлатежаНПД(ЭтотОбъект);
	
	ПредыдущаяДата = Объект.Дата;
	
	ПлатежГосОргану  = УчетДенежныхСредствБП.ПлатежГосОргану(Объект.ВидОперации, Объект.Контрагент);
	ПереводМеждуСчетами = УчетДенежныхСредствКлиентСервер.СписаниеСредствНаДругойСчет(Объект.ВидОперации);
	ПеречислениеФизическомуЛицу = УчетДенежныхСредствКлиентСервер.РасчетыСФизическимиЛицами(Объект.ВидОперации)
		И Объект.ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ЛичныеСредстваПредпринимателя;
	ПеречислениеНаЛичныйСчет = ПеречислениеФизическомуЛицу И НЕ ЗначениеЗаполнено(Объект.Банк);
	УплатаНалогаЗаТретьихЛиц = Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц;
	
	ПлатежноеПоручениеФормы.ОпределитьОрганизацияЮрФизЛицо(ЭтотОбъект);
	БанкИКассаФормы.ОпределитьКонтрагентЮрФизЛицо(ЭтотОбъект);
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым Тогда
		ВидОплатыПоРеестру = ВидОплатыСамозанятымПоРеестру(КонтрагентЮрФизЛицо);
	КонецЕсли;
	
	АвтоЗначенияРеквизитов = УчетДенежныхСредствБП.СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя(
		?(УплатаНалогаЗаТретьихЛиц, Объект.Налогоплательщик, Объект.Организация),
		Объект.СчетОрганизации,
		?(ПереводМеждуСчетами, Объект.Организация, ?(ПеречислениеФизическомуЛицу И НЕ ПеречислениеНаЛичныйСчет, Объект.Банк, Объект.Контрагент)),
		Объект.СчетКонтрагента,
		ПлатежГосОргану,
		Объект.Дата);
	
	ПараметрыНазначенияПлатежа = Документы.ПлатежноеПоручение.НовыеПараметрыНазначенияПлатежа();
	ЗаполнитьЗначенияСвойств(ПараметрыНазначенияПлатежа, Объект);
	ТекстНазначенияАвто = АвтоТекстНазначенияПлатежа(ПараметрыНазначенияПлатежа, ШаблонНазначенияПлатежаВБюджет);
	
	АвтоЗначенияРеквизитов.Вставить("ТекстНазначенияПлатежа", ТекстНазначенияАвто);
	НазначениеПлатежаБылоИзмененоВручную = Документы.ПлатежноеПоручение.НазначениеПлатежаИзмененоВручную(
		Объект.НазначениеПлатежа, ТекстНазначенияАвто);
	
	НадписьРеквизитыПлательщика     = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
	НадписьРеквизитыПолучателя      = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
	НадписьРеквизитыПлатежейВБюджет = ПлатежноеПоручениеФормыКлиентСервер.НадписьРеквизитыПлатежейВБюджет(ЭтотОбъект);
	НадписьСледующийПлатеж          = НадписьСледующийПлатеж(ПравилоРегулярногоПлатежа, ПериодСобытияРегулярногоПлатежа);
	
	ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Налог, "ВидНалога");
	
	НалогУплачиваетсяОтдельно = ЕдиныйНалоговыйСчетВызовСервера.НалогУплачиваетсяОтдельно(Объект.Организация,
		Объект.Дата,
		Объект.Налог,
		Объект.Контрагент);
	
	ДокументСписания = Документы.ПлатежноеПоручение.НайтиДокументСписания(Объект.Ссылка, Объект.ДокументОснование, НадписьДокументСписания);
	
	ПлатежноеПоручениеФормы.УстановитьОграничениеТипаКонтрагента(ЭтотОбъект);
	ПлатежноеПоручениеФормы.УстановитьПараметрыВыбораБанковскихСчетов(ЭтотОбъект);
	ПлатежноеПоручениеФормы.УстановитьСостояниеДокумента(ЭтотОбъект);
	
	УстановитьПараметрыВыбораДоговораСКонтрагентом(ЭтотОбъект);
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") Тогда
		ПредлагатьНовыйДоговор = РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент);
	КонецЕсли;
	
	ЗаполнитьВидНалоговогоОбязательстваНаСервере(ЭтотОбъект, Истина);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПолучитьСостояниеОбмена(Объект.Ссылка));
	УстановитьСвойстваИнформацииСогласования();
	ОбработатьИПоказатьСостоянияОтправки();
	
	ПлатежноеПоручениеФормы.ЗаполнитьРеквизитыСчетаПолучателя(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ИспользоватьНесколькоБанковскихСчетовОрганизации =
			Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Объект.Организация);
	Иначе
		ИспользоватьНесколькоБанковскихСчетовОрганизации = Истина;
	КонецЕсли;
	
	ЗаполнитьОшибкуСчетаОрганизации();
	
	ЦветОшибкиБанковскогоСчета = ЦветаСтиля.ЦветОшибкиБанковскогоСчета;
	ЦветОтсутствияОшибок = ЦветаСтиля.ЦветФонаФормы;
	
	НадежностьБанковКлиентСервер.ПолучитьИнформациюНадежностьБанков(ЭтотОбъект, 
		СчетПолучателяБИКБанка, 
		ПоказыватьИнформациюНадежностьБанков(ЭтотОбъект));
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения
	ПоказатьИндексыСПАРК = ПоказатьИнформациюСпарк(ЭтотОбъект);
	ВидКонтрагентаСПАРК = СПАРКРискиКлиентСерверБП.ВидКонтрагентаСПАРКРиски(
		?(ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.Контрагенты"),  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ЮридическоеФизическоеЛицо"), Неопределено));
	Если ПоказатьИндексыСПАРК Тогда
		ПараметрыПроцедуры = Новый Структура("ВариантОтображения", "Однострочный");		
		СПАРКРиски.ПриСозданииНаСервере(
			ЭтотОбъект,
			Неопределено,
			Объект.Контрагент,
			ВидКонтрагентаСПАРК,
			ПараметрыПроцедуры);
	Иначе
		Элементы.ГруппаИндексыСПАРКРиски.Видимость = Ложь;
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
	ИзменитьТекстПредупрежденияЕНП(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьТекстПредупрежденияЕНП(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ПлатежиВБюджетКлиентСервер.ДействуетПереходныйПериодЕНП(Объект.Дата) Тогда
		Элементы.ДекорацияПредупреждениеОбУплатеНалогаПриЕНП.Заголовок =
			НСтр("ru = 'Уплата налогов и страховых взносов должна осуществляться по реквизитам единого налогового платежа.'");
	Иначе
		Элементы.ДекорацияПредупреждениеОбУплатеНалогаПриЕНП.Заголовок =
			НСтр("ru = 'Внимание! С 01.01.2024 уплата налогов и страховых взносов осуществляется только по реквизитам единого налогового платежа.'")
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ПеречислениеЗарплаты               = Ложь;
	ПереводМеждуСчетами                = Ложь;
	ПеречислениеКонтрагенту            = Ложь;
	ПеречислениеКредитаЗайма           = Ложь;
	УплатаНалога                       = Ложь;
	УплатаНалогаЗаТретьихЛиц           = Ложь;
	ПеречислениеДивидендов             = Ложь;
	ЭтоВыплатыСамозанятым              = Ложь;
	ЭтоУплатаЕдиногоНалоговогоПлатежа  = Ложь;
	
	ВозможноПеречислениеПособияНаКартуМир = ВозможноПеречислениеПособияНаКартуМир(
		Объект.ВидОперации, Форма.КонтрагентЮрФизЛицо);
	ВозможноВзысканиеСПеречисленияФизлицу = ВозможноВзысканиеСПеречисленияФизлицу(
		Объект.Дата, Объект.ВидОперации, Форма.КонтрагентЮрФизЛицо);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога") Тогда
		УплатаНалога = Истина;
		ЭтоУплатаЕдиногоНалоговогоПлатежа = ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоЕдиныйНалоговыйПлатеж(Форма.ВидНалога);
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц") Тогда
		УплатаНалогаЗаТретьихЛиц = Истина;
	ИначеЕсли УчетДенежныхСредствКлиентСервер.ПереводДенегВнутриКомпании(Объект.ВидОперации) Тогда
		ПереводМеждуСчетами = Истина;
	ИначеЕсли УчетДенежныхСредствКлиентСервер.РасчетыСФизическимиЛицами(Объект.ВидОперации) Тогда
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику") Тогда
			Если Форма.УчетЗарплатыИКадровВоВнешнейПрограмме И Форма.ВедетсяУчетРасчетовПоЗарплатеСводно Тогда
				ПеречислениеЗарплаты = Ложь;
			Иначе
				ПеречислениеЗарплаты = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым") Тогда
		ЭтоВыплатыСамозанятым = Истина;
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВозвратКредита")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВыдачаЗаймаКонтрагенту") 
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.РасчетыПоКредитамИЗаймам") Тогда
		ПеречислениеКонтрагенту     = Истина;
		ПеречислениеКредитаЗайма    = Истина;
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДивидендов") Тогда
		ПеречислениеДивидендов = Истина;
	ИначеЕсли Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП")
		И Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание")
		И Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда")
		И Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПоИсполнительномуЛистуРаботника") Тогда
		ПеречислениеКонтрагенту = Истина;
	КонецЕсли;
	
	Элементы.КодВыплат.Видимость                       = ВозможноПеречислениеПособияНаКартуМир;
	Элементы.УдержаниеПоИсполнительномуЛисту.Видимость = ВозможноВзысканиеСПеречисленияФизлицу;
	
	Элементы.ПеречислениеНаЛичныйСчет.Видимость = Форма.ПеречислениеФизическомуЛицу;
	Элементы.Банк.Видимость                     = Форма.ПеречислениеФизическомуЛицу И НЕ Форма.ПеречислениеНаЛичныйСчет;
	Если Форма.ПеречислениеФизическомуЛицу Тогда
		Элементы.Контрагент.Заголовок = НСтр("ru = 'Сотрудник'");
	Иначе
		Элементы.Контрагент.Заголовок = НСтр("ru = 'Получатель'");
	КонецЕсли;
	Элементы.РеестрВыплатСамозанятым.Видимость = ЭтоВыплатыСамозанятым;
	
	Элементы.ПлатежнаяВедомость.Видимость   = ПеречислениеЗарплаты;
	Элементы.Контрагент.Доступность         = НЕ ПереводМеждуСчетами;
	Элементы.СчетКонтрагента.Доступность    = ПереводМеждуСчетами
		ИЛИ НЕ Форма.ПеречислениеФизическомуЛицу И ЗначениеЗаполнено(Объект.Контрагент)
		ИЛИ (Форма.ПеречислениеФизическомуЛицу И НЕ Форма.ПеречислениеНаЛичныйСчет И ЗначениеЗаполнено(Объект.Банк)
			ИЛИ Форма.ПеречислениеНаЛичныйСчет И ЗначениеЗаполнено(Объект.Контрагент));
	
	Если ЭтоЦифровойСчет(Объект.СчетОрганизации) Или ЭтоЦифровойСчет(Объект.СчетКонтрагента) Тогда
		ЭтоПереводНаДругойСчет = Объект.ВидОперации =
			ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет");
		Если ЭтоПереводНаДругойСчет Тогда
			Элементы.СчетКонтрагента.АвтоОтметкаНезаполненного = Неопределено;
		Иначе
			Элементы.СчетКонтрагента.АвтоОтметкаНезаполненного = ЭтоПереводНаДругойСчет;
			Элементы.СчетКонтрагента.ОтметкаНезаполненного     = ЭтоПереводНаДругойСчет;
		КонецЕсли;
		
		Элементы.ИдентификаторОперацииЦР.Видимость         = Истина;
		Элементы.ВидПлатежа.Видимость                      = Ложь;
		Элементы.ГруппаОчередностьПлатежа.Видимость        = Ложь;
	Иначе
		Элементы.СчетКонтрагента.АвтоОтметкаНезаполненного = Неопределено;
		Элементы.ВидПлатежа.Видимость                      = Истина;
		Элементы.ГруппаОчередностьПлатежа.Видимость        = Истина;
		Элементы.ИдентификаторОперацииЦР.Видимость         = Ложь;
	КонецЕсли;
	
	Элементы.ДоговорКонтрагента.Видимость   = ПеречислениеКонтрагенту;
	Элементы.ДоговорКонтрагента.Доступность = ЗначениеЗаполнено(Объект.Контрагент);
	
	Элементы.СуммаПроцентов.Видимость       = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВозвратКредита");
	Элементы.СтавкаНДС.Видимость            = ПеречислениеКонтрагенту И НЕ ПеречислениеКредитаЗайма;
	Элементы.СуммаНДС.Видимость             = ПеречислениеКонтрагенту И НЕ ПеречислениеКредитаЗайма;
	Элементы.СуммаНДС.Доступность           = Объект.СтавкаНДС <> ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС")
		И Объект.СтавкаНДС <> ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0") И ПеречислениеКонтрагенту И НЕ ПеречислениеКредитаЗайма;
	
	Элементы.ГруппаПеречислениеНалога.Видимость     = УплатаНалога;
	Элементы.ВидНалоговогоОбязательства.Видимость   = Не ЭтоУплатаЕдиногоНалоговогоПлатежа;
	Элементы.ВидНалоговогоОбязательства.Доступность = ЗначениеЗаполнено(Объект.Налог);
	
	Элементы.ГруппаПеречислениеНалогаЗаТретьихЛиц.Видимость = УплатаНалогаЗаТретьихЛиц;
	
	Элементы.ГруппаПеречислениеПоИсполнительномуЛистуСотрудника.Видимость = Объект.ПеречислениеПоИсполнительномуЛисту;
	Элементы.ИсполнительныйЛист.Видимость = Объект.ПеречислениеПоИсполнительномуЛисту;
	
	Элементы.ГруппаРеквизитыПлатежейВБюджет.Видимость = Форма.ПлатежГосОргану;
	
	Элементы.ТипКонтрагента.Видимость             = ПеречислениеДивидендов;
	Элементы.ГруппаНачислениеДивидендов.Видимость = ПеречислениеДивидендов;
	Если ПеречислениеДивидендов Тогда
		Элементы.Контрагент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Иначе
		Элементы.Контрагент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	КонецЕсли;
	
	Элементы.ПредупреждениеОбУплатеНалогаПриЕНП.Видимость = УплатаНалога
		И ЗначениеЗаполнено(Объект.Налог) И Не Форма.НалогУплачиваетсяОтдельно
		И ЗначениеЗаполнено(Объект.КодБК)
		И ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(Объект.КодБК);
	
	Элементы.ГруппаПредупреждениеПриПопыткеОплатитьНалогАУСН.Видимость = Форма.ПрименяетсяАУСН;
	Элементы.ДекорацияПредупреждениеПопыткаОплатитьНалогАУСН.Видимость = ЭтоУплатаАУСН(УплатаНалога, Объект.Налог);
	Элементы.ДекорацияПредупреждениеПопыткаОплатитьВзносАУСН.Видимость = ЭтоУплатаВзносаПриАУСН(УплатаНалога, Объект.Налог);
	
	Если Объект.Дата >= ПлатежиВБюджетКлиентСервер.НачалоПримененияИдентификатораПлатежа() Тогда
		Элементы.ИдентификаторПлатежа.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Авто;
		Элементы.ИдентификаторПлатежа.ПредупреждениеПриРедактировании            = "";
	Иначе
		Элементы.ИдентификаторПлатежа.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Отображать;
		Элементы.ИдентификаторПлатежа.ПредупреждениеПриРедактировании            =
			НСтр("ru = 'До 31.03.2014 идентификатор платежа используется только для платежей в бюджет'");
	КонецЕсли;
	
	// Отображение кнопки "Досье".
	Если Форма.ЕстьПравоПросмотраДосье И ПеречислениеКонтрагенту И НЕ ПеречислениеКредитаЗайма Тогда
		Элементы.ДосьеКонтрагента.Видимость = Истина;
		Элементы.Контрагент.Ширина = 33;
		Элементы.Контрагент.РастягиватьПоГоризонтали = Ложь;
	Иначе
		Элементы.ДосьеКонтрагента.Видимость = Ложь;
		Элементы.Контрагент.Ширина = 0;
		Если ПеречислениеДивидендов Тогда
			Элементы.Контрагент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			Элементы.Контрагент.АвтоМаксимальнаяШирина = Ложь;
			Элементы.Контрагент.МаксимальнаяШирина = 24;
		Иначе
			Элементы.Контрагент.РастягиватьПоГоризонтали = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ПлатежноеПоручениеФормыКлиентСервер.ОбновитьПодсказкуОчередностьПлатежа(Форма, Объект.ОчередностьПлатежа);
	
	УстановитьСвойстваПоляСостояние(Форма);  
	УстановитьСвойстваГруппыСогласованияПлатежей(Форма);
	
	ОтобразитьПредупрежденияОбОшибкахСчетаОрганизации(Форма);
	
	НовыеПараметры = Новый Массив;
	НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.ПрименяетсяОсобыйПорядокУплатыНалога", Форма.ПлательщикЕНП));
	НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.Период", НачалоДня(Объект.Дата)));
	Форма.Элементы.Налог.ПараметрыВыбора = Новый ФиксированныйМассив(НовыеПараметры);
	
	УправлениеЭлементамиВалютногоКонтроля(Форма);
	
	СтатусСамозанятого = Форма.СтатусСамозанятого;
	Если СтатусСамозанятого <> Неопределено Тогда
		ОформлениеСтатусаНПД =
			ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ДанныеДляОформленияСтатусаСамозанятогоНаФорме(
				СтатусСамозанятого.ПлательщикНПД, СтатусСамозанятого.Описание, СтатусСамозанятого.ТекстОшибки);
		Элементы.ГруппаДоговорКонтрагента.ЦветФона = ОформлениеСтатусаНПД.ЦветФона;
		Элементы.ДоговорКонтрагентаРасширеннаяПодсказка.Заголовок = ОформлениеСтатусаНПД.ЗаголовокПодсказки;
		
		Если ЗначениеЗаполнено(СтатусСамозанятого.КраткоеОписание) Тогда
			Элементы.ДекорацияПредупреждениеСтатусаСамозанятого.Заголовок = СтатусСамозанятого.КраткоеОписание;
			Элементы.ДекорацияПредупреждениеСтатусаСамозанятого.Видимость = Истина;
		Иначе
			Элементы.ДекорацияПредупреждениеСтатусаСамозанятого.Заголовок = "";
			Элементы.ДекорацияПредупреждениеСтатусаСамозанятого.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Элементы.ГруппаДоговорКонтрагента.ЦветФона = Новый Цвет;
		Элементы.ДоговорКонтрагентаРасширеннаяПодсказка.Заголовок = "";
		Элементы.ДекорацияПредупреждениеСтатусаСамозанятого.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ВидОплатыПоРеестру.Видимость = Объект.ВидОперации =
		ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым");
	
КонецПроцедуры

// Заполняет список выбора вида налогового обязательства
//
// Параметры:
//   ДобавитьТекущееЗначениеПриОтсутствии - Булево - Истина, если при отсутствии в списке текущего значения реквизита,
//     необходимо его добавить. Например, при создании формы или в случаях, когда пользователь явно указал
//     некорректный вид обязательства.
//
&НаСервереБезКонтекста
Процедура ЗаполнитьВидНалоговогоОбязательстваНаСервере(Форма, ДобавитьТекущееЗначениеПриОтсутствии = Ложь)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Объект.ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога Тогда
		Возврат;
	КонецЕсли;
	
	СписокВидовНалоговыхОбязательств = Перечисления.ВидыПлатежейВГосБюджет.СписокВидовНалоговыхОбязательств(Объект.Налог, Объект.Дата);
	Если СписокВидовНалоговыхОбязательств.НайтиПоЗначению(Объект.ВидНалоговогоОбязательства) = Неопределено Тогда
		Если ДобавитьТекущееЗначениеПриОтсутствии Тогда
			СписокВидовНалоговыхОбязательств.Добавить(Объект.ВидНалоговогоОбязательства);
		ИначеЕсли СписокВидовНалоговыхОбязательств.Количество() > 0 Тогда
			Объект.ВидНалоговогоОбязательства = СписокВидовНалоговыхОбязательств[0].Значение;
			ВидаНалоговогоОбязательстваОбработатьИзменение(Форма);
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСписокВыбора(Элементы.ВидНалоговогоОбязательства, СписокВидовНалоговыхОбязательств);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбора(Элемент, СписокДанных)
	
	Элемент.СписокВыбора.Очистить();
	Для каждого ЭлементДанных Из СписокДанных Цикл
		Элемент.СписокВыбора.Добавить(ЭлементДанных.Значение, ЭлементДанных.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзмененияВОрганизацииНаСервере()
	
	ИспользоватьНесколькоБанковскихСчетовОрганизации =
		Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Объект.Организация);
	
	ОсновнойБанковскийСчетОрганизацииЗаполнен =
		ПроверкаРеквизитовОрганизации.ОсновнойБанковскийСчетОрганизацииЗаполнен(Объект.Организация);
	
	Если НЕ ИспользоватьНесколькоБанковскихСчетовОрганизации И ОсновнойБанковскийСчетОрганизацииЗаполнен И Не ЗначениеЗаполнено(Объект.СчетОрганизации) Тогда
		Объект.СчетОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ОсновнойБанковскийСчет");
	КонецЕсли;
	
	ЗаполнитьОшибкуСчетаОрганизации();
	
	УстановитьРеквизитыПлательщикаНаСервере(Истина);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПлатежныеРеквизитыПриИзмененииДаты(РеквизитыПерезаполнены, ИзменилсяМесяц)
	
	ПлатежноеПоручениеФормы.УстановитьФункциональныеОпцииФормы(ЭтотОбъект);
	НалогУплачиваетсяОтдельно = ЕдиныйНалоговыйСчетВызовСервера.НалогУплачиваетсяОтдельно(Объект.Организация,
		Объект.Дата,
		Объект.Налог,
		Объект.Контрагент);
		
	Если НЕ РеквизитыПерезаполнены Тогда
		Документы.ПлатежноеПоручение.ИзменитьПлатежныеРеквизитыПриИзмененииДаты(Объект, ПредыдущаяДата);
		
		НадписьРеквизитыПлательщика = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
		НадписьРеквизитыПолучателя  = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	Если ИзменилсяМесяц Тогда
		ЗаполнитьВидНалоговогоОбязательстваНаСервере(ЭтотОбъект);
	КонецЕсли;
	
	БанкИКассаФормы.ПоказатьБаннерПредупреждениеОЗаполненииРеквизитовПлатежаНПД(ЭтотОбъект);
	
	ДлительнаяОперацияПроверкиПлатежныхРеквизитов = ПлатежноеПоручениеФормы.ПроверитьПлатежныеРеквизитыВФоне(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВозможноПеречислениеПособияНаКартуМир(ВидОперации, КонтрагентЮрФизЛицо)
	
	Возврат ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику")
		Или ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание")
			И КонтрагентЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВозможноВзысканиеСПеречисленияФизлицу(Дата, ВидОперации, КонтрагентЮрФизЛицо)
	
	Результат = ПлатежноеПоручениеФормыКлиентСервер.ВозможноВзысканиеСПеречисленияФизлицу(
		Дата, ВидОперации, КонтрагентЮрФизЛицо);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьГосОрган(РеквизитыГосОргана)
	
	Объект.Контрагент      = РеквизитыГосОргана.Ссылка;
	Объект.СчетКонтрагента = РеквизитыГосОргана.ПлатежныеРеквизиты.БанковскийСчет;
	ПлатежноеПоручениеФормы.УстановитьПараметрыВыбораБанковскихСчетов(ЭтотОбъект);
	
	УстановитьРеквизитыПолучателяНаСервере();
	НадписьРеквизитыПолучателя = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
	
	РеквизитыГосОргана.ТребуетсяПерезаполнитьКонтрагента = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьКонтрагентаНалоговыйОрган(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьГосОрган(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеКПППлательщика(РеквизитыГосОргана)
	
	Если Не ЗначениеЗаполнено(Объект.Налог) Или ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяНалоговымиОрганами(Объект.КодБК) Тогда
		РеквизитыГосОргана = ПлатежноеПоручениеФормы.РеквизитыГосударственногоОрганаПоКППОрганизации(
			Объект.Организация, Объект.КПППлательщика, Объект.Налог, Объект.Дата);
		Если РеквизитыГосОргана <> Неопределено Тогда
			РеквизитыГосОргана.Вставить("ТребуетсяПерезаполнитьКонтрагента",
				ЗначениеЗаполнено(РеквизитыГосОргана.Ссылка) И РеквизитыГосОргана.Ссылка <> Объект.Контрагент);
			Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
				УстановитьГосОрган(РеквизитыГосОргана);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПлатежаВБюджет(ЭтотОбъект, Истина);
	НадписьРеквизитыПлательщика = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АвтоТекстНазначенияПлатежа(Знач ПараметрыНазначенияПлатежа, ШаблонНазначенияПлатежаВБюджет)
	
	Возврат Документы.ПлатежноеПоручение.ТекстНазначенияПлатежа(ПараметрыНазначенияПлатежа, ШаблонНазначенияПлатежаВБюджет);
	
КонецФункции

&НаСервере
Процедура ПерезаполнитьНазначениеПлатежаНаСервере()
	
	ПараметрыНазначенияПлатежа = Документы.ПлатежноеПоручение.НовыеПараметрыНазначенияПлатежа();
	ЗаполнитьЗначенияСвойств(ПараметрыНазначенияПлатежа, Объект);
	АвтоЗначенияРеквизитов.ТекстНазначенияПлатежа = АвтоТекстНазначенияПлатежа(ПараметрыНазначенияПлатежа, ШаблонНазначенияПлатежаВБюджет);
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(Объект, АвтоЗначенияРеквизитов, НазначениеПлатежаБылоИзмененоВручную);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНазначениеПлатежаНаСервере(ТолькоСумму = Ложь)
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(
		Объект,
		АвтоЗначенияРеквизитов,
		НазначениеПлатежаБылоИзмененоВручную,
		ТолькоСумму);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПересчитатьСуммуНДС(СуммаДокумента, СтавкаНДС)
	
	ПроцентНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтавкаНДС);
	СуммаНДС   = Окр(СуммаДокумента * ПроцентНДС / (100 + ПроцентНДС), 2);
	
	Возврат СуммаНДС;
	
КонецФункции

&НаСервере
Функция ПроверкаРеквизитовПередЗаписьюНаСервере()
	
	Возврат ПлатежноеПоручениеФормы.ПроверитьРеквизитыПередЗаписью(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура УстановитьРеквизитыПолучателяНаСервере(ОбработкаОповещения = Ложь, ИзменениеКонтрагента = Истина)
	
	Если ОбработкаОповещения И ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента) И ЗначениеЗаполнено(Объект.Организация) Тогда
			
			РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
				Объект.ДоговорКонтрагента,
				Объект.Контрагент,
				Объект.Организация,
				ВидыДоговоровДокумента(Объект.ВидОперации));
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
			Объект.СчетКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ОсновнойБанковскийСчет");
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьВспомогательныеРеквизитыПолучателя(ИзменениеКонтрагента);
	НадписьРеквизитыПолучателя = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыПлательщикаНаСервере(Знач ИзменениеОрганизации = Ложь)
	
	ПодключенаИнтеграцияСБанком = ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВключена(Объект.СчетОрганизации);
	
	ОбработатьИПоказатьСостоянияОтправки();
	
	СчетПоГосконтракту = ЗначениеЗаполнено(Объект.СчетОрганизации) И
		ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СчетОрганизации, "ГосударственныйКонтракт"));
		
	Если ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		ВидГосударственногоОргана = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ВидГосударственногоОргана");
	Иначе
		ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ПустаяСсылка();
	КонецЕсли;
	
	АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
	
	ПлатежноеПоручениеФормы.ЗаполнитьРеквизитыПлательщикаПоАвтоЗначениямРеквизитов(ЭтотОбъект, АвтоЗначенияРеквизитов, ИзменениеОрганизации);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокОчередностьПлатежа()
	
	ЗаполнитьСписокВыбора(Элементы.ОчередностьПлатежа, УчетДенежныхСредствКлиентСервер.ПолучитьСписокОчередностьПлатежа(Объект.Дата));
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВидОперацииОрганизации()
	
	ДоступныеОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПолучитьСписокДоступныхЗначений(
		Новый Структура("Организация, СчетОрганизации", Объект.Организация, Объект.СчетОрганизации),, Истина);
	
	Если ДоступныеОперации.Количество() > 0 И ДоступныеОперации.НайтиПоЗначению(Объект.ВидОперации) = Неопределено Тогда
		
		Объект.ВидОперации = ДоступныеОперации[0].Значение;
		ВидОперацииПриИзмененииСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПереводМеждуСвоимиСчетами()
	
	Если УчетДенежныхСредствКлиентСервер.ПереводДенегВнутриКомпании(Объект.ВидОперации) Тогда
		Объект.СчетКонтрагента = Неопределено;
		Если УчетДенежныхСредствКлиентСервер.СписаниеСредствНаДругойСчет(Объект.ВидОперации) Тогда
			УстановитьРеквизитыПолучателяНаСервере();
			Объект.Контрагент    = Неопределено;
		Иначе
			РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Организация, "ИндивидуальныйПредприниматель");
			Объект.Контрагент    = РеквизитыОрганизации.ИндивидуальныйПредприниматель;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ПроверитьВидОперацииОрганизации();
	
	ПлатежноеПоручениеФормы.УстановитьФункциональныеОпцииФормы(ЭтотОбъект);
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю Тогда
		Если ПлательщикНДСПриУСН Тогда
			Объект.СтавкаНДС = СтавкаНДСПриУСН;
		Иначе
			Объект.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПользователя(Объект.Дата, ПлательщикНДС);
		КонецЕсли;
		Объект.СуммаНДС = ПересчитатьСуммуНДС(Объект.СуммаДокумента, Объект.СтавкаНДС);
		СформироватьНазначениеПлатежаНаСервере(Истина);
	КонецЕсли;

	НалогУплачиваетсяОтдельно = ЕдиныйНалоговыйСчетВызовСервера.НалогУплачиваетсяОтдельно(Объект.Организация,
		Объект.Дата,
		Объект.Налог,
		Объект.Контрагент);
		
	ПереводМеждуСвоимиСчетами();
	ПлатежноеПоручениеФормы.УстановитьПараметрыВыбораБанковскихСчетов(ЭтотОбъект);
	
	ПлатежноеПоручениеФормы.ОпределитьОрганизацияЮрФизЛицо(ЭтотОбъект);
	
	УчетДенежныхСредствБП.УстановитьБанковскийСчет(
		Объект.СчетОрганизации,
		Объект.Организация,
		Объект.ВалютаДокумента);
	
	ЗаполнитьОшибкуСчетаОрганизации();
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		КонтрагентОбработатьИзменение();
	КонецЕсли;
	
	Если Объект.ПеречислениеВБюджет Тогда
		ИзменилсяКонтрагент = Ложь;
		ЗаполнитьРеквизитыПлатежаВБюджет(ЭтотОбъект, ИзменилсяКонтрагент);
		Если ИзменилсяКонтрагент Тогда
			КонтрагентПриИзмененииНаСервере();
		КонецЕсли;
	Иначе
		ПлатежноеПоручениеФормыКлиентСервер.СброситьПризнакПлатежаВБюджет(ЭтотОбъект);
	КонецЕсли;
	
	ИспользоватьНесколькоБанковскихСчетовОрганизации =
		Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Объект.Организация);
	
	ОсновнойБанковскийСчетОрганизацииЗаполнен = ПроверкаРеквизитовОрганизации.ОсновнойБанковскийСчетОрганизацииЗаполнен(Объект.Организация);
	
	УстановитьРеквизитыПлательщикаНаСервере(Истина);
	
	БанкИКассаФормы.ПоказатьБаннерПредупреждениеОЗаполненииРеквизитовПлатежаНПД(ЭтотОбъект);
	
	Если СчетПоГосконтракту Тогда
		Объект.ИдентификаторПлатежа = ИдентификаторГосконтактаПоСчету(Объект.СчетОрганизации);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура НалогПриИзмененииНаСервере(АктуализированыРеквизитыПлатежаВБюджет, СтрокаПоискаПоКБК)
	
	НалогУплачиваетсяОтдельно = ЕдиныйНалоговыйСчетВызовСервера.НалогУплачиваетсяОтдельно(Объект.Организация,
		Объект.Дата,
		Объект.Налог,
		Объект.Контрагент);
		
	АктуализированыРеквизитыПлатежаВБюджет = Ложь;
	Если НЕ Справочники.ВидыНалоговИПлатежейВБюджет.РеквизитыАктуальны(Объект.Налог, Объект.Дата) Тогда
		Справочники.ВидыНалоговИПлатежейВБюджет.ОбновитьПоставляемыеДанныеИзКлассификатора();
		АктуализированыРеквизитыПлатежаВБюджет = Истина;
	КонецЕсли;
	
	НалогОбработатьИзменение(СтрокаПоискаПоКБК);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура НалогОбработатьИзменение(СтрокаПоискаПоКБК)
	
	ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Налог, "ВидНалога");
	
	Объект.ВидНалоговогоОбязательства = Справочники.ВидыНалоговИПлатежейВБюджет.ВидНалоговогоОбязательстваПоКБК(
		СтрокаПоискаПоКБК, Объект.Дата, ВидНалога);
	ЗаполнитьВидНалоговогоОбязательстваНаСервере(ЭтотОбъект);
	
	ПравилаЗаполнения = ПлатежиВБюджетНастройки.ПравилаЗаполненияРеквизитовПлатежа(Объект.Налог);
	
	ЭтоЕНП = Объект.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.ЕдиныйНалоговыйПлатеж;
	
	ТекущийПолучатель = Объект.Контрагент;
	Если ПравилаЗаполнения.ЗаполнятьПолучателя Тогда
		Получатель = ДанныеГосударственныхОрганов.АдминистраторНалогаОрганизации(Объект.Налог, Объект.Организация, Объект.Дата);
		Если ЗначениеЗаполнено(Получатель) И ТекущийПолучатель <> Получатель Тогда
			Объект.Контрагент = Получатель;
		КонецЕсли;
		
		Если ЭтоЕНП И Получатель = Неопределено Тогда
			Попытка
				ДанныеГосударственныхОрганов.ДобавитьМИДолПолучателяЕНП();
				
				Получатель = ДанныеГосударственныхОрганов.АдминистраторНалогаОрганизации(Объект.Налог, Объект.Организация, Объект.Дата);
				Если ЗначениеЗаполнено(Получатель) Тогда
					Объект.Контрагент = Получатель;
				КонецЕсли;
			Исключение
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ОписаниеОшибки = НСтр("ru = 'Ошибка создания получателя ЕНП'") + Символы.ПС
					+ ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
				ЗаписьЖурналаРегистрации(
					ОбщегоНазначенияБП.СобытиеЖурналаРегистрацииОшибкаЗаписиОбъекта(),
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.Справочники.Контрагенты, , ОписаниеОшибки);
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.Налог <> Справочники.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы
		И Не ЭтоЕНП Тогда
		РегистрацияВНалоговомОргане = Документы.ПлатежноеПоручение.РегистрацияВНалоговомОрганеПоДаннымПлатежногоПоручения(
			Объект.Организация, Объект.Контрагент, Объект.КПППлательщика);
		
		НастройкаЗаполнения = РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.КлючНастройкиУплатыНалога(
			Объект.Налог, Объект.Организация, РегистрацияВНалоговомОргане);
	КонецЕсли;
	
	ПлатежноеПоручениеФормы.ПерезаполнитьСтатьюДДС(ЭтотОбъект);
	
	ЗаполнитьРеквизитыПлатежаВБюджет(ЭтотОбъект,, НастройкаЗаполнения);
	
	Если ТекущийПолучатель <> Объект.Контрагент Тогда
		КонтрагентПриИзмененииНаСервере(, Истина, Истина);
	ИначеЕсли НалогУплачиваетсяОтдельно
		Или ВидНалога = Перечисления.ВидыНалогов.ПустаяСсылка() Тогда
		УстановитьСчетПолучателя();
	КонецЕсли;
	
	БанкИКассаФормы.ПоказатьБаннерПредупреждениеОЗаполненииРеквизитовПлатежаНПД(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВидОперацииПриИзмененииСервер()
	
	ВидОперацииОбработатьИзменение();
	
	Если Объект.ПеречислениеВБюджет Тогда
		ИзменитьПеречислениеВБюджет(,,, Истина);
	Иначе
		ПлатежноеПоручениеФормыКлиентСервер.СброситьПризнакПлатежаВБюджет(ЭтотОбъект);
	КонецЕсли;
	
	НадежностьБанковКлиентСервер.ПолучитьИнформациюНадежностьБанков(ЭтотОбъект,
		СчетПолучателяБИКБанка,
		ПоказыватьИнформациюНадежностьБанков(ЭтотОбъект));
	
	НастроитьСписокВыбораКодВидаДохода(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура НалогоплательщикПриИзмененииНеСервере()
	
	АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
	
	// При изменении налогоплательщика нельзя затирать реквизиты получателя и текст плательщика.
	ЗаполнитьЗначенияСвойств(Объект, АвтоЗначенияРеквизитов,, "ИННПолучателя, КПППолучателя, ТекстПлательщика, ТекстПолучателя");
	
	ЗаполнитьРеквизитыПлатежаВБюджет(ЭтотОбъект);
	
	НадписьРеквизитыПлательщика = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(Объект, АвтоЗначенияРеквизитов, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	Если НЕ НазначениеПлатежаБылоИзмененоВручную Тогда
		АвтоЗначенияРеквизитов.Вставить("ТекстНазначенияПлатежа", "");
	КонецЕсли;
	
	АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
	
	ЗаполнитьЗначенияСвойств(Объект, АвтоЗначенияРеквизитов, "ИННПлательщика, КПППлательщика");
	
	ЗаполнитьРеквизитыПлатежаВБюджет(ЭтотОбъект);
	
	НадписьРеквизитыПлательщика = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(
		Объект, АвтоЗначенияРеквизитов, НазначениеПлатежаБылоИзмененоВручную);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере(ОпределятьОсновнойВариантПеречисления = Ложь, Знач ИзменениеКонтрагента = Ложь, Знач ЗаполнениеПоНастройкеПлатежаВБюджет = Ложь)
	
	НалогУплачиваетсяОтдельно = ЕдиныйНалоговыйСчетВызовСервера.НалогУплачиваетсяОтдельно(Объект.Организация,
		Объект.Дата,
		Объект.Налог,
		Объект.Контрагент);
	
	Если ЗаполнениеПоНастройкеПлатежаВБюджет Тогда
		КонтрагентОбработатьИзменение(ОпределятьОсновнойВариантПеречисления, ИзменениеКонтрагента, Истина);
	Иначе
		КонтрагентОбработатьИзменение(ОпределятьОсновнойВариантПеречисления, ИзменениеКонтрагента);
		ЗаполнитьРеквизитыПлатежаВБюджет(ЭтотОбъект, Истина);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СчетКонтрагентаПриИзмененииНаСервере(Знач ИзменениеКонтрагента = Ложь, Знач ЗаполнениеПоНастройкеПлатежаВБюджет = Ложь)
	
	Если УчетДенежныхСредствКлиентСервер.РасчетыСФизическимиЛицами(Объект.ВидОперации)
			ИЛИ УчетДенежныхСредствКлиентСервер.СписаниеСредствНаДругойСчет(Объект.ВидОперации)
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП Тогда
		Если Объект.ПеречислениеВБюджет Тогда
			ПлатежГосОргану            = Ложь;
			Объект.ПеречислениеВБюджет = Ложь;
			ПлатежноеПоручениеФормыКлиентСервер.СброситьПризнакПлатежаВБюджет(ЭтотОбъект);
		КонецЕсли;
	ИначеЕсли НЕ ЗаполнениеПоНастройкеПлатежаВБюджет Тогда
		ПлатежГосОргану = УчетДенежныхСредствБП.ПлатежГосОргану(Объект.ВидОперации, Объект.Контрагент);
		Если ПлатежГосОргану Тогда
			ИзменитьПеречислениеВБюджет(, ИзменениеКонтрагента);
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьВспомогательныеРеквизитыПолучателя(ИзменениеКонтрагента);
	
	НадписьРеквизитыПолучателя = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
	
	Если ЗаполнениеПоНастройкеПлатежаВБюджет И НЕ ПустаяСтрока(Объект.НазначениеПлатежа) Тогда
		НазначениеПлатежаБылоИзмененоВручную = Истина;
	КонецЕсли;
	
	СформироватьНазначениеПлатежаНаСервере(Ложь);
	
	ДлительнаяОперацияПроверкиПлатежныхРеквизитов = ПлатежноеПоручениеФормы.ПроверитьПлатежныеРеквизитыВФоне(ЭтотОбъект);
	
	НадежностьБанковКлиентСервер.ПолучитьИнформациюНадежностьБанков(ЭтотОбъект,
		СчетПолучателяБИКБанка,
		ПоказыватьИнформациюНадежностьБанков(ЭтотОбъект));
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СчетОрганизацииПриИзмененииНаСервере()
	
	УстановитьРеквизитыПлательщикаНаСервере(ПустаяСтрока(Объект.ИННПлательщика));
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Если Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета И ЗначениеЗаполнено(Объект.СчетОрганизации) Тогда
		ВалютаСчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СчетОрганизации, "ВалютаДенежныхСредств");
		Если Объект.ВалютаДокумента <> ВалютаСчета Тогда
			Объект.ВалютаДокумента = ВалютаСчета;
			
			ПараметрыПлатежа = Новый Структура;
			ПараметрыПлатежа.Вставить("ВидОперации", Объект.ВидОперации);
			ПараметрыПлатежа.Вставить("Организация", Объект.Организация);
			ПараметрыПлатежа.Вставить("Контрагент",  Объект.Контрагент);
			Если Объект.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда
				ПараметрыПлатежа.Вставить("ОплатаВВалюте",   Ложь);
				ПараметрыПлатежа.Вставить("ВалютаДокумента", ВалютаРегламентированногоУчета);
			Иначе
				ПараметрыПлатежа.Вставить("ОплатаВВалюте",   Истина);
				ПараметрыПлатежа.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
			КонецЕсли;
			
			Объект.ДоговорКонтрагента = УчетДенежныхСредствБП.ПолучитьДоговорКонтрагента(ПараметрыПлатежа);
		КонецЕсли;
	КонецЕсли;
	
	Если СчетПоГосконтракту
		Или Справочники.БанковскиеСчета.ЭтоЦифровойСчет(Объект.СчетОрганизации)
			<> Справочники.БанковскиеСчета.ЭтоЦифровойСчет(Объект.СчетКонтрагента) Тогда
		УстановитьСчетПолучателя();
		
		Если ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
			СчетКонтрагентаПриИзмененииНаСервере();
		КонецЕсли;
		
		Если СчетПоГосконтракту Тогда
			Объект.ИдентификаторПлатежа = ИдентификаторГосконтактаПоСчету(Объект.СчетОрганизации); 
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьОшибкуСчетаОрганизации();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	ПроверитьУстановитьСчетОрганизацииПоУмолчанию();
	ПлатежноеПоручениеФормы.УстановитьПараметрыВыбораБанковскихСчетов(ЭтотОбъект);
	
	СчетОрганизацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДоговорПриИзмененииНаСервере(ИзменениеКонтрагента = Истина)
	
	Если ВидОперацииБезДоговора(Объект.ВидОперации) ИЛИ Объект.ПеречислениеВБюджет Тогда
		Объект.СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
		Объект.СуммаНДС  = 0;
		Возврат;
	КонецЕсли;
	
	УстановитьРеквизитыПолучателяНаСервере(, ИзменениеКонтрагента);
	
	ВидыДоговора = ВидыДоговоровДокумента(Объект.ВидОперации);
	Если ВидыДоговора.Найти(Перечисления.ВидыДоговоровКонтрагентов.Прочее) <> Неопределено Тогда
		Объект.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
	ИначеЕсли Объект.ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю Тогда
		Объект.СтавкаНДС = УчетДенежныхСредствБП.ПолучитьСтавкуНДСПоДоговору(Объект.Дата, Объект.ДоговорКонтрагента);
	КонецЕсли;
	
	Объект.СуммаНДС = ПересчитатьСуммуНДС(Объект.СуммаДокумента, Объект.СтавкаНДС);
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(
		Объект, АвтоЗначенияРеквизитов, НазначениеПлатежаБылоИзмененоВручную);
		
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ПредлагатьНовыйДоговор = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастройкиПечатиПриИзмененииНаСервере()
	
	АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
	
	ЗаполнитьЗначенияСвойств(
		Объект,
		АвтоЗначенияРеквизитов,
		"ТекстПлательщика, КПППлательщика, ТекстПолучателя, КПППолучателя");
	
	НадписьРеквизитыПлательщика     = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
	НадписьРеквизитыПолучателя      = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
	НадписьРеквизитыПлатежейВБюджет = ПлатежноеПоручениеФормыКлиентСервер.НадписьРеквизитыПлатежейВБюджет(ЭтотОбъект);
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(
		Объект, АвтоЗначенияРеквизитов, НазначениеПлатежаБылоИзмененоВручную);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидОперацииУплатаНалога()
	
	Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога;
	
	ВидОперацииОбработатьИзменение();
	
	УправлениеФормой(ЭтотОбъект);
	
	ТекущийЭлемент = Элементы.Налог;
	
КонецПроцедуры

&НаСервере
Процедура ВидОперацииОбработатьИзменение()
	
	ВидОперацииДоИзменения = Объект.ВидОперации;
	
	УплатаНалогаЗаТретьихЛиц = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц");
	ПеречислениеФизическомуЛицу = УчетДенежныхСредствКлиентСервер.РасчетыСФизическимиЛицами(Объект.ВидОперации)
		И Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ЛичныеСредстваПредпринимателя");
	ПеречислениеНаЛичныйСчет = Истина;
	Объект.Банк = "";
	
	Если НЕ УплатаНалогаЗаТретьихЛиц Тогда
		Объект.Налогоплательщик = "";
	КонецЕсли;
	
	Если ПеречислениеФизическомуЛицу Тогда
		ОсновнойВариантПеречисления = РегистрыСведений.ОсновныеВариантыПеречисленияСотруднику.ПолучитьОсновнойВариантПеречисления(
		Новый Структура("Организация, ФизическоеЛицо, ВидОперации, БанковскийСчет, НазначениеПлатежа",
			Объект.Организация, Объект.Контрагент, Объект.ВидОперации, Объект.СчетКонтрагента, ""));
		
		Объект.СчетКонтрагента = ОсновнойВариантПеречисления.БанковскийСчет;
		Если ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
			ВладелецСчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СчетКонтрагента, "Владелец");
			Если ВладелецСчета <> Объект.Контрагент Тогда
				Объект.Банк = ВладелецСчета;
				ПеречислениеНаЛичныйСчет = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога Тогда
		Объект.ПеречислениеВБюджет = Истина;
	Иначе
		Объект.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.ПустаяСсылка();
		
		ПлатежГосОргану = УчетДенежныхСредствБП.ПлатежГосОргану(Объект.ВидОперации, Объект.Контрагент);
		Если ПлатежГосОргану Тогда
			Объект.ПеречислениеВБюджет = ПлатежГосОргану;
		КонецЕсли;
		
		Если НЕ Объект.ПеречислениеВБюджет Тогда
			ПлатежноеПоручениеФормыКлиентСервер.СброситьПризнакПлатежаВБюджет(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДивидендов Тогда
		Объект.НачислениеДивидендов = "";
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПоИсполнительномуЛистуРаботника Тогда
		Объект.ПеречислениеПоИсполнительномуЛисту = Истина;
	Иначе
		Объект.ПеречислениеПоИсполнительномуЛисту = Ложь;
		Объект.Сотрудник                          = "";
		Объект.ИсполнительныйЛист                 = "";
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю Тогда
		Если ПлательщикНДСПриУСН Тогда
			Объект.СтавкаНДС = СтавкаНДСПриУСН;
		Иначе
			Объект.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПользователя(Объект.Дата, ПлательщикНДС);
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым Тогда
		ВидОплатыПоРеестру = ВидОплатыСамозанятымПоРеестру(КонтрагентЮрФизЛицо);
		ЗаполнитьСуммуПлатежаПоРееструВыплатСамозанятым();
	КонецЕсли;
	
	БанкИКассаФормы.УстановитьКонтрагентаЮрФизЛицоПоУмолчанию(ЭтотОбъект);
	ПлатежноеПоручениеФормы.УстановитьОграничениеТипаКонтрагента(ЭтотОбъект);
	БанкИКассаФормы.ОпределитьКонтрагентЮрФизЛицо(ЭтотОбъект);
	
	ПереводМеждуСвоимиСчетами();
	ПроверитьУстановитьСчетОрганизацииПоУмолчанию();
	ПлатежноеПоручениеФормы.УстановитьПараметрыВыбораБанковскихСчетов(ЭтотОбъект);
	
	Если Не ВозможноПеречислениеПособияНаКартуМир(Объект.ВидОперации, КонтрагентЮрФизЛицо) Тогда
		Объект.КодВыплат = "";
	КонецЕсли;
	
	КодВидаДоходаПоУмолчанию(ЭтотОбъект);
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент)
		И НЕ УчетДенежныхСредствКлиентСервер.СписаниеСредствНаДругойСчет(Объект.ВидОперации)
		ИЛИ НЕ ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
		УстановитьСчетПолучателя(Истина);
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда Тогда
		Объект.ДокументОснование = Документы.ВедомостьНаВыплатуЗарплатыВБанк.ПустаяСсылка();
	КонецЕсли;
	
	Если ВидОперацииБезДоговора(Объект.ВидОперации) Тогда
		Объект.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ПредлагатьНовыйДоговор = Ложь;
	Иначе
		УстановитьПараметрыВыбораДоговораСКонтрагентом(ЭтотОбъект);
		ПодобратьДоговор();
	КонецЕсли;
	
	ДоговорПриИзмененииНаСервере(Ложь);
	
	ПлатежноеПоручениеФормы.ПерезаполнитьСтатьюДДС(ЭтотОбъект);
	
	ПерезаполнитьНазначениеПлатежаНаСервере();
	
	УстановитьОчередностьПлатежа();
	ПлатежноеПоручениеФормыКлиентСервер.ОбновитьПодсказкуОчередностьПлатежа(ЭтотОбъект, Объект.ОчередностьПлатежа);
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	ПоказатьИндексыСПАРК = ПоказатьИнформациюСпарк(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСчетПолучателя(Знач ИзменениеКонтрагента = Ложь, Знач ЗаполнениеПоНастройкеПлатежаВБюджет = Ложь)
	
	Если СчетПоГосконтракту Тогда
		ПлатежноеПоручениеФормы.УстановитьСчетКонтагентаПоГосконтракту(ЭтотОбъект);
	Иначе
		УстановитьЕдиныйСчет = Ложь;
		ЭтоУплатаНалога = Объект.ПеречислениеВБюджет
			И Объект.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж;
		Если ЭтоУплатаНалога И ПлательщикЕНП Тогда
			СвойстваКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				Объект.Контрагент, "ГосударственныйОрган, ВидГосударственногоОргана");
			УстановитьЕдиныйСчет = ЗначениеЗаполнено(СвойстваКонтрагента.ГосударственныйОрган)
				И СвойстваКонтрагента.ГосударственныйОрган
				И СвойстваКонтрагента.ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган;
		КонецЕсли;
		
		Если УстановитьЕдиныйСчет Тогда
			Объект.СчетКонтрагента = Неопределено; // очистим, иначе замена уже установленного счета не произойдет
			УчетДенежныхСредствБП.УстановитьБанковскийСчетЕдиногоНалоговогоПлатежа(
				Объект.СчетКонтрагента,
				Объект.Контрагент,
				Объект.ВалютаДокумента);
		Иначе
			ЦифровойСчет = Справочники.БанковскиеСчета.ЭтоЦифровойСчет(Объект.СчетОрганизации);
			УчетДенежныхСредствБП.УстановитьБанковскийСчет(
				Объект.СчетКонтрагента,
				?(ПеречислениеФизическомуЛицу И НЕ ПеречислениеНаЛичныйСчет, Объект.Банк, Объект.Контрагент),
				Объект.ВалютаДокумента,,,,, ЦифровойСчет);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
		СчетКонтрагентаПриИзмененииНаСервере(ИзменениеКонтрагента, ЗаполнениеПоНастройкеПлатежаВБюджет);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентОбработатьИзменение(ОпределятьОсновнойВариантПеречисления = Ложь, ИзменениеКонтрагента = Ложь, ЗаполнениеПоНастройкеПлатежаВБюджет = Ложь)
	
	ПеречислениеВБюджетПредыдущее = Объект.ПеречислениеВБюджет;
	
	ПлатежГосОргану            = УчетДенежныхСредствБП.ПлатежГосОргану(Объект.ВидОперации, Объект.Контрагент);
	Объект.ПеречислениеВБюджет = ПлатежГосОргану;
	Если Объект.ПеречислениеВБюджет Тогда
		Если Не ЗначениеЗаполнено(Объект.ВидПеречисленияВБюджет) Тогда
			Объект.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж;
		КонецЕсли;
		
		Если Не ПеречислениеВБюджетПредыдущее Тогда
			АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
			ПлатежноеПоручениеФормы.ЗаполнитьРеквизитыПлательщикаПоАвтоЗначениямРеквизитов(ЭтотОбъект, АвтоЗначенияРеквизитов, Ложь);
		КонецЕсли;
	Иначе
		Объект.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ПустаяСсылка();
		Если ПеречислениеВБюджетПредыдущее
			Или Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПоИсполнительномуЛистуРаботника Тогда
			АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
			ПлатежноеПоручениеФормы.ЗаполнитьРеквизитыПлательщикаПоАвтоЗначениямРеквизитов(ЭтотОбъект, АвтоЗначенияРеквизитов, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	БанкИКассаФормы.ОпределитьКонтрагентЮрФизЛицо(ЭтотОбъект);
	
	Если ПеречислениеФизическомуЛицу Тогда
		Если ОпределятьОсновнойВариантПеречисления Тогда
			ПеречислениеНаЛичныйСчет = Истина;
			
			ОсновнойВариантПеречисления = РегистрыСведений.ОсновныеВариантыПеречисленияСотруднику.ПолучитьОсновнойВариантПеречисления(
				Новый Структура("Организация, ФизическоеЛицо, ВидОперации, БанковскийСчет, НазначениеПлатежа",
					Объект.Организация, Объект.Контрагент, Объект.ВидОперации, Объект.СчетКонтрагента, ""));
			
			Объект.СчетКонтрагента = ОсновнойВариантПеречисления.БанковскийСчет;
			Если ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
				ВладелецСчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СчетКонтрагента, "Владелец");
				Если ВладелецСчета <> Объект.Контрагент Тогда
					Объект.Банк = ВладелецСчета;
					ПеречислениеНаЛичныйСчет = Ложь;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Объект.СчетКонтрагента = "";
		КонецЕсли;
	ИначеЕсли ИзменениеКонтрагента Тогда
		НастроитьСписокВыбораКодВидаДохода(ЭтотОбъект);
		КодВидаДоходаПоУмолчанию(ЭтотОбъект);
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда Тогда
		Если ЗначениеЗаполнено(Объект.Контрагент) И ЗначениеЗаполнено(Объект.ПлатежнаяВедомость) Тогда
			Объект.СуммаДокумента = СуммаВыплаченнойЗарплатыРаботнику(
				Объект.Ссылка, Объект.Контрагент, Объект.ПлатежнаяВедомость, УчетЗарплатыИКадровВоВнешнейПрограмме);
		Иначе
			Объект.СуммаДокумента = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если ИзменениеКонтрагента Или Не ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
		УстановитьСчетПолучателя(Истина, ЗаполнениеПоНастройкеПлатежаВБюджет);
	ИначеЕсли НалогУплачиваетсяОтдельно Тогда
		УстановитьСчетПолучателя();
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
		И НЕ ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
		СформироватьНазначениеПлатежаНаСервере(Ложь);
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым Тогда
		ЗаполнитьСуммуПлатежаПоРееструВыплатСамозанятым();
	КонецЕсли;
	
	УплатаНалогаЗаТретьихЛиц = ПлатежноеПоручениеФормы.УплатаНалогаЗаТретьихЛиц(Объект);
	
	Если ПлатежГосОргану И НЕ УплатаНалогаЗаТретьихЛиц И ИзменениеКонтрагента И НЕ ЗаполнениеПоНастройкеПлатежаВБюджет Тогда
		ПлатежноеПоручениеФормы.ПроверитьИзменитьКПППлательщика(ЭтотОбъект);
	КонецЕсли;
	
	Если НЕ ВидОперацииБезДоговора(Объект.ВидОперации) Тогда
		ПодобратьДоговор();
	КонецЕсли;
	
	ПлатежноеПоручениеФормы.УстановитьПараметрыВыбораБанковскихСчетов(ЭтотОбъект);
	ПроверитьУстановитьСчетОрганизацииПоУмолчанию();
	
	ДоговорПриИзмененииНаСервере();
	
	АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
	
	// В общем случае при изменении контрагента нельзя затирать реквизиты организации,
	// но при уплате ЕНП "за себя" обновляем КПП плательщика значением по умолчанию, т.к. там должен быть указан КПП головной организации.
	СохраняемыеРеквизитыПлательщика = Новый Массив;
	СохраняемыеРеквизитыПлательщика.Добавить("ТекстПлательщика");
	СохраняемыеРеквизитыПлательщика.Добавить("ИННПлательщика");
	Если ПлатежноеПоручениеФормы.СохранятьКПППлательщика(ЭтотОбъект) Тогда
		СохраняемыеРеквизитыПлательщика.Добавить("КПППлательщика");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, АвтоЗначенияРеквизитов,, СтрСоединить(СохраняемыеРеквизитыПлательщика, ", "));
	
	НадписьРеквизитыПлательщика = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
	НадписьРеквизитыПолучателя  = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.СПАРКРиски
	ПоказатьИндексыСПАРК = ПоказатьИнформациюСпарк(ЭтотОбъект);
	ВидКонтрагентаСПАРК = СПАРКРискиКлиентСерверБП.ВидКонтрагентаСПАРКРиски(
		?(ТипЗнч(Объект.Контрагент) = Тип("СправочникСсылка.Контрагенты"),  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ЮридическоеФизическоеЛицо"), Неопределено));
	// Конец ИнтернетПоддержкаПользователей.СПАРКРиски
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПеречислениеВБюджет(НастройкаЗаполнения = Неопределено, ИзменениеКонтрагента = Ложь, ПолучитьКонтекст = Ложь, ИзменениеВидаОперации = Ложь)
	
	БИКБанкаПолучателя   = "";
	НомерСчетаПолучателя = "";
	Если ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
		РеквизитыСчетаПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.СчетКонтрагента, "Банк.Код, Банк.КоррСчет, НомерСчета");
		БИКБанкаПолучателя      = РеквизитыСчетаПолучателя.БанкКод;
		НомерСчетаПолучателя    = РеквизитыСчетаПолучателя.НомерСчета;
		КоррСчетБанкаПолучателя = РеквизитыСчетаПолучателя.БанкКоррСчет;
	КонецЕсли;
	
	УплатаНалогаЗаТретьихЛиц = ПлатежноеПоручениеФормы.УплатаНалогаЗаТретьихЛиц(Объект);
	
	ПлатежВБюджетГосОргану = ПлатежГосОргану
		  И ПлатежиВБюджетКлиентСервер.ЭтоСчетПлатежаВБюджет(НомерСчетаПолучателя, Объект.Дата)
		  И (БанковскиеПравила.ЭтоОтделениеЦентральногоБанка(БИКБанкаПолучателя)
		  	Или БанковскиеПравила.ЭтоБанкУФК(БИКБанкаПолучателя, КоррСчетБанкаПолучателя));
	
	Объект.ПеречислениеВБюджет = Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога
		ИЛИ УплатаНалогаЗаТретьихЛиц
		ИЛИ ПлатежВБюджетГосОргану;
	
	Если Объект.ПеречислениеВБюджет Тогда
		ПлатежноеПоручениеФормы.УстановитьРеквизитыДляПлатежаВБюджет(ЭтотОбъект);
		ПлатежГосОргану = Истина;
	Иначе
		ПлатежноеПоручениеФормыКлиентСервер.СброситьПризнакПлатежаВБюджет(ЭтотОбъект);
	КонецЕсли;
	
	АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
	
	Если НЕ ИзменениеКонтрагента И НЕ ИзменениеВидаОперации Тогда
		ЗаполнитьЗначенияСвойств(Объект, АвтоЗначенияРеквизитов,, "ИННПлательщика, КПППлательщика, ТекстПлательщика");
	Иначе
		ЗаполнитьЗначенияСвойств(Объект, АвтоЗначенияРеквизитов);
	КонецЕсли;
	
	НадписьРеквизитыПлательщика     = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПлательщика(ЭтотОбъект);
	НадписьРеквизитыПолучателя      = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
	НадписьРеквизитыПлатежейВБюджет = ПлатежноеПоручениеФормыКлиентСервер.НадписьРеквизитыПлатежейВБюджет(ЭтотОбъект);
	
	ИзменилсяКонтрагент = ИзменениеКонтрагента;
	ЗаполнитьРеквизитыПлатежаВБюджет(ЭтотОбъект, ИзменилсяКонтрагент, НастройкаЗаполнения);
	Если НЕ ИзменениеКонтрагента И ИзменилсяКонтрагент Тогда
		КонтрагентПриИзмененииНаСервере(,, Истина);
	КонецЕсли;
	
	Если КонтекстПлатежногоДокумента <> Неопределено Тогда
		Если ПолучитьКонтекст Тогда
			КонтекстПлатежногоДокумента = Документы.ПлатежноеПоручение.КонтекстПлатежногоДокумента(Объект);
		Иначе
			Если НомерСчетаПолучателя <> КонтекстПлатежногоДокумента.НомерСчетаПолучателя Тогда
				КонтекстПлатежногоДокумента.НомерСчетаПолучателя = НомерСчетаПолучателя;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИдентификаторГосконтактаПоСчету(ОтдельныйСчет)
	
	Возврат ПлатежноеПоручениеФормы.ИдентификаторГосконтактаПоСчету(ОтдельныйСчет);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоГосударственныйОрганУплатыНалога(Знач Контрагент)
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "ГосударственныйОрган, ВидГосударственногоОргана");
	
	Возврат РеквизитыКонтрагента.ГосударственныйОрган
		И РеквизитыКонтрагента.ВидГосударственногоОргана <> ПредопределенноеЗначение("Перечисление.ВидыГосударственныхОрганов.Прочий");

КонецФункции

&НаСервереБезКонтекста
Функция ВидОперацииБезДоговора(ВидОперации)
	
	Возврат УчетДенежныхСредствКлиентСервер.РасчетыСФизическимиЛицами(ВидОперации)
		Или УчетДенежныхСредствКлиентСервер.СписаниеСредствНаДругойСчет(ВидОперации)
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание
		Или ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыПлатежаВБюджет(Форма, ИзменилсяКонтрагент = Ложь, НастройкаЗаполнения = Неопределено)
	
	Объект = Форма.Объект;
	
	УплатаНалогаЗаТретьихЛиц =
		Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц;
	ЭтоПеречислениеНалога
		= Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога;
	
	Если ЭтоПеречислениеНалога Тогда
		Если (НЕ ЗначениеЗаполнено(Объект.Налог)
			ИЛИ Объект.Налог = Справочники.ВидыНалоговИПлатежейВБюджет.ПрочиеНалогиИСборы)
		И НастройкаЗаполнения = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыОбъекта = ПлатежиВБюджетКлиентСерверПереопределяемый.НовыеРеквизитыПлатежаВБюджет(Объект, Объект.ПеречислениеВБюджет);
	Если УплатаНалогаЗаТретьихЛиц Тогда
		ИсточникДанныхКонтекста = ПлатежноеПоручениеФормы.ИсточникДанныхКонтекстаПлатежногоДокумента(Форма);
		ЗаполнитьРеквизитыПлатежаВБюджетНаСервере(РеквизитыОбъекта, ИсточникДанныхКонтекста, Неопределено);
		ЗаполнитьЗначенияСвойств(Объект, РеквизитыОбъекта,
			"КодОКАТО, СтатусСоставителя, ПоказательОснования, ПоказательПериода");
	ИначеЕсли Объект.ПеречислениеПоИсполнительномуЛисту Тогда
		ИсточникДанныхКонтекста = ПлатежноеПоручениеФормы.ИсточникДанныхКонтекстаПлатежногоДокумента(Форма);
		ЗаполнитьРеквизитыПлатежаИсполнительногоДокументаВБюджетНаСервере(РеквизитыОбъекта, ИсточникДанныхКонтекста, Форма);
		ЗаполнитьЗначенияСвойств(Объект, РеквизитыОбъекта);
	ИначеЕсли Не ЭтоПеречислениеНалога Тогда
		ИсточникДанныхКонтекста = ПлатежноеПоручениеФормы.ИсточникДанныхКонтекстаПлатежногоДокумента(Форма);
		ЗаполнитьРеквизитыПлатежаВБюджетНаСервере(РеквизитыОбъекта, ИсточникДанныхКонтекста, Неопределено);
		ЗаполнитьЗначенияСвойств(Объект, РеквизитыОбъекта,
			"СтатусСоставителя, ПоказательОснования, ПоказательПериода, ПоказательДаты, ПоказательНомера");
	ИначеЕсли Объект.ПеречислениеВБюджет Тогда
		ИсточникДанныхКонтекста = ПлатежноеПоручениеФормы.ИсточникДанныхКонтекстаПлатежногоДокумента(Форма);
		ЗаполнитьРеквизитыПлатежаВБюджетНаСервере(РеквизитыОбъекта, ИсточникДанныхКонтекста, НастройкаЗаполнения);
		
		ИсключатьСвойства = Новый Массив;
		
		// Функция вызвана из обработчика изменения контрагента
		Если ИзменилсяКонтрагент И РеквизитыОбъекта.Свойство("Контрагент") Тогда
			ИсключатьСвойства.Добавить("Контрагент");
			
			Если РеквизитыОбъекта.Свойство("СчетКонтрагента") Тогда
				ИсключатьСвойства.Добавить("СчетКонтрагента");
			КонецЕсли;
		КонецЕсли;
		
		// Назначение платежа заполняем значением "по умолчанию" только для вида операции Уплата налога
		Если ЭтоПеречислениеНалога Тогда
			Если РеквизитыОбъекта.Свойство("ШаблонНазначенияПлатежаВБюджет") Тогда
				Форма.ШаблонНазначенияПлатежаВБюджет = РеквизитыОбъекта.ШаблонНазначенияПлатежаВБюджет;
			КонецЕсли;
		Иначе
			ИсключатьСвойства.Добавить("НазначениеПлатежа");
		КонецЕсли;
		
		Если Форма.СчетПоГосконтракту Тогда
			ИсключатьСвойства.Добавить("ИдентификаторПлатежа");
		КонецЕсли;
		
		ИзменилсяКонтрагент = ИзменилсяКонтрагент ИЛИ РеквизитыОбъекта.Свойство("Контрагент") И РеквизитыОбъекта.Контрагент <> Объект.Контрагент;
		
		ИсключатьСвойстваИзЗаполнения = СтрСоединить(ИсключатьСвойства, ",");
		Если ПустаяСтрока(ИсключатьСвойстваИзЗаполнения) Тогда
			ЗаполнитьЗначенияСвойств(Объект, РеквизитыОбъекта);
		Иначе
			ЗаполнитьЗначенияСвойств(Объект, РеквизитыОбъекта,, ИсключатьСвойстваИзЗаполнения);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НастройкаЗаполнения) Тогда
			ЗаполнитьВидНалоговогоОбязательстваНаСервере(Форма);
		КонецЕсли;
	Иначе
		ПлатежноеПоручениеФормыКлиентСервер.СброситьПризнакПлатежаВБюджет(Форма);
	КонецЕсли;
	
	Форма.НадписьРеквизитыПлатежейВБюджет = ПлатежноеПоручениеФормыКлиентСервер.НадписьРеквизитыПлатежейВБюджет(Форма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыПлатежаВБюджетНаСервере(РеквизитыОбъекта, Знач ИсточникДанных, Знач НастройкаЗаполнения)
	
	Если НастройкаЗаполнения <> Неопределено Тогда
		Если Не ПлатежиВБюджетКлиентСервер.РеквизитЗаполнен(РеквизитыОбъекта.ПоказательПериода) Тогда
			ПоказателиПериода = ПлатежиВБюджетПереопределяемый.ПоказателиПериода();
			ПоказателиПериода.Период = ИсточникДанных.Период;
			ПоказателиПериода.ПоказательПериода = ПлатежиВБюджетКлиентСервер.НалоговыйПериод(
				ИсточникДанных.Период, ПлатежиВБюджетКлиентСервер.ПлатежПоКонкретнойДате());
			ДанныеЗаполнения = РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.ДанныеЗаполнения(
				НастройкаЗаполнения, ИсточникДанных.Период, ИсточникДанных.Организация,, ПоказателиПериода);
		Иначе
			ДанныеЗаполнения = РегистрыСведений.РеквизитыУплатыНалоговИПлатежейВБюджет.ДанныеЗаполнения(
				НастройкаЗаполнения, ИсточникДанных.Период, ИсточникДанных.Организация);
		КонецЕсли;
	КонецЕсли;
	
	Документы.ПлатежноеПоручение.ЗаполнитьРеквизитыПлатежаВБюджетДопустимымиЗначениями(
		РеквизитыОбъекта, ИсточникДанных, ДанныеЗаполнения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыПлатежаИсполнительногоДокументаВБюджетНаСервере(РеквизитыОбъекта, Знач ИсточникДанных, Форма)
	
	Объект = Форма.Объект;
	
	ДанныеЗаполнения = РегистрыСведений.РеквизитыПеречисленийПоИсполнительнымЛистам.РеквизитыПеречисленияПоИсполнительномуЛистуРаботника(
		Объект.Организация, Объект.Сотрудник, Объект.ИсполнительныйЛист);
		
	Если Не ЗначениеЗаполнено(Объект.ИсполнительныйЛист) Тогда
		ДанныеЗаполнения.ПеречислениеВБюджет = УчетДенежныхСредствБП.ПлатежГосОргану(Объект.ВидОперации, Объект.Контрагент);
		ДанныеЗаполнения.ОчередностьПлатежа  = УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаПоИсполнительномуЛисту();
	КонецЕсли;
	
	Документы.ПлатежноеПоручение.ЗаполнитьРеквизитыПлатежаВБюджетДопустимымиЗначениями(
		РеквизитыОбъекта, ИсточникДанных, ДанныеЗаполнения);
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, ДанныеЗаполнения, "ПеречислениеВБюджет, ОчередностьПлатежа");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВспомогательныеРеквизитыПолучателя(ИзменениеКонтрагента = Истина)
	
	АвтоЗначенияРеквизитов = ПлатежноеПоручениеФормы.АвтоЗначенияРеквизитов(ЭтотОбъект);
	
	ПлатежноеПоручениеФормы.ЗаполнитьРеквизитыПолучателяПоАвтоЗначениямРеквизитов(
		ЭтотОбъект, АвтоЗначенияРеквизитов, ИзменениеКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПодсказкуОчередностьПлатежа()
	
	ЗаполнитьСписокВыбора(Элементы.ОчередностьПлатежа, УчетДенежныхСредствКлиентСервер.ПолучитьСписокОчередностьПлатежа(Объект.Дата));
	ПлатежноеПоручениеФормыКлиентСервер.ОбновитьПодсказкуОчередностьПлатежа(ЭтотОбъект, Объект.ОчередностьПлатежа);
	
КонецПроцедуры

&НаСервере
Процедура ИзменениеПериодаПримененияПравилПлатежаВБюджет()
	
	НадписьРеквизитыПлатежейВБюджет = ПлатежноеПоручениеФормыКлиентСервер.НадписьРеквизитыПлатежейВБюджет(ЭтотОбъект);
	
	УстановитьРеквизитыПлательщикаНаСервере();
	
	ПлатежноеПоручениеФормы.НайтиОшибкиПлатежаВБюджет(ЭтотОбъект, ПредыдущаяДата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПлатежныеРеквизиты(Ответ, ДопПараметры) Экспорт
	
	ДлительнаяОперацияПроверкиПлатежныхРеквизитов = ПроверитьПлатежныеРеквизитыВФоне();
	
	ЗапуститьПроверкуФоновогоЗадания();
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПлатежныеРеквизитыВФоне() Экспорт
	
	Возврат ПлатежноеПоручениеФормы.ПроверитьПлатежныеРеквизитыВФоне(ЭтотОбъект);
	
КонецФункции

&НаСервере
Функция ПлатежныеРеквизиты()
	
	Возврат ПлатежноеПоручениеФормы.ПлатежныеРеквизиты(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура УстановитьСостояниеКонтрагента(Результат)
	
	ПлатежноеПоручениеФормы.УстановитьСостояниеКонтрагента(ЭтотОбъект, Результат);
	
КонецПроцедуры

&НаСервере
Функция УстановитьКонтрагентаНаСервере(Контрагент, СчетКонтрагента)
	
	Модифицированность = Истина;
	
	ИзменилсяКонтрагент = Объект.Контрагент <> Контрагент;
	
	Объект.Контрагент      = Контрагент;
	Объект.СчетКонтрагента = СчетКонтрагента;
	
	БанкИКассаФормы.ОпределитьКонтрагентЮрФизЛицо(ЭтотОбъект);
	
	Если ИзменилсяКонтрагент И Объект.ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога Тогда
		ПодобратьДоговор();
		ДоговорПриИзмененииНаСервере();
	КонецЕсли;
	
	ЗаполнитьВспомогательныеРеквизитыПолучателя();
	НадписьРеквизитыПолучателя = ПлатежноеПоручениеФормыКлиентСервер.УстановитьНадписьРеквизитыПолучателя(ЭтотОбъект);
	
	ДлительнаяОперацияПроверкиПлатежныхРеквизитов = ПлатежноеПоручениеФормы.ПроверитьПлатежныеРеквизитыВФоне(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура ПодобратьДоговор()
	
	ПараметрыПлатежа = Новый Структура;
	ПараметрыПлатежа.Вставить("ОплатаВВалюте", Новый Структура("ЗначениеОтбора", Ложь));
	ВидыДоговоров = ВидыДоговоровДокумента(Объект.ВидОперации);
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорКонтрагента, "ВидДоговора");
		Если ВидыДоговоров.Найти(ВидДоговора) = Неопределено Тогда
			Объект.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		Объект.ДоговорКонтрагента, Объект.Контрагент, Объект.Организация, ВидыДоговоров, ПараметрыПлатежа);
	
	ПредлагатьНовыйДоговор = Ложь;
	Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента)
		И Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику
		И ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") Тогда
		ПредлагатьНовыйДоговор = РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидПлатежаПоУмолчанию()
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидПлатежа) Тогда
		СохраненныйВид = ХранилищеОбщихНастроек.Загрузить("ПлатежноеПоручение_ВидПлатежа");
		Если СохраненныйВид <> "" Тогда
			Объект.ВидПлатежа = СохраненныйВид;
		Иначе
			Объект.ВидПлатежа = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПроверкуФоновогоЗадания()
	
	Если ДлительнаяОперацияПроверкиПлатежныхРеквизитов <> Неопределено Тогда
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверитьПлатежныеРеквизитыВФонеЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперацияПроверкиПлатежныхРеквизитов, ОповещениеОЗавершении, ПараметрыОжидания);
		
		ДлительнаяОперацияПроверкиПлатежныхРеквизитов = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПлатежныеРеквизитыВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПредотвратитьСбросРедактируемогоЗначения();
	
	УстановитьСостояниеКонтрагента(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредотвратитьСбросРедактируемогоЗначения() Экспорт
	
	// Обход особенности платформы, когда затирается редактируемое значение
	// в текущем элементе при изменении формы.
	Если НЕ ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
		Если ТипЗнч(ТекущийЭлемент) = Тип("ПолеФормы") И ТекущийЭлемент.Вид = ВидПоляФормы.ПолеВвода Тогда
			ТекущийЭлемент.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.ПриИзмененииЗначения;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СуммаВыплаченнойЗарплатыРаботнику(Знач Ссылка, Знач ФизЛицо, Знач ПлатежнаяВедомость, УчетЗарплатыИКадровВоВнешнейПрограмме)
	
	СуммаДокумента = 0;
	
	Если ЗначениеЗаполнено(ПлатежнаяВедомость) И ЗначениеЗаполнено(ФизЛицо) Тогда
		
		Ведомости = Новый Массив;
		Ведомости.Добавить(ПлатежнаяВедомость);
		
		ТаблицаНеоплаченныхВедомостей = УчетЗарплаты.ПолучитьДанныеВедомостейДляОплатыТаблично(Ссылка, Ведомости, ФизЛицо, Ложь);
		ТаблицаОплаченныхВедомостей   = УчетЗарплаты.ПолучитьДанныеВедомостейДляОплатыТаблично(Ссылка, Ведомости, ФизЛицо, Истина);
		
		ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"ВидОперации");
		
		Если ТаблицаНеоплаченныхВедомостей.Количество() = 0 И ТаблицаОплаченныхВедомостей.Количество() = 0 Тогда
			
			Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда Тогда
				СтрокаУточнениеДохода = НСтр("ru = 'о выплате по договору ГПХ'");
			Иначе
				СтрокаУточнениеДохода = НСтр("ru = 'о заработной плате'");
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В выбранной ведомости отсутствуют сведения %1 сотрудника %2!'"), СтрокаУточнениеДохода, ФизЛицо);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.ПлатежнаяВедомость");
			
		Иначе
			
			Если ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда Тогда
				СтрокаУточнениеДохода = НСтр("ru = 'сумма дохода по договору ГПХ'");
			Иначе
				СтрокаУточнениеДохода = НСтр("ru = 'заработная плата'");
			КонецЕсли;
			
			ВедомостьОплачена = ТаблицаОплаченныхВедомостей.Количество() <> 0;
			
			Если ВедомостьОплачена Тогда
				ТекстСообщения  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'По текущей ведомости %1 сотрудника %2 выплачена!'"), СтрокаУточнениеДохода, ФизЛицо);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.ПлатежнаяВедомость");
				СтрокаВедомости = ТаблицаОплаченныхВедомостей[0];
				СуммаДокумента  = СтрокаВедомости.СуммаКВыплате + СтрокаВедомости.КомпенсацияЗаЗадержкуЗарплаты;
			Иначе
				СтрокаВедомости = ТаблицаНеоплаченныхВедомостей[0];
				СуммаДокумента  = СтрокаВедомости.СуммаКВыплате + СтрокаВедомости.КомпенсацияЗаЗадержкуЗарплаты;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат СуммаДокумента;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоОплатаТруда(ВидОперации)
	
	Возврат ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику
		ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
		ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДепонентов
		ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда
		ИЛИ ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда;
	
КонецФункции

&НаСервере
Функция СуммаУдержанияРаботника(Знач Ссылка, Знач Организация, Знач Контрагент, Знач ФизЛицо, Знач ИсполнительныйЛист)
	
	РеквизитыПолученияДанных = УчетЗарплаты.СтруктураРеквизитовПолученияДанныхПоИСполнительномуДокументу();
	ЗаполнитьЗначенияСвойств(РеквизитыПолученияДанных, Объект);
	Возврат УчетЗарплаты.УдержаноПоИсполнительномуДокументу(РеквизитыПолученияДанных);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОчередностьПлатежаПо345ФЗ(Форма)
	
	Объект = Форма.Объект;
	
	Если Объект.Дата < УчетДенежныхСредствКлиентСервер.НачалоДействияНовыхПравилОчередностиПлатежа() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ОчередностьПлатежа = УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаПоУмолчаниюДо345ФЗ() Тогда
		Объект.ОчередностьПлатежа = УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаПоУмолчанию();
	ИначеЕсли Объект.ПеречислениеВБюджет Тогда
		ПлатежноеПоручениеФормыКлиентСервер.УстановитьОчередностьПлатежаПо345ФЗДляПеречисленияВБюджет(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОчередностьПлатежа()
	
	Если Объект.ПеречислениеВБюджет Тогда
		УстановитьОчередностьПлатежаПо345ФЗ(ЭтотОбъект);
	ИначеЕсли ЭтоОплатаТруда(Объект.ВидОперации)
		И Объект.ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда
		И Объект.ВидОперации <> Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда Тогда
		Объект.ОчередностьПлатежа = УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаОплатаТруда();
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПоИсполнительномуЛистуРаботника Тогда
		Объект.ОчередностьПлатежа = УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаПоИсполнительномуЛисту();
	Иначе
		Объект.ОчередностьПлатежа = УчетДенежныхСредствКлиентСервер.ОчередностьПлатежаПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораДоговораСКонтрагентом(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	НовыеПараметры = Новый Массив();
	ВидыДоговора   = ВидыДоговоровДокумента(Объект.ВидОперации);
	НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора",   Новый ФиксированныйМассив(ВидыДоговора)));
	НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.ОплатаВВалюте", Ложь));
	
	Элементы.ДоговорКонтрагента.ПараметрыВыбора = Новый ФиксированныйМассив(НовыеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура РеестрВыплатСамозанятымПриИзмененииНаСервере()
	
	ЗаполнитьСуммуПлатежаПоРееструВыплатСамозанятым();
	
КонецПроцедуры

&НаСервере
Процедура ПлатежнаяВедомостьПриИзмененииНаСервере()
	
	Если Не НазначениеПлатежаБылоИзмененоВручную Тогда
		АвтоЗначенияРеквизитов.Вставить("ТекстНазначенияПлатежа", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПлатежнаяВедомость) Тогда
		Объект.КодВидаДохода = УчетЗарплаты.КодВидаДоходаПоВидуДоходаИсполнительногоПроизводства(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ПлатежнаяВедомость,
				"ВидДоходаИсполнительногоПроизводства"));
	КонецЕсли;
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(
		Объект, АвтоЗначенияРеквизитов, НазначениеПлатежаБылоИзмененоВручную);
	
КонецПроцедуры

&НаСервере
Процедура ИсполнительныйЛистПриИзмененииНаСервере()
	
	Если НЕ НазначениеПлатежаБылоИзмененоВручную Тогда
		АвтоЗначенияРеквизитов.Вставить("ТекстНазначенияПлатежа", "");
	КонецЕсли;
	
	ЗаполнитьРеквизитыПлатежаВБюджет(ЭтотОбъект);
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(
		Объект, АвтоЗначенияРеквизитов, НазначениеПлатежаБылоИзмененоВручную);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВыбораНалогаПоКБК(Знач КодБК)
	
	Возврат Справочники.ВидыНалоговИПлатежейВБюджет.ДанныеВыбораПоКБК(КодБК);
	
КонецФункции

&НаСервереБезКонтекста
Функция РеквизитыПлатежаВБюджетПриИзмененииВидаНалоговогоОбязательства(Знач РеквизитыОбъекта)
	Перем ПериодПлатежа;
	
	ВидНалога = РеквизитыОбъекта.ВидНалога;
	
	Если Не ЗначениеЗаполнено(ПериодПлатежа)
		И (ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть
			Или ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела) Тогда
		ПоказателиПериода = ПлатежиВБюджетПереопределяемый.ПоказателиНалоговогоПериода(
			РеквизитыОбъекта.Организация,
			ВидНалога,
			РеквизитыОбъекта.Дата,
			РеквизитыОбъекта.ВидНалоговогоОбязательства);
		ПериодПлатежа = ПоказателиПериода.Период;
		РеквизитыОбъекта.ПоказательПериода = ПоказателиПериода.ПоказательПериода;
	ИначеЕсли РеквизитыОбъекта.Свойство("ПоказательПериода") Тогда
		ПоказателиПериода = ПлатежиВБюджетКлиентСервер.РазобратьНалоговыйПериод(РеквизитыОбъекта.ПоказательПериода);
		ПериодПлатежа = ПоказателиПериода.Дата;
	КонецЕсли;
	
	РеквизитыПлатежаВБюджет = Новый Структура("КодБК, НазначениеПлатежа, ПоказательПериода");
	
	Если ЗначениеЗаполнено(ПериодПлатежа) Тогда
		РеквизитыПлатежаВБюджет.КодБК = Справочники.ВидыНалоговИПлатежейВБюджет.КБКПоВидуНалоговогоОбязательства(
			РеквизитыОбъекта.КодБК, ВидНалога, РеквизитыОбъекта.ВидНалоговогоОбязательства, РеквизитыОбъекта.Дата, ПериодПлатежа);
	Иначе
		РеквизитыПлатежаВБюджет.КодБК = Справочники.ВидыНалоговИПлатежейВБюджет.КБКПоВидуНалоговогоОбязательства(
			РеквизитыОбъекта.КодБК, ВидНалога, РеквизитыОбъекта.ВидНалоговогоОбязательства, РеквизитыОбъекта.Дата);
	КонецЕсли;
	
	РеквизитыПлатежаВБюджет.НазначениеПлатежа = НазначениеПлатежаПриИзмененииПоказателяПериода(РеквизитыОбъекта);
	РеквизитыПлатежаВБюджет.ПоказательПериода = РеквизитыОбъекта.ПоказательПериода;
	
	Возврат РеквизитыПлатежаВБюджет;
	
КонецФункции

&НаСервереБезКонтекста
Функция НазначениеПлатежаПриИзмененииПоказателяПериода(Знач РеквизитыОбъекта)
	
	НазначениеПлатежа = Справочники.ВидыНалоговИПлатежейВБюджет.НазначениеПлатежа(
		РеквизитыОбъекта.Налог,
		РеквизитыОбъекта.ВидНалоговогоОбязательства,
		РеквизитыОбъекта.Организация,
		РеквизитыОбъекта.Дата,
		ПлатежиВБюджетКлиентСервер.ПредставлениеНалоговогоПериода(РеквизитыОбъекта.ПоказательПериода),
		РеквизитыОбъекта.ШаблонНазначенияПлатежаВБюджет);
	
	Возврат НазначениеПлатежа;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПлатежГосОргану(ВидОперации, Контрагент)
	
	Возврат УчетДенежныхСредствБП.ПлатежГосОргану(ВидОперации, Контрагент);
	
КонецФункции

&НаСервереБезКонтекста
Функция СтатьяДДСПриИзмененииВидаНалоговогоОбязательства(Знач РеквизитыОбъекта)
	
	КонтекстОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога;
	
	ВидНалоговогоОбязательства = РеквизитыОбъекта.ВидНалоговогоОбязательства;
	Если ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог 
		ИЛИ ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.НалогАкт
		ИЛИ ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.НалогСам Тогда
		
		Если УчетДенежныхСредствКлиентСервер.ЭтоПериодДоЕдиногоНалоговогоПлатежа(РеквизитыОбъекта.Дата) Тогда
			ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыОбъекта.Налог, "ВидНалога");
			Если ВидНалога = Перечисления.ВидыНалогов.НалогНаПрибыль_РегиональныйБюджет
				ИЛИ ВидНалога = Перечисления.ВидыНалогов.НалогНаПрибыль_ФедеральныйБюджет Тогда
					КонтекстОперации = "НалогНаПрибыль";
			КонецЕсли;
		Иначе
			УчетДенежныхСредствКлиентСервер.ОпределитьКонтекстОперацииНалогБезЕдиногоНалоговогоПлатежа(КонтекстОперации, РеквизитыОбъекта.Налог);
		КонецЕсли;
		
	КонецЕсли;
	
	СтатьяДДСПоУмолчанию = УчетДенежныхСредствВызовСервера.СтатьяДДСПоУмолчанию(КонтекстОперации, РеквизитыОбъекта.Дата);
	
	Если НЕ ЗначениеЗаполнено(СтатьяДДСПоУмолчанию) Тогда
		Если КонтекстОперации = "НалогНаПрибыль" Тогда
			СтатьяДДСПоУмолчанию = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
		Иначе
			СтатьяДДСПоУмолчанию = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтатьяДДСПоУмолчанию;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВидаНалоговогоОбязательстваОбработатьИзменение(Форма)
	
	РеквизитыОбъекта = Новый Структура("КодБК, Налог, ВидНалога, ВидНалоговогоОбязательства, Организация, Дата, ПоказательПериода, ШаблонНазначенияПлатежаВБюджет");
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, Форма.Объект);
	РеквизитыОбъекта.ВидНалога = Форма.ВидНалога;
	
	РеквизитыПлатежаВБюджет = РеквизитыПлатежаВБюджетПриИзмененииВидаНалоговогоОбязательства(РеквизитыОбъекта);
	ЗаполнитьЗначенияСвойств(Форма.Объект, РеквизитыПлатежаВБюджет);
	
	СтатьяДДСПоУмолчанию = СтатьяДДСПриИзмененииВидаНалоговогоОбязательства(РеквизитыОбъекта);
	Если СтатьяДДСПоУмолчанию <> Неопределено Тогда
		Форма.Объект.СтатьяДвиженияДенежныхСредств = СтатьяДДСПоУмолчанию;
	КонецЕсли;
	
	Форма.НадписьРеквизитыПлатежейВБюджет = ПлатежноеПоручениеФормыКлиентСервер.НадписьРеквизитыПлатежейВБюджет(Форма);
	
	УправлениеФормой(Форма);
	
КонецПроцедуры

&НаСервере
Процедура ТипКонтрагентаПриИзмененииНаСервере()
	
	ПлатежноеПоручениеФормы.УстановитьОграничениеТипаКонтрагента(ЭтотОбъект);
	КонтрагентПриИзмененииНаСервере(Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРедактированияРеквизитовПлатежаВБюджетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если Результат.Свойство("ПеречислениеВБюджет") Тогда
		Объект.ПеречислениеВБюджет = Результат.ПеречислениеВБюджет;
	КонецЕсли;
	
	Если Объект.ПеречислениеВБюджет Тогда
		
		ПлатежноеПоручениеФормыКлиент.ЗаполнитьРеквизитыПлатежаВБюджетПослеРедактированияВФорме(ЭтотОбъект, Результат);
		
		Если Результат.Модифицированность Тогда
			УплатаНалогаЗаТретьихЛиц = Объект.ВидОперации
				= ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц");
			Если УплатаНалогаЗаТретьихЛиц Тогда
				
				ПерезаполнитьНазначениеПлатежаНаСервере();
				
			Иначе
				
				РеквизитыОбъекта = ПлатежноеПоручениеФормыКлиентСервер.РеквизитыОбъектаДляФормированияНазначенияПлатежа(ЭтотОбъект);
				
				НазначениеПлатежа = НазначениеПлатежаПриИзмененииПоказателяПериода(РеквизитыОбъекта);
				Если НЕ ПустаяСтрока(НазначениеПлатежа) Тогда
					Если НЕ НазначениеПлатежаБылоИзмененоВручную
						ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога") Тогда
						Объект.НазначениеПлатежа = НазначениеПлатежа;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		УстановитьОчередностьПлатежаПо345ФЗ(ЭтотОбъект);
		ПлатежноеПоручениеФормыКлиентСервер.ОбновитьПодсказкуОчередностьПлатежа(ЭтотОбъект, Объект.ОчередностьПлатежа);
		
		// Заменим (добавим) УИН (уникальный идентификатор начисления)
		// С 01 января 2014 по 30 марта 2014 он указывается в назначении платежа
		ПлатежиВБюджетКлиентСервер.ЗаменитьВНазначенииПлатежаУИН(
			Объект.НазначениеПлатежа,
			Объект.ИдентификаторПлатежа,
			Объект.Дата,
			Истина);
		
	Иначе
		
		ПлатежноеПоручениеФормыКлиентСервер.СброситьПризнакПлатежаВБюджет(ЭтотОбъект);
		
	КонецЕсли;
	
	НадписьРеквизитыПлатежейВБюджет = ПлатежноеПоручениеФормыКлиентСервер.НадписьРеквизитыПлатежейВБюджет(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыЗаписи = Новый Структура("Закрыть", Истина);
		Если Записать(ПараметрыЗаписи) Тогда
			Закрыть();
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаписатьСОшибкамиЗавершение(Ответ, ПараметрыЗаписи) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПараметрыЗаписи.Вставить("ПропуститьПроверку", Истина);
		Если (Параметры.Ключ.Пустая() Или Модифицированность)
			И ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			// Сначала запишем документ, а потом проведем - иначе из-за проверки заполнения документ не только не проведется,
			// но и не запишется.
			ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Запись;
			Записать(ПараметрыЗаписи);
			ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
			Записать(ПараметрыЗаписи);
			
			// Форму в этом случае не закрываем - т.к. с большой вероятностью
			// при попытке проведения будут выданы ошибки проверки заполнения и документ не проведется.
			// Если закрыть форму, то эти ошибки пользователь не увидит, т.к. они привязаны к родительскому окну документа.
			// В итоге документ окажется не проведен, что необъяснимо для пользователя:
			//  - он нажимал "Провести и закрыть"
			//  - согласился выполнить это без проверки
			//  - а результат не соответствует ожиданиям.
			Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
				ПараметрыЗаписи.Закрыть = Ложь;
			КонецЕсли;
		Иначе
			Записать(ПараметрыЗаписи);
		КонецЕсли;
		
		Если ПараметрыЗаписи.Свойство("Закрыть") И ПараметрыЗаписи.Закрыть Тогда
			Закрыть();
		ИначеЕсли ПараметрыЗаписи.Свойство("ОткрытьПравилоРегулярногоПлатежа") Тогда
			ОткрытьПравилоРегулярногоПлатежа();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросРедактированиеПравилаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПараметрыЗаписи = Новый Структура("ОткрытьПравилоРегулярногоПлатежа");
		
		Если Записать(ПараметрыЗаписи) Тогда
			
			ОткрытьПравилоРегулярногоПлатежа();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЭтоУплатаНалогаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьВидОперацииУплатаНалога();
	
	ЗапуститьПроверкуФоновогоЗадания();
	
	УведомитьПользователяПлатежиВБюджет();
	
	// СПАРК Риски
	ИндексыСПАРКРиски = Неопределено; // Сбросить полученные значения
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьОтображениеИндексыСПАРК", 0.1, Истина);
	// Конец СПАРК Риски
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКонтрагентаExtendedTooltipОбработкаНавигационнойСсылкиЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") И РезультатВыполнения.Свойство("ПерезаполнитьКонтрагента") Тогда
		
		УстановитьКонтрагентаНаСервере(РезультатВыполнения.Контрагент, РезультатВыполнения.СчетКонтрагента);
		
	КонецЕсли;
	
	ЗапуститьПроверкуФоновогоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКодИзПеречняКВВО()

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",             "Документ");
	ПараметрыФормы.Вставить("НазваниеОбъекта",        "ПлатежноеПоручение");
	ПараметрыФормы.Вставить("НазваниеМакета",         "КлассификаторКВВО");
	ПараметрыФормы.Вставить("ТекущийПериод",          ТекущаяДата());
	ПараметрыФормы.Вставить("ТекущийКод",             Объект.КодВалютнойОперации);
		
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьКодИзКлассификатораЗавершение", 
		ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,, ОповещениеОЗакрытии);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКодИзКлассификатораЗавершение(РезультатЗакрытия, ПараметрыЗавершенияВыбора) Экспорт
	
	ВыбранныйКод = РезультатЗакрытия;
	
	Если ВыбранныйКод <> Неопределено Тогда
		
		Модифицированность = Истина;
		Объект.КодВалютнойОперации = ВыбранныйКод;
		ПерезаполнитьНазначениеПлатежаНаСервере();

	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиВалютногоКонтроля(Форма)

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(Объект.СчетОрганизации) Тогда
		БанкРасчетногоСчета = БанкРасчетногоСчета(Объект.СчетОрганизации);
		РеквизитыБанка = БанковскиеСчетаВызовСервера.ПолучитьРеквизитыБанкаИзСправочника(БанкРасчетногоСчета);
		ИностранныйБанк = Не РеквизитыБанка.ЯвляетсяБанкомРФ;
	Иначе 
		ИностранныйБанк = Ложь;
	КонецЕсли;
	Элементы.КодВалютнойОперации.Видимость = ИностранныйБанк Или УстановленаФункциональнаяОпцияВалютныйКонтроль();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция БанкРасчетногоСчета(СчетОрганизации) 
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СчетОрганизации, "Банк");
КонецФункции

&НаСервереБезКонтекста
Функция УстановленаФункциональнаяОпцияВалютныйКонтроль() 
	Возврат ПолучитьФункциональнуюОпцию("УчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров");
КонецФункции

#Область ОбластьКодВидаДохода

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьСписокВыбораКодВидаДохода(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ПлатежноеПоручениеФормыКлиентСервер.НастроитьСписокВыбораКодВидаДохода(
		Элементы.УдержаниеПоИсполнительномуЛисту.СписокВыбора,
		Объект.ВидОперации,
		Форма.ПеречислениеНаЛичныйСчет,
		Объект.Дата);
	УстановитьПредставлениеКодаУдержанияПоИспЛисту(Форма, Объект.КодВидаДохода);
	
КонецПроцедуры

&НаКлиенте
Функция ОформлениеФормыВыбораКодаВидаДохода()
	
	Описание = Новый Структура;
	Описание.Вставить("Заголовок", НСтр("ru = 'Удержание по исполнительному листу'"));
	Описание.Вставить("ВидимостьПериода", Ложь);
	Описание.Вставить("Пояснение", НСтр("ru = 'Выберите код вида дохода'"));
	Описание.Вставить("ПоказыватьПустоеЗначение", Ложь);
	
	Если Элементы.УдержаниеПоИсполнительномуЛисту.СписокВыбора.Количество() = 0 Тогда
		Возврат Описание;
	КонецЕсли;
	
	ПервыйЭлемент = Элементы.УдержаниеПоИсполнительномуЛисту.СписокВыбора[0];
	Код = КодИзПредставления(ПервыйЭлемент.Значение);
	Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Код) Тогда
		Описание.ПоказыватьПустоеЗначение = Истина;
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

&НаКлиенте
Процедура ВыборКодаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ВыбранныйКод = РезультатЗакрытия;
	Если ВыбранныйКод <> Неопределено Тогда
		Модифицированность = Истина;
		Объект.КодВидаДохода = ВыбранныйКод;
		УстановитьПредставлениеКодаУдержанияПоИспЛисту(ЭтотОбъект, ВыбранныйКод);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КодИзПредставления(Представление)
	
	Возврат Лев(Представление, 1);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредставлениеКодаУдержанияПоИспЛисту(Форма, ВыбранныйКод)
	
	Элементы = Форма.Элементы;
	
	Если Элементы.УдержаниеПоИсполнительномуЛисту.СписокВыбора.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ВыбранныйКод) Тогда
		ПервыйЭлемент = Элементы.УдержаниеПоИсполнительномуЛисту.СписокВыбора[0];
		Код = КодИзПредставления(ПервыйЭлемент.Значение);
		Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Код) Тогда
			Форма.УдержаниеПоИсполнительномуЛисту = ПервыйЭлемент.Значение;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Форма.УдержаниеПоИсполнительномуЛисту = ПлатежноеПоручениеФормыКлиентСервер.ПолучитьПредставлениеПоКоду(
		Элементы.УдержаниеПоИсполнительномуЛисту.СписокВыбора, ВыбранныйКод);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура КодВидаДоходаПоУмолчанию(Форма, ИзменениеДаты = Ложь)
	
	Объект = Форма.Объект;
	Объект.КодВидаДохода = ПлатежноеПоручениеФормыКлиентСервер.КодВидаДоходаПоУмолчанию(Форма, ИзменениеДаты);
	УстановитьПредставлениеКодаУдержанияПоИспЛисту(Форма, Объект.КодВидаДохода);
	
КонецПроцедуры

#КонецОбласти

#Область ДиректБанк

&НаСервере
Процедура СостояниеПриИзмененииНаСервере()

	Если Состояние = Перечисления.СостоянияБанковскихДокументов.Оплачено Тогда
		
		ДокументСписания = Документы.ПлатежноеПоручение.НайтиДокументСписания(
			Объект.Ссылка, Объект.ДокументОснование, НадписьДокументСписания);
			
	КонецЕсли;
		
	ПоказатьИндексыСПАРК = ПоказатьИнформациюСпарк(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СостояниеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСБанкамиКлиент.ОткрытьАктуальныйЭД(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваПоляСостояние(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.Состояние.Видимость = НЕ Форма.ПодключенаИнтеграцияСБанком;
	Элементы.СостояниеИнтеграцияСБанком.Видимость = Форма.ПодключенаИнтеграцияСБанком И ЗначениеЗаполнено(Форма.Состояние);
	
	Элементы.ГруппаПредупреждениеОбОтказеБанка.Видимость = 
		НЕ Форма.ПодключенаИнтеграцияСБанком
		И Форма.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отклонено")
		И ЗначениеЗаполнено(Форма.ПричинаОтклоненияБанком);

	Если Элементы.ГруппаПредупреждениеОбОтказеБанка.Видимость Тогда 
		Элементы.НадписьДокументСписания.Видимость = Ложь;

		ДанныеПодсказки = Новый Массив;
		ДанныеПодсказки.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Этот документ был отклонен банком. Для повтора платежа создайте новое платежное поручение.'")));
		ДанныеПодсказки.Добавить(" ");
		ДанныеПодсказки.Добавить(Новый ФорматированнаяСтрока(
			НСтр("ru = 'Причина отказа'"),,,, "ПричинаОтказа"));
			
		Элементы.ДекорацияЗаголовокПричиныОтказаБанка.Заголовок = Новый ФорматированнаяСтрока(ДанныеПодсказки);
		
	ИначеЕсли Форма.ИспользуетсяСогласованиеПлатежей Тогда     
		Элементы.НадписьДокументСписания.Видимость = 
			НЕ Форма.ПодключенаИнтеграцияСБанком И Форма.Согласовано Или ЗначениеЗаполнено(Форма.ДокументСписания);    
		
	Иначе
		Элементы.НадписьДокументСписания.Видимость = 
			НЕ Форма.ПодключенаИнтеграцияСБанком Или ЗначениеЗаполнено(Форма.ДокументСписания);
	КонецЕсли; 
	
	Если Элементы.Состояние.Видимость Тогда
		НастроитьПолеСостояние(Форма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьПолеСостояние(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если Форма.ТолькоПросмотр
		ИЛИ НЕ ЗначениеЗаполнено(Объект.Организация)
		ИЛИ НЕ ЗначениеЗаполнено(Объект.СчетОрганизации) Тогда
		Элементы.Состояние.ТолькоПросмотр = Истина;
		Элементы.Состояние.КнопкаОткрытия = Ложь;
	Иначе
		Если НЕ Форма.ПодключенаИнтеграцияСБанком Тогда
			СписокВыбора = Новый Массив;
			ЕстьНастройкаДиректБанк = Ложь;
			НастройкаДиректБанк(ЕстьНастройкаДиректБанк, Объект.Организация, Объект.СчетОрганизации, СписокВыбора);
			Элементы.Состояние.СписокВыбора.ЗагрузитьЗначения(СписокВыбора);
			Элементы.Состояние.ТолькоПросмотр = ЕстьНастройкаДиректБанк;
			Элементы.Состояние.КнопкаОткрытия = ЕстьНастройкаДиректБанк;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НастройкаДиректБанк(ЕстьНастройкаДиректБанк, Знач Организация, Знач БанковскийСчет, СписокВыбора)
	
	ПлатежноеПоручениеФормы.НастройкаДиректБанк(ЕстьНастройкаДиректБанк, Организация, БанковскийСчет, СписокВыбора);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеОбмена(Ссылка)
	
	Результат = Новый Структура;
	Результат.Вставить("Состояние");
	Результат.Вставить("Утвердил"); 
	Результат.Вставить("Согласовано");
	Результат.Вставить("ПричинаОтклоненияБанком",ОбменСБанками.ПолучитьПричинуОтклонениияДокументаБанком(Ссылка));
	
	СостояниеДокумента = РегистрыСведений.СостоянияБанковскихДокументов.СостояниеДокумента(Ссылка); 	
	Если СостояниеДокумента <> Неопределено Тогда
		Результат.Вставить("Состояние", СостояниеДокумента.Состояние);
		Результат.Вставить("Утвердил", СостояниеДокумента.Ответственный); 
		Результат.Вставить("Согласовано", СостояниеДокумента.Согласовано);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Баннер

&НаСервере
Процедура ОбработатьИПоказатьСостоянияОтправки()
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ИнтеграцияСБанкамиФормы.ПоказатьБаннерНовогоДокумента(ЭтотОбъект, Объект.СчетОрганизации);
		УстановитьВидимостьНомера();
		
	Иначе
		
		СостоянияИнтеграцииДокументов = РегистрыСведений.ДокументыИнтеграцииСБанком.СостоянияИнтеграцииДокументов(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
		
		ИнтеграцияСБанкамиФормы.ПолучитьДанныеИПоказатьБаннерСостоянияОтправки(ЭтотОбъект, СостоянияИнтеграцииДокументов);
		ПолучитьИПоказатьНомерДокументаИнтеграции();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОбновлениеБаннераСостоянияОтправки()
	
	Если ПодключенаИнтеграцияСБанком
		И ЗначениеЗаполнено(Объект.Ссылка)
		И ИнтервалПроверкиСостоянияИнтеграцииСБанком > 0 Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЗапуститьОбновлениеБаннераСостояниеОтправки", ИнтервалПроверкиСостоянияИнтеграцииСБанком, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗапуститьОбновлениеБаннераСостояниеОтправки() Экспорт
	
	Если ХэшДанныхБаннера(Объект.Ссылка) <> ХешДанныхБаннера Тогда
		ОбработатьИПоказатьСостоянияОтправки();
	КонецЕсли;
	ЗапуститьОбновлениеБаннераСостоянияОтправки();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ХэшДанныхБаннера(Знач ДокументОплаты)
	
	ДокументыОплаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОплаты);
	Возврат ИнтеграцияСБанкамиФормы.ХэшДанныхБаннера(ДокументыОплаты);
	
КонецФункции

&НаСервере
Процедура ПолучитьИПоказатьНомерДокументаИнтеграции()
	
	НомерИнтеграции = "";
	Если Документы.ПлатежноеПоручение.НомерПлатежногоПорученияИзвестен(Объект.Ссылка, Объект.СчетОрганизации) Тогда
		НомерИнтеграции = Объект.Номер;
		АвтоЗаголовок = Истина;
	Иначе
		АвтоЗаголовок = Ложь;
		Заголовок = СтрШаблон(НСтр("ru = 'Платежное поручение %1 от %2'"), " ", Формат(Объект.Дата, "ДЛФ=D"));
	КонецЕсли;
	
	УстановитьВидимостьНомера();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьНомера()
	
	Если ЗначениеЗаполнено(Объект.СчетОрганизации)
		И ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВключена(Объект.СчетОрганизации) Тогда
		Элементы.Номер.Видимость = Ложь;
		Элементы.НомерИнтеграции.Видимость = Истина;
		Если ПустаяСтрока(НомерИнтеграции) Тогда
			Элементы.НомерИнтеграции.Подсказка =
				НСтр("ru = 'Номер будет присвоен после передачи документа в Личный кабинет банка'");
		Иначе
			Элементы.НомерИнтеграции.Подсказка = "";
		КонецЕсли;
	Иначе
		Элементы.Номер.Видимость = Истина;
		Элементы.НомерИнтеграции.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаСчетовИБанков

&НаКлиентеНаСервереБезКонтекста
Функция ПоказыватьИнформациюНадежностьБанков(Форма)

	Если НЕ ЗначениеЗаполнено(Форма.Объект.СчетКонтрагента) 
		ИЛИ НЕ ЗначениеЗаполнено(Форма.СчетПолучателяБИКБанка)
		ИЛИ НЕ Форма.СчетПолучателяВБанкеРФ
		ИЛИ Форма.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Оплачено")
		ИЛИ Форма.Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога")
		ИЛИ Форма.Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц") Тогда
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПоказатьИнформациюНадежностьБанков()
	
	НадежностьБанковКлиент.ПоказатьИнформациюНадежностьБанков(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОшибкуСчетаОрганизации()
	
	РезультатПроверкиСчета = БанковскиеСчетаСервер.ПроверитьКорректностьБанковскогоСчета(Объект.СчетОрганизации, Объект.Дата);
	Если РезультатПроверкиСчета.ЕстьОшибки Тогда
		ПризнакСчета = БанковскиеСчетаСервер.НовыйПризнакСчета();
		ПризнакСчета.СчетОрганизации = Истина;
		ОшибкаСчетаОрганизации = БанковскиеСчетаСервер.ТекстОшибкиБанковскогоСчета(РезультатПроверкиСчета, ПризнакСчета);
	Иначе
		ОшибкаСчетаОрганизации = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьПредупрежденияОбОшибкахСчетаОрганизации(Форма)
	
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(Форма.ОшибкаСчетаОрганизации) Тогда
		Элементы.ГруппаСчетОрганизацииПроверка.ЦветФона = Форма.ЦветОшибкиБанковскогоСчета;
		Если Не Форма.ИспользоватьНесколькоБанковскихСчетовОрганизации Тогда
			#Если Сервер Тогда
			Форма.ТекстПредупрежденияСчетОрганизации = СтроковыеФункции.ФорматированнаяСтрока(
				ТекстПредупрежденияСчетОрганизации(),
				Форма.ОшибкаСчетаОрганизации);
			#Иначе
			Форма.ТекстПредупрежденияСчетОрганизации = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
				ТекстПредупрежденияСчетОрганизации(),
				Форма.ОшибкаСчетаОрганизации);
			#КонецЕсли
		Иначе
			Форма.ТекстПредупрежденияСчетОрганизации = "";
		КонецЕсли;
		Элементы.СчетОрганизации.РасширеннаяПодсказка.Заголовок = Форма.ОшибкаСчетаОрганизации;
		Элементы.СчетОрганизации.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элементы.ГруппаСчетОрганизацииПроверка.ЦветФона = Форма.ЦветОтсутствияОшибок;
		Форма.ТекстПредупрежденияСчетОрганизации = "";
		Элементы.СчетОрганизации.РасширеннаяПодсказка.Заголовок = "";
		Элементы.СчетОрганизации.ОтображениеПодсказки = ОтображениеПодсказки.Авто;
	КонецЕсли;
	
	Элементы.ГруппаПредупреждениеОНекорректномРасчетномСчете.Видимость =
		ЗначениеЗаполнено(Форма.ТекстПредупрежденияСчетОрганизации);
	Если Элементы.ГруппаПредупреждениеОНекорректномРасчетномСчете.Видимость Тогда
		Элементы.ПредупреждениеОЗаполненииРеквизитовСчета.Видимость = Ложь;
	Иначе
		Форма.Элементы.ПредупреждениеОЗаполненииРеквизитовСчета.Видимость =
			ПроверкаРеквизитовОрганизацииКлиентСервер.ПоказатьПредупреждениеОРеквизитахСчета(Форма, Форма.Объект.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекстПредупрежденияСчетОрганизации()
	
	Возврат НСтр("ru = '%1. Добавьте новый <a href=''СписокСчетовСсылка''>банковский счет</a>'");
	
КонецФункции

#КонецОбласти

#Область СПАРК

// Обновляет отображение индексов СПАРК Риски.
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура Подключаемый_ОбновитьОтображениеИндексыСПАРК()
	
	ПредотвратитьСбросРедактируемогоЗначения();
	
	ОбновитьОтображениеИндексыСПАРК();
	
КонецПроцедуры

// Обновляет отображение индексов СПАРК Риски.
//
// Параметры:
//  Нет.
//
&НаКлиенте
Процедура ОбновитьОтображениеИндексыСПАРК()
	
	Если ПоказатьИндексыСПАРК Тогда
		ПараметрыОтображения = Новый Структура("ВариантОтображения", "Однострочный");
		СПАРКРискиКлиент.ОтобразитьИндексыСПАРК(
			ИндексыСПАРКРиски,
			Неопределено,
			Объект.Контрагент, // Искать по ссылке
			ВидКонтрагентаСПАРК,
			ЭтотОбъект,
			ПараметрыОтображения);
	Иначе
		Элементы.ГруппаИндексыСПАРКРиски.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоказатьИнформациюСпарк(Форма)
	
	Объект = Форма.Объект;
	Если ТипЗнч(Объект.Контрагент) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтранаРегистрации         = Неопределено;
	ГосударственныйОрган      = Ложь;
	ОбособленноеПодразделение = Ложь;
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Контрагент, 
			"СтранаРегистрации, ГосударственныйОрган, ОбособленноеПодразделение");
		СтранаРегистрации         = ЗначенияРеквизитов.СтранаРегистрации;
		ГосударственныйОрган      = ЗначенияРеквизитов.ГосударственныйОрган;
		ОбособленноеПодразделение = ЗначенияРеквизитов.ОбособленноеПодразделение;
	КонецЕсли;
	
	ПоказатьИнформацию = (Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыдачаЗаймаКонтрагенту
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.РасчетыПоКредитамИЗаймам
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратЗайма
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПрочиеРасчетыСКонтрагентами)
		И Форма.Состояние <> Перечисления.СостоянияБанковскихДокументов.Оплачено
		И СтранаРегистрации = Справочники.СтраныМира.Россия
		И Не ОбособленноеПодразделение
		И Не ГосударственныйОрган;
	
	Возврат ПоказатьИнформацию;
	
КонецФункции

#КонецОбласти

#Область ПодключаемыеКомандыБиблиотек

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПроверяемыеКомандыНесогласованныхПлатежей
		= УчетДенежныхСредствКлиент.ПроверяемыеКомандыЭДОНесогласованныхПлатежей();
	ПроверяемыеКомандыОтклоненныхПлатежей
		= УчетДенежныхСредствКлиент.ПроверяемыеКомандыЭДООтклоненныхПлатежей();
	
	Если ИспользуетсяСогласованиеПлатежей Тогда
		Если ЭтотОбъект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отклонено")
			И ПроверяемыеКомандыОтклоненныхПлатежей.Найти(Команда.Имя) <> Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Платеж отклонен'"));
			Возврат;
		КонецЕсли;
		
		Если ЭтотОбъект.Состояние = ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Подготовлено")
			И ПроверяемыеКомандыНесогласованныхПлатежей.Найти(Команда.Имя) <> Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Платеж не согласован'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоУплатаАУСН(УплатаНалога, Налог)

	ЭтоУплатаАУСН = Ложь;
	
	Если УплатаНалога И ЗначениеЗаполнено(Налог) Тогда
		НалогАУСН = РасчетыСБюджетомВызовСервера.НалогПоВиду(ПредопределенноеЗначение("Перечисление.ВидыНалогов.АУСН"));
		Если Налог = НалогАУСН Тогда
			ЭтоУплатаАУСН = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭтоУплатаАУСН;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоУплатаВзносаПриАУСН(УплатаНалога, Налог)

	ЭтоУплатаАУСН = Ложь;
	
	Если УплатаНалога И ЗначениеЗаполнено(Налог) Тогда
		ВзносыПриАУСН = РасчетыСБюджетомВызовСервера.НалогПоВиду(ПредопределенноеЗначение("Перечисление.ВидыНалогов.СтраховыеВзносы_ФСС_НСиПЗ"));
		Если Налог = ВзносыПриАУСН Тогда
			ЭтоУплатаАУСН = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЭтоУплатаАУСН;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВидыДоговоровДокумента(ВидОперации)
	
	Возврат РаботаСДоговорамиКонтрагентовБПВызовСервера.ВидыДоговоровДокумента(
		ВидОперации, Тип("ДокументСсылка.ПлатежноеПоручение"));
	
КонецФункции

&НаСервере
Функция ПараметрыСозданияНовогоДоговора()
	
	Возврат РаботаСДоговорамиКонтрагентовБП.ПараметрыСозданияНовогоДоговора(ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_НачатьПолучениеСтатусаСамозанятого()
	
	ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБПКлиент.ДатаПолученияСтатуса(Объект.Дата);
	СохраненныйСтатус = ИнтеграцияСтатусСамозанятогоБПКлиент.СтатусСамозанятого(
		ЭтотОбъект, Объект.ДоговорКонтрагента, ДатаПолученияСтатуса);
	
	Если СохраненныйСтатус <> Неопределено Тогда
		СтатусСамозанятого = РезультатПроверкиСтатусаСамозанятого(
			СохраненныйСтатус.ПлательщикНПД, ДатаПолученияСтатуса, СохраненныйСтатус.ДатаРегистрации);
	Иначе
		СтатусСамозанятого = Неопределено;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РезультатПроверкиСтатусаСамозанятого(ПлательщикНПД, ДатаПолученияСтатуса, ДатаРегистрации = Неопределено, ТекстОшибки = "")
	
	Результат = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.НовыйСтатусСамозанятого();
	Результат.ТекстОшибки = ТекстОшибки;
	Результат.ПлательщикНПД = ПлательщикНПД;
	Если Не ЗначениеЗаполнено(ТекстОшибки) И Не ПлательщикНПД Тогда
		Результат.КраткоеОписание = ИнтеграцияСтатусСамозанятогоКлиентСервер.ОписаниеСтатуса(
			ПлательщикНПД, ДатаРегистрации);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Результат.Описание =
			ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ОписаниеСтатусаСамозанятогоНаДатуДокумента(
				ПлательщикНПД, ДатаРегистрации, ДатаПолученияСтатуса);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПрочитатьСтатусСамозанятого()
	
	ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБП.ДатаПолученияСтатуса(Объект.Дата);
	СохраненныйСтатус = ИнтеграцияСтатусСамозанятогоБП.СохраненныйСтатус(
		Объект.ДоговорКонтрагента, ДатаПолученияСтатуса);
	Если СохраненныйСтатус <> Неопределено Тогда
		СтатусСамозанятого = РезультатПроверкиСтатусаСамозанятого(
			СохраненныйСтатус.ПлательщикНПД, ДатаПолученияСтатуса, СохраненныйСтатус.ДатаРегистрации);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьУстановитьСчетОрганизацииПоУмолчанию() Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.СчетОрганизации) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументМожетБытьОформленВЦР = Документы.ПлатежноеПоручение.ДокументМожетБытьОформленВЦифровыхРублях(
		Объект.ВидОперации, Объект.Дата, Объект.Контрагент);
	Если ДокументМожетБытьОформленВЦР Тогда
		Возврат;
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	СчетОрганизацииЦР = ЭтоЦифровойСчет(Объект.СчетОрганизации);
	Если СчетОрганизацииЦР Тогда
		Объект.СчетОрганизации = Справочники.БанковскиеСчета.ПустаяСсылка();
		УчетДенежныхСредствБП.УстановитьБанковскийСчет(
			Объект.СчетОрганизации, Объект.Организация,
			ВалютаРегламентированногоУчета);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоЦифровойСчет(БанковскийСчет)
	
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Справочники.БанковскиеСчета.ЭтоЦифровойСчет(БанковскийСчет);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМКШапкаФормы();
	АдаптироватьМККоманднаяПанельФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы,
		Элементы.ГруппаПредупреждения);
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьНомерДатаФормаДокумента(Элементы, Элементы.ГруппаНомераИДаты);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаНомераИДаты);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаВидОперации);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Организация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПеречислениеНалогаЗаТретьихЛиц);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаСчетОрганизацииПроверка);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПеречислениеПоИсполнительномуЛистуСотрудника);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПеречислениеНалога);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.НадписьРеквизитыПлательщика);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПолучатель);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаЛевая);
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаПравая);
	
	Элементы.ГруппаКомментарий.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.НадписьСледующийПлатеж);
	
	Элементы.СтавкаНДС.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.СтавкаНДС.РастягиватьПоГоризонтали = Истина;
	Элементы.СтавкаНДС.Ширина = 0;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы,Элементы.ГруппаПлатеж);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементФормы(ЭтотОбъект, Элементы.НазначениеПлатежа);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.НазначениеПлатежа);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаПодвал);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаКнопкиКоманднойПанели.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, Элементы.ПодменюПечать.ПодчиненныеЭлементы);
	
	ПечатьПлатежноеПоручение = Элементы.Найти("ПодменюПечатьОбычное__ПлатежноеПоручение");
	Если ПечатьПлатежноеПоручение <> Неопределено Тогда
		МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВПодменюМКПоделиться(Элементы, ПечатьПлатежноеПоручение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РедактированиеКомментарияВЗакрытомПериоде

&НаКлиенте
Процедура Подключаемый_РедактироватьКомментарий(Команда)
	
	КомментарииДокументовЗакрытогоПериодаКлиент.ИзменитьКомментарий(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВидОплатыПоРееструПриИзмененииНаСервере()
	
	ЗаполнитьСуммуПлатежаПоРееструВыплатСамозанятым();
	ПлатежноеПоручениеФормы.УстановитьОграничениеТипаКонтрагента(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСуммуПлатежаПоРееструВыплатСамозанятым()
	
	Если ЗначениеЗаполнено(Объект.РеестрВыплатСамозанятым) Тогда
		Если ВидОплатыПоРеестру = ВыплатыСамозанятымФормыБПКлиентСервер.ЗначениеОплачиваетсяВесьРеестр() Тогда
			Объект.СуммаДокумента = РегистрыНакопления.ЗадолженностьСамозанятыхПоРеестру.СуммаЗадолженностиПоРеестру(
				Объект.Ссылка, Объект.РеестрВыплатСамозанятым);
		Иначе
			Объект.СуммаДокумента = РегистрыНакопления.ЗадолженностьСамозанятыхПоРеестру.СуммаЗадолженностиПоСамозанятому(
				Объект.Ссылка, Объект.РеестрВыплатСамозанятым, Объект.Контрагент, Объект.СчетКонтрагента);
		КонецЕсли;
	Иначе
		Объект.СуммаДокумента = 0;
	КонецЕсли;
	
	Если Не НазначениеПлатежаБылоИзмененоВручную Тогда
		АвтоЗначенияРеквизитов.Вставить("ТекстНазначенияПлатежа", "");
	КонецЕсли;
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(
		Объект, АвтоЗначенияРеквизитов, НазначениеПлатежаБылоИзмененоВручную);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидОплатыСамозанятымПоРеестру(ВидКонтрагента)
	
	Если ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		Возврат ВыплатыСамозанятымФормыБПКлиентСервер.ЗначениеОплачиваетсяОднаСтрокаРеестра();
	Иначе
		Возврат ВыплатыСамозанятымФормыБПКлиентСервер.ЗначениеОплачиваетсяВесьРеестр();
	КонецЕсли;
	
КонецФункции

#КонецОбласти
