#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Ключ.Пустая() Тогда
		Взаимодействия.УстановитьПредметПоДаннымЗаполнения(Параметры, Предмет);
		Объект.Входящий = Не (Параметры.Свойство("Основание")
			И Не (Параметры.Основание = Ложь Или Параметры.Основание = Неопределено));
	КонецЕсли;
	
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект,Параметры);
	
	Если Параметры.Свойство("ДанныеУчастника") И Объект.Контрагент <> Параметры.ДанныеУчастника.Контакт Тогда
		ЗаполнитьНаОснованииУчастника(Параметры.ДанныеУчастника);
	КонецЕсли;

	Если Параметры.Свойство("ЗначенияЗаполнения") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
		ЗначенияЗаполнения = Параметры.ЗначенияЗаполнения;
		Если ТипЗнч(ЗначенияЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(Объект, ЗначенияЗаполнения);
			
			Если ЗначенияЗаполнения.Свойство("ПредметВзаимодействия") 
				И ЗначениеЗаполнено(ЗначенияЗаполнения.ПредметВзаимодействия) Тогда
				Предмет = ЗначенияЗаполнения.ПредметВзаимодействия;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ВзаимодействияФормыБП.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(СтруктураНовогоТелефона) Тогда
		ДобавитьТелефонКонтактномуЛицуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзмененоСостояниеЛидов") Тогда
		ПараметрыЗаписи.Вставить("ИзмененоСостояниеЛидов", ТекущийОбъект.ДополнительныеСвойства.ИзмененоСостояниеЛидов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ВзаимодействияКлиент.ВзаимодействиеПредметПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи, "ТелефонныйЗвонок");
	
	ИзмененоСостояниеЛидов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыЗаписи, "ИзмененоСостояниеЛидов", Ложь);
	Если ИзмененоСостояниеЛидов Тогда
		ОповеститьОбИзменении(Тип("СправочникСсылка.КонтактныеЛица"));
		Оповестить("ИзмененоСостояниеЛидов", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.КонтактноеЛицо));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПроверитьЗаполнениеРеквизитов(Отказ);
	
	Объект.Предмет = Предмет;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)

	ОчиститьКонтактнуюИнформацию();
	
	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		КонтактноеЛицоПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьВсеРеквизиты(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Ответственный", Объект.Ответственный);
	СтруктураЗаполнения.Вставить("Тема", Объект.Тема);
	СтруктураЗаполнения.Вставить("Описание", Объект.Описание);
	СтруктураЗаполнения.Вставить("Контрагент", Объект.Контрагент);
	СтруктураЗаполнения.Вставить("КонтактноеЛицо", Объект.КонтактноеЛицо);
	СтруктураЗаполнения.Вставить("АбонентКакСвязаться", Объект.АбонентКакСвязаться);
	
	СтруктураЗаполнения.Вставить("ПредметВзаимодействия", Предмет);
	
	ПараметрыФормы.Вставить("СписокТелефонов", Элементы.ТелефонКонтактногоЛица.СписокВыбора);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", СтруктураЗаполнения);
	ОткрытьФорму("Документ.ТелефонныйЗвонок.ФормаОбъекта", ПараметрыФормы, ВладелецФормы);
	
	Модифицированность = Ложь;
	
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТелефон(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ТелефонЗавершениеВыбора", ЭтотОбъект, УникальныйИдентификатор);
		
	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		Вид = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонМобильныйКонтактныеЛица");
	Иначе
		Вид = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента");
	КонецЕсли;
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		Вид,
		КонтактнаяИнформацияПоПредставлению(Элементы.ТелефонКонтактногоЛица.ТекстРедактирования, Вид));
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПозвонитьКонтактномуЛицу(Команда)
	
	Если ЗначениеЗаполнено(Объект.АбонентКакСвязаться) Тогда
		УправлениеКонтактнойИнформациейКлиент.ПозвонитьПоТелефону(Объект.АбонентКакСвязаться);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для совершения звонка введите номер телефона.'"), , "ТелефонКонтактногоЛица");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция КонтактнаяИнформацияПоПредставлению(Знач Представление, Знач ОжидаемыйВид)
	
	Возврат УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ОжидаемыйВид);
	
КонецФункции

&НаКлиенте
Процедура ТелефонЗавершениеВыбора(СтруктураТелефона, ИдентификаторСтроки) Экспорт
	Если ТипЗнч(СтруктураТелефона) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ТелефонКонтактногоЛица.СписокВыбора.Добавить(СтруктураТелефона.Представление);
	Объект.АбонентКакСвязаться = СтруктураТелефона.Представление;
	СтруктураНовогоТелефона = СтруктураТелефона;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКонтактнуюИнформацию()
	
	Объект.АбонентКакСвязаться = "";
	Объект.АбонентКонтакт = Неопределено;
	Элементы.ТелефонКонтактногоЛица.СписокВыбора.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьТелефонКонтактномуЛицуНаСервере()
	
	Если Не ЗначениеЗаполнено(СтруктураНовогоТелефона) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураНовогоТелефона.Представление = Объект.АбонентКакСвязаться Тогда
		Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
			КонтактноеЛицо = Объект.КонтактноеЛицо.ПолучитьОбъект();
			КонтактнаяИнформация = КонтактноеЛицо.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(КонтактнаяИнформация, СтруктураНовогоТелефона);
			КонтактнаяИнформация.ЗначенияПолей = СтруктураНовогоТелефона.КонтактнаяИнформация;
			КонтактноеЛицо.Записать();
		ИначеЕсли ЗначениеЗаполнено(Объект.Контрагент) Тогда
			Контрагент = Объект.Контрагент.ПолучитьОбъект();
			КонтактнаяИнформация = Контрагент.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(КонтактнаяИнформация, СтруктураНовогоТелефона);
			КонтактнаяИнформация.ЗначенияПолей = СтруктураНовогоТелефона.КонтактнаяИнформация;
			Контрагент.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаОснованииУчастника(ДанныеУчастника)
	
	Объект.АбонентКонтакт = ДанныеУчастника.Контакт;
	Если ТипЗнч(ДанныеУчастника.Контакт) = Тип("СправочникСсылка.Контрагенты") Тогда
		Объект.Контрагент = ДанныеУчастника.Контакт;
		Объект.КонтактноеЛицо = Справочники.КонтактныеЛица.КонтактноеЛицоПоУмолчанию(ДанныеУчастника.Контакт);
	ИначеЕсли ТипЗнч(ДанныеУчастника.Контакт) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		Объект.КонтактноеЛицо = ДанныеУчастника.Контакт;
		Объект.Контрагент = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеУчастника.Контакт, "ОбъектВладелец");
	КонецЕсли;

	Объект.АбонентКакСвязаться = ДанныеУчастника.КакСвязаться;
	Объект.АбонентПредставление = ДанныеУчастника.Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Тема) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Тема'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"Тема",
				"Объект",
				Отказ);
			КонецЕсли; 
			
	Если Не ЗначениеЗаполнено(Объект.Описание) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Описание события'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"Описание",
				"Объект",
				Отказ);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.Контрагент)
		И Не ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда

				
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Контактное лицо'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"КонтактноеЛицо",
				"Объект",
				Отказ);
				
	КонецЕсли;
			
	Если Не ЗначениеЗаполнено(Объект.АбонентКакСвязаться) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Телефон'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"АбонентКакСвязаться",
				"Объект",
				Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КонтактноеЛицоПриИзмененииНаСервере()
	
	ВзаимодействияФормыБП.КонтактноеЛицоПриИзмененииНаСервере(ЭтотОбъект);
	
	УстановитьКонтактнуюИнформацию();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКонтактнуюИнформацию()
	
	ВзаимодействияФормыБП.УстановитьКонтактнуюИнформацию(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

