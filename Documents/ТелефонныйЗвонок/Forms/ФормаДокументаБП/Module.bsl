#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстВыбора;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	Если Объект.Ссылка.Пустая() Тогда
		Взаимодействия.УстановитьПредметПоДаннымЗаполнения(Параметры, Объект.Предмет);
		Объект.Входящий = Не (Параметры.Свойство("Основание")
			И Не (Параметры.Основание = Ложь Или Параметры.Основание = Неопределено));
	КонецЕсли;
	
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект,Параметры);
	
	Если Параметры.Свойство("ДанныеУчастника") И Объект.Контрагент <> Параметры.ДанныеУчастника.Контакт Тогда
		ЗаполнитьНаОснованииУчастника(Параметры.ДанныеУчастника);
	КонецЕсли;

	Если Параметры.Свойство("ЗначенияЗаполнения") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
		ЗначенияЗаполнения = Параметры.ЗначенияЗаполнения;
		Если ТипЗнч(ЗначенияЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(Объект, ЗначенияЗаполнения);
			
			Если ЗначенияЗаполнения.Свойство("ПредметВзаимодействия") 
				И ЗначениеЗаполнено(ЗначенияЗаполнения.ПредметВзаимодействия) Тогда
				Предмет = ЗначенияЗаполнения.ПредметВзаимодействия;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
		
	ВзаимодействияФормыБП.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюСоздатьНаОсновании", "Видимость", Ложь);
	
	Элементы.ДокументОснование.ТолькоПросмотр = ТолькоПросмотр;

	ВзаимодействияФормыБП.УстановитьОтборКонтактногоЛица(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	УправлениеФормой(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения	
	
	Предмет = Объект.Предмет;
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	ПроверитьЗаполнениеРеквизитов(Отказ);

	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		Объект.АбонентПредставление = ПредставлениеКонтакта(Объект.КонтактноеЛицо);
	Иначе
		Объект.АбонентПредставление = ПредставлениеКонтакта(Объект.Контрагент);
	КонецЕсли;
	
	Объект.Предмет = Предмет;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзмененоСостояниеЛидов") Тогда
		ПараметрыЗаписи.Вставить("ИзмененоСостояниеЛидов", ТекущийОбъект.ДополнительныеСвойства.ИзмененоСостояниеЛидов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ВзаимодействияКлиент.ВзаимодействиеПредметПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи, "ТелефонныйЗвонок");
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИзмененоСостояниеЛидов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыЗаписи, "ИзмененоСостояниеЛидов", Ложь);
	Если ИзмененоСостояниеЛидов Тогда
		ОповеститьОбИзменении(Тип("СправочникСсылка.КонтактныеЛица"));
		Оповестить("ИзмененоСостояниеЛидов", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.КонтактноеЛицо));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(СтруктураНовогоТелефона) Тогда
		ДобавитьТелефонКонтактномуЛицуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ВзаимодействияКлиентБП.ФормаОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора, КонтекстВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ВзаимодействияКлиент.ОтработатьОповещение(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ОчиститьКонтактнуюИнформацию();
	УстановитьКонтактноеЛицо();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)
	
	ОчиститьКонтактнуюИнформацию();
	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		КонтактноеЛицоПриИзмененииНаСервере();
	КонецЕсли;

	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ВзаимодействияКлиентБП.ПредметНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьТелефон(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ТелефонЗавершениеВыбора", ЭтотОбъект, УникальныйИдентификатор);
	
	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		Вид = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонМобильныйКонтактныеЛица");
	Иначе
		Вид = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента");
	КонецЕсли;
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		Вид,
		КонтактнаяИнформацияПоПредставлению(Элементы.ТелефонКонтактногоЛица.ТекстРедактирования, Вид));
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПозвонитьКонтактномуЛицу(Команда)
	
	Если ЗначениеЗаполнено(Объект.АбонентКакСвязаться) Тогда
		Телефон = Объект.АбонентКакСвязаться;
		Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
			МобильныйКлиентБПКлиент.ПозвонитьПоТелефону(Телефон);
		Иначе
			УправлениеКонтактнойИнформациейКлиент.ПозвонитьПоТелефону(Телефон);
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Для совершения звонка введите номер телефона.'"), , "ТелефонКонтактногоЛица");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаСервереБезКонтекста
Функция ПредставлениеКонтакта(Контакт)
	Возврат Строка(Контакт);
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КонтактноеЛицо");
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Контрагент");
	
	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,	
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Объект.Контрагент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Объект.КонтактноеЛицо", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КонтактноеЛицо");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Контрагент");
	
	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,	
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Объект.КонтактноеЛицо", ВидСравненияКомпоновкиДанных.Заполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Объект.Контрагент", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.Контрагент.Видимость = Не ЭтоЛид(Объект.КонтактноеЛицо);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КонтактнаяИнформацияПоПредставлению(Знач Представление, Знач ОжидаемыйВид)
	
	Возврат УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ОжидаемыйВид);
	
КонецФункции

&НаКлиенте
Процедура ТелефонЗавершениеВыбора(СтруктураТелефона, ИдентификаторСтроки) Экспорт
	
	Если ТипЗнч(СтруктураТелефона) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ТелефонКонтактногоЛица.СписокВыбора.Добавить(СтруктураТелефона.Представление);
	Объект.АбонентКакСвязаться = СтруктураТелефона.Представление;
	СтруктураНовогоТелефона = СтруктураТелефона;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКонтактноеЛицо()
	
	ВзаимодействияФормыБП.УстановитьОтборКонтактногоЛица(ЭтотОбъект);
		
	ДанныеКонтакта = Новый Структура;
	ДанныеКонтакта.Вставить("КакСвязаться", Объект.АбонентКакСвязаться);
	ДанныеКонтакта.Вставить("Представление", Объект.АбонентПредставление);
	ДанныеКонтакта.Вставить("Контакт", Объект.АбонентКонтакт);
	
	ВзаимодействияФормыБП.УстановитьКонтактноеЛицо(ЭтотОбъект, ДанныеКонтакта);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКонтактнуюИнформацию()
	
	ВзаимодействияФормыБП.УстановитьКонтактнуюИнформацию(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКонтактнуюИнформацию()
	
	Объект.АбонентКакСвязаться = "";
	Элементы.ТелефонКонтактногоЛица.СписокВыбора.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьТелефонКонтактномуЛицуНаСервере()
	
	Если Не ЗначениеЗаполнено(СтруктураНовогоТелефона) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураНовогоТелефона.Представление = Объект.АбонентКакСвязаться Тогда
		Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
			КонтактноеЛицо = Объект.КонтактноеЛицо.ПолучитьОбъект();
			КонтактнаяИнформация = КонтактноеЛицо.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(КонтактнаяИнформация, СтруктураНовогоТелефона);
			КонтактнаяИнформация.ЗначенияПолей = СтруктураНовогоТелефона.КонтактнаяИнформация;
			КонтактноеЛицо.Записать();
		ИначеЕсли ЗначениеЗаполнено(Объект.Контрагент) Тогда
			Контрагент = Объект.Контрагент.ПолучитьОбъект();
			КонтактнаяИнформация = Контрагент.КонтактнаяИнформация.Добавить();
			ЗаполнитьЗначенияСвойств(КонтактнаяИнформация, СтруктураНовогоТелефона);
			КонтактнаяИнформация.ЗначенияПолей = СтруктураНовогоТелефона.КонтактнаяИнформация;
			Контрагент.Записать();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


&НаСервереБезКонтекста
Функция ЭтоЛид(КонтактноеЛицо)
	Возврат Справочники.КонтактныеЛица.ЭтоЛид(КонтактноеЛицо);
КонецФункции

&НаСервере
Процедура ЗаполнитьНаОснованииУчастника(ДанныеУчастника)
	
	Объект.АбонентКонтакт = ДанныеУчастника.Контакт;
	Если ТипЗнч(ДанныеУчастника.Контакт) = Тип("СправочникСсылка.Контрагенты") Тогда
		Объект.Контрагент = ДанныеУчастника.Контакт;
		Объект.КонтактноеЛицо = Справочники.КонтактныеЛица.КонтактноеЛицоПоУмолчанию(ДанныеУчастника.Контакт);
	ИначеЕсли ТипЗнч(ДанныеУчастника.Контакт) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		Объект.КонтактноеЛицо = ДанныеУчастника.Контакт;
		Объект.Контрагент = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеУчастника.Контакт, "ОбъектВладелец");
	КонецЕсли;
		
	Объект.АбонентКакСвязаться = ДанныеУчастника.КакСвязаться;
	Объект.АбонентПредставление = ДанныеУчастника.Представление;
	
КонецПроцедуры

&НаСервере
Процедура КонтактноеЛицоПриИзмененииНаСервере()
	
	ВзаимодействияФормыБП.КонтактноеЛицоПриИзмененииНаСервере(ЭтотОбъект);
	
	УстановитьКонтактнуюИнформацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Тема) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Тема'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"Тема",
				"Объект",
				Отказ);
			КонецЕсли; 
			
	Если Не ЗначениеЗаполнено(Объект.Описание) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Описание события'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"Описание",
				"Объект",
				Отказ);
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(Объект.Контрагент)
		И Не ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		
		ТекстСообщения = НСтр("ru = 'Поля ""Контрагент"" и ""Контактное лицо"" не заполнены. Хотя бы одно из них должно быть заполнено'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"Контрагент",
				"Объект",
				Отказ);
				
	КонецЕсли;
			
	Если Не ЗначениеЗаполнено(Объект.АбонентКакСвязаться) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Телефон'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"АбонентКакСвязаться",
				"Объект",
				Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ЛеваяКолонка);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ПраваяКолонка);
	
	Элементы.ГруппаТелефонКонтактногоЛица.Объединенная = Истина;
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

