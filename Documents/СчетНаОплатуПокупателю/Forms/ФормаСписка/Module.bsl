#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОсновнаяОрганизация           = Справочники.Организации.ОрганизацияПоУмолчанию();
	ОтборОрганизация              = ОсновнаяОрганизация;
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	УстановитьВосстановленныеОтборы();
	
	Если ЗначениеЗаполнено(Параметры.СчетаНаОплатуСоСкидкойПоДокументу)
		И ТипЗнч(Параметры.СчетаНаОплатуСоСкидкойПоДокументу) = Тип("СписокЗначений") Тогда
		ПоказатьСчетаСоСкидкойПоДокументу();
	КонецЕсли;
	
	ОтправкаПочтовыхСообщений.ПриСозданииНаСервере(ЭтотОбъект);
	
	// Выделяем в форме списка строки тех документов, которые только что были созданы из ранее запланированных.
	// Используется при работе с функциональностью периодических счетов покупателям.
	СписокВыделенияПериодическихСчетов = Неопределено;
	Если Параметры.Свойство("СписокВыделения", СписокВыделенияПериодическихСчетов)
		И ЗначениеЗаполнено(СписокВыделенияПериодическихСчетов) Тогда
		СписокВыделения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СписокВыделенияПериодическихСчетов.Ссылка);
		УстановитьУсловноеОформление(СписокВыделения);
	КонецЕсли;
	
	НадписьЗапланировано = НадписьЗапланировано();
	Элементы.НадписьЗапланировано.Видимость = ЗначениеЗаполнено(НадписьЗапланировано);
	
	Список.Параметры.УстановитьЗначениеПараметра(
		"СтатусОплатыПоУмолчанию", Перечисления.СтатусОплатыСчета.СтатусНовогоДокумента());
	Список.Параметры.УстановитьЗначениеПараметра(
		"СтатусОтгрузкиПоУмолчанию", Перечисления.СтатусыОтгрузки.СтатусНовогоДокумента());
	
	ОбщегоНазначенияБП.УстановитьВидимостьКолонокДополнительнойИнформации(ЭтотОбъект);
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаСписка(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	Если Не РазделениеВключено Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_Подключение1СЭДО");
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ВидОперации") Тогда
		ВидОперации = Параметры.Отбор.ВидОперации;
		АвтоЗаголовок = Ложь;
		Заголовок = "Счета покупателям основных средств";
		Элементы.ФормаЗагрузитьИзИнтернетМагазина.Видимость = Ложь;
	Иначе
		ВидыОпераций = Новый Массив;
		ВидыОпераций.Добавить(Перечисления.ВидыОперацийСчетаПокупателю.ПустаяСсылка());
		ВидыОпераций.Добавить(Перечисления.ВидыОперацийСчетаПокупателю.ТоварыИУслуги);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ВидОперации", ВидыОпераций);
	КонецЕсли;
	
	// Переопределим команду меню формы
	ГруппаСоздатьНаОсновании = Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы.Найти("ФормаСоздатьНаОсновании");
	Если ГруппаСоздатьНаОсновании <> Неопределено Тогда
		КнопкаСозданиеСчетаНаОплату = ГруппаСоздатьНаОсновании.ПодчиненныеЭлементы.Найти("ФормаДокументРеализацияТоваровУслугСоздатьНаОсновании");
		Если КнопкаСозданиеСчетаНаОплату <> Неопределено Тогда
			
			КнопкаСозданиеСчетаНаОплату.Видимость = Ложь;
			Элементы.ФормаСоздатьНаОснованииРеализацияТоваровУслуг.Видимость = Истина;
			Элементы.Переместить(Элементы.ФормаСоздатьНаОснованииРеализацияТоваровУслуг, ГруппаСоздатьНаОсновании, КнопкаСозданиеСчетаНаОплату);
			
		КонецЕсли;
		
		Документы.СчетНаОплатуПокупателю.УстановитьВидимостьСоздаваемыхНаОсновании(ГруппаСоздатьНаОсновании,
			ВидОперации <> Перечисления.ВидыОперацийСчетаПокупателю.ОсновныеСредства);
		
	КонецЕсли;
	
	// Переопределим команду контекстного меню
	ГруппаКонтекстноеМенюСоздатьНаОсновании = Элементы.Список.КонтекстноеМеню.ПодчиненныеЭлементы.Найти("СписокКонтекстноеМенюСоздатьНаОсновании");
	Если ГруппаКонтекстноеМенюСоздатьНаОсновании <> Неопределено Тогда
		КомандаСоздатьНаОсновании = ГруппаКонтекстноеМенюСоздатьНаОсновании.ПодчиненныеЭлементы.Найти("СписокКонтекстноеМенюДокументРеализацияТоваровУслугСоздатьНаОсновании");
		Если КомандаСоздатьНаОсновании <> Неопределено Тогда
			
			КомандаСоздатьНаОсновании.Видимость = Ложь;
			Элементы.СписокКонтекстноеМенюСоздатьНаОснованииРеализацияТоваровУслуг.Видимость = Истина;
			Элементы.Переместить(Элементы.СписокКонтекстноеМенюСоздатьНаОснованииРеализацияТоваровУслуг, ГруппаКонтекстноеМенюСоздатьНаОсновании, КомандаСоздатьНаОсновании);
		КонецЕсли; 
		
		Документы.СчетНаОплатуПокупателю.УстановитьВидимостьСоздаваемыхНаОсновании(ГруппаКонтекстноеМенюСоздатьНаОсновании,
			ВидОперации <> Перечисления.ВидыОперацийСчетаПокупателю.ОсновныеСредства);
		
	КонецЕсли;
	
	ИспользуетсяЭП = ЭлектронноеВзаимодействиеБП.НастроенОбменЭДО();
	Если ИспользуетсяЭП Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяЭП");
	КонецЕсли;
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.СчетНаОплатуПокупателю",
		"ФормаСписка",
		НСтр("ru = 'Новости: Счета покупателям'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ТарификацияБП.РазместитьИнформациюОбОграниченииПоКоличествуОбъектов(ЭтотОбъект);
	
	АдресХранилищаНастройкиДинСпискаДляРеестра = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	МожноРедактироватьОплатаПлатежнойКартой = ПравоДоступа("Редактирование", Метаданные.Документы.ОплатаПлатежнойКартой);
	
	ИспользоватьОнлайнОплаты = ПолучитьФункциональнуюОпцию("ИспользоватьОнлайнОплаты")
		И МожноРедактироватьОплатаПлатежнойКартой;
	
	ДоступнаИнтеграцияССБП = ИнтеграцияССБПБП.ДоступнаИнтеграцияССБП() И МожноРедактироватьОплатаПлатежнойКартой;
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	Элементы.ФормаЗагрузитьИзИнтернетМагазина.Видимость = ОбменСИнтернетМагазином.ДоступенОбменСИнтернетМагазином();
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ФормаГруппаЗагрузкаBNovo", "Видимость", ИнтеграцияBnovo.ДоступенОбменСBnovo());
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(
			ЭтотОбъект,
			Элементы.ГруппаГлобальныеКоманды);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// РекламныйСервис
	РекламныйСервис.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РекламныйСервис
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ПриСозданииНаСервере(ЭтаФорма, 
		"ГруппаГлобальныеКоманды", "КомандыЭДО", Истина, "СписокКонтекстноеМенюГруппаИзменитьВыделенные");

	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		ОсновнаяОрганизация = Параметр;
		Если ОсновнаяОрганизация <> ОтборОрганизация Тогда
			ОтборОрганизация                 = ОсновнаяОрганизация;
			ОтборОрганизацияИспользование    = ЗначениеЗаполнено(ОтборОрганизация);
			УстановитьВосстановленныеОтборы();
			УстановитьБаннер();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_ПравилаРегулярныхСчетовПокупателям"
		Или ИмяСобытия = "Запись_РегулярныеСчетаПокупателям"
		Или ИмяСобытия = "Запись_СчетПокупателю" И Параметр.Свойство("ВведенДокументПоПравилу") Тогда
		
		НадписьЗапланировано = НадписьЗапланировано();
		Элементы.НадписьЗапланировано.Видимость = (НадписьЗапланировано <> "");
		
	ИначеЕсли ИмяСобытия = "ИзмененСтатусДокументов" Тогда

		Элементы.Список.Обновить();
		
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	ПрисоединенныеФайлыБПКлиент.ОбновитьСписокПослеДобавленияФайла(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещенияЭДО = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСписка();
	ПараметрыОповещенияЭДО.Форма = ЭтотОбъект;
	ПараметрыОповещенияЭДО.МестоРазмещенияКоманд = Элементы.КомандыЭДО;
	ПараметрыОповещенияЭДО.ЕстьОбработчикОбновитьКомандыЭДО = Истина;
	ПараметрыОповещенияЭДО.ИмяДинамическогоСписка = "Список";
	ПараметрыОповещенияЭДО.ЕстьОбработчикОбновленияВидимостиСостоянияЭДО = Истина;
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСписка(ИмяСобытия, Параметр, Источник, ПараметрыОповещенияЭДО);
	// Конец подсистема "ОбменСКонтрагентами".
	
	ОнлайнОплатыБПКлиент.ОбработкаОповещения_ФормаСписка(Элементы.Список, ИмяСобытия, Параметр, Источник);
	
	КомментарииДокументовЗакрытогоПериодаКлиент.ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РеквизитыОрганизацииЗаполнены = ОбщегоНазначенияБПКлиент.ПроверитьНаличиеОрганизаций();
	
	Если РеквизитыОрганизацииЗаполнены Тогда
		
		// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
		ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
		// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
		
	КонецЕсли;
	
	Если ОтображатьСтатусыДокументов Тогда
		
		// Подсистема "ОбменСКонтрагентами"
		ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
		// Конец Подсистема "ОбменСКонтрагентами"
		
	КонецЕсли;
	
	Если ИспользоватьОнлайнОплаты Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЗагрузитьОперацииОнлайнОплат", 1, Истина);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗагрузитьОперацииПоСБП", 1, Истина);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);
	
	// РекламныйСервис
	РекламныйСервисКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец РекламныйСервис
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	СтруктураОтбора = Неопределено;
	Если Параметры.Свойство("Отбор", СтруктураОтбора) И ЗначениеЗаполнено(СтруктураОтбора) 
		И СтруктураОтбора.Свойство("Организация") И ЗначениеЗаполнено(СтруктураОтбора.Организация) Тогда
		
		ОтборОрганизация = СтруктураОтбора.Организация;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		Параметры.Отбор.Удалить("Организация");
	Иначе
		Если ЗначениеЗаполнено(ОсновнаяОрганизация) И ОтборОрганизация <> ОсновнаяОрганизация Тогда
			ОтборОрганизация = ОсновнаяОрганизация;
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		ИначеЕсли НЕ ОтборОрганизацияИспользование Тогда
			ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор", СтруктураОтбора) И ЗначениеЗаполнено(СтруктураОтбора) 
		И СтруктураОтбора.Свойство("Контрагент") И ЗначениеЗаполнено(СтруктураОтбора.Контрагент) Тогда
		
		ОтборКонтрагент = СтруктураОтбора.Контрагент;
		ОтборКонтрагентИспользование = ЗначениеЗаполнено(ОтборКонтрагент);
		Параметры.Отбор.Удалить("Контрагент");
	КонецЕсли;
	
	УстановитьВосстановленныеОтборы();
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		МобильныйКлиентБПФормаОбъектаКлиентСервер.ОбновитьНадписьФиксированногоОтбора(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстРезультатОбменаСИнтернетМагазиномОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСИнтернетМагазиномКлиент.ПоказатьРезультатВыполненияОбмена(РезультатОбменаСИнтернетМагазином);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентИспользованиеПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	ОтборКонтрагентИспользование = ЗначениеЗаполнено(ОтборКонтрагент);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, ОтборОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)

	КлючеваяОперация = "СозданиеФормыСчетНаОплатуПокупателям";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
КонецПроцедуры


&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	КлючеваяОперация = "ОткрытиеФормыСчетНаОплатуПокупателям";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ОбщегоНазначенияБП.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	
	ОтборыСписков.СброситьИспользованиеПользовательскихОтборовВНастройке(Настройки);
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка <> Неопределено Тогда
		
		Если ПрисоединенныеФайлыБПКлиент.ПараметрыПеретаскиванияСодержатФайлы(ПараметрыПеретаскивания) Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Ссылка"                 , Строка);
			ДополнительныеПараметры.Вставить("ПараметрыПеретаскивания", ПараметрыПеретаскивания);
			ДополнительныеПараметры.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ПеретаскиваниеФайловОтветПолучен",
				ПрисоединенныеФайлыБПКлиент,
				ДополнительныеПараметры);
			ШаблонВопроса = НСтр("ru='Присоединить файлы к документу %1?'");
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонВопроса, Строка);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса,РежимДиалогаВопрос.ДаНет);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Подсистема "Обмен с контрагентами"
	ОбменСКонтрагентамиКлиент.ПриАктивизацииСтроки_ФормаСписка(ЭтотОбъект);
	//Конец Подсистема "Обмен с контрагентами"
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьРезультатОбменаСИнтернетМагазиномНажатие(Элемент)
	Элементы.ГруппаРезультатОбменаСИнтернетМагазином.Видимость = Ложь;
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.СостояниеЭДО Тогда
		// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаСписка(ВыбраннаяСтрока, СтандартнаяОбработка);
		// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьСтатус(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	СписокДокументов = Новый СписокЗначений;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяСтрока = Элементы.Список.ДанныеСтроки(ВыделеннаяСтрока);
		
		Если ТекущаяСтрока <> Неопределено Тогда
			СписокДокументов.Добавить(ТекущаяСтрока.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СписокДокументов.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для указанного объекта'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.Форма.ИзменитьСтатус",
		Новый Структура("СписокДокументов", СписокДокументов),
		ЭтотОбъект,
		КлючУникальности);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзИнтернетМагазина(Команда)
	
	Элементы.ГруппаРезультатОбменаСИнтернетМагазином.Видимость = Ложь;
	
	ОбработчикПослеВыполненияОбмена = Новый ОписаниеОповещения("ОбработатьПослеВыполненияОбменаСИнтернетМагазином", ЭтотОбъект);
	ОбменСИнтернетМагазиномКлиент.ВыполнитьОбменСИнтернетМагазином(ЭтотОбъект, ОбработчикПослеВыполненияОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РедактироватьКомментарий(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если Не ТекущаяСтрока = Неопределено Тогда
		КомментарииДокументовЗакрытогоПериодаКлиент.ИзменитьКомментарийВСписке(ТекущаяСтрока.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзBnovo(Команда)
	ПараметрыЗагрузки = Новый Структура;
	Если ОтборОрганизацияИспользование Тогда
		ПараметрыЗагрузки.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	ИнтеграцияBnovoКлиент.ВыполнитьЗагрузку(ЭтотОбъект, ПараметрыЗагрузки);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзBNovo_ЗаПериод(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаНачала",     ДатаЗагрузкиBNovo(?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено)));
	ПараметрыФормы.Вставить("ДатаОкончания",  НачалоДня(ТекущаяДата())-1);
	
	ОповещениеОВыбореПериода = НОвый ОписаниеОповещения("ВыборПериодаЗагрузкиЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.ИнтеграцияBnovo.Форма.ВыборПериода", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , ,ОповещениеОВыбореПериода ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзBNovo_Настройка(Команда)
	ОткрытьФорму("РегистрСведений.НастройкиИнтеграцииBnovo.ФормаСписка",,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзBNovo_Просмотреть(Команда)
	ОткрытьФорму("Обработка.ИнтеграцияBnovo.Форма.Форма", ,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление(ВыделяемыеЗначения = Неопределено)
	
	УсловноеОформление.Элементы.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ВыделяемыеЗначения) Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыделенияЗначений = Новый СписокЗначений();
	СписокВыделенияЗначений.ЗагрузитьЗначения(ВыделяемыеЗначения);
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Ссылка", ВидСравненияКомпоновкиДанных.ВСписке, СписокВыделенияЗначений);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(Новый Шрифт(),,, Истина));
	
КонецПроцедуры

&НаСервере
Процедура НастройкиДинамическогоСписка()
	
	Отчеты.РеестрДокументов.НастройкиДинамическогоСписка(ЭтотОбъект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Если УправлениеПечатьюБПКлиентСервер.ЭтоИмяКомандыРеестрДокументов(Команда.Имя) Тогда
		НастройкиДинамическогоСписка();
	КонецЕсли;
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	Если ИспользуетсяЭП Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяЭП");
	КонецЕсли;
	Если Не РазделениеВключено Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_Подключение1СЭДО");
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);
	
КонецПроцедуры

#Область ИнтернетМагазин
&НаКлиенте
Процедура ОбработатьПослеВыполненияОбменаСИнтернетМагазином(Результат, ДопПараметры) Экспорт
	
	ПослеВыполненияОбменаСИнтернетМагазиномНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ПослеВыполненияОбменаСИнтернетМагазиномНаСервере(Результат)
	
	СписокВыделенияИнтернетМагазин = Новый Массив;
	
	РезультатОбменаСИнтернетМагазином = Результат;
	
	РезультатЗагрузки = ПолучитьИзВременногоХранилища(Результат);
	
	Если РезультатЗагрузки.Успешно Тогда
		Обработано = РезультатЗагрузки.СтатистикаЗагрузки.Заказы.Обработано;
		Если Обработано > 0 Тогда
			ШаблонСообщения = НСтр("ru = 'Загрузка выполнена успешно.'");
			ТекстСообщения = Новый ФорматированнаяСтрока(
				Новый ФорматированнаяСтрока(ШаблонСообщения),
				Новый ФорматированнаяСтрока("Подробнее",,,, "#Подробнее"));
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокВыделенияИнтернетМагазин, РезультатЗагрузки.СтатистикаЗагрузки.Заказы.Создано);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокВыделенияИнтернетМагазин, РезультатЗагрузки.СтатистикаЗагрузки.Заказы.Обновлено, Истина);
		Иначе
			ТекстСообщения = НСтр("ru = 'В интернет-магазине нет заказов к загрузке'")
		КонецЕсли;
	Иначе
		ШаблонСообщения = НСтр("ru = 'Не удалось выполнить обмен с интернет-магазином.'");
		ТекстСообщения = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(ШаблонСообщения),
			Новый ФорматированнаяСтрока("Подробнее",,,, "#Подробнее"));
	КонецЕсли;
	
	Элементы.ТекстРезультатОбменаСИнтернетМагазином.Заголовок = ТекстСообщения;
	Элементы.ГруппаРезультатОбменаСИнтернетМагазином.Видимость = Истина;
	
	УстановитьУсловноеОформление(СписокВыделенияИнтернетМагазин);

КонецПроцедуры

#КонецОбласти

#Область РегулярныеСчета

&НаКлиенте
Процедура НадписьЗапланированоОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФорму("Справочник.ПравилаРегулярныхСчетовПокупателям.ФормаСписка",, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НадписьЗапланировано()
	
	ДанныеДляНадписи = Справочники.ПравилаРегулярныхСчетовПокупателям.ДанныеДляНадписиЗапланировано();
	
	КоличествоЗапланировано = ДанныеДляНадписи.КоличествоЗапланировано;
	КоличествоПросрочено    = ДанныеДляНадписи.КоличествоПросрочено;
	ДатаСледующего          = ДанныеДляНадписи.ДатаСледующего;
	ОсталосьДней            = ДанныеДляНадписи.ОсталосьДней;
	
	Если КоличествоЗапланировано = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ЭлементыНадписи = Новый Массив;
	
	Если КоличествоПросрочено > 0 Тогда
		ЦветСсылки = ЦветаСтиля.ПросроченныеДанныеЦвет;
	Иначе 
		ЦветСсылки = ЦветаСтиля.ЦветГиперссылки;
	КонецЕсли;
	
	ЭлементыНадписи.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Периодические счета'"),, ЦветСсылки,, "Ссылка"));
	ЭлементыНадписи.Добавить(": ");
	
	Если КоличествоЗапланировано <> КоличествоПросрочено Тогда
		
		ПредметИсчисления = НСтр("ru = 'запланирован, запланировано, запланировано'");
		
		ЭлементыНадписи.Добавить(
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоЗапланировано, ПредметИсчисления));
		
		Если КоличествоПросрочено > 0 Тогда
			ЭлементыНадписи.Добавить(", из них ");
		КонецЕсли;
		
	КонецЕсли;
	
	Если КоличествоПросрочено > 0 Тогда
		
		ПредметИсчисления = НСтр("ru = 'просрочен, просрочено, просрочено'");
		
		ЭлементыНадписи.Добавить(
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоПросрочено, ПредметИсчисления));
		
	Иначе
		
		ЭлементыНадписи.Добавить(", ");
		
		Если КоличествоЗапланировано <> 1 Тогда
			ЭлементыНадписи.Добавить(НСтр("ru = 'следующий '"));
		КонецЕсли;
		
		ЭлементыНадписи.Добавить(Формат(ДатаСледующего, "ДЛФ=DD"));
		
		Если ОсталосьДней = 0 Тогда
			
			ЭлементыНадписи.Добавить(Нстр("ru = ' (Сегодня)'"));
			
		ИначеЕсли ОсталосьДней = 1 Тогда
			
			ЭлементыНадписи.Добавить(Нстр("ru = ' (Завтра)'"));
			
		ИначеЕсли ОсталосьДней < 8 Тогда
			
			ПараметрыПредметаИсчисления = НСтр("ru = 'день, дня, дней'");
			НадписьДней = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ОсталосьДней, ПараметрыПредметаИсчисления);
			
			ЭлементыНадписи.Добавить(СтрШаблон(Нстр("ru = ' (осталось %1)'"), НадписьДней));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ЭлементыНадписи);
	
КонецФункции

#КонецОбласти

#Область ИнтеграцияССБП

&НаКлиенте
Процедура ЗагрузитьОперацииПоСБП(ВыводитьОкноОжидания = Истина)
	
	Если Не ДоступнаИнтеграцияССБП Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Загрузка статусов операций.'");
	
	РезультатВыполнения = ЗагрузитьСтатусыОплатыЗапускЗадания(УникальныйИдентификатор);
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ЗагрузитьСтатусыОплатыЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьСтатусыОплатыЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗагрузитьОперацииПоСБП()
	
	ЗагрузитьОперацииПоСБП();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗагрузитьСтатусыОплатыЗапускЗадания(ИдентификаторЗадания)
	
	// Возможно, что фоновое задание было запущено раньше,
	// пользователь дал команду его отменить, однако задание не отменено.
	// В таком случае не следует запускать задание повторно - следует дождаться его выполнения.
	// Мы можем отследить ситуацию только, если все это происходит в одной форме.
	// Потому что подсистема ДлительныеОперации не умеет устанавливать ключ фонового задания.
	Если ЗакрытиеМесяца.ЗаданиеЕщеВыполняется(ИдентификаторЗадания) Тогда
		// Надо ждать
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторЗадания);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка статусов операций.'");
	ПараметрыВыполнения.КлючФоновогоЗадания = "ПолучитьСтатусыОперация";
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияССБПБП.ПолучитьСтатусыОперация",
		,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьСтатусыОплатыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ВыполнененыеОплаты = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		КоличествоОплат = ?(ЗначениеЗаполнено(ВыполнененыеОплаты), ВыполнененыеОплаты.Количество(), 0);
		Если КоличествоОплат > 0 Тогда
			КоличествоОплатСтр = СтрокаСЧислом(НСтр("ru = '; %1 оплата;; %1 оплаты; %1 оплат; %1 оплат'"),
				КоличествоОплат, ВидЧисловогоЗначения.Количественное, "L=ru");
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Загружено из СБП: %1'"), КоличествоОплатСтр);
			ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка завершена'"),
				"e1cib/list/Документ.ОплатаПлатежнойКартой",
				ТекстСообщения,,, "СБП");
		КонецЕсли;
		
		Элементы.Список.Обновить();
		ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплатуПокупателю"));
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьОповещениеПользователя(
			Результат.КраткоеПредставлениеОшибки,
			,
			,
			БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОнлайнОплаты

&НаКлиенте
Процедура Подключаемый_ЗагрузитьОперацииОнлайнОплат()
	
	ОнлайнОплатыБПКлиент.НачатьЗагрузкуОперацийОнлайнОплат(Ложь);
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗагрузитьОперацииОнлайнОплат", 300, Истина); // Каждые 5 минут.
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокДокументовОснований(ВыделенныеДокументы)
	СтруктураОтбора = Новый Структура;
	СписокСчетов = Новый Массив;

	РеквизитыСчетов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВыделенныеДокументы, "Организация, Контрагент");
	Для каждого РеквизитыСчета Из РеквизитыСчетов Цикл
		Если НЕ СтруктураОтбора.Свойство("Организация") Тогда
		
			СтруктураОтбора.Вставить("Организация", РеквизитыСчета.Значение.Организация);
			СтруктураОтбора.Вставить("Контрагент",  РеквизитыСчета.Значение.Контрагент);
			
			СписокСчетов.Добавить(РеквизитыСчета.Ключ);
		
		ИначеЕсли СтруктураОтбора.Организация = РеквизитыСчета.Значение.Организация 
			И СтруктураОтбора.Контрагент = РеквизитыСчета.Значение.Контрагент Тогда
		
			СписокСчетов.Добавить(РеквизитыСчета.Ключ);
		
		КонецЕсли; 
	КонецЦикла; 

	Возврат Новый ФиксированныйМассив(СписокСчетов);
КонецФункции
 

&НаКлиенте
Процедура СоздатьНаОсновании(Команда)
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Если ТипЗнч(ВыделенныеСтроки) = Тип("Массив") Тогда
		СписокСчетовПоКонтрагентам = СписокДокументовОснований(ВыделенныеСтроки);
		
		Если СписокСчетовПоКонтрагентам.Количество() > 0 Тогда
			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("Основание", СписокСчетовПоКонтрагентам);
			
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыЗаполнения, ЭтотОбъект);
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ОбновитьКомандыЭДО() Экспорт
	
	ОбменСКонтрагентамиКлиент.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьВидимостьСостоянияЭДО() Экспорт
	
	ОбменСКонтрагентамиКлиент.ОбработчикОбновленияВидимостьСостоянияЭДО(ЭтотОбъект, Элементы.СостояниеЭДО);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Элементы.Список);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#Область РекламныйСервис

&НаКлиенте
Процедура Подключаемый_ЗаполнитьРекламныйНоситель()
	РекламныйСервисКлиент.ЗаполнитьРекламныйНоситель(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьКнопкуЗакрытьРекламу()
	РекламныйСервисКлиент.ПоказатьКнопкуЗакрытьРекламу(ЭтотОбъект);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_МакетРекламныйСервисПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	РекламныйСервисКлиент.МакетРекламныйСервисНажатие(ЭтотОбъект, ДанныеСобытия, СтандартнаяОбработка);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_КомандаЗакрытьРекламу()
	РекламныйСервисКлиент.КомандаЗакрытьРекламу(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Контрагент");
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьСчетаСоСкидкойПоДокументу()
	
	АвтоЗаголовок = Ложь;
	Заголовок = НСтр("ru = 'Счета на оплату со скидкой по документу в целом'");
	Элементы.ГруппаБыстрыеОтборы.Видимость = Ложь;
	Элементы.ГруппаКоманднаяПанель.Видимость = Ложь;
	
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементОтбора.ПравоеЗначение = Параметры.СчетаНаОплатуСоСкидкойПоДокументу;	
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	АдаптироватьМКОтборыФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
	Элементы.НадписьЗапланировано.АвтоМаксимальнаяШирина = Истина;
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.КомандаСоздать = "ФормаСоздать";
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);

	МобильныйКлиентБПФормаСписка.ПеренестиЭлементВМККомандыФормы(ЭтотОбъект, Элементы.ПодменюОтчеты);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьОтбор(Команда)
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойОтбораМК(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКОтборыФормы()
	
	МобильныйКлиентБПФормаСписка.АдаптироватьМКОтборыФормы(ЭтотОбъект, "Список", "Организация");
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);

	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы, Элементы.ПодменюПечать, Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы, Элементы.ПодменюОтправить, Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы, Элементы.СписокКонтекстноеМенюИзменитьСтатус, Элементы.Список.КонтекстноеМеню);
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементФормы(Элементы, Элементы.ПодменюЗаполнить, Элементы.Список.КонтекстноеМеню);
	
	МобильныйКлиентБПФормаОбъекта.ПереместитьЭлементыФормы(Элементы,
		Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы,
		Элементы.Список.КонтекстноеМеню);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормы(ЭтотОбъект);
	
	//В реквизитах установить "Использовать всегда" для источника КолонкаСостояниеДокумента
	МобильныйКлиентБПФормаСписка.ДобавитьКолонкаСостояниеДокумента(ЭтотОбъект, "СостояниеДокумента", "Список");
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СостояниеДокумента(ЭтотОбъект, Элементы.ЕстьФайлы);
	
	Элементы.Дата.РастягиватьПоГоризонтали = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, Элементы.Дата);
	
	ПолеМК_Номер = Элементы.Добавить("ПолеМК_Номер", Тип("ПолеФормы"), Элементы.Список);
	ПолеМК_Номер.ПутьКДанным = "Список.НомерМобильныйКлиент";
	ПолеМК_Номер.Вид = ВидПоляФормы.ПолеНадписи;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_ДатаНомер(ЭтотОбъект, ПолеМК_Номер);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_СуммаДокумента(ЭтотОбъект, Элементы.СуммаДокумента);
	
	Элементы.Контрагент.РастягиватьПоГоризонтали = Истина;
	Элементы.Контрагент.АвтоВысотаЯчейки = Истина;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Контрагент);
	
	Элементы.ВалютаДокумента.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВалютаДокумента.ОтображатьВШапке = Ложь;
	Элементы.ВалютаДокумента.РастягиватьПоГоризонтали = Ложь;
	Элементы.ВалютаДокумента.Ширина = 3;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.ВалютаДокумента);
	
	ГруппаКолонокМК_Оплата = МобильныйКлиентБПФормаОбъекта.ДобавитьГруппуКолонок(ЭтотОбъект,
		"ГруппаКолонокМК_Оплата",
		ГруппировкаКолонок.ВЯчейке,
		Элементы.Список);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, ГруппаКолонокМК_Оплата, 1);
	
	Элементы.Оплата.ЦветТекста = ЦветаСтиля.ЦветТекстаСтрокаСпискаМК;
	Элементы.Переместить(Элементы.Оплата, ГруппаКолонокМК_Оплата);
	
	Элементы.СрокОплаты.ЦветТекста = ЦветаСтиля.ЦветТекстаСтрокаСпискаМК;
	Элементы.СрокОплаты.Формат = "ДФ='''Срок: '' dd.MM.yyyy'";
	Элементы.Переместить(Элементы.СрокОплаты, ГруппаКолонокМК_Оплата);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Отгрузка, 2);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Прочее(ЭтотОбъект, Элементы.Организация);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаЗагрузкиЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("Период", ВыбранноеЗначение);
	Если ОтборОрганизацияИспользование Тогда
		ПараметрыЗагрузки.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	
	ИнтеграцияBnovoКлиент.ВыполнитьЗагрузку(ЭтотОбъект, ПараметрыЗагрузки);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДатаЗагрузкиBNovo(ОтборОрганизация)
	Возврат ИнтеграцияBnovo.ДатаЗагрузкиBNovo(ОтборОрганизация);
КонецФункции 

#КонецОбласти

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", ?(ОтборОрганизацияИспользование, ОтборОрганизация, Неопределено));
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияСчетПокупателю());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

