
#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;
&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОтправкаПочтовыхСообщений.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		// Обработчик не завершил обновление документов
		Объект.ВидОперации = Перечисления.ВидыОперацийСчетаПокупателю.ТоварыИУслуги;
	КонецЕсли;
	
	ЭтоТоварыИУслуги = Объект.ВидОперации <> Перечисления.ВидыОперацийСчетаПокупателю.ОсновныеСредства;
	
	ГруппаСоздатьНаОсновании = Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы.Найти("ФормаСоздатьНаОсновании");
	
	Документы.СчетНаОплатуПокупателю.УстановитьВидимостьСоздаваемыхНаОсновании(ГруппаСоздатьНаОсновании, ЭтоТоварыИУслуги);

	ПравоРедактированияСрокаОплаты = СрокиОплатыДокументов.ПравоРедактирования();
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
		КалькуляцииРасходов.ЗаполнитьКалькуляциюРасходовПоДокументуОснованию(ЭтотОбъект, "Товары");
		УстановитьГостиницу();
	КонецЕсли;
	
	Если Параметры.Свойство("ПравилоПовторения") И ЗначениеЗаполнено(Параметры.ПравилоПовторения) Тогда
		ПравилоПовторения = Параметры.ПравилоПовторения;
		НадписьПовторение = НадписьПовторение(ПравилоПовторения, Параметры.Ключ.Пустая());
		
		Если ИспользоватьТипыЦенНоменклатуры И ЗначениеЗаполнено(Объект.ТипЦен) Тогда
			ЗаполнитьРассчитатьСуммы(ОБъект.ВалютаДокумента, Объект.КурсВзаиморасчетов, Объект.КратностьВзаиморасчетов, Истина, Ложь, Ложь);
		КонецЕсли;
	Иначе
		ЗаполнитьПравилоПовторенияИНадпись();
	КонецЕсли;

	СсылкаДляПереходаНаКарту = УправлениеКонтактнойИнформациейБП.СтрокаСсылкиПоказатьНаКарте();

	// Активизировать первую непустую табличную часть
	СписокТабличныхЧастей = Новый СписокЗначений;
	СписокТабличныхЧастей.Добавить("Товары",         "Товары");
	СписокТабличныхЧастей.Добавить("ВозвратнаяТара", "ВозвратнаяТара");
	
	АктивизироватьТабличнуюЧасть = ОбщегоНазначенияБПВызовСервера.ПолучитьПервуюНепустуюВидимуюТабличнуюЧасть(
		ЭтотОбъект, СписокТабличныхЧастей);
	ОбщегоНазначенияБПВызовСервера.АктивизироватьЭлементФормы(ЭтотОбъект, АктивизироватьТабличнуюЧасть);
	
	УстановитьУсловноеОформление();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереДокумент(ЭтотОбъект, Параметры);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	УстановитьОтображениеЗакладок();
	УстановитьПараметрыВыбораСтавкиНДС();

	Элементы.НовыйДоговор.Видимость = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.ДоговорыКонтрагентов);
	Если Не ПравоРедактированияСрокаОплаты Тогда
		Элементы.СрокОплаты.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		Элементы.СрокОплаты.ТолькоПросмотр       = Истина;
	КонецЕсли;
	Если НЕ ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.СрокиОплатыПокупателями) Тогда
		// В подсказке отображается ссылка на общую форму СрокиОплатыПокупателям.
		// Без этой ссылки подсказка теряет смысл, поэтому в случае, если пользователю
		// недоступна форма, то не показываем подсказку.
		Элементы.СрокОплаты.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	Если УправлениеПечатьюБП.ИспользуетсяИзмененныйМакет(Объект.Ссылка) Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяИзмененныйМакет");
	КонецЕсли;
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		?(ЭтоТоварыИУслуги, "БП.Документ.СчетНаОплатуПокупателю", "БП.Документ.СчетНаОплатуПокупателюОС"),
		"ФормаДокумента",
		НСтр(?(ЭтоТоварыИУслуги, "ru='Новости: Счет покупателю'", "ru='Новости: Счет покупателю ОС'")),
		ИдентификаторыСобытийПриОткрытии);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
	УправлениеПанельюПодсказки.ПриСозданииНаСервере(ЭтотОбъект);
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(
			ЭтотОбъект,
			Элементы.ГруппаГлобальныеКоманды);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	Если Параметры.Свойство("ОткрыватьФормуНовогоКонтрагента") Тогда
		// открываем форму нового контрагента только в простом интерфейсе, поскольку
		// в полном интерфейсе новое окно не будет активизировано.
		ОткрыватьФормуНовогоКонтрагента = Параметры.ОткрыватьФормуНовогоКонтрагента
			И ОбщегоНазначенияБПВызовСервера.ЭтоПростойИнтерфейс();
	КонецЕсли;
	
	ПараметрОтветственный = Параметры.Ответственный;

	МобильныйКлиентАдаптацияФормы();
	МобильныйКлиентБП.ПроверитьИЗарегистроватьСобытиеДляОценки("просмотрдокументов", ПоказыватьФормуОценкиМК);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// Подсистема "ОбменСКонтрагентами"
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец Подсистема "ОбменСКонтрагентами"
	
	Если ПоказатьЗаполнениеПоДаннымИнтернетМагазина 
		ИЛИ ПоказатьЗаполнениеПоДаннымBNovo Тогда
		
		ПодключитьОбработчикОжидания("ПоказатьНесопоставленныеОбъектыНаКлиенте", 0.1, Истина);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьЗаголовокФормы();
	
	Если ОткрыватьФормуНовогоКонтрагента Тогда
		ПодключитьОбработчикОжидания("ОткрытьФормуНовогоКонтрагента", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.СчетНаОплатуПокупателю.Форма.ФормаРеквизитыПечатиQRКод" Тогда
		ОбработкаВыбораРеквизитыОрганизацииКонтрагента(ВыбранноеЗначение, ИсточникВыбора);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаВыбора"
		Или ИсточникВыбора.ИмяФормы = "Документ.СчетНаОплатуПоставщика.Форма.ФормаВыбора"
		Или ИсточникВыбора.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаВыбора" Тогда
		ОбработкаЗаполненияТабличнойЧастиНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Обработка.ПодборОсновныхСредств.Форма.Форма" Тогда
		ОбработкаВыбораПодборОСНаСервере(ВыбранноеЗначение);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.СчетНаОплатуПокупателю.Форма.ФормаДоставка" Тогда
		ОбработкаВыбораРеквизитыДоставки(ВыбранноеЗначение, ИсточникВыбора);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)

	Если ТипЗнч(НовыйОбъект) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Объект.ДоговорКонтрагента = НовыйОбъект;
		ДоговорКонтрагентаПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбработанаТабличнаяЧастьТовары" И ТипЗнч(Параметр) = Тип("Структура") 
		И Параметр.Свойство("ИдентификаторВызывающейФормы")
		И Параметр.ИдентификаторВызывающейФормы = УникальныйИдентификатор Тогда
		ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметр);
	ИначеЕсли ИмяСобытия = "ИзмененСтатусДокументов" Тогда
		// Статус документа изменен в форме списка или при проведении оплаты от покупателя
		Если Не Объект.Ссылка.Пустая() И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
			ОбработатьОповещениеОбИзмененииСтатусДокумента();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" И Параметр.Владелец = Объект.Организация Тогда
		
		ОбработатьоповещениеОбИзмененииБанковскогоСчетаИлиОрганизации();
		
	ИначеЕсли ИмяСобытия = "Запись_Организации" И Источник = Объект.ОрганизацияПолучатель Тогда
		
		ОбработатьоповещениеОбИзмененииБанковскогоСчетаИлиОрганизации();
		
	ИначеЕсли ИмяСобытия = "ИзменениеНастроекПараметровУчета" Тогда
		
		ОбработатьОповещениеОбИзмененииНастроекПараметровУчета();
		
	ИначеЕсли ИмяСобытия = "ДанныеСкопированыВБуферОбмена" Тогда
		
		УстановитьДоступностьКомандыВставки(ЭтотОбъект, Истина);
		
	ИначеЕсли ИмяСобытия = "ЗаполненыДанныеИнтернетМагазин"
		И Параметр.ИдентификаторВызывающейФормы = УникальныйИдентификатор Тогда
		
		ОбработатьОповещениеОЗаполненииДанных(Параметр, 
			Параметр.Свойство("ВсеДанныеСопоставленыАвтоматически") И Параметр.ВсеДанныеСопоставленыАвтоматически);
		
	ИначеЕсли ИмяСобытия = "Запись_ПравилаРегулярныхСчетовПокупателям" И 
		(Параметр = Объект.Ссылка ИЛИ Источник = ПравилоПовторения) Тогда
		
		ЗаполнитьПравилоПовторенияИНадпись();
		
	ИначеЕсли ИмяСобытия = "Запись_ИспользоватьНумерациюДоговоровСПокупателями" Тогда
		
		УстановитьПредлагатьНовыйДоговорСНумерацией();
		
	ИначеЕсли ИмяСобытия = "Запись_ДоговорыКонтрагентов" И Объект.ДоговорКонтрагента = Источник
		И АвторасчетСрокаОплаты Тогда
		
		УстановитьСрокОплатыДокументаРасчетов();
		
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант" И Источник = "ЗаполнятьОтветственногоПоПродажамИзКонтрагента" Тогда
		
		ОбработатьОповещениеОбИзмененииНастроекОтветственного();
		
	ИначеЕсли ИмяСобытия = "ИзменениеНастроекОповещенияПриИзмененииСтатусовСчетаПокупателю"
		И Источник = Объект.Ссылка Тогда
		
		ОбработатьИзменениеНастроекОповещенияПользователямПоДокументу(Параметр);
	
	ИначеЕсли ИмяСобытия = "СчетНаОплату_СкидкаРаспределенаПоСтрокам" 
			И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
		
		ОбновитьФормуПослеРаспределенияСкидок();
		
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
	
	УправлениеПанельюПодсказкиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	Если ОбрабатыватьОповещениеНаСервере(ИмяСобытия) Тогда
		ОбработкаОповещенияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьОповещениеОбИзмененииБанковскогоСчетаИлиОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		ОрганизацияПолучательПриИзмененииНаСервере();
	Иначе
		ПроверитьКорректностьСчетаСервер();
	КонецЕсли;
	
	ЗаполнитьДополнительныеРеквизитыQRКода(Истина);
	
	УстановитьФункциональныеОпцииФормы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ЭтоТоварыИУслуги = Объект.ВидОперации <> Перечисления.ВидыОперацийСчетаПокупателю.ОсновныеСредства;
	
	ПодготовитьФормуНаСервере();
	
	ЗаполнитьИнформациюОбОплатеСчета();
	
	Если Объект.ЗагрузкаИзBnovo Тогда
		
		ПодготовитьКЗаполнениюПоДаннымBNovo(ТекущийОбъект);
	ИначеЕсли ПолучитьФункциональнуюОпцию("ИспользоватьОбменСИнтернетМагазином") 
		И ОбменСИнтернетМагазином.ДоступенОбменСИнтернетМагазином() Тогда
		
		ПодготовитьКЗаполнениюПоДаннымИнтернетМагазина(ТекущийОбъект);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ПриЧтенииФормыДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект, "ГруппаКомментарий", "Ответственный"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "ПроведениеСчетНаОплатуПокупателю";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПроверкаРеквизитовОрганизации.ПередЗаписьюНаСервере(
		Объект.ОрганизацияПолучатель, 
		Объект.СтруктурнаяЕдиница, 
		ИспользоватьНесколькоБанковскихСчетовОрганизации, 
		Отказ, 
		ПараметрыЗаписи);
	
	СтруктураРеквизитовПодписи = Новый Структура;

	СтруктураРеквизитовПодписи.вставить("Руководитель",		"ЗаРуководителяНаОсновании");
	СтруктураРеквизитовПодписи.вставить("ГлавныйБухгалтер", "ЗаГлавногоБухгалтераНаОсновании");
	
	ОтветственныеЛицаБП.ПроверитьИзменениеПодписейДокумента(ТекущийОбъект,СтруктураРеквизитовПодписи,ПараметрыЗаписи);
	
	КалькуляцииРасходов.ПередЗаписьюНаСервереДокумента(ЭтотОбъект, ТекущийОбъект);
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьСтатусДокумента(ТекущийОбъект.Ссылка);
	
	СрокиОплатыДокументов.ЗаписатьСрокОплатыДокумента(ТекущийОбъект.Ссылка, СрокОплаты);
	
	Если ЗначениеЗаполнено(ПравилоПовторения) Тогда
		
		Если Объект.Ссылка.Пустая() Тогда // Новый документ по правилу.
			
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПравилоПовторения, "Организация") <> ТекущийОбъект.Организация Тогда
				ПравилоОбъект = ПравилоПовторения.ПолучитьОбъект();
				ПравилоОбъект.Организация = ТекущийОбъект.Организация;
				ПравилоОбъект.Записать();
			КонецЕсли;
			
			РегистрыСведений.РегулярныеСчетаПокупателям.УстановитьШаблон(ПравилоПовторения, ТекущийОбъект.Ссылка);
			
			НадписьПовторение = НадписьПовторение(ПравилоПовторения, Ложь);
			
			ПараметрыЗаписи.Вставить("ВведенДокументПоПравилу");
			
		Иначе
			
			РегистрыСведений.РегулярныеСчетаПокупателям.УстановитьШаблон(ПравилоПовторения, ТекущийОбъект.Ссылка,, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьНастройкиОповещенияПоСчетуПокупателю(ТекущийОбъект.Ссылка);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ЗаполнитьДополнительныеРеквизитыQRКода(Истина);

	Если Не ЗначениеЗаполнено(Объект.ТипЦен) И ЭтоТоварыИУслуги Тогда
		Ценообразование.ОбновитьЦеныНоменклатуры(Объект.Ссылка,
			Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам,
			Объект.ВалютаДокумента,
			Объект.СуммаВключаетНДС);
	КонецЕсли;
	
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("ОсновнойВариантРасчетаИсходящегоНДС",
		?(Объект.СуммаВключаетНДС, Перечисления.ВариантыРасчетаНДС.НДСВСумме, Перечисления.ВариантыРасчетаНДС.НДССверху));
		
	УстановитьСостояниеДокумента();
	
	Если ВедетсяУчетНДСПоФЗ335
		И ПокупательНалоговыйАгентПоНДС = Истина Тогда 
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ОтветственныеЛицаБП.УстановитьПодписиПоУмолчанию(ТекущийОбъект.Ссылка, ПараметрыЗаписи);
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами". 
	
	Если ИнтеграцияCRMПовтИсп.ИнтеграцияВИнформационнойБазеВключена() Тогда
		РегистрыСведений.ДокументыИнтеграцииCRM.ЗаполнитьДанныеКОтправкеИОповеститьОбИзмененияхВФоне(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
	КонецЕсли;
	
	КалькуляцииРасходов.ЗаполнитьКалькуляциюРасходов(ЭтотОбъект, "Товары");
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзмененДокументОснование") Тогда
		ПараметрыЗаписи.Вставить("ИзмененДокументОснование", ТекущийОбъект.ДополнительныеСвойства.ИзмененДокументОснование);
	КонецЕсли;
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзмененСтатусДокументаОснования") Тогда
		ПараметрыЗаписи.Вставить("ИзмененСтатусДокументаОснования",
			ТекущийОбъект.ДополнительныеСвойства.ИзмененСтатусДокументаОснования);
	КонецЕсли;
	
	Если ПлательщикТуристическогоНалога Тогда
		УстановитьРеквизитыФормыТуристическийНалог();  
		ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Товары");
		ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Товары");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Оповестить("Запись_СчетПокупателю", ПараметрыЗаписи, Объект.Ссылка);
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ИзмененДокументОснование = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыЗаписи, "ИзмененДокументОснование", Ложь);
		ИзмененСтатусДокументаОснования = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыЗаписи, "ИзмененСтатусДокументаОснования", Ложь);
		Если ИзмененДокументОснование Или ИзмененСтатусДокументаОснования Тогда
			ОповеститьОбИзменении(Объект.ДокументОснование);
		КонецЕсли;
		Если ИзмененДокументОснование Тогда
			Оповестить("ОбновитьФорму", Объект.ДокументОснование, Объект.Ссылка);
			Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.КоммерческоеПредложение") Тогда
				КонтактноеЛицо = КонтактноеЛицоКоммерческогоПредложения(Объект.ДокументОснование);
				Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
					ОповеститьОбИзменении(КонтактноеЛицо);
					Оповестить("ОбновитьФорму", КонтактноеЛицо, Объект.ДокументОснование);
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ИзмененСтатусДокументаОснования Тогда
			Оповестить("ИзмененСтатусДокументов",
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ДокументОснование),
				Объект.Ссылка);
		КонецЕсли;
	КонецЕсли;
		
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтотОбъект, ПараметрыЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НастройкиНалоговИОтчетовПредупреждениеФормы.ПроверкаЗаполнения(ЭтотОбъект, Объект.Организация, Объект.Дата, Ложь, Отказ);
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	НастройкиНалоговИОтчетовПредупреждениеФормыКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокОплатыРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПараметрыОткрытия", Новый Структура("АктивныйЭлемент", "СрокОплатыВыставленныхСчетов"));
	
	ОткрытьФорму("ОбщаяФорма.СрокиОплатыПокупателями", ПараметрыОткрытия);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьКЗаполнениюПоДаннымИнтернетМагазина(ТекущийОбъект)
	
	Результат = ОбменСИнтернетМагазином.ДозаполнитьПоДаннымИнтернетМагазина(ТекущийОбъект.Ссылка, УникальныйИдентификатор);
	ПоказатьЗаполнениеПоДаннымИнтернетМагазина = Результат.ТребуетсяДозаполнение;
	ДанныеДляЗаполнения = Результат;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьКЗаполнениюПоДаннымBNovo(ТекущийОбъект)
	
	Результат = ИнтеграцияBnovo.ДозаполнитьПоДаннымBNovo(ТекущийОбъект.Ссылка, УникальныйИдентификатор);
	ПоказатьЗаполнениеПоДаннымBNovo= Результат.ТребуетсяДозаполнение;
	ДанныеДляЗаполнения = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ПоказыватьФормуОценкиМК Тогда
		ОткрытьФорму("ОбщаяФорма.ОценитьПриложение");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента, Объект.ВалютаДокумента, ВалютаРегламентированногоУчета);
		
	ТребуетсяПерерасчитатьСрокОплаты = ПравоРедактированияСрокаОплаты 
		И АвторасчетСрокаОплаты
		И (НачалоДня(Объект.Дата) <> НачалоДня(ТекущаяДатаДокумента));
	
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера ИЛИ ТребуетсяПерерасчитатьСрокОплаты Тогда
		ДатаПриИзмененииНаСервере(ТребуетсяВызовСервера, ТребуетсяПерерасчитатьСрокОплаты);
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусДокументаПриИзменении(Элемент)
	
	Если СтатусДокумента = ПредопределенноеЗначение("Перечисление.СтатусОплатыСчета.Оплачен")
		ИЛИ СтатусДокумента = ПредопределенноеЗначение("Перечисление.СтатусОплатыСчета.ОплаченЧастично") Тогда
		// ИнтернетПоддержкаПользователей.Новости, код для вывода контекстных новостей при произвольном событии
		ИдентификаторыСобытий = "ИзменениеСтатусаСчетаПокупателюВручную";
		ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриПроизвольномСобытии(
			ЭтотОбъект,
			ИдентификаторыСобытий);
		// Конец ИнтернетПоддержкаПользователей.Новости
	КонецЕсли;
	
	ЗаполнитьИнформациюОбОплатеСчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	ОрганизацияПолучательПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ПодразделениеОрганизацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		КонтрагентПриИзмененииНаСервере();
	Иначе
		Объект.АдресДоставки = "";
		Объект.АдресДоставкиЗначение = "";
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элемент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораКонтрагенты");

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ДоговорКонтрагентаПриИзмененииНаСервере(); 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Истина;
	ОбщегоНазначенияБПКлиент.ОткрытьДоговорКонтрагента(ЭтотОбъект, Элемент, Объект.ДоговорКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(Объект.ДоговорКонтрагента),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(
		Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, ПредлагатьНовыйДоговор, СтандартнаяОбработка, ПредлагатьНовыйДоговорСНумерацией);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПолучательПриИзменении(Элемент)
	
	ОрганизацияПолучательПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзмененияПоКнопкеЦеныИВалюты();

КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОбновитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
    ОтветственныйПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтветственныйПриИзмененииНаСервере()
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);
	
	ЗаполнитьНастройкиОповещения();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРеквизитыОрганизацииКонтрагентаНажатие(Элемент)

	Если НЕ ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",                  ТолькоПросмотр);
	ПараметрыФормы.Вставить("Контрагент",                      Объект.Контрагент);
	ПараметрыФормы.Вставить("АдресДоставки",                   Объект.АдресДоставки);
	ПараметрыФормы.Вставить("АдресДоставкиЗначение",           Объект.АдресДоставкиЗначение);
	ПараметрыФормы.Вставить("ГлавныйБухгалтер",                Объект.ГлавныйБухгалтер);
	ПараметрыФормы.Вставить("Организация",                     Объект.Организация);
	ПараметрыФормы.Вставить("ДатаДокумента",                   Объект.Дата);
	ПараметрыФормы.Вставить("ДополнительныеУсловия",           Объект.ДополнительныеУсловия);
	ПараметрыФормы.Вставить("ЗаГлавногоБухгалтераНаОсновании", Объект.ЗаГлавногоБухгалтераНаОсновании);
	ПараметрыФормы.Вставить("ЗаРуководителяНаОсновании",       Объект.ЗаРуководителяНаОсновании);
	ПараметрыФормы.Вставить("Руководитель",                    Объект.Руководитель);
	ПараметрыФормы.Вставить("Склад",                           Объект.Склад);
	ПараметрыФормы.Вставить("ПоказыватьАдресДоставки", Не ИспользоватьДоставкуТранспортнойКомпанией 
		Или Не ЗначениеЗаполнено(Объект.СпособДоставки));
	ПараметрыФормы.Вставить("ВариантПечатиQRКода",             Объект.ВариантПечатиQRКода);
	ПараметрыФормы.Вставить("ЗаголовокСчета",                  Объект.ЗаголовокСчета);
	ПараметрыФормы.Вставить("СтруктурнаяЕдиница",              Объект.СтруктурнаяЕдиница);
		
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.Форма.ФормаРеквизитыПечатиQRКод", ПараметрыФормы, ЭтотОбъект); 

КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПроверкаРеквизитовОрганизацииКлиент.РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Объект.Организация, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстПредупрежденияСчетОрганизацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	БанковскиеСчетаФормыКлиент.ОткрытьСписокСчетовОрганизации(Объект.Организация, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДОНажатие(Элемент, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СтруктурнаяЕдиницаПриИзменении(Элемент)
	
	БанковскийСчетПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособДоставкиПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.СпособДоставки) Тогда
		Реквизиты = РеквизитыУсловияДоставки(Объект.СпособДоставки);
		
		Если ЗначениеЗаполнено(Реквизиты.УслугаДоставки) 
			И Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура", Реквизиты.УслугаДоставки)).Количество() = 0 Тогда
			
			ОповещениеОЗавершении = Новый ОписаниеОповещения("УсловияДоставкиЗавершение", ЭтотОбъект, Реквизиты);
			ТекстПредупреждения   = НСтр("ru = 'Добавить услугу по доставке?'");
		
			ПоказатьВопрос(ОповещениеОЗавершении, ТекстПредупреждения, РежимДиалогаВопрос.ДаНетОтмена);
		КонецЕсли; 
	КонецЕсли; 
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПриИзменении(Элемент)
	Элементы.СсылкаДляПереходаНаКарту.Доступность = ЗначениеЗаполнено(Объект.АдресДоставки);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.АдресДоставкиНачалоВыбора(Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.АдресДоставкиОбработкаВыбора(Объект, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

#Область Панель_Подсказки

&НаКлиенте
Процедура СоветПоРаботе1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе4ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СрокОплатыПриИзменении(Элемент)
	
	АвторасчетСрокаОплаты = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;

	Если ПлательщикТуристическогоНалога И ТекущиеДанные <> Неопределено И НомерАктивнойСтроки <> ТекущиеДанные.НомерСтроки Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьПредставлениеИДРодСтрок", 0.2, Истина);	
	КонецЕсли;
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущиеДанныеСтроки = Элемент.ТекущиеДанные;

		Если Не Копирование Тогда
			ТекущиеДанныеСтроки.СчетНаОплатуПокупателю = Объект.Ссылка;
			ТекущиеДанныеСтроки.СтавкаНДС              = СтавкаНДСПриРеализации;
			ТекущиеДанныеСтроки.ЭтоУслуга              = Истина;
			ТекущиеДанныеСтроки.РасходыНаЕдиницу       = ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначения();
		Иначе
			Если ЗначениеЗаполнено(ТекущиеДанныеСтроки.КлючКалькуляцииРасходов) Тогда
				СтарыйКлючКалькуляцииРасходов = ТекущиеДанныеСтроки.КлючКалькуляцииРасходов;
				ТекущиеДанныеСтроки.КлючКалькуляцииРасходов = Новый УникальныйИдентификатор;
				СкопироватьКалькуляциюРасходов(СтарыйКлючКалькуляцииРасходов, ТекущиеДанныеСтроки.КлючКалькуляцииРасходов);
			КонецЕсли;
		КонецЕсли;
		
		ТекущиеДанныеСтроки.ИдентификаторСтроки = Новый УникальныйИдентификатор;

	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураИнтерактивнаяПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	ТекущаяСтрока.Номенклатура = ТекущаяСтрока.НоменклатураИнтерактивная;
	ТоварыНоменклатураПриИзмененииНаКлиенте(ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОсновноеСредствоИнтерактивноеПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	ТекущаяСтрока.Номенклатура = ТекущаяСтрока.ОсновноеСредствоИнтерактивное;
	ТоварыНоменклатураПриИзмененииНаКлиенте(ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураИнтерактивнаяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораНоменклатура");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураИнтерактивнаяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураИнтерактивнаяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Товары, Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураИнтерактивнаяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Товары, Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСодержаниеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	РаботаСНоменклатуройКлиентБП.СодержаниеПриИзменении(ТекущиеДанные, ТекущаяДатаДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	Строка = Элементы.Товары.ТекущиеДанные;
	
	Если НЕ Строка.ЭтоУслуга И Строка.Количество < 0 Тогда
	
		Строка.Количество = 0;
	
	КонецЕсли;
	
	ПересчитатьСуммуТовары("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	Строка = Элементы.Товары.ТекущиеДанные;
	
	Если НЕ Строка.ЭтоУслуга И Строка.Цена < 0 Тогда
	
		Строка.Цена = 0;
	
	КонецЕсли;
	
	ПересчитатьСуммуТовары("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	Строка = Элементы.Товары.ТекущиеДанные;
	
	Если НЕ Строка.ЭтоУслуга И Строка.Сумма < 0 Тогда
	
		Строка.Сумма = 0;
	
	КонецЕсли;
	
	Строка.СуммаСкидки = Строка.Сумма * Строка.ПроцентСкидки / 100;
	
	ПересчитатьЦену("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСкидкиПриИзменении(Элемент)
	
	ВидыСкидок = ВидыСкидок();
	
	Если ВидСкидки = ВидыСкидок.НаОтдельныеПозиции И Объект.СуммаСкидки <> 0 Тогда
		РаспределитьСкидкуПоТабличнойЧасти();
	ИначеЕсли ВидСкидки = ВидыСкидок.ПоДокументуВЦелом Тогда
		Объект.СуммаСкидки = Объект.Товары.Итог("СуммаСкидки");
	ИначеЕсли ВидСкидки = ВидыСкидок.НеПредоставляется Тогда
		Объект.СуммаСкидки = 0;
	КонецЕсли;
	
	Если ВидСкидки <> ВидыСкидок.НаОтдельныеПозиции 
			И (Объект.Товары.Итог("СуммаСкидки") <> 0 ИЛИ Объект.Товары.Итог("ПроцентСкидки") <> 0) Тогда				
		Для каждого Строка Из Объект.Товары Цикл
			Если Строка.СуммаСкидки = 0 И Строка.ПроцентСкидки = 0 Тогда
				Продолжить;
			КонецЕсли;
			Строка.СуммаСкидки = 0;
			Строка.ПроцентСкидки = 0;
			НалоговыйАгентПоФЗ335 = ПокупательНалоговыйАгентПоНДС = Истина И ВедетсяУчетНДСПоФЗ335;
			ОбщегоНазначенияБПКлиент.ПересчитатьСуммуНДС(Строка, Объект.СуммаВключаетНДС,, НалоговыйАгентПоФЗ335);
		КонецЦикла;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости, код для вывода контекстных новостей при произвольном событии
	ИдентификаторыСобытий = "СкидкиПоСчетуИзмененныйМакет";
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриПроизвольномСобытии(
		ЭтотОбъект,
		ИдентификаторыСобытий);
	// Конец ИнтернетПоддержкаПользователей.Новости
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СкидкаПоДокументуПриИзменении(Элемент)
	ОбновитьИтоги(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПроцентСкидкиПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	СтрокаТабличнойЧасти.СуммаСкидки = СтрокаТабличнойЧасти.Сумма * СтрокаТабличнойЧасти.ПроцентСкидки / 100;
	
	НалоговыйАгентПоФЗ335 = ПокупательНалоговыйАгентПоНДС = Истина И ВедетсяУчетНДСПоФЗ335;
	ОбщегоНазначенияБПКлиент.ПересчитатьСуммуНДС(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС,,НалоговыйАгентПоФЗ335);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаСкидкиПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	СтрокаТабличнойЧасти.ПроцентСкидки = ?(СтрокаТабличнойЧасти.Сумма = 0, 0, СтрокаТабличнойЧасти.СуммаСкидки / СтрокаТабличнойЧасти.Сумма * 100);
	
	НалоговыйАгентПоФЗ335 = ПокупательНалоговыйАгентПоНДС = Истина И ВедетсяУчетНДСПоФЗ335;
	ОбщегоНазначенияБПКлиент.ПересчитатьСуммуНДС(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС,,НалоговыйАгентПоФЗ335);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ПересчитатьНДС("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	Строка = Элементы.Товары.ТекущиеДанные;
	
	Если НЕ Строка.ЭтоУслуга И Строка.СуммаНДС < 0 Тогда
	
		Строка.СуммаНДС = 0;
	
	КонецЕсли;
	
	ПересчитатьВсего("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТоварыРасходНаЕдиницу" Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяСтрока = Элементы.Товары.ТекущиеДанные;
		АдресСРасчетамиКалькуляции = АдресВременногоХранилищаСРасчетамиКалькуляции(ВыбраннаяСтрока.КлючКалькуляцииРасходов);
		КалькуляцииРасходовКлиент.ОткрытьКалькуляциюРасходов(ЭтотОбъект, ВыбраннаяСтрока, АдресСРасчетамиКалькуляции);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	
	УдалитьНесвязанныеКлючиКалькуляцииРасходов();
	
	Если ПлательщикТуристическогоНалога Тогда
		ТуристическийНалогКлиентСервер.УдалитьНесвязанныеИдентификаторы(ЭтотОбъект, "Товары");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИдентификаторРодительскойСтрокиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Товары");
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИдентификаторРодительскойСтрокиПриИзменении(Элемент) 
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ТуристическийНалогКлиентСервер.ПредставлениеИДРодСтроки(ТекущиеДанные, ТаблицаОсновныхУслугРазмещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИдентификаторРодительскойСтрокиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, 
		СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ТуристическийНалогКлиентСервер.ДанныеВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Товары", Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИдентификаторРодительскойСтрокиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, 
		СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ТуристическийНалогКлиентСервер.ДанныеВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Товары", Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИдентификаторРодительскойСтрокиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные,
		ВыборДобавлением, СтандартнаяОбработка)
		
	Если ВыбранноеЗначение = "ПоказатьВсе" Тогда
		АдресТаблицыУслуг = АдресТаблицыОсновныхУслугРазмещения();
		ТуристическийНалогКлиент.ВыборСтрокиУслугиРазмещения(ЭтотОбъект, СтандартнаяОбработка, АдресТаблицыУслуг, "Товары");  
    КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		НомерАктивнойСтроки = ТекущиеДанные.НомерСтроки;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыВозвратнаяТара

&НаКлиенте
Процедура ВозвратнаяТараПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураПриИзменении(Элемент)

	ДанныеСтрокаТаблицы = Новый Структура("Номенклатура, Количество, Цена, Сумма, СчетУчета");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, Элементы.ВозвратнаяТара.ТекущиеДанные);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка, ТипЦен, СуммаВключаетНДС, ДокументБезНДС,
		|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов, Склад, ДоговорКонтрагента, СтавкаНДС");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	Если Не ИспользоватьТипыЦенНоменклатуры Тогда
		ПараметрыОбъекта.ТипЦен = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
	КонецЕсли;
	ПараметрыОбъекта.СуммаВключаетНДС = Истина;
	ПараметрыОбъекта.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
	
	ВозвратнаяТараНоменклатураПриИзмененииНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта);
	
	ЗаполнитьЗначенияСвойств(Элементы.ВозвратнаяТара.ТекущиеДанные, ДанныеСтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "ВозвратнаяТара");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "ВозвратнаяТара");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараСуммаПриИзменении(Элемент)

	ПересчитатьЦену("ВозвратнаяТара");

КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараКоличествоПриИзменении(Элемент)

	ПересчитатьСуммуТовары("ВозвратнаяТара");
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараЦенаПриИзменении(Элемент)
	
	ПересчитатьСуммуТовары("ВозвратнаяТара");
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НовыйДоговор(Команда)
	
	Если ПустаяСтрока(Объект.Номер) И НЕ Записать() Тогда
		Возврат;
	КонецЕсли;
	
	НомерДоговора = СокрЛП(ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Объект.Номер, Истина, Ложь));
	НовыйДоговор = СоздатьНовыйДоговор(НомерДоговора, Объект.Дата, Объект.Контрагент, Объект.Организация);
	
	Если Объект.ДоговорКонтрагента <> НовыйДоговор Тогда
		Модифицированность = Истина;
		
		Объект.ДоговорКонтрагента = НовыйДоговор;
		
		Если НЕ Объект.ДоговорКонтрагента.Пустая() Тогда
			ДоговорКонтрагентаПриИзмененииНаСервере();	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборТовары(Команда)

	Если Не ЭтоТоварыИУслуги Тогда
		
		ПараметрыФормы = Новый Структура;
		Если Объект.Товары.Количество() > 0 Тогда
			ПараметрыФормы.Вставить("АдресОСВХранилище", ПоместитьОСВХранилище());
		КонецЕсли;
		ПараметрыФормы.Вставить("ПоказыватьГрупповыеОС", Истина);
		
		ОткрытьФорму("Обработка.ПодборОсновныхСредств.Форма.Форма", ПараметрыФормы, ЭтаФорма);
	Иначе
		ПараметрыПодбора = ПолучитьПараметрыПодбора("Товары");
		Если ПараметрыПодбора <> Неопределено Тогда
			ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтотОбъект, УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПоместитьОСВХранилище()

	ТаблицаОС = Объект.Товары.Выгрузить(, "НомерСтроки, Номенклатура");
	ТаблицаОС.Колонки.Найти("Номенклатура").Имя = "ОсновноеСредство";
	Возврат ПоместитьВоВременноеХранилище(ТаблицаОС);

КонецФункции

&НаКлиенте
Процедура ПодборВозвратнаяТара(Команда)

	ПараметрыПодбора = ПолучитьПараметрыПодбора("ВозвратнаяТара");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтотОбъект, УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТовары(Команда)

	ПараметрыФормы = ПолучитьПараметрыОбработкиТабличнойЧастиТовары();
	
	Если ПараметрыФормы <> Неопределено Тогда
		ОткрытьФорму("Обработка.ИзменениеТаблицыТоваров.Форма.Форма", ПараметрыФормы,
			ЭтотОбъект, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентов(Команда)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентовВДокументеПоКнопке(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДобавитьИзРеализации(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"Товары", ИмяСпособаЗаполненияДобавить(), "РеализацияТоваровУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДобавитьИзПоступления(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"Товары", ИмяСпособаЗаполненияДобавить(), "ПоступлениеТоваровУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДобавитьИзСчетаПоставщика(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"Товары", ИмяСпособаЗаполненияДобавить(), "СчетНаОплатуПоставщика");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЗаполнитьИзРеализации(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"Товары", ИмяСпособаЗаполненияЗаполнить(), "РеализацияТоваровУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЗаполнитьИзПоступления(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"Товары", ИмяСпособаЗаполненияЗаполнить(), "ПоступлениеТоваровУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЗаполнитьИзСчетаПоставщика(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"Товары", ИмяСпособаЗаполненияЗаполнить(), "СчетНаОплатуПоставщика");
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараДобавитьИзРеализации(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"ВозвратнаяТара", ИмяСпособаЗаполненияДобавить(), "РеализацияТоваровУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараДобавитьИзПоступления(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"ВозвратнаяТара", ИмяСпособаЗаполненияДобавить(), "ПоступлениеТоваровУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараДобавитьИзСчетаПоставщика(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"ВозвратнаяТара", ИмяСпособаЗаполненияДобавить(), "СчетНаОплатуПоставщика");
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараЗаполнитьИзРеализации(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"ВозвратнаяТара", ИмяСпособаЗаполненияЗаполнить(), "РеализацияТоваровУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараЗаполнитьИзПоступления(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"ВозвратнаяТара", ИмяСпособаЗаполненияЗаполнить(), "ПоступлениеТоваровУслуг");
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараЗаполнитьИзСчетаПоставщика(Команда)
	
	НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
		"ВозвратнаяТара", ИмяСпособаЗаполненияЗаполнить(), "СчетНаОплатуПоставщика");
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаЗаполненияТабличнойЧастиНаСервере(ВыбранноеЗначение, ТабличнаяЧасть)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ЗаполнениеНаОснованииРеализации = ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.РеализацияТоваровУслуг");
	ИзменятьЦены = Не ЗаполнениеНаОснованииРеализации;
	Если ТабличнаяЧасть = "Товары" Тогда
		ДокументОбъект.СкопироватьТовары(ВыбранноеЗначение, ИзменятьЦены);
		Если ЗаполнениеНаОснованииРеализации Тогда
			ДокументОбъект.СкопироватьУслуги(ВыбранноеЗначение, ИзменятьЦены);
		КонецЕсли;
	ИначеЕсли ТабличнаяЧасть = "ВозвратнаяТара" Тогда
		ДокументОбъект.СкопироватьВозвратнуюТару(ВыбранноеЗначение, ИзменятьЦены);
	КонецЕсли;
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзВнешнихФайловКлиент.НовыйПараметрыЗагрузкиВТабЧасть();
	
	ПараметрыЗагрузки.ИмяТабличнойЧасти = "Документ.СчетНаОплатуПокупателю.Товары";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка товаров из файла'");
	ПараметрыЗагрузки.Оповещение = Новый ОписаниеОповещения("ЗавершитьЗагрузкуИзФайла", ЭтотОбъект);
	ПараметрыЗагрузки.Пояснение = НСтр("ru = 'Укажите названия реквизитов в заголовках колонок, которые хотите загрузить. <span style=""color: НезаполненныйРеквизит"">Ненайденная</span> номенклатура не будет загружена.'");
	
	
	ПараметрыЗагрузки.ПрикладнаяЗагрузка = Истина;
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.Период = Объект.Дата;
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.Организация = Объект.Организация;
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ИмяМакета = "ЗагрузкаИзФайла";
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ОграничениеТипов = "СправочникСсылка.ОсновныеСредства";
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.ОбъектДляЗагрузки = ПредопределенноеЗначение(
		"Документ.СчетНаОплатуПокупателю.ПустаяСсылка");
	
	РеквизитыДляСопоставления = Новый СписокЗначений;
	РеквизитыДляСопоставления.Добавить("Номенклатура_Артикул");
	РеквизитыДляСопоставления.Добавить("Номенклатура_Наименование");
	ПараметрыЗагрузки.РеквизитыДляСопоставления = ПоместитьВоВременноеХранилище(
		РеквизитыДляСопоставления, УникальныйИдентификатор);

	ЗагрузкаДанныхИзВнешнихФайловКлиент.ВыбратьФайлДляЗагрузки(
		"Обработка.ЗагрузкаДанныхВТабличнуюЧасть.Форма",
		УникальныйИдентификатор,
		ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОповещения(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Настройка оповещений доступна только после записи документа.
							|Документ будет записан.'");
		ОписаниеОповещения = Новый ОписаниеОповещения("НастроитьОповещенияПослеПодтвержденияЗаписи", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.ОК);
	Иначе
		НастроитьОповещенияПослеПодтвержденияЗаписи(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервереБезКонтекста
Функция РеквизитыУсловияДоставки(СпособДоставки)
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СпособДоставки, "УслугаДоставки");
КонецФункции

&НаКлиенте
Процедура УсловияДоставкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
	
		НоваяСтрокаУслуги = Объект.Товары.Добавить();
		НоваяСтрокаУслуги.Номенклатура = ДополнительныеПараметры.УслугаДоставки;
		
		ТоварыНоменклатураПриИзмененииНаКлиенте(НоваяСтрокаУслуги);
	КонецЕсли; 
КонецПроцедуры 

&НаКлиенте
Процедура ТоварыНоменклатураПриИзмененииНаКлиенте(СтрокаТаблицы)

	ДанныеСтрокаТаблицы = Новый Структура("Номенклатура, Количество, Цена, Сумма, СтавкаНДС, СуммаНДС, 
		|ЭтоУслуга, УслугаРазмещения, СуммаСкидки, ПроцентСкидки, ЕдиницаИзмерения, Артикул, Код");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокаТаблицы, СтрокаТаблицы);
	
	ПараметрыОбъекта = Новый Структура("Организация, Дата, Ссылка, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов, Склад, ДоговорКонтрагента,
		|ДокументБезНДС, Реализация, ПокупательНалоговыйАгентПоНДС, ВедетсяУчетНДСПоФЗ335");
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, Объект);
	Если Не ИспользоватьТипыЦенНоменклатуры Тогда
		ПараметрыОбъекта.ТипЦен = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
	КонецЕсли;
	ПараметрыОбъекта.Реализация = Истина;
	ПараметрыОбъекта.ВедетсяУчетНДСПоФЗ335 = ВедетсяУчетНДСПоФЗ335;
	ПараметрыОбъекта.ПокупательНалоговыйАгентПоНДС = ПокупательНалоговыйАгентПоНДС;
	ТоварыНоменклатураПриИзмененииНаСервере(ДанныеСтрокаТаблицы, ПараметрыОбъекта);
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеСтрокаТаблицы);
	
	Если ЗначениеЗаполнено(СтрокаТаблицы.КлючКалькуляцииРасходов) Тогда
		УдалитьСтарыеДанныеКалькуляцииРасходов(СтрокаТаблицы.КлючКалькуляцииРасходов);
		СтрокаТаблицы.КлючКалькуляцииРасходов = Неопределено;
	КонецЕсли;
	
	Если СтрокаТаблицы.Свойство("Всего") Тогда
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС) - СтрокаТаблицы.СуммаСкидки;
	КонецЕсли;
	
	Если ПлательщикТуристическогоНалога Тогда
		
		УстановитьПараметрыТуристическогоНалога(СтрокаТаблицы, ЗначениеЛьготыПоУмолчанию, ДопУслугаРазмещения);
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторРодительскойСтроки)
			И СтрокаТаблицы.УслугаРазмещения <> ДопУслугаРазмещения Тогда  
			СтрокаТаблицы.ИдентификаторРодительскойСтроки = Неопределено; 
		ИначеЕсли СтрокаТаблицы.УслугаРазмещения = ДопУслугаРазмещения Тогда
			ТуристическийНалогКлиентСервер.ПодобратьСвободнуюСтрокуСУслугойРазмещения(ЭтотОбъект, "Товары");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторСтроки) Тогда
			ТуристическийНалогКлиентСервер.УдалитьСтарыеДанныеИдентификаторРодительскойСтроки(
				ЭтотОбъект, "Товары", СтрокаТаблицы.ИдентификаторСтроки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыТуристическогоНалога(СтрокаТаблицы, ЗначениеЛьготыПоУмолчанию, ДопУслугаРазмещения)
	
	Если ЗначениеЗаполнено(СтрокаТаблицы.УслугаРазмещения)
		И Не СтрокаТаблицы.УслугаРазмещения = ДопУслугаРазмещения Тогда
		СтрокаТаблицы.ВидЛьготыПоТуристическомуНалогу = ЗначениеЛьготыПоУмолчанию; 
	Иначе
		СтрокаТаблицы.ВидЛьготыПоТуристическомуНалогу = Неопределено;
	КонецЕсли;
		
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	Если УправлениеПечатьюБПВызовСервера.ИспользуетсяИзмененныйМакет(Объект.Ссылка) Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяИзмененныйМакет");
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	НастройкиУсловногоОформления = Новый Структура;

	УсловноеОформление.Элементы.Очистить();
	
	УстановитьУсловноеОформлениеВидимость();
	
	ОбновитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеВидимость()
	
	// ТоварыСтавкаНДС, ТоварыСуммаНДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаНДС");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.ДокументБезНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"ПокупательНалоговыйАгентПоНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"ВедетсяУчетНДСПоФЗ335",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ТоварыВсего
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыВсего");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		
	ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"Объект.ДокументБезНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ВидыСкидок = ВидыСкидок();
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"ВидСкидки",
		ВидСравненияКомпоновкиДанных.НеРавно,
		ВидыСкидок.НаОтдельныеПозиции);
		
	ГруппаОтбора3 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора3,
		"ПокупательНалоговыйАгентПоНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора3,
		"ВедетсяУчетНДСПоФЗ335",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора3,
		"ВидСкидки",
		ВидСравненияКомпоновкиДанных.НеРавно,
		ВидыСкидок.НаОтдельныеПозиции);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ТоварыПроцентСкидки, ТоварыСуммаСкидки
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПроцентСкидки");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаСкидки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВидСкидки", ВидСравненияКомпоновкиДанных.НеРавно, ВидыСкидок.НаОтдельныеПозиции);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Условное оформление полей калькуляции расходов
	
	КалькуляцииРасходов.УстановитьУсловноеОформление(ЭтотОбъект, "Товары");
	
	//ТоварыНоменклатураИнтерактивная
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНоменклатураИнтерактивная");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЭтоТоварыИУслуги", ВидСравненияКомпоновкиДанных.НеРавно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//ТоварыОсновноеСредствоИнтерактивное
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыОсновноеСредствоИнтерактивное");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЭтоТоварыИУслуги", ВидСравненияКомпоновкиДанных.НеРавно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ТоварыВидЛьготыПоТуристическомуНалогу
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыВидЛьготыПоТуристическомуНалогу");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.СанаторноКурортноеЛечение);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.УслугаРазмещения);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	// ТоварыИдентификаторРодительскойСтроки
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыИдентификаторРодительскойСтроки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыИдентификаторРодительскойСтроки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.УслугаРазмещения", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ИдентификаторРодительскойСтроки", ВидСравненияКомпоновкиДанных.НеЗаполнено, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "<...>");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыИдентификаторРодительскойСтроки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.УслугаРазмещения", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ИдентификаторРодительскойСтроки", ВидСравненияКомпоновкиДанных.Заполнено, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.Товары.ПредставлениеИДРодСтроки"));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьУсловноеОформление(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТовары
		И НЕ Форма.НастройкиУсловногоОформления.Свойство("ТоварыПроинициализировано") Тогда
		
		Форма.УстановитьУсловноеОформлениеТовары();
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеТовары() Экспорт
	
	НастройкиУсловногоОформления.Вставить("ТоварыПроинициализировано", Истина);

	// Номенклатура
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	Если ЭтоТоварыИУслуги Тогда
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНоменклатураИнтерактивная");
	Иначе
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыОсновноеСредствоИнтерактивное");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.Содержание", ВидСравненияКомпоновкиДанных.НеЗаполнено);	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.Номенклатура", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// Содержание
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСодержание");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ЭтоУслуга", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Количество
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКоличество");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ЭтоУслуга", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// ТоварыСуммаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	РеквизитыОрганизацииСсылка = ПроверкаРеквизитовОрганизации.СтрокаСообщенияНеЗаполненБанковскийСчет();
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьСостояниеДокумента();
	
	СтатусДокумента = ПолучитьСтатусДокумента(Объект.Организация, Объект.Ссылка);
	
	СрокОплатыВРабочихДнях = Константы.СрокОплатыСчетовПокупателю.Получить();
	УстановитьСрокОплатыДокументаРасчетов();
	
	ТекущаяДатаДокумента = Объект.Дата;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ОсновнойБанковскийСчетОрганизацииЗаполнен = ЗначениеЗаполнено(Объект.ОрганизацияПолучатель.ОсновнойБанковскийСчет);
	
	ИспользоватьДоставкуТранспортнойКомпанией = ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуТранспортнойКомпанией");
	
	ИспользоватьНесколькоСкладовБухгалтерскийУчет = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладовБухгалтерскийУчет");
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ЮрФизЛицо         = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ЮридическоеФизическоеЛицо");
		ПокупательФизлицо = (ЮрФизЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.ДоговорКонтрагента, "ВидДоговора, РасчетыВУсловныхЕдиницах, ВалютаВзаиморасчетов, УчетАгентскогоНДСПокупателем");
		ЭтоКомиссия = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
		ПокупательНалоговыйАгентПоНДС = РеквизитыДоговора.УчетАгентскогоНДСПокупателем;
		ВалютаВзаиморасчетов = РеквизитыДоговора.ВалютаВзаиморасчетов;
		Если РеквизитыДоговора.РасчетыВУсловныхЕдиницах Тогда
			ВалютаОплаты = ВалютаРегламентированногоУчета;
		Иначе
			ВалютаОплаты = ВалютаВзаиморасчетов;
		КонецЕсли;
		ЗаполнитьСписокАдресовКонтрагента();
	Иначе
		
		ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
		ЭтоКомиссия = Ложь;
		ПокупательНалоговыйАгентПоНДС = Ложь;
		ВалютаОплаты = ВалютаРегламентированногоУчета;
		
		Если ЗначениеЗаполнено(Объект.Контрагент)
		и НЕ ЗначениеЗаполнено(Объект.Ссылка) 
		и НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			КонтрагентОбработатьИзменение();
		Иначе
			ЗаполнитьСписокАдресовКонтрагента();
			Если ВестиУчетПоДоговорам Тогда
				ПредлагатьНовыйДоговор = РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПредлагатьНовыйДоговорСНумерацией();
	
	ВидыСкидок = ВидыСкидок();
	Если Объект.СуммаСкидки <> 0 Тогда
		ВидСкидки = ВидыСкидок.ПоДокументуВЦелом;
	ИначеЕсли Объект.Товары.Итог("СуммаСкидки") <> 0 Тогда
		ВидСкидки = ВидыСкидок.НаОтдельныеПозиции;
	Иначе
		ВидСкидки = ВидыСкидок.НеПредоставляется;
	КонецЕсли;
	Элементы.ОрганизацияПолучатель.СписокВыбора.ЗагрузитьЗначения(
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделений(Объект.Организация).ВыгрузитьЗначения());
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	Элементы.ОрганизацияПолучатель.Видимость = НЕ ЗначениеЗаполнено(Объект.ОрганизацияПолучатель)
		ИЛИ Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	ПроверитьКорректностьСчетаСервер();
	
	ЗаполнитьДополнительныеРеквизитыQRКода(Истина);
		
	УстановитьОтборСпособДоставки();
	
	НастройкиНалоговИОтчетовПредупреждениеФормы.ОтобразитьПредупреждение(ЭтотОбъект, Объект.Организация, Объект.Дата, Ложь);
	
	ЗаполнитьНастройкиОповещения();
	
	УправлениеФормой(ЭтотОбъект);
	
	Элементы.ТоварыРасходНаЕдиницу.Видимость = ИспользуетсяКалькуляцияРасходов И ЭтоТоварыИУслуги;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
					Элементы,
					"ФормаПоказатьВСпискеСчетНаОплатуПокупателю",
					"Видимость",
					ЭтоТоварыИУслуги);
					
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
					Элементы,
					"ФормаПоказатьВСпискеДокументыПоОС",
					"Видимость",
					НЕ ЭтоТоварыИУслуги);
	
	КалькуляцииРасходов.ЗаполнитьКалькуляциюРасходов(ЭтотОбъект, "Товары");
	
	// Проверка буфера обмена на наличие скопированных строк
	УстановитьДоступностьКомандыВставки(ЭтотОбъект, Не ОбщегоНазначения.ПустойБуферОбмена());
	
	Если ПлательщикТуристическогоНалога Тогда
		УстановитьРеквизитыФормыТуристическийНалог();  
		ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Товары");
		ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Товары");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	ПрименяетсяУСН = УчетнаяПолитика.ПрименяетсяУСН(Объект.Организация, Объект.Дата);
	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата)
		Или УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Объект.Организация, Объект.Дата);
	ИспользоватьТипыЦенНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры")
		И ПравоДоступа("Чтение", Метаданные.Справочники.ТипыЦенНоменклатуры);
	ЕстьВалютныйУчет                = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
	ВестиУчетПоДоговорам            = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ВедетсяУчетНДСПоФЗ335           = УчетНДС.ВедетсяУчетНДСПоФЗ335(Объект.Дата);
	
	ИспользоватьНесколькоБанковскихСчетовОрганизации =
		Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Объект.ОрганизацияПолучатель);
		
	
	ЗаполнятьОтветственногоПоПродажамИзКонтрагента = Константы.ЗаполнятьОтветственногоПоПродажамИзКонтрагента.Получить();
	
	КалькуляцииРасходов.УстановитьФункциональныеОпцииФормы(ЭтотОбъект);
	
	ПлательщикТуристическогоНалога = УчетнаяПолитика.ПлательщикТуристическогоНалога(Объект.Организация, Объект.Дата);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	УстановитьПодсказкуСрокаОплаты(Форма);
	
	Если Не Форма.ЭтоТоварыИУслуги Тогда
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ВидДоговора",
			Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"))));
		НовыеПараметры = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НовыйПараметр));
		
		Элементы.ДоговорКонтрагента.ПараметрыВыбора = НовыеПараметры;
	КонецЕсли;
	
	Элементы.ТоварыСодержание.Видимость = Форма.ЭтоТоварыИУслуги;
	Элементы.НадписьПовторение.Видимость = Форма.ЭтоТоварыИУслуги;
	Элементы.ЗагрузитьИзФайла.Видимость = Форма.ЭтоТоварыИУслуги;
	
	Если НЕ Форма.ЭтоТоварыИУслуги Тогда
		Элементы.ТоварыНоменклатураАртикул.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ИзменитьТовары.Видимость = Форма.ЭтоТоварыИУслуги;
	Элементы.ТоварыГруппаЗаполнить.Видимость = Форма.ЭтоТоварыИУслуги;
	
	// Доступность взаимосвязанных полей
	Элементы.ДоговорКонтрагента.Доступность       = ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Контрагент);
	Элементы.НовыйДоговор.Доступность             = ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Контрагент);
	Элементы.ПодразделениеОрганизации.Доступность = ЗначениеЗаполнено(Объект.Организация);
	Элементы.ОрганизацияПолучатель.Доступность    = ЗначениеЗаполнено(Объект.Организация)
		И Элементы.ОрганизацияПолучатель.СписокВыбора.Количество() > 1;
	Элементы.СтруктурнаяЕдиница.Доступность       = ЗначениеЗаполнено(Объект.ОрганизацияПолучатель);
	
	Элементы.ГруппаАдресДоставки.Видимость = Форма.ИспользоватьДоставкуТранспортнойКомпанией 
		И ЗначениеЗаполнено(Объект.СпособДоставки)
		И Объект.СпособДоставки <> ПредопределенноеЗначение("Справочник.СпособыДоставки.Самовывоз");
	
	Элементы.Доставка.Видимость = Форма.ИспользоватьНесколькоСкладовБухгалтерскийУчет
		Или Не Форма.ИспользоватьДоставкуТранспортнойКомпанией
		Или Не ЗначениеЗаполнено(Объект.СпособДоставки);
	
	ВидыСкидок = ВидыСкидок();
	Элементы.ИтогиСкидка.Видимость = (Форма.ВидСкидки = ВидыСкидок.НаОтдельныеПозиции);
	Элементы.ИтогиВсего.Заголовок = ?(Форма.ВидСкидки = ВидыСкидок.НеПредоставляется, "Всего", "Всего со скидкой");
	
	НалоговыйАгентПоФЗ335 = Форма.ПокупательНалоговыйАгентПоНДС = Истина И Форма.ВедетсяУчетНДСПоФЗ335;
	
	Элементы.ИтогиВсегоНДС.Видимость = Не (Объект.ДокументБезНДС ИЛИ НалоговыйАгентПоФЗ335);
	
	Элементы.СкидкаПоДокументу.Видимость = (Форма.ВидСкидки = ВидыСкидок.ПоДокументуВЦелом);
	
	ОбновитьИтоги(Форма);
	СформироватьНадписьЦеныИВалюта(Форма);
	Элементы.ТоварыЦеныИВалюта.Видимость = НЕ НалоговыйАгентПоФЗ335;
	
	УстановитьЗаголовокРеквизитыПечатиQRКод(Форма);
	
	ПользователиБПКлиентСервер.УстановитьПодсказкуОтветственного(Форма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораРеквизитыОрганизацииКонтрагента(ВыбранноеЗначение, ИсточникВыбора)

	Модифицированность = Истина;
	
	ЗначениеСкладаДоИзменения = Объект.Склад;
	
	ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение);
	
	Если ЗначениеСкладаДоИзменения <> Объект.Склад И ЗначениеЗаполнено(Объект.Склад) Тогда
		Объект.ПодразделениеОрганизации = ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(Объект.Организация, Объект.Склад);
	КонецЕсли;
	
	УстановитьЗаголовокРеквизитыПечатиQRКод(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеЗакладок()

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару")
			ИЛИ Не ЭтоТоварыИУслуги Тогда
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;

КонецПроцедуры

// Обслуживание типа цен - валюты - НДС:

&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюты()

	// 1. Формируем структуру параметров для заполнения формы "Цены и Валюта".
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВалютаДокумента",      Объект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс",                 Объект.КурсВзаиморасчетов);
	СтруктураПараметров.Вставить("Кратность",            Объект.КратностьВзаиморасчетов);
	СтруктураПараметров.Вставить("СуммаВключаетНДС",     Объект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("ДокументБезНДС",       Объект.ДокументБезНДС);
	СтруктураПараметров.Вставить("Контрагент",           Объект.Контрагент);
	СтруктураПараметров.Вставить("Договор",              Объект.ДоговорКонтрагента);
	СтруктураПараметров.Вставить("Организация",          Объект.Организация);
	СтруктураПараметров.Вставить("ДатаДокумента",        Объект.Дата);
	СтруктураПараметров.Вставить("ТипЦен",               Объект.ТипЦен);
	СтруктураПараметров.Вставить("ТолькоПросмотр",       ТолькоПросмотр);

	// 2. Открываем форму "Цены и Валюта".
	ДополнительныеПараметры = Новый Структура;
	
	Если ИспользоватьТипыЦенНоменклатуры
		ИЛИ (ЕстьВалютныйУчет И ВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета) Тогда 
		ОткрыватьИзМеню = Ложь;
	Иначе
		ОткрыватьИзМеню = Истина;
		ДополнительныеПараметры.Вставить("СтруктураПараметровКоманды", СтруктураПараметров);
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ОткрыватьИзМеню Тогда
		
		СписокКоманд = Новый СписокЗначений;
		
		Если Не ПлательщикНДС Тогда
			СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.ДокументБезНДС"));
		КонецЕсли;
		СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДССверху"));
		СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме"));
		
		ПоказатьВыборИзМеню(ОповещениеОЗакрытии, СписокКоманд, Элементы.ТоварыЦеныИВалюта);		
	Иначе
		ОткрытьФорму("ОбщаяФорма.ФормаЦеныИВалюта", СтруктураПараметров,,,,,ОповещениеОЗакрытии);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.Свойство("СтруктураПараметровКоманды") Тогда
		
		СтруктураЦеныИВалюта = ДополнительныеПараметры.СтруктураПараметровКоманды;
		
		СуммаВключаетНДСДоИзменения = СтруктураЦеныИВалюта.СуммаВключаетНДС;
		Если РезультатЗакрытия = Неопределено Тогда 
			Возврат;
		ИначеЕсли РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.ДокументБезНДС") Тогда
			СтруктураЦеныИВалюта.ДокументБезНДС 	= Истина;
		ИначеЕсли РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме") Тогда
			СтруктураЦеныИВалюта.ДокументБезНДС 	= Ложь;
			СтруктураЦеныИВалюта.СуммаВключаетНДС 	= Истина;
		Иначе
			СтруктураЦеныИВалюта.ДокументБезНДС 	= Ложь;
			СтруктураЦеныИВалюта.СуммаВключаетНДС 	= Ложь;
		КонецЕсли;
		
		СтруктураЦеныИВалюта.Вставить("ПерезаполнитьЦены",    Ложь);
		СтруктураЦеныИВалюта.Вставить("ПересчитатьЦены",      Ложь);
		СтруктураЦеныИВалюта.Вставить("ПересчитатьНДС",       СуммаВключаетНДСДоИзменения <> СтруктураЦеныИВалюта.СуммаВключаетНДС
			ИЛИ Объект.ДокументБезНДС <> СтруктураЦеныИВалюта.ДокументБезНДС);
		СтруктураЦеныИВалюта.Вставить("БылиВнесеныИзменения", СуммаВключаетНДСДоИзменения <> СтруктураЦеныИВалюта.СуммаВключаетНДС
			ИЛИ Объект.ДокументБезНДС <> СтруктураЦеныИВалюта.ДокументБезНДС);
			
	Иначе
		СтруктураЦеныИВалюта = РезультатЗакрытия;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураЦеныИВалюта) = Тип("Структура") И СтруктураЦеныИВалюта.БылиВнесеныИзменения Тогда

		ВалютаДоИзменения    = Объект.ВалютаДокумента;
		КурсДоИзменения 	 = Объект.КурсВзаиморасчетов;
		КратностьДоИзменения = Объект.КратностьВзаиморасчетов;
		
		Объект.ТипЦен					= СтруктураЦеныИВалюта.ТипЦен;
		Объект.ВалютаДокумента 			= СтруктураЦеныИВалюта.ВалютаДокумента;
		Объект.КурсВзаиморасчетов 		= СтруктураЦеныИВалюта.Курс;
		Объект.КратностьВзаиморасчетов 	= СтруктураЦеныИВалюта.Кратность;
		Объект.СуммаВключаетНДС 		= СтруктураЦеныИВалюта.СуммаВключаетНДС;
		Объект.ДокументБезНДС		   	= СтруктураЦеныИВалюта.ДокументБезНДС;
		
		Модифицированность = Истина;
		
		Если СтруктураЦеныИВалюта.ПерезаполнитьЦены ИЛИ СтруктураЦеныИВалюта.ПересчитатьЦены ИЛИ СтруктураЦеныИВалюта.ПересчитатьНДС Тогда
			ЗаполнитьРассчитатьСуммы(
				ВалютаДоИзменения, 
				КурсДоИзменения,
				КратностьДоИзменения,
				СтруктураЦеныИВалюта.ПерезаполнитьЦены,
				СтруктураЦеныИВалюта.ПересчитатьЦены,
				СтруктураЦеныИВалюта.ПересчитатьНДС);
		КонецЕсли;
		
		СформироватьНадписьЦеныИВалюта(ЭтотОбъект);
		
	КонецЕсли;
		 	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРассчитатьСуммы(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь)
	
	ТаблицаЦенНоменклатуры = Неопределено;
	
	Если ПерезаполнитьЦены Тогда
		
		СписокНоменклатуры	= ОбщегоНазначения.ВыгрузитьКолонку(Объект.Товары, "Номенклатура", Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокНоменклатуры,
			ОбщегоНазначения.ВыгрузитьКолонку(Объект.ВозвратнаяТара, "Номенклатура"), Истина);
		
		Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда	
			ТаблицаЦенНоменклатуры = Ценообразование.ПолучитьТаблицуЦенНоменклатуры(
				СписокНоменклатуры,
				Объект.ТипЦен,
				Объект.Дата);
		КонецЕсли;
		
	ИначеЕсли ПересчитатьЦены Тогда
		
		Если КурсДоИзменения <> 0 И КратностьДоИзменения <> 0 Тогда
			СтруктураКурса = Новый Структура("Курс, Кратность", КурсДоИзменения, КратностьДоИзменения);
		Иначе
			СтруктураКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДоИзменения, Объект.Дата);
		КонецЕсли;
		
	КонецЕсли;

	Для каждого Строка Из Объект.Товары Цикл
		ЗначениеПустогоКоличества = ?(Строка.ЭтоУслуга, 1, 0);
		ЗаполнитьРассчитатьСуммыВСтроке(Строка, ТаблицаЦенНоменклатуры, ВалютаДоИзменения, СтруктураКурса, ПерезаполнитьЦены,
			ПересчитатьЦены, ПересчитатьНДС, Истина, ЗначениеПустогоКоличества);
	КонецЦикла;
	Для каждого Строка Из Объект.ВозвратнаяТара Цикл
		ЗаполнитьРассчитатьСуммыВСтроке(Строка, ТаблицаЦенНоменклатуры, ВалютаДоИзменения, СтруктураКурса, ПерезаполнитьЦены,
			ПересчитатьЦены, ПересчитатьНДС, Ложь, 0);
	КонецЦикла;
	
	УправлениеФормой(ЭтотОбъект);
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРассчитатьСуммыВСтроке(Строка, ТаблицаЦенНоменклатуры, ВалютаПередИзменением, СтруктураКурса, ПерезаполнитьЦены, ПересчитатьЦены, ПересчитатьНДС, ЕстьНДС, ЗначениеПустогоКоличества)
	Если ПерезаполнитьЦены И ТаблицаЦенНоменклатуры <> Неопределено Тогда
		
		НайденнаяСтрока	= ТаблицаЦенНоменклатуры.Найти(Строка.Номенклатура, "Номенклатура");
		Если НайденнаяСтрока <> Неопределено Тогда
			Цена = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				НайденнаяСтрока.Цена, НайденнаяСтрока.Валюта, Объект.ВалютаДокумента, НайденнаяСтрока.Курс,
				Объект.КурсВзаиморасчетов, НайденнаяСтрока.Кратность, Объект.КратностьВзаиморасчетов);
			ЦенаВключаетНДС = НайденнаяСтрока.ЦенаВключаетНДС;
		Иначе
			Цена = 0;
			// Признак того, что цена включает НДС, хранится в реквизите ЦенаВключаетНДС типа цен
			Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
				ЦенаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТипЦен, "ЦенаВключаетНДС");
			Иначе
				ЦенаВключаетНДС = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ПересчитатьЦены Тогда
			
			Цена = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				Строка.Цена, ВалютаПередИзменением, Объект.ВалютаДокумента, СтруктураКурса.Курс,
				Объект.КурсВзаиморасчетов, СтруктураКурса.Кратность, Объект.КратностьВзаиморасчетов);
			
		Иначе
			Цена = Строка.Цена;
		КонецЕсли;
		// Признак того, что цена включает НДС, хранится в реквизите СуммаВключаетНДС документа
		ЦенаВключаетНДС = ?(ПересчитатьНДС, НЕ Объект.СуммаВключаетНДС, Объект.СуммаВключаетНДС);
	КонецЕсли;

	Если ЕстьНДС Тогда
		
		Если Объект.ДокументБезНДС Тогда
			Строка.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
		КонецЕсли;
		
		Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(Цена, ЦенаВключаетНДС, Объект.СуммаВключаетНДС, 
			УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(Строка.СтавкаНДС));
			
		ЦенаИзменилась = Цена <> Строка.Цена;
		
		Если ЦенаИзменилась Тогда
			Строка.Цена        = Цена;
			Строка.Сумма       = Строка.Цена * ?(Строка.Количество = 0, ЗначениеПустогоКоличества, Строка.Количество);
			Строка.СуммаСкидки = Строка.Сумма * Строка.ПроцентСкидки / 100;
		КонецЕсли;
		
		Если ЦенаИзменилась ИЛИ ПересчитатьНДС Тогда
			Строка.СуммаНДС    = УчетНДСКлиентСервер.РассчитатьСуммуНДС(Строка.Сумма - Строка.СуммаСкидки, Объект.СуммаВключаетНДС,
				УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(Строка.СтавкаНДС));
				
			Строка.Всего       = Строка.Сумма + ?(Объект.СуммаВключаетНДС, 0, Строка.СуммаНДС) - Строка.СуммаСкидки;
		КонецЕсли;
		
	ИначеЕсли Цена <> Строка.Цена Тогда
		Строка.Цена  = Цена;
		Строка.Сумма = Строка.Цена * ?(Строка.Количество = 0, ЗначениеПустогоКоличества, Строка.Количество);
	КонецЕсли;
КонецПроцедуры

// Серверная обработка изменения реквизитов:

&НаСервере
Процедура ДатаПриИзмененииНаСервере(ТребуетсяВызовСервера, ТребуетсяПерерасчитатьСрокОплаты)
	
	Если ТребуетсяВызовСервера Тогда
		
		ДатаОбработатьИзменение();
		РаботаСНоменклатуройБП.ОбновитьСодержаниеУслуг(Объект.Товары, Объект.Дата, ТекущаяДатаДокумента);
		УправлениеФормой(ЭтотОбъект);
		
		ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);
		
	КонецЕсли;
	
	Если ТребуетсяПерерасчитатьСрокОплаты Тогда
		УстановитьСрокОплатыДокументаРасчетов();
	КонецЕсли;
	
	ПроверитьКорректностьСчетаСервер();
	
	ЗаполнитьДополнительныеРеквизитыQRКода();
	
	НастройкиНалоговИОтчетовПредупреждениеФормы.ОтобразитьПредупреждение(ЭтотОбъект, Объект.Организация, Объект.Дата, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ДатаОбработатьИзменение()
	
	УстановитьФункциональныеОпцииФормы();
	УстановитьПараметрыВыбораСтавкиНДС();
	
	Если ПлательщикНДС Тогда 
		Объект.ДокументБезНДС = Ложь;
	КонецЕсли;
	
	Если (Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета) Тогда
		СтруктураКурсаДокумента        = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
		Объект.КурсВзаиморасчетов      = СтруктураКурсаДокумента.Курс;
		Объект.КратностьВзаиморасчетов = СтруктураКурсаДокумента.Кратность;
	КонецЕсли;
	
	Если ПлательщикТуристическогоНалога Тогда
		УстановитьГостиницу();
		УстановитьРеквизитыФормыТуристическийНалог();  
		ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Товары");
		ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Товары");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ОрганизацияОбработатьИзменение();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ОрганизацияОбработатьИзменение()

	Если ПравоДоступа("Чтение", Метаданные.Справочники.Склады)
		И ПравоДоступа("Чтение", Метаданные.Справочники.ПодразделенияОрганизаций) Тогда
		
		ПодразделениеПоУмолчанию = ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(Объект.Организация, Объект.Склад);
		
		Если НЕ ЗначениеЗаполнено(ПодразделениеПоУмолчанию) Тогда
			ПодразделениеПоУмолчанию = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ПодразделениеОрганизации) 
			И БухгалтерскийУчетПереопределяемый.ПодразделениеПринадлежитОрганизации(ПодразделениеПоУмолчанию, Объект.Организация) Тогда
			Объект.ПодразделениеОрганизации = ПодразделениеПоУмолчанию;
		КонецЕсли;
		
	КонецЕсли;

	Объект.ОрганизацияПолучатель = Объект.Организация;
	Документы.СчетНаОплатуПокупателю.ЗаполнитьУсловияСчетаПоУмолчанию(Объект);
	Элементы.ОрганизацияПолучатель.СписокВыбора.ЗагрузитьЗначения(
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделений(Объект.Организация).ВыгрузитьЗначения());

	УстановитьПараметрыВыбораСтавкиНДС();
	УстановитьФункциональныеОпцииФормы();
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);

	ОсновнойБанковскийСчетОрганизацииЗаполнен = ПроверкаРеквизитовОрганизации.ОсновнойБанковскийСчетОрганизацииЗаполнен(Объект.ОрганизацияПолучатель);
	
	Если ПлательщикНДС Тогда 
		Объект.ДокументБезНДС = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		КонтрагентОбработатьИзменение();
	КонецЕсли;
		
	ЗаполнитьИнформациюОбОплатеСчета();
	
	УстановитьОтборСпособДоставки();
	
	НастройкиНалоговИОтчетовПредупреждениеФормы.ОтобразитьПредупреждение(ЭтотОбъект, Объект.Организация, Объект.Дата, Ложь);
	
	Если ПлательщикТуристическогоНалога Тогда
		УстановитьГостиницу();
		УстановитьРеквизитыФормыТуристическийНалог();  
		ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Товары");
		ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Товары");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСпособДоставки()

	СписокВыбора = Новый Массив;
	СписокВыбора.Добавить(Справочники.Организации.ПустаяСсылка());
	СписокВыбора.Добавить(Объект.Организация);
	
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Организация", Новый ФиксированныйМассив(СписокВыбора)));
	
	Элементы.СпособДоставки.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);

КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	
	КонтрагентОбработатьИзменение();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокАдресовКонтрагента()

	МассивАдресовДоставкиКонтрагента = УправлениеКонтактнойИнформациейБП.СписокАдресовОбъекта(Объект.Контрагент, Объект.Дата, Истина);
	Если МассивАдресовДоставкиКонтрагента.Количество() > 0 Тогда
		Элементы.АдресДоставки.СписокВыбора.Очистить();
		Для каждого АдресДоставки Из МассивАдресовДоставкиКонтрагента Цикл
			Элементы.АдресДоставки.СписокВыбора.Добавить(АдресДоставки.Значение.Значение, АдресДоставки.Представление);
		КонецЦикла; 
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура КонтрагентОбработатьИзменение()
	
	ЗаполнитьСписокАдресовКонтрагента();
	
	СписокАдресов = Элементы.АдресДоставки.СписокВыбора;
	Если СписокАдресов.Количество() > 0 Тогда
		ЗначениеАдреса = СписокАдресов[0].Значение;
		Объект.АдресДоставкиЗначение = ?(ТипЗнч(ЗначениеАдреса) = Тип("Структура"), ЗначениеАдреса.Значение, ЗначениеАдреса);
		Объект.АдресДоставки = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(Объект.АдресДоставкиЗначение);
	Иначе
		Объект.АдресДоставкиЗначение = "";
		Объект.АдресДоставки = "";
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		Объект.ДоговорКонтрагента, Объект.Контрагент, Объект.Организация, 
		ПолучитьМассивВидовДоговоров());

	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ДоговорКонтрагентаОбработатьИзменение();
		ПредлагатьНовыйДоговор = Ложь;
	ИначеЕсли ВестиУчетПоДоговорам Тогда
		ПредлагатьНовыйДоговор = РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент);
	КонецЕсли;
	
	УстановитьПредлагатьНовыйДоговорСНумерацией();
	
	ЮрФизЛицо  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ЮридическоеФизическоеЛицо");
	ЭтоФизлицо = (ЮрФизЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	
	ЗаполнитьДополнительныеРеквизитыQRКода(ПокупательФизлицо <> ЭтоФизлицо);
	
	ПокупательФизлицо = ЭтоФизлицо;
	
	ПользователиБП.УстановитьОтветственногоДокументаПродаж(Объект, ЗаполнятьОтветственногоПоПродажамИзКонтрагента);
	
	Документы.СчетНаОплатуПокупателю.ЗаполнитьВариантПечатиQRКода(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	
	ДоговорКонтрагентаОбработатьИзменение();
	
	ЗаполнитьДополнительныеРеквизитыQRКода();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаОбработатьИзменение()
	
	ВалютаДоИзменения 	 = Объект.ВалютаДокумента;
	КурсДоИзменения   	 = Объект.КурсВзаиморасчетов;
	КратностьДоИзменения = Объект.КратностьВзаиморасчетов;
	ТипЦенДоИзменения    = Объект.ТипЦен;
	СуммаВключаетНДСДоИзменения   = Объект.СуммаВключаетНДС;
	ПокупательНалоговыйАгентПоНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.ДоговорКонтрагента, "УчетАгентскогоНДСПокупателем");
	
	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Объект.ДоговорКонтрагента, "ВалютаВзаиморасчетов, Владелец, ТипЦен, ВидДоговора, РасчетыВУсловныхЕдиницах");
	
	ВалютаВзаиморасчетов   = РеквизитыДоговора.ВалютаВзаиморасчетов;
	Объект.ВалютаДокумента = ВалютаВзаиморасчетов;
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Объект.Контрагент  = РеквизитыДоговора.Владелец;
	КонецЕсли;
	
	Если ВалютаДоИзменения <> Объект.ВалютаДокумента Тогда
		СтруктураКурсаДокумента        = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
		Объект.КурсВзаиморасчетов      = СтруктураКурсаДокумента.Курс;
		Объект.КратностьВзаиморасчетов = СтруктураКурсаДокумента.Кратность;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыДоговора.ТипЦен) Тогда
		 Объект.ТипЦен           = РеквизитыДоговора.ТипЦен;
		 Объект.СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыДоговора.ТипЦен, "ЦенаВключаетНДС");
	КонецЕсли;
	
	Если ПравоРедактированияСрокаОплаты Тогда
		УстановитьСрокОплатыДокументаРасчетов();
	КонецЕсли; 
	
	ПересчитатьЦены = Объект.ВалютаДокумента <> ВалютаДоИзменения
		ИЛИ Объект.КурсВзаиморасчетов <> КурсДоИзменения
		ИЛИ Объект.ТипЦен <> ТипЦенДоИзменения;
	ПересчитатьНДС = Объект.СуммаВключаетНДС <> СуммаВключаетНДСДоИзменения;
	
	Если ЕстьСтрокиВТабличныхЧастях() Тогда
		ЗаполнитьРассчитатьСуммы(ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, Ложь, ПересчитатьЦены, ПересчитатьНДС);
	КонецЕсли;
	
	ЭтоКомиссия = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
	
	Если РеквизитыДоговора.РасчетыВУсловныхЕдиницах Тогда
		ВалютаОплаты = ВалютаРегламентированногоУчета;
	Иначе
		ВалютаОплаты = РеквизитыДоговора.ВалютаВзаиморасчетов;
	КонецЕсли;
	
	ПредлагатьНовыйДоговор = Ложь;

	ОрганизацияПолучательПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПолучательПриИзмененииНаСервере()
	
	ОсновнойБанковскийСчетОрганизацииЗаполнен =
		ПроверкаРеквизитовОрганизации.ОсновнойБанковскийСчетОрганизацииЗаполнен(Объект.ОрганизацияПолучатель);
		
	Если ПолучитьФункциональнуюОпцию("ИспользуетсяГособоронзаказ") И ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ГосударственныйКонтракт = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорКонтрагента, "ГосударственныйКонтракт");
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтруктурнаяЕдиница, "ГосударственныйКонтракт") <> ГосударственныйКонтракт Тогда
			Объект.СтруктурнаяЕдиница = Неопределено;
		КонецЕсли; 
	Иначе
		ГосударственныйКонтракт = Неопределено;
	КонецЕсли; 
		
	УчетДенежныхСредствБП.УстановитьБанковскийСчет(
		Объект.СтруктурнаяЕдиница, Объект.ОрганизацияПолучатель, ВалютаОплаты, Истина,,,ГосударственныйКонтракт);
	
	БанковскийСчетПриИзмененииНаСервере();
	
КонецПроцедуры

// Пересчеты реквизитов в строках табличных частей

&НаКлиенте
Процедура ПересчитатьСуммуТовары(ИмяТабЧасти)

	Строка = Элементы[ИмяТабЧасти].ТекущиеДанные;
	
	ЗначениеПустогоКоличества = ?(ИмяТабЧасти = "Товары" И Строка.ЭтоУслуга, 1, 0);
	
	НалоговыйАгентПоФЗ335 = ПокупательНалоговыйАгентПоНДС = Истина И ВедетсяУчетНДСПоФЗ335;
	ОбщегоНазначенияБПКлиент.ПересчитатьСумму(Строка, Объект.СуммаВключаетНДС, ЗначениеПустогоКоличества,,НалоговыйАгентПоФЗ335);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьЦену(ИмяТабЧасти)

	Строка = Элементы[ИмяТабЧасти].ТекущиеДанные;
	
	Строка.Цена        = Строка.Сумма / ?(Строка.Количество = 0, 1, Строка.Количество);
	
	НалоговыйАгентПоФЗ335 = ПокупательНалоговыйАгентПоНДС = Истина И ВедетсяУчетНДСПоФЗ335;
	ОбщегоНазначенияБПКлиент.ПересчитатьСуммуНДС(Строка, Объект.СуммаВключаетНДС,,НалоговыйАгентПоФЗ335);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьНДС(ИмяТабЧасти)
	
	Строка = Элементы[ИмяТабЧасти].ТекущиеДанные;
	
	НалоговыйАгентПоФЗ335 = ПокупательНалоговыйАгентПоНДС = Истина И ВедетсяУчетНДСПоФЗ335;
	ОбщегоНазначенияБПКлиент.ПересчитатьСуммуНДС(Строка, Объект.СуммаВключаетНДС,,НалоговыйАгентПоФЗ335);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьВсего(ИмяТабЧасти)

	Строка = Элементы[ИмяТабЧасти].ТекущиеДанные;
	Строка.Всего = Строка.Сумма + ?(Объект.СуммаВключаетНДС, 0, Строка.СуммаНДС) - Строка.СуммаСкидки;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ТоварыНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ПараметрыОбъекта)

	МетаданныеДокумента = ПараметрыОбъекта.Ссылка.Метаданные();
	ЭтоОсновноеСредство = ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) = Тип("СправочникСсылка.ОсновныеСредства");
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура) Тогда
		СтрокаТабличнойЧасти.Вставить("Содержание", "");
		СтрокаТабличнойЧасти.ЭтоУслуга = Истина; 
		СтрокаТабличнойЧасти.УслугаРазмещения = Неопределено;
	Иначе
		
		Если ЭтоОсновноеСредство Тогда
			ИменаРеквизитов = "Наименование, НаименованиеПолное, ЕдиницаУчета, Код";
		Иначе
			ИменаРеквизитов = "Наименование, НаименованиеПолное, ПериодичностьУслуги, Услуга, УслугаРазмещения, 
			|ЕдиницаИзмерения, Артикул, Код";
		КонецЕсли;
		
		РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТабличнойЧасти.Номенклатура,
			ИменаРеквизитов);
		
		Если ЭтоОсновноеСредство Тогда
			РеквизитыНоменклатуры.Вставить("Услуга", Ложь);
			СтрокаТабличнойЧасти.Вставить("ЕдиницаИзмерения", РеквизитыНоменклатуры.ЕдиницаУчета);
			СтрокаТабличнойЧасти.Вставить("Код", РеквизитыНоменклатуры.Код);
		Иначе
			СтрокаТабличнойЧасти.Вставить("ЕдиницаИзмерения", РеквизитыНоменклатуры.ЕдиницаИзмерения);
			СтрокаТабличнойЧасти.Вставить("Код", РеквизитыНоменклатуры.Код);
			СтрокаТабличнойЧасти.Вставить("Артикул", РеквизитыНоменклатуры.Артикул);
		КонецЕсли;
		
		Если НЕ РеквизитыНоменклатуры.Услуга Тогда
			Содержание = "";
		Иначе
			Если ПустаяСтрока(РеквизитыНоменклатуры.НаименованиеПолное) Тогда
				Содержание = РеквизитыНоменклатуры.Наименование;
			Иначе
				Содержание = РеквизитыНоменклатуры.НаименованиеПолное;
			КонецЕсли;
			Содержание = РаботаСНоменклатуройКлиентСерверБП.СодержаниеУслуги(
				Содержание,
				РеквизитыНоменклатуры.ПериодичностьУслуги,
				ПараметрыОбъекта.Дата);
			СтрокаТабличнойЧасти.УслугаРазмещения = РеквизитыНоменклатуры.УслугаРазмещения;
		КонецЕсли;
		СтрокаТабличнойЧасти.Вставить("Содержание", Содержание);
		СтрокаТабличнойЧасти.ЭтоУслуга = РеквизитыНоменклатуры.Услуга;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыОбъекта.ТипЦен) Тогда
		ПараметрыОбъекта.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам);
	КонецЕсли;
	
	Если ЭтоОсновноеСредство Тогда
		СведенияОНоменклатуре = УчетОСПереопределяемый.СведенияОбОсновномСредстве(СтрокаТабличнойЧасти.Номенклатура);
	Иначе
		СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		СтрокаТабличнойЧасти.Номенклатура,
		ПараметрыОбъекта,
		Ложь);
	КонецЕсли;
	
	Если СведенияОНоменклатуре <> Неопределено Тогда
		
		Если СведенияОНоменклатуре.Цена <> 0 Тогда
			СтрокаТабличнойЧасти.Цена  = СведенияОНоменклатуре.Цена;
		КонецЕсли;
		
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, 1);
		
		СтрокаТабличнойЧасти.СуммаСкидки = СтрокаТабличнойЧасти.Сумма * СтрокаТабличнойЧасти.ПроцентСкидки / 100;
		
		Если ПараметрыОбъекта.ПокупательНалоговыйАгентПоНДС = Истина
			И ПараметрыОбъекта.ВедетсяУчетНДСПоФЗ335 Тогда
			
			СтрокаТабличнойЧасти.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(ПараметрыОбъекта.Дата, ПараметрыОбъекта.Организация);
			СтрокаТабличнойЧасти.СуммаНДС = 0;
			
		ИначеЕсли НЕ ЭтоОсновноеСредство Тогда
			
			СтрокаТабличнойЧасти.СтавкаНДС = СведенияОНоменклатуре.СтавкаНДС;
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ПараметрыОбъекта.СуммаВключаетНДС);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВозвратнаяТараНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ПараметрыОбъекта)

	Если Не ЗначениеЗаполнено(ПараметрыОбъекта.ТипЦен) Тогда
		ПараметрыОбъекта.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам);
	КонецЕсли;
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		СтрокаТабличнойЧасти.Номенклатура,
		ПараметрыОбъекта,
		Ложь);
	
	Если СведенияОНоменклатуре <> Неопределено Тогда
		СтрокаТабличнойЧасти.Цена = СведенияОНоменклатуре.Цена;
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, 1);
	КонецЕсли;
	
КонецПроцедуры

// Внешний вид, содержание надписей и т.п.
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	Объект = Форма.Объект;
	
	// Скидка по документу считается в итогах 
	Если Форма.ВидСкидки = ВидыСкидок().ПоДокументуВЦелом И Объект.Товары.Итог("Всего") > 0 Тогда
		ДанныеОбъекта = Новый Структура("СуммаСкидки, СуммаВключаетНДС");
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
		
		Итоги = ИтогиСУчетомСкидки(Объект.Товары, ДанныеОбъекта);
		
		Форма.ИтогиВсего    = Итоги.ИтогиВсего;
		Форма.ИтогиСкидка   = Итоги.ИтогиСкидка;
		Форма.ИтогиВсегоНДС = Итоги.ИтогиВсегоНДС;
	Иначе
		Форма.ИтогиВсего    = Объект.Товары.Итог("Всего");
		Форма.ИтогиСкидка   = Объект.Товары.Итог("СуммаСкидки");
		Форма.ИтогиВсегоНДС = Объект.Товары.Итог("СуммаНДС");
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьНадписьЦеныИВалюта(Форма)
	
	Объект = Форма.Объект;
	СтруктураНадписи = Новый Структура(
		"ВалютаДокумента, Курс, Кратность, СуммаВключаетНДС, ДокументБезНДС, ВалютаРегламентированногоУчета",
		Объект.ВалютаДокумента,
		Объект.КурсВзаиморасчетов,
		Объект.КратностьВзаиморасчетов,
		Объект.СуммаВключаетНДС,
		Объект.ДокументБезНДС,
		Форма.ВалютаРегламентированногоУчета);
	Если Форма.ИспользоватьТипыЦенНоменклатуры Тогда
		СтруктураНадписи.Вставить("ТипЦен", Объект.ТипЦен);
	КонецЕсли;
	Форма.ЦеныИВалюта = ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи);

КонецПроцедуры

// Прочий функционал:

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьМассивВидовДоговоров()

	СписокВидовДоговоров = Новый Массив;
	СписокВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
	СписокВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"));
	СписокВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентом"));
	СписокВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку"));

	Возврат СписокВидовДоговоров;

КонецФункции

&НаСервере
Функция ЕстьСтрокиВТабличныхЧастях()
	
	ЕстьСтроки = Объект.Товары.Количество() > 0 
		ИЛИ Объект.ВозвратнаяТара.Количество() > 0;
	
	Возврат ЕстьСтроки;

КонецФункции

&НаКлиенте
Функция ПолучитьПараметрыПодбора(ИмяТаблицы)
	
	ПараметрыФормы = Новый Структура;
	
	ДатаРасчетов 	 = ?(НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДата()), Неопределено, Объект.Дата);
	ЗаголовокПодбора = НСтр("ru = 'Подбор номенклатуры в %1 (%2)'");
	
	Валюта = Объект.ВалютаДокумента;
	
	Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		Параметрыформы.Вставить("ПоказыватьЦены", Истина);
	КонецЕсли;
	
	Если ИмяТаблицы = "Товары" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Товары'");
		ПараметрыФормы.Вставить("ПоказыватьОстатки"    , Истина);
		ПараметрыФормы.Вставить("КомандаВыбратьОстаток", Истина);
		
	ИначеЕсли ИмяТаблицы = "ВозвратнаяТара" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Возвратная тара'");
		
		Отказ = Ложь;
		ОчиститьСообщения();
		Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
			Валюта = ВалютаВзаиморасчетов;
			Если НЕ ЗначениеЗаполнено(Валюта) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Валюта расчетов'"));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ДоговорКонтрагента", "Объект", Отказ);
			КонецЕсли;
		Иначе
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Договор'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ДоговорКонтрагента", "Объект", Отказ);
		КонецЕсли;
		Если Отказ Тогда
			Возврат Неопределено;
		КонецЕсли;
		ПараметрыФормы.Вставить("ПоказыватьОстатки",     Истина);
		ПараметрыФормы.Вставить("КомандаВыбратьОстаток", Истина);
		
	КонецЕсли;
	
	ЗаголовокПодбора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокПодбора, Объект.Ссылка, ПредставлениеТаблицы);
	
	ПараметрыФормы.Вставить("ЕстьЦена"          , Истина);
	ПараметрыФормы.Вставить("ЕстьКоличество"    , Истина);
	ПараметрыФормы.Вставить("ДатаРасчетов"      , ДатаРасчетов);
	ПараметрыФормы.Вставить("ТипЦен"            , Объект.ТипЦен);
	ПараметрыФормы.Вставить("Валюта"            , Валюта);
	ПараметрыФормы.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	ПараметрыФормы.Вставить("Контрагент"        , Объект.Контрагент);
	ПараметрыФормы.Вставить("Организация"       , Объект.Организация);
	ПараметрыФормы.Вставить("Подразделение"     , Объект.ПодразделениеОрганизации);
	ПараметрыФормы.Вставить("Склад"             , Объект.Склад);
	ПараметрыФормы.Вставить("Заголовок"         , ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВидПодбора"        , ПолучитьВидПодбора(ИмяТаблицы));
	ПараметрыФормы.Вставить("ИмяТаблицы"        , ИмяТаблицы);
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		ПараметрыФормы.Вставить("Реализация", Истина);
		ПараметрыФормы.Вставить("ЗаполнятьЦеныПоПродаже", Истина);
		Если ИмяТаблицы = "ВозвратнаяТара" Тогда
			ПараметрыФормы.Вставить("СуммаВключаетНДС"		, Истина);
		Иначе
			ПараметрыФормы.Вставить("СуммаВключаетНДС"		, Объект.СуммаВключаетНДС);
		КонецЕсли;
	КонецЕсли;


	Возврат ПараметрыФормы;

КонецФункции

&НаКлиенте
Функция ПолучитьВидПодбора(ИмяТаблицы)

	ВидПодбора = "";

	Возврат ВидПодбора;

КонецФункции

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()

	Если ЭтоТоварыИУслуги Тогда
		ДанныеОбъекта = Новый Структура("Дата, Организация, ДокументБезНДС, ЕдиницаИзмерения");
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
		СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
			ОбщегоНазначения.ВыгрузитьКолонку(Объект.Товары, "Номенклатура", Истина),
			ДанныеОбъекта,
			Ложь,  // Не требуется определять счета учета
			Ложь); // Не требуется определять цены
	Иначе
		СоответствиеСведенийОбОсновномСредстве = УчетОСПереопределяемый.СведенияОСпискеОсновныхСредств(
			ОбщегоНазначения.ВыгрузитьКолонку(Объект.Товары, "Номенклатура", Истина));
	КонецЕсли;
		
	// Заполнение колонок "Всего" в табличных частях
	Для каждого СтрокаТаблицы Из Объект.Товары Цикл
		
		СтрокаТаблицы.Всего                  = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС) - СтрокаТаблицы.СуммаСкидки;
		СтрокаТаблицы.СчетНаОплатуПокупателю = Объект.Ссылка;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
			СтрокаТаблицы.ЭтоУслуга = Истина;
		ИначеЕсли ЭтоТоварыИУслуги Тогда
			СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТаблицы.Номенклатура);
			Если СведенияОНоменклатуре <> Неопределено Тогда
				СтрокаТаблицы.ЭтоУслуга        = СведенияОНоменклатуре.Услуга;
				СтрокаТаблицы.ЕдиницаИзмерения = СведенияОНоменклатуре.ЕдиницаИзмерения;
				СтрокаТаблицы.Код              = СведенияОНоменклатуре.Код;
				СтрокаТаблицы.Артикул          = СведенияОНоменклатуре.Артикул;
				СтрокаТаблицы.УслугаРазмещения = СведенияОНоменклатуре.УслугаРазмещения;
			КонецЕсли;
			
			СтрокаТаблицы.НоменклатураИнтерактивная = СтрокаТаблицы.Номенклатура;
		Иначе
			СведенияОбОсновномСредстве     = СоответствиеСведенийОбОсновномСредстве.Получить(СтрокаТаблицы.Номенклатура);
			СтрокаТаблицы.ЕдиницаИзмерения = СведенияОбОсновномСредстве.ЕдиницаУчета;
			СтрокаТаблицы.Код              = СведенияОбОсновномСредстве.Код;
			СтрокаТаблицы.ЭтоУслуга        = Ложь;
			
			СтрокаТаблицы.ОсновноеСредствоИнтерактивное = СтрокаТаблицы.Номенклатура;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТоварыВоВременноеХранилищеНаСервере()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Функция ПолучитьПараметрыОбработкиТабличнойЧастиТовары()

	ПараметрыОбработки = Новый Структура;
	
	ПараметрыОбработки.Вставить("АдресХранилищаТовары", 		ПоместитьТоварыВоВременноеХранилищеНаСервере());
	ПараметрыОбработки.Вставить("ЗаполнятьЦеныПоПокупке", 		Ложь);
	
	ПараметрыОбработки.Вставить("ДокументСсылка", 				Объект.Ссылка);
	ПараметрыОбработки.Вставить("ДокументДата", 				Объект.Дата);
	ПараметрыОбработки.Вставить("ДокументОрганизация", 			Объект.Организация);
	ПараметрыОбработки.Вставить("ДокументВалюта", 				Объект.ВалютаДокумента);
	ПараметрыОбработки.Вставить("ДокументКурс", 				Объект.КурсВзаиморасчетов);
	ПараметрыОбработки.Вставить("ДокументКратность", 			Объект.КратностьВзаиморасчетов); 
	ПараметрыОбработки.Вставить("ДокументБезНДС",               Объект.ДокументБезНДС);
	ПараметрыОбработки.Вставить("ДокументСуммаВключаетНДС", 	Объект.СуммаВключаетНДС);
	ПараметрыОбработки.Вставить("ДокументТипЦен", 				Объект.ТипЦен);
	ПараметрыОбработки.Вставить("ДокументСклад", 				Объект.Склад);
	
	ИменаТаблицИсточников = Новый СписокЗначений();
	ИменаТаблицИсточников.Добавить("Товары");
	ИменаТаблицИсточников.Добавить("Услуги");

	ПараметрыОбработки.Вставить("ИмяТаблицы", 					"Товары");
	ПараметрыОбработки.Вставить("РазрешитьУслуги", 				Истина);
	ПараметрыОбработки.Вставить("ПоказыватьСкидку",				ВидСкидки = ВидыСкидок().НаОтдельныеПозиции);
	ПараметрыОбработки.Вставить("ИменаТаблицИсточников",		ИменаТаблицИсточников);

	Возврат ПараметрыОбработки;
	
КонецФункции 

&НаСервере
Процедура ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметры)
	
	ТаблицаОбработки = ПолучитьИзВременногоХранилища(Параметры.АдресОбработаннойТабличнойЧастиТоварыВХранилище);
	Объект.Товары.Загрузить(ТаблицаОбработки);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьНовыйДоговор(НомерДоговора, ДатаДоговора, Контрагент, Организация)
	
	НаименованиеДоговора = "Счет %1 от %2";
	НаименованиеДоговора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеДоговора, 
		НомерДоговора, 
		Формат(ДатаДоговора, "ДЛФ=D"));
		
	ОрганизацияДоговора = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Организация);
	ВалютаВзаиморасчетов = Константы.ВалютаРегламентированногоУчета.Получить();
	ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", НаименованиеДоговора);
	Запрос.УстановитьПараметр("Организация", ОрганизацияДоговора);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", ВалютаВзаиморасчетов);
	Запрос.УстановитьПараметр("ВидДоговора", ВидДоговора);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Наименование = &Наименование
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.Владелец = &Контрагент
	|	И ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаВзаиморасчетов
	|	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	СуществующийДоговор = Выборка.Ссылка;

	Если ЗначениеЗаполнено(СуществующийДоговор) Тогда
		
		Возврат СуществующийДоговор;
		
	Иначе
	
		НовыйДоговор = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		НовыйДоговор.Наименование 	= НаименованиеДоговора;
		НовыйДоговор.Номер 			= НомерДоговора;
		НовыйДоговор.Дата 			= ДатаДоговора;
		НовыйДоговор.Организация 	= ОрганизацияДоговора;
		НовыйДоговор.Владелец 		= Контрагент;
		НовыйДоговор.ВидДоговора 	= ВидДоговора;
		НовыйДоговор.ВалютаВзаиморасчетов = ВалютаВзаиморасчетов;
		НовыйДоговор.СпособВыставленияДокументов = Перечисления.СпособыВыставленияДокументов.Автоматически;
		НовыйДоговор.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПриРеализации(ДатаДоговора, Организация);
		
		Справочники.ДоговорыКонтрагентов.ЗаполнитьРуководителяОрганизации(НовыйДоговор);
		Справочники.ДоговорыКонтрагентов.ЗаполнитьРуководителяКонтрагента(НовыйДоговор);
		
		НовыйДоговор.Записать();
		
		Возврат НовыйДоговор.Ссылка;
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбработатьОповещениеОбИзмененииНастроекПараметровУчета()
	
	СрокОплатыВРабочихДняхНовоеЗначение = Константы.СрокОплатыСчетовПокупателю.Получить();
	
	ИзменилсяСрокОплатыВРабочихДнях = (СрокОплатыВРабочихДнях <> СрокОплатыВРабочихДняхНовоеЗначение);
	
	Если ИзменилсяСрокОплатыВРабочихДнях Тогда
		СрокОплатыВРабочихДнях = СрокОплатыВРабочихДняхНовоеЗначение;
		УстановитьПодсказкуСрокаОплаты(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюОбОплатеСчета()
	
	Если ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Ссылка)
		И СтатусДокумента = Перечисления.СтатусОплатыСчета.ОплаченЧастично Тогда
		
		ОплаченнаяСуммаСчета = Документы.СчетНаОплатуПокупателю.ОплаченнаяСуммаСчета(Объект.Организация, Объект.Ссылка);
		ОжидаетсяОплата = Макс(0, Объект.СуммаДокумента - ОплаченнаяСуммаСчета);
		
		ШаблонСтроки = НСтр("ru = '%1 %2'");
		Если ОплаченнаяСуммаСчета <> 0 Тогда
			ТекстОплачено = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтроки,
				Формат(ОплаченнаяСуммаСчета, "ЧЦ=15; ЧДЦ=2; ЧН=0"),
				Объект.ВалютаДокумента);
		КонецЕсли;
		
		Если ОжидаетсяОплата <> 0 Тогда
			ТекстОжидаетсяОплата = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСтроки,
				Формат(ОжидаетсяОплата, "ЧЦ=15; ЧДЦ=2; ЧН=0"),
				Объект.ВалютаДокумента);
		КонецЕсли;
		
	Иначе
		ТекстОплачено = "";
		ТекстОжидаетсяОплата = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОткрытиеФормыВыбораДокументаДляЗаполнения(
	ТабличнаяЧасть, СпособЗаполнения, ИмяДокумента) Экспорт
	
	Если СпособЗаполнения = ИмяСпособаЗаполненияЗаполнить()
		И Объект[ТабличнаяЧасть].Количество() > 0 Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);
		ДополнительныеПараметры.Вставить("ИмяДокумента", ИмяДокумента);
		
		Оповещение = Новый ОписаниеОповещения(
			"ВопросПередЗаполнениемТабличнойЧастиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ТекстВопроса = НСтр("ru = 'Перед заполнением табличная часть будет очищена. Заполнить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Иначе
		ОткрытьФормуВыбораДокументаДляЗаполнения(ТабличнаяЧасть, СпособЗаполнения, ИмяДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗаполнениемТабличнойЧастиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Объект[ДополнительныеПараметры.ТабличнаяЧасть].Очистить();
		ОткрытьФормуВыбораДокументаДляЗаполнения(
			ДополнительныеПараметры.ТабличнаяЧасть,
			ИмяСпособаЗаполненияЗаполнить(),
			ДополнительныеПараметры.ИмяДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораДокументаДляЗаполнения(ТабличнаяЧасть, СпособЗаполнения, ИмяДокумента)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Организация", Объект.Организация);
	СтруктураОтбора.Вставить("ПометкаУдаления", Ложь);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Отбор", СтруктураОтбора);
	СтруктураПараметров.Вставить("РежимВыбора", Истина);
	СтруктураПараметров.Вставить("МножественныйВыбор", Ложь);
	СтруктураПараметров.Вставить("ИмяТаблицы", ТабличнаяЧасть);
	
	ОткрытьФорму("Документ." + ИмяДокумента + ".ФормаВыбора", СтруктураПараметров, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ИмяСпособаЗаполненияДобавить()
	
	Возврат "Добавить";
	
КонецФункции

&НаКлиенте
Функция ИмяСпособаЗаполненияЗаполнить()
	
	Возврат "Заполнить";
	
КонецФункции

&НаКлиенте
Процедура УстановитьЗаголовокФормы()
	Если НЕ ЭтоТоварыИУслуги Тогда
		ЭтотОбъект.Автозаголовок = Ложь;
		ТекстЗаголовка = НСтр("ru = 'Счет на оплату покупателю основных средств'");
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ЭтотОбъект.Заголовок = ТекстЗаголовка + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru=' %1 от %2'"), Объект.Номер, Объект.Дата);
		Иначе
			ЭтотОбъект.Заголовок = ТекстЗаголовка + НСтр("ru = ' (создание)'");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПроверитьКорректностьСчетаСервер()
	
	РезультатПроверкиСчета = БанковскиеСчетаСервер.ПроверитьКорректностьБанковскогоСчета(
		Объект.СтруктурнаяЕдиница, Объект.Дата);
	Если РезультатПроверкиСчета.ЕстьОшибки Тогда
		ПризнакСчета = БанковскиеСчетаСервер.НовыйПризнакСчета();
		ПризнакСчета.СчетОрганизации = Истина;
		ТекстОшибки = БанковскиеСчетаСервер.ТекстОшибкиБанковскогоСчета(РезультатПроверкиСчета, ПризнакСчета);
		Элементы.ГруппаСтруктурнаяЕдиницаПроверка.ЦветФона = ЦветаСтиля.ЦветОшибкиБанковскогоСчета;
		Элементы.СтруктурнаяЕдиница.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ТекстОшибки);
		Элементы.СтруктурнаяЕдиница.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
		Если Не ИспользоватьНесколькоБанковскихСчетовОрганизации Тогда
			ТекстПредупрежденияСчетОрганизации = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '%1. Добавьте новый <a href=''СписокСчетовСсылка''>банковский счет</a>'"), ТекстОшибки);
		Иначе
			ТекстПредупрежденияСчетОрганизации = "";
		КонецЕсли;
	Иначе
		Элементы.СтруктурнаяЕдиница.РасширеннаяПодсказка.Заголовок = "";
		Элементы.СтруктурнаяЕдиница.ОтображениеПодсказки = ОтображениеПодсказки.Авто;
		ТекстПредупрежденияСчетОрганизации = "";
		Элементы.ГруппаСтруктурнаяЕдиницаПроверка.ЦветФона = ОбщегоНазначенияВызовСервера.ЦветСтиля("ЦветФонаФормы");
	КонецЕсли;
	
	Элементы.ГруппаПредупреждениеОНекорректномРасчетномСчете.Видимость =
		ЗначениеЗаполнено(ТекстПредупрежденияСчетОрганизации);
	Если Элементы.ГруппаПредупреждениеОНекорректномРасчетномСчете.Видимость Тогда
		Элементы.ПредупреждениеОЗаполненииРеквизитовСчета.Видимость = Ложь;
	Иначе
		Элементы.ПредупреждениеОЗаполненииРеквизитовСчета.Видимость =
			ПроверкаРеквизитовОрганизацииКлиентСервер.ПоказатьПредупреждениеОРеквизитахСчета(
				ЭтотОбъект, Объект.ОрганизацияПолучатель);
			КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Функция ОбрабатыватьОповещениеНаСервере(ИмяСобытия) Экспорт
	
	Возврат ИмяСобытия = "Запись_НастройкиСистемыНалогообложения"
		Или ИмяСобытия = "ИзменениеПатента";
	
КонецФункции

&НаСервере
Процедура ОбработкаОповещенияНаСервере()
	
	НастройкиНалоговИОтчетовПредупреждениеФормы.ОтобразитьПредупреждение(ЭтотОбъект, Объект.Организация, Объект.Дата, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьЗагрузкуИзФайла(АдресЗагруженныхДанных, НеиспользуемыйПараметр) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено
		Или НЕ ЭтоАдресВременногоХранилища(АдресЗагруженныхДанных) Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТабличнуюЧастьИзФайла(АдресЗагруженныхДанных);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьИзФайла(АдресЗагруженныхДанных)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	Если ТипЗнч(ЗагруженныеДанные) <> Тип("ТаблицаЗначений")
		Или ЗагруженныеДанные.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	НоменклатураБезЦены = ЗагруженныеДанные.Скопировать(Новый Структура("Цена", 0), "Номенклатура");
	Если НоменклатураБезЦены.Количество() > 0 Тогда 
		
		СписокНоменклатуры = НоменклатураБезЦены.ВыгрузитьКолонку("Номенклатура");
		Если ИспользоватьТипыЦенНоменклатуры И ЗначениеЗаполнено(Объект.ТипЦен) Тогда
			ЦеныНоменклатуры = Ценообразование.ПолучитьТаблицуЦенНоменклатуры(СписокНоменклатуры, Объект.ТипЦен, Объект.Дата);
		Иначе
			ЦеныНоменклатуры = Ценообразование.ПолучитьТаблицуЦенНоменклатурыДокументов(
				СписокНоменклатуры, Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам, Объект.Дата);
		КонецЕсли;
		
	КонецЕсли;
	
	НоменклатураИзФайла = ОбщегоНазначения.ВыгрузитьКолонку(ЗагруженныеДанные, "Номенклатура", Истина);
	СведенияОбУслуге = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(НоменклатураИзФайла, "Услуга");
	
	ЗагруженныеДанные.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Для Каждого ЗагруженнаяСтрока Из ЗагруженныеДанные Цикл 
		
		Если ЗначениеЗаполнено(ЗагруженнаяСтрока.Номенклатура) Тогда
			ЭтоУслуга = СведенияОбУслуге[ЗагруженнаяСтрока.Номенклатура] = Истина;
			Если ЗначениеЗаполнено(ЗагруженнаяСтрока.Содержание) И Не ЭтоУслуга Тогда
				// Содержание может быть только по услуге
				ЗагруженнаяСтрока.Содержание = "";
			КонецЕсли;
		КонецЕсли;
		
		Если ЗагруженнаяСтрока.Цена = 0 Тогда 
			СведенияОЦенеНоменклатуры = ЦеныНоменклатуры.Найти(ЗагруженнаяСтрока.Номенклатура, "Номенклатура");
			Если СведенияОЦенеНоменклатуры <> Неопределено Тогда
				ЗагруженнаяСтрока.Цена = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
					СведенияОЦенеНоменклатуры.Цена,
					СведенияОЦенеНоменклатуры.Валюта, Объект.ВалютаДокумента,
					СведенияОЦенеНоменклатуры.Курс, Объект.КурсВзаиморасчетов,
					СведенияОЦенеНоменклатуры.Кратность, Объект.КратностьВзаиморасчетов);
				
				Если ЗначениеЗаполнено(Объект.ТипЦен) И (СведенияОЦенеНоменклатуры.Валюта <> Объект.ВалютаДокумента) Тогда
					ЗагруженнаяСтрока.Цена = Ценообразование.ОкруглитьЦену(ЗагруженнаяСтрока.Цена, Объект.ТипЦен);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(ЗагруженнаяСтрока);
		
	КонецЦикла;
	
	СтруктураЗагруженныхДанных = Новый Структура();
	СтруктураЗагруженныхДанных.Вставить("АдресПодобраннойНоменклатурыВХранилище", 
		ПоместитьВоВременноеХранилище(ЗагруженныеДанные, УникальныйИдентификатор));
	
	ОбработкаВыбораПодборВставкаИзБуфераНаСервере(СтруктураЗагруженныхДанных, "Товары");
	
КонецПроцедуры

#Область СтатусыДокументов

&НаСервереБезКонтекста
Функция ПолучитьСтатусДокумента(Знач Организация, Знач Ссылка)
	
	Возврат РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокумента(Ссылка, Организация).Статус;
	
КонецФункции

&НаСервере
Процедура УстановитьСтатусДокумента(Ссылка)
	
	СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
	СтатусыДокумента.Статус = СтатусДокумента;
	
	РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(Ссылка, СтатусыДокумента);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьОповещениеОбИзмененииСтатусДокумента()
	
	СтатусДокумента = ПолучитьСтатусДокумента(Объект.Организация, Объект.Ссылка);
	ЗаполнитьИнформациюОбОплатеСчета();
	
КонецПроцедуры

#КонецОбласти

#Область СрокиОплатыДокументов

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПодсказкуСрокаОплаты(Форма)
	
	Если Форма.Элементы.СрокОплаты.ОтображениеПодсказки = ОтображениеПодсказки.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Подстроки = Новый Массив;
	
	// Текст до гиперссылки
	Если Форма.СрокОплатыВРабочихДнях = 0 Тогда
		
		Подстроки.Добавить(НСтр("ru='Срок оплаты – 0 рабочих дней (не заполнять).'"));
		
	Иначе
		
		ПараметрыПредметаИсчисления = НСтр("ru='рабочий день, рабочих дня, рабочих дней'");
		
		ТекстКоличествоДнейОтсрочки = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(
			Форма.СрокОплатыВРабочихДнях,
			ПараметрыПредметаИсчисления);
		
		Подстроки.Добавить(СтрШаблон(НСтр("ru = 'Срок оплаты – %1.'"), ТекстКоличествоДнейОтсрочки));
		
	КонецЕсли;
	
	Подстроки.Добавить(Символы.ПС + НСтр("ru = 'Изменить значение по умолчанию можно в меню Продажи – '"));
	
	// Добавление гиперссылки в подсказку.
	// Используется "ПустаяСсылка", потому что не используется стандартная обработка навигационной ссылки.
	Подстроки.Добавить(Новый ФорматированнаяСтрока("Сроки оплаты покупателями",,,,"ПустаяСсылка"));
	
	// Текст после гиперссылки
	Подстроки.Добавить("." + Символы.ПС
		+ НСтр("ru = 'Если срок оплаты не заполнен, он не выводится в печатную форму.'"));
	
	Форма.Элементы.СрокОплатыРасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(Подстроки);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаКонтрагентов

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания) Экспорт	
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтотОбъект, ПараметрыФоновогоЗадания);
КонецПроцедуры

&НаСервере
Процедура ПодразделениеОрганизацииПриИзмененииНаСервере()
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КоличествоСтрок = Элементы[ИмяТаблицы].ВыделенныеСтроки.Количество();
	Если КоличествоСтрок = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	СкопироватьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОКопированииСтрокВБуферОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	КоличествоСтрок = ВставитьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОВставкеСтрокИзБуфераОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИмяТаблицы)

	ЭтоВставкаИзБуфера = ВыбранноеЗначение.Свойство("ЭтоВставкаИзБуфера");
	СписокСвойств = Неопределено;
	Если ЭтоВставкаИзБуфера Тогда
		
		ТаблицаТоваров = ВыбранноеЗначение.Данные;
		СписокСвойств = ВыбранноеЗначение.СписокСвойств;
		
	Иначе
		
		ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
		
	КонецЕсли;
	
	Если ТаблицаТоваров.Колонки.Найти("СтавкаНДС") = Неопределено Тогда
		ТаблицаТоваров.Колонки.Добавить("СтавкаНДС");
	КонецЕсли;
	
	Если ТаблицаТоваров.Колонки.Найти("Цена") = Неопределено Тогда
		ТаблицаТоваров.Колонки.Добавить("Цена", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	КонецЕсли;
	
	Если ТаблицаТоваров.Колонки.Найти("РасходыНаЕдиницу") = Неопределено Тогда
		ТаблицаТоваров.Колонки.Добавить("РасходыНаЕдиницу", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	КонецЕсли;
	
	Если ИмяТаблицы = "Товары" И ТаблицаТоваров.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаТоваров.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	КонецЕсли;
	
	Если ТаблицаТоваров.Колонки.Найти("УслугаРазмещения") = Неопределено Тогда
		ТаблицаТоваров.Колонки.Добавить("УслугаРазмещения", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУслугРазмещения"));
	КонецЕсли;

	СписокНоменклатуры	= ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина);
	ДанныеОбъекта = Новый Структура("Организация, Дата, Ссылка, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов, Склад, ДоговорКонтрагента, ДокументБезНДС");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	МетаданныеДокумента = ДанныеОбъекта.Ссылка.Метаданные();
	
	Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		ЦенаВключаетНДС	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТипЦен, "ЦенаВключаетНДС");
	Иначе
		ЦенаВключаетНДС	= Объект.СуммаВключаетНДС;
	КонецЕсли;

	КоличествоДобавленныхСтрок = 0;
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТовара.Номенклатура) Тогда
			Цена = СтрокаТовара.Цена;
		Иначе
			ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТовара, ДанныеОбъекта, "Товары", МетаданныеДокумента);
			
			Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
				СтрокаТовара.Цена, ЦенаВключаетНДС, Объект.СуммаВключаетНДС,
				УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТовара.СтавкаНДС));
		КонецЕсли;
			
		СтрокаТабличнойЧасти = Неопределено;
		Если Не ЭтоВставкаИзБуфера Тогда
		
			Если ИмяТаблицы = "Товары"
				И ЗначениеЗаполнено(СтрокаТовара.Содержание) Тогда

				СтруктураОтбора = Новый Структура("Номенклатура, Содержание, Цена",
					СтрокаТовара.Номенклатура, СтрокаТовара.Содержание, Окр(Цена, 2, 1));
			Иначе
				СтруктураОтбора = Новый Структура("Номенклатура, Цена", СтрокаТовара.Номенклатура, Окр(Цена, 2, 1));
			КонецЕсли;
			СтрокаТабличнойЧасти = Неопределено;

			МассивНайденныхСтрок = Объект[ИмяТаблицы].НайтиСтроки(СтруктураОтбора);
			Если МассивНайденныхСтрок.Количество() > 0 Тогда
				СтрокаТабличнойЧасти = МассивНайденныхСтрок[0];
			КонецЕсли;
			
		КонецЕсли;
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			// Нашли - увеличиваем количество.
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + СтрокаТовара.Количество;
			
			Если ИмяТаблицы = "Товары" Тогда
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
				
				Если ПокупательНалоговыйАгентПоНДС = Истина
					И ВедетсяУчетНДСПоФЗ335 Тогда
					СтрокаТабличнойЧасти.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(ДанныеОбъекта.Дата, ДанныеОбъекта.Организация);
					СтрокаТабличнойЧасти.СуммаНДС = 0;
				Иначе
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
				КонецЕсли;
				
			ИначеЕсли ИмяТаблицы = "ВозвратнаяТара" Тогда
				// Рассчитываем реквизиты табличной части
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
			КонецЕсли;
			
		Иначе
			
			НоваяСтрока = Объект[ИмяТаблицы].Добавить();
			КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок + 1;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара, СписокСвойств);
			
			Если Не ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ИмяТаблицы = "Товары" Тогда
				
				// Заполняем реквизиты табличной части.
				Если ЭтоТоварыИУслуги Тогда
					РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НоваяСтрока.Номенклатура,
					"Наименование, НаименованиеПолное, Услуга, ПериодичностьУслуги, УслугаРазмещения");
					
					Если Не ЗначениеЗаполнено(НоваяСтрока.Содержание) Тогда
						
						Если НЕ РеквизитыНоменклатуры.Услуга Тогда
							Содержание = "";
						Иначе
							Если ПустаяСтрока(РеквизитыНоменклатуры.НаименованиеПолное) Тогда
								Содержание = РеквизитыНоменклатуры.Наименование;
							Иначе
								Содержание = РеквизитыНоменклатуры.НаименованиеПолное;
							КонецЕсли;
							
							Содержание = РаботаСНоменклатуройКлиентСерверБП.СодержаниеУслуги(
							Содержание,
							РеквизитыНоменклатуры.ПериодичностьУслуги,
							ДанныеОбъекта.Дата);
						КонецЕсли;
						
						НоваяСтрока.Содержание = Содержание;
						
					КонецЕсли;
					НоваяСтрока.ЭтоУслуга = РеквизитыНоменклатуры.Услуга;
					НоваяСтрока.УслугаРазмещения = РеквизитыНоменклатуры.УслугаРазмещения;

					ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(НоваяСтрока, ДанныеОбъекта, "Товары", МетаданныеДокумента);
					
					Если ПлательщикТуристическогоНалога Тогда
						УстановитьПараметрыТуристическогоНалога(НоваяСтрока, ЗначениеЛьготыПоУмолчанию, ДопУслугаРазмещения);
					КонецЕсли;

				Иначе
					НоваяСтрока.ЭтоУслуга = Ложь;
					НоваяСтрока.СтавкаНДС = СтавкаНДСПриРеализации;
				КонецЕсли;
				
				ЗначениеПустогоКоличества = ?(ЭтоВставкаИзБуфера И НоваяСтрока.ЭтоУслуга, 1, 0);
				
				// Рассчитываем реквизиты табличной части.
				Если ЦенаВключаетНДС <> Объект.СуммаВключаетНДС ИЛИ ЭтоВставкаИзБуфера Тогда
					НоваяСтрока.Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
							НоваяСтрока.Цена, ЦенаВключаетНДС, Объект.СуммаВключаетНДС,
							УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
							
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(НоваяСтрока, ЗначениеПустогоКоличества);
				КонецЕсли;
				
				Если ПокупательНалоговыйАгентПоНДС = Истина
					И ВедетсяУчетНДСПоФЗ335 Тогда
					НоваяСтрока.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСИсчисляетсяНалоговымАгентом(ДанныеОбъекта.Дата, ДанныеОбъекта.Организация);
					НоваяСтрока.СуммаНДС = 0;
				Иначе
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(НоваяСтрока, Объект.СуммаВключаетНДС);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаТовара.РасходыНаЕдиницу)
					И СтрокаТовара.РасходыНаЕдиницу <> ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначения() Тогда
					НоваяСтрока.КлючКалькуляцииРасходов = Новый УникальныйИдентификатор;
					НоваяСтрока.РасходыНаЕдиницу = СтрокаТовара.РасходыНаЕдиницу;
					СкопироватьКалькуляциюРасходов(СтрокаТовара.КлючКалькуляцииРасходов, НоваяСтрока.КлючКалькуляцииРасходов);
				КонецЕсли;
				
			ИначеЕсли ИмяТаблицы = "ВозвратнаяТара" Тогда
				// Рассчитать реквизиты табличной части.
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(НоваяСтрока);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	ЗаполнитьДобавленныеКолонкиТаблиц();

	ОбновитьИтоги(ЭтотОбъект);
	
	Если ЭтоВставкаИзБуфера Тогда
		
		ВыбранноеЗначение.КоличествоДобавленныхСтрок = КоличествоДобавленныхСтрок;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборОСНаСервере(ВыбранноеЗначение)
	
	ТаблицаОС = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресОСВХранилище);
	
	ИсходныеДанные = Объект.Товары.Выгрузить();
	ИсходныеДанные.Индексы.Добавить("Номенклатура");
	
	СоответствиеСведенийОбОсновномСредстве = УчетОСПереопределяемый.СведенияОСпискеОсновныхСредств(
			ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаОС, "ОсновноеСредство", Истина));
	
	Объект.Товары.Очистить();
	
	Для каждого СтрокаТаблицыОС Из ТаблицаОС Цикл
		ЭлементКоллекции = Объект.Товары.Добавить();
		
		СтрокаИсходныхДанных = ИсходныеДанные.Найти(СтрокаТаблицыОС.ОсновноеСредство, "Номенклатура");
		
		Если СтрокаИсходныхДанных <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЭлементКоллекции, СтрокаИсходныхДанных);
		Иначе
			СведенияОбОсновномСредстве        = СоответствиеСведенийОбОсновномСредстве.Получить(СтрокаТаблицыОС.ОсновноеСредство);
			
			ЭлементКоллекции.ЕдиницаИзмерения = СведенияОбОсновномСредстве.ЕдиницаУчета;
			ЭлементКоллекции.Код              = СведенияОбОсновномСредстве.Код;
			ЭлементКоллекции.СтавкаНДС        = СтавкаНДСПриРеализации;
			ЭлементКоллекции.Количество       = 1;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ЭлементКоллекции, СтрокаТаблицыОС);
		ЭлементКоллекции.Номенклатура = СтрокаТаблицыОС.ОсновноеСредство;
	КонецЦикла;
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область КопированиеВставкаСтрокЧерезБуферОбмена

&НаСервере
Процедура СкопироватьСтрокиНаСервере(ИмяТаблицы)
	
	ОбщегоНазначения.СкопироватьСтрокиВБуферОбмена(Объект[ИмяТаблицы], 
		Элементы[ИмяТаблицы].ВыделенныеСтроки, Объект.Ссылка.Метаданные().Имя);

КонецПроцедуры

&НаСервере
Функция ВставитьСтрокиНаСервере(ИмяТаблицы)
	
	ПараметрыВставки = ОбработкаТабличныхЧастей.ПолучитьПараметрыВставкиДанныхИзБуфераОбмена(Объект.Ссылка, ИмяТаблицы);
	ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки);
	ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ПараметрыВставки, ПараметрыВставки.ИмяТаблицы);
	
	Возврат ПараметрыВставки.КоличествоДобавленныхСтрок;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИмяТекущейТабличнойЧасти()
	
	Возврат "Товары";
	
КонецФункции

&НаСервере
Процедура ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки)

	СписокСвойств = Новый Массив;
	СписокСвойств.Добавить("Номенклатура");
	СписокСвойств.Добавить("Содержание");
	СписокСвойств.Добавить("Количество");
	СписокСвойств.Добавить("Цена");
	СписокСвойств.Добавить("ПроцентСкидки");
	
	ПараметрыВставки.СписокСвойств = ОбработкаТабличныхЧастей.ПолучитьСписокСвойствИмеющихсяВТаблицеДанных(
		ПараметрыВставки.Данные, СписокСвойств);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандыВставки(Форма, Доступность)

	Доступность = Не Форма.ТолькоПросмотр И Доступность;
	Элементы = Форма.Элементы;
	Элементы.ТоварыВставитьСтроки.Доступность					 = Доступность;
	Элементы.ТоварыКонтекстноеМенюВставитьСтроки.Доступность	 = Доступность;

КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область ИнтернетМагазин

&НаКлиенте
Процедура ПоказатьНесопоставленныеОбъектыНаКлиенте()
	ЗаблокироватьДанныеФормыДляРедактирования();
	
	Если ЗначениеЗаполнено(ДанныеДляЗаполнения.ДанныеСопоставленныеАвтоматически) Тогда
		ОбработатьОповещениеОЗаполненииДанных(
			ДанныеДляЗаполнения.ДанныеСопоставленныеАвтоматически, ДанныеДляЗаполнения.ВсеДанныеСопоставленыАвтоматически);
	КонецЕсли;
	
	Если ДанныеДляЗаполнения.ВсеДанныеСопоставленыАвтоматически Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаСопоставления = ?(ПоказатьЗаполнениеПоДаннымИнтернетМагазина, "ОбменСИнтернетМагазином", "ИнтеграцияBnovo");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДокументСсылка", Объект.Ссылка);
	ПараметрыФормы.Вставить("АдресХранилища", ДанныеДляЗаполнения.ХранилищеДанныхДляСопоставления);
	
	ОткрытьФорму(СтрШаблон("Обработка.%1.Форма.ФормаСопоставления", ОбработкаСопоставления), ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура НадписьПовторениеНажатие(Элемент, СтандартнаяОбработка)
	
	Если Объект.Организация.Пустая() Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Организация'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Организация", "Объект");
		
	ИначеЕсли Объект.Ссылка.Пустая()  Тогда
		
		ТекстВопроса = НСтр("ru = 'Расписание повторения доступно только после записи. Записать?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросРедактированиеПравилаЗавершение", ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да, ТекстВопроса);
		
	Иначе
		
		ОткрытьПравилоПовторения();
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИтогиСУчетомСкидки(Знач ТабличнаяЧасть, ДанныеОбъекта)
	ТаблицаТовары = ТабличнаяЧасть.Выгрузить();
	
	ОбработкаТабличныхЧастей.РаспределитьСкидкуПоСтрокамТабЧасти(ТаблицаТовары, ДанныеОбъекта);
	
	Результат = Новый Структура;
	Результат.Вставить("ИтогиВсего",    ТаблицаТовары.Итог("Сумма") + ?(ДанныеОбъекта.СуммаВключаетНДС, 0, ТаблицаТовары.Итог("СуммаНДС")));
	Результат.Вставить("ИтогиСкидка",   ДанныеОбъекта.СуммаСкидки);
	Результат.Вставить("ИтогиВсегоНДС", ТаблицаТовары.Итог("СуммаНДС"));
	
	Возврат Результат;
КонецФункции 

&НаСервере
Процедура ОбработатьОповещениеОЗаполненииДанных(Параметры, ВсеДанныеСопоставленыАвтоматически)
	Если Параметры.Свойство("Контрагент") 
		И ЗначениеЗаполнено(Параметры.Контрагент) Тогда
		
		Объект.Контрагент = Параметры.Контрагент;
		КонтрагентПриИзмененииНаСервере();
	КонецЕсли;
	
	Если Параметры.Свойство("Номенклатура") 
		И ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		Отбор = Новый Структура;
		Для Каждого СоответствиеНоменклатуры из Параметры.Номенклатура Цикл
			НомерСтрокиТЧ = СоответствиеНоменклатуры.Ключ + 1;
			Отбор.Вставить("НомерСтроки", НомерСтрокиТЧ);
			СписокСтрокТаблицыТовары = Объект.Товары.НайтиСтроки(Отбор);
				
			Если СписокСтрокТаблицыТовары.Количество() = 1 Тогда
				СписокСтрокТаблицыТовары[0].Номенклатура = СоответствиеНоменклатуры.Значение;
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьДобавленныеКолонкиТаблиц();
		
		//Очистим Содержание для товаров
		Для каждого СтрокаТаблицы Из Объект.Товары Цикл
			Если НЕ СтрокаТаблицы.ЭтоУслуга Тогда
				СтрокаТаблицы.Содержание = "";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВсеДанныеСопоставленыАвтоматически Тогда
		Записать();
		РазблокироватьДанныеФормыДляРедактирования();
	Иначе
		Модифицированность = Истина;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область РегулярныеСчета

&НаКлиенте
Процедура ОткрытьПравилоПовторения()
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПравила = Новый Структура;
	ПараметрыПравила.Вставить("Шаблон",      Объект.Ссылка);
	ПараметрыПравила.Вставить("ДатаШаблона", Объект.Дата);
	
	Если ЗначениеЗаполнено(ПравилоПовторения) Тогда
		ПараметрыПравила.Вставить("Ключ", ПравилоПовторения);
	Иначе
		
		ЗначенияЗаполнения = Новый Структура;
		
		ЗначенияЗаполнения.Вставить("Выполняется",   Истина);
		ЗначенияЗаполнения.Вставить("Периодичность", ПредопределенноеЗначение("Перечисление.Периодичность.Месяц"));
		
		ПараметрыПравила.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
	КонецЕсли;
	
	ОткрытьФорму("Справочник.ПравилаРегулярныхСчетовПокупателям.ФормаОбъекта", ПараметрыПравила, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПравилоПовторенияИНадпись()
	
	ПравилоПовторения = Справочники.ПравилаРегулярныхСчетовПокупателям.ПравилоПоСчету(Объект.Ссылка);
	
	НадписьПовторение = НадписьПовторение(ПравилоПовторения, Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НадписьПовторение(ПравилоПовторения, НовыйДокумент)
	
	Если ЗначениеЗаполнено(ПравилоПовторения)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПравилоПовторения, "Выполняется") Тогда
		
		ДатаСледующего = Справочники.ПравилаРегулярныхСчетовПокупателям.ДатаСледующего(ПравилоПовторения);
		
		Если НовыйДокумент Тогда
			ДатаСледующего = ИнтерфейсыВзаимодействияБРОКлиентСервер.ДобавитьПериод(ДатаСледующего,
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПравилоПовторения, "Периодичность"));
		КонецЕсли;
		
		НадписьПовторение = СтрШаблон(НСтр("ru = 'Следующий %1'"), Формат(ДатаСледующего, "ДЛФ=D"));
		
	Иначе
		НадписьПовторение = НСтр("ru = 'Повторять?'");
	КонецЕсли;
	
	Возврат НадписьПовторение;
	
КонецФункции

&НаКлиенте
Процедура ВопросРедактированиеПравилаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПараметрыЗаписи = Новый Структура("ОткрытьПравилоПовторения");
		
		Если Записать(ПараметрыЗаписи) Тогда
			
			ОткрытьПравилоПовторения();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаДляПереходаНаКартуНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, Объект.АдресДоставки);
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область КалькуляцияРасходов

&НаКлиенте
Процедура ПерезаполнитьКалькуляциюРасходов(РезультатЗакрытия, ПараметрыЗакрытияФормы) Экспорт
	
	Если РезультатЗакрытия = "ОК" Тогда
		
		ВыбраннаяСтрока = ПараметрыЗакрытияФормы.ВыбраннаяСтрока;
		
		ПерезаполнитьКалькуляциюРасходовНаСервере(ПараметрыЗакрытияФормы.АдресВременногоХранилища,
			ПараметрыЗакрытияФормы.КлючКалькуляцииРасходов,
			ВыбраннаяСтрока.НомерСтроки);
		
		ПересчитатьСуммуТовары("Товары");
		ОбновитьИтоги(ЭтотОбъект);
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьКалькуляциюРасходовНаСервере(АдресВременногоХранилища, КлючКалькуляцииРасходов, НомерСтрокиТовары)
	
	КалькуляцииРасходов.ПерезаполнитьКалькуляциюРасходов(ЭтотОбъект,
		АдресВременногоХранилища,
		КлючКалькуляцииРасходов,
		"Товары",
		НомерСтрокиТовары);
	
КонецПроцедуры

&НаСервере
Функция АдресВременногоХранилищаСРасчетамиКалькуляции(КлючКалькуляцииРасходов)
	
	Возврат КалькуляцииРасходовВызовСервера.АдресВременногоХранилищаСРасчетамиКалькуляции(
		ЭтотОбъект, КлючКалькуляцииРасходов);
	
КонецФункции

&НаСервере
Процедура УдалитьСтарыеДанныеКалькуляцииРасходов(КлючКалькуляцииРасходов)
	
	КалькуляцииРасходов.УдалитьСтарыеДанныеКалькуляцииРасходов(ЭтотОбъект, КлючКалькуляцииРасходов);
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьКалькуляциюРасходов(СтарыйКлючКалькуляцииРасходов, НовыйКлючКалькуляцииРасходов)
	
	КалькуляцииРасходов.СкопироватьКалькуляциюРасходов(ЭтотОбъект,
		СтарыйКлючКалькуляцииРасходов, НовыйКлючКалькуляцииРасходов);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНесвязанныеКлючиКалькуляцииРасходов()
	
	КалькуляцииРасходов.УдалитьНесвязанныеКлючиКалькуляцииРасходов(ЭтотОбъект, "Товары");
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура БанковскийСчетПриИзмененииНаСервере()

	ПроверитьКорректностьСчетаСервер();     
	
	Документы.СчетНаОплатуПокупателю.ЗаполнитьВариантПечатиQRКода(Объект);
	
	ЗаполнитьДополнительныеРеквизитыQRКода(Истина);

КонецПроцедуры 

#Область ДопреквизитыКодаДляОплаты

&НаСервере
Функция ИзменилисьКлючевыеРеквизиты()
	Если ТекущиеЗначенияРеквизитов = Неопределено Тогда
		ТекущиеЗначенияРеквизитов = Новый Структура("Контрагент, ДоговорКонтрагента, СтруктурнаяЕдиница, Дата");
	КонецЕсли; 
	
	ЗначенияРеквизитовИзменены = ТекущиеЗначенияРеквизитов.Контрагент <> Объект.Контрагент 
		ИЛИ ТекущиеЗначенияРеквизитов.ДоговорКонтрагента <> Объект.ДоговорКонтрагента 
		ИЛИ ТекущиеЗначенияРеквизитов.Дата <> Объект.Дата 
		ИЛИ ТекущиеЗначенияРеквизитов.СтруктурнаяЕдиница <> Объект.СтруктурнаяЕдиница;
		
	ЗаполнитьЗначенияСвойств(ТекущиеЗначенияРеквизитов, Объект);
	
	Возврат ЗначенияРеквизитовИзменены;
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьРеквизитыКодаДляОплаты(БанковскийСчетСсылка, КонтрагентСсылка)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("БанковскийСчетСсылка", БанковскийСчетСсылка);
	Запрос.УстановитьПараметр("Контрагент",           КонтрагентСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеквизитыКодаДляОплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РеквизитыКодаДляОплаты КАК РеквизитыКодаДляОплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникКонтрагенты
	|		ПО (СправочникКонтрагенты.Ссылка = &Контрагент)
	|ГДЕ
	|	РеквизитыКодаДляОплаты.Владелец = &БанковскийСчетСсылка
	|	И СправочникКонтрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРеквизитыКодаДляОплаты()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СчетНаОплатуРеквизитыКодаДляОплаты.РеквизитКодаДляОплаты КАК РеквизитКодаДляОплаты,
	|	МАКСИМУМ(СчетНаОплатуПокупателю.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТ_ЗначенияРеквизитовПоДатам
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю.РеквизитыКодаДляОплаты КАК СчетНаОплатуРеквизитыКодаДляОплаты
	|		ПО СчетНаОплатуПокупателю.Ссылка = СчетНаОплатуРеквизитыКодаДляОплаты.Ссылка
	|ГДЕ
	|	СчетНаОплатуПокупателю.Контрагент = &Контрагент
	|	И СчетНаОплатуПокупателю.ДоговорКонтрагента = &ДоговорКонтрагента
	|	И СчетНаОплатуПокупателю.Организация = &Организация
	|	И СчетНаОплатуПокупателю.МоментВремени < &МоментВремени
	|	И НЕ СчетНаОплатуРеквизитыКодаДляОплаты.РеквизитКодаДляОплаты ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетНаОплатуРеквизитыКодаДляОплаты.РеквизитКодаДляОплаты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетНаОплатуРеквизитыКодаДляОплаты.РеквизитКодаДляОплаты КАК РеквизитКодаДляОплаты,
	|	СчетНаОплатуРеквизитыКодаДляОплаты.Значение КАК Значение
	|ПОМЕСТИТЬ ВТ_ЗначенияРеквизитовСрезПоследних
	|ИЗ
	|	ВТ_ЗначенияРеквизитовПоДатам КАК ВТ_ЗначенияРеквизитовПоДатам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю.РеквизитыКодаДляОплаты КАК СчетНаОплатуРеквизитыКодаДляОплаты
	|		ПО ВТ_ЗначенияРеквизитовПоДатам.РеквизитКодаДляОплаты = СчетНаОплатуРеквизитыКодаДляОплаты.РеквизитКодаДляОплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|		ПО (СчетНаОплатуПокупателю.Ссылка = СчетНаОплатуРеквизитыКодаДляОплаты.Ссылка)
	|			И ВТ_ЗначенияРеквизитовПоДатам.Дата = СчетНаОплатуПокупателю.Дата
	|ГДЕ
	|	СчетНаОплатуПокупателю.Контрагент = &Контрагент
	|	И СчетНаОплатуПокупателю.ДоговорКонтрагента = &ДоговорКонтрагента
	|	И СчетНаОплатуПокупателю.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыКодаДляОплаты.Ссылка КАК РеквизитСсылка,
	|	РеквизитыКодаДляОплаты.Код КАК Идентификатор,
	|	ЕСТЬNULL(ЕСТЬNULL(ЗначенияРеквизитовДокумента.Значение, ВТ_ЗначенияРеквизитовСрезПоследних.Значение), """") КАК Значение
	|ИЗ
	|	Справочник.РеквизитыКодаДляОплаты КАК РеквизитыКодаДляОплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗначенияРеквизитовСрезПоследних КАК ВТ_ЗначенияРеквизитовСрезПоследних
	|		ПО РеквизитыКодаДляОплаты.Ссылка = ВТ_ЗначенияРеквизитовСрезПоследних.РеквизитКодаДляОплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю.РеквизитыКодаДляОплаты КАК ЗначенияРеквизитовДокумента
	|		ПО РеквизитыКодаДляОплаты.Ссылка = ЗначенияРеквизитовДокумента.РеквизитКодаДляОплаты
	|			И (ЗначенияРеквизитовДокумента.Ссылка = &СчетНаОплатуСсылка)
	|ГДЕ
	|	РеквизитыКодаДляОплаты.Владелец = &БанковскийСчетСсылка
	|	И РеквизитыКодаДляОплаты.Код <> """"
	|	И (НЕ РеквизитыКодаДляОплаты.ПометкаУдаления
	|			ИЛИ НЕ ЕСТЬNULL(ЗначенияРеквизитовДокумента.Значение, """") = """")
	|
	|УПОРЯДОЧИТЬ ПО
	|	Идентификатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыКодаДляОплатыДопустимыеЗначения.Ссылка КАК Ссылка,
	|	РеквизитыКодаДляОплатыДопустимыеЗначения.Значение КАК Значение,
	|	РеквизитыКодаДляОплатыДопустимыеЗначения.Представление КАК Представление
	|ИЗ
	|	Справочник.РеквизитыКодаДляОплаты.ДопустимыеЗначения КАК РеквизитыКодаДляОплатыДопустимыеЗначения
	|ГДЕ
	|	РеквизитыКодаДляОплатыДопустимыеЗначения.Ссылка.Владелец = &БанковскийСчетСсылка";
	
	Возврат ТекстЗапроса;
КонецФункции

&НаСервере
Процедура ЗаполнитьЗначенияРеквизитовКодаДляОплаты(УдаляемыеЭлементыФормы, ОбновитьЭлементыФормы)
	Запрос = Новый Запрос(ТекстЗапросаРеквизитыКодаДляОплаты());
	
	Если Объект.Ссылка.Пустая() Тогда
		МоментВремени = Новый МоментВремени(КонецДня(Объект.Дата));
	Иначе
		МоментВремени = Объект.Ссылка.МоментВремени();
	КонецЕсли;

	Запрос.УстановитьПараметр("БанковскийСчетСсылка", Объект.СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("СчетНаОплатуСсылка",   Объект.Ссылка);
	Запрос.УстановитьПараметр("МоментВремени",        МоментВремени);
	Запрос.УстановитьПараметр("Организация",          Объект.Организация);
	Запрос.УстановитьПараметр("Контрагент",           Объект.Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента",   Объект.ДоговорКонтрагента);
	
	КэшируемыеЗначения = Новый Структура;
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет(); 
	
	ДопустимыеЗначенияРеквизитов = РезультатыЗапроса[3].Выгрузить();
	
	РезультатЗапроса = РезультатыЗапроса[2].Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
		
		ИмяЭлемента  = СтрШаблон("РеквизитQRКода_%1", РезультатЗапроса.Идентификатор);
		ЭлементФормы = УдаляемыеЭлементыФормы.Найти(ИмяЭлемента);
		
		СуществующиеСтроки = Объект.РеквизитыКодаДляОплаты.НайтиСтроки(Новый Структура("ИмяЭлемента", ИмяЭлемента));
		
		Если ЭлементФормы <> Неопределено Тогда
			СтрокаРеквизита = СуществующиеСтроки[0];
			// Если реквизит, связанный с элементом формы, есть в новом наборе допреквизитов - то его удалять не надо
			УдаляемыеЭлементыФормы.Удалить(ЭлементФормы);
		ИначеЕсли ОбновитьЭлементыФормы Тогда
			Если СуществующиеСтроки.Количество() = 0 Тогда
				СтрокаРеквизита = Объект.РеквизитыКодаДляОплаты.Добавить();
				
				СтрокаРеквизита.РеквизитКодаДляОплаты    = РезультатЗапроса.РеквизитСсылка;
				СтрокаРеквизита.АвтоматическоеЗаполнение = Истина;
			Иначе
				СтрокаРеквизита = СуществующиеСтроки[0];
			КонецЕсли; 
			
			СтрокаРеквизита.Идентификатор            = РезультатЗапроса.Идентификатор;
			СтрокаРеквизита.ИмяЭлемента              = ИмяЭлемента;
			
			СтрокаРеквизита.ДопустимыеЗначения.Очистить();
			
			ДопустимыеЗначения = ДопустимыеЗначенияРеквизитов.НайтиСтроки(Новый Структура("Ссылка", СтрокаРеквизита.РеквизитКодаДляОплаты));
			Для каждого ДопустимоеЗначение Из ДопустимыеЗначения Цикл
				Представление = СтрШаблон("%1 - %2", ДопустимоеЗначение.Значение, ДопустимоеЗначение.Представление);
				СтрокаРеквизита.ДопустимыеЗначения.Добавить(ДопустимоеЗначение.Значение, Представление);
			КонецЦикла; 
			
			// Если значение списка только одно - то установим его по умолчанию
			Если СтрокаРеквизита.ДопустимыеЗначения.Количество() = 1 Тогда
				СтрокаРеквизита.Значение = СтрокаРеквизита.ДопустимыеЗначения[0].Значение;
				
				// Единственное возможное значение перезаполняться не должно
				СтрокаРеквизита.АвтоматическоеЗаполнение = Ложь;
			КонецЕсли;
		Иначе
			Продолжить;
		КонецЕсли; 
		
		// Если значение не было заполнено ранее, или стоит флаг автоматического 
		// перезаполнения - заполним реквизит значениями по умолчанию
		Если НЕ ЗначениеЗаполнено(СтрокаРеквизита.Значение) 
			ИЛИ СтрокаРеквизита.АвтоматическоеЗаполнение Тогда
			
			ЗначениеПоУмолчанию = ЗначениеПоУмолчаниюДляQRКода(СтрокаРеквизита.Идентификатор, КэшируемыеЗначения);
			Если ЗначениеЗаполнено(ЗначениеПоУмолчанию) Тогда
				// Из данных ИБ
				ЗначениеЗаполнения = ЗначениеПоУмолчанию;
			Иначе
				// Последнее выбранное для контрагента/договора
				ЗначениеЗаполнения = РезультатЗапроса.Значение;
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(ЗначениеЗаполнения) 
				И (СтрокаРеквизита.ДопустимыеЗначения.Количество() = 0 
					ИЛИ СтрокаРеквизита.ДопустимыеЗначения.НайтиПоЗначению(ЗначениеЗаполнения) <> Неопределено) Тогда
			
				СтрокаРеквизита.Значение = ЗначениеЗаполнения;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДополнительныеРеквизитыQRКода(ОбновитьЭлементыФормы = Ложь)
	Если НЕ ИзменилисьКлючевыеРеквизиты() Тогда
		Возврат;
	КонецЕсли;
	
	УдаляемыеЭлементыФормы = ОбщегоНазначения.ВыгрузитьКолонку(Объект.РеквизитыКодаДляОплаты, "ИмяЭлемента");
	
	Если ЕстьРеквизитыКодаДляОплаты(Объект.СтруктурнаяЕдиница, Объект.Контрагент) Тогда
		ЗаполнитьЗначенияРеквизитовКодаДляОплаты(УдаляемыеЭлементыФормы, ОбновитьЭлементыФормы);
	КонецЕсли; 
	
	Если ОбновитьЭлементыФормы Тогда
		ОбновитьЭлементыФормы(УдаляемыеЭлементыФормы);
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы(УдаляемыеЭлементыФормы)
	СтрокиКУдалению = Новый Массив;
	
	СуществующиеЭлементыФормы = ОбщегоНазначения.ВыгрузитьКолонку(Объект.РеквизитыКодаДляОплаты, "ИмяЭлемента");
	Для каждого ИмяЭлемента Из СуществующиеЭлементыФормы Цикл
		Элемент = Элементы.Найти(ИмяЭлемента);
		Если Элемент <> Неопределено Тогда
			Элементы.Удалить(Элемент);
		КонецЕсли; 
		
		Если УдаляемыеЭлементыФормы.Найти(ИмяЭлемента) <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				СтрокиКУдалению,
				Объект.РеквизитыКодаДляОплаты.НайтиСтроки(Новый Структура("ИмяЭлемента", ИмяЭлемента)));
		КонецЕсли; 
	КонецЦикла; 
	
	РеквизитыQRКода = Справочники.РеквизитыКодаДляОплаты.СписокРеквизитов();
	
	НомерСтроки = 0;
	Пока НомерСтроки < Объект.РеквизитыКодаДляОплаты.Количество() Цикл
		ОписаниеРеквизита = Объект.РеквизитыКодаДляОплаты[НомерСтроки];
		
		Если СтрокиКУдалению.Найти(ОписаниеРеквизита) <> Неопределено Тогда
			Объект.РеквизитыКодаДляОплаты.Удалить(ОписаниеРеквизита);
			Продолжить;
		КонецЕсли; 
		
		ЗаголовокЭлемента = РеквизитыQRКода[ОписаниеРеквизита.Идентификатор].Представление;
		
		НовыйЭлемент = Элементы.Добавить(ОписаниеРеквизита.ИмяЭлемента, Тип("ПолеФормы"), Элементы.ГруппаРеквизитыQRКода);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = ЗаголовокЭлемента;
		НовыйЭлемент.ПутьКДанным = СтрШаблон("Объект.РеквизитыКодаДляОплаты[%1].Значение", НомерСтроки);
		НовыйЭлемент.ВысотаЗаголовка = Окр(СтрДлина(ЗаголовокЭлемента)/15);
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииДопреквизита");
		
		Если ОписаниеРеквизита.ДопустимыеЗначения.Количество() > 0 Тогда
			Для каждого СтрокаСписка Из ОписаниеРеквизита.ДопустимыеЗначения Цикл
				НовыйЭлемент.СписокВыбора.Добавить(СтрокаСписка.Значение, СтрокаСписка.Представление);
			КонецЦикла; 
			НовыйЭлемент.РежимВыбораИзСписка  = Истина;
		КонецЕсли;
		
		НомерСтроки = НомерСтроки+1;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДопреквизита(Элемент)
	Модифицированность = Истина;
	
	// Если модифицируется автоматически заполняемый реквизит, то не перезаполняем его при изменении контрагента/договора
	СтрокиТаблицы = Объект.РеквизитыКодаДляОплаты.НайтиСтроки(Новый Структура("ИмяЭлемента", Элемент.Имя));
	Если СтрокиТаблицы.Количество() = 1 Тогда
		СтрокиТаблицы[0].АвтоматическоеЗаполнение = Ложь;
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Функция ЗначениеПоУмолчаниюДляQRКода(Идентификатор, КэшируемыеЗначения)
	Если КэшируемыеЗначения.Свойство(Идентификатор) Тогда
		// Значение было получено ранее
	ИначеЕсли Идентификатор = "contract" 
		И ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		
		КэшируемыеЗначения.Вставить(Идентификатор, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорКонтрагента, "Номер"));
	ИначеЕсли СтрНайти("PayerINN, lastName, firstName, middleName, payerAddress, flat, phone", Идентификатор) > 0
		И ЗначениеЗаполнено(Объект.Контрагент)
		И ЗначениеЗаполнено(Объект.Дата) Тогда
	
		СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Объект.Контрагент, Объект.Дата);
		
		СтруктураФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(СведенияОКонтрагенте.ПолноеНаименование);
		КэшируемыеЗначения.Вставить("lastName", СтруктураФИО.Фамилия);
		КэшируемыеЗначения.Вставить("firstName", СтруктураФИО.Имя);
		КэшируемыеЗначения.Вставить("middleName", СтруктураФИО.Отчество);
		
		КэшируемыеЗначения.Вставить("payerAddress", СведенияОКонтрагенте.ЮридическийАдрес);
		КэшируемыеЗначения.Вставить("phone", СведенияОКонтрагенте.Телефоны);
		
		АдресСтруктурой = УправлениеКонтактнойИнформациейБП.СтруктураАдреса(СведенияОКонтрагенте.ЗначениеJSONЮридическийАдрес);
		КэшируемыеЗначения.Вставить("flat", АдресСтруктурой.Квартира);
		
		КэшируемыеЗначения.Вставить("PayerINN", СведенияОКонтрагенте.ИНН);
	ИначеЕсли СтрНайти("PayeeINN, KPP", Идентификатор) > 0
		И ЗначениеЗаполнено(Объект.Организация) 
		И ЗначениеЗаполнено(Объект.Дата) Тогда
		
		СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Объект.Организация, Объект.Дата);
		
		КэшируемыеЗначения.Вставить("PayeeINN", СведенияОКонтрагенте.ИНН);
		КэшируемыеЗначения.Вставить("KPP", СведенияОКонтрагенте.КПП);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат КэшируемыеЗначения[Идентификатор];
КонецФункции

#КонецОбласти 

&НаСервере
Процедура ИзменитьВидимостьПодсказокНаСервере()
	
	УправлениеПанельюПодсказки.ИзменитьВидимостьПодсказок(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьЭлементПодсказкиНаФорме(ИмяЭлементаПодсказки) Экспорт
	
	СнятьВыделениеЭлементаПодсказкиНаФорме();
	
	Если ИмяЭлементаПодсказки = "ЗагрузитьИзФайла" Тогда
		
		ВыделенныйЭлементПодсказки = "ЗагрузитьИзФайла";
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = УправлениеПанельюПодсказкиКлиентСервер.ЦветФонаВыделенияЭлементаПодсказки();
		ПодключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме", 1.3, Истина);
		
	ИначеЕсли ИмяЭлементаПодсказки = "ИзменитьТовары" Тогда

		ВыделенныйЭлементПодсказки = "ИзменитьТовары";
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = УправлениеПанельюПодсказкиКлиентСервер.ЦветФонаВыделенияЭлементаПодсказки();
		ПодключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме", 1.3, Истина);
		
	ИначеЕсли ИмяЭлементаПодсказки = "РеквизитыПечати" Тогда
		
		ВыделенныйЭлементПодсказки = "РеквизитыПечатиQRКод";
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = УправлениеПанельюПодсказкиКлиентСервер.ЦветФонаВыделенияЭлементаПодсказки();
		ПодключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме", 1.3, Истина);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделениеЭлементаПодсказкиНаФорме() Экспорт
	
	ОтключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме");
	
	Если ВыделенныйЭлементПодсказки = "ЗагрузитьИзФайла"
		Или ВыделенныйЭлементПодсказки = "ИзменитьТовары"
		Или ВыделенныйЭлементПодсказки = "РеквизитыПечатиQRКод" Тогда
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = Новый Цвет();
	КонецЕсли;
	
	ВыделенныйЭлементПодсказки = "";
	
КонецПроцедуры

&НаСервере
Функция ПараметрыСозданияНовогоДоговора()
	
	Возврат РаботаСДоговорамиКонтрагентовБП.ПараметрыСозданияНовогоДоговора(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура УстановитьПредлагатьНовыйДоговорСНумерацией()
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьПредлагатьНовыйДоговорСНумерацией(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСрокОплатыДокументаРасчетов()
	
	Если Не Модифицированность Тогда
		
		СтруктураСрокОплаты = СрокиОплатыДокументов.СрокОплатыДокументаРасчетов(
			Объект.Организация, Объект.Ссылка, Объект.Дата, Объект.ДоговорКонтрагента);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураСрокОплаты);
		
	ИначеЕсли АвторасчетСрокаОплаты Тогда
		
		СрокОплаты = СрокиОплатыДокументов.СрокОплатыДокументаРасчетовАвторасчет(
			Объект.Ссылка, Объект.Дата, Объект.ДоговорКонтрагента);
		
	КонецЕсли;
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаСервереБезКонтекста
Функция КонтактноеЛицоКоммерческогоПредложения(КоммерческоеПредложение)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КоммерческоеПредложение, "КонтактноеЛицо");
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуНовогоКонтрагента()
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Ответственный", ПараметрОтветственный);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
	
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаНовогоЭлемента", ПараметрыФормы, Элементы.Контрагент);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьОповещениеОбИзмененииНастроекОтветственного()
	
	ПользователиБП.ОбработатьОповещениеОбИзмененииНастроекОтветственного(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиОповещения()
	
	НастройкаОповещенийДоступна = ОповещенияПользователейОСобытиях.ИспользуютсяОповещенияОтветственнымПоСчету()
		И Объект.Ответственный = Пользователи.ТекущийПользователь();
	
	Элементы.НастроитьОповещения.Видимость = НастройкаОповещенийДоступна;
	
	Если НастройкаОповещенийДоступна Тогда
		
		Если Не ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Ответственный) Тогда
			ОповещатьПриИзмененииСтатусаОплаты = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
				ОповещенияПользователейОСобытияхКлиентСервер.ИмяНастройкиОповещенияПриИзмененииСтатусаОплатыСчета());
			ОповещатьПриИзмененииСтатусаОтгрузки = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
				ОповещенияПользователейОСобытияхКлиентСервер.ИмяНастройкиОповещенияПриИзмененииСтатусаОтгрузкиСчета());
		Иначе
			НастройкиОповещенияПоСчету = 
				РегистрыСведений.НастройкиОповещенийПоСчетамПокупателю.УстановленныеНастройкиОповещенияПоСчетуПокупателю(Объект.Ссылка);
			ОповещатьПриИзмененииСтатусаОплаты = НастройкиОповещенияПоСчету.ОповещатьПриИзмененииСтатусаОплаты;
			ОповещатьПриИзмененииСтатусаОтгрузки = НастройкиОповещенияПоСчету.ОповещатьПриИзмененииСтатусаОтгрузки;
		КонецЕсли;
		
		ОповещенияПоДокументуВключены = ОповещатьПриИзмененииСтатусаОплаты Или ОповещатьПриИзмененииСтатусаОтгрузки;
		Элементы.НастроитьОповещения.Картинка =
			ОповещенияПользователейОСобытияхКлиентСервер.КартинкаНастройкиОповещений(ОповещенияПоДокументуВключены);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиОповещенияПоСчетуПокупателю(Счет)
	
	НастройкаОповещенийДоступна = ОповещенияПользователейОСобытиях.ИспользуютсяОповещенияОтветственнымПоСчету()
		И Объект.Ответственный = Пользователи.ТекущийПользователь();
	
	Если НастройкаОповещенийДоступна И Не ЗначениеЗаполнено(Объект.Ссылка)
		И (ОповещатьПриИзмененииСтатусаОплаты Или ОповещатьПриИзмененииСтатусаОтгрузки) Тогда
		ОповещенияПользователейОСобытиях.УстановитьНастройкиОповещенияПриИзмененииСтатусовСчета(
			Счет, ОповещатьПриИзмененииСтатусаОплаты, ОповещатьПриИзмененииСтатусаОтгрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОповещенияПослеПодтвержденияЗаписи(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Если Не Записать() Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СчетПокупателю", Объект.Ссылка);
	ПараметрыФормы.Вставить("ОповещатьПриИзмененииСтатусаОплаты", ОповещатьПриИзмененииСтатусаОплаты);
	ПараметрыФормы.Вставить("ОповещатьПриИзмененииСтатусаОтгрузки", ОповещатьПриИзмененииСтатусаОтгрузки);
	
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.Форма.НастройкаОповещений", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеНастроекОповещенияПользователямПоДокументу(НастройкиОповещения)
	
	ОповещатьПриИзмененииСтатусаОплаты = НастройкиОповещения.ОповещатьПриИзмененииСтатусаОплаты;
	ОповещатьПриИзмененииСтатусаОтгрузки = НастройкиОповещения.ОповещатьПриИзмененииСтатусаОтгрузки;
	
	ОповещенияПоДокументуВключены = ОповещатьПриИзмененииСтатусаОплаты Или ОповещатьПриИзмененииСтатусаОтгрузки;
	Элементы.НастроитьОповещения.Картинка =
		ОповещенияПользователейОСобытияхКлиентСервер.КартинкаНастройкиОповещений(ОповещенияПоДокументуВключены);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораСтавкиНДС()
	
	УчетНДС.УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "ТоварыСтавкаНДС");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьГостиницу()
	
	Если Не (ЗначениеЗаполнено(Объект.Организация) И ПлательщикТуристическогоНалога) Тогда
		Объект.Гостиница = Неопределено;
		Возврат;
	КонецЕсли;
		
	ТуристическийНалогСервер.УстановитьГостиницу(Объект.Гостиница, Объект.Организация);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВидыСкидок()
	
	ВидыСкидок = Новый Структура;
	ВидыСкидок.Вставить("НеПредоставляется", 0);
	ВидыСкидок.Вставить("НаОтдельныеПозиции", 1);
	ВидыСкидок.Вставить("ПоДокументуВЦелом", 2);
	
	Возврат ВидыСкидок;
	
КонецФункции

&НаСервере
Процедура РаспределитьСкидкуПоТабличнойЧасти()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.РаспределитьСкидкуПоСтрокамТабличнойЧасти();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФормуПослеРаспределенияСкидок()
	
	Прочитать();
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыФормыТуристическийНалог()
	
	ЗначениеЛьготыПоУмолчанию = Перечисления.ВидыЛьготПоТуристическомуНалогу.НеПрименяется;
	ДопУслугаРазмещения = Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд;
	ГиперссылкаЦвет = ЦветаСтиля.ГиперссылкаЦвет;
 	ЦветНеактивнойСтроки = ЦветаСтиля.ЦветНеАктивнойСтроки;  
	ГиперссылкаШрифт = Новый Шрифт(, , , ,Истина);

КонецПроцедуры

&НаСервере
Функция АдресТаблицыОсновныхУслугРазмещения()

	Возврат ПоместитьВоВременноеХранилище(ТаблицаОсновныхУслугРазмещения.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбновитьПредставлениеИДРодСтрок()
	ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Товары");
	ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Товары");
	НомерАктивнойСтроки = Элементы.Товары.ТекущиеДанные.НомерСтроки;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокРеквизитыПечатиQRКод(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ЗаголовокСчета = ?(ЗначениеЗаполнено(Объект.ЗаголовокСчета),
		Строка(Объект.ЗаголовокСчета),
		ПечатьТорговыхДокументовКлиентСервер.ЗаголовокСчетаНаОплатуПоУмолчанию());
	
	Если ЗначениеЗаполнено(Объект.ВариантПечатиQRКода) Тогда
		Если Объект.ВариантПечатиQRКода = ПредопределенноеЗначение("Перечисление.ВариантПечатиQRКода.НеПечатать") Тогда
			ЗаголовокСчета = СтрШаблон(НСтр("ru = '%1 без QR-кода'"), ЗаголовокСчета);
		ИначеЕсли Объект.ВариантПечатиQRКода = ПредопределенноеЗначение("Перечисление.ВариантПечатиQRКода.ПлатежныеРеквизиты") Тогда
			ЗаголовокСчета = СтрШаблон(НСтр("ru = '%1 по QR-коду с платежными реквизитами'"), ЗаголовокСчета);
		Иначе
			ЗаголовокСчета = СтрШаблон(НСтр("ru = '%1 по QR-коду %2'"), ЗаголовокСчета, Строка(Объект.ВариантПечатиQRКода));
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.ДополнительныеУсловия) Тогда
		ЗаголовокСчета = СтрШаблон(НСтр("ru = '%1 (%2)'"), ЗаголовокСчета, Строка(Объект.ДополнительныеУсловия));
	КонецЕсли;
	
	Элементы.РеквизитыПечатиQRКод.Заголовок = ЗаголовокСчета;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	АдаптироватьМКСписокФормыТовары();
	АдаптироватьМКСписокФормыВозвратнаяТара();
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаКоманднаяПанель.ПодчиненныеЭлементы);
	//Печать создается в группе родителе ПодменюПечать, если команда печати одна
	ПечатьСчетЗаказ = Элементы.Найти("ПодменюПечатьОбычное__СчетЗаказ");
	Если ПечатьСчетЗаказ <> Неопределено Тогда
		МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВПодменюМКПоделиться(Элементы, ПечатьСчетЗаказ);
	КонецЕсли;
	
	ОтправитьСсылкуНаДокумент = Элементы.Найти("ПодменюОтправитьВажное__ОтправитьСсылкуНаДокумент");
	Если ОтправитьСсылкуНаДокумент <> Неопределено Тогда
		ОтправитьСсылкуНаДокумент.Заголовок = НСтр("ru = 'Ссылка на документ'");
		МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВПодменюМКПоделиться(Элементы, ОтправитьСсылкуНаДокумент);
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, Элементы.ПодменюПечать.ПодчиненныеЭлементы);
	
	ПодменюОтправить = Элементы.Найти("ПодменюОтправить");
	Если ПодменюОтправить <> Неопределено Тогда
		МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, ПодменюОтправить);
	КонецЕсли;
	
	КомандыЭДО = Элементы.Найти("КомандыЭДО");
	Если КомандыЭДО <> Неопределено Тогда
		МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, КомандыЭДО);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьНомерДатаФормаДокумента(Элементы, Элементы.ГруппаНомерДата);
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаНомерДата);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Организация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаКонтрагент);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаДоговор);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаСкидка);
	
	Элементы.ТоварыЦеныИВалюта.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ТоварыЦеныИВалюта.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ТоварыЦеныИВалюта);
	
	Элементы.Переместить(Элементы.АдресДоставки, Элементы.ГруппаДоставка);
	Элементы.Переместить(Элементы.СсылкаДляПереходаНаКарту, Элементы.ГруппаДоставка);
	Элементы.ГруппаДоставка.Объединенная = Истина;
	
	Элементы.ГруппаСтатусСрокОплаты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.ГруппаСтатусСрокОплаты.Объединенная = Истина;
	Элементы.СрокОплаты.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	
	Элементы.ГруппаПрочиеРеквизиты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаПрочиеРеквизиты);
	
	//Переместим поля в Прочее
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаЛевая);
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаПравая);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаСостояниеЭДО);
	
	Элементы.ГруппаКомментарий.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаКомментарий);
		
	//Подвал
	Элементы.ВалютаДокументаИтогиВсего.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ВалютаДокументаИтогиВсего.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВалютаДокументаИтогиВсего.РастягиватьПоГоризонтали = Истина;
	
	Элементы.ГруппаИтогиФон.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
	Элементы.ГруппаИтогиФон.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаИтоги.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Авто;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаИтоги);
	
	Если Не Объект.Ссылка.Пустая() Тогда
		МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаРеквизитыQRКода);
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.НадписьПовторение);
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаОплачено);
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "Товары");
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "ВозвратнаяТара");
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормыТовары()
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыПодбор = "ПодборТовары";
	КомандыПоля.ИмяКомандыДобавитьСтроку = "ТоварыДобавить";
	КомандыПоля.КомандыТаблицыПрочие.Добавить("ТоварыГруппаЗаполнить");
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаТовары;
	ОписаниеГруппыПоля.ПоказатьГруппуНового = Истина;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.КолонкаНомерСтроки = "ТоварыНомерСтроки";
	ОписаниеКолонокПоля.ВажныеКолонки = "ТоварыГруппаСодержание,ТоварыСодержание";
	ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = 
		"ТоварыНоменклатураИнтерактивная,ТоварыОсновноеСредствоИнтерактивное,ТоварыСодержание";
	ОписаниеКолонокПоля.СкрытьКолонки = "ТоварыОтступ";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
		Элементы.Товары, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
	
	Элементы.Переместить(Элементы.ТоварыНоменклатураКод,
		Элементы.Товары,
		Элементы.ТоварыГруппаКоличество);
	Элементы.Переместить(Элементы.ТоварыНоменклатураАртикул,
		Элементы.Товары,
		Элементы.ТоварыГруппаКоличество);
	
	Элементы.ТоварыГруппаНоменклатура.Высота = 2;
	Элементы.ТоварыСодержание.Ширина = 20;
	
	Элементы.ТоварыКоличество.Формат = "ЧФ='Кол-во: Ч'";
	Элементы.ТоварыЦена.Формат = "ЧФ='Цена: Ч'";
	Элементы.ТоварыСумма.Формат = "ЧФ='Сумма: Ч'";
	Элементы.ТоварыПроцентСкидки.Формат = "ЧФ='Скидка %: Ч'";
	Элементы.ТоварыСуммаСкидки.Формат = "ЧФ='Скидка: Ч'";
	Элементы.ТоварыСуммаНДС.Формат = "ЧФ='НДС: Ч'";
	Элементы.ТоварыВсего.Формат = "ЧФ='Всего: Ч'";
	Элементы.ТоварыРасходНаЕдиницу.Формат = "ЧФ='Расходы на единицу: Ч'";
	
	Элементы.ТоварыРасходНаЕдиницу.ПодсказкаВвода = НСтр("ru = 'Нажмите для выбора'");
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормыВозвратнаяТара()
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
	
		КомандыМК = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
		КомандыМК.ИмяКомандыПодбор ="ПодборВозвратнаяТара";
		КомандыМК.ИмяКомандыДобавитьСтроку = "ВозвратнаяТараДобавить";
		КомандыМК.КомандыТаблицыПрочие.Добавить("ВозвратнаяТараГруппаЗаполнить");
		
		ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
		ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаВозвратнаяТара;
		
		ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
		ОписаниеКолонокПоля.КолонкаНомерСтроки = "ВозвратнаяТараНомерСтроки";
		ОписаниеКолонокПоля.ВажныеКолонки = "ВозвратнаяТараНоменклатура";
		ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = "ВозвратнаяТараНоменклатура";
		ОписаниеКолонокПоля.СкрытьКолонки = "ВозвратнаяТараОтступ";
		
		МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
			Элементы.ВозвратнаяТара, ОписаниеГруппыПоля, КомандыМК, ОписаниеКолонокПоля);
		
		Элементы.Переместить(Элементы.ВозвратнаяТараНоменклатураКод, Элементы.ВозвратнаяТара, Элементы.ВозвратнаяТараГруппаКоличество);
		Элементы.Переместить(Элементы.ВозвратнаяТараНоменклатураАртикул, Элементы.ВозвратнаяТара, Элементы.ВозвратнаяТараГруппаКоличество);
		
		Элементы.ВозвратнаяТараКоличество.Формат = "ЧФ='Количество: Ч'";
		Элементы.ВозвратнаяТараЦена.Формат = "ЧФ='Цена: Ч'";
		Элементы.ВозвратнаяТараСумма.Формат = "ЧФ='Сумма: Ч'";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РедактированиеКомментарияВЗакрытомПериоде

&НаКлиенте
Процедура Подключаемый_РедактироватьКомментарий(Команда) 
	
	КомментарииДокументовЗакрытогоПериодаКлиент.ИзменитьКомментарий(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоставкаНажатие(Элемент)

	Если Не ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",                  ТолькоПросмотр);
	ПараметрыФормы.Вставить("Контрагент",                      Объект.Контрагент);
	ПараметрыФормы.Вставить("АдресДоставки",                   Объект.АдресДоставки);
	ПараметрыФормы.Вставить("АдресДоставкиЗначение",           Объект.АдресДоставкиЗначение);
	ПараметрыФормы.Вставить("ДатаДокумента",                   Объект.Дата);
	ПараметрыФормы.Вставить("Склад",                           Объект.Склад);
	ПараметрыФормы.Вставить("ПоказыватьАдресДоставки", Не ИспользоватьДоставкуТранспортнойКомпанией 
		Или Не ЗначениеЗаполнено(Объект.СпособДоставки));
	
	ОткрытьФорму("Документ.СчетНаОплатуПокупателю.Форма.ФормаДоставка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораРеквизитыДоставки(ВыбранноеЗначение, ИсточникВыбора)

	Модифицированность = Истина;
	
	ЗначениеСкладаДоИзменения = Объект.Склад;
	
	ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение);
	
	Если ЗначениеСкладаДоИзменения <> Объект.Склад И ЗначениеЗаполнено(Объект.Склад) Тогда
		Объект.ПодразделениеОрганизации = ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(Объект.Организация, Объект.Склад);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
