#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ВыделеноНесколькоСчетов Тогда
		ЧастиТекста = Новый Массив;
		ЧастиТекста.Добавить(НСтр("ru = 'Формат электронного документа ""Счет на оплату"" не поддерживает скидку по документу в целом.
								  |Перед формированием электронных документов для'"));
		ЧастиТекста.Добавить(" ");						  
		ЧастиТекста.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Счетов на оплату:'"), , , , "СсылкаНаСписокСчетовНаОплату"));
		ЧастиТекста.Добавить(Символы.ПС);
		ЧастиТекста.Добавить(НСтр("ru = '  - Способ расчета скидки будет изменен на ""Скидка на отдельные позиции""
								  |  - Сумма скидки будет распределена по строкам табличной части
								  |
								  |Распределить скидку по позициям?'"));						  
		Элементы.ТекстВопроса.Заголовок = Новый ФорматированнаяСтрока(ЧастиТекста);
	Иначе
		Элементы.ТекстВопроса.Заголовок = НСтр("ru = 'Формат электронного документа ""Счет на оплату"" не поддерживает скидку по документу в целом.
											   |Перед формированием электронного документа в Счете на оплату:
											   |  - Способ расчета скидки будет изменен на ""Скидка на отдельные позиции""
											   |  - Сумма скидки будет распределена по строкам табличной части
											   |
											   |Распределить скидку по позициям?'");
	КонецЕсли;											  
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ТекстВопросаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Параметры.СчетаНаОплатуСоСкидкойПоДокументу) Тогда
		Возврат;
	КонецЕсли;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СсылкаНаСписокСчетовНаОплату" Тогда
		СписокСчетов = Новый СписокЗначений();
		СписокСчетов.ЗагрузитьЗначения(Параметры.СчетаНаОплатуСоСкидкойПоДокументу);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СчетаНаОплатуСоСкидкойПоДокументу", СписокСчетов);
		
		ОткрытьФорму("Документ.СчетНаОплатуПокупателю.Форма.ФормаСписка", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Распределить(Команда)
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВсегдаРаспределять(Команда)
	
	ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекСохранить(ОбменСКонтрагентамиБПКлиент.КлючОбъектаСчетНаОплату(),
		ОбменСКонтрагентамиБПКлиент.КлючНастроекВсегдаРаспределятьСкидкуВСчетеНаОплату(), Истина);
	
	Закрыть(КодВозвратаДиалога.ОК);

КонецПроцедуры

#КонецОбласти