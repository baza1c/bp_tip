#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполним реквизиты формы из параметров.
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"ЗаголовокСчета, ДополнительныеУсловия, ВариантПечатиQRКода,
		|Руководитель, ЗаРуководителяНаОсновании, ГлавныйБухгалтер, ЗаГлавногоБухгалтераНаОсновании,
		|ДатаДокумента, Организация, ТолькоПросмотр");
	
	ЗаполнитьСписокВыбораВариантовПечатиQR(Параметры.СтруктурнаяЕдиница, Параметры.Контрагент);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.СчетНаОплатуПокупателю",
		"ФормаРеквизитыПечатиQRКод",
		НСтр("ru = 'Новости: Счет покупателю'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ЛоготипИПечать = НСтр("ru = 'Логотип и печать'");
	Элементы.ЛоготипИПечать.Видимость = ЗначениеЗаполнено(Организация);
	
	УправлениеФормойНаСервере();
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
	ИначеЕсли Модифицированность И Не ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент И Модифицированность Тогда
		
		СтруктураРезультат = Новый Структура("ЗаголовокСчета, ДополнительныеУсловия, ВариантПечатиQRКода,
			|Руководитель, ЗаРуководителяНаОсновании, ГлавныйБухгалтер, ЗаГлавногоБухгалтераНаОсновании");
		
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, ЭтотОбъект);
		
		ОповеститьОВыборе(СтруктураРезультат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчкиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаголовокСчетаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("Организация", Организация);
	
	ОткрытьФорму("Справочник.ЗаголовкиСчетовПокупателям.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокСчетаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") 
		И (Не ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		Или Элемент.ОграничениеТипа.СодержитТип(Тип("СправочникСсылка.ЗаголовкиСчетовПокупателям"))) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ПараметрыФормы.Вставить("ТекстЗаполнения", ВыбранноеЗначение);
		
		ОткрытьФорму("Справочник.ЗаголовкиСчетовПокупателям.ФормаОбъекта", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокСчетаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Не ПустаяСтрока(Текст) 
		И (Не ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		Или Элемент.ОграничениеТипа.СодержитТип(Тип("СправочникСсылка.ЗаголовкиСчетовПокупателям"))) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ДанныеВыбораЗаголовка(ПараметрыПолученияДанных, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокСчетаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если Не ПустаяСтрока(Текст) 
		И (Не ЗначениеЗаполнено(Элемент.ОграничениеТипа)
		Или Элемент.ОграничениеТипа.СодержитТип(Тип("СправочникСсылка.ЗаголовкиСчетовПокупателям"))) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ДанныеВыбораЗаголовка(ПараметрыПолученияДанных, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РуководительПриИзменении(Элемент)
	
	РуководительПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ГлавныйБухгалтерПриИзменении(Элемент)
	
	ЗаГлавногоБухгалтераНаОсновании = ПолучитьОснованиеПраваПодписиФизЛица(ГлавныйБухгалтер, Организация, ДатаДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛоготипИПечатьНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Организация);
	ПараметрыФормы.Вставить("АктивироватьПолеПечать", Истина);
	
	ФормаОрганизации = ПолучитьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
	
	Если ФормаОрганизации.Открыта() Тогда
		Оповестить("ФормаОрганизации_АктивироватьПолеПечать", Организация);
		ФормаОрганизации.Активизировать();
	Иначе
		ФормаОрганизации.Открыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаПредпросмотрНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяКартинки", ИмяКартинкиПолный);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru='Образец печатной формы ""Счет покупателю""'"));
	ОткрытьФорму("ОбщаяФорма.ОтображениеКартинки",
		ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	ПеренестиВДокумент = Истина;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)

	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораВариантовПечатиQR(СтруктурнаяЕдиница, Контрагент)
	
	СписокВыбораQR = Элементы.ВариантПечатиQRКода.СписокВыбора;
	СписокВыбораQR.Добавить(Перечисления.ВариантПечатиQRКода.НеПечатать);
	
	Если Не ЗначениеЗаполнено(СтруктурнаяЕдиница) Или Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктурнаяЕдиница, "Банк, Валютный, ЦифровойСчет");
	Если РеквизитыСчета.Валютный Или РеквизитыСчета.ЦифровойСчет Тогда
		Возврат;
	КонецЕсли;
	
	СписокВыбораQR.Добавить(Перечисления.ВариантПечатиQRКода.ПлатежныеРеквизиты);
	
	РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Контрагент, "ЮридическоеФизическоеЛицо, ИндивидуальныйПредприниматель");
	ЭтоОрганизация = РеквизитыКонтрагента.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо
		Или РеквизитыКонтрагента.ИндивидуальныйПредприниматель;
	
	Если ЭтоОрганизация Тогда
		ИнтеграцияДоступна = ПереводыСБПb2b.ПереводыСБПДоступны();
	Иначе
		ИнтеграцияДоступна = ПереводыСБПc2b.ПереводыСБПДоступны();
	КонецЕсли;
	
	Если ИнтеграцияДоступна Тогда
		НастройкаИнтеграцииСБП = Неопределено;
		Если ЭтоОрганизация Тогда
			НастройкаИнтеграцииСБП = ИнтеграцияССБПБППовтИсп.НастройкаИнтеграцииСБППоБанку(
				Организация, РеквизитыСчета.Банк, "b2b");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НастройкаИнтеграцииСБП) И Не ЭтоОрганизация Тогда
			НастройкаИнтеграцииСБП = ИнтеграцияССБПБППовтИсп.НастройкаИнтеграцииСБППоБанку(
				Организация, РеквизитыСчета.Банк, "c2b");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НастройкаИнтеграцииСБП) Тогда
			СписокВыбораQR.Добавить(Перечисления.ВариантПечатиQRКода.СБП);
		КонецЕсли;
	КонецЕсли;
	
	СписокВыбораQR.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОснованиеПраваПодписиФизЛица(ФизическоеЛицо, Организация, ДатаОтбор)
	
	Возврат Справочники.ОснованияПраваПодписи.ОснованиеПраваПодписиФизЛица(ФизическоеЛицо, Организация, ДатаОтбор);
	
КонецФункции

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВыбораЗаголовка(Параметры, ОкончаниеВводаТекста = Ложь)
	
	Возврат Справочники.ЗаголовкиСчетовПокупателям.ДанныеВыбораЗаголовка(Параметры, ОкончаниеВводаТекста);
	
КонецФункции

&НаСервере
Процедура РуководительПриИзмененииНаСервере()
	
	ЗаРуководителяНаОсновании = ПолучитьОснованиеПраваПодписиФизЛица(Руководитель, Организация, ДатаДокумента);
	УправлениеФормойНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойНаСервере()
	
	ИмяКартинкиПредпросмотр = "РеквизитыПечатиСчетаПредпросмотр";
	ИмяКартинкиПолный = "РеквизитыПечатиСчетаПолный";
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Организация, "ЮридическоеФизическоеЛицо, ИндивидуальныйПредприниматель");
	Если РеквизитыОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		СчетВыставленЛичноПредпринимателем = Не ЗначениеЗаполнено(Руководитель)
			Или Руководитель = РеквизитыОрганизации.ИндивидуальныйПредприниматель;
		СтатусФизическогоЛица = Справочники.Организации.СтатусФизическогоЛицаНаДату(
			Организация, ДатаДокумента);
		ЭтоСамозанятый = СтатусФизическогоЛица = Перечисления.СтатусыФизическихЛиц.Самозанятый;
		Если СчетВыставленЛичноПредпринимателем Или ЭтоСамозанятый Тогда
			ИмяКартинкиПредпросмотр = "РеквизитыПечатиСчетаПредпросмотрПредприниматель";
			ИмяКартинкиПолный = "РеквизитыПечатиСчетаПолныйПредприниматель";
		Иначе
			ИмяКартинкиПредпросмотр = "РеквизитыПечатиСчетаПредпросмотрЗаПредпринимателя";
			ИмяКартинкиПолный = "РеквизитыПечатиСчетаПолныйЗаПредпринимателя";
		КонецЕсли;
	КонецЕсли;
	
	Элементы.КартинкаПредпросмотр.Картинка = БиблиотекаКартинок[ИмяКартинкиПредпросмотр];
	
КонецПроцедуры

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы,
		КоманднаяПанель.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаРеквизитыПечати);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаПредпросмотр);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#КонецОбласти