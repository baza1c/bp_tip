
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СчетПокупателю = Параметры.СчетПокупателю;
	ОповещатьПриИзмененииСтатусаОплаты = Параметры.ОповещатьПриИзмененииСтатусаОплаты;
	ОповещатьПриИзмененииСтатусаОтгрузки = Параметры.ОповещатьПриИзмененииСтатусаОтгрузки;
	
	Если Не ЗначениеЗаполнено(СчетПокупателю) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокВариантовНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
	ИначеЕсли Модифицированность И Не НастройкиИзменены Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Настройки были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НастройкиИзменены И Модифицированность Тогда
		НастройкиОповещения = Новый Структура;
		НастройкиОповещения.Вставить("ОповещатьПриИзмененииСтатусаОплаты", ОповещатьПриИзмененииСтатусаОплаты);
		НастройкиОповещения.Вставить("ОповещатьПриИзмененииСтатусаОтгрузки", ОповещатьПриИзмененииСтатусаОтгрузки);

		Оповестить("ИзменениеНастроекОповещенияПриИзмененииСтатусовСчетаПокупателю", НастройкиОповещения, СчетПокупателю);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОповещатьПриИзмененииСтатусаОплатыПриИзменении(Элемент)
	
	ОбработатьИзменениеНастроекОповещения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещатьПриИзмененииСтатусаОтгрузкиПриИзменении(Элемент)
	
	ОбработатьИзменениеНастроекОповещения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьНастройки(Команда)
	
	Если Модифицированность Тогда
		СохранитьНастройки();
	КонецЕсли;
	
	НастройкиИзменены = Истина;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	НастройкиИзменены = Ложь;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройки()
	
	Если ВариантНастройки = ИмяВариантаНастройкиВсеСчета() Тогда
		СохранитьНастройкиОтветственного();
	КонецЕсли;
		
	ОповещенияПользователейОСобытиях.УстановитьНастройкиОповещенияПриИзмененииСтатусовСчета(
		СчетПокупателю, ОповещатьПриИзмененииСтатусаОплаты, ОповещатьПриИзмененииСтатусаОтгрузки);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтветственного()
	
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию(
		ОповещенияПользователейОСобытияхКлиентСервер.ИмяНастройкиОповещенияПриИзмененииСтатусаОплатыСчета(),
		ОповещатьПриИзмененииСтатусаОплаты);
	
	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию(
		ОповещенияПользователейОСобытияхКлиентСервер.ИмяНастройкиОповещенияПриИзмененииСтатусаОтгрузкиСчета(),
		ОповещатьПриИзмененииСтатусаОтгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		СохранитьНастройки();
		НастройкиИзменены = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		НастройкиИзменены = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВариантовНастройки()
	
	СписокВыбораВарианта = Элементы.ВариантНастройки.СписокВыбора;
	
	СписокВыбораВарианта.Очистить();
	
	СписокВыбораВарианта.Добавить(ИмяВариантаНастройкиТекущийСчет(), НСтр("ru = 'для текущего счета'"));
	
	СписокВыбораВарианта.Добавить(ИмяВариантаНастройкиВсеСчета(), НСтр("ru = 'для всех счетов'"));
	
	УстановитьВариантНастройкиПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВариантНастройкиПоУмолчанию()
	
	НастройкаОповещатьПриИзмененииСтатусаОплатыВсеСчета = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
		ОповещенияПользователейОСобытияхКлиентСервер.ИмяНастройкиОповещенияПриИзмененииСтатусаОплатыСчета());
	
	НастройкаОповещатьПриИзмененииСтатусаОтгрузкиВсеСчета = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
		ОповещенияПользователейОСобытияхКлиентСервер.ИмяНастройкиОповещенияПриИзмененииСтатусаОтгрузкиСчета());
	
	Если НастройкаОповещатьПриИзмененииСтатусаОплатыВсеСчета = ОповещатьПриИзмененииСтатусаОплаты
		И НастройкаОповещатьПриИзмененииСтатусаОтгрузкиВсеСчета = ОповещатьПриИзмененииСтатусаОтгрузки Тогда
		ВариантНастройки = ИмяВариантаНастройкиВсеСчета();
	Иначе
		ВариантНастройки = ИмяВариантаНастройкиТекущийСчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеНастроекОповещения()
	
	ВариантНастройки = ИмяВариантаНастройкиТекущийСчет();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяВариантаНастройкиТекущийСчет()
	
	Возврат "ТекущийСчет";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяВариантаНастройкиВсеСчета()
	
	Возврат "ВсеСчета";
	
КонецФункции

#КонецОбласти

