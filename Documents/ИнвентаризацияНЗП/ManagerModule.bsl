#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает время документа по умолчанию.
// Требуется если пользователь используется опцию:
// "Время документов устанавливать автоматически".
// 
// Возвращаемое значение:
//  Структура -  Время документа по умолчанию:
//   * Часы - Число
//   * Минуты - Число
//
Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 6, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//  Ограничение - См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиОбновления

Процедура УстановитьПроведен(ПараметрыОтложенногоОбновления = Неопределено) Экспорт
	
	ОбновлениеСПредыдущейРедакции.УстановитьПроведен(Метаданные.Документы.ИнвентаризацияНЗП, ПараметрыОтложенногоОбновления);
	
КонецПроцедуры

Процедура ЗаполнитьВидОперации(ПараметрыОбработчика) Экспорт
	
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ИнвентаризацияНЗП.Ссылка КАК Ссылка,
	|	ИнвентаризацияНЗП.МоментВремени КАК Курсор
	|ИЗ
	|	Документ.ИнвентаризацияНЗП КАК ИнвентаризацияНЗП
	|ГДЕ
	|	ИнвентаризацияНЗП.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИнвентаризацияНЗП.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Курсор УБЫВ";
	
	Если Не ПараметрыОбработчика.Свойство("Курсор") Тогда
		Курсор = Неопределено;
		ТекстЗапроса = ШаблонТекстаЗапроса;
	Иначе
		Курсор = ПараметрыОбработчика.Курсор;
		Запрос = СхемыЗапросов.НайтиТаблицуПоПсевдониму(ШаблонТекстаЗапроса, "ИнвентаризацияНЗП");
		Запрос.Оператор.Отбор.Добавить("ИнвентаризацияНЗП.МоментВремени < &Курсор");
		ТекстЗапроса = Запрос.Схема.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Курсор", Курсор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Ложь;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПараметрыОбработчика.Вставить("Курсор", Выборка.Курсор);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ИнвентаризацияНЗП");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
			Если ДокументОбъект = Неопределено Или ЗначениеЗаполнено(ДокументОбъект.ВидОперации) Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийИнвентаризацияНЗП.СуммоваяОценка;
		
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение = СтрШаблон(
				НСтр("ru = 'Ошибка при выполнении обработчика Документы.ИнвентаризацияНЗП.ЗаполнитьВидОперации.
                      |%1'"),
				ПредставлениеОшибки);
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ИнвентаризацияНЗП,
				Выборка.Ссылка,
				Сообщение);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт инвентаризации
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ";
	КомандаПечати.Представление = НСтр("ru = 'Акт инвентаризации'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Приказ о проведении инвентаризации (ИНВ-22)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ22";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о проведении инвентаризации (ИНВ-22)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Инвентаризация незавершенного производства""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета ИНВ формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ", НСтр("ru = 'Акт инвентаризации'"), 
			ПечатьИНВ(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "ОбщийМакет.ПФ_MXL_ИНВ");
		
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ22") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ22",НСтр("ru = 'ИНВ-22 (Приказ о проведении инвентаризации)'"), 
			ИнвентаризацияАктивовОбязательств.ПечатьИНВ22(МассивОбъектов, ОбъектыПечати), , "ОбщийМакет.ПФ_MXL_ИНВ22");
		
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "ПодразделениеОрганизации");
	
	Возврат Результат;
	
КонецФункции

// В документе "Инвентаризация незавершенного производства" с видом операции "КоличественнаяОценка" незавершенное
// производство указывается в разрезе изготавливаемой продукции. Также в документе требуется указать,
// к какой номенклатурной группе относится продукция, так как одна и та же продукция может относиться к разным номенклатурным группам.
// Но на практике часто бывает, что можно автоматически достаточно точно определить к какой номенклатурной относится продукция.
// Например, когда используется только одна номенклатурная группа "Основная номенклатурная группа", которая заполняется
// автоматически и пользователю даже не предоставляется ее выбор.
// Метод возвращает номенклатурные группы для списка продукции, чтобы автоматически заполнить их в табличной части документа.
// Номенклатурная группа для каждой продукции определяется по следующему алгоритму (в порядке убывания приоритета):
// 1. Если используется одна номенклатурная группа, то возвращается основная номенклатурная группа;
// 2. Если в оборотах по регистру Хозрасчетный для всех организаций по счетам, на которых есть одновременно субконто
// "Продукция" и "Номенклатурная группа", для заданной продукции встречается единственная непустая номенклатурная группа,
// то возвращается эта номенклатурная группа;
// 3. Номенклатурная группа, заданная в карточке справочника Номенклатура, соответствующей продукции.
// 
// Параметры:
//  СписокПродукции - СправочникСсылка.Номенклатура
//                  - Массив из СправочникСсылка.Номенклатура
//  ИспользоватьОднуНоменклатурнуюГруппу - Булево
//  ОсновнаяНоменклатурнаяГруппа - СправочникСсылка.НоменклатурныеГруппы
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение - Номенклатурные группы, соответствующие заданной продукции:
//    * Ключ - СправочникСсылка.Номенклатура
//    * Значение - СправочникСсылка.НоменклатурныеГруппы
//
Функция НоменклатурныеГруппыПоПродукции(Знач СписокПродукции, ИспользоватьОднуНоменклатурнуюГруппу, ОсновнаяНоменклатурнаяГруппа, ДатаДокумента) Экспорт

	НоменклатурныеГруппыПоПродукции = Новый Соответствие;
	
	Если ТипЗнч(СписокПродукции) <> Тип("Массив") Тогда
		СписокПродукции = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СписокПродукции);
	Иначе
		СписокПродукции = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокПродукции);
	КонецЕсли;
	
	Если ИспользоватьОднуНоменклатурнуюГруппу Тогда
		Для Каждого Продукция Из СписокПродукции Цикл
			НоменклатурныеГруппыПоПродукции.Вставить(Продукция, ОсновнаяНоменклатурнаяГруппа);
		КонецЦикла;
		Возврат НоменклатурныеГруппыПоПродукции;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	// Запрос состоит из двух частей, одна из которых выполняется в привилегированном режиме, чтобы получить данные по всем
	// организациям, а не только по тем, которые доступны пользователю.
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокПродукции", СписокПродукции);
	Запрос.УстановитьПараметр("НачалоПрошлогоГода", НачалоГода(ДобавитьМесяц(ДатаДокумента, -12)));
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Продукция);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Субконто1 КАК Продукция,
	|	МАКСИМУМ(ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.НоменклатурныеГруппы)) КАК НоменклатурнаяГруппа
	|ПОМЕСТИТЬ ИспользуемыеНоменклатурныеГруппы
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПрошлогоГода, , , , &ВидыСубконто, Субконто1 В (&СписокПродукции), , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	(ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.НоменклатурныеГруппы)) <> ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто1
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ХозрасчетныйОбороты.Субконто2) = 1";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	// Поскольку первая часть выполняется в привилегированном режиме, результат может содержать ссылки на номенклатурные группы,
	// недоступные пользователю из-за ограничения по ролям. В интерфейсе они будут отображаться как "Объект не найден...".
	// Чтобы избежать этого, в запросе делаем явное соединение со справочником "НоменклатурныеГруппы".
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИспользуемыеНоменклатурныеГруппы.Продукция КАК Продукция,
	|	ИспользуемыеНоменклатурныеГруппы.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	1 КАК Приоритет
	|ПОМЕСТИТЬ ВариантыНоменклатурныхГрупп
	|ИЗ
	|	ИспользуемыеНоменклатурныеГруппы КАК ИспользуемыеНоменклатурныеГруппы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
	|		ПО ИспользуемыеНоменклатурныеГруппы.НоменклатурнаяГруппа = НоменклатурныеГруппы.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	Номенклатура.НоменклатурнаяГруппа,
	|	2
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В(&СписокПродукции)
	|	И Номенклатура.НоменклатурнаяГруппа <> ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриоритетыНоменклатурныхГрупп.Продукция КАК Продукция,
	|	МИНИМУМ(ПриоритетыНоменклатурныхГрупп.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ПриоритетыНоменклатурныхГрупп
	|ИЗ
	|	ВариантыНоменклатурныхГрупп КАК ПриоритетыНоменклатурныхГрупп
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриоритетыНоменклатурныхГрупп.Продукция
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВариантыНоменклатурныхГрупп.Продукция КАК Продукция,
	|	ВариантыНоменклатурныхГрупп.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|ИЗ
	|	ВариантыНоменклатурныхГрупп КАК ВариантыНоменклатурныхГрупп
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриоритетыНоменклатурныхГрупп КАК ПриоритетыНоменклатурныхГрупп
	|		ПО ВариантыНоменклатурныхГрупп.Приоритет = ПриоритетыНоменклатурныхГрупп.Приоритет
	|			И ВариантыНоменклатурныхГрупп.Продукция = ПриоритетыНоменклатурныхГрупп.Продукция";

	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоменклатурныеГруппыПоПродукции.Вставить(Выборка.Продукция, Выборка.НоменклатурнаяГруппа)
	КонецЦикла;
	
	Возврат НоменклатурныеГруппыПоПродукции;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьИНВ(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru = 'Председатель комиссии'"));
	ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru = 'Члены комиссии:'"));
	ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
		НСтр("ru = 'Материально ответственное(ые) лицо(а):'"));
	ЗаголовокРазделаПодписей.Вставить("Проверяющий",
		НСтр("ru = 'Указанные в настоящем акте данные и расчеты проверил'"));
		
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияОбъектовУчета_ИНВ";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ИНВ");
	
	// Получаем области макета для вывода в табличный документ
	Шапка            = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Шапка");
	Подпись          = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Подпись");
	ПолеДаты         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ПолеДаты");
	ПередТаблицей    = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ПередТаблицей");
	Подсчеты         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Подсчеты");
	Претензии        = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Претензии");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СчетЗатрат КАК СчетЗатрат,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснованиеДата = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Реквизиты.Дата
	|		ИНАЧЕ Реквизиты.ДокументОснованиеДата
	|	КОНЕЦ КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ИнвентаризацияНЗП КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка В(&МассивОбъектов)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.СчетЗатрат КАК НомерСчета,
	|	Реквизиты.СчетЗатрат.Наименование КАК СчетНаименование,
	|	ЕСТЬNULL(Состав.НоменклатурнаяГруппа, ЕСТЬNULL(СоставПоПродукции.НоменклатурнаяГруппа, ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка))) КАК НоменклатурнаяГруппа,
	|	ЕСТЬNULL(Состав.Сумма, ЕСТЬNULL(СоставПоПродукции.Количество, 0)) КАК Стоимость,
	|	СоставПоПродукции.Продукция КАК Продукция,
	|	ЕСТЬNULL(Состав.НомерСтроки, ЕСТЬNULL(СоставПоПродукции.НомерСтроки, 0)) КАК НомерСтроки,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	Реквизиты.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНЗП.Состав КАК Состав
	|		ПО Реквизиты.Ссылка = Состав.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНЗП.СоставПоПродукции КАК СоставПоПродукции
	|		ПО Реквизиты.Ссылка = СоставПоПродукции.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.Председатель, ИСТИНА) КАК Председатель,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.НомерСтроки, 1) КАК НомерСтроки,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.ФизЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ФизЛицо
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНЗП.ИнвентаризационнаяКомиссия КАК ИнвентаризационнаяКомиссия
	|		ПО Реквизиты.Ссылка = ИнвентаризационнаяКомиссия.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер,
	|	Председатель УБЫВ,
	|	НомерСтроки";
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ВестиУчетЗатратПоПодразделениям = ПолучитьФункциональнуюОпцию("ВестиУчетЗатратПоПодразделениям");
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИндексПоследнегоРезультата = РезультатЗапроса.Количество() - 1;
	
	Док = РезультатЗапроса[ИндексПоследнегоРезультата - 1].Выбрать();
	ИнвентаризационнаяКомиссия = РезультатЗапроса[ИндексПоследнегоРезультата].Выбрать();
	
	ПервыйДокумент = Истина;
	
	КоллекцияИтоговСтраницы = НовыйИтогиПоказателей();

	Пока Док.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выведем шапку документа
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
		
		Если Шапка <> Неопределено Тогда
			Шапка.Параметры.Заполнить(Док);
		КонецЕсли;
	
		ПараметрыПечатнойФормы = Новый Структура;
		ПараметрыПечатнойФормы.Вставить("Организация",          ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации));
		ПараметрыПечатнойФормы.Вставить("ОрганизацияКодПоОКПО", СведенияОбОрганизации.КодПоОКПО);

		ПараметрыПечатнойФормы.Вставить("НомерДокумента",       ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.Номер, Истина, Ложь));
		ПараметрыПечатнойФормы.Вставить("ДатаДокумента",        Док.Дата);
		
		НаименованиеКолонки1 = "";
		НаименованиеКолонки2 = "";
		НаименованиеКолонки3 = "";
		ПараметрыСтроки = Новый Структура;
		
		ВидАктива =  НСтр("ru = 'НЕЗАВЕРШЕННОГО ПРОИЗВОДСТВА'");
		ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 г. проведена инвентаризация незавершенного производства.
			|При инвентаризации установлено следующее:'"), формат(Док.Дата, "ДФ=dd.MM.yyyy"));
		ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
			НСтр("ru = 'Лицо(а), ответственное(ые) за учет незавершенного производства:'"));
			
		Если ВестиУчетЗатратПоПодразделениям Тогда
			НаименованиеКолонки1 = НСтр("ru = 'Подразделение'");
			ПараметрыСтроки.Вставить("Колонка1", "Подразделение");
			НаименованиеКолонки2 = НСтр("ru = 'Номенклатурная группа'");
			ПараметрыСтроки.Вставить("Колонка2", "НоменклатурнаяГруппа");
			КоличествоКолонок = 2;
			Если Док.ВидОперации = Перечисления.ВидыОперацийИнвентаризацияНЗП.КоличественнаяОценка Тогда
				НаименованиеКолонки3 = НСтр("ru = 'Продукция'");
				ПараметрыСтроки.Вставить("Колонка3", "Продукция");
				КоличествоКолонок = 3;
			КонецЕсли;
		Иначе
			НаименованиеКолонки1 = НСтр("ru = 'Номенклатурная группа'");
			ПараметрыСтроки.Вставить("Колонка1", "НоменклатурнаяГруппа");
			КоличествоКолонок = 1;
			Если Док.ВидОперации = Перечисления.ВидыОперацийИнвентаризацияНЗП.КоличественнаяОценка Тогда
				НаименованиеКолонки2 = НСтр("ru = 'Продукция'");
				ПараметрыСтроки.Вставить("Колонка2", "Продукция");
				КоличествоКолонок = 2;
			КонецЕсли;
		КонецЕсли;
		ЗаголовокТаблицы = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ЗаголовокТаблицыКолонки" + КоличествоКолонок);
		ИтогоПоСтранице  = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ИтогоПоСтраницеКолонки" + КоличествоКолонок);
		СтрокаТаблицы    = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "СтрокаТаблицыКолонки" + КоличествоКолонок);
		
		Если Док.ВидОперации = Перечисления.ВидыОперацийИнвентаризацияНЗП.КоличественнаяОценка Тогда
			НаименованиеСуммы = НСтр("ru = 'Количество'");
		Иначе
			НаименованиеСуммы = НСтр("ru = 'Сумма, руб. коп.'");
		КонецЕсли;
			
		ПараметрыПечатнойФормы.Вставить("ВидАктива",     ВидАктива);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Шапка, ПараметрыПечатнойФормы);
		
		ДанныеМОЛ = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, Док.ОтветственноеЛицо, Док.Дата);
		ПараметрыПодписиОтветственноеЛицо = НовыйПараметрыПодписи();
		ПараметрыПодписиОтветственноеЛицо.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ОтветственноеЛицо;
		ПараметрыПодписиОтветственноеЛицо.Должность = ДанныеМОЛ.Должность;
		ПараметрыПодписиОтветственноеЛицо.РасшифровкаПодписи = ДанныеМОЛ.Представление;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
		ПараметрыПередТаблицей = Новый Структура;
		ПараметрыПередТаблицей.Вставить("ТекстКомиссиейУстановлено", ТекстКомиссиейУстановлено);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПередТаблицей, ПараметрыПередТаблицей);
		
		ПараметрыЗаголовокТаблицы = Новый Структура;
		ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки1", НаименованиеКолонки1);
		ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки2", НаименованиеКолонки2);
		ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки3", НаименованиеКолонки3);
		ПараметрыЗаголовокТаблицы.Вставить("НаименованиеСуммы", НаименованиеСуммы);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы, ПараметрыЗаголовокТаблицы);
		
		ОбластьПроверкиВывода = Новый Массив;
		Если СтрокаТаблицы <> Неопределено Тогда
			ОбластьПроверкиВывода.Добавить(СтрокаТаблицы);
		КонецЕсли;
		
		НомерСтроки = 0;
		КоллекцияИтоговСтраницы = НовыйИтогиПоказателей();
		КоллекцияИтоговДокумента = НовыйИтогиПоказателей();
		Пока Док.Следующий() Цикл
			НомерСтроки = НомерСтроки + 1;
			Если СтрокаТаблицы <> Неопределено Тогда
				СтрокаТаблицы.Параметры.Заполнить(Новый Структура("НомерСтроки", НомерСтроки));
			КонецЕсли;
			Для Каждого ПараметрСтрока Из ПараметрыСтроки Цикл
				СтрокаТаблицы.Параметры.Заполнить(Новый Структура(ПараметрСтрока.Ключ, Док[ПараметрСтрока.Значение]));
			КонецЦикла;
			
			Если ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ОбластьПроверкиВывода) Тогда
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, Док);
			Иначе
				ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы, ПараметрыЗаголовокТаблицы);
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, Док);
			КонецЕсли;
			
			КоллекцияИтоговСтраницы.Стоимость = КоллекцияИтоговСтраницы.Стоимость + Док.Стоимость;
			КоллекцияИтоговСтраницы.КоличествоСтрок = КоллекцияИтоговСтраницы.КоличествоСтрок + 1;
			
			КоллекцияИтоговДокумента.Стоимость = КоллекцияИтоговДокумента.Стоимость + Док.Стоимость;
			КоллекцияИтоговДокумента.КоличествоСтрок = КоллекцияИтоговДокумента.КоличествоСтрок + 1;
			
		КонецЦикла;
		
		ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ИтогоПоСтранице, КоллекцияИтоговСтраницы);
		
		ПараметрыПодписиПредседатель = НовыйПараметрыПодписи();
		ПараметрыПодписиПредседатель.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
		КоличествоЧленовКомиссииУнифицированнойФормы = 3;
		Если ИнвентаризационнаяКомиссия.СледующийПоЗначениюПоля("Ссылка") Тогда
			ИнвентаризационнаяКомиссия.Следующий();
			ДанныеПредседателя = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);

			ПараметрыПодписиПредседатель.Должность = ДанныеПредседателя.Должность;
			ПараметрыПодписиПредседатель.РасшифровкаПодписи = ДанныеПредседателя.Представление;
			
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПредседатель);
			
			Для НомерЧленаКомиссии = 1 По КоличествоЧленовКомиссииУнифицированнойФормы Цикл
				ПараметрыПодписи = НовыйПараметрыПодписи();
				Если НомерЧленаКомиссии = 1 Тогда
					ПараметрыПодписи.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ЧленыКомиссии;
				КонецЕсли;
				Если ИнвентаризационнаяКомиссия.Следующий() Тогда
					ДанныеЧленаКомиссии = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
					ПараметрыПодписи.Должность = ДанныеЧленаКомиссии.Должность;
					ПараметрыПодписи.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
				КонецЕсли;

				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			КонецЦикла;
			
			Пока ИнвентаризационнаяКомиссия.Следующий() Цикл
				ДанныеЧленаКомиссии = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
				ПараметрыПодписи.Должность = ДанныеЧленаКомиссии.Должность;
				ПараметрыПодписи.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			КонецЦикла;
				
		Иначе
			
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПредседатель);
			
			ПараметрыПодписи = НовыйПараметрыПодписи();
			ПараметрыПодписи.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ЧленыКомиссии;
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			Для НомерЧленаКомиссии = 2 По КоличествоЧленовКомиссииУнифицированнойФормы Цикл
				ПараметрыПодписи = НовыйПараметрыПодписи();
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			КонецЦикла;
		КонецЕсли;
		
		ПараметрыПретензии = Новый Структура("НомерСтрокиНачальный, НомерСтрокиКонечный, ВидАктива");
		Если НомерСтроки > 0 Тогда
			ПараметрыПретензии.НомерСтрокиНачальный = 1;
			ПараметрыПретензии.НомерСтрокиКонечный  = НомерСтроки;
		Иначе
			ПараметрыПретензии.НомерСтрокиНачальный = "______";
			ПараметрыПретензии.НомерСтрокиКонечный  = "______";
		КонецЕсли;
		ПараметрыПретензии.ВидАктива = НРег(ВидАктива);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Претензии, ПараметрыПретензии);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиОтветственноеЛицо);
			
		ПараметрыПодписи = НовыйПараметрыПодписи();
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПолеДаты, Док);
		
		ПараметрыПодписиПроверяющий = НовыйПараметрыПодписи();
		ПараметрыПодписиПроверяющий.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.Проверяющий;
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПроверяющий);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПолеДаты, Док);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета)
	КоллекцияИтоговСтраницы.КоличествоПорядковыхНомеровПрописью = ЧислоПрописью(КоллекцияИтоговСтраницы.КоличествоСтрок,, ",,,м,,,,,0");
	КоллекцияИтоговСтраницы.СтоимостьПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(
		КоллекцияИтоговСтраницы.Стоимость,
		ВалютаРегламентированногоУчета);
КонецПроцедуры
	
Функция НовыйИтогиПоказателей()

	ИтогиПоказателей = Новый Структура;
	ИтогиПоказателей.Вставить("Стоимость", 0);
	ИтогиПоказателей.Вставить("КоличествоСтрок", 0);
	ИтогиПоказателей.Вставить("КоличествоПорядковыхНомеровПрописью", "");
	ИтогиПоказателей.Вставить("СтоимостьПрописью", "");
	Возврат ИтогиПоказателей;
КонецФункции

Функция НовыйПараметрыПодписи()

	ПараметрыПодписи = Новый Структура;
	ПараметрыПодписи.Вставить("ЗаголовокРазделаПодписей");
	ПараметрыПодписи.Вставить("Должность");
	ПараметрыПодписи.Вставить("РасшифровкаПодписи");
	Возврат ПараметрыПодписи;
	
КонецФункции

#КонецОбласти

#КонецЕсли