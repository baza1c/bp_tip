
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу остатков по объектам учета по переданному списку счетов.
//
// Параметры:
//  Параметры - Структура:
//    Дата - Дата
//    Организация - СправочникСсылка.Организации
//    ОтветственноеЛицо - СправочникСсылка.ФизическиеЛица
//    Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//    РазделУчета - ПеречислениеСсылка.РазделыУчетаДляИнвентаризации
//    СчетаУчета - Массив
// 
// Возвращаемое значение:
//  Структура:
//    ОбъектыУчета - ТаблицаЗначений
//
Функция ДанныеПоУчету(Параметры) Экспорт
	
	РезультатЗаполнения = Новый Структура;
	РезультатЗаполнения.Вставить("ОбъектыУчета", Новый ТаблицаЗначений);
	
	Если Параметры.СчетаУчета.Количество() = 0 Тогда
		Возврат РезультатЗаполнения; 
	КонецЕсли;
	
	УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловиеОтбора.ИспользоватьВПроводках = Истина;
	Если Параметры.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения Тогда
		СчетаДляИнвентаризации = ИнвентаризацияАктивовОбязательств.СчетаДляИнвентаризации(Параметры.РазделУчета, Параметры.Дата, Параметры.Организация);
		УсловиеОтбора.СчетаИсключения = СчетаДляИнвентаризации.ИсключаемыеСчета;
	КонецЕсли;
	СчетаУчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(Параметры.СчетаУчета, УсловиеОтбора);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК СчетУчета,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет.Вид <> ЗНАЧЕНИЕ(ВидСчета.Активный)
	|				И ХозрасчетныйОстатки.СуммаОстаток < 0
	|			ТОГДА -ХозрасчетныйОстатки.СуммаОстаток
	|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстаток
	|	КОНЕЦ КАК Сумма,
	|	ХозрасчетныйОстатки.Валюта КАК Валюта,
	|	ХозрасчетныйОстатки.ВалютнаяСуммаОстаток КАК ВалютнаяСумма,
	|	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто1) = ТИП(Справочник.ФизическиеЛица)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто2) = ТИП(Справочник.ФизическиеЛица)
	|			ТОГДА ХозрасчетныйОстатки.Субконто2
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто3) = ТИП(Справочник.ФизическиеЛица)
	|			ТОГДА ХозрасчетныйОстатки.Субконто3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоМОЛ
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата, Счет В (&СчетаУчета), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.СчетУчета КАК СчетУчета,
	|	Остатки.Субконто1 КАК Субконто1,
	|	Остатки.Субконто2 КАК Субконто2,
	|	Остатки.Субконто3 КАК Субконто3,
	|	Остатки.Количество КАК Количество,
	|	Остатки.Сумма КАК Сумма,
	|	Остатки.Валюта КАК Валюта,
	|	Остатки.ВалютнаяСумма КАК ВалютнаяСумма,
	|	Остатки.Подразделение КАК Подразделение,
	|	Остатки.СубконтоМОЛ КАК СубконтоМОЛ
	|ИЗ
	|	Остатки КАК Остатки
	|ГДЕ
	|	&УсловиеПодразделение
	|	И &УсловиеОтветственноеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетУчета,
	|	Подразделение,
	|	Субконто1,
	|	Субконто2,
	|	Субконто3
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Дата", КонецМесяца(Параметры.Дата)+1);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
	Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПодразделение", "Подразделение = &Подразделение");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПодразделение", "Истина");
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ОтветственноеЛицо) Тогда
		Запрос.УстановитьПараметр("ОтветственноеЛицо", Параметры.ОтветственноеЛицо);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтветственноеЛицо", "(СубконтоМОЛ = &ОтветственноеЛицо Или СубконтоМОЛ = НЕОПРЕДЕЛЕНО)");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтветственноеЛицо", "Истина");
	КонецЕсли;
	
	РезультатЗаполнения.ОбъектыУчета = Запрос.Выполнить().Выгрузить();
	Возврат РезультатЗаполнения;
	
КонецФункции

// Возвращает описание выбранных счетов документа, участвующих в инвентаризации.
//
// Параметры:
//   СчетаУчета - ТабличнаяЧасть
//
// Возвращаемое значение:
//   Строка
//
Функция ПереченьСчетовДокумента(СчетаУчета) Экспорт
	
	СчетаУчаствующиеВРасчетах = Новый Массив;
	
	Для Каждого ТекущийСчет Из СчетаУчета Цикл
		
		Если ТекущийСчет.УчаствуетВРасчетах Тогда
			СчетаУчаствующиеВРасчетах.Добавить(ТекущийСчет.СчетУчета);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтрСоединить(СчетаУчаствующиеВРасчетах, ", ");
	
КонецФункции

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ВыбранныйРаздел = Неопределено;
	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВыбранныйРаздел = Параметры.Ключ.РазделУчета;
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") Тогда
		Параметры.ЗначенияЗаполнения.Свойство("РазделУчета", ВыбранныйРаздел);
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ВыбранныйРаздел = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Параметры.ЗначениеКопирования, "РазделУчета");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ВыбранныйРаздел) Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "ФормаДокумента";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

#Область СтандартныеПодсистемы_УправлениеДоступом

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СтандартныеПодсистемы_Свойства

// СтандартныеПодисистемы.Свойства
// 
// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Документ_ИнвентаризацияОбъектовУчета";
	Набор.Идентификатор = Новый УникальныйИдентификатор("ade6b723-2a8b-417b-9dad-b6d804ea66be");
	Набор.Используется  = Истина;
	
КонецПроцедуры

// Конец СтандартныеПодисистемы.Свойства

#КонецОбласти

#Область СтандартныеПодсистемы_Печать

// СтандартныеПодсистемы.Печать
// 
// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт инвентаризации
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ";
	КомандаПечати.Представление = НСтр("ru = 'Акт инвентаризации'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Приказ о проведении инвентаризации (ИНВ-22)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ22";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о проведении инвентаризации (ИНВ-22)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Инвентаризация объектов учета""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета ИНВ формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ", НСтр("ru = 'Акт инвентаризации'"), 
			ПечатьИНВ(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "ОбщийМакет.ПФ_MXL_ИНВ");
		
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ22") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ22",НСтр("ru = 'ИНВ-22 (Приказ о проведении инвентаризации)'"), 
			ИнвентаризацияАктивовОбязательств.ПечатьИНВ22(МассивОбъектов, ОбъектыПечати), , "ОбщийМакет.ПФ_MXL_ИНВ22");
		
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СтандартныеПодсистемы_ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// @skip-check module-empty-method
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьИНВ(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияОбъектовУчета_ИНВ";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ИНВ");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснованиеДата = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Реквизиты.Дата
	|		ИНАЧЕ Реквизиты.ДокументОснованиеДата
	|	КОНЕЦ КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Реквизиты.РазделУчета КАК РазделУчета,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	ЕСТЬNULL(ОбъектыУчета.Субконто1, Неопределено) КАК Субконто1,
	|	ЕСТЬNULL(ОбъектыУчета.Субконто2, Неопределено) КАК Субконто2,
	|	ЕСТЬNULL(ОбъектыУчета.Субконто3, Неопределено) КАК Субконто3,
	|	ЕСТЬNULL(ОбъектыУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК НомерСчета,
	|	ЕСТЬNULL(ОбъектыУчета.СчетУчета.Наименование, """") КАК СчетНаименование,
	|	ЕСТЬNULL(ОбъектыУчета.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(ОбъектыУчета.Сумма, 0) КАК Стоимость,
	|	ЕСТЬNULL(ОбъектыУчета.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ОбъектыУчета.Валюта, 0) КАК Валюта,
	|	ЕСТЬNULL(ОбъектыУчета.ВалютнаяСумма, 0) КАК ВалютнаяСумма,
	|	ЕСТЬNULL(ОбъектыУчета.НомерСтроки, 0) КАК НомерСтроки,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	Реквизиты.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	Реквизиты.РазделУчета КАК РазделУчета
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ОбъектыУчета
	|		ПО Реквизиты.Ссылка = ОбъектыУчета.Ссылка
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	ОбъектыУчета.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ОбъектыУчета.СчетУчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ОбъектыУчета.СчетУчета
	|		КОГДА ОбъектыУчета.СчетУчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ОбъектыУчета.СчетУчета.Родитель
	|		ИНАЧЕ ОбъектыУчета.СчетУчета.Родитель.Родитель
	|	КОНЕЦ КАК СчетУчетаРодитель,
	|	Реквизиты.РазделУчета КАК РазделУчета,
	|	ОбъектыУчета.СчетУчета.Наименование КАК СчетУчетаНаименование,
	|	ОбъектыУчета.СчетУчета.Код КАК СчетУчетаКод,
	|	ОбъектыУчета.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ СчетаУчета
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ОбъектыУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО ОбъектыУчета.Ссылка = Реквизиты.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчета.Ссылка КАК Ссылка,
	|	СчетаУчета.Дата КАК Дата,
	|	СчетаУчета.СчетУчета КАК СчетУчета,
	|	СчетаУчета.СчетУчетаРодитель КАК СчетУчетаРодитель,
	|	СчетаУчета.РазделУчета КАК РазделУчета,
	|	СчетаУчета.СчетУчетаНаименование КАК СчетУчетаНаименование,
	|	СчетаУчета.СчетУчетаКод КАК СчетУчетаКод,
	|	МИНИМУМ(СчетаУчета.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	СчетаУчета КАК СчетаУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаУчета.Ссылка,
	|	СчетаУчета.СчетУчетаНаименование,
	|	СчетаУчета.Дата,
	|	СчетаУчета.СчетУчетаКод,
	|	СчетаУчета.РазделУчета,
	|	СчетаУчета.СчетУчетаРодитель,
	|	СчетаУчета.СчетУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчета.Ссылка КАК Ссылка,
	|	СчетаУчета.Дата КАК Дата,
	|	МИНИМУМ(СчетаУчета.СчетУчета) КАК СчетУчета,
	|	СчетаУчета.СчетУчетаРодитель КАК СчетУчетаРодитель,
	|	СчетаУчета.РазделУчета КАК РазделУчета,
	|	МИНИМУМ(СчетаУчета.СчетУчетаНаименование) КАК СчетУчетаНаименование,
	|	МИНИМУМ(СчетаУчета.СчетУчетаКод) КАК СчетУчетаКод,
	|	МИНИМУМ(СчетаУчета.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	СчетаУчета КАК СчетаУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаУчета.Ссылка,
	|	СчетаУчета.Дата,
	|	СчетаУчета.СчетУчетаРодитель,
	|	СчетаУчета.РазделУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Ссылка КАК Ссылка,
	|	Данные.Дата КАК Дата,
	|	Данные.Номер КАК Номер,
	|	Данные.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Данные.Организация.КодОКВЭД2 = """"
	|			ТОГДА Данные.Организация.КодОКВЭД
	|		ИНАЧЕ Данные.Организация.КодОКВЭД2
	|	КОНЕЦ КАК ВидДеятельности,
	|	Данные.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА Данные.НомерСчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА Данные.НомерСчета
	|		КОГДА Данные.НомерСчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА Данные.НомерСчета.Родитель
	|		ИНАЧЕ Данные.НомерСчета.Родитель.Родитель
	|	КОНЕЦ КАК СчетУчетаРодитель,
	|	Данные.НомерСчета КАК НомерСчета,
	|	Данные.НомерСчета.Наименование КАК НомерСчетаНаименование,
	|	Данные.Подразделение КАК Подразделение,
	|	Данные.Субконто1 КАК Субконто1,
	|	Данные.Субконто2 КАК Субконто2,
	|	Данные.Субконто3 КАК Субконто3,
	|	Данные.Стоимость КАК Стоимость,
	|	ВЫБОР
	|		КОГДА Данные.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ Данные.Количество
	|	КОНЕЦ КАК Количество,
	|	Данные.Валюта КАК Валюта,
	|	Данные.ВалютнаяСумма КАК ВалютнаяСумма,
	|	Данные.НомерСтроки КАК НомерСтроки,
	|	Данные.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Данные.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	Данные.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Данные.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Данные.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Данные.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Данные.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	Данные.РазделУчета КАК РазделУчета,
	|	ВЫБОР
	|		КОГДА Данные.Субконто1 ССЫЛКА Справочник.БанковскиеСчета
	|			ТОГДА Данные.Субконто1.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР
	|		КОГДА Данные.Субконто1 ССЫЛКА Справочник.БанковскиеСчета
	|			ТОГДА Данные.Субконто1.НомерСчета
	|	КОНЕЦ КАК НомерБанковскогоСчета
	|ИЗ
	|	Данные КАК Данные
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.Председатель, ИСТИНА) КАК Председатель,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.НомерСтроки, 1) КАК НомерСтроки,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.ФизЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ФизЛицо
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета.ИнвентаризационнаяКомиссия КАК ИнвентаризационнаяКомиссия
	|		ПО Реквизиты.Ссылка = ИнвентаризационнаяКомиссия.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер,
	|	Председатель УБЫВ,
	|	НомерСтроки";

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИндексПоследнегоРезультата = РезультатЗапроса.Количество() - 1;
	
	СчетаУчетаТаблица = РезультатЗапроса[ИндексПоследнегоРезультата - 3].Выгрузить();
	СчетаУчетаРодителиТаблица = РезультатЗапроса[ИндексПоследнегоРезультата - 2].Выгрузить();
	Док = РезультатЗапроса[ИндексПоследнегоРезультата - 1].Выбрать();
	ИнвентаризационнаяКомиссия = РезультатЗапроса[ИндексПоследнегоРезультата].Выбрать();
	
	ПервыйДокумент = Истина;
	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru = 'Председатель комиссии'"));
	ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru = 'Члены комиссии:'"));
	КоличествоЧленовКомиссииУнифицированнойФормы = 3;
	
	Пока Док.СледующийПоЗначениюПоля("Ссылка") Цикл
		Комиссия = Новый Структура();
		Комиссия.Вставить("ПараметрыПодписиПредседатель", Новый Массив);
		Комиссия.Вставить("ПараметрыПодписи", Новый Массив);
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		СчетаУчета = СчетаУчетаТаблица.НайтиСтроки(Новый Структура("Ссылка", Док.Ссылка));
		СчетаУчетаРодители = СчетаУчетаРодителиТаблица.НайтиСтроки(Новый Структура("Ссылка", Док.Ссылка));
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ПараметрыЗаголовкаТаблицыИСтроки = НовыйПараметрыЗаголовкаТаблицыИСтроки();
		ПервыйСчет = Истина;
		
		Если ИнвентаризационнаяКомиссия.СледующийПоЗначениюПоля("Ссылка") Тогда
			ИнвентаризационнаяКомиссия.Следующий();
			ПараметрыПодписиПредседатель = НовыйПараметрыПодписи();
			ПараметрыПодписиДок = Новый Массив;
			ПараметрыПодписиПредседатель.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
			ДанныеПредседателя = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
			
			ПараметрыПодписиПредседатель.Должность = ДанныеПредседателя.Должность;
			ПараметрыПодписиПредседатель.РасшифровкаПодписи = ДанныеПредседателя.Представление;

			Для НомерЧленаКомиссии = 1 По КоличествоЧленовКомиссииУнифицированнойФормы Цикл
				ПараметрыПодписи = НовыйПараметрыПодписи();
				Если НомерЧленаКомиссии = 1 Тогда
					ПараметрыПодписи.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ЧленыКомиссии;
				КонецЕсли;
				Если ИнвентаризационнаяКомиссия.Следующий() Тогда
					ДанныеЧленаКомиссии = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
					ПараметрыПодписи.Должность = ДанныеЧленаКомиссии.Должность;
					ПараметрыПодписи.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
				КонецЕсли;
				ПараметрыПодписиДок.Добавить(ПараметрыПодписи);
			КонецЦикла;
			
			Пока ИнвентаризационнаяКомиссия.Следующий() Цикл
				ДанныеЧленаКомиссии = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
				ПараметрыПодписи = НовыйПараметрыПодписи();
				ПараметрыПодписи.Должность = ДанныеЧленаКомиссии.Должность;
				ПараметрыПодписи.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
				ПараметрыПодписиДок.Добавить(ПараметрыПодписи);
			КонецЦикла;
			Комиссия.ПараметрыПодписиПредседатель = ПараметрыПодписиПредседатель;
			Комиссия.ПараметрыПодписи = ПараметрыПодписиДок;
		КонецЕсли;
		
		Если СчетаУчета.Количество() = 0 Тогда
			// Печать для пустой ТЧ ОбъектыУчета
			ПечатьПоСчетуУчета(Док, Комиссия, ТабличныйДокумент, Макет, ПараметрыЗаголовкаТаблицыИСтроки);
		Иначе
			
			// Если несколько разноплановых счетов в одном документе, то отдельные печатные формы на каждый такой счет.
			Если Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета
				Или Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
				
				Для Каждого СтрокаСчетаУчетаРодители Из СчетаУчетаРодители Цикл
					Если Не ПервыйСчет Тогда
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					КонецЕсли;
					ПервыйСчет = Ложь;
					СчетаУчетаТек = СчетаУчетаТаблица.НайтиСтроки(Новый Структура("Ссылка, СчетУчетаРодитель", Док.Ссылка, СтрокаСчетаУчетаРодители.СчетУчетаРодитель));
					ЗаполнитьПараметрыЗаголовкаТаблицыИСтроки(СчетаУчетаТек, ПараметрыЗаголовкаТаблицыИСтроки, Док.РазделУчета);
					Если Док.СледующийПоЗначениюПоля("СчетУчетаРодитель") Тогда
						ПечатьПоСчетуУчета(Док, Комиссия, ТабличныйДокумент, Макет, ПараметрыЗаголовкаТаблицыИСтроки, СтрокаСчетаУчетаРодители);
					КонецЕсли;
				КонецЦикла;
				
			Иначе
				
				ЗаполнитьПараметрыЗаголовкаТаблицыИСтроки(СчетаУчета, ПараметрыЗаголовкаТаблицыИСтроки, Док.РазделУчета);
				Если ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыСтрокиТаблица.Количество() > 0 Тогда
					ПечатьПоСчетуУчета(Док, Комиссия, ТабличныйДокумент, Макет, ПараметрыЗаголовкаТаблицыИСтроки);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ПечатьПоСчетуУчета(Док, Комиссия, ТабличныйДокумент, Макет, ПараметрыЗаголовкаТаблицыИСтроки, СчетаУчета = Неопределено)
	
	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
		НСтр("ru = 'Материально ответственное(ые) лицо(а):'"));
	ЗаголовокРазделаПодписей.Вставить("Проверяющий",
		НСтр("ru = 'Указанные в настоящем акте данные и расчеты проверил'"));
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	// Получаем области макета для вывода в табличный документ
	Шапка            = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Шапка");
	Расписка         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Расписка");
	Подпись          = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Подпись");
	ПолеДаты         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ПолеДаты");
	ПередТаблицей    = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ПередТаблицей");
	Подсчеты         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Подсчеты");
	Претензии        = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Претензии");
	
	// Выведем шапку документа
	СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
	
	Если Шапка <> Неопределено Тогда
		Шапка.Параметры.Заполнить(Док);
	КонецЕсли;
	
	ПараметрыПечатнойФормы = Новый Структура;
	ПараметрыПечатнойФормы.Вставить("Организация",          ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации));
	ПараметрыПечатнойФормы.Вставить("ОрганизацияКодПоОКПО", СведенияОбОрганизации.КодПоОКПО);
	
	ПараметрыПечатнойФормы.Вставить("НомерДокумента",       ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.Номер, Истина, Ложь));
	ПараметрыПечатнойФормы.Вставить("ДатаДокумента",        Док.Дата);
	
	ПараметрыСтрокиТаблица = ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыСтрокиТаблица;
	ПараметрыЗаголовокТаблицы = ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыЗаголовокТаблицы;
	КоличествоКолонок = ПараметрыЗаголовкаТаблицыИСтроки.КоличествоКолонок;
	
	ВидАктива = ВРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(Док.РазделУчета, 2));
	ЗаголовокРазделаПодписей.ОтветственноеЛицо = СтрШаблон(НСтр("ru = 'Лицо(а), ответственное(ые) за учет %1:'"), НРег(ВидАктива));
	// Для некоторых разделов расписки может и не быть
	ТекстРасписки = Неопределено;
	ФорматДаты = "ДФ=dd.MM.yyyy";
	Если ПараметрыСтрокиТаблица.Количество() = 0 Тогда
		ВидАктиваДательныйПадеж = НРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(Док.РазделУчета, 3));
		ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'При инвентаризации по состоянию на %1 г. комиссия не выявила наличие остатков по %2.'"),
			формат(Док.Дата, ФорматДаты), ВидАктиваДательныйПадеж);
	Иначе
		КоличествоКолонок = ?(КоличествоКолонок = 0, 1, КоличествоКолонок);
		ЗаголовокТаблицы = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, СтрШаблон("ЗаголовокТаблицыКолонки%1", КоличествоКолонок));
		ИтогоПоСтранице = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, СтрШаблон("ИтогоПоСтраницеКолонки%1", КоличествоКолонок));
		СтрокаТаблицы = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, СтрШаблон("СтрокаТаблицыКолонки%1", КоличествоКолонок));
		
		ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 г. проведена инвентаризация %2.
			|При инвентаризации установлено следующее:'"), формат(Док.Дата, ФорматДаты), НРег(ВидАктива));
		Если Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения Тогда
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по капитальным вложениям получены бухгалтерией и введены в учетную систему.'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям Тогда
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 г. проведена инвентаризация по счету 19 НДС по приобретенным ценностям.
				|При инвентаризации установлено следующее:'"), формат(Док.Дата, ФорматДаты));
			ЗаголовокРазделаПодписей.ОтветственноеЛицо = НСтр("ru = 'Лицо(а), ответственное(ые) за учет НДС по приобретенным ценностям:'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета Тогда
			ТекстРасписки = НСтр("ru = 'все банковские выписки получены бухгалтерией и введены в учетную систему.'");
			ВидАктива =  НСтр("ru = 'БЕЗНАЛИЧНЫХ ДЕНЕЖНЫХ СРЕДСТВ НА РАСЧЕТНОМ СЧЕТЕ'");
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'При инвентаризации по состоянию на %1 г. комиссией установлено:'"), формат(Док.Дата, ФорматДаты));
			ПодсчетыДопТекст = НСтр("ru = 'Остатки денежных средств на расчетных счетах по данным бухгалтерского учета соответствуют остаткам, указанным в банковских выписках.'");
			ЗаголовокРазделаПодписей.ОтветственноеЛицо = НСтр("ru = 'Лицо(а), ответственное(ые) за ведение расчетного счета:'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения Тогда
			ЕстьКоличество = ПараметрыСтрокиТаблица.Найти("Количество") <> Неопределено;
			// Добавим колонку с количеством, если по другим счетам раздела такая колонка есть.
			Если ЕстьКоличество Тогда
				н = 5; // максимальное количество колонок у таблицы в печатной форме = 5
				НомерКолонки = 0;
				Пока н > 0 Цикл
					СтрокаПараметрыСтрокиКоличество = ПараметрыСтрокиТаблица.Найти("Количество", "Колонка" + н);
					Если СтрокаПараметрыСтрокиКоличество <> Неопределено Тогда
						НомерКолонки = н;
						Прервать;
					КонецЕсли;
					н = н - 1;
				КонецЦикла;
				Для Каждого СтрокаПараметрыСтроки Из ПараметрыСтрокиТаблица Цикл
					СтрокаПараметрыСтроки["Колонка" + НомерКолонки] = "Количество";
					// Очистим предыдущее значение колонки с количеством
					н = НомерКолонки - 1;
					Пока н > 0 Цикл
						Если СтрокаПараметрыСтроки["Колонка" + н] = "Количество" Тогда
							СтрокаПараметрыСтроки["Колонка" + н] = "";
							Прервать;
						КонецЕсли;
						н = н - 1;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			ТекстРасписки = 
				НСтр("ru = 'все приходные и расходные документы по финансовым вложениям получены бухгалтерией и введены в учетную систему.'");
		ИначеЕсли Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета
			Или Док.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
			Счет = СчетаУчета.СчетУчетаРодитель;
			Если СчетаУчета.СчетУчета = ПланыСчетов.Хозрасчетный.ВыполнениеРемонтовОС
				Или СчетаУчета.СчетУчета = ПланыСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости Тогда
				Счет = СчетаУчета.СчетУчета;
			КонецЕсли;
			РеквизитыСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Счет, "Код, Наименование");
			ВидАктива = ВРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(РеквизитыСчета.Наименование, 2));
			ТекстКомиссиейУстановлено = СтрШаблон(НСтр("ru = 'Акт составлен комиссией о том, что по состоянию на %1 г. проведена инвентаризация по счету %2 %3.
				|При инвентаризации установлено следующее:'"), формат(Док.Дата, ФорматДаты), РеквизитыСчета.Код, РеквизитыСчета.Наименование);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПечатнойФормы.Вставить("ТекстРасписки", ТекстРасписки);
	ПараметрыПечатнойФормы.Вставить("ВидАктива",     ВидАктива);
	
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Шапка, ПараметрыПечатнойФормы);
	
	ДанныеМОЛ = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, Док.ОтветственноеЛицо, Док.Дата);
	ПараметрыПодписиОтветственноеЛицо = НовыйПараметрыПодписи();
	ПараметрыПодписиОтветственноеЛицо.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ОтветственноеЛицо;
	ПараметрыПодписиОтветственноеЛицо.Должность = ДанныеМОЛ.Должность;
	ПараметрыПодписиОтветственноеЛицо.РасшифровкаПодписи = ДанныеМОЛ.Представление;
	Если ЗначениеЗаполнено(ТекстРасписки) Тогда
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Расписка, ПараметрыПечатнойФормы);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиОтветственноеЛицо);
		
		ПараметрыПодписи = НовыйПараметрыПодписи();
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
	КонецЕсли;
	
	ПараметрыПередТаблицей = Новый Структура;
	ПараметрыПередТаблицей.Вставить("ТекстКомиссиейУстановлено", ТекстКомиссиейУстановлено);
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПередТаблицей, ПараметрыПередТаблицей);
	
	Если ПараметрыСтрокиТаблица.Количество() > 0 Тогда
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы, ПараметрыЗаголовокТаблицы);
		
		ОбластьПроверкиВывода = Новый Массив;
		Если СтрокаТаблицы <> Неопределено Тогда
			ОбластьПроверкиВывода.Добавить(СтрокаТаблицы);
		КонецЕсли;
		
		НомерСтроки = 0;
		КоллекцияИтоговСтраницы = НовыйИтогиПоказателей();
		КоллекцияИтоговДокумента = НовыйИтогиПоказателей();
		Пока Док.Следующий() Цикл
			НомерСтроки = НомерСтроки + 1;
			ПараметрыСтрокиПоСчету = ПараметрыСтрокиТаблица.Найти(Док.НомерСчета, "СчетУчета");
			ПараметрыСтроки = Новый Структура;
			Если ПараметрыСтрокиПоСчету <> Неопределено Тогда
				ПараметрыСтроки = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ПараметрыСтрокиПоСчету);
			КонецЕсли;
			Если СтрокаТаблицы <> Неопределено Тогда
				СтрокаТаблицы.Параметры.Заполнить(Новый Структура("НомерСтроки", НомерСтроки));
				ИмяКолонкиВалюта = "";
				Для Каждого ПараметрСтрока Из ПараметрыСтроки Цикл
					Если ПараметрСтрока.Ключ = "СчетУчета" Тогда
						Продолжить;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(ПараметрСтрока.Значение) Тогда
						СтрокаТаблицы.Параметры.Заполнить(Новый Структура(ПараметрСтрока.Ключ, ""));
					Иначе
						СтрокаТаблицы.Параметры.Заполнить(Новый Структура(ПараметрСтрока.Ключ, Док[ПараметрСтрока.Значение]));
						Если ПараметрСтрока.Значение = "Валюта" Тогда
							ИмяКолонкиВалюта = ПараметрСтрока.Ключ;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Если ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ОбластьПроверкиВывода) Тогда
				Если ЗначениеЗаполнено(ИмяКолонкиВалюта) Тогда
					ОбластьЯчеек = СтрокаТаблицы.НайтиТекст(ИмяКолонкиВалюта);
					Если ОбластьЯчеек <> Неопределено Тогда
						ОбластьЯчеек.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
					КонецЕсли
				КонецЕсли;
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, Док);
			Иначе
				ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы, ПараметрыЗаголовокТаблицы);
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, Док);
			КонецЕсли;
			
			КоллекцияИтоговСтраницы.Стоимость = КоллекцияИтоговСтраницы.Стоимость + Док.Стоимость;
			КоллекцияИтоговСтраницы.КоличествоСтрок = КоллекцияИтоговСтраницы.КоличествоСтрок + 1;
			
			КоллекцияИтоговДокумента.Стоимость = КоллекцияИтоговДокумента.Стоимость + Док.Стоимость;
			КоллекцияИтоговДокумента.КоличествоСтрок = КоллекцияИтоговДокумента.КоличествоСтрок + 1;
			
		КонецЦикла;
		
		ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ИтогоПоСтранице, КоллекцияИтоговСтраницы);
		
		ПараметрыПодсчеты = Новый Структура;
		ПараметрыПодсчеты.Вставить("ПодсчетыДопТекст", ПодсчетыДопТекст);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подсчеты, ПараметрыПодсчеты);
		
	КонецЕсли;
	
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, Комиссия.ПараметрыПодписиПредседатель);
	Для Каждого СтрокаПараметрыПодписи Из Комиссия.ПараметрыПодписи Цикл
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, СтрокаПараметрыПодписи);
	КонецЦикла;
	
	Если ПараметрыСтрокиТаблица.Количество() > 0 Тогда
		
		ПараметрыПретензии = Новый Структура("НомерСтрокиНачальный, НомерСтрокиКонечный, ВидАктива");
		Если НомерСтроки > 0 Тогда
			ПараметрыПретензии.НомерСтрокиНачальный = 1;
			ПараметрыПретензии.НомерСтрокиКонечный  = НомерСтроки;
		Иначе
			ПараметрыПретензии.НомерСтрокиНачальный = "______";
			ПараметрыПретензии.НомерСтрокиКонечный  = "______";
		КонецЕсли;
		ПараметрыПретензии.ВидАктива = НРег(ВидАктива);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Претензии, ПараметрыПретензии);
	КонецЕсли;
		
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиОтветственноеЛицо);
	
	ПараметрыПодписи = НовыйПараметрыПодписи();
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
	
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПолеДаты, Док);
	
	ПараметрыПодписиПроверяющий = НовыйПараметрыПодписи();
	ПараметрыПодписиПроверяющий.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.Проверяющий;
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПроверяющий);
	
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПолеДаты, Док);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЗаголовкаТаблицыИСтроки(СчетаУчета, ПараметрыЗаголовкаТаблицыИСтроки, РазделУчета)
	
	ПараметрыСтрокиТаблица = ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыСтрокиТаблица;
	ПараметрыЗаголовокТаблицы = ПараметрыЗаголовкаТаблицыИСтроки.ПараметрыЗаголовокТаблицы;

	ОсновнойСчет = ОсновнойСчетРаздела(СчетаУчета, РазделУчета); // счет с максимальным количеством колонок
	
	Для Каждого СтрокаСчетаУчета Из СчетаУчета Цикл
		
		НоваяСтрокаПараметрыСтроки = ПараметрыСтрокиТаблица.Добавить();
		НоваяСтрокаПараметрыСтроки.СчетУчета = СтрокаСчетаУчета.СчетУчета;
		
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаСчетаУчета.СчетУчета);
		НомерСубконтоЦенныеБумаги = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ЦенныеБумаги);
		НомерСубконтоБанковскиеСчета = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета);
		ЭтоОсновнойСчет = СтрокаСчетаУчета.СчетУчета = ОсновнойСчет;
		КоличествоСубконто = СвойстваСчета.КоличествоСубконто;
		СубконтоНаСчете = Новый ТаблицаЗначений;
		СубконтоНаСчете.Колонки.Добавить("Субконто");
		СубконтоНаСчете.Колонки.Добавить("ВидСубконто");
		Для н = 1 По СвойстваСчета.КоличествоСубконто Цикл
			Если Не СвойстваСчета["ВидСубконто" + н + "ТолькоОбороты"] Тогда
				НоваяСтрокаСубконто = СубконтоНаСчете.Добавить();
				НоваяСтрокаСубконто.Субконто = "Субконто"+ н;
				НоваяСтрокаСубконто.ВидСубконто = "ВидСубконто" + н + "Наименование";
			Иначе
				КоличествоСубконто = КоличествоСубконто - 1;
			КонецЕсли;
		КонецЦикла;
		КоличествоКолонок = КоличествоКолонок(СвойстваСчета, КоличествоСубконто);
		Если ЭтоОсновнойСчет Тогда
			ПараметрыЗаголовкаТаблицыИСтроки.КоличествоКолонок = КоличествоКолонок;
		КонецЕсли;
		Если КоличествоКолонок = 0 Тогда
			Если ЭтоОсновнойСчет Тогда
				ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки1", СтрокаСчетаУчета.СчетУчетаНаименование);
			КонецЕсли;
		Иначе
			НомерСубконто = 0;
			НомерКолонки = 1;
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				НаименованиеКолонки = НСтр("ru = 'Подразделение'");
				ИмяПоля = "Подразделение";
				ЗаполнитьКолонку(НоваяСтрокаПараметрыСтроки, ПараметрыЗаголовокТаблицы, НомерКолонки, НаименованиеКолонки, ИмяПоля, ЭтоОсновнойСчет);
			КонецЕсли;
			Пока НомерКолонки <= КоличествоКолонок И НомерСубконто < КоличествоСубконто Цикл
				НаименованиеКолонки = СвойстваСчета[СубконтоНаСчете[НомерСубконто].ВидСубконто];
				ИмяПоля = СубконтоНаСчете[НомерСубконто].Субконто;
				ЗаполнитьКолонку(НоваяСтрокаПараметрыСтроки, ПараметрыЗаголовокТаблицы, НомерКолонки, НаименованиеКолонки, ИмяПоля, ЭтоОсновнойСчет);
				НомерСубконто = НомерСубконто + 1;
			КонецЦикла;
			Если НомерКолонки <= КоличествоКолонок Тогда
				Если СвойстваСчета.Количественный Тогда
					НаименованиеКолонки = НСтр("ru = 'Количество'");
					ИмяПоля = "Количество";
					ЗаполнитьКолонку(НоваяСтрокаПараметрыСтроки, ПараметрыЗаголовокТаблицы, НомерКолонки, НаименованиеКолонки, ИмяПоля, ЭтоОсновнойСчет);
				КонецЕсли;
				Если СвойстваСчета.Валютный Тогда
					НаименованиеКолонки = НСтр("ru = 'Валюта'");
					ИмяПоля = "Валюта";
					ЗаполнитьКолонку(НоваяСтрокаПараметрыСтроки, ПараметрыЗаголовокТаблицы, НомерКолонки, НаименованиеКолонки, ИмяПоля, ЭтоОсновнойСчет);
					НаименованиеКолонки = НСтр("ru = 'Валютная сумма'");
					ИмяПоля = "ВалютнаяСумма";
					ЗаполнитьКолонку(НоваяСтрокаПараметрыСтроки, ПараметрыЗаголовокТаблицы, НомерКолонки, НаименованиеКолонки, ИмяПоля, ЭтоОсновнойСчет);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям Тогда
			ПараметрыЗаголовокТаблицы.НаименованиеСуммы = НСтр("ru = 'Сумма НДС, руб. коп.'");
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета Тогда
			НомерСубконтоБанковскиеСчета = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета);
			// Субконто Банковские счета преобразуем в 2 колонки: Наименование банка, Номер счета
			Если НомерСубконтоБанковскиеСчета <> Неопределено Тогда
				Для н = 1 По КоличествоКолонок Цикл
					Если НоваяСтрокаПараметрыСтроки["Колонка" + н] = "Субконто" + НомерСубконтоБанковскиеСчета Тогда
						НоваяСтрокаПараметрыСтроки["Колонка" + н] = "НаименованиеБанка";
						сч = КоличествоКолонок;
						// Остальные колонки двигаем вниз
						Пока сч <> н Цикл
							НоваяСтрокаПараметрыСтроки["Колонка" + (сч + 1)] = НоваяСтрокаПараметрыСтроки["Колонка" + сч];
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + (сч + 1)] = ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + сч];
							сч = сч - 1;
						КонецЦикла;
						НоваяСтрокаПараметрыСтроки["Колонка" + (н + 1)] = "НомерБанковскогоСчета";
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовкаТаблицыИСтроки.КоличествоКолонок = КоличествоКолонок + 1;
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + н] = НСтр("ru = 'Наименование банка'");
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + (н + 1)] = НСтр("ru = 'Номер счета'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ПараметрыЗаголовокТаблицы.НаименованиеСуммы = НСтр("ru = 'Остаток безналичных денежных средств, руб. коп.'");
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения Тогда
			НомерСубконтоКонтрагенты = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
			Для н = 1 По КоличествоКолонок Цикл
				Если НомерСубконтоЦенныеБумаги = Неопределено Тогда
					Если НоваяСтрокаПараметрыСтроки["Колонка" + н] = "Субконто" + НомерСубконтоКонтрагенты Тогда
						ЗначениеСубконто = НоваяСтрокаПараметрыСтроки["Колонка" + (н + 1)];
						Если ЗначениеСубконто = "" Тогда
							ЗначениеСубконто = "НомерСчетаНаименование";
						КонецЕсли;
						НоваяСтрокаПараметрыСтроки["Колонка" + (н + 1)] = "Субконто" + НомерСубконтоКонтрагенты;
						НоваяСтрокаПараметрыСтроки["Колонка" + н] = ЗначениеСубконто;
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + н] = НСтр("ru = 'Наименование финансового вложения'");
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + (н + 1)] = НСтр("ru = 'Контрагенты'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				Иначе
					Если НоваяСтрокаПараметрыСтроки["Колонка" + н] = "Субконто" + НомерСубконтоЦенныеБумаги Тогда
						НоваяСтрокаПараметрыСтроки["Колонка" + (н - 1)] = "Субконто" + НомерСубконтоЦенныеБумаги;
						НоваяСтрокаПараметрыСтроки["Колонка" + н] = "Субконто" + НомерСубконтоКонтрагенты;
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + (н - 1)] = НСтр("ru = 'Наименование финансового вложения'");
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + н] = НСтр("ru = 'Контрагенты'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОтложенныеНалоговыеАктивыОбязательства Тогда
			НомерСубконтоВидыАктивовИОбязательств = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыАктивовИОбязательств);
			Если НомерСубконтоВидыАктивовИОбязательств <> Неопределено Тогда
				Для н = 1 По КоличествоКолонок Цикл
					Если НоваяСтрокаПараметрыСтроки["Колонка" + н] = "Субконто" + НомерСубконтоВидыАктивовИОбязательств Тогда
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + н] = НСтр("ru = 'Вид активов и обязательств'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ОценочныеОбязательства Тогда
			НомерСубконтоРезервы = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Резервы);
			Если НомерСубконтоРезервы <> Неопределено Тогда
				Для н = 1 По КоличествоКолонок Цикл
					Если НоваяСтрокаПараметрыСтроки["Колонка" + н] = "Субконто" + НомерСубконтоРезервы Тогда
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + н] = НСтр("ru = 'Обязательство (резерв)'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.РасходыБудущихПериодов Тогда
			НомерСубконтоРасходыБудущихПериодов = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов);
			НомерСубконтоРаботникиОрганизаций = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
			НомерСубконтоКонтрагенты = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
			Для н = 1 По КоличествоКолонок Цикл
				Если НомерСубконтоРасходыБудущихПериодов <> Неопределено И НоваяСтрокаПараметрыСтроки["Колонка" + н] <> "Подразделение" Тогда
					НомерТек = н;
					НоваяСтрокаПараметрыСтроки["Колонка" + НомерТек] = "Субконто" + НомерСубконтоРасходыБудущихПериодов;
					Если ЭтоОсновнойСчет Тогда
						ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерТек] = НСтр("ru = 'Вид расходов'");
					КонецЕсли;
					Если НомерСубконтоРаботникиОрганизаций <> Неопределено Тогда
						НомерТек = НомерТек + 1;
						НоваяСтрокаПараметрыСтроки["Колонка" + НомерТек] = "Субконто" + НомерСубконтоРаботникиОрганизаций;
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерТек] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций.Наименование;
						КонецЕсли;
					КонецЕсли;
					Если НомерСубконтоКонтрагенты <> Неопределено Тогда
						НомерТек = НомерТек + 1;
						НоваяСтрокаПараметрыСтроки["Колонка" + НомерТек] = "Субконто" + НомерСубконтоКонтрагенты;
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + НомерТек] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты.Наименование;
						КонецЕсли;
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если СтрокаСчетаУчета.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ДоходыБудущихПериодов Тогда
			НомерСубконтоДоходыБудущихПериодов = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоходыБудущихПериодов);
			Если КоличествоКолонок = 0 Тогда
				НоваяСтрокаПараметрыСтроки["Колонка1"] = "НомерСчетаНаименование";
				Если ЭтоОсновнойСчет Тогда
					ПараметрыЗаголовокТаблицы["НаименованиеКолонки1"] = НСтр("ru = 'Вид доходов'");
				КонецЕсли;
			КонецЕсли;
			НомерСубконтоРаботникиОрганизаций = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
			Для н = 1 По КоличествоКолонок Цикл
				Если НомерСубконтоДоходыБудущихПериодов = Неопределено Тогда
					Если (КоличествоСубконто = 0 Или НомерСубконтоРаботникиОрганизаций <> Неопределено) И НоваяСтрокаПараметрыСтроки["Колонка" + н] <> "Подразделение" Тогда
						НоваяСтрокаПараметрыСтроки["Колонка" + н] = "НомерСчетаНаименование";
						Если НомерСубконтоРаботникиОрганизаций <> Неопределено Тогда
							НоваяСтрокаПараметрыСтроки["Колонка" + (н + 1)] = "Субконто" + НомерСубконтоРаботникиОрганизаций;
						КонецЕсли;
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + н] = НСтр("ru = 'Вид доходов'");
							Если НомерСубконтоРаботникиОрганизаций <> Неопределено Тогда
								ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + (н + 1)] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций.Наименование;
							КонецЕсли;
						КонецЕсли;
						Прервать;
					КонецЕсли;
				Иначе
					Если НоваяСтрокаПараметрыСтроки["Колонка" + н] = "Субконто" + НомерСубконтоДоходыБудущихПериодов Тогда
						Если ЭтоОсновнойСчет Тогда
							ПараметрыЗаголовокТаблицы["НаименованиеКолонки" + н] = НСтр("ru = 'Вид доходов'");
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция ОсновнойСчетРаздела(СчетаУчета, РазделУчета)
	
	ОсновнойСчет = Неопределено;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета Тогда
		ОтборСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета;
		ОсновнойСчет = ОсновнойСчет(СчетаУчета, ОтборСубконто);
	КонецЕсли;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения Тогда
		ОтборСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ЦенныеБумаги;
		ОсновнойСчет = ОсновнойСчет(СчетаУчета, ОтборСубконто);
	КонецЕсли;
	
	Если ОсновнойСчет = Неопределено Тогда
		ОсновнойСчет = ОсновнойСчет(СчетаУчета);
	КонецЕсли;
	
	Возврат ОсновнойСчет;
	
КонецФункции

Функция ОсновнойСчет(СчетаУчета, ОтборСубконто = Неопределено)
	
	СчетаКоличествоКолонок = Новый ТаблицаЗначений;
	СчетаКоличествоКолонок.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	СчетаКоличествоКолонок.Колонки.Добавить("КоличествоКолонок", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	
	Для Каждого СтрокаСчетаУчета Из СчетаУчета Цикл
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаСчетаУчета.СчетУчета);
		НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ОтборСубконто);
		
		Если Не ЗначениеЗаполнено(ОтборСубконто) Или НомерСубконто <> Неопределено Тогда
			КоличествоСубконто = СвойстваСчета.КоличествоСубконто;
			Для н = 1 По СвойстваСчета.КоличествоСубконто Цикл
				Если СвойстваСчета["ВидСубконто" + н + "ТолькоОбороты"] Тогда
					КоличествоСубконто = КоличествоСубконто - 1;
				КонецЕсли;
			КонецЦикла;
			КоличествоКолонок = КоличествоКолонок(СвойстваСчета, КоличествоСубконто);
			
			НоваяСтрокаСчетаКоличествоКолонок = СчетаКоличествоКолонок.Добавить();
			НоваяСтрокаСчетаКоличествоКолонок.СчетУчета = СтрокаСчетаУчета.СчетУчета;
			НоваяСтрокаСчетаКоличествоКолонок.КоличествоКолонок = КоличествоКолонок;
		КонецЕсли;
	КонецЦикла;
	
	Если СчетаКоличествоКолонок.Количество() > 0 Тогда
		СчетаКоличествоКолонок.Сортировать("КоличествоКолонок УБЫВ");
		Возврат СчетаКоличествоКолонок[0].СчетУчета;
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

Функция КоличествоКолонок(СвойстваСчета, КоличествоСубконто)
	
	КоличествоКолонок = ?(СвойстваСчета.УчетПоПодразделениям, КоличествоСубконто + 1, КоличествоСубконто);
	КоличествоКолонок = ?(СвойстваСчета.Количественный, КоличествоКолонок + 1, КоличествоКолонок);
	КоличествоКолонок = ?(СвойстваСчета.Валютный, КоличествоКолонок + 2, КоличествоКолонок);
	Возврат КоличествоКолонок;

КонецФункции

Процедура ЗаполнитьКолонку(НоваяСтрокаПараметрыСтроки, ПараметрыЗаголовокТаблицы, НомерКолонки, НаименованиеКолонки, ИмяПоля, ЭтоОсновнойСчет)
	
	ИмяКолонки = "НаименованиеКолонки" + НомерКолонки;
	НоваяСтрокаПараметрыСтроки["Колонка" + НомерКолонки] = ИмяПоля;
	Если ЭтоОсновнойСчет Тогда
		ПараметрыЗаголовокТаблицы[ИмяКолонки] = НаименованиеКолонки;
	КонецЕсли;
	НомерКолонки = НомерКолонки + 1;
	
КонецПроцедуры

Процедура ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета)
	
	КоллекцияИтоговСтраницы.КоличествоПорядковыхНомеровПрописью = ЧислоПрописью(КоллекцияИтоговСтраницы.КоличествоСтрок,, ",,,м,,,,,0");
	КоллекцияИтоговСтраницы.СтоимостьПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(
		КоллекцияИтоговСтраницы.Стоимость,
		ВалютаРегламентированногоУчета);
		
КонецПроцедуры

Функция НовыйПараметрыЗаголовкаТаблицыИСтроки()
	
	ПараметрыЗаголовокТаблицы = Новый Структура;
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки1", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки2", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки3", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки4", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки5", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеКолонки6", "");
	ПараметрыЗаголовокТаблицы.Вставить("НаименованиеСуммы", НСтр("ru = 'Сумма по балансу, руб. коп.'"));
	ПараметрыСтрокиТаблица = Новый ТаблицаЗначений;
	ПараметрыСтрокиТаблица.Колонки.Добавить("СчетУчета", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка1", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка2", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка3", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка4", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка5", Новый ОписаниеТипов("Строка"));
	ПараметрыСтрокиТаблица.Колонки.Добавить("Колонка6", Новый ОписаниеТипов("Строка"));
	
	ПараметрыЗаголовкаТаблицыИСтроки = Новый Структура();
	ПараметрыЗаголовкаТаблицыИСтроки.Вставить("ПараметрыЗаголовокТаблицы", ПараметрыЗаголовокТаблицы);
	ПараметрыЗаголовкаТаблицыИСтроки.Вставить("ПараметрыСтрокиТаблица", ПараметрыСтрокиТаблица);
	ПараметрыЗаголовкаТаблицыИСтроки.Вставить("КоличествоКолонок", 0);
	Возврат ПараметрыЗаголовкаТаблицыИСтроки;
	
КонецФункции

Функция НовыйИтогиПоказателей()

	ИтогиПоказателей = Новый Структура;
	ИтогиПоказателей.Вставить("Стоимость", 0);
	ИтогиПоказателей.Вставить("КоличествоСтрок", 0);
	ИтогиПоказателей.Вставить("КоличествоПорядковыхНомеровПрописью", "");
	ИтогиПоказателей.Вставить("СтоимостьПрописью", "");
	Возврат ИтогиПоказателей;
	
КонецФункции

Функция НовыйПараметрыПодписи()

	ПараметрыПодписи = Новый Структура;
	ПараметрыПодписи.Вставить("ЗаголовокРазделаПодписей");
	ПараметрыПодписи.Вставить("Должность");
	ПараметрыПодписи.Вставить("РасшифровкаПодписи");
	Возврат ПараметрыПодписи;
	
КонецФункции

#КонецОбласти

#КонецЕсли
