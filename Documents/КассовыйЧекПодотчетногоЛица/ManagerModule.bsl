#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Форматная строка даты покупки на кассовом чеке.
// Дата включет время без секунд: секунды в кассовом чеке не содержатся
// 
// Возвращаемое значение:
//  Строка
//
Функция ФорматнаяСтрокаДатаПокупки() Экспорт
	Возврат НСтр("ru = 'ДФ=''dd.MM.yyyy HH:mm'''");
КонецФункции

// Обеспечивает наличие готового к обработке документа с указанным QR-кодом:
// находит, при отсутствии - создает документ.
// Контроль прав не выполняется.
// 
// Параметры:
//  QRКод - Строка - текст QR-кода
//  ПользовательШиныМобильныхПриложений
//     - Неопределено, если чек отсканирован без использования шины.
//       Чек записывается как отсканированный текущим пользователем
//     - ОпределяемыйТип.ПользовательШиныМобильныхПриложений - пользователь, отсканировавший чек.
// 
// Возвращаемое значение:
//  ДокументыСсылка.КассовыйЧекПодотчетногоЛица - найденный (созданный) документ.
//    При некорректных параметрах возвращается пустая ссылка.
//
Функция ПолучитьПоQRКоду(QRКод, ПользовательШиныМобильныхПриложений = Неопределено) Экспорт
	
	Если ПустаяСтрока(QRКод) Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Ссылка = НайтиПоРеквизиту("QRКод", QRКод);
	
	ИспользуетсяШинаМобильныхПриложений = (ПользовательШиныМобильныхПриложений <> Неопределено);
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		РегистрыСведений.СканированиеКассовыхЧековПодотчетныхЛиц.Включить(
			Ссылка,
			ПользовательШиныМобильныхПриложений);
		
		Возврат Ссылка;
		
	КонецЕсли;
	
	РеквизитыЧека = КассовыеЧекиQRКоды.РеквизитыЧека(QRКод);
	Если Не КассовыеЧекиQRКоды.РеквизитыЧекаЗаполнены(РеквизитыЧека) Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Запись = СоздатьДокумент();
	
	Запись.Заполнить(QRКод);
	
	Если ПустаяСтрока(Запись.QRКод) Тогда
		// Невалидный QR-код
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Если ИспользуетсяШинаМобильныхПриложений Тогда
		// Запрос выполнен (будет выполен) шиной, ожидаем сообщение от шины с содержимым кассового чека
		РегистрыСведений.СканированиеКассовыхЧековПодотчетныхЛиц.УстановитьПользовательШиныОтсканировалЧек(
			Запись,
			ПользовательШиныМобильныхПриложений);
	Иначе
		РегистрыСведений.КассовыеЧекиПодотчетныхЛицЗапросыДетальныхДанных.УстановитьТребуетсяЗапрос(Запись);
		РегистрыСведений.СканированиеКассовыхЧековПодотчетныхЛиц.УстановитьТекущийПользовательОтсканировалЧек(Запись);
	КонецЕсли;
	
	Запись.Записать(РежимЗаписиДокумента.Запись);
	
	Возврат Запись.Ссылка;
	
КонецФункции

// Исключает чек из числа отсканированных пользователем.
// Если после этого чек становится "ничей", он помечается на удаление.
//
// Параметры:
//  КассовыйЧек - ДокументСсылка.КассовыйЧекПодотчетногоЛица
//              - Строка - QR-код
//  Пользователь - ОпределяемыйТип.ПользовательШиныМобильныхПриложений
//               - Неопределено - текущий пользователь
// 
// Возвращаемое значение:
//  Булево - Истина, если чек принадлежал пользователю
//
Функция УдалитьЧекПользователя(КассовыйЧек, Пользователь = Неопределено) Экспорт
	
	Если ТипЗнч(КассовыйЧек) = Тип("ДокументСсылка.КассовыйЧекПодотчетногоЛица") Тогда
		Ссылка = КассовыйЧек;
	Иначе
		Ссылка = НайтиПоРеквизиту("QRКод", КассовыйЧек);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Документ.КассовыйЧекПодотчетногоЛица");
	ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка);
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СканированиеКассовыхЧековПодотчетныхЛиц");
	ЭлементБлокировки.УстановитьЗначение("КассовыйЧек", Ссылка);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ЧекНайден = Ложь;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка.Заблокировать();
		
		УстановитьПривилегированныйРежим(Истина);
		ЧекНайден = РегистрыСведений.СканированиеКассовыхЧековПодотчетныхЛиц.НайтиЧекПользователя(Ссылка, Пользователь);
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ЧекНайден Тогда
			
			УстановитьПривилегированныйРежим(Истина);
			РегистрыСведений.СканированиеКассовыхЧековПодотчетныхЛиц.Исключить(Ссылка, Пользователь);
			УстановитьПривилегированныйРежим(Ложь);
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка",       Ссылка);
			Запрос.УстановитьПараметр("Пользователь", Пользователь);
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	КассовыеЧеки.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.КассовыйЧекПодотчетногоЛица КАК КассовыеЧеки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СканированиеКассовыхЧековПодотчетныхЛиц КАК Сканирование
			|		ПО КассовыеЧеки.Ссылка = Сканирование.КассовыйЧек
			|			И (Сканирование.Пользователь <> &Пользователь)
			|ГДЕ
			|	КассовыеЧеки.Ссылка = &Ссылка
			|	И НЕ КассовыеЧеки.ПометкаУдаления
			|	И Сканирование.КассовыйЧек ЕСТЬ NULL";
			
			УстановитьПривилегированныйРежим(Истина);
			Если Не Запрос.Выполнить().Пустой() Тогда
				
				Запись = Ссылка.ПолучитьОбъект();
				Запись.УстановитьПометкуУдаления(Истина);
				
			КонецЕсли;
			УстановитьПривилегированныйРежим(Ложь);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ЧекНайден;
	
КонецФункции

// Текстовое сокращенное представление содержимого чека - в виде, имитирующем напечатанный чек.
// Предназначено для предварительного просмотра пользователем, чтобы идентифицировать смысл покупки.
//
// Параметры:
//  Ссылка - ДокументСсылка.КассовыйЧекПодотчетногоЛица
// 
// Возвращаемое значение:
//  Строка
//
Функция ПредварительныйПросмотрЧека(Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КассовыйЧек.Ссылка КАК Ссылка,
	|	КассовыйЧек.Статус КАК Статус,
	|	КассовыйЧек.Номер КАК Номер,
	|	КассовыйЧек.Дата КАК Дата,
	|	КассовыйЧек.ПоставщикНаименование КАК ПоставщикНаименование,
	|	КассовыйЧек.ПоставщикИНН КАК ПоставщикИНН,
	|	КассовыйЧек.СуммаДокумента КАК СуммаДокумента,
	|	КассовыйЧек.QRКод КАК QRКод,
	|	КассовыйЧек.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		НоменклатураНаименование КАК Наименование,
	|		Количество КАК Количество,
	|		Сумма КАК Сумма
	|	) КАК Товары
	|ИЗ
	|	Документ.КассовыйЧекПодотчетногоЛица КАК КассовыйЧек
	|ГДЕ
	|	КассовыйЧек.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КассовыйЧекТовары.Ссылка КАК КассовыйЧек,
	|	КассовыйЧекТовары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(КассовыйЧекТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(КассовыйЧекТовары.Сумма) КАК Сумма
	|ИЗ
	|	Документ.КассовыйЧекПодотчетногоЛица.Товары КАК КассовыйЧекТовары
	|ГДЕ
	|	КассовыйЧекТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КассовыйЧекТовары.СтавкаНДС,
	|	КассовыйЧекТовары.Ссылка";
	
	ДлинаСтрокиЧека              = 35;
	МинимальнаяДлинаНаименования = 10;
	Пробел = Символы.НПП;
	Разделитель = СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("-", ДлинаСтрокиЧека);

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ИндексПоследнегоРезультата = РезультатЗапроса.Количество() - 1;
	ВыборкаСсылка = РезультатЗапроса[ИндексПоследнегоРезультата - 1].Выбрать();
	СуммыНДСТаблица = РезультатЗапроса[ИндексПоследнегоРезультата].Выгрузить();
	Пока ВыборкаСсылка.Следующий() Цикл
		
		ДанныеТабличнойЧастиТовары = ДанныеТабличнойЧастиТовары(ВыборкаСсылка.Товары, ДлинаСтрокиЧека, МинимальнаяДлинаНаименования, Пробел);
		ТелоЧека = ДанныеТабличнойЧастиТовары.ТелоЧека;
		ЕстьКоличество = ДанныеТабличнойЧастиТовары.ЕстьКоличество;
		МаксимальнаяДлинаСумма = ДанныеТабличнойЧастиТовары.МаксимальнаяДлинаСумма;
		
		// Пример:
		// КАССОВЫЙ ЧЕК № 99999
		// 01.01.2021 00:00
		// АО "Ромашка" ИНН 9999999999
		//
		// Товары, услуги      Кол-во    Итого
		// -----------------------------------
		// <тут товары, см. выше>
		// -----------------------------------
		// ИТОГ:                      9 999.99
		// НДС по ставке <ставка НДС>   999.99  
		
		ПредварительныйПросмотр = Новый Массив;
		
		// Заголовок чека
		ПредварительныйПросмотр.Добавить(СтрШаблон(НСтр("ru = 'КАССОВЫЙ ЧЕК № %1'"), ВыборкаСсылка.Номер));
		ПредварительныйПросмотр.Добавить(Формат(
			ВыборкаСсылка.Дата,
			ФорматнаяСтрокаДатаПокупки()));
		ПредставлениеОрганизацииТекст = ПредставлениеОрганизацииТекст(ВыборкаСсылка.ПоставщикНаименование, ВыборкаСсылка.ПоставщикИНН, ДлинаСтрокиЧека);
		Если Не ПустаяСтрока(ПредставлениеОрганизацииТекст) Тогда
			ПредварительныйПросмотр.Добавить(ПредставлениеОрганизацииТекст);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТелоЧека) Тогда
		
			// Заголовок списка
			ЗаголовокКоличество = "";
			Если ЕстьКоличество Тогда
				ЗаголовокКоличество = НСтр("ru = 'Кол-во'") + Пробел;
			КонецЕсли;
			
			ЗаголовокИтого = НСтр("ru = 'Итого'");
			Если СтрДлина(ЗаголовокИтого) < МаксимальнаяДлинаСумма Тогда
				ЗаголовокИтого = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ЗаголовокИтого, МаксимальнаяДлинаСумма, Пробел);
			КонецЕсли;
			
			ЗаголовокТоварыУслуги = НСтр("ru = 'Товары, услуги'");
			МаксимальнаяДлинаЗаголовокТоварыУслуги = ДлинаСтрокиЧека - СтрДлина(ЗаголовокКоличество) - СтрДлина(ЗаголовокИтого);
			Если СтрДлина(ЗаголовокТоварыУслуги) >= МаксимальнаяДлинаЗаголовокТоварыУслуги Тогда
				ЗаголовокТоварыУслуги = "";
			КонецЕсли;
			ЗаголовокТоварыУслуги = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(
				ЗаголовокТоварыУслуги,
				МаксимальнаяДлинаЗаголовокТоварыУслуги,
				Пробел,
				"Справа");
				
			ПредварительныйПросмотр.Добавить("");
			ПредварительныйПросмотр.Добавить(ЗаголовокТоварыУслуги + ЗаголовокКоличество + ЗаголовокИтого);
			
			// Список
			ПредварительныйПросмотр.Добавить(Разделитель);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредварительныйПросмотр, ТелоЧека);
			
		КонецЕсли;
		
		// Итог
		ПредварительныйПросмотр.Добавить(Разделитель);
		ПредварительныйПросмотр.Добавить(СтрокаИтогоСумма(ВыборкаСсылка.СуммаДокумента, ДлинаСтрокиЧека, Пробел));
		
		// НДС
		СуммыНДС = СуммыНДСТаблица.НайтиСтроки(Новый Структура("КассовыйЧек", ВыборкаСсылка.Ссылка));
		Для Каждого Строка Из СуммыНДС Цикл
			ПредварительныйПросмотр.Добавить(СтрокаИтогоНДС(Строка, ДлинаСтрокиЧека, Пробел));
		КонецЦикла;
		
		Если ВыборкаСсылка.Статус <> Перечисления.СтатусыКассовыхЧековПодотчетныхЛиц.Получен Тогда
			ПредварительныйПросмотр.Добавить(Символы.ПС);
			ПредварительныйПросмотр.Добавить(СтрШаблон(НСтр("ru = 'Статус чека: %1'"), ВыборкаСсылка.Статус));
		КонецЕсли;
		
		Возврат СтрСоединить(ПредварительныйПросмотр, Символы.ПС);
		
	КонецЦикла;
	
КонецФункции

// Формирует табличный документ с печатной формой с кассовыми чеками.
//
// Параметры:
//  МассивОбъектов	 - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ОбъектыПечати	 - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
// 
// Возвращаемое значение:
//  Табличный документ
//
Функция ПечатьКассовыхЧеков(МассивОбъектов, ОбъектыПечати) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб          = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КассовыйЧекПодотчетногоЛица_КассовыйЧек";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КассовыйЧек");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапросаДляФормированияПечатнойФормыКассовыхЧеков();
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ИндексПоследнегоРезультата = РезультатЗапроса.Количество() - 1;
	ВыборкаДокумент = РезультатЗапроса[ИндексПоследнегоРезультата - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	СуммыНДСТаблица = РезультатЗапроса[ИндексПоследнегоРезультата].Выгрузить();
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДокумент.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВыборкаЧек = ВыборкаДокумент.Выбрать();
		КоличествоЧеков = ВыборкаЧек.Количество();
		
		ТабличныйДокументЛеваяКолонка = Новый ТабличныйДокумент;
		ТабличныйДокументПраваяКолонка = Новый ТабличныйДокумент;
		ТабличныеДокументы = Новый ТаблицаЗначений;
		ТабличныеДокументы.Колонки.Добавить("НомерСтраницы",          Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
		ТабличныеДокументы.Колонки.Добавить("ТабличныйДокумент",      Новый ОписаниеТипов("ТабличныйДокумент"));
		ТабличныеДокументы.Колонки.Добавить("ПервыйЧекНаСтранице",    Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
		ТабличныеДокументы.Колонки.Добавить("ПоследнийЧекНаСтранице", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
		
		СтрокаТабличныеДокументы = ТабличныеДокументы.Добавить();
		СтрокаТабличныеДокументы.НомерСтраницы = 1;
		СтрокаТабличныеДокументы.ТабличныйДокумент = Новый ТабличныйДокумент;
		СтрокаТабличныеДокументы.ПервыйЧекНаСтранице = 1;
		СтрокаТабличныеДокументы.ПоследнийЧекНаСтранице = 0;
		
		ЛеваяКолонка = Истина;
		ДлинаСтрокиЧека = 35;
		МинимальнаяДлинаНаименования = 10;
		Пробел = Символы.НПП;
		
		ОбластьМакетаПриложение  = Макет.ПолучитьОбласть("Приложение");
		Если ТипЗнч(ВыборкаДокумент.ДокументСсылка) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
			НаименованиеДокумента = "Авансовому отчету";
		ИначеЕсли ТипЗнч(ВыборкаДокумент.ДокументСсылка) = Тип("ДокументСсылка.ПутевойЛист") Тогда
			НаименованиеДокумента = "Путевому листу";
		ИначеЕсли ТипЗнч(ВыборкаДокумент.ДокументСсылка) = Тип("ДокументСсылка.РасходыПредпринимателя") Тогда
			НаименованиеДокумента = "Расходам предпринимателя";
		Иначе
			НаименованиеДокумента = ВыборкаДокумент.ДокументСсылка.Метаданные().Синоним;
		КонецЕсли;
		ОбластьМакетаПриложение.Параметры.НаименованиеДокумента = НаименованиеДокумента;
		ОбластьМакетаПриложение.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокумент.НомерДокумента, Истина, Ложь);
		ОбластьМакетаПриложение.Параметры.ДатаДокумента = Формат(ВыборкаДокумент.ДатаДокумента, "ДФ='dd.MM.yyyy ""г.""'");
		ОбластьМакетаКолонтитул = Макет.ПолучитьОбласть("Колонтитул");
		ОбластьМакетаПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
		
		Пока ВыборкаЧек.Следующий() Цикл
			
			РеквизитыЧека = КассовыеЧекиQRКоды.РеквизитыЧека(ВыборкаЧек.QRКод);
			ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка|Чек");
			ОбластьМакетаСтрока = Макет.ПолучитьОбласть("Строка|Чек");
			ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал|Чек");
			ОбластьМакетаСтрокаИтогоНДС = Макет.ПолучитьОбласть("СтрокаИтогоНДС|Чек");
			ИмяОбластиМакетаQRКод = ?(ЗначениеЗаполнено(ВыборкаЧек.QRКод), "QRКод|Чек", "QRКодНетСведений|Чек");
			ОбластьМакетаQRКод = Макет.ПолучитьОбласть(ИмяОбластиМакетаQRКод);
			ОбластьМакетаПереносЧекаНачало = Макет.ПолучитьОбласть("ПереносНачало|Чек");
			ОбластьМакетаПереносЧекаОкончание = Макет.ПолучитьОбласть("ПереносОкончание|Чек");
			
			СуммыНДС = СуммыНДСТаблица.НайтиСтроки(Новый Структура("КассовыйЧек", ВыборкаЧек.Ссылка));
			
			ВыводимыеОбласти = Новый Массив;
			ВыводимыеОбласти.Добавить(ОбластьМакетаШапка);
			ВыводимыеОбласти.Добавить(ОбластьМакетаСтрока);
			ВыводимыеОбласти.Добавить(ОбластьМакетаСтрока);
			ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
			Для Каждого СтрокаСуммыНДС Из СуммыНДС Цикл
				ВыводимыеОбласти.Добавить(ОбластьМакетаСтрокаИтогоНДС);
			КонецЦикла;
			ВыводимыеОбласти.Добавить(ОбластьМакетаQRКод);
			Если ТабличныеДокументы.Количество() = 1 Тогда
				ВыводимыеОбласти.Добавить(ОбластьМакетаПриложение);
			КонецЕсли;
			ВыводимыеОбласти.Добавить(ОбластьМакетаПустаяСтрока);
			ВыводимыеОбласти.Добавить(ОбластьМакетаКолонтитул);
			ПоследнийЧекНаСтранице = СтрокаТабличныеДокументы.ПоследнийЧекНаСтранице;
			Если ЛеваяКолонка И НЕ ТабличныйДокументЛеваяКолонка.ПроверитьВывод(ВыводимыеОбласти) Тогда
				ЛеваяКолонка = Ложь;
			ИначеЕсли Не ЛеваяКолонка И НЕ ТабличныйДокументПраваяКолонка.ПроверитьВывод(ВыводимыеОбласти) Тогда
				ЗаполнитьТекущуюСтраницу(СтрокаТабличныеДокументы.ТабличныйДокумент, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка);
				ПервыйЧекНаСтранице = СтрокаТабличныеДокументы.ПоследнийЧекНаСтранице + 1;
				ДобавитьНовуюСтраницу(ТабличныеДокументы, СтрокаТабличныеДокументы.НомерСтраницы, ПервыйЧекНаСтранице);
				ЛеваяКолонка = Истина;
				СтрокаТабличныеДокументы = ТабличныеДокументы[ТабличныеДокументы.Количество()-1];
			КонецЕсли;
			СтрокаТабличныеДокументы.ПоследнийЧекНаСтранице = ПоследнийЧекНаСтранице + 1;
			
			ОбластьМакетаШапка.Параметры.НомерЧека = ВыборкаЧек.Номер;
			ОбластьМакетаШапка.Параметры.ДатаЧека = Формат(ВыборкаЧек.Дата,
			Документы.КассовыйЧекПодотчетногоЛица.ФорматнаяСтрокаДатаПокупки());
			ПредставлениеОрганизацииТекст = ПредставлениеОрганизацииТекст(ВыборкаЧек.ПоставщикНаименование, ВыборкаЧек.ПоставщикИНН, ДлинаСтрокиЧека);
			Если Не ПустаяСтрока(ПредставлениеОрганизацииТекст) Тогда
				ОбластьМакетаШапка.Параметры.ПредставлениеОрганизации = ПредставлениеОрганизацииТекст;
			КонецЕсли;
			
			ВывестиОбласть(ОбластьМакетаШапка, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка, ЛеваяКолонка);
			
			ВыводимыеОбласти = Новый Массив;
			ВыводимыеОбласти.Добавить(ОбластьМакетаСтрока);
			ВыводимыеОбласти.Добавить(ОбластьМакетаСтрока);
			Если ТабличныеДокументы.Количество() = 1 Тогда
				ВыводимыеОбласти.Добавить(ОбластьМакетаПриложение);
			КонецЕсли;
			ВыводимыеОбласти.Добавить(ОбластьМакетаПустаяСтрока);
			ВыводимыеОбласти.Добавить(ОбластьМакетаКолонтитул);
			ВыводимыеОбласти.Добавить(ОбластьМакетаПустаяСтрока);
			ВыводимыеОбласти.Добавить(ОбластьМакетаПереносЧекаОкончание);
			
			ДанныеТабличнойЧастиТовары = ДанныеТабличнойЧастиТовары(ВыборкаЧек.Товары, ДлинаСтрокиЧека, МинимальнаяДлинаНаименования, Пробел, Истина);
			ТелоЧека = ДанныеТабличнойЧастиТовары.ТелоЧека;
			НомерСтроки = 0;
			
			Для Каждого СтрокаЧека Из ТелоЧека Цикл
				НомерСтроки = НомерСтроки + 1;
				ОбластьМакетаСтрока.Параметры.СтрокаЧека = СтрокаЧека;
				
				Если НомерСтроки = ТелоЧека.Количество() - 1 Тогда
					ВыводимыеОбласти.Добавить(ОбластьМакетаСтрока);
					ВыводимыеОбласти.Добавить(ОбластьМакетаПодвал);
					Для Каждого СтрокаСуммыНДС Из СуммыНДС Цикл
						ВыводимыеОбласти.Добавить(ОбластьМакетаСтрокаИтогоНДС);
					КонецЦикла;
					ВыводимыеОбласти.Добавить(ОбластьМакетаQRКод);
				КонецЕсли;
				Если ЛеваяКолонка Тогда
					Если НЕ ТабличныйДокументЛеваяКолонка.ПроверитьВывод(ВыводимыеОбласти)Тогда
						ЛеваяКолонка = Ложь;
						ТабличныйДокументЛеваяКолонка.Вывести(ОбластьМакетаПереносЧекаОкончание);
						ОбластьМакетаПереносЧекаНачало.Параметры.Заполнить(ВыборкаЧек);
						ОбластьМакетаПереносЧекаНачало.Параметры.НомерЧека = ВыборкаЧек.Номер;
						ОбластьМакетаПереносЧекаНачало.Параметры.ДатаЧека = Формат(ВыборкаЧек.Дата,
							Документы.КассовыйЧекПодотчетногоЛица.ФорматнаяСтрокаДатаПокупки());
						ТабличныйДокументПраваяКолонка.Вывести(ОбластьМакетаПереносЧекаНачало);
					КонецЕсли;
				Иначе
					Если НЕ ТабличныйДокументПраваяКолонка.ПроверитьВывод(ВыводимыеОбласти)Тогда
						ТабличныйДокументПраваяКолонка.Вывести(ОбластьМакетаПереносЧекаОкончание);
						ЗаполнитьТекущуюСтраницу(СтрокаТабличныеДокументы.ТабличныйДокумент, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка);
						ПервыйЧекНаСтранице = СтрокаТабличныеДокументы.ПоследнийЧекНаСтранице;
						ДобавитьНовуюСтраницу(ТабличныеДокументы, СтрокаТабличныеДокументы.НомерСтраницы, ПервыйЧекНаСтранице);
						СтрокаТабличныеДокументы = ТабличныеДокументы[ТабличныеДокументы.Количество()-1];
						ЛеваяКолонка = Истина;
						СтрокаТабличныеДокументы.ПоследнийЧекНаСтранице = СтрокаТабличныеДокументы.ПервыйЧекНаСтранице;
						ОбластьМакетаПереносЧекаНачало.Параметры.НомерЧека = ВыборкаЧек.Номер;
						ОбластьМакетаПереносЧекаНачало.Параметры.ДатаЧека = Формат(ВыборкаЧек.Дата,
							Документы.КассовыйЧекПодотчетногоЛица.ФорматнаяСтрокаДатаПокупки());
						ТабличныйДокументЛеваяКолонка.Вывести(ОбластьМакетаПереносЧекаНачало);
					КонецЕсли;
				КонецЕсли;
				ВывестиОбласть(ОбластьМакетаСтрока, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка, ЛеваяКолонка)
			КонецЦикла;
			
			ОбластьМакетаПодвал.Параметры.Итого = СтрокаИтогоСумма(ВыборкаЧек.СуммаДокумента, ДлинаСтрокиЧека, Пробел);
			ВывестиОбласть(ОбластьМакетаПодвал, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка, ЛеваяКолонка);
			
			Для Каждого Строка Из СуммыНДС Цикл
				ОбластьМакетаСтрокаИтогоНДС.Параметры.ИтогоНДС = СтрокаИтогоНДС(Строка, ДлинаСтрокиЧека, Пробел);
				ВывестиОбласть(ОбластьМакетаСтрокаИтогоНДС, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка, ЛеваяКолонка);
			КонецЦикла;
			
			Если ЗначениеЗаполнено(ВыборкаЧек.QRКод) Тогда
				ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(ВыборкаЧек.QRКод, 3, 100);
				Если ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
					ОбластьМакетаQRКод.Рисунки.QRРисунок.Картинка = Новый Картинка(ДанныеQRКода);
				КонецЕсли;
				ОбластьМакетаQRКод.Параметры.ФН = РеквизитыЧека.КодУстройства;
				ОбластьМакетаQRКод.Параметры.ФПД = РеквизитыЧека.КодПроверки;
			КонецЕсли;
			
			ВывестиОбласть(ОбластьМакетаQRКод, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка, ЛеваяКолонка);
			
		КонецЦикла;
		
		ЗаполнитьТекущуюСтраницу(СтрокаТабличныеДокументы.ТабличныйДокумент, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка);
		
		ТабличныйДокумент.Вывести(ОбластьМакетаПриложение);
		Для Каждого СтрокаТабличныеДокументы Из ТабличныеДокументы Цикл
			ТабличныйДокумент.Вывести(СтрокаТабличныеДокументы.ТабличныйДокумент);
			ВывестиКолонтитул(ТабличныйДокумент, ОбластьМакетаКолонтитул, ОбластьМакетаПустаяСтрока, СтрокаТабличныеДокументы.ПервыйЧекНаСтранице,
			СтрокаТабличныеДокументы.ПоследнийЧекНаСтранице, КоличествоЧеков);
			Если СтрокаТабличныеДокументы.НомерСтраницы < ТабличныеДокументы.Количество() Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокумент.ДокументСсылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Определяет перечень и основные данные кассовых чеков, которые можно загрузить в документ
// (пользователь их отсканировал и отправил для загрузки, но они еще не включены ни в один записанный документ)
//
// Параметры:
//  ЧекиКЗагрузке - ТаблицаЗначений - см. УчетКассовыхЧековПодотчетныхЛиц.НовыйЧекиКЗагрузке,
//                                    заполняемая коллекция
//  ПроверятьСообщения - Булево - требуется ли запустить фоновое задание получения сообщений с Шины,
//                                частое получение сообщений может быть ресурсоемким.
//
Процедура ПрочитатьЧекиКЗагрузке(ЧекиКЗагрузке, ПроверятьСообщения = Истина) Экспорт
	
	Если ПроверятьСообщения Тогда
		РегистрыСведений.МобильноеПриложениеСканированиеЧеков.НачатьПроверкуСообщений();
	КонецЕсли;
	
	ЧекиКЗагрузке.Очистить();
	
	СтатусыЧеков = Новый Массив;
	СтатусыЧеков.Добавить(Перечисления.СтатусыКассовыхЧековПодотчетныхЛиц.Получен);
	СтатусыЧеков.Добавить(Перечисления.СтатусыКассовыхЧековПодотчетныхЛиц.Отменен);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатусыЧеков", СтатусыЧеков);
	// Пользователем мобильного приложения может быть как пользователь программы (справочник Пользователи),
	// так и иной сотрудник (справочник ФизическиеЛица)
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КассовыеЧеки.Ссылка КАК Ссылка,
	|	КассовыеЧеки.Дата КАК Дата,
	|	КассовыеЧеки.СуммаДокумента КАК Сумма,
	|	КассовыеЧеки.ПоставщикНаименование КАК Поставщик,
	|	ВЫБОР
	|		КОГДА Сканирование.Пользователь ССЫЛКА Справочник.Пользователи
	|				И Пользователи.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА Пользователи.ФизическоеЛицо
	|		ИНАЧЕ Сканирование.Пользователь
	|	КОНЕЦ КАК Пользователь,
	|	ВЫБОР
	|		КОГДА Сканирование.Пользователь ССЫЛКА Справочник.Пользователи
	|				И Пользователи.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА Пользователи.ФизическоеЛицо
	|		ИНАЧЕ Сканирование.Пользователь
	|	КОНЕЦ КАК КоличествоПользователей,
	|	ВЫБОР
	|		КОГДА Сканирование.Пользователь ССЫЛКА Справочник.Пользователи
	|				И Пользователи.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ТОГДА Пользователи.ФизическоеЛицо.ФИО
	|		ИНАЧЕ Сканирование.Пользователь.Наименование
	|	КОНЕЦ КАК ПользовательПредставление
	|ИЗ
	|	Документ.КассовыйЧекПодотчетногоЛица КАК КассовыеЧеки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыПоКассовымЧекамПодотчетныхЛиц КАК УчтенныеЧеки
	|		ПО КассовыеЧеки.Ссылка = УчтенныеЧеки.КассовыйЧек
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СканированиеКассовыхЧековПодотчетныхЛиц КАК Сканирование
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|			ПО Сканирование.Пользователь = Пользователи.Ссылка
	|		ПО КассовыеЧеки.Ссылка = Сканирование.КассовыйЧек
	|ГДЕ
	|	КассовыеЧеки.Статус В(&СтатусыЧеков)
	|	И УчтенныеЧеки.КассовыйЧек ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка
	|ИТОГИ
	|	МАКСИМУМ(Пользователь),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КоличествоПользователей),
	|	МАКСИМУМ(ПользовательПредставление)
	|ПО
	|	Ссылка";
	
	ВыборкаСсылка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСсылка.Следующий() Цикл
		
		Запись = ЧекиКЗагрузке.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаСсылка);
		
		Если ВыборкаСсылка.КоличествоПользователей < 2 Тогда
			Запись.Пользователи = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ПредставлениеПользователяКраткое(
				ВыборкаСсылка.Пользователь,
				ВыборкаСсылка.ПользовательПредставление);
			Продолжить;
		КонецЕсли;
		
		ИменаПользователей = Новый Массив;
		
		Выборка = ВыборкаСсылка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл
			ПредставлениеПользователя = РегистрыСведений.МобильноеПриложениеСканированиеЧеков.ПредставлениеПользователяКраткое(
				Выборка.Пользователь,
				Выборка.ПользовательПредставление);
			ИменаПользователей.Добавить(ПредставлениеПользователя);
		КонецЦикла;
		
		Запись.Пользователи = СтрСоединить(ИменаПользователей, НСтр("ru = ', '"));
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет детальные чека сведениями, полученными из сервиса.
//
// Параметры:
//  КассовыйЧек - ДокументСсылка.КассовыйЧекПодотчетногоЛица - заполняемый документ
//  ИмяСтатуса  - Строка - имя значения перечисления СтатусыКассовыхЧековПодотчетныхЛиц
//  ДанныеЧека  - Структура - детальные данные о покупке
//
Процедура ЗаполнитьДанныеЧека(КассовыйЧек, ИмяСтатуса, ДанныеЧека) Экспорт
	
	КешСтавкиНДС = Новый Соответствие;
	
	Запись = КассовыйЧек.ПолучитьОбъект();
	Запись.Статус = Перечисления.СтатусыКассовыхЧековПодотчетныхЛиц[ИмяСтатуса];
	
	Если Запись.Статус = Перечисления.СтатусыКассовыхЧековПодотчетныхЛиц.Получен
		И ДанныеЧека <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(Запись, ДанныеЧека);
		
		Если ДанныеЧека.Свойство("Товары") Тогда
			Запись.Товары.Очистить();
			Для Каждого СтрокаТовары Из ДанныеЧека.Товары Цикл
				
				ЗаписьТовары = Запись.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьТовары, СтрокаТовары);
				
				УстановитьСтавкуНДС(ЗаписьТовары, СтрокаТовары, КешСтавкиНДС);
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Запись.Записать(РежимЗаписиДокумента.Запись);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеТабличнойЧастиТовары(Товары, ДлинаСтрокиЧека, МинимальнаяДлинаНаименования, Пробел, ПоказыватьНДС = Ложь)
	
	СтрокиЧека = Новый ТаблицаЗначений;
	СтрокиЧека.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	СтрокиЧека.Колонки.Добавить("Количество",   Новый ОписаниеТипов("Строка"));
	СтрокиЧека.Колонки.Добавить("Сумма",        Новый ОписаниеТипов("Строка"));
	СтрокиЧека.Колонки.Добавить("СтавкаНДС",    Новый ОписаниеТипов("Строка"));
	
	МаксимальнаяДлинаСумма      = 0;
	МаксимальнаяДлинаКоличество = 0;
	
	ВыборкаТовары = Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		
		ДанныеСтроки = СтрокиЧека.Добавить();
		
		ДанныеСтроки.Наименование = ВыборкаТовары.Наименование;
		
		ДанныеСтроки.Сумма = ОбщегоНазначенияБПВызовСервера.ФорматСумм(ВыборкаТовары.Сумма);
		МаксимальнаяДлинаСумма = Макс(СтрДлина(ДанныеСтроки.Сумма), МаксимальнаяДлинаСумма);
		
		Если ВыборкаТовары.Количество <> 1 И ВыборкаТовары.Количество <> 0 Тогда
			ДанныеСтроки.Количество = Строка(ВыборкаТовары.Количество);
		КонецЕсли;
		
		Если ПоказыватьНДС Тогда
			Если ВыборкаТовары.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС
				Или Не ЗначениеЗаполнено(ВыборкаТовары.СтавкаНДС) Тогда
				ДанныеСтроки.СтавкаНДС = НСтр("ru = 'НДС не облагается'");
			Иначе
				ДанныеСтроки.СтавкаНДС = СтрШаблон(НСтр("ru = 'НДС %1'"), ВыборкаТовары.СтавкаНДС);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ТелоЧека = Новый Массив;
	ЕстьКоличество = Ложь;
	
	Для Каждого ДанныеСтроки Из СтрокиЧека Цикл
		
		// Пример:
		// Молоток                1 000.00
		// Гвозди           x3.25   160.50
		// Жидкость для снятия ла   216.50
		// Пакет                      1.50
		
		// Суммы дополняем так, чтобы они отделялись от наименования и были выровнены в колонку.
		// Так, в примере выше нужно получить " 1 000.00" и "     1.50"
		Сумма = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ДанныеСтроки.Сумма, МаксимальнаяДлинаСумма, Пробел);
		Сумма = СтрШаблон(НСтр("ru = ' %1'"), Сумма);
		
		Количество = "";
		Если Не ПустаяСтрока(ДанныеСтроки.Количество) Тогда
			Количество     = СтрШаблон(НСтр("ru = ' x%1'"), ДанныеСтроки.Количество);
			ЕстьКоличество = Истина;
		КонецЕсли;
		
		// От наименования нужно оставить такую часть, чтобы суммы были выровнены, а перед суммой помещалось количество.
		// Если оставшаяся часть наименования окажется слишком короткой, то информацию о товаре разделим на две строки:
		// в первой будет только наимнование, а во второй - количество и сумма
		ДлинаНаименование             = СтрДлина(ДанныеСтроки.Наименование);
		ДлинаНаименованиеОднойСтрокой = Макс(0, ДлинаСтрокиЧека - СтрДлина(Сумма) - СтрДлина(Количество));
		
		Если ДлинаНаименование <= ДлинаНаименованиеОднойСтрокой Тогда
			
			// Наименование короткое, его выравниваем так, чтобы количества и суммы образовывали колонки
			// Пример:
			// Пакет*****************     1.50
			// (символом * показаны добавленные пробелы)
			
			Наименование = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(
				ДанныеСтроки.Наименование,
				ДлинаНаименованиеОднойСтрокой,
				Пробел,
				"Справа");
			
		ИначеЕсли ДлинаНаименованиеОднойСтрокой >= МинимальнаяДлинаНаименования Тогда
			// Наименование длинное, но и обрезанное наименование получится достаточно понятным
			// Пример:
			// Жидкость для снятия ла   216.50
			Наименование = Лев(ДанныеСтроки.Наименование, ДлинаНаименованиеОднойСтрокой);
			
		Иначе
			// С учетом места, оставшегося под наименование, оно будет нести мало информации, например:
			// Прибор оц x2  48 151 623 420.00
			
			// Поэтому разбиваем на две строки:
			// Прибор оценки проходимости Марс
			//           x2  48 151 623 420.00
			
			ТелоЧека.Добавить(Лев(ДанныеСтроки.Наименование, ДлинаСтрокиЧека));
			Наименование = СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов(Пробел, ДлинаНаименованиеОднойСтрокой);
		КонецЕсли;
		
		СтрокаНаименованиеКоличествоСумма = Новый Массив;
		СтрокаНаименованиеКоличествоСумма.Добавить(Наименование);
		СтрокаНаименованиеКоличествоСумма.Добавить(Количество);
		СтрокаНаименованиеКоличествоСумма.Добавить(Сумма);
		ТелоЧека.Добавить(СтрСоединить(СтрокаНаименованиеКоличествоСумма));
		
		Если ПоказыватьНДС Тогда
			СтавкаНДС = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(
				ДанныеСтроки.СтавкаНДС,
				ДлинаСтрокиЧека,
				Пробел);
			ТелоЧека.Добавить(СтавкаНДС);
		КонецЕсли;
		
	КонецЦикла;
	Возврат Новый Структура("ТелоЧека, ЕстьКоличество, МаксимальнаяДлинаСумма", ТелоЧека, ЕстьКоличество, МаксимальнаяДлинаСумма);
	
КонецФункции

Функция ПредставлениеОрганизацииТекст(ПоставщикНаименование, ПоставщикИНН, ДлинаСтрокиЧека)
	
	ПредставлениеОрганизацииТекст = "";
	ПредставлениеОрганизации = Новый Массив;
	Если Не ПустаяСтрока(ПоставщикНаименование) Тогда
		ПредставлениеОрганизации.Добавить(ПоставщикНаименование);
	КонецЕсли;
	Если Не ПустаяСтрока(ПоставщикИНН) Тогда
		ПредставлениеОрганизации.Добавить(СтрШаблон(НСтр("ru = 'ИНН %1'"), ПоставщикИНН));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПредставлениеОрганизации) Тогда
		ПредставлениеОрганизацииТекст = СтрСоединить(ПредставлениеОрганизации, " ");
		Если СтрДлина(ПредставлениеОрганизацииТекст) > ДлинаСтрокиЧека
			И ПредставлениеОрганизации.Количество() > 1 Тогда
			// Слишком длинное представление, его можно разбить на строки
			ПредставлениеОрганизацииТекст = СтрСоединить(ПредставлениеОрганизации, Символы.ПС);
		КонецЕсли;
	КонецЕсли;
	Возврат ПредставлениеОрганизацииТекст;
	
КонецФункции

Функция СтрокаИтогоСумма(СуммаДокумента, ДлинаСтрокиЧека, Пробел)
	
	СтрокаИтогоСумма = Новый Массив;
	ЗаголовокИтого = НСтр("ru = 'ИТОГ:'"); // см. Приказ ФНС ММВ-7-20/229@ от 21.03.2017, Таблица 4 "Перечень реквизитов ФД"
	СтрокаИтогоСумма.Добавить(ЗаголовокИтого);
	ДлинаСумма = ДлинаСтрокиЧека - СтрДлина(ЗаголовокИтого);
	Сумма = ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаДокумента);
	СтрокаИтогоСумма.Добавить(СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Сумма, ДлинаСумма, Пробел));
	Возврат СтрСоединить(СтрокаИтогоСумма);
	
КонецФункции

Функция СтрокаИтогоНДС(Строка, ДлинаСтрокиЧека, Пробел)
	
	СтрокаИтогоНДС = Новый Массив;
	Если Строка.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС
		Или Не ЗначениеЗаполнено(Строка.СтавкаНДС) Тогда
		ЗаголовокИтого = НСтр("ru = 'НДС не облагается'");
		Сумма = Строка.Сумма; // показываем какая сумма не облагается
	Иначе
		ЗаголовокИтого = СтрШаблон(НСтр("ru = 'НДС по ставке %1'"), Строка.СтавкаНДС);
		Сумма = Строка.СуммаНДС;
	КонецЕсли;
	СтрокаИтогоНДС.Добавить(ЗаголовокИтого);
	ДлинаСумма = ДлинаСтрокиЧека - СтрДлина(ЗаголовокИтого);
	Сумма = ОбщегоНазначенияБПВызовСервера.ФорматСумм(Сумма);
	СтрокаИтогоНДС.Добавить(СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Сумма, ДлинаСумма, Пробел));
	
	Возврат СтрСоединить(СтрокаИтогоНДС);
	
КонецФункции

Функция ТекстЗапросаДляФормированияПечатнойФормыКассовыхЧеков()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АвансовыйОтчетТовары.КассовыйЧек КАК КассовыйЧек,
	|	АвансовыйОтчетТовары.Ссылка КАК ДокументСсылка
	|ПОМЕСТИТЬ КассовыеЧеки
	|ИЗ
	|	Документ.АвансовыйОтчет.Товары КАК АвансовыйОтчетТовары
	|ГДЕ
	|	АвансовыйОтчетТовары.Ссылка В(&МассивОбъектов)
	|	И АвансовыйОтчетТовары.КассовыйЧек <> ЗНАЧЕНИЕ(Документ.КассовыйЧекПодотчетногоЛица.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АвансовыйОтчетПрочее.КассовыйЧек,
	|	АвансовыйОтчетПрочее.Ссылка
	|ИЗ
	|	Документ.АвансовыйОтчет.Прочее КАК АвансовыйОтчетПрочее
	|ГДЕ
	|	АвансовыйОтчетПрочее.Ссылка В(&МассивОбъектов)
	|	И АвансовыйОтчетПрочее.КассовыйЧек <> ЗНАЧЕНИЕ(Документ.КассовыйЧекПодотчетногоЛица.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПутевойЛистТопливо.КассовыйЧек,
	|	ПутевойЛистТопливо.Ссылка
	|ИЗ
	|	Документ.ПутевойЛист.Топливо КАК ПутевойЛистТопливо
	|ГДЕ
	|	ПутевойЛистТопливо.Ссылка В(&МассивОбъектов)
	|	И ПутевойЛистТопливо.КассовыйЧек <> ЗНАЧЕНИЕ(Документ.КассовыйЧекПодотчетногоЛица.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасходыПредпринимателяРасходы.КассовыйЧек,
	|	РасходыПредпринимателяРасходы.Ссылка
	|ИЗ
	|	Документ.РасходыПредпринимателя.Расходы КАК РасходыПредпринимателяРасходы
	|ГДЕ
	|	РасходыПредпринимателяРасходы.Ссылка В(&МассивОбъектов)
	|	И РасходыПредпринимателяРасходы.КассовыйЧек <> ЗНАЧЕНИЕ(Документ.КассовыйЧекПодотчетногоЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КассовыеЧеки.ДокументСсылка КАК ДокументСсылка,
	|	КассовыеЧеки.ДокументСсылка.Номер КАК НомерДокумента,
	|	КассовыеЧеки.ДокументСсылка.Дата КАК ДатаДокумента,
	|	КассовыеЧеки.КассовыйЧек.Ссылка КАК Ссылка,
	|	КассовыеЧеки.КассовыйЧек.СуммаДокумента КАК СуммаДокумента,
	|	КассовыеЧеки.КассовыйЧек.QRКод КАК QRКод,
	|	КассовыеЧеки.КассовыйЧек.Номер КАК Номер,
	|	КассовыеЧеки.КассовыйЧек.Дата КАК Дата,
	|	КассовыеЧеки.КассовыйЧек.ПоставщикНаименование КАК ПоставщикНаименование,
	|	КассовыеЧеки.КассовыйЧек.ПоставщикИНН КАК ПоставщикИНН,
	|	КассовыеЧеки.КассовыйЧек.Товары.(
	|		НомерСтроки КАК НомерСтроки,
	|		НоменклатураНаименование КАК Наименование,
	|		Количество КАК Количество,
	|		Сумма КАК Сумма,
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС
	|	) КАК Товары
	|ИЗ
	|	КассовыеЧеки КАК КассовыеЧеки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументСсылка
	|ИТОГИ ПО
	|	ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КассовыеЧеки.КассовыйЧек КАК КассовыйЧек,
	|	КассовыйЧекПодотчетногоЛицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(КассовыйЧекПодотчетногоЛицаТовары.СуммаНДС) КАК СуммаНДС,
	|	СУММА(КассовыйЧекПодотчетногоЛицаТовары.Сумма) КАК Сумма
	|ИЗ
	|	Документ.КассовыйЧекПодотчетногоЛица.Товары КАК КассовыйЧекПодотчетногоЛицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеЧеки КАК КассовыеЧеки
	|		ПО КассовыйЧекПодотчетногоЛицаТовары.Ссылка = КассовыеЧеки.КассовыйЧек
	|ГДЕ
	|	КассовыйЧекПодотчетногоЛицаТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				КассовыеЧеки.КассовыйЧек КАК КассовыйЧек
	|			ИЗ
	|				КассовыеЧеки КАК КассовыеЧеки)
	|
	|СГРУППИРОВАТЬ ПО
	|	КассовыйЧекПодотчетногоЛицаТовары.СтавкаНДС,
	|	КассовыеЧеки.КассовыйЧек";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗаполнитьТекущуюСтраницу(ТекущаяСтраница, ЛеваяКолонка, ПраваяКолонка)
	
	ТекущаяСтраница.Присоединить(ЛеваяКолонка);
	ТекущаяСтраница.Присоединить(ПраваяКолонка);
	ЛеваяКолонка.Очистить();
	ПраваяКолонка.Очистить();
	
КонецПроцедуры

Процедура ДобавитьНовуюСтраницу(ТабличныеДокументы, НомерСтраницы, ПервыйЧекНаСтранице)
	
	НоваяСтрокаТабличныеДокументы = ТабличныеДокументы.Добавить();
	НоваяСтрокаТабличныеДокументы.НомерСтраницы = НомерСтраницы + 1;
	НоваяСтрокаТабличныеДокументы.ТабличныйДокумент = Новый ТабличныйДокумент;
	НоваяСтрокаТабличныеДокументы.ПервыйЧекНаСтранице = ПервыйЧекНаСтранице;
	
КонецПроцедуры

Процедура ВывестиКолонтитул(ТабличныйДокумент, ОбластьМакетаКолонтитул, ОбластьМакетаПустаяСтрока, ПервыйЧекНаСтранице, ПоследнийЧекНаСтранице, КоличествоЧеков)

	ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьМакетаПустаяСтрока);
	ВыводимыеОбласти.Добавить(ОбластьМакетаКолонтитул);
	
	Пока ТабличныйДокумент.ПроверитьВывод(ВыводимыеОбласти) Цикл
		ТабличныйДокумент.Вывести(ОбластьМакетаПустаяСтрока);
	КонецЦикла;
	ОбластьМакетаКолонтитул.Параметры.ПервыйЧекНаСтранице = ПервыйЧекНаСтранице;
	ОбластьМакетаКолонтитул.Параметры.ПоследнийЧекНаСтранице = ПоследнийЧекНаСтранице;
	ОбластьМакетаКолонтитул.Параметры.КоличествоЧековВсего = КоличествоЧеков;
	ТабличныйДокумент.Вывести(ОбластьМакетаКолонтитул);
	
КонецПроцедуры

Процедура ВывестиОбласть(Область, ТабличныйДокументЛеваяКолонка, ТабличныйДокументПраваяКолонка, ЛеваяКолонка)
	
	Если ЛеваяКолонка Тогда
		ТабличныйДокументЛеваяКолонка.Вывести(Область);
	Иначе
		ТабличныйДокументПраваяКолонка.Вывести(Область);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСтавкуНДС(ЗаписьТовары, СтрокаТовары, КешСтавкиНДС)
	
	Если Не СтрокаТовары.Свойство("СтавкаНДС") Тогда
		Возврат;
	КонецЕсли;
	
	СтавкаНДС = КешСтавкиНДС[СтрокаТовары.СтавкаНДС];
	Если СтавкаНДС = Неопределено Тогда
		
		Если Метаданные.Перечисления.СтавкиНДС.ЗначенияПеречисления.Найти(СтрокаТовары.СтавкаНДС) = Неопределено Тогда
			СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
		Иначе
			СтавкаНДС = Перечисления.СтавкиНДС[СтрокаТовары.СтавкаНДС];
		КонецЕсли;
		
		КешСтавкиНДС.Вставить(СтрокаТовары.СтавкаНДС, СтавкаНДС);
		
	КонецЕсли;
	
	ЗаписьТовары.СтавкаНДС = СтавкаНДС;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
