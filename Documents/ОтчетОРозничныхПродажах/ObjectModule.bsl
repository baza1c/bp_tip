#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено Тогда
		Если ТипДанныхЗаполнения <> Тип("Структура")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
			ДокументОснование = ДанныеЗаполнения;
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
			И ДанныеЗаполнения.Свойство("Основание")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Основание.Метаданные()) Тогда
			ДокументОснование = ДанныеЗаполнения.Основание;
		КонецЕсли;
		
		Если ДокументОснование <> Неопределено Тогда
			ЗаполнитьПоДокументуОснованию(ДокументОснование);
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
			И ДанныеЗаполнения.Свойство("СуммаВключаетНДС") Тогда
			СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС
		Иначе
			СуммаВключаетНДС = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
				"ОсновнойВариантРасчетаИсходящегоНДС") = Перечисления.ВариантыРасчетаНДС.НДСВСумме;
		КонецЕсли;
	Иначе
		СуммаВключаетНДС = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
			"ОсновнойВариантРасчетаИсходящегоНДС") = Перечисления.ВариантыРасчетаНДС.НДСВСумме;
	КонецЕсли;
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения, Истина);

	РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "ТипСклада, ТипЦенРозничнойТорговли");

	// Склад может заполниться по умолчанию значением, которое не должно выбираться
	Если ЗначениеЗаполнено(Склад) Тогда
		Если ВидОперации = Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетККМОПродажах Тогда
			Если РеквизитыСклада.ТипСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
				Склад = Справочники.Склады.ПустаяСсылка();
			КонецЕсли;
		Иначе
			Если РеквизитыСклада.ТипСклада <> Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
				Склад = Справочники.Склады.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		ТипЦен = РеквизитыСклада.ТипЦенРозничнойТорговли;
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
	ВалютаДокумента, Дата);
	
	КурсДокумента      = СтруктураКурсаВзаиморасчетов.Курс;
	КратностьДокумента = СтруктураКурсаВзаиморасчетов.Кратность;
	
	ЭтоОтчетПоНТТ = 
		ВидОперации = Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах;
	ЕстьСтрокиВТаблицеОплата = Оплата.Количество() > 0;
	
	Если ЭтоОтчетПоНТТ 
		и ЕстьСтрокиВТаблицеОплата Тогда
		Оплата.Очистить();
	КонецЕсли;
	
	Если ОбъектКопирования.ЕстьМаркируемаяПродукцияГИСМ Тогда
		Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "КиЗ_ГИСМ");
	КонецЕсли;
	
	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Дата);
		
	СведенияПрослеживаемости.Очистить();
	РаботаСНоменклатуройБП.ОбновитьПризнакПрослеживаемости(Товары, ВедетсяУчетПрослеживаемыхТоваров, Дата);
	РаботаСНоменклатуройБП.ОбновитьПризнакПрослеживаемости(Возвраты, ВедетсяУчетПрослеживаемыхТоваров, Дата);

	РаботаСНоменклатуройБП.ОбновитьСодержаниеУслуг(АгентскиеУслуги, Дата);
	ЗаполнениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ПлательщикНДФЛ	= УчетнаяПолитика.ПлательщикНДФЛ(Организация, Дата);
	ПрименяетсяУСН 	= УчетнаяПолитика.ПрименяетсяУСН(Организация, Дата);
	ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, Дата);
	ПлательщикТуристическогоНалога = УчетнаяПолитика.ПлательщикТуристическогоНалога(Организация, Дата);
	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Организация, Дата);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// Реквизиты с проверкой по различным условиям
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Субконто");
	Если НЕ ПлательщикНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Сумма");
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Гостиница");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ВидЛьготыПоТуристическомуНалогу");

	ТребуетсяСчетРасходовПоОказаниюУслуг = РегистрыНакопления.РеализацияУслуг.ТребуетсяСчетРасходовПоОказаниюУслуг(Дата, Организация);
	
	СпособОценкиТоваровВРознице = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата);
	УчетПоПродажнойСтоимости    = СпособОценкиТоваровВРознице = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;
	
	ТипСклада	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ТипСклада");
	
	НТТПоПродажнойСтоимости = УчетПоПродажнойСтоимости
		И ВидОперации = Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах;

	// Проверка: сумма безналичных оплат не должна превышать сумму по реализации товаров и услуг
	СуммаВыручки = Товары.Итог("Сумма") + ?(СуммаВключаетНДС, 0, Товары.Итог("СуммаНДС"))
		+ АгентскиеУслуги.Итог("Сумма") + ?(СуммаВключаетНДС, 0, АгентскиеУслуги.Итог("СуммаНДС"))
		+ ПодарочныеСертификаты.Итог("Сумма") 
		+ Предоплаты.Итог("Сумма");
	
	Если Оплата.Итог("СуммаОплаты") > СуммаВыручки  Тогда
		
		ТекстОписаниеОшибки = НСтр("ru = 'Сумма безналичных оплат превышает сумму выручки от реализации!'");
		ТекстСообщения      = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Список", "Корректность",,, НСтр("ru = 'Безналичные оплаты'"), ТекстОписаниеОшибки);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Оплата", "Объект", Отказ);
		
	КонецЕсли;
	
	// Проверка: сумма безналичных возвратов не должна превышать сумму возвратам
	СуммаВыручкиВозврат = Возвраты.Итог("Сумма") + ?(СуммаВключаетНДС, 0, Возвраты.Итог("СуммаНДС"))+Предоплаты.Итог("СуммаВозврат");
	
	Если ВозвратОплаты.Итог("СуммаОплаты") > СуммаВыручкиВозврат  Тогда
		
		ТекстОписаниеОшибки = НСтр("ru = 'Сумма возврата безналичных оплат превышает сумму возвращенной выручки от реализации!'");
		ТекстСообщения      = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Список", "Корректность",,, НСтр("ru = 'Возврат безналичных оплат'"), ТекстОписаниеОшибки);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ВозвратОплаты", "Объект", Отказ);
		
	КонецЕсли;
	
	Если НЕ ДеятельностьНаПатенте Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Патент");
	КонецЕсли;
	
	// Проверка ТЧ Товары
	ОсновныеУслугиРазмещения = ТуристическийНалогСервер.МассивУслугРазмещения();
	МассивНоменклатуры     = ОбщегоНазначения.ВыгрузитьКолонку(Товары, "Номенклатура", Истина);
	ПараметрыПроверки = ТуристическийНалогСервер.НовыйПараметрыПроверкиЛьготы();
	ПараметрыПроверки.МассивУслуг = ОсновныеУслугиРазмещения; 
    ПараметрыПроверки.ИмяТабличнойЧасти = "Товары";

	ОбщегоНазначенияБПКлиентСервер.УдалитьНеЗаполненныеЭлементыМассива(МассивНоменклатуры);
	
	РеквизитыНоменклатуры  = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивНоменклатуры, "Услуга, УслугаРазмещения");
	
	ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();

	ИменаПолей = Новый Структура;
	ИменаПолей.Вставить("Номенклатура", НСтр("ru = 'Номенклатура'"));
	ИменаПолей.Вставить("Количество", НСтр("ru = 'Количество'"));
	ИменаПолей.Вставить("Сумма",        НСтр("ru = 'Сумма'"));
	ИменаПолей.Вставить("СтавкаНДС", НСтр("ru = '% НДС'"));
	ИменаПолей.Вставить("Гостиница", НСтр("ru = 'Гостиница'"));
	ИменаПолей.Вставить("ВидЛьготы", НСтр("ru = 'Льгота по тур.налогу'"));
	ИменаПолей.Вставить("ИдентификаторРодительскойСтроки", НСтр("ru = 'Связано с'"));
	
	Для Каждого СтрокаТаблицы Из Товары Цикл
		
		ЭтоУслуга = Ложь;
				
		СвойстваНоменклатуры = РеквизитыНоменклатуры[СтрокаТаблицы.Номенклатура];
		Если СвойстваНоменклатуры <> Неопределено Тогда
			ЭтоУслуга           = СвойстваНоменклатуры.Услуга;
		КонецЕсли;
		
		Если ПлательщикТуристическогоНалога Тогда 

			Если ЗначениеЗаполнено(СвойстваНоменклатуры.УслугаРазмещения)
				И Не ЗначениеЗаполнено(СтрокаТаблицы.Гостиница) Тогда
				СообщитьОНезаполненномПолеТаблицыТовары(
					СтрокаТаблицы.НомерСтроки, "Гостиница", ИменаПолей.Гостиница, Отказ);
			КонецЕсли;
			
			Если ОсновныеУслугиРазмещения.Найти(СвойстваНоменклатуры.УслугаРазмещения) <> Неопределено
				И Не ЗначениеЗаполнено(СтрокаТаблицы.ВидЛьготыПоТуристическомуНалогу) Тогда
				СообщитьОНезаполненномПолеТаблицыТовары(
					СтрокаТаблицы.НомерСтроки, "ВидЛьготыПоТуристическомуНалогу", ИменаПолей.ВидЛьготы, Отказ);
			КонецЕсли;
			
			Если СвойстваНоменклатуры.УслугаРазмещения = Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд
				И Не ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторРодительскойСтроки) Тогда
				СообщитьОНезаполненномПолеТаблицыТовары(
					СтрокаТаблицы.НомерСтроки, "ИдентификаторРодительскойСтроки", ИменаПолей.ИдентификаторРодительскойСтроки, Отказ);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторРодительскойСтроки)
				И СтрокаТаблицы.Гостиница <> ТуристическийНалогСервер.ГостиницаСвязаннойУслуги(Товары,
					СтрокаТаблицы.ИдентификаторРодительскойСтроки) Тогда 
					
				ТекстОшибки = НСтр("ru = 'Гостиница не совпадает с гостиницей в связанной строке'");
				СообщитьОНекорректномЗначенииТаблицыТовары(СтрокаТаблицы.НомерСтроки, "Гостиница", 
					ИменаПолей.Гостиница, ТекстОшибки, Отказ);
				
			КонецЕсли;
			
       		ПараметрыПроверки.УслугаРазмещения = СвойстваНоменклатуры.УслугаРазмещения; 
			ПараметрыПроверки.СтрокаУслуги = СтрокаТаблицы;
	        
			ТуристическийНалогСервер.ПроверитьКорректностьЗаполненияВидаЛьготы(ЭтотОбъект, ПараметрыПроверки, Отказ);

		КонецЕсли;
		
		Если Не ЭтоУслуга И СтрокаТаблицы.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
			// Не допускается ставка 0% по реализации товаров в рознице.
			Пояснение = НСтр("ru='Ставка 0% не применяется при реализации товаров в рознице'");
			СообщитьОНекорректномЗначенииТаблицыТовары(СтрокаТаблицы.НомерСтроки, "СтавкаНДС", ИменаПолей.СтавкаНДС, Пояснение, Отказ);
		КонецЕсли;
		
		Если НЕ ПлательщикНДС 
			И СтрокаТаблицы.Сумма = 0 
			И СтрокаТаблицы.СтавкаНДС <> Перечисления.СтавкиНДС.БезНДС Тогда
		
			СообщитьОНезаполненномПолеТаблицыТовары(СтрокаТаблицы.НомерСтроки, "Сумма", ИменаПолей.Сумма, Отказ);
		КонецЕсли;
		
		Если НТТПоПродажнойСТоимости 
			И ЗначениеЗаполнено(СтрокаТаблицы.СчетУчета) 
			И ПользовательУправляетСчетамиУчета Тогда
			
			СвойстваСчетаУчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетУчета);
			Если НЕ СвойстваСчетаУчета.Забалансовый Тогда
				ТекстОшибки = НСтр("ru = 'Продажи собственных товаров в НТТ при учете по продажной стоимости
				|должны отражаться документом ""Поступление наличных""!'");
				СообщитьОНекорректномЗначенииТаблицыТовары(СтрокаТаблицы.НомерСтроки, "Номенклатура", ИменаПолей.Номенклатура, ТекстОшибки, Отказ);
				МассивНепроверяемыхРеквизитов.Добавить("Товары.СчетУчета");
			КонецЕсли;
		КонецЕсли;
		
		// Для прослеживаемого товара обязательно указывается страна происхождения.
		Если СтрокаТаблицы.ПрослеживаемыйТовар 
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтранаПроисхождения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Страна происхождения'"),
				СтрокаТаблицы.НомерСтроки, "Товары");
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТаблицы.НомерСтроки, "СтранаПроисхождения");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если СтрокаТаблицы.ПрослеживаемыйКомплект Тогда
				СуммаБезНДС = ?(СуммаВключаетНДС, СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС, СтрокаТаблицы.Сумма);
				Отбор = Новый Структура("ИдентификаторСтроки", СтрокаТаблицы.ИдентификаторСтроки);
				СтрокиСРНПТ = СведенияПрослеживаемости.Выгрузить(Отбор);
				Если СуммаБезНДС < СтрокиСРНПТ.Итог("Сумма") Тогда 
					ТекстСообщения = НСтр("ru = 'Сумма без НДС по прослеживаемым комплектующим больше суммы без НДС по товару'");
					
					Поле = "Товары" + "["+ Формат(СтрокаТаблицы.НомерСтроки-1,"ЧГ=") + "].РНПТ";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
		КонецЕсли;
			
		Если НЕ ЭтоУслуга И СтрокаТаблицы.Количество = 0 Тогда
			СообщитьОНезаполненномПолеТаблицыТовары(СтрокаТаблицы.НомерСтроки, "Количество", ИменаПолей.Количество, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	МассивНепроверяемыхРеквизитов.Добавить("Возвраты.Субконто");
	
	Если ВидОперации = Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетККМОПродажах 
		И (ПрименяетсяУСН ИЛИ ПрименяетсяУСНПатент)
		И ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Организация, Дата) Тогда
		
		// В режиме отложенного проведения для УСН и патента не поддерживается:
		// 1. работа с эквайрингом
		// 2. одновременная продажа и оплата собственными сертификатами, которые учитываются по разным договорам.

		МассивВидовОплат = ОбщегоНазначения.ВыгрузитьКолонку(ПодарочныеСертификаты, "ВидОплаты", Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			МассивВидовОплат, 
			ОбщегоНазначения.ВыгрузитьКолонку(Оплата, "ВидОплаты", Истина), 
			Истина);

		СведенияОВидахОплаты = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
			МассивВидовОплат, "ТипОплаты, ДоговорКонтрагента");
		
		ДоговорыПоПроданнымСертификатам = Новый Соответствие;
		ДоговорыПоПринятымСертификатам	= Новый Соответствие;
		
		// Определяем различные договоры по проданным сертификатам.
		
		Для Каждого СтрокаТаблицы Из ПодарочныеСертификаты Цикл

			РеквизитыВидаОплаты = СведенияОВидахОплаты[СтрокаТаблицы.ВидОплаты];

			Если РеквизитыВидаОплаты = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если РеквизитыВидаОплаты.ТипОплаты = Перечисления.ТипыОплат.ПодарочныйСертификатСобственный Тогда
				ДоговорыПоПроданнымСертификатам.Вставить(РеквизитыВидаОплаты.ДоговорКонтрагента, Истина);
			КонецЕсли;
			
		КонецЦикла;

		// Определяем различные договоры по принятым сертификатам.

		БылоСообщениеПроЭквайринг = Ложь;

		Для Каждого СтрокаТаблицы Из Оплата Цикл
		
			РеквизитыВидаОплаты = СведенияОВидахОплаты[СтрокаТаблицы.ВидОплаты];

			Если РеквизитыВидаОплаты = Неопределено Тогда
				Продолжить;
			КонецЕсли;

			Если РеквизитыВидаОплаты.ТипОплаты = Перечисления.ТипыОплат.ПлатежнаяКарта
				ИЛИ РеквизитыВидаОплаты.ТипОплаты = Перечисления.ТипыОплат.БанковскийКредит Тогда

				Если БылоСообщениеПроЭквайринг Тогда
					Продолжить;
				КонецЕсли;

				ТекстСообщения = НСтр("ru = 'В разделе ""Администрирование"" - ""Проведение документов"" установлен режим ""Расчеты выполняются при закрытии месяца"".
					|В этом случае для организаций, применяющих упрощенную или патентную систему налогообложения, не поддерживается оплата платежными картами и банковскими кредитами.'");
				ПутьКТабличнойЧасти = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Оплата", СтрокаТаблицы.НомерСтроки, "ВидОплаты");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, ПутьКТабличнойЧасти, "Объект", Отказ);
				
				БылоСообщениеПроЭквайринг = Истина;
				
			КонецЕсли;
			
			Если РеквизитыВидаОплаты.ТипОплаты = Перечисления.ТипыОплат.ПодарочныйСертификатСобственный Тогда
				ДоговорыПоПринятымСертификатам.Вставить(РеквизитыВидаОплаты.ДоговорКонтрагента, Истина);
			КонецЕсли;
					
		КонецЦикла;
		
		Если ДоговорыПоПроданнымСертификатам.Количество() > 0
			И ДоговорыПоПринятымСертификатам.Количество() > 0 Тогда
			
			ДоговорыСовпадают = Ложь;

			Если ДоговорыПоПроданнымСертификатам.Количество() = 1 
				И ДоговорыПоПринятымСертификатам.Количество() = 1 Тогда
				Для Каждого КлючИЗначение Из ДоговорыПоПроданнымСертификатам Цикл
					Если ДоговорыПоПринятымСертификатам[КлючИЗначение.Ключ] <> Неопределено Тогда
						ДоговорыСовпадают = Истина;
					КонецЕсли;
				Конеццикла;
			КонецЕсли;
			
			Если НЕ ДоговорыСовпадают Тогда
				ТекстСообщения = НСтр("ru = 'В разделе ""Администрирование"" - ""Проведение документов"" установлен режим ""Расчеты выполняются при закрытии месяца"".
					|В этом случае для организаций, применяющих упрощенную или патентную систему налогообложения, 
					|документ ""Отчет о розничных продажах"" не поддерживает одновременно продажу и оплату cобственными подарочными сертификатами, которые учитываются на разных договорах.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ПодарочныеСертификаты", "Объект", Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Возвраты.ДатаРеализации");
	
	Для Каждого СтрокаТаблицы Из Возвраты Цикл
		
		Если СтрокаТаблицы.ПрослеживаемыйТовар И Не ЗначениеЗаполнено(СтрокаТаблицы.ДатаРеализации) Тогда
			Поле = "Возвраты[%1].ДатаРеализации";
			Поле = СтрШаблон(Поле, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Дата реализации'"),
				СтрокаТаблицы.НомерСтроки, "Возвраты");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		// Для прослеживаемого товара обязательно указывается страна происхождения.
		Если СтрокаТаблицы.ПрослеживаемыйТовар 
			И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтранаПроисхождения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Страна происхождения'"),
				СтрокаТаблицы.НомерСтроки, "Возвраты");
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Возвраты", СтрокаТаблицы.НомерСтроки, "СтранаПроисхождения");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если СтрокаТаблицы.ДатаРеализации > КонецДня(ЭтотОбъект.Дата) Тогда
			Поле = "Возвраты[%1].ДатаРеализации";
			Поле = СтрШаблон(Поле, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка","Корректность", НСтр("ru = 'Дата реализации'"), СтрокаТаблицы.НомерСтроки, "Возвраты",
				НСтр("ru='Дата реализации возвращаемых товаров не может быть позднее даты возврата'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если СтрокаТаблицы.ПрослеживаемыйКомплект Тогда
				СуммаБезНДС = ?(СуммаВключаетНДС, СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС, СтрокаТаблицы.Сумма);
				Отбор = Новый Структура("ИдентификаторСтроки", СтрокаТаблицы.ИдентификаторСтроки);
				СтрокиСРНПТ = СведенияПрослеживаемости.Выгрузить(Отбор);
				Если СуммаБезНДС < СтрокиСРНПТ.Итог("Сумма") Тогда 
					ТекстСообщения = НСтр("ru = 'Сумма без НДС по прослеживаемым комплектующим больше суммы без НДС по товару'");
					
					Поле = "Возвраты" + "["+ Формат(СтрокаТаблицы.НомерСтроки-1,"ЧГ=") + "].РНПТ";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если Не СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты) Тогда
		УчетДоходовИРасходовПредпринимателя.ПроверитьЗаполнениеСубконтоНоменклатурныеГруппы(
			ЭтотОбъект, 
			"СчетДоходов", 
			"Субконто", 
			НСтр("ru = 'Субконто'"), 
			"Товары", 
			НСтр("ru = 'Товары'"), 
			Отказ);
			
		УчетДоходовИРасходовПредпринимателя.ПроверитьЗаполнениеСубконтоНоменклатурныеГруппы(
			ЭтотОбъект, 
			"СчетДоходов", 
			"Субконто", 
			НСтр("ru = 'Субконто'"), 
			"Возвраты", 
			НСтр("ru = 'Возвраты'"), 
			Отказ);
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Для НТТ операции недоступны
	Если ВидОперации = Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах Тогда
		Возвраты.Очистить();
		ВозвратОплаты.Очистить();
	КонецЕсли; 
	
	СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(ЭтотОбъект, РежимЗаписи);
	
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорВТабличнойЧастиПередЗаписью(Предоплаты, ЭтотОбъект);
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорВТабличнойЧастиПередЗаписью(ЗачетАвансов, ЭтотОбъект);
	
	ЕстьПрослеживаемыйТовар = Ложь;
	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Дата);
		
	ПрослеживаемыйТовар        = Товары.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ПрослеживаемыйТоварВозврат = Возвраты.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ЕстьПрослеживаемыйТовар = ПрослеживаемыйТовар.Количество() <> 0 Или ПрослеживаемыйТоварВозврат.Количество() <> 0;
	
	ИменаТабличныхЧастей = Новый Массив;
	ИменаТабличныхЧастей.Добавить("Товары");
	ИменаТабличныхЧастей.Добавить("Возвраты");

	ОбщегоНазначенияБПКлиентСервер.ЗаполнитьИдентификаторыСтрок(ЭтотОбъект, ИменаТабличныхЧастей);
	
	Если ВедетсяУчетпрослеживаемыхТоваров Тогда
		ИменаТабличныхЧастей = Новый Массив;
		ИменаТабличныхЧастей.Добавить("Товары");
		ИменаТабличныхЧастей.Добавить("Возвраты");
		ОбщегоНазначенияБП.ОчиститьЛишниеДанныеПоПрослеживаемости(ЭтотОбъект, ИменаТабличныхЧастей);
	КонецЕсли;

	Если ЕстьПрослеживаемыйТовар Тогда
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ПрослеживаемостьБП.ПодобратьРНПТ(ЭтотОбъект, Отказ);
			Документы.ОтчетОРозничныхПродажах.ПодобратьРНПТВозврат(ЭтотОбъект, Отказ);
		КонецЕсли;
	Иначе
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	
	ИнтеграцияГИСМБП.УстановитьПризнакЕстьМаркируемаяПродукцияГИСМ(ЭтотОбъект);
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "Товары") 
		- УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "Возвраты") 
		+ УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "АгентскиеУслуги")
		+ ПодарочныеСертификаты.Итог("Сумма") 
		+ (Предоплаты.Итог("Сумма") - Предоплаты.Итог("СуммаВозврат")); // предоплаты на которые не были отгружены товары

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.ОтчетОРозничныхПродажах.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ
	
	// ПОДГОТОВКА ДАННЫХ ВОЗВРАТЫ
	
	// Таблицы возвращаемых и списываемых товаров с указанием партии возврата и себестоимости
	ТаблицыТовары = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуСписанныеТоварыСУчетомВозврата(
		ПараметрыПроведения.Товары, 
		ПараметрыПроведения.ВозвратыТоваровТаблицаТовары, ПараметрыПроведения.ВозвратыТоваровСчетаУчета,
		ПараметрыПроведения.ВозвратыТоваровНоменклатура, ПараметрыПроведения.ВозвратыТоваровРеквизиты,
		ПараметрыПроведения.Реквизиты, Отказ);
		
	// Таблицы сторно выручки от возврата товаров: собственных товаров и отдельно комиссионных
	ТаблицыВозврат = УчетДоходовРасходов.ПодготовитьТаблицыВыручкиОтРеализацииВозвратРозница(
		ПараметрыПроведения.ВозвратыТоваровТаблицаДокумента, 
		ТаблицыТовары.ВозвращенныеСписанныеТовары, ПараметрыПроведения.ВозвратыТоваровРеквизиты, Отказ);
		
	// Таблицы УСН
	СуммаСторноРасходов = 0;
	СуммаСторноНДС      = 0;
	СуммаПризнанияНДС   = 0;
	ТаблицаРасходовДляУСН = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуВозвратаРасходовУСН(
		ТаблицыТовары.ВозвращенныеСписанныеТовары,
		ПараметрыПроведения.УСНТаблицаСделки,
		ПараметрыПроведения.Реквизиты,
		СуммаСторноРасходов, // Рассчитывается и возвращается
		СуммаСторноНДС, // Рассчитывается и возвращается
		СуммаПризнанияНДС, // Рассчитывается и возвращается
		Отказ);
		
	// Учет доходов и расходов ИП
	ТаблицаВыручкиОтРеализацииИП = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуВыручкиОтРеализацииИП(
		ПараметрыПроведения.ВозвратыТоваровТаблицаДокумента, ПараметрыПроведения.Реквизиты);
	
	ТаблицыВозвратаТоваровИП = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицыВозвратаМПЗВРозницу(
		ТаблицаВыручкиОтРеализацииИП, ТаблицыТовары.ВозвращенныеСписанныеТовары, ПараметрыПроведения.ВозвратыТоваровРеквизиты); 

	// Подготовка данных реализация
	ТаблицаТоварыТуристическийНалог = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуТуристическийНалог(
		ПараметрыПроведения.ТуристическийНалогТаблицаТоваров, ПараметрыПроведения.ТуристическийНалогПредоплаты, 
		ПараметрыПроведения.ОплатаСчетов, ПараметрыПроведения.Реализация, ПараметрыПроведения.Реквизиты, Отказ); 

	// Таблицы выручки от реализации: собственных товаров и услуг и отдельно комиссионных
	ТаблицыРеализация = УчетДоходовРасходов.ПодготовитьТаблицыВыручкиОтРеализации(ПараметрыПроведения.Реализация,
		Неопределено, ТаблицыТовары.СписанныеТовары, ПараметрыПроведения.Реквизиты, Отказ);

	ТаблицаТоварыУслугиКомитентов = ТаблицыРеализация.ТоварыУслугиКомитентов;
	
	// АТТ 
	
	ТаблицаВзаиморасчетыВозвратПредоплаты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовПогашениеЗадолженности(
		ПараметрыПроведения.ВозвратВыручкаПредоплата, ПараметрыПроведения.РеквизитыВозвратПредоплаты, Отказ);

	
	// Таблица проданных подарочных сертификатов с зачетом принятых ранее в оплату
	ПроданныеСертификатыВзаиморасчеты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовПогашениеЗадолженности(
		ПараметрыПроведения.ВыручкаПоПодарочнымСертификатам, ПараметрыПроведения.Реквизиты, Отказ);
	
	// В безналичной оплате учтем взаиморасчеты по проданным ранее подарочным сертификатам и переплатам от эквайера
	БезналичныеОплатыВзаиморасчеты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовБезналичнаяРозничнаяВыручка(
		ПараметрыПроведения.БезналичныеОплаты,
		ПараметрыПроведения.РеквизитыБезналичныхОплат,
		Отказ);
		
	ТаблицыРаспределеннойВыручки = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицыРаспределенияВыручкиПоОплатам(
		ПараметрыПроведения.ВыручкаДляРаспределенияОплатыУСН,
		БезналичныеОплатыВзаиморасчеты,
		ПроданныеСертификатыВзаиморасчеты,
		ПараметрыПроведения.Реквизиты,
		Отказ);
	
	ТаблицаБезналичныеОплаты    = ТаблицыРаспределеннойВыручки.БезналичныеОплаты;
	ТаблицаПроданныеСертификаты = ТаблицыРаспределеннойВыручки.ПроданныеСертификаты;
	ТаблицаНаличнаяОплата       = ТаблицыРаспределеннойВыручки.ТаблицаНаличнаяОплата;
	ТаблицаВыручкаУСН           = ТаблицыРаспределеннойВыручки.ТаблицаВыручка;
	
	
	ТаблицыРаспределеннойВыручкиВозврат = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуВозвратаВыручки(
		ПараметрыПроведения.ВыручкаДляРаспределенияВозвратовУСН,
		ПараметрыПроведения.ВозвратБезналичныхОплат,
		ПараметрыПроведения.Реквизиты,
		Отказ);
		
	ТаблицаПрочихРасчетовАТТ = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуПрочихРасчетовАТТ(
		ТаблицаБезналичныеОплаты,
		ПараметрыПроведения.РеквизитыБезналичныхОплат);
	
	// НТТ
	СтруктураТаблицВыручкиЗаМесяц = УчетДоходовРасходов.ПодготовитьТаблицыВыручкиЗаМесяц(ПараметрыПроведения.РеквизитыВыручки, Отказ);
	
	// Сводная таблица выручки
	ТаблицаВыручкиЗаМесяц = СтруктураТаблицВыручкиЗаМесяц.ТаблицаВыручкиЗаМесяц;
	// Выручка по документам и отражению в НУ при УСН
	ТаблицаВыручкиПоДокументамПлатежа = СтруктураТаблицВыручкиЗаМесяц.ТаблицаВыручкиПоДокументамПлатежа;
	
	ТаблицыБезналичнойВыручкиНТТ = Документы.ОтчетОРозничныхПродажах.ПодготовитьСтруктуруТаблицБезналичнойВыручкиНТТ(
		ПараметрыПроведения.РеквизитыВыручки, ТаблицаВыручкиЗаМесяц, Отказ);
	
	ТаблицаПрочихРасчетовНТТ              = ТаблицыБезналичнойВыручкиНТТ.ТаблицаПрочихРасчетовНТТ;
	ТаблицаНеоплаченнойБезналичнойВыручки = ТаблицыБезналичнойВыручкиНТТ.ТаблицаНеоплаченнойБезналичнойВыручки;
	
	// Таблица оплаченной выручки за месяц при учете в НТТ в ценах продажи,
	ТаблицаОплаченнойВыручкиУСН = УчетДоходовРасходов.ПодготовитьТаблицуОплаченнойВыручкиЗаМесяц(
		ПараметрыПроведения.РеквизитыВыручки, ТаблицаВыручкиПоДокументамПлатежа, ТаблицаНеоплаченнойБезналичнойВыручки, Отказ);
		
	// Таблица для сторнирования ранее отраженной документами ПКО выручки НТТ при учете по продажной стоимости 
	// в случае реализации товаров комитента
	ТаблицаСторноВыручки = УчетДоходовРасходов.ПодготовитьТаблицуСторноВыручкиНТТ(ПараметрыПроведения.Выручка,
		ПараметрыПроведения.РеквизитыВыручки, ТаблицаВыручкиЗаМесяц, Отказ);
	ТаблицаСторноВыручкиНДС = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуНДСДокументаСторноВыручки(
		ТаблицаСторноВыручки, ПараметрыПроведения.РеквизитыВыручки, Ссылка);

	Документы.РеализацияТоваровУслуг.ДобавитьКолонкуСодержание(ТаблицыРеализация.СобственныеТоварыУслуги);
	
	// Структура таблиц для отражения в налоговом учете УСН
	СтруктураТаблицУСН = Новый Структура();
	СтруктураТаблицУСН.Вставить("ТаблицаТМЦ",                ТаблицыТовары.СписанныеТовары);      // Общ
	
	СтруктураТаблицУСН.Вставить("ПроданныеСертификаты",      ТаблицаПроданныеСертификаты); // АТТ
	СтруктураТаблицУСН.Вставить("БезналичныеОплаты",         ТаблицаБезналичныеОплаты);    // АТТ
	СтруктураТаблицУСН.Вставить("ТаблицаВыручка",            ТаблицаВыручкаУСН);           // АТТ
	
	СтруктураТаблицУСН.Вставить("ТаблицаРасчетов",           ТаблицаПрочихРасчетовНТТ);    // НТТ
	СтруктураТаблицУСН.Вставить("ТаблицаОплаченнойВыручки",  ТаблицаОплаченнойВыручкиУСН); // НТТ в ценах продажи
	
	// Учет доходов и расходов ИП
	СписанныеМПЗ = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуСписанныеМПЗ(
		ТаблицыТовары.СписанныеТовары, ПараметрыПроведения.Реализация, ПараметрыПроведения.Реквизиты);
	
	ТаблицыСписанияТоваровИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыСписанияМПЗСУчетомВозврата(
		СписанныеМПЗ, ПараметрыПроведения.Реквизиты, ТаблицыВозвратаТоваровИП.СписокМПЗ, Отказ);
	
	ТаблицаОказаниеУслугИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОказаниеУслуг(
		ТаблицыРеализация.СобственныеТоварыУслуги, ПараметрыПроведения.Реквизиты);
	
	СтруктураТаблицИП = Документы.ОтчетОРозничныхПродажах.ПодготовитьСтруктуруТаблицИП(
		ПараметрыПроведения.ВыручкаДляРаспределенияОплатыИП, БезналичныеОплатыВзаиморасчеты, ПроданныеСертификатыВзаиморасчеты,
		ПараметрыПроведения.Реквизиты, ТаблицаВыручкиЗаМесяц);
	
	ТаблицаВзаиморасчетовИП = СтруктураТаблицИП.ТаблицаВзаиморасчетовИП;
	ТаблицаПрочихРасчетовИП = СтруктураТаблицИП.ТаблицаПрочихРасчетовИП;
	ТаблицаЗачтенныхОплатИП = СтруктураТаблицИП.ТаблицаЗачтенныхОплатИП;
	
	// Реализации, оплаченные проданными подарочными сертификатами
	ТаблицаОплаченныеПродажиИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОплатыПокупателя(
		ТаблицаЗачтенныхОплатИП, ПараметрыПроведения.Реквизиты);
		
	СобственныеТоварыУслугиНДСВозврат = ?(ТаблицыВозврат.СобственныеТоварыУслуги <> Неопределено,
		ТаблицыВозврат.СобственныеТоварыУслуги.Скопировать(), Неопределено);

	Документы.ОтчетОРозничныхПродажах.ПроставитьСторноТаблиц(
		ТаблицыТовары.ВозвращенныеСписанныеТовары, ТаблицыВозврат.СобственныеТоварыУслуги, 
		ТаблицыВозврат.ТоварыУслугиКомитентов, ТаблицыВозврат.РеализованныеТоварыКомитентов);
	
	// Учет НДС
	СобственныеТоварыУслугиНДСВозврат = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуСобственныеТоварыУслугиНДСВозврат(
		СобственныеТоварыУслугиНДСВозврат, ПараметрыПроведения.ВозвратыНДС);
	
	ТаблицаПрочихРасчетов = ТаблицаПрочихРасчетовНТТ.Скопировать();
	
	ТаблицаПрочиеРасчетыВозвратОплаты = Документы.ОтчетОРозничныхПродажах.ПодготовитьТаблицуПрочиеРасчетыВозвратБезналичныхОплат(
		ПараметрыПроведения.Реквизиты, 
		ПараметрыПроведения.ВозвратыТоваровОплатаКартой, Отказ);
	
	Если ЗначениеЗаполнено(ТаблицаПрочихРасчетовАТТ) Тогда
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаПрочихРасчетовАТТ, ТаблицаПрочихРасчетов);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТаблицаПрочихРасчетовИП) Тогда
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаПрочихРасчетовИП, ТаблицаПрочихРасчетов);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТаблицаПрочиеРасчетыВозвратОплаты) Тогда
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаПрочиеРасчетыВозвратОплаты, ТаблицаПрочихРасчетов);
	КонецЕсли; 
	
	Если Не ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		ТаблицаСтатусовСчетов = СтатусыДокументов.ПодготовитьТаблицуСтатусовОплатыСчетов(
			ПараметрыПроведения.ОплатаСчетов, ПараметрыПроведения.Реквизиты);
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	Документы.ОтчетОРозничныхПродажах.СформироватьДвиженияТуристическийНалог(
		ТаблицаТоварыТуристическийНалог,
		ПараметрыПроведения.Реквизиты,
		Движения,
		Отказ);
		
	// Движения возврат
	Документы.ОтчетОРозничныхПродажах.СформироватьДвиженияНДСВозвратТоваровВРознице(
		СобственныеТоварыУслугиНДСВозврат,
		ТаблицыТовары.ВозвращенныеСписанныеТовары,
		ПараметрыПроведения.ВозвратыТоваровТаблицаДвиженийНДС,
		ПараметрыПроведения.ВозвратыСГТД,
		ПараметрыПроведения.ВозвратТоваровРаздельныйУчетНДС,
		ПараметрыПроведения.Реквизиты,
		ПараметрыПроведения.ВозвратТоваровРаздельныйУчетНДСПоТекущемуДокументу,
		Движения,
		Отказ);
		
	Документы.ОтчетОРозничныхПродажах.СформироватьДвиженияВозвратОплаты(
		ПараметрыПроведения.ВозвратОплатыНаличные,
		ПараметрыПроведения.ВозвратБезналичныхОплат, 
		ПараметрыПроведения.Реквизиты, 
		Движения, 
		Отказ);
		
	УчетВзаиморасчетов.СформироватьТолькоДвиженияПоСчетамУСНЗачетАвансов(
		ТаблицыРаспределеннойВыручкиВозврат.ТаблицаБезналичнаяОплата, 
		ПараметрыПроведения.Реквизиты, 
		Движения, 
		Отказ);
		
	// Сторнируем ранее отгруженные товары:
	УчетТоваров.СформироватьДвиженияСписанияТоваровВозвратОтПокупателя(ТаблицыТовары.ВозвращенныеСписанныеТовары,
		ПараметрыПроведения.ВозвратыТоваровРеквизиты, Движения, Отказ);
	
	// Сторнируем выручку
	УчетДоходовРасходов.СформироватьДвиженияВозвратРеализации(ТаблицыВозврат.СобственныеТоварыУслуги,
		ТаблицыВозврат.ТоварыУслугиКомитентов, ТаблицыВозврат.РеализованныеТоварыКомитентов,
		ПараметрыПроведения.ВозвратыТоваровРеквизиты, Движения, Отказ);
		
	// Движения реализация
	УчетТоваров.СформироватьДвиженияСписаниеТоваров(ТаблицыТовары.СписанныеТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетВзаиморасчетов.СформироватьДвиженияПоПрочимРасчетам(ТаблицаПрочихРасчетов, Движения, Отказ);
	
	Документы.ОтчетОРозничныхПродажах.СформироватьДвиженияПоСчетамУСН(ПараметрыПроведения.Реквизиты,
		ПараметрыПроведения.ВыручкаДляРаспределенияОплатыУСН, ТаблицаПрочихРасчетовНТТ, Движения, Отказ);

	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансовКомитентов(ТаблицыРеализация.ТоварыУслугиКомитентов,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияРеализация(ТаблицыРеализация.СобственныеТоварыУслуги,
		ТаблицыРеализация.ТоварыУслугиКомитентов, ТаблицыРеализация.РеализованныеТоварыКомитентов,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияПереоценкаТоваровВРознице(ПараметрыПроведения.Переоценка,
		ТаблицыТовары.СписанныеТовары, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетДоходовРасходов.СформироватьДвиженияПереоценкаТоваровВРознице(ПараметрыПроведения.ПереоценкаВозвратов,
		ТаблицыТовары.ВозвращенныеСписанныеТовары, ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияРаспределениеРозничнойВыручки(ПараметрыПроведения.Выручка,
		ПараметрыПроведения.РеквизитыВыручки, ТаблицаВыручкиЗаМесяц, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияСторноРозничнойВыручки(ТаблицаСторноВыручки,
		ПараметрыПроведения.РеквизитыВыручки, Движения, Отказ);

	УчетВзаиморасчетов.СформироватьДвиженияПогашениеЗадолженности(
		ТаблицаПроданныеСертификаты,
		ПараметрыПроведения.Реквизиты,
		Движения, Отказ);
		
	УчетВзаиморасчетов.СформироватьДвиженияПогашениеЗадолженности(
		ТаблицаВзаиморасчетыВозвратПредоплаты,
		ПараметрыПроведения.РеквизитыВозвратПредоплаты,
		Движения, Отказ);
	
	УчетВзаиморасчетов.СформироватьДвиженияПоступленияОтРозничныхПокупателей(ТаблицаБезналичныеОплаты,
		ТаблицаНаличнаяОплата, ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	РегистрыНакопления.РеализацияУслуг.ДобавитьДвижения(
		Движения.РеализацияУслуг,
		ПараметрыПроведения.ТаблицаРеализацияУслуг,
		Неопределено, // Не надо пересчитывать по курсу аванса
		ПараметрыПроведения.Реквизиты);
		
	// Учет НДС
	УчетНДС.СформироватьДвиженияРеализацияТоваровУслуг(
		ТаблицыРеализация.СобственныеТоварыУслуги, ПараметрыПроведения.ТоварыНДС, ТаблицыТовары.СписанныеТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	// Сторнирование ранее начисленного НДС в НТТ при учете по продажной стоимости в случае реализации товаров комитента
	УчетНДС.СформироватьДвиженияРозничнаяВыручка(ТаблицаСторноВыручкиНДС, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетНДС.СформироватьДвиженияРеализацияКомиссионныхТоваров(
		ПараметрыПроведения.НДСТоварыНаКомиссииРеализация,
		ТаблицаТоварыУслугиКомитентов,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Прослеживаемость
	ПрослеживаемостьБП.СформироватьДвиженияРеализацияТоваров(
		ПараметрыПроведения.ПрослеживаемыеТовары,
		ПараметрыПроведения.ПрослеживаемыеОперации,
		ПараметрыПроведения.Реквизиты,
		Движения);
		
	ПрослеживаемостьБП.СформироватьДвиженияПоступлениеТоваров(
		ПараметрыПроведения.ПрослеживаемыеТоварыВозврат,
		ПараметрыПроведения.ПрослеживаемыеОперацииВозврат,
		Неопределено,
		ПараметрыПроведения.Реквизиты,
		Движения);
		
	// Движения по расходам УСН возвраты со сделки + КУДиР
	Документы.ОтчетОРозничныхПродажах.СформироватьДвиженияВозвратПоРегистрамУСН(
		ТаблицаРасходовДляУСН,
		ПараметрыПроведения.Реквизиты,
		СуммаСторноРасходов,
		СуммаСторноНДС,
		СуммаПризнанияНДС,
		Движения, Отказ);
		
	Документы.ОтчетОРозничныхПродажах.СформироватьДвиженияКУДИРВозвратПокупателя(
		ПараметрыПроведения.ДвиженияВозвратКУДиР,
		ПараметрыПроведения.ДвиженияВозвратКУДиРПатент,
		ТаблицыРаспределеннойВыручкиВозврат.ТаблицаНаличнаяОплата,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Если НЕ Отказ И Движения.РасходыПриУСН.Количество()>0 Тогда
		Движения.РасходыПриУСН.Записать(Истина);
		Движения.РасходыПриУСН.Записывать = Ложь;
	КонецЕсли;
		
	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект, СтруктураТаблицУСН);
	
	СтатусыДокументов.СформироватьДвиженияОплатаСчетов(
		ПараметрыПроведения.ОплатаСчетов, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	Если Не ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		СтатусыДокументов.СформироватьДвиженияСтатусовДокументов(
			ТаблицаСтатусовСчетов, ПараметрыПроведения.Реквизиты);
	КонецЕсли;
	
	// Учет доходов и расходов ИП
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияВозвратМПЗОтПокупателя(
		ТаблицыВозвратаТоваровИП, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	ТаблицаИПМПЗОтгруженные	= УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияСписаниеМПЗ(
		ТаблицыСписанияТоваровИП,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	ТаблицаИПМПЗОтгруженныеУслуги	= УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияОказаниеУслуг(
		ТаблицаОказаниеУслугИП,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаИПМПЗОтгруженныеУслуги, ТаблицаИПМПЗОтгруженные);
	
	ТаблицаИПМПЗОтгруженные.Свернуть(
		"НоменклатурнаяГруппа, ХарактерДеятельности, ВидМПЗ, Номенклатура, Партия, ДокументОплаты, ДокументОтгрузки", 
		"Количество, Сумма, НДС, Выручка, НДСНачисленный");
		
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияЗачетОплатыПокупателя(
		ТаблицаИПМПЗОтгруженные,
		ТаблицаВзаиморасчетовИП,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетДоходовИРасходовПредпринимателя.ЗачетАвансаПоСертификатамИППрочиеДоходы(
		СтруктураТаблицИП.ТаблицаПрочиеДоходыИП,
		ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ПараметрыПроведения.Реквизиты[0]),
		Движения,
		Отказ);
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияОплатаПокупателя(
		ТаблицаОплаченныеПродажиИП,
		ПараметрыПроведения.Реквизиты,
		Движения, Отказ);
	
	// Переоценка валютных остатков - после формирования проводок всеми другими механизмами
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкаВалютныхОстатковПоПроводкамДокумента(
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияРасчетПереоценкиВалютныхСредств(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Отложенные расчеты с контрагентами.
	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентами(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);

	// Регистрация в последовательности
	Документы.ОтчетОРозничныхПродажах.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(
		ЭтотОбъект,
		ПараметрыПроведения,
		ТаблицыТовары.СписанныеТовары,
		Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


Процедура СообщитьОНезаполненномПолеТаблицыТовары(НомерСтроки, ИмяПоля, ЗначениеПоля, Отказ)

	ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Заполнение",
		ЗначениеПоля, НомерСтроки, НСтр("ru = 'Товары'"));

	Поле = "Товары[" + Формат(НомерСтроки - 1, "ЧН=0; ЧГ=") + "]." + ИмяПоля;
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);

КонецПроцедуры

Процедура СообщитьОНекорректномЗначенииТаблицыТовары(НомерСтроки, ИмяПоля, ЗначениеПоля, ТекстОшибки, Отказ)

	ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Корректность",
		ЗначениеПоля, НомерСтроки, НСтр("ru = 'Товары'"), ТекстОшибки);

	Поле = "Товары[" + Формат(НомерСтроки - 1, "ЧН=0; ЧГ=") + "]." + ИмяПоля;
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);

КонецПроцедуры
 
// Заполнение документа по инвентаризации товаров на розничном складе
//
Процедура ЗаполнитьПоДокументуОснованию(Основание)

	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе") Тогда

		// Заполнение шапки
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание, "Организация,Склад,Дата");

		ИнвентаризацияТоваровНаСкладе = Основание;
		
		РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "ТипЦенРозничнойТорговли, ТипСклада");

		Если ЗначениеЗаполнено(РеквизитыСклада.ТипЦенРозничнойТорговли) Тогда
			ТипЦен = РеквизитыСклада.ТипЦенРозничнойТорговли;
		КонецЕсли;

		СуммаВключаетНДС = Истина;

		Если РеквизитыСклада.ТипСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка Тогда
			ВидОперации = Перечисления.ВидыОперацийОтчетОРозничныхПродажах.ОтчетНТТОПродажах;
		КонецЕсли;

		// Заполнение ТЧ Товары
		Документы.ОтчетОРозничныхПродажах.ЗаполнитьТоварыПоИнвентаризацииТоваров(ЭтотОбъект, Основание);

	КонецЕсли;

КонецПроцедуры

#КонецЕсли