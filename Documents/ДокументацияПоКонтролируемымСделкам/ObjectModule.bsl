#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьЛишниеДанныеМетодовЦенообразования();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	// При копировании не нужно сохранять приложенные документы.
	// Т.к. файл из ФайлыПриложения ссылается на присоединенные файлы другого объекта.
	Приложения.Очистить();
	ФайлыПриложения.Очистить();
	
	ЗаполнениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтефрейс

Функция ВыгрузитьДокумент(УникальныйИдентификатор) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОсновныеСведенияВыгрузки = Документы.ДокументацияПоКонтролируемымСделкам.ОсновныеСведенияДокументацииДляВыгрузки(Ссылка);
	
	Если ОсновныеСведенияВыгрузки.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ВызватьИсключение "Формат ФНС не предусматривает передачу документации по контролируемым сделкам физическими лицами";
	КонецЕсли;
	
	ДанныеДокументации = Документы.ДокументацияПоКонтролируемымСделкам.ДанныеДокументацииДляВыгрузки(Ссылка);
	
	ФайлыВХранилище = КонтролируемыеСделкиВыгрузка.ФайлВыгрузкиДокументацииКС(ОсновныеСведенияВыгрузки, ДанныеДокументации, УникальныйИдентификатор);
	
	Возврат ФайлыВХранилище;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОчиститьЛишниеДанныеМетодовЦенообразования()
	
	ИдентификаторыМетодовЦенообразования = Новый Соответствие;
	Для Каждого СтрокаМетода Из МетодыЦенообразования Цикл
		ИдентификаторыМетодовЦенообразования.Вставить(СтрокаМетода.ИдентификаторСтроки, Новый Массив);
	КонецЦикла;
	
	// Удаляем те записи в СвойстваЦенообразования, в которых идентификатор указывает на метод, которого нет.
	// Если есть - то сохраняем для обработки.
	ИндексСтроки = СвойстваЦенообразования.Количество() - 1;
	Пока ИндексСтроки >=0 Цикл
		
		СтрокаСвойствЦенообразования = СвойстваЦенообразования[ИндексСтроки];
		
		ЗначениеСоответствия = ИдентификаторыМетодовЦенообразования.Получить(
			СтрокаСвойствЦенообразования.ИдентификаторСтрокиМетода);
		Если ЗначениеСоответствия = Неопределено Тогда
			СвойстваЦенообразования.Удалить(ИндексСтроки);
		Иначе
			ЗначениеСоответствия.Добавить(СтрокаСвойствЦенообразования);
		КонецЕсли;
		
		ИндексСтроки = ИндексСтроки - 1;
		
	КонецЦикла;
	
	// Нужно проверить, что для каждой строки МетодыЦенообразования
	// нужное количество строк свойств. Если их больше - то нужно удалить.
	// Такое может быть в случае, если пользователь что-то активно редактировал в таблице.
	Для Каждого СтрокаМетода Из МетодыЦенообразования Цикл
		
		СтрокиСвойств = ИдентификаторыМетодовЦенообразования.Получить(СтрокаМетода.ИдентификаторСтроки);
		Если Не ЗначениеЗаполнено(СтрокиСвойств) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаМетода.КодМетодаЦенообразования) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаМетода.КодМетодаЦенообразования <> КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов() Тогда
			// Для всех вариантов, кроме комплексного, в таблице свойств должна быть только одна строка в СвойстваЦенообразования.
			// И код метода ценообразования в ней должен совпадать с кодом в строке МетодыЦенообразования.
			// Все несовпадающие строки нужно удалить.
			СтрокаКодаМетодаНайдена = Ложь;
			Для Каждого СтрокаСвойств Из СтрокиСвойств Цикл
				Если Не СтрокаКодаМетодаНайдена
					И СтрокаМетода.КодМетодаЦенообразования = СтрокаСвойств.КодМетодаЦенообразования Тогда
					СтрокаКодаМетодаНайдена = Истина;
					Продолжить;
				КонецЕсли;
				
				СвойстваЦенообразования.Удалить(СвойстваЦенообразования.Индекс(СтрокаСвойств));
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли