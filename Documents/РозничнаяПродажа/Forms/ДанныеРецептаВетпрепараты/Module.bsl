
#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщегоНазначенияКлиентСервер.ДополнитьСписок(СписокТовары, Параметры.СписокТовары);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ДатаРецепта, НомерРецепта");
	ВидРецепта = ?(Параметры.ВетеринарноеТребование, "требование", "рецепт");
	
	Если СписокТовары.Количество() = 1 Тогда
		СписокТовары.ЗаполнитьПометки(Истина);
		Элементы.Товары.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Отказ = Истина;
	Для каждого СтрокаСписка Из СписокТовары Цикл
		Если Строкасписка.Пометка Тогда
			Отказ = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Отказ Тогда
		ОбщегоНазначения.СообщитьПользователю(НСТр("ru = 'Укажите препарат, на который выписан рецепт'"),,"СписокТоваров");
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы
	
&НаКлиенте
Процедура Готово(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура();
	Результат.Вставить("ДатаРецепта",  ДатаРецепта);
	Результат.Вставить("НомерРецепта", НомерРецепта);
	Результат.Вставить("ВетеринарноеТребование", ВидРецепта = "требование");
	Результат.Вставить("СписокТовары", ОбщегоНазначенияКлиентСервер.СкопироватьСписокЗначений(СписокТовары));
	
	Закрыть(Результат);
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	СписокТовары.ЗаполнитьПометки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделение(Команда)
	СписокТовары.ЗаполнитьПометки(Ложь);
КонецПроцедуры
#КонецОбласти

