
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Номенклатура = Параметры.Номенклатура;
	СчетУчета    = Параметры.СчетУчета;
	Количество   = Параметры.Количество; 
	
	Для Каждого СтрокаТаблицы Из Параметры.ТаблицаНоменклатураОприходование Цикл 
		Если (СтрокаТаблицы.Количество > 0) Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаНоменклатураОприходование.Добавить(), СтрокаТаблицы);
		КонецЕсли;	
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Параметры.НоменклатураОприходование) Тогда
		НайденныеСтроки = ТаблицаНоменклатураОприходование
			.НайтиСтроки(Новый Структура("Номенклатура", Параметры.НоменклатураОприходование));
		Если (НайденныеСтроки.Количество() > 0) Тогда	
			Элементы.ТаблицаНоменклатураОприходование.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;	
	КонецЕсли;
	
	УстановитьВидимостьСчетовУчета();
	
	ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	
	Если ПользовательУправляетСчетамиУчета Тогда
		ШаблонНадписьКоличество = НСтр("ru = 'Количество: %1 %2; Счет учета: %3'");
	Иначе
		ШаблонНадписьКоличество = НСтр("ru = 'Количество: %1 %2'");
	КонецЕсли;	
	
	НадписьКоличество = СтроковыеФункцииКлиентСервер
		.ПодставитьПараметрыВСтроку(ШаблонНадписьКоличество, Количество, Номенклатура.ЕдиницаИзмерения, СчетУчета);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицаНоменклатураОприходованиеВыбор

&НаКлиенте
Процедура ТаблицаНоменклатураОприходованиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ПроизвестиВыбор(ВыбраннаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)

	ПроизвестиВыбор(Элементы.ТаблицаНоменклатураОприходование.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив();  
	ЭлементыСчетов.Добавить("ТаблицаНоменклатураОприходованиеСчетУчета");
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизвестиВыбор(Знач ИдентификаторСтрокиВыбора)
	Если ИдентификаторСтрокиВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ДанныеВыбора = ТаблицаНоменклатураОприходование.НайтиПоИдентификатору(ИдентификаторСтрокиВыбора);
	Если ДанныеВыбора <> Неопределено Тогда
		ОповеститьОВыборе(Новый Структура("Номенклатура, Количество", ДанныеВыбора.Номенклатура, ДанныеВыбора.Количество));
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти
