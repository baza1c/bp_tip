#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс	

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область СчетаУчета

Процедура УстановитьПравилаЗаполненияСчетовУчета(Правила) Экспорт
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета", "ЗапасыКромеЗабалансовых");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Дата");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Организация");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Склад");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "ПодразделениеОрганизации");
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти 

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура выполняет заполнение табличной части по инвентаризации Товаров.
//
// Параметры:
//  ДокументОбъект    - заполняемый документ (Пересортица товаров)
//  ДокументОснование - ссылка на документ основание (инвентаризация Товаров).
//
Процедура ЗаполнитьТоварыПоИнвентаризацииТоваров(ДокументОбъект, ДокументОснование) Экспорт

	МассивИсклСчетов = Новый Массив;
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный);
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации);
	МассивИсклСчетов.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТекущийДокументСсылка"  , ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснованиеСсылка", ДокументОснование);
	Запрос.УстановитьПараметр("МассивИсклСчетов"       , МассивИсклСчетов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписаниеТоваров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ОтборСписаниеТоваров
	|ИЗ
	|	Документ.СписаниеТоваров КАК СписаниеТоваров
	|ГДЕ
	|	СписаниеТоваров.Проведен
	|	И СписаниеТоваров.ИнвентаризацияТоваровНаСкладе = &ДокументОснованиеСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПересортицаТоваров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ОтборПересортицаТоваров
	|ИЗ
	|	Документ.ПересортицаТоваров КАК ПересортицаТоваров
	|ГДЕ
	|	ПересортицаТоваров.Проведен
	|	И ПересортицаТоваров.ИнвентаризацияТоваровНаСкладе = &ДокументОснованиеСсылка
	|	И ПересортицаТоваров.Ссылка <> &ТекущийДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеСписания.Номенклатура КАК Номенклатура,
	|	ПредыдущиеСписания.СчетУчета КАК СчетУчета,
	|	СУММА(ПредыдущиеСписания.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ПредыдущиеСписания
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписаниеТоваровТовары.Номенклатура КАК Номенклатура,
	|		СписаниеТоваровТовары.СчетУчета КАК СчетУчета,
	|		СписаниеТоваровТовары.Количество КАК Количество
	|	ИЗ
	|		Документ.СписаниеТоваров.Товары КАК СписаниеТоваровТовары
	|	ГДЕ
	|		СписаниеТоваровТовары.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_ОтборСписаниеТоваров.Ссылка
	|				ИЗ
	|					ВТ_ОтборСписаниеТоваров)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПересортицаТоваровТовары.Номенклатура,
	|		ПересортицаТоваровТовары.СчетУчета,
	|		ПересортицаТоваровТовары.Количество
	|	ИЗ
	|		Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
	|	ГДЕ
	|		ПересортицаТоваровТовары.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_ОтборПересортицаТоваров.Ссылка
	|				ИЗ
	|					ВТ_ОтборПересортицаТоваров)) КАК ПредыдущиеСписания
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредыдущиеСписания.Номенклатура,
	|	ПредыдущиеСписания.СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияТоваровНаСкладеТовары.Номенклатура КАК Номенклатура,
	|	ИнвентаризацияТоваровНаСкладеТовары.СчетУчета КАК СчетУчета,
	|	СУММА(ИнвентаризацияТоваровНаСкладеТовары.КоличествоУчет - ИнвентаризацияТоваровНаСкладеТовары.Количество) КАК Количество,
	|	СУММА(ЕСТЬNULL(ВТ_ПредыдущиеСписания.Количество, 0)) КАК КоличествоСписанное
	|ПОМЕСТИТЬ ВТ_ДанныеИнвентаризации
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредыдущиеСписания КАК ВТ_ПредыдущиеСписания
	|		ПО ИнвентаризацияТоваровНаСкладеТовары.Номенклатура = ВТ_ПредыдущиеСписания.Номенклатура
	|			И ИнвентаризацияТоваровНаСкладеТовары.СчетУчета = ВТ_ПредыдущиеСписания.СчетУчета
	|ГДЕ
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка = &ДокументОснованиеСсылка
	|	И НЕ ИнвентаризацияТоваровНаСкладеТовары.СчетУчета В (&МассивИсклСчетов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияТоваровНаСкладеТовары.Номенклатура,
	|	ИнвентаризацияТоваровНаСкладеТовары.СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеИнвентаризации.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеИнвентаризации.СчетУчета КАК СчетУчета,
	|	ВТ_ДанныеИнвентаризации.Количество - ВТ_ДанныеИнвентаризации.КоличествоСписанное КАК Количество
	|ИЗ
	|	ВТ_ДанныеИнвентаризации КАК ВТ_ДанныеИнвентаризации
	|ГДЕ
	|	ВТ_ДанныеИнвентаризации.Количество - ВТ_ДанныеИнвентаризации.КоличествоСписанное > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ДанныеИнвентаризации.Номенклатура.Наименование";
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();

	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ДокументОбъект);
	
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина), ДанныеОбъекта, Ложь, Ложь);
	ВедетсяУчетПрослеживаемыхТоваров  = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(ДанныеОбъекта.Дата);
	
	СтрокиДляЗаполненияСчетовУчета = Новый Массив;

	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		СтрокаТабличнойЧасти = ДокументОбъект.Товары.Добавить();
		СтрокиДляЗаполненияСчетовУчета.Добавить(СтрокаТабличнойЧасти);

		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТовара);
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТовара.Номенклатура);
		Если СведенияОНоменклатуре = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.НомерГТД				= СведенияОНоменклатуре.НомерГТД;
		СтрокаТабличнойЧасти.СтранаПроисхождения	= СведенияОНоменклатуре.СтранаПроисхождения;
		Если ВедетсяУчетПрослеживаемыхТоваров Тогда 
			СтрокаТабличнойЧасти.ПрослеживаемыйТовар       = СведенияОНоменклатуре.ПрослеживаемыйТовар
				Или СведенияОНоменклатуре.ПрослеживаемыйКомплект;
			СтрокаТабличнойЧасти.ПрослеживаемыйКомплект    = СведенияОНоменклатуре.ПрослеживаемыйКомплект;
		Иначе
			СтрокаТабличнойЧасти.ПрослеживаемыйТовар       = Ложь;
			СтрокаТабличнойЧасти.ПрослеживаемыйКомплект    = Ложь;
		КонецЕсли;
		
	КонецЦикла;	

	СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиДляЗаполненияСчетовУчета, "Товары", ДокументОбъект, Документы.ПересортицаТоваров);
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В документе ""%1 № %2"" отсутствуют товары к зачету пересортицы или незачтенные ранее.'"), 
				ДокументОснование.Метаданные().Представление(),
				ДокументОснование.Номер);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ИнвентаризацияТоваровНаСкладе", "Объект");
	КонецЕсли;
	
КонецПроцедуры

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад КАК Склад,
	|	ЕСТЬNULL(Реквизиты.Склад.ТипСклада, НЕОПРЕДЕЛЕНО) КАК ТипСклада,
	|	ЕСТЬNULL(Реквизиты.Склад.ТипЦенРозничнойТорговли, НЕОПРЕДЕЛЕНО) КАК ТипЦенРозничнойТорговли,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.ИнвентаризацияТоваровНаСкладе КАК ИнвентаризацияТоваровНаСкладе,
	|	Реквизиты.ПриходоватьТоварыПоСебестоимостиСписания КАК ПриходоватьТоварыПоСебестоимостиСписания,
	|	Реквизиты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ПересортицаТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПриходоватьТоварыПоСебестоимостиСписания КАК ПриходоватьТоварыПоСебестоимостиСписания,
	|	Реквизиты.ТипСклада КАК ТипСклада
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Результат = Запрос.Выполнить();
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);
	
	Если Не УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	ПрименяетсяУСН					 = УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация, Реквизиты.Период);
	ПрименяетсяУСНДоходыМинусРасходы = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Реквизиты.Организация, Реквизиты.Период);
	ПлательщикНДФЛ					 = УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период);
	ВестиУчетПоВидамДеятельностиИП	 = УчетнаяПолитика.ВестиУчетПоВидамДеятельностиИП(Реквизиты.Организация, Реквизиты.Период);
	ПлательщикНДС                    = УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период);
	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		                             И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Реквизиты.Период);
	ОтражатьВРасходахУСН             = (Реквизиты.Период >= '20130101') И ПрименяетсяУСНДоходыМинусРасходы;
	СпособОценкиТоваровВРознице		 = УчетнаяПолитика.СпособОценкиТоваровВРознице(Реквизиты.Организация, Реквизиты.Период);
	ОсновнаяНоменклатурнаяГруппа	 = УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация, Реквизиты.Период);
	
	Запрос.УстановитьПараметр("ПлательщикНДФЛ",	                    ПлательщикНДФЛ);
	Запрос.УстановитьПараметр("ПлательщикНДС",                      ПлательщикНДС);
	Запрос.УстановитьПараметр("ВестиУчетПоВидамДеятельностиИП",	    ВестиУчетПоВидамДеятельностиИП);
	Запрос.УстановитьПараметр("ОсновнаяНоменклатурнаяГруппа",	    ОсновнаяНоменклатурнаяГруппа);
	Запрос.УстановитьПараметр("СпособОценкиТоваровВРознице",	    СпособОценкиТоваровВРознице);
	Запрос.УстановитьПараметр("ПрименяетсяУСН",	                    ПрименяетсяУСН);
	Запрос.УстановитьПараметр("СинонимТовары",                      НСтр("ru = 'Товары'"));
	Запрос.УстановитьПараметр("Счета10",                            БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Материалы));
	Запрос.УстановитьПараметр("ВалютаРеглУчета",                    Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ОтражатьВРасходахУСН",               ОтражатьВРасходахУСН);						
	Запрос.УстановитьПараметр("ТипСклада",		                    Реквизиты.ТипСклада);
	Запрос.УстановитьПараметр("СтатьяДоходовРасходовОприходование", КлассификаторыДоходовРасходов.ПредопределенныйЭлемент(
	                                                                  	Справочники.ПрочиеДоходыИРасходы, "ПересортицаТоваровОприходование"));
																		
	НомераТаблиц = Новый Структура; 
	
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)
	             + ТекстЗапросаСписаниеТоваров(НомераТаблиц)
				 + ТекстЗапросаОприходованиеТоваров(НомераТаблиц)
                 + ТекстЗапросаОприходованиеТоваровУСН(НомераТаблиц)
				 + ТекстЗапросаКнигаУчетаДоходовИРасходовУСН(НомераТаблиц)				 
                 + ТекстЗапросаСписаниеТоваровТорговаяНаценка(НомераТаблиц)
 				 + ТекстЗапросаСписаниеТоваровНДС(НомераТаблиц)
				 + ТекстЗапросаОприходованиеТоваровИП(НомераТаблиц)
				 + ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ВедетсяУчетПрослеживаемыхТоваров);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Если Не ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ПрослеживаемыеТовары",   Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации", Неопределено);
	КонецЕсли;
	
	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)

	// Временная таблица
	НомераТаблиц.Вставить("ВременнаяТаблицаТовары", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Количество КАК Количество,
	|	Товары.Сумма КАК Сумма,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.СчетУчета КАК СчетУчета,
	|	Товары.СчетУчета.Забалансовый КАК СчетУчетаЗабалансовый,
	|	Товары.НоменклатураОприходование КАК НоменклатураОприходование,
	|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются) КАК ОтражениеВУСН,
	|	Товары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	Товары.НомерГТД КАК НомерГТД,
	|	Товары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСписаниеТоваров(НомераТаблиц)
	
	НомераТаблиц.Вставить("СписаниеТоваровРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеТоваровТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	""Списание"" КАК ТипСписания,
	|	Реквизиты.Ссылка КАК ДокументРеализации,
	|	НЕОПРЕДЕЛЕНО КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	ЛОЖЬ КАК ДеятельностьНаПатенте,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.ПодразделениеОрганизации КАК КорПодразделение,
	|	Реквизиты.ПриходоватьТоварыПоСебестоимостиСписания КАК ПриходоватьТоварыПоСебестоимостиСписания,
	|	""Пересортица товаров"" КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	Реквизиты.Склад КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
	|	0 КАК Себестоимость,
	|	ТаблицаТовары.Количество КАК Количество,
	|	Реквизиты.ПодразделениеОрганизации КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКомиссии,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетАвансовСКомитентом,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетРасчетовСКомитентом,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаРасчетовСКомитентом,
	|	0 КАК СуммаРасчетовСКомитентом,
	|	ТаблицаТовары.СчетУчета КАК КорСчетСписания,
	|	ТаблицаТовары.НоменклатураОприходование КАК КорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура) КАК ВидКорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто3,
	|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК ТаблицаТовары
	|		ПО Реквизиты.Ссылка = ТаблицаТовары.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

Функция ТекстЗапросаОприходованиеТоваров(НомераТаблиц)
	
	НомераТаблиц.Вставить("ОприходованиеТоваровТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.НоменклатураОприходование КАК НоменклатураОприходование,
	|	Реквизиты.Склад КАК Склад,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.Количество КАК Количество,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК ТаблицаТовары
	|		ПО Реквизиты.Ссылка = ТаблицаТовары.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

Функция ТекстЗапросаКнигаУчетаДоходовИРасходовУСН(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыУСН", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	ЛОЖЬ КАК ОсобыйПорядокНалогообложения,
	|	СУММА(0) КАК СтрокаДокумента,
	|	СУММА(ТаблицаТовары.Сумма) КАК Графа4,
	|	СУММА(ТаблицаТовары.Сумма) КАК Графа5,
	|	СУММА(0) КАК Графа6,
	|	СУММА(0) КАК Графа7,
	|	СУММА(0) КАК НДС,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
	|	Реквизиты.ИнвентаризацияТоваровНаСкладе КАК ИнвентаризацияТоваровНаСкладе
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК ТаблицаТовары
	|		ПО Реквизиты.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	&ПрименяетсяУСН
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.Номер,
	|	Реквизиты.Организация,
	|	Реквизиты.Дата,
	|	Реквизиты.ИнвентаризацияТоваровНаСкладе,
	|	Реквизиты.Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаОприходованиеТоваровУСН(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыПоступлениеРасходовУСН", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	&ВалютаРеглУчета КАК Валюта,
	|	ЛОЖЬ КАК УчетАгентскогоНДС,
	|	ИСТИНА КАК ЭтоВозврат,
	|	ЛОЖЬ КАК РасходыПредпринимателя
	|ИЗ
	|	Документ.ПересортицаТоваров КАК Реквизиты
	|ГДЕ
	|	&ОтражатьВРасходахУСН
	|	И Реквизиты.Ссылка = &Ссылка"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	НомераТаблиц.Вставить("УСНТаблицаРасходов", НомераТаблиц.Количество());
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Номенклатура) КАК ВидРасхода,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.Ссылка КАК Партия,
	|	ТаблицаТовары.НоменклатураОприходование КАК ЭлементРасхода,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СчетУчета В (&Счета10)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СчетУчета В (&Счета10)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоМатериал,
	|	ТаблицаТовары.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	0 КАК НДС
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	НЕ ТаблицаТовары.СчетУчетаЗабалансовый
	|	И &ОтражатьВРасходахУСН"
	;

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции // ТекстЗапросаОприходованиеТоваровУСН()

Функция ТекстЗапросаОприходованиеТоваровИП(НомераТаблиц)
	
	НомераТаблиц.Вставить("ОприходованиеТоваровИПРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОприходованиеТоваровИПТаблица",   НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад.ТипСклада КАК ТипСклада,
	|	&СтатьяДоходовРасходовОприходование КАК СтатьяДоходов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения) КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	ИСТИНА КАК ПринятиеКНалоговомуУчету
	|ИЗ
	|	Документ.ПересортицаТоваров КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураОприходование КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
	|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
	|		ИНАЧЕ ТаблицаТовары.Номенклатура.НоменклатурнаяГруппа
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ТаблицаТовары.Ссылка КАК Партия,
	|	ТаблицаТовары.Ссылка КАК ДокументОплаты,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	&ПлательщикНДФЛ
	|	И НЕ(&СпособОценкиТоваровВРознице = ЗНАЧЕНИЕ(Перечисление.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости)
	|				И &ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.НеавтоматизированнаяТорговаяТочка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

Функция ТекстЗапросаСписаниеТоваровТорговаяНаценка(НомераТаблиц)

	НомераТаблиц.Вставить("СписаниеТоваровТорговаяНаценкаРеквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	&ВалютаРеглУчета КАК Валюта,
	|	1 КАК КурсВзаиморасчетов,
	|	1 КАК КратностьВзаиморасчетов,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.ПодразделениеОрганизации КАК КорПодразделение,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	Реквизиты.ТипЦенРозничнойТорговли КАК ТипЦенРозничнойТорговли,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТорговаяНаценкаАТТ) КАК КорСчетСписания
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСписаниеТоваровНДС(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТоварыНДС",    НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Неопределено КАК Контрагент,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтрокиДокумента,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.НоменклатураОприходование КАК НоменклатураОприходование,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТовары.НомерГТД КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.УчитываетсяВCтоимости) КАК НовыйСпособУчетаНДС
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	НЕ ТаблицаТовары.СчетУчетаЗабалансовый";
		
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ВедетсяУчетПрослеживаемыхТоваров)
	Если Не ВедетсяУчетПрослеживаемыхТоваров Тогда
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТовары",   НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПересортицаТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ПересортицаТоваровСведенияПрослеживаемости.Количество) КАК Количество,
	|	ПересортицаТоваровСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(ПересортицаТоваровСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ПересортицаТоваровТовары.Ссылка КАК ДокументОперации,
	|	НАЧАЛОПЕРИОДА(ПересортицаТоваровТовары.Ссылка.Дата, КВАРТАЛ) КАК ПериодОперации,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях) КАК ОтражениеВОтчетности,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.НедостачаВыявленнаяВХодеИнвентаризации) КАК КодОперации,
	|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт) КАК ТипДокументаВПрослеживаемости,
	|	ПересортицаТоваровТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ПересортицаТоваровТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	ПересортицаТоваровСведенияПрослеживаемости.Номенклатура КАК Комплектующие,
	|	ПересортицаТоваровСведенияПрослеживаемости.Сумма КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА ПересортицаТоваровТовары.ПрослеживаемыйКомплект
	|			ТОГДА ПересортицаТоваровСведенияПрослеживаемости.СтранаПроисхождения
	|		ИНАЧЕ ПересортицаТоваровТовары.СтранаПроисхождения
	|	КОНЕЦ КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ВТ_Прослеживаемость
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПересортицаТоваров.СведенияПрослеживаемости КАК ПересортицаТоваровСведенияПрослеживаемости
	|		ПО ПересортицаТоваровТовары.Ссылка = ПересортицаТоваровСведенияПрослеживаемости.Ссылка
	|			И ПересортицаТоваровТовары.ИдентификаторСтроки = ПересортицаТоваровСведенияПрослеживаемости.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПересортицаТоваровТовары.Ссылка = ДанныеПервичныхДокументов.Документ
	|			И ПересортицаТоваровТовары.Ссылка.Организация = ДанныеПервичныхДокументов.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПересортицаТоваров КАК ПересортицаТоваров
	|		ПО ПересортицаТоваровТовары.Ссылка = ПересортицаТоваров.Ссылка
	|ГДЕ
	|	ПересортицаТоваровТовары.ПрослеживаемыйТовар
	|	И ПересортицаТоваров.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПересортицаТоваровСведенияПрослеживаемости.РНПТ,
	|	ПересортицаТоваровТовары.Номенклатура,
	|	ПересортицаТоваровТовары.Ссылка,
	|	НАЧАЛОПЕРИОДА(ПересортицаТоваровТовары.Ссылка.Дата, КВАРТАЛ),
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """"),
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)),
	|	ПересортицаТоваровТовары.ИдентификаторСтроки,
	|	ПересортицаТоваровТовары.ПрослеживаемыйКомплект,
	|	ПересортицаТоваровСведенияПрослеживаемости.Номенклатура,
	|	ПересортицаТоваровСведенияПрослеживаемости.Сумма,
	|	ВЫБОР
	|		КОГДА ПересортицаТоваровТовары.ПрослеживаемыйКомплект
	|			ТОГДА ПересортицаТоваровСведенияПрослеживаемости.СтранаПроисхождения
	|		ИНАЧЕ ПересортицаТоваровТовары.СтранаПроисхождения
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.ПрослеживаемыйКомплект
	|			ТОГДА ВТ_Прослеживаемость.Комплектующие
	|		ИНАЧЕ ВТ_Прослеживаемость.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВТ_Прослеживаемость.Количество КАК Количество,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.ДокументОперации КАК ДокументОперации,
	|	ВТ_Прослеживаемость.ПериодОперации КАК ПериодОперации,
	|	ВТ_Прослеживаемость.НомерДокумента КАК НомерДокумента,
	|	ВТ_Прослеживаемость.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_Прослеживаемость.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
	|	ВТ_Прослеживаемость.Контрагент КАК Контрагент,
	|	ВТ_Прослеживаемость.КодОперации КАК КодОперации,
	|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
	|	ВТ_Прослеживаемость.СуммаБезНДС КАК СуммаБезНДС,
	|	ВТ_Прослеживаемость.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_Прослеживаемость.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДляВозврата,
	|	ВТ_Прослеживаемость.Комплектующие КАК Комплектующие,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.Номенклатура,
	|	ВТ_Прослеживаемость.РНПТ,
	|	ВТ_Прослеживаемость.Комплектующие,
	|	ВТ_Прослеживаемость.СтранаПроисхождения";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Процедура ДобавитьКолонкуСодержаниеУСН(ТаблицаЗначений) Экспорт

	Если ТаблицаЗначений.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(300));
	КонецЕсли;

	Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ИнвентаризацияТоваровНаСкладе) Тогда
			СтрокаТаблицы.Содержание = "Оприходованы излишки ТМЦ.";
		Иначе
			Содержание = "Оприходованы излишки, выявленные при проведении %1";
			Содержание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Содержание,
				ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(
					СтрокаТаблицы.ИнвентаризацияТоваровНаСкладе,
					"инвентаризации"));
			СтрокаТаблицы.Содержание = Содержание;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ПодготовитьТаблицуСписанныеТовары(ТаблицаСписанныеТовары, ПараметрыПроведения) Экспорт
	
	Реквизиты = ПараметрыПроведения.СписаниеТоваровРеквизиты[0];
	Если Не Реквизиты.ПриходоватьТоварыПоСебестоимостиСписания Тогда
		Возврат;
	КонецЕсли;	
	
	Для Каждого СтрокаТаблицы Из ТаблицаСписанныеТовары Цикл
		НайденнаяСтрока = ПараметрыПроведения.УСНТаблицаРасходов.Найти(СтрокаТаблицы.НомерСтроки, "НомерСтроки");
		Если НайденнаяСтрока <> Неопределено Тогда
			НайденнаяСтрока.Сумма = СтрокаТаблицы.СуммаСписания;	
		КонецЕсли;	
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаСписанныеТовары Цикл
		НайденнаяСтрока = ПараметрыПроведения.ОприходованиеТоваровИПТаблица.Найти(СтрокаТаблицы.НомерСтроки, "НомерСтроки");
		Если НайденнаяСтрока <> Неопределено Тогда
			НайденнаяСтрока.Сумма = СтрокаТаблицы.СуммаСписания;	
		КонецЕсли;	
	КонецЦикла;
	
	СуммаСписанияИтого = ТаблицаСписанныеТовары.Итог("СуммаСписания");
	Для Каждого СтрокаТаблицы Из ПараметрыПроведения.РеквизитыУСН Цикл
		СтрокаТаблицы.Графа4 = СуммаСписанияИтого;
		Если Не СтрокаТаблицы.ОсобыйПорядокНалогообложения Тогда
			СтрокаТаблицы.Графа5 = СуммаСписанияИтого;  
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры	

Функция ПодготовитьТаблицыМатериаловПродукцииИП(ТаблицаСписанныеМПЗ, ТаблицаРеквизиты) Экспорт
	Перем СтруктураТаблиц;
	
	СтруктураТаблиц = Новый Структура("ТаблицаМатериалов, ПолученоПродукции");
	
	Если Не ЗначениеЗаполнено(ТаблицаСписанныеМПЗ)
			Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	
	Если Не УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	МассивТиповЗатрат = Новый Массив;
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.СтатьиЗатрат"));
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
	МассивТиповЗатрат.Добавить(Тип("СправочникСсылка.РасходыБудущихПериодов"));
	ОписаниеТиповЗатрат = Новый ОписаниеТипов(МассивТиповЗатрат);
	
	ТаблицаМатериалов = ТаблицаСписанныеМПЗ.Скопировать();
	ТаблицаМатериалов.ЗаполнитьЗначения(ПланыСчетов.Хозрасчетный.ПрочиеРасходы, "КорСчетСписания");
	ТаблицаМатериалов.Колонки.КорСчетСписания.Имя = "СчетЗатрат";
	ТаблицаМатериалов.Колонки.Добавить("Продукция",            Новый ОписаниеТипов("СправочникСсылка.ПрочиеДоходыИРасходы"));
	ТаблицаМатериалов.Колонки.Добавить("НоменклатурнаяГруппа", Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
	ТаблицаМатериалов.Колонки.Добавить("СтатьяЗатрат",         ОписаниеТиповЗатрат);
	ТаблицаМатериалов.Колонки.Добавить("СчетДоходов",          Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаМатериалов.Колонки.Добавить("КоличествоПродукции",  ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	ОсновнаяНоменклатурнаяГруппа = УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация, Реквизиты.Период);
	
	МассивНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаМатериалов, "Номенклатура", Истина);
	СоответствиеНоменклатурныхГрупп = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивНоменклатуры, "НоменклатурнаяГруппа");
	
	ПрочиеДоходыИРасходыПересортицаТоваровОприходование = КлассификаторыДоходовРасходов
		.ПредопределенныйЭлемент(Справочники.ПрочиеДоходыИРасходы, "ПересортицаТоваровОприходование");
	
	Для Каждого Материал Из ТаблицаМатериалов Цикл
		
		НоменклатурнаяГруппа = СоответствиеНоменклатурныхГрупп.Получить(Материал.Номенклатура);
		Если Не ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
			НоменклатурнаяГруппа = ОсновнаяНоменклатурнаяГруппа;
		КонецЕсли;
		
		Материал.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
		Материал.Продукция            = ПрочиеДоходыИРасходыПересортицаТоваровОприходование;
		Материал.КоличествоПродукции  = Материал.Количество;
		
	КонецЦикла;
	
	СтруктураТаблиц.ТаблицаМатериалов =
		УчетДоходовИРасходовПредпринимателя.ДополнитьТаблицуСписанияМатериалов(ТаблицаМатериалов, ТаблицаРеквизиты);
	
	Если СтруктураТаблиц.ТаблицаМатериалов <> Неопределено Тогда
		СтруктураТаблиц.ПолученоПродукции = СтруктураТаблиц.ТаблицаМатериалов.Скопировать();
		СтруктураТаблиц.ПолученоПродукции.Колонки.СчетЗатрат.Имя = "СчетУчетаПродукции";
		СтруктураТаблиц.ПолученоПродукции.Свернуть("СчетУчетаПродукции, Продукция, НоменклатурнаяГруппа", "КоличествоПродукции");
	КонецЕсли;
	
	Возврат СтруктураТаблиц;

КонецФункции

Функция ПодготовитьТаблицуСписанияТорговойНаценкиАТТ(ТаблицаСписанныеТовары, ТаблицаСписанияТорговойНаценкиАТТ) Экспорт

	Для Каждого СтрокаТаблицы Из ТаблицаСписанныеТовары Цикл
		НайденнаяСтрока = ТаблицаСписанияТорговойНаценкиАТТ.Найти(СтрокаТаблицы.НомерСтроки, "НомерСтроки");
		Если НайденнаяСтрока <> Неопределено Тогда
			НайденнаяСтрока.КорСчетСписания = ПланыСчетов.Хозрасчетный.ТорговаяНаценкаАТТ;
			НайденнаяСтрока.ВидКорСубконто1 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
			НайденнаяСтрока.ВидКорСубконто2 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады;
			НайденнаяСтрока.ВидКорСубконто3 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
			НайденнаяСтрока.КорСубконто1    = СтрокаТаблицы.КорСубконто1;
			НайденнаяСтрока.КорСубконто2    = СтрокаТаблицы.Склад;
			НайденнаяСтрока.КорСубконто3    = СтрокаТаблицы.Партия;
		КонецЕсли;	
	КонецЦикла;

КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
КонецПроцедуры

#КонецОбласти

#КонецЕсли