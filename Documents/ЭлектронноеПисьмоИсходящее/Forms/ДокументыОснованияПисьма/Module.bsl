#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстВыбора;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресВХранилищеДокументовОснований = Параметры.АдресВХранилищеДокументовОснований;
	ДокументыОснования.Загрузить(ПолучитьИзВременногоХранилища(АдресВХранилищеДокументовОснований));
	
	Если Параметры.Свойство("ТолькоПросмотр") Тогда
		Элементы.ЗаписатьИЗакрыть.Доступность = Не Параметры.ТолькоПросмотр;
		Элементы.ДокументыОснования.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ВзаимодействияКлиентБП.ФормаОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора, КонтекстВыбора);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДокументыОснования

&НаКлиенте
Процедура ДокументыОснованияДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВзаимодействияКлиентБП.ПредметНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеРеквизитовФормы(Отказ);
	
	Если Не Отказ Тогда
		ЗаписатьОснованияВХранилище();
		Закрыть(КодВозвратаДиалога.OK);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитовФормы(Отказ)
	
	НомерСтроки = 0;
		
	Для Каждого СтрокаОснования Из ДокументыОснования Цикл
		НомерСтроки = НомерСтроки + 1;
		Если Не ЗначениеЗаполнено(СтрокаОснования.ДокументОснование) Тогда
			Сообщение = Новый СообщениеПользователю();
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не заполнена колонка ""Основание"" в строке %1  списка ""Основания"".'"), Строка(НомерСтроки));
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Поле = "Документ";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьОснованияВХранилище()
	
	ОснованияВХранилище = ДокументыОснования.Выгрузить(, "ДокументОснование");
	ПоместитьВоВременноеХранилище(ОснованияВХранилище, АдресВХранилищеДокументовОснований);
	
КонецПроцедуры

#КонецОбласти
