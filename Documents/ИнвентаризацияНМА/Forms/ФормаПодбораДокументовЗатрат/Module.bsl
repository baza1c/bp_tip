
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектУчета = Параметры.ОбъектУчета;
	Организация = Параметры.Организация;
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресДокументыЗатрат) Тогда
		ДокументыЗатрат.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресДокументыЗатрат));
	КонецЕсли;
	
	// Предопределенный список типов используем только при первом открытии формы. В дальнейшем будет использоваться
	// список, установленный пользователем.
	СписокВыбранныхТипов =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПодборДокументовЗатрат", "ВидыДокументов", ВыбранныеТипыПоУмолчанию());
		
	ВидыДокументов.Очистить();
	
	СписокВсехВозможныхТипов = Документы.ИнвентаризацияНМА.ИменаИспользуемыхРегистраторовРегистраБухгалтерии(
		Параметры.Организация,
		Метаданные.ОпределяемыеТипы.ДокументыЗатрат.Тип.Типы());
	
	Для Каждого ИмяРегистратора Из СписокВсехВозможныхТипов Цикл
		
		Если СписокВыбранныхТипов.Получить(ИмяРегистратора.Значение) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ВидыДокументов.Добавить();
		НоваяСтрока.ПолноеИмя = ИмяРегистратора.Значение;
		НоваяСтрока.Представление = ИмяРегистратора.Представление;
		
	КонецЦикла;
	
	СписокВсехВозможныхТипов.СортироватьПоПредставлению();
	УстановитьПодсказкуВводаДляВидовДокументов(ЭтотОбъект);
	Элементы.СписокДокументовВалютаДокумента.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет");

	УстановитьОтборПоВидуДокументов();
	УстановитьОтборПоПериоду();
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокДокументов,
		"Организация",
		Параметры.Организация,
		Истина);
	
	УстановитьУсловноеОформление();
	ПрименитьУсловноеОформлениеВыбранныхДокументов();
	
	ЗагрузитьИсториюПоиска();
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПодборДокументовЗатрат", "ВидыДокументов", СписокВыбранныхТипов);
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	УстановитьОтборПоКонтрагенту();
	УстановитьОтборПоСчету("Дт");
	УстановитьОтборПоСчету("Кт");
	УстановитьОтборПоПериоду();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли; 
	
	Если ПеренестиВДокумент Тогда
		ОповеститьОВыборе(РезультатЗакрытия());
	КонецЕсли;
	
	ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекСохранить("ПодборДокументовЗатрат", "ВидыДокументов", СписокВыбранныхТипов);
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПеренестиВДокумент И ИзмененыДанные Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru = 'Перенести информацию о затратах в документ инвентаризации?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПеренестиВДокументЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидыДокументовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = СписокВсехВозможныхТипов;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВидыДокументовНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СписокВсехВозможныхТипов.ЗаполнитьПометки(Ложь);
	Для Каждого ВозможныйТип Из СписокВсехВозможныхТипов Цикл
		Если СписокВыбранныхТипов.Получить(ВозможныйТип.Значение) <> Неопределено Тогда
			ВозможныйТип.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;

	Если Ждать СписокВсехВозможныхТипов.ОтметитьЭлементыАсинх(НСтр("ru = 'Выберите виды документов затрат'")) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтметитьВыбранныеТипы();
	ОбновитьСписокВыбранныхТипов();
	УстановитьПодсказкуВводаДляВидовДокументов(ЭтотОбъект);
	УстановитьОтборПоВидуДокументов();
КонецПроцедуры

&НаКлиенте
Процедура СчетДтПриИзменении(Элемент)
	УстановитьОтборПоСчету("Дт");
КонецПроцедуры

&НаКлиенте
Процедура СчетКтПриИзменении(Элемент)
	УстановитьОтборПоСчету("Кт");
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	УстановитьОтборПоКонтрагенту();
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовПриИзменении(Элемент)
	
	ЗаполнитьПредставленияВыбранныхВидовДокументов();
	ОбновитьСписокВыбранныхТипов();
	УстановитьПодсказкуВводаДляВидовДокументов(ЭтотОбъект);
	УстановитьОтборПоВидуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	УстановитьОтборПоДоговору();
КонецПроцедуры

&НаКлиенте
Процедура ДоговорНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Организация);
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ИмяРеквизитаКонтрагента = БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьИмяРеквизитаКонтрагентДоговора();
		Отбор.Вставить(ИмяРеквизитаКонтрагента, Контрагент);
	КонецЕсли;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	БухгалтерскийУчетКлиентПереопределяемый.ОткрытьФормуВыбораДоговора(ПараметрыФормы, Элемент, "ФормаВыбора", СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	УстановитьОтборПоПериоду();
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	УстановитьОтборПоПериоду();
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	НастроитьПоиск();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументов

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДобавитьНовыеСтроки(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбраннаяСтрока));
	ИзмененыДанные = Истина;

КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	// На практике, таблица формы предоставляет Массив.
	// При этом для платформенной логики проверки возможности перетаскивания важно, чтобы в Значение остался этот тип.
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	// Для того, чтобы элемент-получатель перетаскивания смог идентифицировать элемент-источник значения,
	// обернем значение в контейнер
	ЗначенияПеретаскивания = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПараметрыПеретаскивания.Значение);
	ПараметрыПеретаскивания.Значение.Очистить();

	КонтейнерЗначений = Новый Структура;
	КонтейнерЗначений.Вставить("Источник", Элемент.Имя);
	КонтейнерЗначений.Вставить("Значения", ЗначенияПеретаскивания);

	ПараметрыПеретаскивания.Значение.Добавить(КонтейнерЗначений);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДокументыЗатрат

&НаКлиенте
Процедура ДокументыЗатратПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		// Ожидаем перетаскивание из таблицы формы "СписокНоменклатуры".
		// На практике, таблица формы предоставляет Массив.
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ПараметрыПеретаскивания.Значение) Тогда
		Возврат;
	КонецЕсли;

	КонтейнерЗначений = ПараметрыПеретаскивания.Значение[0];
	Если ТипЗнч(КонтейнерЗначений) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Если Не КонтейнерЗначений.Свойство("Источник") Или Не КонтейнерЗначений.Свойство("Значения") Тогда
		Возврат;
	КонецЕсли;

	Если КонтейнерЗначений.Источник <> "СписокДокументов" Тогда
		Возврат;
	КонецЕсли;

	Если КонтейнерЗначений.Значения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДобавитьНовыеСтроки(КонтейнерЗначений.Значения);
	ИзмененыДанные = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДокументыЗатратПослеУдаления(Элемент)
	ПрименитьУсловноеОформлениеВыбранныхДокументов();
КонецПроцедуры

&НаКлиенте
Процедура ДокументыЗатратПриИзменении(Элемент)
	ИзмененыДанные = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", НачалоПериода, КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокВыбранныхТипов()

	ВыбранныеТипы = Новый Соответствие;
	Для Каждого ВидДокумента Из ВидыДокументов Цикл
		ВыбранныеТипы.Вставить(ВидДокумента.ПолноеИмя, Истина);
	КонецЦикла;
	СписокВыбранныхТипов = Новый ФиксированноеСоответствие(ВыбранныеТипы);
КонецПроцедуры

&НаСервере
Процедура НастроитьПоиск()
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		СохранитьСтрокуПоискаВИстории(СтрокаПоиска);
	КонецЕсли;
	
	Использование = ЗначениеЗаполнено(СтрокаПоиска);
	СписокСубконто = Новый СписокЗначений();
	ОтборДокументов = Новый СписокЗначений();
	Если Использование Тогда
		
		ТипыСубконто = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип;
		ОбластьПоиска = Новый Массив;
		Для Каждого ТипСубконто Из ТипыСубконто.Типы() Цикл
			ОбластьПоиска.Добавить(Метаданные.НайтиПоТипу(ТипСубконто));
		КонецЦикла;
		
		СписокПоиска = ПолнотекстовыйПоиск.СоздатьСписок(СтрокаПоиска, 10);
		СписокПоиска.ИспользованиеМетаданных = ИспользованиеМетаданныхПолнотекстовогоПоиска.НеИспользовать;
		СписокПоиска.ПолучатьОписание = Ложь;
		СписокПоиска.ПолучатьПредставление = Ложь;
		СписокПоиска.ОбластьПоиска = ОбластьПоиска;
		
		Попытка
		СписокПоиска.ПерваяЧасть();
		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
		
		Для НомерПорции = 0 По СписокПоиска.Количество() - 1 Цикл
			ЭлементСписка = СписокПоиска.Получить(НомерПорции);
			Если ТипыСубконто.ПривестиЗначение(ЭлементСписка.Значение) <> Неопределено Тогда
				СписокСубконто.Добавить(ЭлементСписка.Значение);
			КонецЕсли;
			Если Метаданные.ОпределяемыеТипы.ДокументыЗатрат.Тип.ПривестиЗначение(ЭлементСписка.Значение) <> Неопределено Тогда
				ОтборДокументов.Добавить(ЭлементСписка.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Если СписокСубконто.Количество() = 0 Тогда
			ТекстыПоиска = СтрРазделить(СтрокаПоиска, " ");
			СписокПоиска = ПолнотекстовыйПоиск.СоздатьСписок(СтрСоединить(ТекстыПоиска, "* ") + "*", 10);
			СписокПоиска.ИспользованиеМетаданных = ИспользованиеМетаданныхПолнотекстовогоПоиска.НеИспользовать;
			СписокПоиска.ПолучатьОписание = Ложь;
			СписокПоиска.ПолучатьПредставление = Ложь;
			СписокПоиска.ОбластьПоиска = ОбластьПоиска;

			Попытка
				СписокПоиска.ПерваяЧасть();
			Исключение
				ОбщегоНазначения.СообщитьПользователю(ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				Возврат;
			КонецПопытки;
		
			Для НомерПорции = 0 По СписокПоиска.Количество() - 1 Цикл
				ЭлементСписка = СписокПоиска.Получить(НомерПорции);
				Если ТипыСубконто.ПривестиЗначение(ЭлементСписка.Значение) <> Неопределено Тогда
					СписокСубконто.Добавить(ЭлементСписка.Значение);
				КонецЕсли;
				// Отбор по списку документов будет заведомо небольшой (максимум 20), поэтому от дублей документов в списке не избавляемся.
				Если Метаданные.ОпределяемыеТипы.ДокументыЗатрат.Тип.ПривестиЗначение(ЭлементСписка.Значение) <> Неопределено Тогда
					ОтборДокументов.Добавить(ЭлементСписка.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

	КонецЕсли;
	
	ГруппаОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		СписокДокументов.КомпоновщикНастроек.Настройки.Отбор,
		"ПоискПоПодстроке",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	УстановитьОтборПоСубконто(ГруппаОтбора, "Дт", Использование, СписокСубконто);
	УстановитьОтборПоСубконто(ГруппаОтбора, "Кт", Использование, СписокСубконто);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ГруппаОтбора,
		"Ссылка",
		ОтборДокументов,
		ВидСравненияКомпоновкиДанных.ВСписке,
		,
		Использование);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ГруппаОтбора,
		"Комментарий",
		СтрокаПоиска,
		ВидСравненияКомпоновкиДанных.Содержит,
		,
		Использование);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСубконто(ГруппаОтбора, НаправлениеСчета, Использование, СписокСубконто)

	ЗапрещенныеПоля = Новый Массив;
	СписокДокументов.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Свойство("ЗапрещенныеПоляСчет" + НаправлениеСчета, ЗапрещенныеПоля);	
	
	ГруппаОтбораПоСчету = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтбора,
		"ПоискПоПодстрокеСчет" + НаправлениеСчета,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
	Для НомерСубконто = 1 По БухгалтерскийУчет.МаксимальноеКоличествоСубконто() Цикл
		ИмяПоляОтбора = "Субконто" + НаправлениеСчета + ".Субконто" + НаправлениеСчета + XMLСтрока(НомерСубконто);
		
		Если ЗапрещенныеПоля <> Неопределено И ЗапрещенныеПоля.Найти(ИмяПоляОтбора) <> Неопределено Тогда
			// Если поле запрещено, то, значит, запрещены и все последующие.
			Прервать;
		КонецЕсли;
			
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаОтбораПоСчету,
			ИмяПоляОтбора,
			СписокСубконто,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Использование);

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьСтрокуПоискаВИстории(СтрокаПоиска)
	
	ИсторияПоиска = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПоискДокументовЗатрат", "", Новый Массив);
	СохраненнаяСтрока = ИсторияПоиска.Найти(СтрокаПоиска);
	Если СохраненнаяСтрока <> Неопределено Тогда
		ИсторияПоиска.Удалить(СохраненнаяСтрока);
	КонецЕсли;
	
	ИсторияПоиска.Вставить(0, СтрокаПоиска);
	КоличествоСтрок = ИсторияПоиска.Количество();
	Если КоличествоСтрок > 20 Тогда
		ИсторияПоиска.Удалить(КоличествоСтрок - 1);
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПоискДокументовЗатрат", "", ИсторияПоиска);
	Элементы.СтрокаПоиска.СписокВыбора.ЗагрузитьЗначения(ИсторияПоиска);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИсториюПоиска()
	
	ИсторияПоиска = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПоискДокументовЗатрат", "", Новый Массив);
	Элементы.СтрокаПоиска.СписокВыбора.ЗагрузитьЗначения(ИсторияПоиска);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСчету(ДтКт)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокДокументов.КомпоновщикНастроек.Настройки.Отбор,
		"Счет" + ДтКт,
		ЭтотОбъект["Счет" + ДтКт],
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(ЭтотОбъект["Счет" + ДтКт]));
	НастроитьПоляСубконто(ДтКт);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПоляСубконто(ДтКт)
	
	ЗапрещенныеПоля = Новый Массив;

	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ЭтотОбъект["Счет" + ДтКт]);
	КоличествоСубконто = СвойстваСчета.КоличествоСубконто;
	
	КомпоновщикНастроек = СписокДокументов.КомпоновщикНастроек;
	КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки = "Отбор";
	ПользовательскийОтбор = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти("Отбор");
	
	ОбщийТип = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Тип;
	ЗначениеПоУмолчанию = ОбщийТип.ПривестиЗначение(Неопределено);

	Для НомерСубконто = 1 По БухгалтерскийУчет.МаксимальноеКоличествоСубконто() Цикл
		
		ПутьКСубконто = "Субконто" + ДтКт + ".Субконто" + ДтКт + XMLСтрока(НомерСубконто); // Например, СубконтоДт.СубконтоДт1
		Если Не ЗначениеЗаполнено(ЭтотОбъект["Счет" + ДтКт]) Тогда
			ПолеСубконто = СписокДокументов.Поля.Найти(ПутьКСубконто);
			ПолеСубконто.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Субконто %1%2'"), ?(ДтКт = "Дт", НСтр("ru = 'Дт'"), НСтр("ru = 'Кт'")), Формат(НомерСубконто, ""));
			// Обход ошибки платформы 60016362. После исправления установить в динамическом списке связь по типу для субконто,
			// установку типа в коде - удалить.
			ПолеСубконто.ТипЗначения = ОбщийТип;

		ИначеЕсли НомерСубконто <= КоличествоСубконто Тогда
			ПолеСубконто = СписокДокументов.Поля.Найти(ПутьКСубконто);
			ПолеСубконто.Заголовок = СвойстваСчета["ВидСубконто" + XMLСтрока(НомерСубконто) + "Наименование"];
			// Обход ошибки платформы 60016362. После исправления установить в динамическом списке связь по типу для субконто,
			// установку типа в коде - удалить.
			ПолеСубконто.ТипЗначения = СвойстваСчета["ВидСубконто" + XMLСтрока(НомерСубконто) + "ТипЗначения"];
		Иначе
			ЗапрещенныеПоля.Добавить(ПутьКСубконто);
		КонецЕсли;
		
		Для Каждого ЭлементОтбора Из ПользовательскийОтбор.Элементы Цикл
			
			Если ЭлементОтбора.ЛевоеЗначение <> Новый ПолеКомпоновкиДанных(ПутьКСубконто) Тогда
				Продолжить;
			КонецЕсли;
			
			ЭлементОтбора.Использование = Ложь;
			// Обход ошибки платформы 60016362.
			ЭлементОтбора.ПравоеЗначение = ЗначениеПоУмолчанию;

		КонецЦикла;
		
	КонецЦикла;
	
	ГруппаОтбораПоискаПоСтроке = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(
		СписокДокументов.КомпоновщикНастроек.Настройки.Отбор,
		,
		"ПоискПоПодстроке");
		
	// Очистим отбор по подстроке, так как среди полей полей могут оказаться запрещенные поля или, наборот,
	// не по всем субконто окажется установленным отбор.
	Если ГруппаОтбораПоискаПоСтроке.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ГруппаОтбораПоискаПоСтроке[0], , "ПоискПоПодстрокеСчет" + ДтКт);
	КонецЕсли;
	
	// Сохраним запрещенные поля в дополнительные свойства настроек компоновщика и добавим к ним ранее установленные
	// ограничения (для счета Дт или счета Кт), поскольку метод УстановитьОграниченияИспользованияВОтборе
	// отменяет ранее установленные ограничения, если в списке запрещенных полей эти свойства явно не указаны.
	ДополнительныеНастройки = СписокДокументов.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	ДополнительныеНастройки.Вставить("ЗапрещенныеПоляСчет" + ДтКт, ОбщегоНазначения.СкопироватьРекурсивно(ЗапрещенныеПоля));
	СимметричноеНаправление = ?(ДтКт = "Дт", "Кт", "Дт");
	
	РанееЗапрещенныеПоля = Новый Массив;
	Если ДополнительныеНастройки.Свойство("ЗапрещенныеПоляСчет" + СимметричноеНаправление, РанееЗапрещенныеПоля) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЗапрещенныеПоля, РанееЗапрещенныеПоля);
	КонецЕсли;
	
	//@skip-check bsl-legacy-check-dynamic-feature-access
	СписокДокументов.УстановитьОграниченияИспользованияВОтборе(ЗапрещенныеПоля);
	
	// Восстановим очищенный поиск по подстроке
	Если ГруппаОтбораПоискаПоСтроке.Количество() > 0 Тогда
		Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
			УстановитьОтборПоСубконто(ГруппаОтбораПоискаПоСтроке[0], ДтКт, Истина, СписокСубконто);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоКонтрагенту()
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокДокументов.КомпоновщикНастроек.Настройки.Отбор,
		"Информация",
		Контрагент,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Контрагент));
	УстановитьОтборПоДоговору();
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоВидуДокументов()
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокДокументов,
		"ВидыДокументов",
		ЗначениеПараметраВидыДокументов(ВидыДокументов));
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоДоговору()
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		СписокДокументов.КомпоновщикНастроек.Настройки.Отбор,
		"ДоговорКонтрагента",
		Договор,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Договор));
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовыеСтроки(Значения)
	ВыбранныеДокументы = Новый Массив;
	Для Каждого Значение Из Значения Цикл
		
		СтрокаСпискаДокументов = Элементы.СписокДокументов.ДанныеСтроки(Значение);
		
		Если Не СтандартныеПодсистемыКлиент.ЭтоЭлементДинамическогоСписка(СтрокаСпискаДокументов) Тогда
			Продолжить;
		КонецЕсли;
		
		ВыбранныеДокументы.Добавить(Новый Структура("ДокументЗатрат, СуммаЗатратПоДокументу", СтрокаСпискаДокументов.Ссылка, СтрокаСпискаДокументов.СуммаДокумента));
		
	КонецЦикла;
	ДобавитьНовыеСтрокиСервер(ВыбранныеДокументы);

КонецПроцедуры

&НаСервере
Процедура ДобавитьНовыеСтрокиСервер(Значения)

	Для Каждого Значение Из Значения Цикл

		ПоискСтроки = Новый Структура("ДокументЗатрат", Значение.ДокументЗатрат);
		Если ДокументыЗатрат.НайтиСтроки(ПоискСтроки).Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(ДокументыЗатрат.Добавить(), Значение);
		КонецЕсли;

	КонецЦикла;
	
	ПрименитьУсловноеОформлениеВыбранныхДокументов();
КонецПроцедуры

&НаСервере
Процедура ПрименитьУсловноеОформлениеВыбранныхДокументов()
	ПодобранныеДокументы.ЗагрузитьЗначения(ДокументыЗатрат.Выгрузить().ВыгрузитьКолонку("ДокументЗатрат"));
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокДокументов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СписокДокументов.Ссылка", ВидСравненияКомпоновкиДанных.ВСписке, ПодобранныеДокументы);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыЗатратДокументЗатрат");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДокументыЗатрат.ДокументЗатрат", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Документ отсутствует в базе>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПеренестиВДокументЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		ПеренестиВДокумент = Ложь;
		ИзмененыДанные = Ложь;
		ДокументыЗатрат.Очистить();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеПараметраВидыДокументов(ВидыДокументов)
	
	ЗначениеПараметра = Новый Массив;
	Если ЗначениеЗаполнено(ВидыДокументов) Тогда
		Для Каждого ВидДокумента Из ВидыДокументов Цикл
			ЗначениеПараметра.Добавить(Тип(СтрЗаменить(ВидДокумента.ПолноеИмя, "Документ.", "ДокументСсылка.")));
		КонецЦикла;
	Иначе
		ЗначениеПараметра = Метаданные.ОпределяемыеТипы.ДокументыЗатрат.Тип.Типы();
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции

&НаСервере
Функция РезультатЗакрытия()
	
	РезультатЗакрытия = Новый Структура;
	Если ПеренестиВДокумент Тогда
		РезультатЗакрытия.Вставить("ОбъектУчета", ОбъектУчета);
		РезультатЗакрытия.Вставить("АдресДокументовЗатрат", ПоместитьВоВременноеХранилище(ДокументыЗатрат.Выгрузить(), УникальныйИдентификатор));
	КонецЕсли;
	
	Возврат РезультатЗакрытия;
	
КонецФункции

&НаСервере
Процедура УстановитьОтборПоПериоду()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокДокументов,
		"НачалоПериода",
		НачалоПериода,
		ЗначениеЗаполнено(НачалоПериода));
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокДокументов,
		"КонецПериода",
		КонецДня(КонецПериода),
		ЗначениеЗаполнено(КонецПериода));
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПодсказкуВводаДляВидовДокументов(Форма)
	// У поля, содержащего множественные значения, подсказка ввода отображается даже при заполненном
	// значении. Исправим это поведение.
	Элементы = Форма.Элементы;
	Если Форма.ВидыДокументов.Количество() = 0 Тогда
		Элементы.ВидыДокументов.ПодсказкаВвода = НСтр("ru = 'Все'");
	Иначе
		Элементы.ВидыДокументов.ПодсказкаВвода = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВыбранныеТипы()
	ВидыДокументов.Очистить();
	Для Каждого ВозможныйТип Из СписокВсехВозможныхТипов Цикл
		Если ВозможныйТип.Пометка Тогда
			НоваяСтрока = ВидыДокументов.Добавить();
			НоваяСтрока.ПолноеИмя = ВозможныйТип.Значение;
			НоваяСтрока.Представление = ВозможныйТип.Представление;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ВыбранныеТипыПоУмолчанию()
	ДокументыПоУмолчанию = Новый Соответствие;
	ДокументыПоУмолчанию.Вставить(Метаданные.Документы.ПоступлениеТоваровУслуг.ПолноеИмя(), Истина);
	ДокументыПоУмолчанию.Вставить(Метаданные.Документы.ПринятиеКУчетуНМА.ПолноеИмя(), Истина);
	ДокументыПоУмолчанию.Вставить(Метаданные.Документы.ПоступлениеНМА.ПолноеИмя(), Истина);
	Возврат Новый ФиксированноеСоответствие(ДокументыПоУмолчанию);
КонецФункции

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатВыбора, "НачалоПериода, КонецПериода");
	
	УстановитьОтборПоПериоду();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПредставленияВыбранныхВидовДокументов()
	
	Для Каждого ВидДокумента Из ВидыДокументов Цикл
		Если Не ЗначениеЗаполнено(ВидДокумента.Представление) Тогда
			ВидДокумента.Представление = СписокВсехВозможныхТипов.НайтиПоЗначению(ВидДокумента.ПолноеИмя).Представление;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
