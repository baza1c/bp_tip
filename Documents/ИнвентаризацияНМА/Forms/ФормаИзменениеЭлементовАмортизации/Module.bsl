
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПереложитьПараметрыВРеквизиты(Параметры);

	// Тумблер реквизита типа Булево слева отображает значение Ложь, что соответствует значению "Неопределенный срок использования".
	// Чтобы слева отображалось значение, соответствующее "Срок использования можно определить", используем реквизит типа Булево
	// с инвертированным значением.
	СрокПолезногоИспользованияМожноОпределить =
		Не НеопределенныйСрокПолезногоИспользования;
	
	Если Не СрокПолезногоИспользованияМожноОпределить
		И Не ЗначениеЗаполнено(СпособНачисленияАмортизации)
		И Не ТолькоПросмотр
		И КлючНазначенияИспользования = "ЭлементыАмортизации" Тогда
		СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный;
	КонецЕсли;
	
	НастроитьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособНачисленияАмортизацииПриИзменении(Элемент)
	НастроитьЭлементыАмортизации();
	Пересмотрено = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СрокПолезногоИспользованияМожноОпределитьПриИзменении(Элемент)
	НеопределенныйСрокПолезногоИспользования = Не СрокПолезногоИспользованияМожноОпределить;
	НастроитьЭлементыАмортизации();
	Пересмотрено = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПересмотреноПриИзменении(Элемент)
	ПересмотреноПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура СрокПолезногоИспользованияПриИзменении(Элемент)
	Пересмотрено = Истина;
	Если КлючНазначенияИспользования = "СрокИспользования" Тогда
		НеопределенныйСрокПолезногоИспользования = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КоэффициентПриИзменении(Элемент)
	Пересмотрено = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбъемПродукцииРаботПриИзменении(Элемент)
	Пересмотрено = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЛиквидационнаяСтоимостьПриИзменении(Элемент)
	Пересмотрено = Истина;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не Модифицированность И Не ИзменениеНесколькихСтрок Тогда
		Закрыть(Неопределено);
		Возврат;
	КонецЕсли;
	
	РезультатЗакрытия = Новый Структура;
	РезультатЗакрытия.Вставить("ИзменениеНесколькихСтрок", ИзменениеНесколькихСтрок);
	РезультатЗакрытия.Вставить("Пересмотрено", Пересмотрено);
	Если Пересмотрено Тогда
		РезультатЗакрытия.Вставить("ЭлементыАмортизацииПересмотрено", РезультатПересмотра());
	КонецЕсли;
	РезультатЗакрытия.Вставить("КлючНазначенияИспользования", КлючНазначенияИспользования);
	
	Закрыть(РезультатЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция РезультатПересмотра()
	РезультатПересмотра = Новый Структура;
	Для Каждого ПересмотренноеСвойство Из ЭлементыАмортизации Цикл
		РезультатПересмотра.Вставить(ПересмотренноеСвойство.Ключ + "Пересмотрено", ЭтотОбъект[ПересмотренноеСвойство.Ключ]);
	КонецЦикла;
	
	Возврат РезультатПересмотра;
КонецФункции

&НаСервере
Процедура ПереложитьПараметрыВРеквизиты(ПараметрыФормы)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыФормы, "ИзменениеНесколькихСтрок, Пересмотрено, ЭлементыАмортизации");

	Если Пересмотрено Тогда
		Для Каждого ЭлементКоллекции Из ПараметрыФормы.ЭлементыАмортизации Цикл
			ЭтотОбъект[ЭлементКоллекции.Ключ] = ПараметрыФормы.ЭлементыАмортизацииПересмотрено[ЭлементКоллекции.Ключ + "Пересмотрено"];
		КонецЦикла;
	Иначе
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыФормы.ЭлементыАмортизации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересмотреноПриИзмененииСервер()
	
	Если Пересмотрено = Ложь Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЭлементыАмортизации);
		СрокПолезногоИспользованияМожноОпределить = Не НеопределенныйСрокПолезногоИспользования;
		НастроитьФорму();
	Иначе
		Если КлючНазначенияИспользования = "СрокИспользования" Тогда
			НеопределенныйСрокПолезногоИспользования = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	Если КлючНазначенияИспользования = "ЭлементыАмортизации" Тогда
		Заголовок = НСтр("ru = 'Элементы амортизации'");
		НастроитьЭлементыАмортизации();
	Иначе
		Заголовок = НСтр("ru = 'Срок использования'");
		НастроитьЭлементыСрокИспользования(); // В этом случае видимость устанавливается только один раз, при открытии формы.
	КонецЕсли;
	
	Если ИзменениеНесколькихСтрок Тогда
		Модифицированность = Истина;
		Пересмотрено = Истина;
		Элементы.Пересмотрено.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыСрокИспользования()
	
	Элементы.СрокПолезногоИспользованияМожноОпределить.Видимость = Ложь;
	Элементы.СпособНачисленияАмортизации.Видимость               = Ложь;
	Элементы.Коэффициент.Видимость                               = Ложь;
	Элементы.ГруппаОбъемПродукции.Видимость                      = Ложь;
	Элементы.ГруппаЛиквидационнаяСтоимость.Видимость             = Ложь;
	
	Элементы.СрокПолезногоИспользования.Заголовок = НСтр("ru = 'Остаток срока использования'");
	Элементы.Пересмотрено.Заголовок = НСтр("ru = 'Пересмотреть срок использования'");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыАмортизации()
	
	Элементы.СпособНачисленияАмортизации.Видимость   = Не НеопределенныйСрокПолезногоИспользования;
	Элементы.ГруппаЛиквидационнаяСтоимость.Видимость = Не НеопределенныйСрокПолезногоИспользования;
	
	Если НеопределенныйСрокПолезногоИспользования Тогда
		
		Элементы.ГруппаСрокИспользования.Видимость  = Ложь;
		Элементы.Коэффициент.Видимость              = Ложь;
		Элементы.ГруппаОбъемПродукции.Видимость     = Ложь;
			
	Иначе

		ЛинейныйСпособ = (СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный);
		ПоОбъемуПродукции = (СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции);
		УменьшаемыйОстаток = (СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка);

		Элементы.ГруппаСрокИспользования.Видимость = ЛинейныйСпособ Или УменьшаемыйОстаток;
		Элементы.Коэффициент.Видимость = УменьшаемыйОстаток;
		Элементы.ГруппаОбъемПродукции.Видимость = ПоОбъемуПродукции;
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти