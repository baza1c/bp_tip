
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ТипЗнч(Параметры.КритерииКонтроля) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.КритерииКонтроля);
		// После изменения настроек контроля будем проверять, дейстительно ли они изменились, так как заполнение отметки о контроле - ресурсоемкая операция
		КритерииКонтроляИсходные = Параметры.КритерииКонтроля;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если Не КонтролироватьРанееОбесцененные И Не КонтролироватьСрокИспользования И Не КонтролироватьНеограниченныйСрокДействияПрава Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не указан ни один из критериев контроля.'"),
			, 
			"КонтролироватьРанееОбесцененные",
			,
			Отказ);
		Возврат;
	КонецЕсли;
	
	Если КонтролироватьСрокИспользования Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаОкончанияКонтроль");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

	Если Не Модифицированность Тогда
		Закрыть(Неопределено);
		Возврат;
	КонецЕсли;

	РезультатФормы = Неопределено;

	Если ПроверитьЗаполнение() Тогда

		РезультатФормы = УстановитьВыполненныеНастройки();

		Закрыть(РезультатФормы);

	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция УстановитьВыполненныеНастройки()
	
	КритерииКонтроля = Документы.ИнвентаризацияНМА.НовыйКритерииКонтроля(Дата(1, 1, 1));
	ЗаполнитьЗначенияСвойств(КритерииКонтроля, ЭтотОбъект);
	Если ОбщегоНазначения.ДанныеСовпадают(КритерииКонтроля, КритерииКонтроляИсходные) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Новый Структура("КритерииКонтроля", КритерииКонтроля);
	
КонецФункции

#КонецОбласти
