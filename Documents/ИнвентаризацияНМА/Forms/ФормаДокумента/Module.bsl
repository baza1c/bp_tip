
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	//
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		Параметры.Свойство("ЗаполнениеИзПомощника", ЗаполнениеИзПомощника);
		Если ЗаполнениеИзПомощника Тогда
			ЗаполнитьЗначенияСвойств(Объект, Параметры, "Организация, Дата");
		КонецЕсли;
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ЗаполнениеИзПомощника Тогда
		ЗаполнитьНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	
	Если ИмяСобытия = "Запись_НематериальныйАктив" Тогда
		
		// Обработка требует серверного вызова, поэтому обработывать будем только те изменения, которые явно подходят под данные документа.
		Если НачалоДня(Параметр.НачалоДействияПрава) > Объект.Дата
			Или Параметр.ОтветственноеЛицо <> Объект.ОтветственноеЛицо Тогда
				Возврат;
		КонецЕсли;
		
		// Если справочник изменялся из текущего документа, то пользователь скорее всего спозиционирован на текущей строке.
		ТекущиеДанные = Элементы.ОбъектыУчета.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И ТекущиеДанные.ОбъектУчета = Параметр.НематериальныйАктив Тогда
			ТекущиеДанные.СрокДействияПраваПредставление = УчетНМАВызовСервера.ПредставлениеСрокаДействияПрава(ТекущиеДанные.ОбъектУчета);
		Иначе
			ОбновитьСрокДействияПраваПредставление(Параметр.НематериальныйАктив);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_ПринятиеКУчетуНМА" Или ИмяСобытия = "Запись_СписаниеНМА" Тогда
		ИзменитьДокументОтражения(ИмяСобытия, Источник);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ПроведениеСервер.УстановитьПризнакПроверкиРеквизитов(Отказ, ТекущийОбъект,  ПараметрыЗаписи);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УстановитьСостояниеДокумента();
	ПолучитьАктуальныеДанныеОбЭлементахАмортизации();
	ЗаполнитьДобавленныеКолонки();
	ЗаполнитьДокументыОтражения();
	ЗаполнитьНадписьДокументыЗатрат();
	ОтметитьОсобыйКонтроль();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Организация", Объект.Организация);
	ПараметрыОповещения.Вставить("РазделУчета", ПредопределенноеЗначение("Перечисление.РазделыУчетаДляИнвентаризации.НематериальныеАктивы"));
	Оповестить("ОбновитьФормуПомощникаИнвентаризации", ПараметрыОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНематериальныхАктивов.Форма.Форма" Тогда
		ОбработкаВыбораПодборОбъектовУчетаНаСервере(ВыбранноеЗначение);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.ИнвентаризацияНМА.Форма.ФормаПодбораДокументовЗатрат" Тогда
		ПеренестиПодобранныеДокументыЗатрат(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура ОрганизацияПриИзменении(Элемент)

		ПредупреждениеОЗаписи = "";
		Если Объект.Проведен Тогда
			ПредупреждениеОЗаписи = НСтр("ru = 'Перед заполнением документ будет записан.'");
		КонецЕсли;
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перезаполнить таблицу Объекты учета?
			|%1'"),
			ПредупреждениеОЗаписи);
		
	Если Объект.ОбъектыУчета.Количество() > 0
		И Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			ЗаполнитьНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьЭлементыАмортизацииПриИзменении(Элемент)
	ПроверятьЭлементыАмортизацииПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ПроверятьДокументыЗатратПриИзменении(Элемент)
	НастроитьВидимость(ЭтотОбъект);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИнвентаризационнаяКомиссия

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПередУдалением(Элемент, Отказ)
	ИнвентаризацияАктивовОбязательствКлиент.ИнвентаризационнаяКомиссияПередУдалением(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ИнвентаризацияАктивовОбязательствКлиент.ИнвентаризационнаяКомиссияОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ИнвентаризацияАктивовОбязательствКлиент.ИнвентаризационнаяКомиссияПриНачалеРедактирования(ЭтотОбъект, Копирование);
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияФизЛицоПриИзменении(Элемент)
	ИнвентаризацияАктивовОбязательствКлиент.ИнвентаризационнаяКомиссияФизЛицоПриИзменении(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияФизЛицоОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	ИнвентаризацияАктивовОбязательствКлиент.ИнвентаризационнаяКомиссияИсключитьДублиФизическихЛиц(
		Объект.ИнвентаризационнаяКомиссия,
		Элементы.ИнвентаризационнаяКомиссия,
		ВыбранноеЗначение,
		СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризационнаяКомиссияПредседательПриИзменении(Элемент)
	ИнвентаризацияАктивовОбязательствКлиент.ИнвентаризационнаяКомиссияПредседательПриИзменении(ЭтотОбъект);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыУчета

&НаКлиенте
Процедура ОбъектыУчетаОбъектУчетаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ОбъектыУчета.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДанныеСтроки = Новый Структура;
	ПриИзмененииОбъектаУчетаСервер(ДанныеСтроки, ТекущиеДанные.ОбъектУчета);
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтроки);
	// По данным учета есть только те объекты, которые попадают в документ по кнопке "Заполнить".
	ТекущиеДанные.НаличиеПоДаннымУчета = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыУчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ОбъектыУчета.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ОбъектыУчетаЭлементыАмортизации" Или Поле.Имя = "ОбъектыУчетаПересмотрено" Тогда
		СтандартнаяОбработка = Ложь;
		НачалоИзмененияЭлементовАмортизации(ТекущиеДанные);
	ИначеЕсли Поле.Имя = "ОбъектыУчетаСрокДействияПраваПредставление" И ЗначениеЗаполнено(ТекущиеДанные.ОбъектУчета) Тогда
		НачалоИзмененияСрокаДействияПрава(ТекущиеДанные);
	ИначеЕсли Поле.Имя = "ОбъектыУчетаДокументыЗатрат" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОбъектУчета", ТекущиеДанные.ОбъектУчета);
		ПараметрыФормы.Вставить("АдресДокументыЗатрат", ДокументыЗатрат(ТекущиеДанные.ОбъектУчета));
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ОткрытьФорму("Документ.ИнвентаризацияНМА.Форма.ФормаПодбораДокументовЗатрат", // ИмяФормы
			ПараметрыФормы, // Параметры
			ЭтотОбъект, // Владелец
			ТекущиеДанные.ОбъектУчета, // Уникальность
			, // Окно
			, // НавигационнаяСсылка
			, // ОписаниеОповещенияОЗакрытии
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); // РежимОткрытия
	ИначеЕсли Поле.Имя = "ОбъектыУчетаОтражениеРезультатовИнвентаризации" Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ТекущиеДанные.ОтражениеРезультатовИнвентаризации) Тогда
			ПоказатьЗначение(, ТекущиеДанные.ОтражениеРезультатовИнвентаризации);
		ИначеЕсли ТекущиеДанные.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА")
			И ТекущиеДанные.НаличиеПоДаннымУчета И Не ТекущиеДанные.НаличиеФактическое Тогда
			СоздатьДокументНаОсновании("СписаниеНМА", ТекущиеДанные)
		ИначеЕсли ТекущиеДанные.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА")
			И Не ТекущиеДанные.НаличиеПоДаннымУчета И ТекущиеДанные.НаличиеФактическое Тогда
			СоздатьДокументНаОсновании("ПринятиеКУчетуНМА", ТекущиеДанные);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ОбъектыУчетаОбъектУчета" Тогда
		// В строках, заполненных по данным учета, нельзя изменить НМА, но при этом может понадобиться посмотреть карточку объета.
		// Признак строк, заполненных по данным учета, - НаличиеПоДаннымУчета = Истина.
		Если ТекущиеДанные.НаличиеПоДаннымУчета Тогда
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение( , ТекущиеДанные.ОбъектУчета);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыУчетаВидОбъектаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ОбъектыУчета.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные.ЭлементыАмортизации = ПредставлениеЭлементовАмортизации(ТекущиеДанные.ОбъектУчета, ТекущиеДанные.ВидОбъекта);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область СтандартныеПодсистемы_ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаКлиенте
Процедура Подбор(Команда)
	СтруктураПараметров = Новый Структура;
	Если Объект.ОбъектыУчета.Количество() > 0 Тогда
		СтруктураПараметров.Вставить("АдресВХранилище", ПоместитьОбъектыУчетаВХранилище());
	КонецЕсли;
	ОткрытьФорму("Обработка.ПодборНематериальныхАктивов.Форма.Форма", СтруктураПараметров, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаКонтроля(Команда)
	
	ОповещениеЗавершениеНастроек = Новый ОписаниеОповещения("ВыборНастроекЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("КритерииКонтроля", КритерииКонтроля);
	
	ОткрытьФорму("Документ.ИнвентаризацияНМА.Форма.ФормаНастройкиПроверок",
		ПараметрыФормы,
		ЭтотОбъект,
		, 
		,
		,
		ОповещениеЗавершениеНастроек,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьСтрокиНаКонтроле(Команда)
	ОтобратьСтроки(Команда.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьСтрокиВсе(Команда)
	ОтобратьСтроки(Команда.Имя);
КонецПроцедуры

&НаКлиенте
Асинх Процедура Заполнить(Команда)
	
	Если Объект.Проведен И Объект.ОбъектыУчета.Количество() > 0 Тогда
		ПредупреждениеАсинх(НСтр("ru = 'Заполнение возможно только в непроведенном документе'"), 60);
		Возврат;
	КонецЕсли;
	// Заполение выполяется в фоновом задании. Если в результате выполнения фонового задания возникает ошибка, то на форму
	// выводится сообщение об ошибке.
	ОчиститьСообщения();
	
	ТекстВопросаУдалениеСтрок = НСтр("ru = 'Перед заполнением все строки таблицы ""Объекты учета"" будут удалены. Продолжить?'");
		
	Если Объект.ОбъектыУчета.Количество() = 0
		Или Ждать ВопросАсинх(ТекстВопросаУдалениеСтрок, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборФизическихЛиц(Команда)
	ИнвентаризацияАктивовОбязательствКлиент.ПодборФизическихЛиц(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьФактическоеНаличие(Команда)
	
	Если Элементы.ОбъектыУчета.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ФактическоеНаличие = Новый СписокЗначений();
	ФактическоеНаличие.Добавить(Истина, НСтр("ru = 'В наличии'"), , БиблиотекаКартинок.УстановитьФлажки);
	ФактическоеНаличие.Добавить(Ложь, НСтр("ru = 'Отсутствует'"), , БиблиотекаКартинок.СнятьФлажки);
	ВыбратьЗначениеФактическогоНаличия(ФактическоеНаличие);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСрокИспользования(Команда)
	
	ВыделенныеСтроки = Элементы.ОбъектыУчета.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выделите строки с объектами учета, у которых нужно изменить срок использования'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИзменениеНесколькихСтрок", Истина);
	ПараметрыФормы.Вставить("ЭлементыАмортизации", НовыйЭлементыАмортизации());
	ПараметрыФормы.Вставить("ЭлементыАмортизацииПересмотрено", НовыйЭлементыАмортизацииПересмотрено());
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "СрокИспользования");
	
	ОткрытьФормуРедактированияЭлементовАмортизации(ПараметрыФормы, ВыделенныеСтроки);
		
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЭлементыАмортизации(Команда)
	
	ВыделенныеСтроки = Элементы.ОбъектыУчета.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выделите строки с объектами учета, у которых нужно изменить элементы амортизации'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИзменениеНесколькихСтрок", Истина);
	ПараметрыФормы.Вставить("ЭлементыАмортизации", НовыйЭлементыАмортизации());
	ПараметрыФормы.Вставить("ЭлементыАмортизацииПересмотрено", НовыйЭлементыАмортизацииПересмотрено());
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ЭлементыАмортизации");
	
	ОткрытьФормуРедактированияЭлементовАмортизации(ПараметрыФормы, ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКомиссиюИзПредыдущегоДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Организация'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Организация");
		Возврат;
	КонецЕсли;
	
	ТекстПредупреждения = ЗаполнитьИнвентаризационнуюКомиссию();
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(,ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуРедактированияЭлементовАмортизации(ПараметрыФормы, СтрокиДляИзменения)

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ИзменениеЭлементовАмортизацииЗавершение", ЭтотОбъект, СтрокиДляИзменения);
	ОткрытьФорму("Документ.ИнвентаризацияНМА.Форма.ФормаИзменениеЭлементовАмортизации", // ИмяФормы
		ПараметрыФормы, // Параметры
		ЭтотОбъект, // Владелец
		, // Уникальность
		, // Окно
		, // НавигационнаяСсылка
		ОповещениеОЗакрытии, // ОписаниеОповещенияОЗакрытии
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); // РежимОткрытия

КонецПроцедуры

&НаКлиенте
Процедура ИзменениеЭлементовАмортизацииЗавершение(Результат, ДанныеДляИзменения) Экспорт

	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ПостфиксЭлементовПредставления = "";
	Если Результат.ИзменениеНесколькихСтрок Тогда
		Для Каждого ИдентификаторСтроки Из ДанныеДляИзменения Цикл
			
			ВыделеннаяСтрока = Объект.ОбъектыУчета.НайтиПоИдентификатору(ИдентификаторСтроки);
			
			Если ВыделеннаяСтрока = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если Результат.КлючНазначенияИспользования = "ЭлементыАмортизации"
				И ВыделеннаяСтрока.ВидОбъекта <> ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА") Тогда
				Продолжить;
			КонецЕсли;
			
			Если Результат.КлючНазначенияИспользования = "СрокИспользования"
				И ВыделеннаяСтрока.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА") Тогда
				Продолжить;
			КонецЕсли;				
			
			ВыделеннаяСтрока.Пересмотрено = Истина;
			ЗаполнитьЗначенияСвойств(ВыделеннаяСтрока, Результат.ЭлементыАмортизацииПересмотрено);
			
			ПостфиксЭлементовПредставления = "Пересмотрено";
			
			УстановитьЭлементыАмортизации(ВыделеннаяСтрока, ПостфиксЭлементовПредставления);
				
		КонецЦикла;
	Иначе
		ВыделеннаяСтрока = Объект.ОбъектыУчета.НайтиПоИдентификатору(ДанныеДляИзменения);
		ВыделеннаяСтрока.Пересмотрено = Результат.Пересмотрено;

		Если Результат.Пересмотрено Тогда
			ЗаполнитьЗначенияСвойств(ВыделеннаяСтрока, Результат.ЭлементыАмортизацииПересмотрено);
			ПостфиксЭлементовПредставления = "Пересмотрено";
		КонецЕсли;
		УстановитьЭлементыАмортизации(ВыделеннаяСтрока, ПостфиксЭлементовПредставления);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЭлементыАмортизации(ВыделеннаяСтрока, ПостфиксЭлементовПредставления)
	
	СведенияНМА = УчетНМАКлиентСервер.НовыйСведенияНМА();
	ДанныеДокументаНМА = УчетНМАКлиентСервер.НовыйДанныеДокументаНМА();
	УчетНМАКлиентСервер.ЗаполнитьСведенияНМАБезПостфиксов(ВыделеннаяСтрока, ПостфиксЭлементовПредставления, СведенияНМА, ДанныеДокументаНМА);
	
	ВыделеннаяСтрока.ЭлементыАмортизации =
		УчетНМАКлиентСервер.ПредставлениеЭлементовАмортизации(СведенияНМА, ДанныеДокументаНМА, ПостфиксЭлементовПредставления);
		
КонецПроцедуры

&НаСервере
Процедура ПроверятьЭлементыАмортизацииПриИзмененииСервер()
	НастроитьВидимость(ЭтотОбъект);
	АктивныйОтборСтрок = Элементы.ОтобратьСтрокиВсе.ИмяКоманды;
	ОтобратьСтрокиНаСервере(АктивныйОтборСтрок);
КонецПроцедуры

&НаСервере
Процедура ПеренестиПодобранныеДокументыЗатрат(РезультатПодбора)
	ПодобранныеДокументыЗатрат = ПолучитьИзВременногоХранилища(РезультатПодбора.АдресДокументовЗатрат);
	ПоискСтрок = Новый Структура("ОбъектУчета", РезультатПодбора.ОбъектУчета);
	
	ЗаполненныеРанееСтроки = Объект.ДокументыЗатрат.НайтиСтроки(ПоискСтрок);
	
	Для Каждого ИмеющаясяСтрока Из ЗаполненныеРанееСтроки Цикл
		Объект.ДокументыЗатрат.Удалить(ИмеющаясяСтрока);
	КонецЦикла;
	
	СуммаЗатрат = 0;
	Для Каждого СтрокаДокументов Из ПодобранныеДокументыЗатрат Цикл
		НоваяСтрока = Объект.ДокументыЗатрат.Добавить();
		НоваяСтрока.ОбъектУчета = РезультатПодбора.ОбъектУчета;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокументов);
		СуммаЗатрат = СуммаЗатрат + НоваяСтрока.СуммаЗатратПоДокументу;
	КонецЦикла;
	
	СтрокиОбъектыУчета = Объект.ОбъектыУчета.НайтиСтроки(ПоискСтрок);
	
	ТекстНадписьДокументыЗатрат = ТекстНадписиДокументыЗатрат(ПодобранныеДокументыЗатрат);
	
	Для Каждого СтрокаОбъектУчета Из СтрокиОбъектыУчета Цикл
		СтрокаОбъектУчета.ДокументыЗатрат = ТекстНадписьДокументыЗатрат;
		// Сумму затрат автоматически заполняем из формы подбора только если она не заполнена, чтобы не затереть данные.
		Если СтрокаОбъектУчета.Стоимость = 0 Тогда
			СтрокаОбъектУчета.Стоимость = СуммаЗатрат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ТекстНадписиДокументыЗатрат(ПодобранныеДокументыЗатрат)

	ТекстНадписьДокументыЗатрат = "";
	
	Если ПодобранныеДокументыЗатрат.Количество() = 0 Тогда
		Возврат ТекстНадписьДокументыЗатрат;
	КонецЕсли;
	
	Если ПодобранныеДокументыЗатрат.Количество() = 1 Тогда
		Если ЗначениеЗаполнено(ПодобранныеДокументыЗатрат[0].ДокументЗатрат) Тогда
			ТекстНадписьДокументыЗатрат = Строка(ПодобранныеДокументыЗатрат[0].ДокументЗатрат);
		Иначе
			ТекстНадписьДокументыЗатрат = НСтр("ru = '<Документ отсутствует в базе>'");
		КонецЕсли;
	Иначе
		ТекстНадписьДокументыЗатрат = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';%1 документ;;%1 документа;%1 документов;%1 документов'"),
			ПодобранныеДокументыЗатрат.Количество());
	КонецЕсли;
	
	Возврат ТекстНадписьДокументыЗатрат;
	
КонецФункции

&НаСервере
Функция ДокументыЗатрат(ОбъектУчета)
	ОтборСтрок = Новый Структура("ОбъектУчета", ОбъектУчета);
	ДетальныеСтроки = Объект.ДокументыЗатрат.Выгрузить(ОтборСтрок);
	Возврат ПоместитьВоВременноеХранилище(ДетальныеСтроки, УникальныйИдентификатор);
КонецФункции

&НаСервере
Процедура ИзменитьДокументОтражения(ИмяСобытия, Источник)
	
	РеквизитыИсточника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "НематериальныйАктив, Проведен, ДокументОснование");
	
	Если РеквизитыИсточника.ДокументОснование <> Объект.Ссылка Тогда
		Возврат;
	КонецЕсли;
	
	НематериальныйАктив = РеквизитыИсточника.НематериальныйАктив;
	Отбор = Новый Структура("ОбъектУчета", НематериальныйАктив);
	НайденныеСтроки = Объект.ОбъектыУчета.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() = 0 Тогда
		// Измененный документ не связан с текущей инвентаризацией.
		Возврат;
	КонецЕсли;
	ЭтоПроведение = РеквизитыИсточника.Проведен;
	
	// Если документ был проведен, то добавляем его на форму и показываем, в противном случае -
	// удаляем с формы и скрываем.
	Если ЭтоПроведение Тогда
		СписокДокументовОтражения = Новый Соответствие(ДокументыОтраженияРезультата);
		СписокДокументовОтражения.Вставить(НематериальныйАктив, Источник);
		ДокументыОтраженияРезультата = Новый ФиксированноеСоответствие(СписокДокументовОтражения);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НайденнаяСтрока.ОтражениеРезультатовИнвентаризации = Источник;
		КонецЦикла;
	Иначе
		Если ДокументыОтраженияРезультата.Получить(НематериальныйАктив) <> Неопределено Тогда
			СписокДокументовОтражения = Новый Соответствие(ДокументыОтраженияРезультата);
			СписокДокументовОтражения.Удалить(НематериальныйАктив);
			ДокументыОтраженияРезультата = Новый ФиксированноеСоответствие(СписокДокументовОтражения);
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НайденнаяСтрока.ОтражениеРезультатовИнвентаризации = Неопределено;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура СоздатьДокументНаОсновании(ВидДокумента, ТекущиеДанные)

	// Для связи документов должен быть заполнен реквизит ДокументОснование, для этого у ИнвентаризацииНМА должна быть ссылка.
	ТекстВопроса = НСтр("ru = 'Документ еще не записан.
						|Отразить в учете результаты инвентаризации можно только после ее записи.
						|Документ будет записан.'");
		
	Если ЗначениеЗаполнено(Объект.Ссылка)
		Или Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ОКОтмена) = КодВозвратаДиалога.ОК Тогда
		СоздатьДокументНаОснованииЗавершение(ВидДокумента, ТекущиеДанные);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументНаОснованииЗавершение(ВидДокумента, ТекущиеДаные)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Организация", Объект.Организация);
	ЗначенияЗаполнения.Вставить("НематериальныйАктив", ТекущиеДаные.ОбъектУчета);
	ЗначенияЗаполнения.Вставить("ДокументОснование", Объект.Ссылка);
	Если ВидДокумента = "ПринятиеКУчетуНМА" Тогда
		ЗначенияЗаполнения.Вставить("СтоимостьБУ", ТекущиеДаные.Стоимость);
		ЗначенияЗаполнения.Вставить("СтоимостьНУ", ТекущиеДаные.Стоимость);
		ПодготовитьПараметрыЗаполненияПринятияКУчетуНаСервере(ЗначенияЗаполнения, ТекущиеДаные.ОбъектУчета);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ИмяОткрываемойФормы = СтрШаблон("Документ.%1.Форма.ФормаДокумента", ВидДокумента);
	
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, ЭтотОбъект, ТекущиеДаные.ОбъектУчета);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьПараметрыЗаполненияПринятияКУчетуНаСервере(ЗначенияЗаполнения, НематериальныйАктив)
	ЗначенияЗаполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНаОснованииИнвентаризации);
	ОтражениеНУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НематериальныйАктив, "ОтражениеНУ");
	Если ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.РасходыНаНИОКР Тогда
		ЗначенияЗаполнения.Вставить("ВидОбъектаУчета", Перечисления.ВидыОбъектовУчетаНМА.РасходыНаНИОКР);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачалоИзмененияСрокаДействияПрава(ТекущиеДанные)
	ПоказатьЗначение( , ТекущиеДанные.ОбъектУчета);
КонецПроцедуры

&НаКлиенте
Процедура НачалоИзмененияЭлементовАмортизации(ТекущиеДанные)

	// Для НМА, которые еще не отражены в учете, элементы амортизации заполняются в документе принятия к учету
	// Для НМА, которые подлежат списанию, нет смысла заполнять элементы амортизации.
	Если ТекущиеДанные.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА")
		И (Не ТекущиеДанные.НаличиеПоДаннымУчета Или Не ТекущиеДанные.НаличиеФактическое) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ЭлементыАмортизации = НовыйЭлементыАмортизации();
	ЭлементыАмортизацииПересмотрено = НовыйЭлементыАмортизацииПересмотрено();
	ЗаполнитьЗначенияСвойств(ЭлементыАмортизации, ТекущиеДанные);
	ЗаполнитьЗначенияСвойств(ЭлементыАмортизацииПересмотрено, ТекущиеДанные);
	ПараметрыФормы.Вставить("ЭлементыАмортизации", ЭлементыАмортизации);
	ПараметрыФормы.Вставить("ЭлементыАмортизацииПересмотрено", ЭлементыАмортизацииПересмотрено);
	Если ТекущиеДанные.ВидОбъекта = ПредопределенноеЗначение("Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА") Тогда
		ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ЭлементыАмортизации");
	Иначе
		ПараметрыФормы.Вставить("КлючНазначенияИспользования", "СрокИспользования");
	КонецЕсли;
	ПараметрыФормы.Вставить("Пересмотрено", ТекущиеДанные.Пересмотрено);
	
	ОткрытьФормуРедактированияЭлементовАмортизации(ПараметрыФормы, Элементы.ОбъектыУчета.ТекущаяСтрока);
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьСрокДействияПраваПредставление(ОбъектУчета)
	Отбор = Новый Структура("ОбъектУчета", ОбъектУчета);
	ОбновляемыеСтроки = Объект.ОбъектыУчета.НайтиСтроки(Отбор);
	Если ОбновляемыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СрокДействияПрава = Справочники.НематериальныеАктивы.ПредставлениеСрокаДействияПрава(ОбъектУчета);
	Для Каждого ОбновляемаяСтрока Из ОбновляемыеСтроки Цикл
		ОбновляемаяСтрока.СрокДействияПраваПредставление = СрокДействияПрава;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция НовыйЭлементыАмортизации()
	ЭлементыАмортизации = Новый Структура;

	ЭлементыАмортизации.Вставить("НеопределенныйСрокПолезногоИспользования", Ложь);
	ЭлементыАмортизации.Вставить("СпособНачисленияАмортизации", ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииНМА.Линейный"));
	ЭлементыАмортизации.Вставить("СрокПолезногоИспользования", 12);
	ЭлементыАмортизации.Вставить("ОбъемПродукцииРабот", 0);
	ЭлементыАмортизации.Вставить("Коэффициент", 1);
	ЭлементыАмортизации.Вставить("ЛиквидационнаяСтоимость", 0);
	
	Возврат ЭлементыАмортизации;
КонецФункции

&НаКлиенте
Функция НовыйЭлементыАмортизацииПересмотрено()
	НовыйЭлементыАмортизацииПересмотрено = Новый Структура;
	Для Каждого ЭлементСтруктуры Из НовыйЭлементыАмортизации() Цикл
		НовыйЭлементыАмортизацииПересмотрено.Вставить(ЭлементСтруктуры.Ключ + "Пересмотрено");
	КонецЦикла;
	Возврат НовыйЭлементыАмортизацииПересмотрено;
КонецФункции

&НаСервере
Функция КонтекстДокумента()
	
	КонтекстДокумента = Новый Структура;
	КонтекстДокумента.Вставить("Организация",       Объект.Организация);
	КонтекстДокумента.Вставить("Дата",              Объект.Дата);
	КонтекстДокумента.Вставить("ОтветственноеЛицо", Объект.ОтветственноеЛицо);
	
	Возврат КонтекстДокумента;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииОбъектаУчетаСервер(ДанныеСтроки, ОбъектУчета)
	
	КонтекстДокумента = КонтекстДокумента();
	ДанныеЗаполнения = Документы.ИнвентаризацияНМА.ДанныеПоУчету(
		КонтекстДокумента,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектУчета));
	
	Если ДанныеЗаполнения.ОбъектыУчета.Количество() = 0 Тогда
		ДанныеЗаполненияСтроки = Документы.ИнвентаризацияНМА.НовыйДанныеЗаполненияОбъектыУчета().Добавить();
		ДанныеСтроки = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ДанныеЗаполненияСтроки);
		ДанныеСтроки.ОбъектУчета = ОбъектУчета;
		ВидОбъекта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектУчета, "ОтражениеБУ");
		Если ВидОбъекта <> Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив Тогда
			ДанныеСтроки.ВидОбъекта = Перечисления.ВидыОбъектовИнвентаризацииНМА.ПрочийОбъектПрава;
		КонецЕсли;
	Иначе				
		ДанныеСтроки = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ДанныеЗаполнения.ОбъектыУчета[0]);
	КонецЕсли;
	
	СведенияНМА = УчетНМАКлиентСервер.НовыйСведенияНМА();
	ДанныеДокументаНМА = УчетНМАКлиентСервер.НовыйДанныеДокументаНМА();
	УчетНМАКлиентСервер.ЗаполнитьСведенияНМАБезПостфиксов(ДанныеСтроки, "", СведенияНМА, ДанныеДокументаНМА);
	ДанныеСтроки.Вставить("ЭлементыАмортизации", УчетНМАКлиентСервер.ПредставлениеЭлементовАмортизации(СведенияНМА, ДанныеДокументаНМА));
	ДанныеСтроки.Вставить("СрокДействияПраваПредставление",
		Справочники.НематериальныеАктивы.ПредставлениеСрокаДействияПрава(ОбъектУчета));
	
	ДанныеСтроки.Вставить("ОтражениеРезультатовИнвентаризации", ДокументыОтраженияРезультата.Получить(ОбъектУчета));
	
	// Если строка с объектом удалялась, а теперь добавляется вновь, и если при этом между этими событиями документ не был записан,
	// ранее подобранные документы затрат "восстановятся" из ранее введенных данных.
	НайденныеДокументыЗатрат = Объект.ДокументыЗатрат.НайтиСтроки(Новый Структура("ОбъектУчета", ОбъектУчета));
	ДанныеСтроки.Вставить("ДокументыЗатрат", ТекстНадписиДокументыЗатрат(НайденныеДокументыЗатрат));
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеЭлементовАмортизации(ОбъектУчета, ВидОбъектаУчета)

	ДанныеСтроки = Документы.ИнвентаризацияНМА.НовыйДанныеЗаполненияОбъектыУчета().Добавить();
	ДанныеСтроки.ОбъектУчета = ОбъектУчета;
	ДанныеСтроки.ВидОбъекта = ВидОбъектаУчета;
	СведенияНМА = УчетНМАКлиентСервер.НовыйСведенияНМА();
	ДанныеДокументаНМА = УчетНМАКлиентСервер.НовыйДанныеДокументаНМА();
	УчетНМАКлиентСервер.ЗаполнитьСведенияНМАБезПостфиксов(ДанныеСтроки, "", СведенияНМА, ДанныеДокументаНМА);

	Возврат УчетНМАКлиентСервер.ПредставлениеЭлементовАмортизации(СведенияНМА, ДанныеДокументаНМА);
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьНаКлиенте()
	ОжидатьЗавершениеЗаполнения(НачатьЗаполнение());
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборОбъектовУчетаНаСервере(ВыбранноеЗначение)
	
	ТаблицаНМА = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресВХранилище);
	
	ИсходныеДанные = Объект.ОбъектыУчета.Выгрузить();
	ИсходныеДанные.Индексы.Добавить("ОбъектУчета");
	
	ДобавленныеСтроки = Новый Соответствие;
	СписокДобавленныхОбъектов = Новый Массив;
	
	// В форму подбора передаются все объекты прав из документа. Некоторые из них могут быть там удалены.
	// При возвращении на форму документа строки, соответствующие удаленным объектам, также удаляются из табличной части.
	Объект.ОбъектыУчета.Очистить();
	Для Каждого СтрокаТаблицы Из ТаблицаНМА Цикл
		ЭлементКоллекции = Объект.ОбъектыУчета.Добавить();
		
		СтрокаИсходныхДанных = ИсходныеДанные.Найти(СтрокаТаблицы.НематериальныйАктив, "ОбъектУчета");
		Если СтрокаИсходныхДанных <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЭлементКоллекции, СтрокаИсходныхДанных);
		Иначе
			СписокДобавленныхОбъектов.Добавить(СтрокаТаблицы.НематериальныйАктив);
			ДобавленныеСтроки.Вставить(СтрокаТаблицы.НематериальныйАктив, ЭлементКоллекции);
		КонецЕсли;
			
		ЗаполнитьЗначенияСвойств(ЭлементКоллекции, СтрокаТаблицы);
		ЭлементКоллекции.ОбъектУчета = СтрокаТаблицы.НематериальныйАктив;
	КонецЦикла;
	
	КонтекстДокумента = КонтекстДокумента();
	ДанныеЗаполнения = Документы.ИнвентаризацияНМА.ДанныеПоУчету(
		КонтекстДокумента,
		СписокДобавленныхОбъектов);
	
	Для Каждого ДанныеПоОбъекту Из ДанныеЗаполнения.ОбъектыУчета Цикл
		ДобавленнаяСтрока = ДобавленныеСтроки.Получить(ДанныеПоОбъекту.ОбъектУчета);
		Если ДобавленнаяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, ДанныеПоОбъекту);
		ДобавленнаяСтрока.НаличиеПоДаннымУчета = Ложь;
		СведенияНМА = УчетНМАКлиентСервер.НовыйСведенияНМА();
		ДанныеДокументаНМА = УчетНМАКлиентСервер.НовыйДанныеДокументаНМА();
		УчетНМАКлиентСервер.ЗаполнитьСведенияНМАБезПостфиксов(ДобавленнаяСтрока, "", СведенияНМА, ДанныеДокументаНМА);
		ДобавленнаяСтрока.ЭлементыАмортизации = УчетНМАКлиентСервер.ПредставлениеЭлементовАмортизации(СведенияНМА, ДанныеДокументаНМА);
		ДобавленнаяСтрока.СрокДействияПраваПредставление =
			Справочники.НематериальныеАктивы.ПредставлениеСрокаДействияПрава(ДобавленнаяСтрока.ОбъектУчета);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПоместитьОбъектыУчетаВХранилище()
	ТаблицаНМА = Объект.ОбъектыУчета.Выгрузить(, "НомерСтроки, ОбъектУчета");
	// Форма подбора рассчитана на название реквизита "Нематериальный актив".
	ТаблицаНМА.Колонки.Найти("ОбъектУчета").Имя = "НематериальныйАктив";
	Возврат ПоместитьВоВременноеХранилище(ТаблицаНМА);
КонецФункции

&НаКлиенте
Процедура ОтобратьСтроки(ИмяКоманды)
	Если ИмяКоманды = АктивныйОтборСтрок Тогда
		Возврат;
	Иначе
		АктивныйОтборСтрок = ИмяКоманды;
	КонецЕсли;
	
	ОтобратьСтрокиНаСервере(ИмяКоманды);
	
КонецПроцедуры

&НаСервере
Функция ЗначениеОтбораСтрок()
	
	Отбор = Новый Структура;
	Если АктивныйОтборСтрок = "ОтобратьСтрокиНаКонтроле" Тогда
		Отбор.Вставить("ОсобыйКонтроль", Истина);
	КонецЕсли;
	Возврат Отбор;

КонецФункции

&НаСервере
Процедура ОтобратьСтрокиНаСервере(ИмяКоманды)
	УстановитьВидимостьНастройкиОтбора(ИмяКоманды);
	ЭлементыФормБП.ОтобратьСтрокиНаСервере(ЭтотОбъект, ИмяКоманды, "ОбъектыУчета", ЗначениеОтбораСтрок());	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьНастройкиОтбора(ИмяКоманды)
	Элементы.ГруппаОтбор.Видимость = Объект.ПроверятьЭлементыАмортизации;
	Элементы.ОбъектыУчетаНастройкаКонтроля.Видимость = (ИмяКоманды = "ОтобратьСтрокиНаКонтроле");
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьСостояниеДокумента();
	
	УстановитьВидимостьНастройкиОтбора(АктивныйОтборСтрок);
	ЭлементыФормБП.НастроитьОтборСтрок(ЭтотОбъект);
	ПолучитьАктуальныеДанныеОбЭлементахАмортизации();
	ЗаполнитьДобавленныеКолонки();
	
	ДокументыОтраженияРезультата = Документы.ИнвентаризацияНМА.ДокументыОтраженияРезультатаИнвентаризации(Объект.Ссылка);
	
	ЗаполнитьДокументыОтражения();
	ЗаполнитьНадписьДокументыЗатрат();
	
	НастроитьВидимость(ЭтотОбъект);
	
	КритерииКонтроля = Документы.ИнвентаризацияНМА.НовыйКритерииКонтроля(Объект.Дата);
	
	ОтметитьОсобыйКонтроль();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьВидимость(Форма)
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	Элементы.ОбъектыУчетаПересмотрено.Видимость             = Объект.ПроверятьЭлементыАмортизации;
	Элементы.ОбъектыУчетаЭлементыАмортизации.Видимость      = Объект.ПроверятьЭлементыАмортизации;
	Элементы.ОбъектыУчетаКомментарийКОбъектуУчета.Видимость = Объект.ПроверятьЭлементыАмортизации;
	Элементы.ОбъектыУчетаДокументыЗатрат.Видимость          = Объект.ПроверятьДокументыЗатрат;
КонецПроцедуры

&НаСервере
Процедура ПолучитьАктуальныеДанныеОбЭлементахАмортизации()
	МенеджерДокумента = Документы.ИнвентаризацияНМА;
	ДанныеЗаполненияОбъектыУчета = МенеджерДокумента.НовыйДанныеЗаполненияОбъектыУчета();
	
	СписокНМА = Новый Массив;
	СписокПрочихПрав = Новый Массив;
	
	Для Каждого СтрокаОбъектыУчета Из Объект.ОбъектыУчета Цикл
		Если СтрокаОбъектыУчета.ВидОбъекта = Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА Тогда
			СписокНМА.Добавить(СтрокаОбъектыУчета.ОбъектУчета);
		Иначе
			СписокПрочихПрав.Добавить(СтрокаОбъектыУчета.ОбъектУчета);
		КонецЕсли;
	КонецЦикла;
	
	МенеджерДокумента.ЗаполнитьОбъектыУчета(ДанныеЗаполненияОбъектыУчета, КонтекстЗаполнения(), , СписокНМА, СписокПрочихПрав);
		
	Для Каждого СтрокаОбъектыУчета Из Объект.ОбъектыУчета Цикл
		ЗаполненныеСтроки = ДанныеЗаполненияОбъектыУчета.НайтиСтроки(Новый Структура("ОбъектУчета", СтрокаОбъектыУчета.ОбъектУчета));
		Для Каждого ЗаполненнаяСтрока Из ЗаполненныеСтроки Цикл
			ЗаполнитьЗначенияСвойств(СтрокаОбъектыУчета, ЗаполненнаяСтрока, 
				"Коэффициент,
				|ЛиквидационнаяСтоимость,
				|НеопределенныйСрокПолезногоИспользования,
				|СпособНачисленияАмортизации,
				|СрокПолезногоИспользования,
				|ОбъемПродукцииРабот,
				|Бессрочный,
				|НачалоДействияПрава,
				|Обесценивался");
			Прервать;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОтметитьОсобыйКонтроль()

	Для Каждого СтрокаДокумента Из Объект.ОбъектыУчета Цикл
		СтрокаДокумента.ОсобыйКонтроль = Ложь;
		Если КритерииКонтроля.КонтролироватьРанееОбесцененные Тогда
			СтрокаДокумента.ОсобыйКонтроль = СтрокаДокумента.Обесценивался;
			Если СтрокаДокумента.ОсобыйКонтроль Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если КритерииКонтроля.КонтролироватьНеограниченныйСрокДействияПрава Тогда
			СтрокаДокумента.ОсобыйКонтроль = СтрокаДокумента.Бессрочный;
			Если СтрокаДокумента.ОсобыйКонтроль Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если КритерииКонтроля.КонтролироватьСрокИспользования Тогда
			ЭтоНМА = СтрокаДокумента.ВидОбъекта = Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА;
			СтрокаДокумента.ОсобыйКонтроль =
				ЭтоНМА И УчетНМА.СпособыАмортизацииПоСрокуИспользования().Найти(СтрокаДокумента.СпособНачисленияАмортизации) <> Неопределено
				И Не СтрокаДокумента.НеопределенныйСрокПолезногоИспользования
				И ДобавитьМесяц(Объект.Дата, СтрокаДокумента.СрокПолезногоИспользования) <= КритерииКонтроля.ДатаОкончанияКонтроль
				Или Не ЭтоНМА И ДобавитьМесяц(Объект.Дата, СтрокаДокумента.СрокПолезногоИспользования) <= КритерииКонтроля.ДатаОкончанияКонтроль;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНадписьДокументыЗатрат()
	ДокументыЗатрат = Объект.ДокументыЗатрат.Выгрузить();
	ДокументыЗатрат.Индексы.Добавить("ОбъектУчета");
	Для Каждого СтрокаОбъектУчета Из Объект.ОбъектыУчета Цикл
		НайденныеДокументыЗатрат = ДокументыЗатрат.НайтиСтроки(Новый Структура("ОбъектУчета", СтрокаОбъектУчета.ОбъектУчета));
		СтрокаОбъектУчета.ДокументыЗатрат = ТекстНадписиДокументыЗатрат(НайденныеДокументыЗатрат);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	#Область Видимость_ЭлементыАмортизации
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора, "Объект.ОбъектыУчета.Пересмотрено", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
	ГруппаОтбораПринимаемыеКУчетуНМА = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораПринимаемыеКУчетуНМА,
		"Объект.ОбъектыУчета.ВидОбъекта",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораПринимаемыеКУчетуНМА,
		"Объект.ОбъектыУчета.НаличиеПоДаннымУчета",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
		
	ГруппаОтбораСписываемыеНМА = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораСписываемыеНМА,
		"Объект.ОбъектыУчета.ВидОбъекта",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораСписываемыеНМА,
		"Объект.ОбъектыУчета.НаличиеФактическое",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаПересмотрено");
	
	// Чтобы не было отступа перед надписью элементов амортизации, которые не пересматриваются, а также
	// чтобы не отображалась картинка у НМА, которых нет по факту или по данным учета.
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	// Элементы амортизации для списываемых НМА не отображаются
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	ГруппаОтбораСписываемыеНМА = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораСписываемыеНМА,
		"Объект.ОбъектыУчета.ВидОбъекта",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбораСписываемыеНМА,
		"Объект.ОбъектыУчета.НаличиеФактическое",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаЭлементыАмортизации");
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "");
	
	#КонецОбласти
	
	#Область Текст_ОтражениеРезультатаИнвентаризации
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаОтражениеРезультатовИнвентаризации");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.ОтражениеРезультатовИнвентаризации", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.ВидОбъекта", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.ОбъектУчета", ВидСравненияКомпоновкиДанных.Заполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.НаличиеФактическое", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.НаличиеПоДаннымУчета", ВидСравненияКомпоновкиДанных.Равно, Ложь);
				
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Создать принятие к учету'"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаОтражениеРезультатовИнвентаризации");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.ОтражениеРезультатовИнвентаризации", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.ВидОбъекта", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.ОбъектУчета", ВидСравненияКомпоновкиДанных.Заполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.НаличиеФактическое", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.НаличиеПоДаннымУчета", ВидСравненияКомпоновкиДанных.Равно, Истина);
				
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Создать списание'"));
	
	#КонецОбласти
	
	#Область Текст_ДокументыЗатрат

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаДокументыЗатрат");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор, "Объект.ОбъектыУчета.ДокументыЗатрат", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Выберите документы'"));
	
	#КонецОбласти
	
	#Область НаличиеПоУчету
	
	// Для НМА "наличие по учету" заполняется автоматически и не должно изменяться.
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.ОбъектыУчета.ВидОбъекта",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОбъектыУчетаНаличиеПоДаннымУчета");
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	#КонецОбласти
КонецПроцедуры

&НаКлиенте
Процедура ВыборНастроекЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(КритерииКонтроля, Результат);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат); // Перенос установленных свойств в реквизит формы
	УстановитьПризнакОсобогоКонтроля();
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакОсобогоКонтроля()
	ОтметитьОсобыйКонтроль();
	ЭлементыФормБП.УстановитьОтборСтрок(ЭтотОбъект, "ОбъектыУчета", ЗначениеОтбораСтрок());
КонецПроцедуры

&НаСервере
Функция НачатьЗаполнение()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Документы.ИнвентаризацияНМА.ДанныеПоУчету",
		КонтекстЗаполнения());
	
КонецФункции

&НаСервере
Функция КонтекстЗаполнения()
	
	Контекст = НовыйКонтекстЗаполнения();
	ЗаполнитьЗначенияСвойств(Контекст, Объект);
	ЗаполнитьЗначенияСвойств(Контекст, ЭтотОбъект);
	Возврат Контекст;
	
КонецФункции

&НаСервере
Функция НовыйКонтекстЗаполнения()
	НовыйКонтекстЗаполнения = Новый Структура;
	НовыйКонтекстЗаполнения.Вставить("Дата", Дата(1, 1, 1));
	НовыйКонтекстЗаполнения.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	НовыйКонтекстЗаполнения.Вставить("ОтветственноеЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	НовыйКонтекстЗаполнения.Вставить("КритерииКонтроля", Документы.ИнвентаризацияНМА.НовыйКритерииКонтроля(Объект.Дата));
	Возврат НовыйКонтекстЗаполнения;
КонецФункции

&НаКлиенте
Процедура ОжидатьЗавершениеЗаполнения(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершениеОперации = Новый ОписаниеОповещения("ЗавершитьЗаполнение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ЗавершениеОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьЗаполнение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось заполнить документ. Подробная информация в журнале регистрации.'");
	КонецЕсли;
	
	ЗавершитьЗаполнениеНаСервере(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьЗаполнениеНаСервере(АдресРезультата)
	
	ДанныеЗаполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Объект.ОбъектыУчета.Загрузить(ДанныеЗаполнения.ОбъектыУчета);
	Объект.ДокументыЗатрат.Загрузить(ДанныеЗаполнения.ДокументыЗатрат);
	
	ЗаполнитьДобавленныеКолонки();
	ЗаполнитьНадписьДокументыЗатрат();
	ЗаполнитьДокументыОтражения();
	ОтметитьОсобыйКонтроль();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументыОтражения()
	
	Для Каждого СтрокаДокумента Из Объект.ОбъектыУчета Цикл
		
		ДокументОтражения = ДокументыОтраженияРезультата.Получить(СтрокаДокумента.ОбъектУчета);
		Если  ДокументОтражения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДокумента.ОтражениеРезультатовИнвентаризации = ДокументОтражения;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонки()
	
	Для Каждого СтрокаОбъектовУчета Из Объект.ОбъектыУчета Цикл
		
		Постфикс = ?(СтрокаОбъектовУчета.Пересмотрено, "Пересмотрено", "");
		
		СведенияНМА = УчетНМАКлиентСервер.НовыйСведенияНМА();
		ДанныеДокументаНМА = УчетНМАКлиентСервер.НовыйДанныеДокументаНМА();
		УчетНМАКлиентСервер.ЗаполнитьСведенияНМАБезПостфиксов(СтрокаОбъектовУчета, Постфикс, СведенияНМА, ДанныеДокументаНМА);
		
		СтрокаОбъектовУчета.ЭлементыАмортизации = УчетНМАКлиентСервер.ПредставлениеЭлементовАмортизации(
			СведенияНМА, ДанныеДокументаНМА, Постфикс);
		СтрокаОбъектовУчета.СрокДействияПраваПредставление =
			Справочники.НематериальныеАктивы.ПредставлениеСрокаДействияПрава(СтрокаОбъектовУчета.ОбъектУчета);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыбратьЗначениеФактическогоНаличия(Список)

	Обещание = ВыбратьИзМенюАсинх(Список, Элементы.ОбъектыУчетаНаличиеФактическое);
	Результат = Ждать Обещание;
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	УстановитьЗначениеФактическогоНаличия(Результат.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеФактическогоНаличия(Значение)
	
	// В процессе изменения реквизитов может меняться видимость реквизитов, что приводит к нарушению порядка
	// массива идентификаторов выделенных строк.
	ВыделенныеСтроки = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Элементы.ОбъектыУчета.ВыделенныеСтроки);
	
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		ВыделеннаяСтрока = Элементы.ОбъектыУчета.ДанныеСтроки(ИдентификаторСтроки);
		Если ВыделеннаяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВыделеннаяСтрока.НаличиеФактическое = Значение;

	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьИнвентаризационнуюКомиссию()
	
	Возврат ИнвентаризацияАктивовОбязательств.ЗаполнитьИнвентаризационнуюКомиссию(Объект);
	
КонецФункции
	
#КонецОбласти
