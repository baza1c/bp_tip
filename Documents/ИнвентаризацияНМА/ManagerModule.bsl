
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицу объектов учета - нематериальных активов, исключительных прав, лицензий -
// используемых в организации на выбранную дату и закрепленных за указанным ответственным лицом,
// а также таблицу документов затрат.
// 
// Параметры:
//  Контекст - Структура:
//    * Организация - СправочникСсылка.Организации
//    * Дата - Дата
//    * ОтветственноеЛицо - СправочникСсылка.ФизическиеЛица
//    * КритерииКонтроля - см. НовыйКритерииКонтроля
//  СписокОбъектов - Массив Из СправочникСсылка.НематериальныеАктивы
//                 - Неопределено - список объектов, для которых необходимо получить информацию (если указан массив)
// 
// Возвращаемое значение:
//  Структура:
//    * ОбъектыУчета - см. НовыйДанныеЗаполненияОбъектыУчета
//    * ДокументыЗатрат - см. НовыйДанныеЗаполненияДокументыЗатрат
//
Функция ДанныеПоУчету(Контекст, СписокОбъектов = Неопределено) Экспорт
	
	ДанныеЗаполненияОбъектыУчета = НовыйДанныеЗаполненияОбъектыУчета();
	ДанныеЗаполненияДокументыЗатрат = НовыйДанныеЗаполненияДокументыЗатрат();
	
	ЗаполнитьОбъектыУчета(ДанныеЗаполненияОбъектыУчета, Контекст, СписокОбъектов);
	ЗаполнитьДокументыЗатрат(ДанныеЗаполненияДокументыЗатрат, Контекст, ДанныеЗаполненияОбъектыУчета.ВыгрузитьКолонку("ОбъектУчета"));
	
	ДанныеПоУчету = Новый Структура;
	ДанныеПоУчету.Вставить("ОбъектыУчета", ДанныеЗаполненияОбъектыУчета);
	ДанныеПоУчету.Вставить("ДокументыЗатрат", ДанныеЗаполненияДокументыЗатрат);
	
	Возврат ДанныеПоУчету;
	
КонецФункции

// Конструктор таблицы ДанныеЗаполненияОбъектыУчета.
// Используется для заполнения табличной части ОбъектыУчета.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. текст функции
//
Функция НовыйДанныеЗаполненияОбъектыУчета() Экспорт
	
	МетаданныеТабличнойЧасти = Метаданные.Документы.ИнвентаризацияНМА.ТабличныеЧасти.ОбъектыУчета.Реквизиты;
	ДанныеЗаполнения = Новый ТаблицаЗначений();
	ДанныеЗаполнения.Колонки.Добавить("ОбъектУчета", Новый ОписаниеТипов("СправочникСсылка.НематериальныеАктивы"));
	ДанныеЗаполнения.Колонки.Добавить("ВидОбъекта", МетаданныеТабличнойЧасти.ВидОбъекта.Тип);
	ДанныеЗаполнения.Колонки.Добавить("Стоимость", БухгалтерскийУчетВызовСервераПовтИсп.ТипСуммаНеотрицательная());
	ДанныеЗаполнения.Колонки.Добавить("НаличиеПоДаннымУчета", Новый ОписаниеТипов("Булево"));
	ДанныеЗаполнения.Колонки.Добавить("НаличиеФактическое", Новый ОписаниеТипов("Булево"));
	ДанныеЗаполнения.Колонки.Добавить("НеопределенныйСрокПолезногоИспользования", Новый ОписаниеТипов("Булево"));
	ДанныеЗаполнения.Колонки.Добавить("СрокПолезногоИспользования", МетаданныеТабличнойЧасти.СрокПолезногоИспользованияПересмотрено.Тип);
	ДанныеЗаполнения.Колонки.Добавить("СпособНачисленияАмортизации", МетаданныеТабличнойЧасти.СпособНачисленияАмортизацииПересмотрено.Тип);
	ДанныеЗаполнения.Колонки.Добавить("ОбъемПродукцииРабот", МетаданныеТабличнойЧасти.ОбъемПродукцииРаботПересмотрено.Тип);
	ДанныеЗаполнения.Колонки.Добавить("Коэффициент", МетаданныеТабличнойЧасти.КоэффициентПересмотрено.Тип);
	ДанныеЗаполнения.Колонки.Добавить("ЛиквидационнаяСтоимость", МетаданныеТабличнойЧасти.ЛиквидационнаяСтоимостьПересмотрено.Тип);
	ДанныеЗаполнения.Колонки.Добавить("СрокДействияПраваДата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ДанныеЗаполнения.Колонки.Добавить("НачалоДействияПрава", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ДанныеЗаполнения.Колонки.Добавить("СрокДействияПраваКоличествоМесяцев", ОбщегоНазначения.ОписаниеТипаЧисло(4));
	ДанныеЗаполнения.Колонки.Добавить("Обесценивался", Новый ОписаниеТипов("Булево"));
	ДанныеЗаполнения.Колонки.Добавить("Бессрочный", Новый ОписаниеТипов("Булево"));
	
	Возврат ДанныеЗаполнения;
КонецФункции

// Конструктор критериев отбора объектов учета, находящихся на особом контроле, используемый по умолчанию.
// 
// Параметры:
//  Дата - Дата - Дата документа.
// 
// Возвращаемое значение:
//  Структура:
//   * КонтролироватьРанееОбесцененные - Булево
//   * КонтролироватьСрокИспользования - Булево
//   * ДатаОкончанияКонтроль - Дата
//   * КонтролироватьНеограниченныйСрокДействияПрава - Булево
//
Функция НовыйКритерииКонтроля(Дата) Экспорт
	
	НовыйКритерииКонтроля = Новый Структура;
	НовыйКритерииКонтроля.Вставить("КонтролироватьРанееОбесцененные", Истина);
	НовыйКритерииКонтроля.Вставить("КонтролироватьСрокИспользования", Истина);
	НовыйКритерииКонтроля.Вставить("ДатаОкончанияКонтроль", КонецГода(КонецГода(Дата) + 1)); // Конец следующего года
	НовыйКритерииКонтроля.Вставить("КонтролироватьНеограниченныйСрокДействияПрава", Истина);
	Возврат НовыйКритерииКонтроля;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

#Область СтандартныеПодсистемы_УправлениеДоступом

// СтандартныеПодисистемы.УправлениеДоступом

// Ограничение доступа к данным.
//
// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	| ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодисистемы.УправлениеДоступомм

#КонецОбласти

#Область СтандартныеПодсистемы_Свойства

// СтандартныеПодисистемы.Свойства
// 
// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Документ_ИнвентаризацияНМА";
	Набор.Идентификатор = Новый УникальныйИдентификатор("4cd55025-e979-4dfe-8485-9e8942c5e0e2");
	Набор.Используется  = Истина;
	
КонецПроцедуры

// Конец СтандартныеПодисистемы.Свойства

#КонецОбласти

#Область СтандартныеПодсистемы_Печать

// СтандартныеПодсистемы.Печать
// 
// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Инвентаризационная опись ОС (ИНВ-1)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ1а";
	КомандаПечати.Представление = НСтр("ru = 'Инвентаризационная опись НМА (ИНВ-1а)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Сличительная ведомость (ИНВ-18)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ18";
	КомандаПечати.Представление = НСтр("ru = 'Сличительная ведомость (ИНВ-18)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Приказ о проведении инвентаризации (ИНВ-22)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ22";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о проведении инвентаризации (ИНВ-22)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Ведомость прав
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ВедомостьПрав";
	КомандаПечати.Представление = НСтр("ru = 'Ведомость прав на результаты интеллектуальной деятельности'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Инвентаризация НМА""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ1а") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ1а", НСтр("ru = 'ИНВ-1а (Инвентаризационная опись)'"), 
			ПечатьИНВ1а(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "Документ.ИнвентаризацияНМА.ПФ_MXL_ИНВ1а");
		
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ18") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ18", НСтр("ru = 'ИНВ-18 (Сличительная ведомость)'"), 
			ПечатьИНВ18(МассивОбъектов, ОбъектыПечати, ПараметрыПечати), , "Документ.ИнвентаризацияНМА.ПФ_MXL_ИНВ18_НМА");
		
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ22") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ИНВ22",НСтр("ru = 'ИНВ-22 (Приказ о проведении инвентаризации)'"), 
			ИнвентаризацияАктивовОбязательств.ПечатьИНВ22(МассивОбъектов, ОбъектыПечати), , "ОбщийМакет.ПФ_MXL_ИНВ22");
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВедомостьПрав") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ВедомостьПрав",НСтр("ru = 'Ведомость прав на результаты интеллектуальной деятельности'"), 
			ПечатьВедомостьПрав(МассивОбъектов, ОбъектыПечати), , "Документ.ИнвентаризацияНМА.ВедомостьПрав");
		
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СтандартныеПодсистемы_ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// @skip-check module-empty-method
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "ПричинаПроведенияИнвентаризации");
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизиты(НомераТаблиц);

	Результат = Запрос.Выполнить();
	ТаблицаРеквизиты = Результат.Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);

	Если Не УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПроверятьЭлементыАмортизации", Реквизиты.ПроверятьЭлементыАмортизации);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст =
		ТекстЗапросаЭлементыАмортизацииНМА(НомераТаблиц)
		+ ТекстЗапросаСрокиИспользованияСтоимостьОбъектовПрава(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции

Процедура ЗаполнитьОбъектыУчета(ДанныеЗаполненияОбъектыУчета, Контекст, СписокВсехОбъектов = Неопределено, СписокОбъектовНМА = Неопределено, СписокПрочихПрав = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Контекст.Организация);
	Запрос.УстановитьПараметр("Период", Контекст.Дата - 1);
	Запрос.УстановитьПараметр("СчетаАктивов", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НематериальныеАктивы));
	Запрос.УстановитьПараметр("СубконтоНематериальныеАктивы", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	Запрос.УстановитьПараметр("ОтветственноеЛицо", Контекст.ОтветственноеЛицо);
	Запрос.УстановитьПараметр("СчетаКапитальныхВложений", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы));
	Запрос.УстановитьПараметр("ДатаДокумента", Контекст.Дата);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НематериальныеАктивы.Ссылка КАК НематериальныйАктив
	|ПОМЕСТИТЬ СписокОбъектов
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|ГДЕ
	|	&УсловиеСписокОбъектов
	|	И &УсловиеОтветственноеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НематериальныеАктивы.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(НематериальныеАктивы.Стоимость) КАК Стоимость
	|ПОМЕСТИТЬ Остатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХозрасчетныйОстатки.Субконто1 КАК НематериальныйАктив,
	|		ХозрасчетныйОстатки.СуммаОстатокДт КАК Стоимость
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&ДатаДокумента,
	|				Счет В (&СчетаАктивов),
	|				&СубконтоНематериальныеАктивы,
	|				Организация = &Организация
	|					И Субконто1 В
	|						(ВЫБРАТЬ
	|							СписокОбъектов.НематериальныйАктив
	|						ИЗ
	|							СписокОбъектов КАК СписокОбъектов)) КАК ХозрасчетныйОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НематериальныеАктивы.Ссылка,
	|		0
	|	ИЗ
	|		Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|	ГДЕ
	|		НематериальныеАктивы.Ссылка В(&СписокОбъектов)
	|		И &УказанСписокНМА) КАК НематериальныеАктивы
	|
	|СГРУППИРОВАТЬ ПО
	|	НематериальныеАктивы.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведения.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведения.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведения.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведения.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведения.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРабот,
	|	ПервоначальныеСведения.Коэффициент КАК Коэффициент,
	|	ПервоначальныеСведения.Период КАК Период
	|ПОМЕСТИТЬ ПервоначальныеСведения
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокОбъектов.НематериальныйАктив
	|					ИЗ
	|						СписокОбъектов КАК СписокОбъектов)) КАК ПервоначальныеСведения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭлементыАмортизации.НематериальныйАктив КАК НематериальныйАктив,
	|	ЭлементыАмортизации.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ЭлементыАмортизации.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ЭлементыАмортизации.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ЭлементыАмортизации.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	ЭлементыАмортизации.Коэффициент КАК Коэффициент,
	|	ЭлементыАмортизации.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ЭлементыАмортизации.Период КАК Период
	|ПОМЕСТИТЬ ЭлементыАмортизации
	|ИЗ
	|	РегистрСведений.ЭлементыАмортизацииНМАБухгалтерскийУчет.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокОбъектов.НематериальныйАктив
	|					ИЗ
	|						СписокОбъектов КАК СписокОбъектов)) КАК ЭлементыАмортизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбесценениеВнеоборотныхАктивовНМА.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ Обесцененные
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов.НМА КАК ОбесценениеВнеоборотныхАктивовНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбесценениеВнеоборотныхАктивов КАК ОбесценениеВнеоборотныхАктивов
	|		ПО ОбесценениеВнеоборотныхАктивовНМА.Ссылка = ОбесценениеВнеоборотныхАктивов.Ссылка
	|ГДЕ
	|	ОбесценениеВнеоборотныхАктивов.Дата < &Период
	|	И ОбесценениеВнеоборотныхАктивов.Проведен
	|	И ОбесценениеВнеоборотныхАктивов.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбесценениеВнеоборотныхАктивовНМА.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияНМАОрганизацийСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	СостоянияНМАОрганизацийСрезПоследних.Состояние КАК Состояние
	|ПОМЕСТИТЬ СостоянияНМА
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокОбъектов.НематериальныйАктив
	|					ИЗ
	|						СписокОбъектов КАК СписокОбъектов)) КАК СостоянияНМАОрганизацийСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.НематериальныйАктив КАК ОбъектУчета,
	|	НЕ ВЫБОР
	|			КОГДА ЭлементыАмортизации.НачислятьАмортизацию ЕСТЬ NULL
	|					ИЛИ ЭлементыАмортизации.Период < ПервоначальныеСведения.Период
	|				ТОГДА ЕСТЬNULL(ПервоначальныеСведения.НачислятьАмортизацию, ЛОЖЬ)
	|			ИНАЧЕ ЭлементыАмортизации.НачислятьАмортизацию
	|		КОНЕЦ КАК НеопределенныйСрокПолезногоИспользования,
	|	ВЫБОР
	|		КОГДА ЭлементыАмортизации.СпособНачисленияАмортизации ЕСТЬ NULL
	|				ИЛИ ЭлементыАмортизации.Период < ПервоначальныеСведения.Период
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведения.СпособНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПустаяСсылка))
	|		ИНАЧЕ ЭлементыАмортизации.СпособНачисленияАмортизации
	|	КОНЕЦ КАК СпособНачисленияАмортизации,
	|	ВЫБОР
	|		КОГДА ЭлементыАмортизации.СрокПолезногоИспользования ЕСТЬ NULL
	|				ИЛИ ЭлементыАмортизации.Период < ПервоначальныеСведения.Период
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведения.СрокПолезногоИспользования, 0)
	|		ИНАЧЕ ЭлементыАмортизации.СрокПолезногоИспользования
	|	КОНЕЦ КАК СрокПолезногоИспользования,
	|	ВЫБОР
	|		КОГДА ЭлементыАмортизации.ОбъемПродукцииРабот ЕСТЬ NULL
	|				ИЛИ ЭлементыАмортизации.Период < ПервоначальныеСведения.Период
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведения.ОбъемПродукцииРабот, 0)
	|		ИНАЧЕ ЭлементыАмортизации.ОбъемПродукцииРабот
	|	КОНЕЦ КАК ОбъемПродукцииРабот,
	|	ВЫБОР
	|		КОГДА ЭлементыАмортизации.Коэффициент ЕСТЬ NULL
	|				ИЛИ ЭлементыАмортизации.Период < ПервоначальныеСведения.Период
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведения.Коэффициент, 0)
	|		ИНАЧЕ ЭлементыАмортизации.Коэффициент
	|	КОНЕЦ КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ЭлементыАмортизации.ЛиквидационнаяСтоимость ЕСТЬ NULL
	|				ИЛИ ЭлементыАмортизации.Период < ПервоначальныеСведения.Период
	|			ТОГДА 0
	|		ИНАЧЕ ЭлементыАмортизации.ЛиквидационнаяСтоимость
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость
	|ПОМЕСТИТЬ АктуальныеЭлементыАмортизации
	|ИЗ
	|	Остатки КАК Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведения КАК ПервоначальныеСведения
	|		ПО Остатки.НематериальныйАктив = ПервоначальныеСведения.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЭлементыАмортизации КАК ЭлементыАмортизации
	|		ПО Остатки.НематериальныйАктив = ЭлементыАмортизации.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	События.НематериальныйАктив КАК НематериальныйАктив,
	|	МАКСИМУМ(НАЧАЛОПЕРИОДА(События.Период, ДЕНЬ)) КАК Дата
	|ПОМЕСТИТЬ НачалоОтсчетаСрока
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций КАК События
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеЭлементыАмортизации КАК АктуальныеЭлементыАмортизации
	|		ПО События.НематериальныйАктив = АктуальныеЭлементыАмортизации.ОбъектУчета
	|ГДЕ
	|	События.Период <= &Период
	|	И События.Организация = &Организация
	|	И События.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|
	|СГРУППИРОВАТЬ ПО
	|	События.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИспользованиеРесурса.НематериальныйАктив КАК НематериальныйАктив,
	|	ИспользованиеРесурса.КоличествоОборот КАК ИспользованоВсего
	|ПОМЕСТИТЬ ИспользованиеРесурса
	|ИЗ
	|	РегистрНакопления.ВыработкаНМА.Обороты(
	|			,
	|			&Период,
	|			,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						Отбор.ОбъектУчета
	|					ИЗ
	|						АктуальныеЭлементыАмортизации КАК Отбор
	|					ГДЕ
	|						Отбор.СпособНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции))) КАК ИспользованиеРесурса
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.НематериальныйАктив КАК ОбъектУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА) КАК ВидОбъекта,
	|	Остатки.Стоимость КАК Стоимость,
	|	ИСТИНА КАК НаличиеФактическое,
	|	ВЫБОР
	|		КОГДА Остатки.Стоимость > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаличиеПоДаннымУчета,
	|	АктуальныеЭлементыАмортизации.НеопределенныйСрокПолезногоИспользования КАК НеопределенныйСрокПолезногоИспользования,
	|	АктуальныеЭлементыАмортизации.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	АктуальныеЭлементыАмортизации.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	АктуальныеЭлементыАмортизации.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	АктуальныеЭлементыАмортизации.Коэффициент КАК Коэффициент,
	|	АктуальныеЭлементыАмортизации.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	НЕ Обесцененные.НематериальныйАктив ЕСТЬ NULL КАК Обесценивался,
	|	НематериальныеАктивы.СрокДействияПраваКоличествоМесяцев = 0
	|		И НематериальныеАктивы.СрокДействияПраваДата = ДАТАВРЕМЯ(1, 1, 1) КАК Бессрочный,
	|	НематериальныеАктивы.НачалоДействияПрава КАК НачалоДействияПрава,
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(НачалоОтсчетаСрока.Дата, &Период), ДЕНЬ) КАК ДатаНачалаИспользования,
	|	ЕСТЬNULL(ИспользованиеРесурса.ИспользованоВсего, 0) КАК ИспользованоВсего
	|ПОМЕСТИТЬ НематериальныеАктивы
	|ИЗ
	|	Остатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ПО Остатки.НематериальныйАктив = НематериальныеАктивы.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеЭлементыАмортизации КАК АктуальныеЭлементыАмортизации
	|		ПО Остатки.НематериальныйАктив = АктуальныеЭлементыАмортизации.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Обесцененные КАК Обесцененные
	|		ПО Остатки.НематериальныйАктив = Обесцененные.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ НачалоОтсчетаСрока КАК НачалоОтсчетаСрока
	|		ПО Остатки.НематериальныйАктив = НачалоОтсчетаСрока.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИспользованиеРесурса КАК ИспользованиеРесурса
	|		ПО Остатки.НематериальныйАктив = ИспользованиеРесурса.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОбъектов.НематериальныйАктив КАК ОбъектУчетаПрочиеПрава
	|ПОМЕСТИТЬ СписокПрочихПрав
	|ИЗ
	|	СписокОбъектов КАК СписокОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ НематериальныеАктивы КАК НематериальныеАктивы
	|		ПО СписокОбъектов.НематериальныйАктив = НематериальныеАктивы.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияНМА КАК СостоянияНМА
	|		ПО СписокОбъектов.НематериальныйАктив = СостоянияНМА.НематериальныйАктив
	|ГДЕ
	|	НематериальныеАктивы.ОбъектУчета ЕСТЬ NULL
	|	И НЕ &УстановленСписокПрочихПрав
	|	И ЕСТЬNULL(СостоянияНМА.Состояние, ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НематериальныеАктивы.Ссылка
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|ГДЕ
	|	НематериальныеАктивы.Ссылка В(&СписокПрочихПрав)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрочиеОбъектыПрава.ОбъектУчета КАК ОбъектУчета,
	|	ПрочиеОбъектыПрава.Период КАК ДатаПоследнейИнвентаризации,
	|	ПрочиеОбъектыПрава.Стоимость КАК Стоимость,
	|	ПрочиеОбъектыПрава.ДатаОкончанияИспользования КАК ДатаОкончанияИспользования,
	|	ПрочиеОбъектыПрава.ДатаОкончанияИспользования > &Период
	|		ИЛИ ПрочиеОбъектыПрава.ДатаОкончанияИспользования = ДАТАВРЕМЯ(1, 1, 1) КАК Используется
	|ПОМЕСТИТЬ РезультатыПриобретенияПравИПрошлойИнвентаризации
	|ИЗ
	|	РегистрСведений.РезультатыИнтеллектуальнойДеятельности.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ОбъектУчета В
	|					(ВЫБРАТЬ
	|						СписокПрочихПрав.ОбъектУчетаПрочиеПрава
	|					ИЗ
	|						СписокПрочихПрав КАК СписокПрочихПрав)) КАК ПрочиеОбъектыПрава
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИнвентаризацияНМА.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПредыдущаяИнвентаризация
	|ИЗ
	|	Документ.ИнвентаризацияНМА КАК ИнвентаризацияНМА
	|ГДЕ
	|	ИнвентаризацияНМА.ОтветственноеЛицо = &ОтветственноеЛицо
	|	И ИнвентаризацияНМА.Организация = &Организация
	|	И ИнвентаризацияНМА.Дата <= &Период
	|	И ИнвентаризацияНМА.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НематериальныеАктивы.Ссылка КАК ОбъектУчета,
	|	НематериальныеАктивы.ОтражениеБУ КАК ОтражениеБУ,
	|	НематериальныеАктивы.СрокДействияПраваКоличествоМесяцев КАК СрокДействияПраваКоличествоМесяцев,
	|	НематериальныеАктивы.СрокДействияПраваДата КАК СрокДействияПраваДата,
	|	НематериальныеАктивы.НачалоДействияПрава КАК НачалоДействияПрава
	|ПОМЕСТИТЬ АктуальныеНеучтенныеОбъектыПрава
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокПрочихПрав КАК СписокПрочихПрав
	|		ПО НематериальныеАктивы.Ссылка = СписокПрочихПрав.ОбъектУчетаПрочиеПрава
	|		ЛЕВОЕ СОЕДИНЕНИЕ РезультатыПриобретенияПравИПрошлойИнвентаризации КАК РезультатыПрошлойИнвентаризации
	|		ПО НематериальныеАктивы.Ссылка = РезультатыПрошлойИнвентаризации.ОбъектУчета
	|ГДЕ
	|	РезультатыПрошлойИнвентаризации.ОбъектУчета ЕСТЬ NULL
	|	И (&УстановленСписокПрочихПрав
	|			ИЛИ НематериальныеАктивы.Ссылка В (&СписокОбъектов)
	|			ИЛИ (НематериальныеАктивы.СрокДействияПраваКоличествоМесяцев = 0
	|					И НематериальныеАктивы.СрокДействияПраваДата = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ ДОБАВИТЬКДАТЕ(НематериальныеАктивы.НачалоДействияПрава, МЕСЯЦ, НематериальныеАктивы.СрокДействияПраваКоличествоМесяцев) > &ДатаДокумента
	|					И НематериальныеАктивы.СрокДействияПраваКоличествоМесяцев <> 0
	|				ИЛИ НематериальныеАктивы.СрокДействияПраваДата > &ДатаДокумента)
	|				И НематериальныеАктивы.НачалоДействияПрава <= &ДатаДокумента
	|				И НЕ НематериальныеАктивы.ПометкаУдаления
	|				И НЕ 1 В
	|						(ВЫБРАТЬ ПЕРВЫЕ 1
	|							1 КАК Отбор
	|						ИЗ
	|							ПредыдущаяИнвентаризация КАК ПредыдущаяИнвентаризация)
	|				И НЕ НематериальныеАктивы.Ссылка В (&ОбъектыДругихОрганизаций))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК ОбъектУчета,
	|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК Стоимость
	|ПОМЕСТИТЬ СтоимостьКапитальныхВложенийАктуальныхНеучтеныхОбъектовПрава
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			,
	|			&Период,
	|			,
	|			НЕ СчетДт В (&СчетаАктивов),
	|			&СубконтоНематериальныеАктивы,
	|			СчетКт В (&СчетаКапитальныхВложений),
	|			&СубконтоНематериальныеАктивы,
	|			Организация = &Организация
	|				И СубконтоКт1 В
	|					(ВЫБРАТЬ
	|						АктуальныеНеучтенныеОбъектыПрава.ОбъектУчета
	|					ИЗ
	|						АктуальныеНеучтенныеОбъектыПрава КАК АктуальныеНеучтенныеОбъектыПрава)) КАК ХозрасчетныйОборотыДтКт
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НематериальныеАктивы.Ссылка КАК ОбъектУчета,
	|	ВЫБОР
	|		КОГДА НематериальныеАктивы.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.МалоценныйНМА)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.ПрочийОбъектПрава)
	|	КОНЕЦ КАК ВидОбъекта,
	|	ПрочиеОбъектыПрава.Стоимость КАК Стоимость,
	|	ИСТИНА КАК НаличиеФактическое,
	|	ИСТИНА КАК НаличиеПоДаннымУчета,
	|	РАЗНОСТЬДАТ(&ДатаДокумента, ПрочиеОбъектыПрава.ДатаОкончанияИспользования, МЕСЯЦ) КАК СрокПолезногоИспользования,
	|	НематериальныеАктивы.СрокДействияПраваКоличествоМесяцев = 0
	|		И НематериальныеАктивы.СрокДействияПраваДата = ДАТАВРЕМЯ(1, 1, 1) КАК Бессрочный,
	|	НематериальныеАктивы.НачалоДействияПрава КАК НачалоДействияПрава,
	|	ПрочиеОбъектыПрава.ДатаОкончанияИспользования = ДАТАВРЕМЯ(1, 1, 1) КАК НеопределенныйСрокПолезногоИспользования
	|ПОМЕСТИТЬ ПрочиеОбъектыПрава
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РезультатыПриобретенияПравИПрошлойИнвентаризации КАК ПрочиеОбъектыПрава
	|		ПО НематериальныеАктивы.Ссылка = ПрочиеОбъектыПрава.ОбъектУчета
	|ГДЕ
	|	(&УстановленСписокПрочихПрав
	|			ИЛИ ПрочиеОбъектыПрава.Используется)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АктуальныеНеучтенныеОбъектыПрава.ОбъектУчета,
	|	ВЫБОР
	|		КОГДА АктуальныеНеучтенныеОбъектыПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.МалоценныйНМА)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.ПрочийОбъектПрава)
	|	КОНЕЦ,
	|	ЕСТЬNULL(СтоимостьКапитальныхВложений.Стоимость, 0),
	|	ИСТИНА,
	|	ИСТИНА,
	|	РАЗНОСТЬДАТ(&ДатаДокумента, ВЫБОР
	|			КОГДА АктуальныеНеучтенныеОбъектыПрава.СрокДействияПраваКоличествоМесяцев = 0
	|				ТОГДА АктуальныеНеучтенныеОбъектыПрава.СрокДействияПраваДата
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(АктуальныеНеучтенныеОбъектыПрава.НачалоДействияПрава, МЕСЯЦ, АктуальныеНеучтенныеОбъектыПрава.СрокДействияПраваКоличествоМесяцев)
	|		КОНЕЦ, МЕСЯЦ),
	|	АктуальныеНеучтенныеОбъектыПрава.СрокДействияПраваКоличествоМесяцев = 0
	|		И АктуальныеНеучтенныеОбъектыПрава.СрокДействияПраваДата = ДАТАВРЕМЯ(1, 1, 1),
	|	АктуальныеНеучтенныеОбъектыПрава.НачалоДействияПрава,
	|	АктуальныеНеучтенныеОбъектыПрава.СрокДействияПраваКоличествоМесяцев = 0
	|		И АктуальныеНеучтенныеОбъектыПрава.СрокДействияПраваДата = ДАТАВРЕМЯ(1, 1, 1)
	|ИЗ
	|	АктуальныеНеучтенныеОбъектыПрава КАК АктуальныеНеучтенныеОбъектыПрава
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьКапитальныхВложенийАктуальныхНеучтеныхОбъектовПрава КАК СтоимостьКапитальныхВложений
	|		ПО АктуальныеНеучтенныеОбъектыПрава.ОбъектУчета = СтоимостьКапитальныхВложений.ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НематериальныеАктивы.ОбъектУчета КАК ОбъектУчета,
	|	НематериальныеАктивы.ВидОбъекта КАК ВидОбъекта,
	|	НематериальныеАктивы.Стоимость КАК Стоимость,
	|	НематериальныеАктивы.НаличиеФактическое КАК НаличиеФактическое,
	|	НематериальныеАктивы.НаличиеПоДаннымУчета КАК НаличиеПоДаннымУчета,
	|	НематериальныеАктивы.НеопределенныйСрокПолезногоИспользования КАК НеопределенныйСрокПолезногоИспользования,
	|	НематериальныеАктивы.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	НематериальныеАктивы.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	НематериальныеАктивы.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	НематериальныеАктивы.Коэффициент КАК Коэффициент,
	|	НематериальныеАктивы.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	НематериальныеАктивы.Обесценивался КАК Обесценивался,
	|	НематериальныеАктивы.Бессрочный КАК Бессрочный,
	|	НематериальныеАктивы.НачалоДействияПрава КАК НачалоДействияПрава,
	|	НематериальныеАктивы.ДатаНачалаИспользования КАК ДатаНачалаИспользования,
	|	НематериальныеАктивы.ИспользованоВсего КАК ИспользованоВсего
	|ИЗ
	|	НематериальныеАктивы КАК НематериальныеАктивы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрочиеОбъектыПрава.ОбъектУчета,
	|	ПрочиеОбъектыПрава.ВидОбъекта,
	|	ПрочиеОбъектыПрава.Стоимость,
	|	ПрочиеОбъектыПрава.НаличиеФактическое,
	|	ПрочиеОбъектыПрава.НаличиеПоДаннымУчета,
	|	ПрочиеОбъектыПрава.НеопределенныйСрокПолезногоИспользования,
	|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияРасходовПоАмортизации.ПустаяСсылка),
	|	ПрочиеОбъектыПрава.СрокПолезногоИспользования,
	|	0,
	|	0,
	|	0,
	|	ЛОЖЬ,
	|	ПрочиеОбъектыПрава.Бессрочный,
	|	ПрочиеОбъектыПрава.НачалоДействияПрава,
	|	ПрочиеОбъектыПрава.НачалоДействияПрава,
	|	0
	|ИЗ
	|	ПрочиеОбъектыПрава КАК ПрочиеОбъектыПрава";
	
	// Заполнение может происходить в нескольких режимах:
	// 1. Заполнение "по остаткам" по ответственному лицу, вид объекта учета определяется в момент заполнения - СписокВсехОбъектов = Неопределено,
	// СписокОбъектовНМА = Неопределено, СписокПрочихПрав = Неопределено;
	// 2. Заполнение информации об элементах амортизации при открытии формы документа: объекты прав уже классифицированы и разбиты на списки:
	// отдельно есть список НМА и отдельно - список всех остальных прав. В этом режиме заполняется информация об элементах затрат,
	// сроках использования и критериях отбора - СписокВсехОбъектов = Неопределено, СписокОбъектовНМА = Массив, СписокПрочихПрав = Массив;
	// 3. Заполнение данных о конкретных объектах - добавляемых на форму вручную путем добавления строки или из формы подбора справочника "Нематериальные активы",
	// которые пока для нас неклассифицированы: один и тот же объект может быть и НМА, и МалоценнымНМА - в процессе заполнения 
	// определяется помимо всего прочего и вид объекта - СписокВсехОбъектов - Масссив, СписокОбъектовНМА = Неопределено, СписокПрочихПрав = Неопределено.
	
	// Вариант заполнения №1
	Если СписокВсехОбъектов = Неопределено И СписокОбъектовНМА = Неопределено Тогда
		Запрос.УстановитьПараметр("УсловиеСписокОбъектов", Истина);
		Запрос.УстановитьПараметр("СписокОбъектов", Новый Массив);
		Запрос.УстановитьПараметр("УказанСписокНМА", Ложь);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтветственноеЛицо", "ОтветственноеЛицо = &ОтветственноеЛицо");
		Запрос.УстановитьПараметр("УстановленСписокПрочихПрав", Ложь); // Прочие права - это все, что не НМА.
		Запрос.УстановитьПараметр("СписокПрочихПрав", Новый Массив);
		// Нематериальные активы, которые относятся к другим организациям - были приняты к учету, поступили, но еще не приняты к учету, списанные НМА,
		// а также иные права, учтенные в других организациях, не должны попадать в список Прочих прав текущей организации - то есть таких прав,
		// которые на момент первой инвентаризации по ответственному лицу,подпадают под критерии инвентаризации
		// по ответственному лицу и сроку действия права, указанным в справочнике НМА.
		Запрос.УстановитьПараметр("ОбъектыДругихОрганизаций", ОбъектыДругихОрганизаций(Контекст.Организация, Контекст.Дата - 1, Контекст.ОтветственноеЛицо));
	// Вариант заполнения №2
	ИначеЕсли СписокОбъектовНМА <> Неопределено Тогда
		Запрос.УстановитьПараметр("СписокОбъектов", СписокОбъектовНМА);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСписокОбъектов", "Ссылка В (&СписокОбъектов)");
		// Возвращаются параметры амортизации даже для тех объектов, которых нет в остатках
		Запрос.УстановитьПараметр("УказанСписокНМА", Истина);
		Запрос.УстановитьПараметр("УсловиеОтветственноеЛицо", Истина); // Игнорируется отбор по ответственному лицу
		Запрос.УстановитьПараметр("УстановленСписокПрочихПрав", Истина);
		Запрос.УстановитьПараметр("СписокПрочихПрав", СписокПрочихПрав);
		Запрос.УстановитьПараметр("ОбъектыДругихОрганизаций", Новый Массив);
	// Вариант заполнения №3 
	Иначе
		Запрос.УстановитьПараметр("СписокОбъектов", СписокВсехОбъектов);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСписокОбъектов", "Ссылка В (&СписокОбъектов)");
		Запрос.УстановитьПараметр("УказанСписокНМА", Ложь);
		Запрос.УстановитьПараметр("УсловиеОтветственноеЛицо", Истина); // Игнорируется отбор по ответственному лицу
		Запрос.УстановитьПараметр("УстановленСписокПрочихПрав", Ложь);
		Запрос.УстановитьПараметр("СписокПрочихПрав", Новый Массив);
		Запрос.УстановитьПараметр("ОбъектыДругихОрганизаций", Новый Массив);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	АлгоритмПериодаАмортизации = Перечисления.АлгоритмыПериодаАмортизации.АлгоритмРасчетаАмортизации(Контекст.Дата, Контекст.Организация);
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока =  ДанныеЗаполненияОбъектыУчета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если НоваяСтрока.ВидОбъекта = Перечисления.ВидыОбъектовИнвентаризацииНМА.НМА Тогда
			НачалоОтсчетаРесурсМесяцев = Выборка.ДатаНачалаИспользования;
			Если АлгоритмПериодаАмортизации <> Перечисления.АлгоритмыПериодаАмортизации.СДатыПризнания Тогда
				НачалоОтсчетаРесурсМесяцев = КонецМесяца(Выборка.ДатаНачалаИспользования) + 1;
			КонецЕсли;
			КонецСрокаПолезногоИспользования = ДобавитьМесяц(
				НачалоОтсчетаРесурсМесяцев,
				Выборка.СрокПолезногоИспользования) - 1;
			ОстатокСрокаМесяцев = Макс(0, АмортизацияАктивов.РазностьМесяцев(КонецСрокаПолезногоИспользования, НачалоДня(Контекст.Дата)));
			НоваяСтрока.СрокПолезногоИспользования = ОстатокСрокаМесяцев;
			Если Выборка.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда
				НоваяСтрока.ОбъемПродукцииРабот = Макс(НоваяСтрока.ОбъемПродукцииРабот - Выборка.ИспользованоВсего, 0);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеЗаполненияОбъектыУчета.Индексы.Добавить("ОбъектУчета");
	
КонецПроцедуры

Функция ДокументыОтраженияРезультатаИнвентаризации(Ссылка) Экспорт
	
	Если Ссылка.Пустая() Тогда
		Возврат Новый ФиксированноеСоответствие(Новый Соответствие);
	КонецЕсли;

	НайденныеДокументыОтражения = Новый Соответствие;
	
	// Получаем документы "с запасом" по всем объектам на случай, если объект после создания списания или принятия к учету,
	// по ошибке удалили из документа, а позже снова решили добавить в документ.
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СвязанныеДокументы.Ссылка КАК ДокументОтражения
	|ПОМЕСТИТЬ СвязанныеДокументы
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&Инвентаризация) КАК СвязанныеДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПринятиеКУчету.НематериальныйАктив КАК НематериальныйАктив,
	|	ПринятиеКУчету.Ссылка КАК ДокументОтражения,
	|	ПринятиеКУчету.Номер КАК Номер,
	|	ПринятиеКУчету.МоментВремени КАК МоментВремени
	|ПОМЕСТИТЬ ДокументыОтражения
	|ИЗ
	|	СвязанныеДокументы КАК СвязанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчету
	|		ПО СвязанныеДокументы.ДокументОтражения = ПринятиеКУчету.Ссылка
	|ГДЕ
	|	ПринятиеКУчету.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Списание.НематериальныйАктив,
	|	Списание.Ссылка,
	|	Списание.Номер,
	|	Списание.МоментВремени
	|ИЗ
	|	СвязанныеДокументы КАК СвязанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеНМА КАК Списание
	|		ПО СвязанныеДокументы.ДокументОтражения = Списание.Ссылка
	|ГДЕ
	|	Списание.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыОтражения.НематериальныйАктив КАК НематериальныйАктив,
	|	ДокументыОтражения.ДокументОтражения КАК ДокументОтражения
	|ИЗ
	|	ДокументыОтражения КАК ДокументыОтражения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыОтражения.МоментВремени УБЫВ
	|ИТОГИ ПО
	|	НематериальныйАктив";

	Запрос.УстановитьПараметр("Инвентаризация", Ссылка);

	ВыборкаНМА = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНМА.Следующий() Цикл
		
		ВыборкаДокументов = ВыборкаНМА.Выбрать();
		
		// Если есть несколько документов принятия к учету, то запоминаем самый первый: последний по моменту времени.
		ВыборкаДокументов.Следующий();
		
		НайденныеДокументыОтражения.Вставить(ВыборкаНМА.НематериальныйАктив, ВыборкаДокументов.ДокументОтражения);
	КонецЦикла;
		
	Возврат Новый ФиксированноеСоответствие(НайденныеДокументыОтражения);
	
КонецФункции

// Возвращает список имен документов по выбранной организации, имеющих движения по регистру бухгалтерии.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ОтборТипов - Неопределено
//             - Массив из Тип
// 
// Возвращаемое значение:
//  СписокЗначений из Строка
//
Функция ИменаИспользуемыхРегистраторовРегистраБухгалтерии(Организация, ОтборТипов = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТИПЗНАЧЕНИЯ(Хозрасчетный.Регистратор) КАК ТипЗначения
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Организация = &Организация
		|	И &УсловиеТипа";
	
	Если ОтборТипов = Неопределено Тогда
		Запрос.УстановитьПараметр("УсловиеТипа", Истина);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеТипа", "ТИПЗНАЧЕНИЯ(Хозрасчетный.Регистратор) В (&ТипыДокументов)");
		Запрос.УстановитьПараметр("ТипыДокументов", ОтборТипов);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИменаРегистраторов = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		МетаданныеРегистратора = Метаданные.НайтиПоТипу(Выборка.ТипЗначения);
		ИменаРегистраторов.Добавить(МетаданныеРегистратора.ПолноеИмя(), МетаданныеРегистратора.Представление());
	КонецЦикла;
	
	ИменаРегистраторов.СортироватьПоПредставлению();
	
	Возврат ИменаРегистраторов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбъектыДругихОрганизаций(Организация, Период, ОтветственноеЛицо)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СостоянияНМА.НематериальныйАктив КАК ОбъектПрава
		|ИЗ
		|	РегистрСведений.СостоянияНМАОрганизаций.СрезПоследних(
		|			&Период,
		|			Организация <> &Организация
		|				И НематериальныйАктив.ОтветственноеЛицо = &ОтветственноеЛицо) КАК СостоянияНМА
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПрочиеОбъектыПрав.ОбъектУчета
		|ИЗ
		|	РегистрСведений.РезультатыИнтеллектуальнойДеятельности.СрезПоследних(
		|			&Период,
		|			Организация <> &Организация
		|				И ОбъектУчета.ОтветственноеЛицо = &ОтветственноеЛицо) КАК ПрочиеОбъектыПрав";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ОтветственноеЛицо", ОтветственноеЛицо);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОбъектПрава");

КонецФункции

Функция НовыйДанныеЗаполненияДокументыЗатрат()
	ДанныеЗаполнения = Новый ТаблицаЗначений();
	ДанныеЗаполнения.Колонки.Добавить("ОбъектУчета", Новый ОписаниеТипов("СправочникСсылка.НематериальныеАктивы"));
	ДанныеЗаполнения.Колонки.Добавить("СуммаЗатратПоДокументу", БухгалтерскийУчетВызовСервераПовтИсп.ТипСуммаНеотрицательная());
	ДанныеЗаполнения.Колонки.Добавить("КомментарийКДокументуЗатрат", Новый ОписаниеТипов("Строка"));
	ДанныеЗаполнения.Колонки.Добавить("ДокументЗатрат", Метаданные.ОпределяемыеТипы.ДокументыЗатрат.Тип);
	Возврат ДанныеЗаполнения;
КонецФункции

Процедура ЗаполнитьДокументыЗатрат(ДанныеЗаполненияДокументыЗатрат, Контекст, СписокОбъектов)
	
	// Заполнение документов затрат происходит по алгоритму:
	// 1. Если объект участвовал в инвентаризации и для него указывались документы затрат, то они будут перенесены
	// из прошлого документа;
	// 2. Если инвентаризации по объекту не было и объект права приобретен документом "Поступлением товаров и услуг"
	// с видом операции "Приобретение права", то документ приобретения права будет признан документом затрат;
	// 3. Иначе - документы, которые делали движения по субконто "Нематериальные активы" (при условии, что этих документов не больше двух).
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета КАК ОбъектУчета,
	|	МАКСИМУМ(ИнвентаризацияНМАДокументыЗатрат.Ссылка.Дата) КАК Дата
	|ПОМЕСТИТЬ МаксимальныеДаты
	|ИЗ
	|	Документ.ИнвентаризацияНМА.ДокументыЗатрат КАК ИнвентаризацияНМАДокументыЗатрат
	|ГДЕ
	|	ИнвентаризацияНМАДокументыЗатрат.Ссылка.Проведен
	|	И ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета В(&СписокОбъектов)
	|	И ИнвентаризацияНМАДокументыЗатрат.Ссылка.Организация = &Организация
	|	И ИнвентаризацияНМАДокументыЗатрат.Ссылка.Дата <= &Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета КАК ОбъектУчета,
	|	МАКСИМУМ(ИнвентаризацияНМАДокументыЗатрат.Ссылка) КАК ПредыдущаяИнвентаризацияСДокументами
	|ПОМЕСТИТЬ ПредыдущиеИнвентаризации
	|ИЗ
	|	Документ.ИнвентаризацияНМА.ДокументыЗатрат КАК ИнвентаризацияНМАДокументыЗатрат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальныеДаты КАК МаксимальныеДаты
	|		ПО ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета = МаксимальныеДаты.ОбъектУчета
	|			И ИнвентаризацияНМАДокументыЗатрат.Ссылка.Дата = МаксимальныеДаты.Дата
	|ГДЕ
	|	ИнвентаризацияНМАДокументыЗатрат.Ссылка.Проведен
	|	И ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета В(&СписокОбъектов)
	|	И ИнвентаризацияНМАДокументыЗатрат.Ссылка.Организация = &Организация
	|	И ИнвентаризацияНМАДокументыЗатрат.Ссылка.Дата <= &Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета,
	|	ПредыдущаяИнвентаризацияСДокументами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияНМАДокументыЗатрат.ДокументЗатрат КАК ДокументЗатрат,
	|	ИнвентаризацияНМАДокументыЗатрат.СуммаЗатратПоДокументу КАК СуммаЗатратПоДокументу,
	|	ИнвентаризацияНМАДокументыЗатрат.КомментарийКДокументуЗатрат КАК КомментарийКДокументуЗатрат,
	|	ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета КАК ОбъектУчета
	|ПОМЕСТИТЬ ДокументыПрошлыхИнвентаризаций
	|ИЗ
	|	Документ.ИнвентаризацияНМА.ДокументыЗатрат КАК ИнвентаризацияНМАДокументыЗатрат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПредыдущиеИнвентаризации КАК ПредыдущиеИнвентаризации
	|		ПО ИнвентаризацияНМАДокументыЗатрат.ОбъектУчета = ПредыдущиеИнвентаризации.ОбъектУчета
	|			И ИнвентаризацияНМАДокументыЗатрат.Ссылка = ПредыдущиеИнвентаризации.ПредыдущаяИнвентаризацияСДокументами
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатыИнтеллектуальнойДеятельности.Регистратор КАК Регистратор,
	|	РезультатыИнтеллектуальнойДеятельности.Стоимость КАК Стоимость,
	|	РезультатыИнтеллектуальнойДеятельности.ОбъектУчета КАК ОбъектУчета
	|ПОМЕСТИТЬ НовыеДокументыПриобретенияПравНеНМА
	|ИЗ
	|	РегистрСведений.РезультатыИнтеллектуальнойДеятельности.СрезПоследних(
	|			&Дата,
	|			Организация = &Организация
	|				И Активность
	|				И ОбъектУчета В (&СписокОбъектов)
	|				И Регистратор ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И НЕ Регистратор В
	|						(ВЫБРАТЬ
	|							ДокументыПрошлыхИнвентаризаций.ДокументЗатрат
	|						ИЗ
	|							ДокументыПрошлыхИнвентаризаций КАК ДокументыПрошлыхИнвентаризаций)) КАК РезультатыИнтеллектуальнойДеятельности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДокументыПриобретенияПрав.Регистратор КАК Регистратор,
	|	НовыеДокументыПриобретенияПрав.Стоимость КАК Стоимость,
	|	НовыеДокументыПриобретенияПрав.ОбъектУчета КАК ОбъектУчета
	|ПОМЕСТИТЬ НовыеДокументыЗатрат
	|ИЗ
	|	НовыеДокументыПриобретенияПравНеНМА КАК НовыеДокументыПриобретенияПрав
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйДвиженияССубконто.Регистратор,
	|	ХозрасчетныйДвиженияССубконто.Сумма,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			&Дата,
	|			Организация = &Организация
	|				И СчетДт В (&СчетаСтоимости)
	|				И ВидСубконтоДт1 = &ВидСубконтоНематериальныеАктивы
	|				И СубконтоДт1 В (&СписокОбъектов)
	|				И Активность
	|				И ТИПЗНАЧЕНИЯ(Регистратор) В (&Типы)
	|				И НЕ Регистратор В
	|						(ВЫБРАТЬ
	|							ДокументыПрошлыхИнвентаризаций.ДокументЗатрат
	|						ИЗ
	|							ДокументыПрошлыхИнвентаризаций КАК ДокументыПрошлыхИнвентаризаций
	|			
	|						ОБЪЕДИНИТЬ ВСЕ
	|			
	|						ВЫБРАТЬ
	|							НовыеДокументыПриобретенияПрав.Регистратор
	|						ИЗ
	|							НовыеДокументыПриобретенияПравНеНМА КАК НовыеДокументыПриобретенияПрав),
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйДвиженияССубконто.Регистратор,
	|	ХозрасчетныйДвиженияССубконто.Сумма,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт1
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			&Дата,
	|			Организация = &Организация
	|				И СчетКт В (&СчетаСтоимости)
	|				И ВидСубконтоКт1 = &ВидСубконтоНематериальныеАктивы
	|				И СубконтоКт1 В (&СписокОбъектов)
	|				И Активность
	|				И ТИПЗНАЧЕНИЯ(Регистратор) В (&Типы)
	|				И НЕ Регистратор В
	|						(ВЫБРАТЬ
	|							ДокументыПрошлыхИнвентаризаций.ДокументЗатрат
	|						ИЗ
	|							ДокументыПрошлыхИнвентаризаций КАК ДокументыПрошлыхИнвентаризаций
	|			
	|						ОБЪЕДИНИТЬ ВСЕ
	|			
	|						ВЫБРАТЬ
	|							НовыеДокументыПриобретенияПрав.Регистратор
	|						ИЗ
	|							НовыеДокументыПриобретенияПравНеНМА КАК НовыеДокументыПриобретенияПрав),
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПрошлыхИнвентаризаций.ДокументЗатрат КАК ДокументЗатрат,
	|	ДокументыПрошлыхИнвентаризаций.СуммаЗатратПоДокументу КАК СуммаЗатратПоДокументу,
	|	ДокументыПрошлыхИнвентаризаций.КомментарийКДокументуЗатрат КАК КомментарийКДокументуЗатрат,
	|	ДокументыПрошлыхИнвентаризаций.ОбъектУчета КАК ОбъектУчета,
	|	ИСТИНА КАК ЭтоДокументыПрошлойИнвентаризации
	|ИЗ
	|	ДокументыПрошлыхИнвентаризаций КАК ДокументыПрошлыхИнвентаризаций
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НовыеДокументыЗатрат.Регистратор,
	|	НовыеДокументыЗатрат.Стоимость,
	|	"""",
	|	НовыеДокументыЗатрат.ОбъектУчета,
	|	ЛОЖЬ
	|ИЗ
	|	НовыеДокументыЗатрат КАК НовыеДокументыЗатрат
	|ИТОГИ
	|	МАКСИМУМ(ЭтоДокументыПрошлойИнвентаризации)
	|ПО
	|	ОбъектУчета";
	
	Запрос.УстановитьПараметр("Организация", Контекст.Организация);
	Запрос.УстановитьПараметр("СписокОбъектов", СписокОбъектов);
	Запрос.УстановитьПараметр("Дата", Контекст.Дата - 1);
	Запрос.УстановитьПараметр("Типы", Метаданные.ОпределяемыеТипы.ДокументыЗатрат.Тип.Типы());
	Запрос.УстановитьПараметр("ВидСубконтоНематериальныеАктивы", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	СчетаСтоимости = Новый Массив;
	СчетаСтоимости.Добавить(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы);
	СчетаСтоимости.Добавить(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
	Запрос.УстановитьПараметр("СчетаСтоимости", БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаСтоимости));
		
	ВыборкаНМА = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНМА.Следующий() Цикл
		
		Выборка = ВыборкаНМА.Выбрать();
		
		// В идеальной ситуации будет максимум два документа: поступление НМА и принятие к учету.
		// Если документов больше, есть вероятность, что затрат по документу очень много: они могли быть накоплены
		// в течение длительного времени и включают в себя начисление зарплаты, различные рег. операции и пр.
		// Поэтому если количество документов больше двух, предоставим пользователю возможность самому их подобрать на форме
		// подбора документов затрат (гиперссылка в поле "Документы затрат"). При этом если по ранее инвентаризированным объектам были
		// отражены новые затраты, добавляем их в документ.
		
		Если Не ВыборкаНМА.ЭтоДокументыПрошлойИнвентаризации И Выборка.Количество() > 2 Тогда
			Продолжить;
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ДанныеЗаполненияДокументыЗатрат.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаРеквизиты(НомераТаблиц)
	
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПроверятьЭлементыАмортизации КАК ПроверятьЭлементыАмортизации,
	|	Реквизиты.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ИнвентаризацияНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСрокиИспользованияСтоимостьОбъектовПрава(НомераТаблиц)
	НомераТаблиц.Вставить("СрокиИспользованияСтоимость", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбъектыУчета.ОбъектУчета КАК ОбъектУчета,
	|	ВЫБОР
	|		КОГДА НЕ ОбъектыУчета.НаличиеФактическое
	|			ТОГДА 0
	|		ИНАЧЕ ОбъектыУчета.СрокПолезногоИспользованияПересмотрено
	|	КОНЕЦ КАК СрокПолезногоИспользованияПоРезультатамИнвентаризации,
	|	НематериальныеАктивы.НачалоДействияПрава КАК НачалоДействияПрава,
	|	НематериальныеАктивы.СрокДействияПраваКоличествоМесяцев КАК СрокДействияПраваКоличествоМесяцев,
	|	НематериальныеАктивы.СрокДействияПраваДата КАК СрокДействияПраваДата,
	|	ОбъектыУчета.Пересмотрено КАК Пересмотрено,
	|	ОбъектыУчета.НаличиеФактическое КАК НаличиеФактическое,
	|	ОбъектыУчета.Стоимость КАК Стоимость
	|ИЗ
	|	Документ.ИнвентаризацияНМА.ОбъектыУчета КАК ОбъектыУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ПО ОбъектыУчета.ОбъектУчета = НематериальныеАктивы.Ссылка
	|ГДЕ
	|	ОбъектыУчета.Ссылка = &Ссылка
	|	И ОбъектыУчета.ВидОбъекта <> ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА)";
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

Функция ТекстЗапросаЭлементыАмортизацииНМА(НомераТаблиц)
	НомераТаблиц.Вставить("ИзменениеЭлементовАмортизации", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЭлементыАмортизации.ОбъектУчета КАК НематериальныйАктив,
	|	НЕ ЭлементыАмортизации.НеопределенныйСрокПолезногоИспользованияПересмотрено КАК НачислятьАмортизацию,
	|	ВЫБОР
	|		КОГДА НЕ ЭлементыАмортизации.НаличиеФактическое
	|			ТОГДА 0
	|		ИНАЧЕ ЭлементыАмортизации.СрокПолезногоИспользованияПересмотрено
	|	КОНЕЦ КАК ОстатокСрокаПолезногоИспользования,
	|	ЭлементыАмортизации.СпособНачисленияАмортизацииПересмотрено КАК СпособНачисленияАмортизации,
	|	ВЫБОР
	|		КОГДА ЭлементыАмортизации.СпособНачисленияАмортизацииПересмотрено = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЭлементыАмортизации.ОбъемПродукцииРаботПересмотрено
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОстатокОбъемаПродукцииРабот,
	|	ВЫБОР
	|		КОГДА ЭлементыАмортизации.СпособНачисленияАмортизацииПересмотрено = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка)
	|			ТОГДА ЭлементыАмортизации.КоэффициентПересмотрено
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент,
	|	ЭлементыАмортизации.ЛиквидационнаяСтоимостьПересмотрено КАК ЛиквидационнаяСтоимость
	|ИЗ
	|	Документ.ИнвентаризацияНМА.ОбъектыУчета КАК ЭлементыАмортизации
	|ГДЕ
	|	ЭлементыАмортизации.Ссылка = &Ссылка
	|	И ЭлементыАмортизации.ВидОбъекта = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА)
	|	И ЭлементыАмортизации.Пересмотрено
	|	И ЭлементыАмортизации.НаличиеФактическое
	|	И ЭлементыАмортизации.НаличиеПоДаннымУчета
	|	И &ПроверятьЭлементыАмортизации";
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

Функция ПечатьИНВ18(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияНМА_ИНВ18";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИнвентаризацияНМА.ПФ_MXL_ИНВ18_НМА");
	
	// Получаем области макета для вывода в табличный документ
	Шапка                     = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Шапка");
	СекцияМОЛ                 = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "СекцияМОЛ");
	СтрокаНадТаблицей         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "СтрокаНадТаблицей");
	ЗаголовокТаблицы          = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ЗаголовокТаблицы");
	СтрокаТаблицы             = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "СтрокаТаблицы");
	ПодвалТаблицы             = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ПодвалТаблицы");
	ПодписьГлавногоБухгалтера = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ПодписьГлавногоБухгалтера");
	ШапкаПодписейМОЛ          = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ШапкаПодписейМОЛ");
	Подпись                   = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Подпись");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИнвентаризацияНМА.Ссылка КАК Ссылка,
	|	ИнвентаризацияНМА.Дата КАК Дата,
	|	ИнвентаризацияНМА.Номер КАК Номер,
	|	ИнвентаризацияНМА.Организация КАК Организация,
	|	ИнвентаризацияНМА.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	ИнвентаризацияНМА.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ИнвентаризацияНМА.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	ИнвентаризацияНМА.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	ИнвентаризацияНМА.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	ИнвентаризацияНМА.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	НематериальныеАктивы.НомерСтроки КАК НомерСтроки,
	|	НематериальныеАктивы.ОбъектУчета КАК НематериальныйАктив,
	|	ВЫБОР
	|		КОГДА НематериальныеАктивы.ОбъектУчета.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА НематериальныеАктивы.ОбъектУчета.Наименование
	|		ИНАЧЕ НематериальныеАктивы.ОбъектУчета.НаименованиеПолное
	|	КОНЕЦ КАК НематериальныйАктивНаименованиеПолное,
	|	НематериальныеАктивы.ОбъектУчета.НачалоДействияПрава КАК ДатаВыпуска,
	|	ВЫБОР
	|		КОГДА НематериальныеАктивы.НаличиеПоДаннымУчета
	|			ТОГДА НематериальныеАктивы.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьПоДаннымУчета,
	|	ВЫБОР
	|		КОГДА НематериальныеАктивы.НаличиеФактическое
	|			ТОГДА НематериальныеАктивы.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьФактическая,
	|	ВЫБОР
	|		КОГДА НематериальныеАктивы.НаличиеПоДаннымУчета
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НаличиеПоДаннымУчета,
	|	ВЫБОР
	|		КОГДА НематериальныеАктивы.НаличиеФактическое
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НаличиеФактическое,
	|	НематериальныеАктивы.ОбъектУчета.Код КАК ИнвентарныйНомер
	|ИЗ
	|	Документ.ИнвентаризацияНМА КАК ИнвентаризацияНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНМА.ОбъектыУчета КАК НематериальныеАктивы
	|		ПО ИнвентаризацияНМА.Ссылка = НематериальныеАктивы.Ссылка
	|ГДЕ
	|	ИнвентаризацияНМА.Ссылка В(&МассивОбъектов)
	|	И НематериальныеАктивы.ВидОбъекта = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА)
	|	И НематериальныеАктивы.НаличиеПоДаннымУчета <> НематериальныеАктивы.НаличиеФактическое
	|	И НематериальныеАктивы.Стоимость <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НематериальныеАктивы.НомерСтроки";

	Док = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	ОбработанныхОбъектов = Ложь;

	Пока Док.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		ОбработанныхОбъектов = Истина;
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выведем шапку документа
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
		
		Если Шапка <> Неопределено Тогда
			Шапка.Параметры.Заполнить(Док);
		КонецЕсли;
		
		ПараметрыПечатнойФормы = Новый Структура;
		ПараметрыПечатнойФормы.Вставить("Организация", ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации));
		ПараметрыПечатнойФормы.Вставить("ОрганизацияКодПоОКПО", СведенияОбОрганизации.КодПоОКПО);
		
		ПараметрыПечатнойФормы.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.Номер, Истина, Ложь));
		ПараметрыПечатнойФормы.Вставить("ДатаДокумента", Док.Дата);
		ПараметрыПечатнойФормы.Вставить("ДатаОкончанияИнвентаризацииЛокальныйФормат", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Нстр("ru = 'По состоянию на %1 проведена инвентаризация фактического наличия ценностей, находящихся на ответственном хранении'"),
			Формат(Док.ДатаОкончанияИнвентаризации, "ДЛФ=ДД")));
			
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Шапка, ПараметрыПечатнойФормы);
		
		// Выведем подпись ответственного лица
		СтрокаМОЛ = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, Док.ОтветственноеЛицо, Док.Дата);
	
		ПараметрыПечатнойФормы.Вставить("Должность", СтрокаМОЛ.Должность);
		ПараметрыПечатнойФормы.Вставить("ФИО", СтрокаМОЛ.Представление);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СекцияМОЛ, ПараметрыПечатнойФормы, Истина);
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 2-х
		// подписей, как в форме, утвержденной Госкомстатом)
		ПараметрыПечатнойФормы.Должность = "";
		ПараметрыПечатнойФормы.ФИО = "";
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СекцияМОЛ, ПараметрыПечатнойФормы, Истина);
		
		// Проверим, помещаются ли строка над таблицей, заголовок и первая строка.
		Если СтрокаНадТаблицей <> Неопределено И ЗаголовокТаблицы <> Неопределено И СтрокаТаблицы <> Неопределено Тогда
			ШапкаТаблицы = Новый Массив;
			ШапкаТаблицы.Добавить(СтрокаНадТаблицей);
			ШапкаТаблицы.Добавить(ЗаголовокТаблицы);
			ШапкаТаблицы.Добавить(СтрокаТаблицы);
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ШапкаТаблицы) Тогда
					
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();

			КонецЕсли;
		КонецЕсли;
		
		// Выведем строку над таблицей
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаНадТаблицей, Новый Структура);
		
		// Выведем заголовок таблицы
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы, Новый Структура);
		
		// Создадим переменные для накопления итогов по каждой странице
		ИтогиТаблицы = Новый Структура;
		ИтогиТаблицы.Вставить("ИтогИзлишекСтоимость", 0);
		ИтогиТаблицы.Вставить("ИтогНедостачаСтоимость", 0);
		
		// Выведем строки таблицы
		Пока Док.Следующий() Цикл
			
			СтрокаСПодвалом = Новый Массив;
			
			Если СтрокаТаблицы <> Неопределено И ПодвалТаблицы <> Неопределено Тогда
				СтрокаТаблицы.Параметры.Заполнить(Док);
				СтрокаСПодвалом.Добавить(СтрокаТаблицы);
				СтрокаСПодвалом.Добавить(ПодвалТаблицы);
			КонецЕсли;
			Если Док.НомерСтроки = Док.Количество() И ПодписьГлавногоБухгалтера <> Неопределено Тогда  // если последняя строка, должна
				СтрокаСПодвалом.Добавить(ПодписьГлавногоБухгалтера); // помещаться и подпись гл.бухгалтера
			КонецЕсли;
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом) Тогда
				
				// Выведем подвал таблицы
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПодвалТаблицы, ИтогиТаблицы);
					
				// Обнулим переменные для накопления итогов по каждой странице
				ИтогиТаблицы.ИтогИзлишекСтоимость    = 0;
				ИтогиТаблицы.ИтогНедостачаСтоимость  = 0;
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				// Выведем заголовок таблицы
				Если ЗаголовокТаблицы <> Неопределено Тогда
					ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
				КонецЕсли;
				
			КонецЕсли;
			
			РазницаПоСтоимости = Док.СтоимостьФактическая - Док.СтоимостьПоДаннымУчета;
			
			РасхожденияПоСтроке = Новый Структура;
			РасхожденияПоСтроке.Вставить("ИзлишекСтоимость", ?(РазницаПоСтоимости > 0, РазницаПоСтоимости, 0));
			РасхожденияПоСтроке.Вставить("НедостачаСтоимость", ?(РазницаПоСтоимости < 0, - РазницаПоСтоимости, 0));
			
			ИтогиТаблицы.ИтогИзлишекСтоимость    = ИтогиТаблицы.ИтогИзлишекСтоимость + РасхожденияПоСтроке.ИзлишекСтоимость;
			ИтогиТаблицы.ИтогНедостачаСтоимость  = ИтогиТаблицы.ИтогНедостачаСтоимость + РасхожденияПоСтроке.НедостачаСтоимость;
			
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, РасхожденияПоСтроке);
			
		КонецЦикла;
		
		// Выведем подвал таблицы
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПодвалТаблицы, ИтогиТаблицы);
			
		// Выведем подпись бухгалтера
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Док.Организация, Док.Дата);
		
		Если ПодписьГлавногоБухгалтера <> Неопределено Тогда
			ПодписьГлавногоБухгалтера.Параметры.РасшифровкаПодписи = Руководители.ГлавныйБухгалтерПредставление;
			
			ТабличныйДокумент.Вывести(ПодписьГлавногоБухгалтера);
		КонецЕсли;
		
		// Проверим, помещаются ли шапка подписей и одна подпись
		Если ШапкаПодписейМОЛ <> Неопределено И Подпись <> Неопределено Тогда
			
			Подписи = Новый Массив;
			Подписи.Добавить(ШапкаПодписейМОЛ);
			Подписи.Добавить(Подпись);
			
			Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подписи) Тогда
					
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();

			КонецЕсли;
			
		КонецЕсли;
		
		// Выведем шапку подписей МОЛ
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ШапкаПодписейМОЛ, Новый Структура);
		
		// Выведем подписи МОЛов
		ЗаголовокРазделаПодписей =
			НСтр("ru = 'Лицо, ответственное за сохранность документов, удостоверяющих право организации на нематериальные активы'");
		ВыводитьЗаголовок = Истина;
		
		// Сначала выведем МОЛов из выборки
		Если Подпись <> Неопределено И Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подпись) Тогда
				
			// Выведем разрыв страницы
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
		
		КонецЕсли;
		
		ПараметрыПечатнойФормы.Вставить("ЗаголовокРазделаПодписей", ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей, ""));
		ПараметрыПечатнойФормы.Вставить("Должность", СтрокаМОЛ.Должность);
		ПараметрыПечатнойФормы.Вставить("РасшифровкаПодписи", СтрокаМОЛ.Представление);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПечатнойФормы);
		
		ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 2-х
		// подписей, как в форме, утвержденной Госкомстатом)
		Если Подпись <> Неопределено И Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подпись) Тогда
				
			// Выведем разрыв страницы
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
			
		КонецЕсли;
			
		ПараметрыПечатнойФормы.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей, "");
		ПараметрыПечатнойФормы.Должность                = "";
		ПараметрыПечатнойФормы.РасшифровкаПодписи       = "";
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПечатнойФормы);
		
		ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
	КонецЦикла;
	
	Если Не ОбработанныхОбъектов Тогда 
		Шаблон = МассивОбъектов[0];

		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Шапка, Новый Структура);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СекцияМОЛ, Новый Структура);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаНадТаблицей, Новый Структура);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы, Новый Структура);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПодвалТаблицы, Новый Структура);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПодписьГлавногоБухгалтера, Новый Структура);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ШапкаПодписейМОЛ, Новый Структура);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, Новый Структура);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, ТабличныйДокумент.ВысотаТаблицы + 1, ОбъектыПечати, Шаблон);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьИНВ1а(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru = 'Председатель комиссии'"));
	ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru = 'Члены комиссии:'"));
	ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",
		НСтр("ru = 'Лицо(а), ответственное(ые) за сохранность документов, удостоверяющих право организации на нематериальные активы:'"));
	ЗаголовокРазделаПодписей.Вставить("Проверяющий",
		НСтр("ru = 'Указанные в настоящей описи данные и расчеты проверил'"));
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы   = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияНМА_ИНВ1а";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИнвентаризацияНМА.ПФ_MXL_ИНВ1а");
	
	// Получаем области макета для вывода в табличный документ
	Шапка            = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Шапка");
	Расписка         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Расписка");
	Подпись          = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Подпись");
	ПолеДаты         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ПолеДаты");
	ЗаголовокТаблицы = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ЗаголовокТаблицы");
	ИтогоПоСтранице  = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ИтогоПоСтранице");
	ИтогоПоОписи     = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ИтогоПоОписи");
	Итоги            = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Итоги");
	Подсчеты         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Подсчеты");
	Претензии        = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Претензии");
	СтрокаТаблицы    = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "СтрокаТаблицы");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДокументОснованиеДата = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Реквизиты.Дата
	|		ИНАЧЕ Реквизиты.ДокументОснованиеДата
	|	КОНЕЦ КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ИнвентаризацияНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	ОбъектыУчета.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(ВЫБОР
	|			КОГДА ОбъектыУчета.НаличиеПоДаннымУчета
	|				ТОГДА ОбъектыУчета.Стоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СтоимостьПоДаннымУчета,
	|	СУММА(ВЫБОР
	|			КОГДА ОбъектыУчета.НаличиеФактическое
	|				ТОГДА ОбъектыУчета.Стоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СтоимостьФактическая,
	|	МАКСИМУМ(ОбъектыУчета.НомерСтроки) КАК НомерСтроки,
	|	ОбъектыУчета.ОбъектУчета.ДокументРегистрацииНаименование КАК ДокументРегистрацииНаименование,
	|	ОбъектыУчета.ОбъектУчета.ДокументРегистрацииДата КАК ДокументРегистрацииДата,
	|	ОбъектыУчета.ОбъектУчета.ДокументРегистрацииНомер КАК ДокументРегистрацииНомер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	Реквизиты.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо
	|ПОМЕСТИТЬ НМА
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНМА.ОбъектыУчета КАК ОбъектыУчета
	|		ПО Реквизиты.Ссылка = ОбъектыУчета.Ссылка
	|			И (ОбъектыУчета.ВидОбъекта = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.НМА))
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыУчета.ОбъектУчета,
	|	ОбъектыУчета.ОбъектУчета.ДокументРегистрацииНаименование,
	|	ОбъектыУчета.ОбъектУчета.ДокументРегистрацииДата,
	|	ОбъектыУчета.ОбъектУчета.ДокументРегистрацииНомер,
	|	Реквизиты.Дата,
	|	Реквизиты.Организация,
	|	Реквизиты.Ссылка,
	|	Реквизиты.ДатаНачалаИнвентаризации,
	|	Реквизиты.ДатаОкончанияИнвентаризации,
	|	Реквизиты.ДокументОснованиеДата,
	|	Реквизиты.ДокументОснованиеНомер,
	|	Реквизиты.ПричинаПроведенияИнвентаризации,
	|	Реквизиты.ДокументОснованиеВид,
	|	Реквизиты.Номер,
	|	Реквизиты.ОтветственноеЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОбъектУчета,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НМА.ОбъектУчета КАК ОбъектУчета,
	|	НМА.Ссылка КАК Ссылка,
	|	НМА.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(СостоянияНМАОрганизаций.Период, ДЕНЬ)), ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПостановкиНаУчет
	|ПОМЕСТИТЬ ДатыНачалаИспользования
	|ИЗ
	|	НМА КАК НМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|		ПО НМА.Организация = СостоянияНМАОрганизаций.Организация
	|			И НМА.Дата >= СостоянияНМАОрганизаций.Период
	|			И НМА.ОбъектУчета = СостоянияНМАОрганизаций.НематериальныйАктив
	|			И (СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету))
	|
	|СГРУППИРОВАТЬ ПО
	|	НМА.ОбъектУчета,
	|	НМА.Ссылка,
	|	НМА.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки,
	|	Ссылка,
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НМА.Ссылка КАК Ссылка,
	|	НМА.Дата КАК Дата,
	|	НМА.Номер КАК Номер,
	|	НМА.Организация КАК Организация,
	|	НМА.ОбъектУчета КАК ОбъектУчета,
	|	НМА.СтоимостьФактическая КАК СтоимостьФактическая,
	|	НМА.СтоимостьПоДаннымУчета КАК СтоимостьПоДаннымУчета,
	|	НМА.НомерСтроки КАК НомерСтроки,
	|	НМА.ДокументРегистрацииНаименование КАК ДокументРегистрацииНаименование,
	|	НМА.ДокументРегистрацииДата КАК ДокументРегистрацииДата,
	|	НМА.ДокументРегистрацииНомер КАК ДокументРегистрацииНомер,
	|	ДатыНачалаИспользования.ДатаПостановкиНаУчет КАК ДатаПостановкиНаУчет,
	|	ВЫБОР
	|		КОГДА НМА.ОбъектУчета.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА НМА.ОбъектУчета.Наименование
	|		ИНАЧЕ НМА.ОбъектУчета.НаименованиеПолное
	|	КОНЕЦ КАК НематериальныйАктивНаименованиеПолное,
	|	НМА.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	НМА.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	НМА.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	НМА.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	НМА.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	НМА.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	НМА.ОтветственноеЛицо КАК ОтветственноеЛицо
	|ИЗ
	|	НМА КАК НМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыНачалаИспользования КАК ДатыНачалаИспользования
	|		ПО НМА.НомерСтроки = ДатыНачалаИспользования.НомерСтроки
	|			И НМА.Ссылка = ДатыНачалаИспользования.Ссылка
	|			И НМА.ОбъектУчета = ДатыНачалаИспользования.ОбъектУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.Председатель, ИСТИНА) КАК Председатель,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.НомерСтроки, 1) КАК НомерСтроки,
	|	ЕСТЬNULL(ИнвентаризационнаяКомиссия.ФизЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ФизЛицо
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНМА.ИнвентаризационнаяКомиссия КАК ИнвентаризационнаяКомиссия
	|		ПО Реквизиты.Ссылка = ИнвентаризационнаяКомиссия.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	Председатель УБЫВ,
	|	НомерСтроки";
			
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИндексПоследнегоРезультата = РезультатЗапроса.Количество() - 1;
	
	Док = РезультатЗапроса[ИндексПоследнегоРезультата - 1].Выбрать();
	ИнвентаризационнаяКомиссия = РезультатЗапроса[ИндексПоследнегоРезультата].Выбрать();
	
	ПервыйДокумент = Истина;
	
	КоллекцияИтоговСтраницы = НовыйИтогиПоказателей();

	Пока Док.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выведем шапку документа
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
		
		Если Шапка <> Неопределено Тогда
			Шапка.Параметры.Заполнить(Док);
		КонецЕсли;
	
		ПараметрыПечатнойФормы = Новый Структура;
		ПараметрыПечатнойФормы.Вставить("Организация",          ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации));
		ПараметрыПечатнойФормы.Вставить("ОрганизацияКодПоОКПО", СведенияОбОрганизации.КодПоОКПО);

		ПараметрыПечатнойФормы.Вставить("НомерДокумента",       ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.Номер, Истина, Ложь));
		ПараметрыПечатнойФормы.Вставить("ДатаДокумента",        Док.Дата);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Шапка, ПараметрыПечатнойФормы);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Расписка, ПараметрыПечатнойФормы);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
		ДанныеМОЛ = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, Док.ОтветственноеЛицо, Док.Дата);
		
		ПараметрыПодписиОтветственноеЛицо = НовыйПараметрыПодписи();
		
		ПараметрыПодписиОтветственноеЛицо.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ОтветственноеЛицо;
		ПараметрыПодписиОтветственноеЛицо.Должность = ДанныеМОЛ.Должность;
		ПараметрыПодписиОтветственноеЛицо.РасшифровкаПодписи = ДанныеМОЛ.Представление;
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиОтветственноеЛицо);
		
		ПараметрыПодписи = НовыйПараметрыПодписи();
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Вторая и последующие страницы
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы);
		
		// В конце каждой странице выводятся итоги.
		// Прежде чем выводить строку таблицы, будем проверять, остается ли место для итогов.
		ОбластьПроверкиВывода = Новый Массив;
		Если СтрокаТаблицы <> Неопределено И ИтогоПоСтранице <> Неопределено И Итоги <> Неопределено Тогда
			ОбластьПроверкиВывода.Добавить(СтрокаТаблицы);
			ОбластьПроверкиВывода.Добавить(ИтогоПоСтранице);
			ОбластьПроверкиВывода.Добавить(Итоги);
		КонецЕсли;
		
		НомерСтроки = 0;
		КоллекцияИтоговСтраницы = НовыйИтогиПоказателей();
		КоллекцияИтоговДокумента = НовыйИтогиПоказателей();
		Пока Док.Следующий() Цикл
			НомерСтроки = НомерСтроки + 1;
			Если СтрокаТаблицы <> Неопределено Тогда
				СтрокаТаблицы.Параметры.Заполнить(Новый Структура("НомерСтроки", НомерСтроки));
			КонецЕсли;

			Если ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ОбластьПроверкиВывода) Тогда
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, Док);
			Иначе
				ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета);
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ИтогоПоСтранице, КоллекцияИтоговСтраницы);
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Итоги, КоллекцияИтоговСтраницы);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ЗаголовокТаблицы);
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицы, Док);
			КонецЕсли;
			
			КоллекцияИтоговСтраницы.СтоимостьФактическая = КоллекцияИтоговСтраницы.СтоимостьФактическая + Док.СтоимостьФактическая;
			КоллекцияИтоговСтраницы.СтоимостьПоДаннымУчета = КоллекцияИтоговСтраницы.СтоимостьПоДаннымУчета + Док.СтоимостьПоДаннымУчета;
			КоллекцияИтоговСтраницы.КоличествоСтрок = КоллекцияИтоговСтраницы.КоличествоСтрок + 1;
			
			КоллекцияИтоговДокумента.СтоимостьФактическая = КоллекцияИтоговДокумента.СтоимостьФактическая + Док.СтоимостьФактическая;
			КоллекцияИтоговДокумента.КоличествоСтрок = КоллекцияИтоговДокумента.КоличествоСтрок + 1;
			
		КонецЦикла;
		
		ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ИтогоПоСтранице, КоллекцияИтоговСтраницы);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Итоги, КоллекцияИтоговСтраницы);
				
		// Последняя страница
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ИтогоПоОписи);
		ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговДокумента, ВалютаРегламентированногоУчета);
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Итоги, КоллекцияИтоговДокумента);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подсчеты);
		
		ПараметрыПодписиПредседатель = НовыйПараметрыПодписи();
		ПараметрыПодписиПредседатель.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
		КоличествоЧленовКомиссииУнифицированнойФормы = 3;
		Если ИнвентаризационнаяКомиссия.СледующийПоЗначениюПоля("Ссылка") Тогда
			ИнвентаризационнаяКомиссия.Следующий();
			ДанныеПредседателя = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);

			ПараметрыПодписиПредседатель.Должность = ДанныеПредседателя.Должность;
			ПараметрыПодписиПредседатель.РасшифровкаПодписи = ДанныеПредседателя.Представление;
			
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПредседатель);
			
			Для НомерЧленаКомиссии = 1 По КоличествоЧленовКомиссииУнифицированнойФормы Цикл
				ПараметрыПодписи = НовыйПараметрыПодписи();
				Если НомерЧленаКомиссии = 1 Тогда
					ПараметрыПодписи.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ЧленыКомиссии;
				КонецЕсли;
				Если ИнвентаризационнаяКомиссия.Следующий() Тогда
					ДанныеЧленаКомиссии = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
					ПараметрыПодписи.Должность = ДанныеЧленаКомиссии.Должность;
					ПараметрыПодписи.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
				КонецЕсли;

				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			КонецЦикла;
			
			Пока ИнвентаризационнаяКомиссия.Следующий() Цикл
				ДанныеЧленаКомиссии = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, ИнвентаризационнаяКомиссия.Дата);
				ПараметрыПодписи.Должность = ДанныеЧленаКомиссии.Должность;
				ПараметрыПодписи.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			КонецЦикла;
				
		Иначе
			
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПредседатель);
			
			ПараметрыПодписи = НовыйПараметрыПодписи();
			ПараметрыПодписи.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ЧленыКомиссии;
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			Для НомерЧленаКомиссии = 2 По КоличествоЧленовКомиссииУнифицированнойФормы Цикл
				ПараметрыПодписи = НовыйПараметрыПодписи();
				ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
			КонецЦикла;
		КонецЕсли;
		
		ПараметрыПретензии = Новый Структура("НомерСтрокиНачальный, НомерСтрокиКонечный");
		Если НомерСтроки > 0 Тогда
			ПараметрыПретензии.НомерСтрокиНачальный = 1;
			ПараметрыПретензии.НомерСтрокиКонечный  = НомерСтроки;
		Иначе
			ПараметрыПретензии.НомерСтрокиНачальный = "______";
			ПараметрыПретензии.НомерСтрокиКонечный  = "______";
		КонецЕсли;
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Претензии, ПараметрыПретензии);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиОтветственноеЛицо);
		
		ПараметрыПодписи = НовыйПараметрыПодписи();
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписи);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПолеДаты, Док);
		
		ПараметрыПодписиПроверяющий = НовыйПараметрыПодписи();
		ПараметрыПодписиПроверяющий.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.Проверяющий;
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, Подпись, ПараметрыПодписиПроверяющий);
		
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПолеДаты, Док);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

Функция ПечатьВедомостьПрав(МассивОбъектов, ОбъектыПечати)

	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru = 'Председатель комиссии'"));
	ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru = 'Члены комиссии:'"));
	ЗаголовокРазделаПодписей.Вставить("ОтветственноеЛицо",    НСтр("ru = 'Ответственное лицо'"));
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы      = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияНМА_ВедомостьПрав";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИнвентаризацияНМА.ВедомостьПрав");
	
	// Получаем области макета для вывода в табличный документ
	ШапкаДокумента               = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ШапкаДокумента");
	ШапкаРаздела                 = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ШапкаРаздела");
	ШапкаТаблицыОсновная         = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ШапкаТаблицыОсновная");
	ШапкаТаблицыДополнительная   = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ШапкаТаблицыДополнительная");
	СтрокаТаблицыОсновная        = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "СтрокаТаблицыОсновная");
	СтрокаТаблицыДополнительная  = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "СтрокаТаблицыДополнительная");
	ПодвалТаблицы                = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "ПодвалТаблицы");
	РазделПодписей               = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "РазделПодписей");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	Реквизиты.ПроверятьДокументыЗатрат КАК ПроверятьДокументыЗатрат
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ИнвентаризацияНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыУчета.Ссылка КАК Ссылка,
	|	ОбъектыУчета.ВидОбъекта КАК РазделУчета,
	|	ОбъектыУчета.ОбъектУчета КАК ОбъектУчета,
	|	ОбъектыУчета.ОбъектУчета.Код КАК ИнвНомер,
	|	ВЫБОР
	|		КОГДА ОбъектыУчета.НаличиеФактическое
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НаличиеПоФакту,
	|	ВЫБОР
	|		КОГДА ОбъектыУчета.НаличиеПоДаннымУчета
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НаличиеПоУчету,
	|	ОбъектыУчета.ОбъектУчета.НачалоДействияПрава КАК НачалоДействияПрава,
	|	ОбъектыУчета.ОбъектУчета.СрокДействияПраваДата КАК СрокДействияПраваДата,
	|	ОбъектыУчета.ОбъектУчета.СрокДействияПраваКоличествоМесяцев КАК СрокДействияПраваКоличествоМесяцев,
	|	ВЫБОР
	|		КОГДА ОбъектыУчета.НаличиеФактическое
	|			ТОГДА РезультатыИнтеллектуальнойДеятельности.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|	РезультатыИнтеллектуальнойДеятельности.ДатаОкончанияИспользования КАК ДатаОкончанияИспользования,
	|	ОбъектыУчета.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ОбъектыУчета
	|ИЗ
	|	Документ.ИнвентаризацияНМА.ОбъектыУчета КАК ОбъектыУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РезультатыИнтеллектуальнойДеятельности КАК РезультатыИнтеллектуальнойДеятельности
	|		ПО ОбъектыУчета.ОбъектУчета = РезультатыИнтеллектуальнойДеятельности.ОбъектУчета
	|			И ОбъектыУчета.Ссылка = РезультатыИнтеллектуальнойДеятельности.Регистратор
	|ГДЕ
	|	ОбъектыУчета.Ссылка В(&МассивОбъектов)
	|	И ОбъектыУчета.ВидОбъекта В (ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.МалоценныйНМА), ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовИнвентаризацииНМА.ПрочийОбъектПрава))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	ОбъектыУчета.РазделУчета КАК РазделУчета,
	|	ОбъектыУчета.ОбъектУчета КАК ОбъектУчета,
	|	ОбъектыУчета.ИнвНомер КАК ИнвНомер,
	|	ОбъектыУчета.НаличиеПоФакту КАК НаличиеПоФакту,
	|	ОбъектыУчета.НаличиеПоУчету КАК НаличиеПоУчету,
	|	ОбъектыУчета.НачалоДействияПрава КАК НачалоДействияПрава,
	|	ОбъектыУчета.СрокДействияПраваДата КАК СрокДействияПраваДата,
	|	ОбъектыУчета.СрокДействияПраваКоличествоМесяцев КАК СрокДействияПраваКоличествоМесяцев,
	|	ОбъектыУчета.Стоимость КАК Стоимость,
	|	ОбъектыУчета.ДатаОкончанияИспользования КАК ДатаОкончанияИспользования,
	|	ДокументыЗатрат.ДокументЗатрат КАК ДокументЗатрат,
	|	СУММА(ДокументыЗатрат.СуммаЗатратПоДокументу) КАК СуммаЗатратПоДокументу,
	|	Реквизиты.ПроверятьДокументыЗатрат КАК ПроверятьДокументыЗатрат,
	|	ОбъектыУчета.НомерСтроки КАК НомерСтрокиОбъектУчета,
	|	МИНИМУМ(ДокументыЗатрат.НомерСтроки) КАК НомерСтрокиДокументЗатрат
	|ПОМЕСТИТЬ ДокументыЗатрат
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыУчета КАК ОбъектыУчета
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНМА.ДокументыЗатрат КАК ДокументыЗатрат
	|			ПО ОбъектыУчета.Ссылка = ДокументыЗатрат.Ссылка
	|				И ОбъектыУчета.ОбъектУчета = ДокументыЗатрат.ОбъектУчета
	|		ПО Реквизиты.Ссылка = ОбъектыУчета.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.Ссылка,
	|	Реквизиты.Дата,
	|	Реквизиты.Организация,
	|	Реквизиты.ОтветственноеЛицо,
	|	ОбъектыУчета.РазделУчета,
	|	ОбъектыУчета.ОбъектУчета,
	|	ОбъектыУчета.ИнвНомер,
	|	ОбъектыУчета.НаличиеПоФакту,
	|	ОбъектыУчета.НаличиеПоУчету,
	|	ОбъектыУчета.НачалоДействияПрава,
	|	ОбъектыУчета.СрокДействияПраваДата,
	|	ОбъектыУчета.СрокДействияПраваКоличествоМесяцев,
	|	ОбъектыУчета.Стоимость,
	|	ОбъектыУчета.ДатаОкончанияИспользования,
	|	Реквизиты.ПроверятьДокументыЗатрат,
	|	ОбъектыУчета.НомерСтроки,
	|	ДокументыЗатрат.ДокументЗатрат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДокументЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыЗатрат.Ссылка КАК Ссылка,
	|	ДокументыЗатрат.Дата КАК ДатаИнвентаризации,
	|	ДокументыЗатрат.Организация КАК Организация,
	|	ДокументыЗатрат.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	ДокументыЗатрат.РазделУчета КАК РазделУчета,
	|	ДокументыЗатрат.ОбъектУчета КАК ОбъектУчета,
	|	ДокументыЗатрат.ИнвНомер КАК ИнвНомер,
	|	ДокументыЗатрат.НаличиеПоФакту КАК НаличиеПоФакту,
	|	ДокументыЗатрат.НаличиеПоУчету КАК НаличиеПоУчету,
	|	ДокументыЗатрат.НачалоДействияПрава КАК НачалоДействияПрава,
	|	ДокументыЗатрат.СрокДействияПраваДата КАК СрокДействияПраваДата,
	|	ДокументыЗатрат.СрокДействияПраваКоличествоМесяцев КАК СрокДействияПраваКоличествоМесяцев,
	|	ДокументыЗатрат.Стоимость КАК Стоимость,
	|	ДокументыЗатрат.ДатаОкончанияИспользования КАК ДатаОкончанияИспользования,
	|	ДокументыЗатрат.ДокументЗатрат КАК ДокументЗатрат,
	|	ДокументыЗатрат.СуммаЗатратПоДокументу КАК СуммаЗатратПоДокументу,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокументаЗатрат,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокументаЗатрат,
	|	ДокументыЗатрат.ПроверятьДокументыЗатрат КАК ПроверятьДокументыЗатрат,
	|	ДокументыЗатрат.НомерСтрокиОбъектУчета КАК НомерСтрокиОбъектУчета,
	|	ДокументыЗатрат.НомерСтрокиДокументЗатрат КАК НомерСтрокиДокументЗатрат,
	|	ТИПЗНАЧЕНИЯ(ДокументыЗатрат.ДокументЗатрат) КАК НаименованиеДокументаЗатрат,
	|	ВЫБОР
	|		КОГДА ДокументыЗатрат.ОбъектУчета.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ДокументыЗатрат.ОбъектУчета.Наименование
	|		ИНАЧЕ ДокументыЗатрат.ОбъектУчета.НаименованиеПолное
	|	КОНЕЦ КАК ОбъектУчетаНаименование
	|ИЗ
	|	ДокументыЗатрат КАК ДокументыЗатрат
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ДокументыЗатрат.Организация = ДанныеПервичныхДокументов.Организация
	|			И ДокументыЗатрат.ДокументЗатрат = ДанныеПервичныхДокументов.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаИнвентаризации,
	|	Ссылка,
	|	РазделУчета,
	|	НомерСтрокиОбъектУчета,
	|	НомерСтрокиДокументЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Комиссия.ФизЛицо КАК ФизЛицо,
	|	Комиссия.Председатель КАК Председатель
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНМА.ИнвентаризационнаяКомиссия КАК Комиссия
	|		ПО Реквизиты.Ссылка = Комиссия.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Реквизиты.Дата,
	|	Реквизиты.Ссылка,
	|	Председатель УБЫВ,
	|	Комиссия.НомерСтроки";
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ПредставлениеВалютыРегУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВалютаРегУчета, "Наименование");
	Если Не ЗначениеЗаполнено(ПредставлениеВалютыРегУчета) Тогда
		ПредставлениеВалютыРегУчета = НСтр("ru = 'руб.'");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ИндексПоследнегоРезультата = РезультатЗапроса.Количество() - 1;
	
	Док = РезультатЗапроса[ИндексПоследнегоРезультата - 1].Выбрать();
	ИнвентаризационнаяКомиссия = РезультатЗапроса[ИндексПоследнегоРезультата].Выбрать();
		
	ОбластьПроверки = Новый Массив;
	Если СтрокаТаблицыОсновная <> Неопределено И ПодвалТаблицы <> Неопределено Тогда
		ОбластьПроверки.Добавить(СтрокаТаблицыОсновная);
		ОбластьПроверки.Добавить(ПодвалТаблицы);
	КонецЕсли;
	
	Пока Док.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		ИнвентаризационнаяКомиссия.СледующийПоЗначениюПоля("Ссылка");
		
		// Если в табличной части нет данных для вывода, то ничего и не выводим.
		Если Не ЗначениеЗаполнено(Док.РазделУчета) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выведем шапку документа
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.ДатаИнвентаризации);
	
		ПараметрыПечатнойФормы = Новый Структура;
		ПараметрыПечатнойФормы.Вставить("Организация",          ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации));
		ПараметрыПечатнойФормы.Вставить("Дата", Формат(Док.ДатаИнвентаризации, "ДЛФ=DD;"));
		
		// Часть табличного документа, одинаковая для всех разделов
		ПодписиЧленовКомиссии = ПодписиЧленовКомиссии(РазделПодписей, ИнвентаризационнаяКомиссия, Док, ЗаголовокРазделаПодписей);
		ПервыйДокумент = Истина;
		
		// Каждый раздел - малоценные НМА и прочие права - выводится на отдельном листе.
		Пока Док.СледующийПоЗначениюПоля("РазделУчета") Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;
			
			НомерСтроки = 0;
			ИтогПоФакту = 0;
			ИтогПоУчету = 0;
			ИтогСтоимость = 0;
			
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ШапкаДокумента, ПараметрыПечатнойФормы);
			
			РазделУчета = "";
			Если Док.РазделУчета = Перечисления.ВидыОбъектовИнвентаризацииНМА.МалоценныйНМА Тогда
				НастройкиСущественностиВНА =
					Справочники.НастройкиСущественностиВнеоборотныхАктивов.НастройкиСущественностиВНА(Новый Структура("Организация, Период", Док.Организация, Док.ДатаИнвентаризации));
				Лимит = 0;
				Если НастройкиСущественностиВНА.СпособУказанияЛимита = Перечисления.СпособыУказанияЛимитаСущественностиВНА.ПоСтоимости Тогда
					Лимит = НастройкиСущественностиВНА.ЛимитСтоимости;				
				КонецЕсли;
				
				Если Лимит <> 0 Тогда
					ШаблонТекстаРазделаУчета = НСтр("ru = 'Нематериальные активы со стоимостью ниже установленного лимита %1 %2'");
					РазделУчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаРазделаУчета, Формат(Лимит, "ЧДЦ=0; ЧРГ=;"), ПредставлениеВалютыРегУчета);
				Иначе
					РазделУчета =  НСтр("ru = 'Малоценные нематериальные активы'");
				КонецЕсли;
			Иначе
				РазделУчета = НСтр("ru = 'Прочие права'");
			КонецЕсли;
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ШапкаРаздела, Новый Структура("РазделУчета", РазделУчета));
			ВывестиШапкуТаблицы(ТабличныйДокумент, ШапкаТаблицыОсновная, ШапкаТаблицыДополнительная, ПредставлениеВалютыРегУчета, Док);
			
			Пока Док.СледующийПоЗначениюПоля("ОбъектУчета") Цикл
				
				ИтогПоФакту = ИтогПоФакту + Док.НаличиеПоФакту;
				ИтогПоУчету = ИтогПоУчету + Док.НаличиеПоУчету;
				ИтогСтоимость = ИтогСтоимость + Док.Стоимость;
				НомерСтроки = НомерСтроки + 1;
				
				Если Не Док.ПроверятьДокументыЗатрат И Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ОбластьПроверки) Тогда
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ВывестиШапкуТаблицы(ТабличныйДокумент, ШапкаТаблицыОсновная, ШапкаТаблицыДополнительная, ПредставлениеВалютыРегУчета, Док);
				КонецЕсли;
				
				ПараметрыВыводаТаблицаВедомостьПрав = ПараметрыВыводаТаблицаВедомостьПрав();
				ЗаполнитьЗначенияСвойств(ПараметрыВыводаТаблицаВедомостьПрав, Док);
				ПараметрыВыводаТаблицаВедомостьПрав.НомерСтроки = НомерСтроки;
				ПараметрыВыводаТаблицаВедомостьПрав.СрокДействия = Справочники.НематериальныеАктивы.ПредставлениеСрокаДействияПрава(Док);
				
				ОформитьСрокДействияПрава(ПараметрыВыводаТаблицаВедомостьПрав, Док);
				
				Если Не Док.ПроверятьДокументыЗатрат Тогда
					ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, СтрокаТаблицыОсновная, ПараметрыВыводаТаблицаВедомостьПрав);
				Иначе
					НоваяОбластьСтроки = Новый ТабличныйДокумент;
					ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(НоваяОбластьСтроки, СтрокаТаблицыОсновная, ПараметрыВыводаТаблицаВедомостьПрав);
					ОбластьДокументов = Новый ТабличныйДокумент;
					Если Док.Количество() = 0 Тогда
						ОбластьДокументов.Вывести(СтрокаТаблицыДополнительная);
					Иначе
						Пока Док.Следующий() Цикл
							ЗаполнитьЗначенияСвойств(ПараметрыВыводаТаблицаВедомостьПрав, Док);
							ЗаполнитьПараметрыСтрокДокументовЗатрат(ПараметрыВыводаТаблицаВедомостьПрав, Док);
							ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ОбластьДокументов, СтрокаТаблицыДополнительная, ПараметрыВыводаТаблицаВедомостьПрав);
						КонецЦикла;
						НоваяОбластьСтроки.Присоединить(ОбластьДокументов);
						Если Док.Количество() > 1 Тогда
							КоличествоСтолбцовОсновнойТаблицы = СтрокаТаблицыОсновная.ШиринаТаблицы;
							Для НомерСтолбца = 1 По КоличествоСтолбцовОсновнойТаблицы Цикл
								ОбластьДляОбъединения = НоваяОбластьСтроки.Область(1, НомерСтолбца, НоваяОбластьСтроки.ВысотаТаблицы, НомерСтолбца);
								ОбластьДляОбъединения.Объединить();
								ОбластьДляОбъединения.ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
								ОбластьДляОбъединения.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
							КонецЦикла;
						КонецЕсли;
						Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, НоваяОбластьСтроки) Тогда
							ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
							ВывестиШапкуТаблицы(ТабличныйДокумент, ШапкаТаблицыОсновная, ШапкаТаблицыДополнительная, ПредставлениеВалютыРегУчета, Док);
						КонецЕсли;
						ТабличныйДокумент.Вывести(НоваяОбластьСтроки);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Итоги = Новый Структура("ИтогПоФакту, ИтогПоУчету, ИтогСтоимость", ИтогПоФакту, ИтогПоУчету, ИтогСтоимость);
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ПодвалТаблицы, Итоги);
			
			ПараметрыОтветственноеЛицо = НовыйПараметрыПодписи();
			ДанныеОтветственногоЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, Док.ОтветственноеЛицо, Док.ДатаИнвентаризации);
			
			ПараметрыОтветственноеЛицо.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ОтветственноеЛицо;
			ПараметрыОтветственноеЛицо.Должность = ДанныеОтветственногоЛица.Должность;
			ПараметрыОтветственноеЛицо.РасшифровкаПодписи = ДанныеОтветственногоЛица.Представление;
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, РазделПодписей, ПараметрыОтветственноеЛицо);

			ТабличныйДокумент.Вывести(ПодписиЧленовКомиссии);
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

Функция ПодписиЧленовКомиссии(РазделПодписей, ИнвентаризационнаяКомиссия, Док, ЗаголовокРазделаПодписей)

	ПодписиЧленовКомиссии = Новый ТабличныйДокумент();
	ПараметрыПредседатель = НовыйПараметрыПодписи();
	Если ИнвентаризационнаяКомиссия.Следующий() Тогда
		ПараметрыПредседатель.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
		ДанныеПредседателя = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, Док.ДатаИнвентаризации);
		ПараметрыПредседатель.Должность = ДанныеПредседателя.Должность;
		ПараметрыПредседатель.РасшифровкаПодписи = ДанныеПредседателя.Представление;
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ПодписиЧленовКомиссии, РазделПодписей, ПараметрыПредседатель);
	КонецЕсли;
	
	ПараметрыЧленыКомиссии = НовыйПараметрыПодписи();
	ПараметрыЧленыКомиссии.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ЧленыКомиссии;
	НомерЧленаКомиссии = 1;
	Пока ИнвентаризационнаяКомиссия.Следующий() Или НомерЧленаКомиссии <= 3 Цикл
		ДанныеЧленаКомиссии = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Док.Организация, ИнвентаризационнаяКомиссия.ФизЛицо, Док.ДатаИнвентаризации);
		ПараметрыЧленыКомиссии.Должность = ДанныеЧленаКомиссии.Должность;
		ПараметрыЧленыКомиссии.РасшифровкаПодписи = ДанныеЧленаКомиссии.Представление;
		ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ПодписиЧленовКомиссии, РазделПодписей, ПараметрыЧленыКомиссии);
		// Для членов комиссии заголовок раздела подписей выводится только у первого человека.
		ПараметрыЧленыКомиссии.ЗаголовокРазделаПодписей = "";
		НомерЧленаКомиссии = НомерЧленаКомиссии + 1;
	КонецЦикла;
	
	Возврат ПодписиЧленовКомиссии;
	
КонецФункции

Процедура ОформитьСрокДействияПрава(ПараметрыВыводаТаблицаВедомостьПрав, Док)
	Если ЗначениеЗаполнено(Док.ДатаОкончанияИспользования) Тогда
		ОстатокСрокаИспользования = Макс(0, АмортизацияАктивов.РазностьМесяцев(Док.ДатаОкончанияИспользования, Док.ДатаИнвентаризации));
		ПараметрыВыводаТаблицаВедомостьПрав.ОстатокСрокаИспользования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 мес.'"),
			Формат(ОстатокСрокаИспользования, "ЧДЦ=0; ЧН=0;"));
	Иначе
		ПараметрыВыводаТаблицаВедомостьПрав.ОстатокСрокаИспользования = НСтр("ru = 'Не определен'");
	КонецЕсли;
КонецПроцедуры

Процедура ВывестиШапкуТаблицы(ТабличныйДокумент, ШапкаТаблицыОсновная, ШапкаТаблицыДополнительная, ПредставлениеВалютыРегУчета, Док)
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокумент, ШапкаТаблицыОсновная, Новый Структура("ВалютаРегУчета", ПредставлениеВалютыРегУчета));
	Если Док.ПроверятьДокументыЗатрат И ШапкаТаблицыДополнительная <> Неопределено Тогда
		ШапкаТаблицыДополнительная.Параметры.Заполнить(Новый Структура("ВалютаРегУчета", ПредставлениеВалютыРегУчета));
		ТабличныйДокумент.Присоединить(ШапкаТаблицыДополнительная);
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьПараметрыСтрокДокументовЗатрат(ПараметрыВыводаТаблицаВедомостьПрав, Док)

	Если ЗначениеЗаполнено(Док.ДокументЗатрат) И 
		(Не ЗначениеЗаполнено(Док.НомерДокументаЗатрат) Или Не ЗначениеЗаполнено(Док.ДатаДокументаЗатрат)) Тогда
			// В регистре ДанныеПервичныхДокументов нет информации о дате и номере документа, используем системные значения
			НомерДатаДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Док.ДокументЗатрат, "Номер, Дата");
			ПараметрыВыводаТаблицаВедомостьПрав.НомерДокументаЗатрат = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НомерДатаДокумента.Номер, Истина, Ложь);
			ПараметрыВыводаТаблицаВедомостьПрав.ДатаДокументаЗатрат = НомерДатаДокумента.Дата;
	ИначеЕсли Не ЗначениеЗаполнено(Док.ДокументЗатрат) Тогда
		ПараметрыВыводаТаблицаВедомостьПрав.НаименованиеДокументаЗатрат = НСтр("ru = 'Прочие документы'");
		ПараметрыВыводаТаблицаВедомостьПрав.НомерДокументаЗатрат = НСтр("ru = '-'");
	КонецЕсли;
КонецПроцедуры

Функция ПараметрыВыводаТаблицаВедомостьПрав()

	ПараметрыВыводаТаблицаВедомостьПрав = Новый Структура;
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("НомерСтроки");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("ИнвНомер");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("ОбъектУчетаНаименование");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("СрокДействия");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("ОстатокСрокаИспользования");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("НаличиеПоФакту");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("НаличиеПоУчету");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("Стоимость");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("НаименованиеДокументаЗатрат");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("НомерДокументаЗатрат");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("ДатаДокументаЗатрат");
	ПараметрыВыводаТаблицаВедомостьПрав.Вставить("СуммаЗатратПоДокументу");
	Возврат ПараметрыВыводаТаблицаВедомостьПрав;
КонецФункции

Процедура ЗаполнитьСтроковыеПоляИтогов(КоллекцияИтоговСтраницы, ВалютаРегламентированногоУчета)
	КоллекцияИтоговСтраницы.КоличествоПорядковыхНомеровПрописью = ЧислоПрописью(КоллекцияИтоговСтраницы.КоличествоСтрок,, ",,,м,,,,,0");
	КоллекцияИтоговСтраницы.СтоимостьФактическаяПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(
		КоллекцияИтоговСтраницы.СтоимостьФактическая,
		ВалютаРегламентированногоУчета);
КонецПроцедуры

Функция НовыйИтогиПоказателей()

	ИтогиПоказателей = Новый Структура;
	ИтогиПоказателей.Вставить("СтоимостьФактическая", 0);
	ИтогиПоказателей.Вставить("СтоимостьПоДаннымУчета", 0);
	ИтогиПоказателей.Вставить("КоличествоСтрок", 0);
	ИтогиПоказателей.Вставить("КоличествоПорядковыхНомеровПрописью", "");
	ИтогиПоказателей.Вставить("СтоимостьФактическаяПрописью", "");
	Возврат ИтогиПоказателей;
КонецФункции

Функция НовыйПараметрыПодписи()

	ПараметрыПодписи = Новый Структура;
	ПараметрыПодписи.Вставить("ЗаголовокРазделаПодписей");
	ПараметрыПодписи.Вставить("Должность");
	ПараметрыПодписи.Вставить("РасшифровкаПодписи");
	Возврат ПараметрыПодписи;
	
КонецФункции

#КонецОбласти

#КонецЕсли
