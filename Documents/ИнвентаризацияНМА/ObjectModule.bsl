
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено
			И ТипДанныхЗаполнения <> Тип("Структура")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.ИнвентаризацияНМА.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УчетНМА.СформироватьДвиженияИзменениеЭлементовАмортизации(ПараметрыПроведения, Движения);
	
	УчетНМА.СформироватьДвиженияУчетПрав(ПараметрыПроведения, Движения);
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	
	Движения.Записать();
 
	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОбъектыУчета", Новый Структура("ОбъектУчета"), Отказ);
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	ЗаполнениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// Очищаем лишние документы затрат, не отображаемые на форме в случае, если документы затрат были подобраны, а затем
	// строка с этим объектом учета была удалена или заменена на другой объект.
	ОбъектыОсновнойТаблицы = ОбъектыУчета.Выгрузить().ВыгрузитьКолонку("ОбъектУчета");
	ОбъектыДокументовЗатрат = ДокументыЗатрат.Выгрузить().ВыгрузитьКолонку("ОбъектУчета");
	
	ОбъектыОсновнойТаблицы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОбъектыОсновнойТаблицы);
	ОбъектыДокументовЗатрат = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОбъектыДокументовЗатрат);
	
	ЛишниеОбъекты = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ОбъектыДокументовЗатрат, ОбъектыОсновнойТаблицы);
	
	Для Каждого ЛишнийОбъект Из ЛишниеОбъекты Цикл
		Поиск = Новый Структура("ОбъектУчета", ЛишнийОбъект);
		НайденныеСтроки = ДокументыЗатрат.НайтиСтроки(Поиск);
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			ДокументыЗатрат.Удалить(НайденнаяСтрока);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(Основание)

	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
