#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 12, 0);
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходовНУ
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Результат = Запрос.Выполнить();
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);

	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СинонимНМА", НСтр("ru = 'Нематериальные активы'"));
	Запрос.УстановитьПараметр("ПлательщикНалогаНаПрибыль", УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период));
	
	КритерииПримененияВычета = ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета();
	ЗаполнитьЗначенияСвойств(КритерииПримененияВычета, Реквизиты);
	КритерииПримененияВычета.Дата = Реквизиты.Период;
	КритерииПримененияВычета.МенеджерДокумента = Документы.ПринятиеКУчетуНМА;

	Запрос.УстановитьПараметр("ПроцентИнвестиционногоВычета", ИнвестиционныйНалоговыйВычет.ПроцентИнвестиционногоВычета(КритерииПримененияВычета));
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст =
	ТекстЗапросаПринятиеКУчетуНМА(НомераТаблиц)
	+ ТекстЗапросаСостоянияНМА(НомераТаблиц)
	+ ТекстЗапросаПервоначальныеСведенияНМАБУ(НомераТаблиц)
	+ ТекстЗапросаСчетаБухгалтерскогоУчетаНМА(НомераТаблиц)
	+ ТекстЗапросаСпособыОтраженияРасходовПоАмортизацииНМАБУ(НомераТаблиц)
	+ ТекстЗапросаПервоначальныеСведенияНМАНУ(НомераТаблиц)
	+ ТекстЗапросаПервоначальныеСведенияНМАУСН(НомераТаблиц)
	+ ТекстЗапросаНачислениеАмортизацииНМАСпециальныйКоэффициентНУ(НомераТаблиц)
	+ ТекстЗапросаВключениеВРасходыПриПринятииКУчетуНУ(НомераТаблиц)
	+ ТекстЗапросаНДС(НомераТаблиц)
	+ ТекстЗапросаПервоначальныеСведенияНМАИП(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ПодготовитьТаблицыПараметровПринятиеКУчетуНМА(ТаблицаПринятияКУчетуНМА, ТаблицаСписания) Экспорт
	Параметры = ПодготовитьПараметрыТаблицПринятиеКУчетуНМА(ТаблицаПринятияКУчетуНМА, ТаблицаСписания);
	
	ПринятиеКУчетуНМА = Параметры.ТаблицаПринятияКУчетуНМА[0];
	
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаНУ",     ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаНУДт", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаПРДт", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаВРДт", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаНУКт", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаПРКт", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаВРКт", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаПРКорректировка", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	Параметры.ТаблицаСписания.Колонки.Добавить("СуммаВРКорректировка", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	
	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(ПринятиеКУчетуНМА.Организация, ПринятиеКУчетуНМА.Период);
	
	Если Не ОтражатьВНалоговомУчете
		Или Не СписыватьНУ(ПринятиеКУчетуНМА)
		Или ПринятиеКУчетуНМА.СтоимостьНУ = 0 Тогда
		
		Параметры.ТаблицаСписания.Очистить();
		Возврат Параметры;
	КонецЕсли;
	
	ОбщегоНазначенияБПВызовСервера.РаспределитьСуммуПоКолонкеТаблицы(
		ПринятиеКУчетуНМА.СтоимостьНУ, Параметры.ТаблицаСписания, "СуммаНУ", "Коэффициент");

	Для Каждого СтрокаСписания Из Параметры.ТаблицаСписания Цикл
		ЭтоНепринимаемйРасход = НалоговыйУчет.ЭтоНепринимаемыйРасходНУ(СтрокаСписания.Субконто1, СтрокаСписания.Субконто2, СтрокаСписания.Субконто3);
		
		Если ЭтоНепринимаемйРасход Тогда
			СтрокаСписания.СуммаНУДт = 0;
			СтрокаСписания.СуммаНУКт = СтрокаСписания.СуммаНУ;
			СтрокаСписания.СуммаПРДт = 0;
			СтрокаСписания.СуммаПРКт = - СтрокаСписания.СуммаНУ;
			СтрокаСписания.СуммаВРДт = 0;
			СтрокаСписания.СуммаВРКт = 0;
			СтрокаСписания.СуммаВРКорректировка = 0;
			СтрокаСписания.СуммаПРКорректировка = СтрокаСписания.СуммаНУ;
		Иначе
			
			КоэффициентРасходов = 1;

			ПрименяетсяПовышающийКоэффициент = ПрименяетсяПовышающийКоэффициент(ПринятиеКУчетуНМА, СтрокаСписания.ВидРасходовНУ);
				
			Если ПрименяетсяПовышающийКоэффициент Тогда
				КоэффициентРасходов = УчетНМА.ПовышающийКоэффициентНалоговогоУчета(
					ПринятиеКУчетуНМА.Период, ПринятиеКУчетуНМА.ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.РасходыНаНИОКР);
			КонецЕсли;
			
			СтрокаСписания.СуммаНУДт = СтрокаСписания.СуммаНУ * КоэффициентРасходов;
			СтрокаСписания.СуммаНУКт = СтрокаСписания.СуммаНУ;
			СтрокаСписания.СуммаПРДт = СтрокаСписания.СуммаНУ * (1 - КоэффициентРасходов);
			СтрокаСписания.СуммаПРКт = 0;
			СтрокаСписания.СуммаВРДт = - СтрокаСписания.СуммаНУ;
			СтрокаСписания.СуммаВРКт = - СтрокаСписания.СуммаНУ;
			СтрокаСписания.СуммаВРКорректировка = СтрокаСписания.СуммаНУ;
			СтрокаСписания.СуммаПРКорректировка = 0;

		 КонецЕсли;
	КонецЦикла;
		
	СуммаПРКорректировка = Параметры.ТаблицаСписания.Итог("СуммаПРКорректировка");
	СуммаВРКорректировка = Параметры.ТаблицаСписания.Итог("СуммаВРКорректировка");
	
	Если ПринятиеКУчетуНМА.ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.РасходыНаНИОКР Тогда
		ПринятиеКУчетуНМА.СтоимостьНУ = ПринятиеКУчетуНМА.СтоимостьНУ - СуммаПРКорректировка - СуммаВРКорректировка;
		ПринятиеКУчетуНМА.СтоимостьПР = ПринятиеКУчетуНМА.СтоимостьПР + СуммаПРКорректировка;
		ПринятиеКУчетуНМА.СтоимостьВР = ПринятиеКУчетуНМА.СтоимостьВР + СуммаВРКорректировка;
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт о приеме объекта НМА
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктПриема";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приеме'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 10;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Принятие к учету НМА""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "НематериальныйАктив");
	
	Возврат Результат;
	
КонецФункции

//Процедуры обновления
Процедура ЗаполнитьПорядокВключенияСтоимостиВСоставРасходовНУ() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПорядокВключенияСтоимостиВСоставРасходовНУ",
		Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ПустаяСсылка());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПринятиеКУчетуНМА.Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|ГДЕ
	|	ПринятиеКУчетуНМА.ПорядокВключенияСтоимостиВСоставРасходовНУ = &ПорядокВключенияСтоимостиВСоставРасходовНУ";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПринятиеКУчетуНМА = Выборка.Ссылка.ПолучитьОбъект();
		ПринятиеКУчетуНМА.ПорядокВключенияСтоимостиВСоставРасходовНУ = 
			Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации;
			
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ПринятиеКУчетуНМА, Истина);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет новый реквизит "СтавкаНДС" в ТЧ "ОплатаНМА".
Процедура ЗаполнитьСтавкуНДС(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПринятиеКУчетуНМАОплатаНМА.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА.ОплатаНМА КАК ПринятиеКУчетуНМАОплатаНМА
	|ГДЕ
	|	ПринятиеКУчетуНМАОплатаНМА.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектовОбработано = ОбъектовОбработано + 1;
		НачатьТранзакцию();

		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПринятиеКУчетуНМА");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		Блокировка.Заблокировать();
		
		ПринятиеКУчетуНМА = Выборка.Ссылка.ПолучитьОбъект();
		
		Если ПринятиеКУчетуНМА = Неопределено Тогда
			ОтменитьТранзакцию();
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаОплаты Из ПринятиеКУчетуНМА.ОплатаНМА Цикл
			Если СтрокаОплаты.СтавкаНДС.Пустая() Тогда
				СтрокаОплаты.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ПринятиеКУчетуНМА, Истина);
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Процедуре Документы.ПринятиеКУчетуНМА.ЗаполнитьСтавкуНДС() не удалось обработать документ %1 по причине:
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				ПринятиеКУчетуНМА.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПринятиеКУчетуНМА,
				Выборка.Ссылка,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре Документы.ПринятиеКУчетуНМА.ЗаполнитьСтавкуНДС() не удалось обработать документы: в %1 из %2 возникли ошибки'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ПроблемныхОбъектов,
			ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		Параметры.ОбработкаЗавершена = Истина;
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ПринятиеКУчетуНМА,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Документы.ПринятиеКУчетуНМА.ЗаполнитьСтавкуНДС() успешно завершена: обработано %1 документов'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

// Функция проверяет, заполнен ли новый реквизит "СтавкаНДС" в ТЧ "ОплатаНМА" (см. ЗаполнитьСтавкуНДС()).
// Определена в свойстве ПроцедураПроверки соответствующего обработчика обновления.
//
// Параметры:
//  Параметры - Структура - см. документацию к БСП
// 
// Возвращаемое значение:
//   - Булево - если Истина, то можно редактировать и записывать данные, Ложь - данные недоступны, пока не будут обработаны процедурой обновления
//
Функция СтавкаНДСЗаполнена(Параметры) Экспорт
	
	Документ = Неопределено;
	
	Если ТипЗнч(Параметры.Данные) = Тип("ДокументСсылка.ПринятиеКУчетуНМА") Тогда
		Документ = Параметры.Данные;
	ИначеЕсли ТипЗнч(Параметры.Данные) = Тип("ДокументОбъект.ПринятиеКУчетуНМА")
		 Или ТипЗнч(Параметры.Данные) = Тип("ДанныеФормыСтруктура") Тогда
		Документ = Параметры.Данные.Ссылка;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат Истина;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПринятиеКУчетуНМАОплатаНМА.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА.ОплатаНМА КАК ПринятиеКУчетуНМАОплатаНМА
	|ГДЕ
	|	ПринятиеКУчетуНМАОплатаНМА.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
	|	И ПринятиеКУчетуНМАОплатаНМА.Ссылка = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает признак, что документ не может быть документом расхода по федеральному инвестиционному вычету.
// см. ИнвестиционныйНалоговыйВычет.МожноПрименятьНалоговыйВычет.
// Вызов функции универсален для менеджеров документов, описанных в параметре МенеджерДокумента
// функции ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета.
// 
// Параметры:
//  КритерииПримененияВычета - см. ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета
// 
// Возвращаемое значение:
//  Булево
//
Функция ЗапрещеноПрименятьНалоговыйВычет(КритерииПримененияВычета) Экспорт
	
	СписокДопустимыхОпераций = СписокОперацийПрименяетсяИнвестиционныйВычет();
	Если СписокДопустимыхОпераций.НайтиПоЗначению(КритерииПримененияВычета.ВидОперации) = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если КритерииПримененияВычета.ВидОбъектаУчета <> Перечисления.ВидыОбъектовУчетаНМА.НематериальныйАктив Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если КритерииПримененияВычета.ПорядокВключенияСтоимостиВСоставРасходовНУ <> Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрименяетсяПовышающийКоэффициент(Знач СтрокаПринятиеКУчету, Знач ВидРасходовНУ)
	ВидОбъектаУчета = СтрокаПринятиеКУчету.ВидОбъектаУчета;
	ЭтоЛицензияИзРеестраРоссийскогоПО = СтрокаПринятиеКУчету.ЭтоЛицензияИзРеестраРоссийскогоПО;
	Возврат ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.РасходыНаНИОКР
					И ВидРасходовНУ = Перечисления.ВидыРасходовНУ.НИОКРПоПеречнюПравительстваРФ
				Или ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.НематериальныйАктив
					И ВидРасходовНУ = Перечисления.ВидыРасходовНУ.РасходыНаКомпьютерныеЛицензииСПовышающимКоэффициентом
					И ЭтоЛицензияИзРеестраРоссийскогоПО;
КонецФункции

Функция СписыватьНУ(Расходы)
	СписыватьНУ =
		(Расходы.ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.НематериальныйАктив
			И Расходы.ПорядокВключенияСтоимостиВСоставРасходовНУ = Перечисления.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
		Или (Расходы.ВидОбъектаУчета = Перечисления.ВидыОбъектовУчетаНМА.РасходыНаНИОКР
			И Расходы.ПорядокСписанияНИОКРНаРасходыНУ = Перечисления.ПорядокСписанияНИОКРНУ.ПриПринятииКУчету);
	Возврат СписыватьНУ;
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриема") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктПриема", 
			НСтр("ru = 'Акт приема объекта НМА'"), ПечатьАктПриема(МассивОбъектов, ОбъектыПечати),,
			"Документ.ПринятиеКУчетуНМА.ПФ_MXL_АктПриема");
	КонецЕсли;

КонецПроцедуры

Функция ПечатьАктПриема(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПринятиеКУчетуНМА_АктПриема";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПринятиеКУчетуНМА.ПФ_MXL_АктПриема");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ГруппаРезультатыДеятельности", НСтр("ru = 'Результаты интеллектуальной деятельности'"));
	Запрос.УстановитьПараметр("ГруппаСредстваИдивидуализации", НСтр("ru = 'Средства идивидуализации'"));
	Запрос.УстановитьПараметр("ГруппаИныеОбъекты", НСтр("ru = 'Иные объекты НМА'"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПринятиеКУчету.Ссылка КАК Ссылка,
	|	ПринятиеКУчету.Номер КАК НомерДокумента,
	|	ПринятиеКУчету.Дата КАК ДатаДокумента,
	|	ПринятиеКУчету.Организация КАК Организация,
	|	ПринятиеКУчету.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ПринятиеКУчету.ПодразделениеОрганизации.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПринятиеКУчету.ПодразделениеОрганизации.Наименование
	|		ИНАЧЕ ПринятиеКУчету.ПодразделениеОрганизации.НаименованиеПолное
	|	КОНЕЦ КАК ПредставлениеПодразделения,
	|	ВЫБОР
	|		КОГДА ПринятиеКУчету.НематериальныйАктив.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПринятиеКУчету.НематериальныйАктив.Наименование
	|		ИНАЧЕ ПринятиеКУчету.НематериальныйАктив.НаименованиеПолное
	|	КОНЕЦ КАК Наименование,
	|	ПринятиеКУчету.НематериальныйАктив.Код КАК ИнвентарныйНомер,
	|	ПринятиеКУчету.НематериальныйАктив.ПрочиеСведения КАК Описание,
	|	ПринятиеКУчету.НематериальныйАктив.ВидНематериальногоАктива КАК ВидНематериальногоАктива,
	|	ПринятиеКУчету.НематериальныйАктив.ВидНМАПоПБУ14 КАК ВидНМАПоПБУ14,
	|	ПринятиеКУчету.НематериальныйАктив.КодОКОФ КАК КодОКОФ,
	|	ЕСТЬNULL(ПринятиеКУчету.НематериальныйАктив.КодОКОФ.Наименование, """") КАК КодОКОФНаименование,
	|	ПринятиеКУчету.СпособПоступления КАК СпособПоступления,
	|	ПринятиеКУчету.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ПринятиеКУчету.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	ПринятиеКУчету.СпособНачисленияАмортизацииБУ КАК СпособНачисленияАмортизацииБУ,
	|	ПринятиеКУчету.СтоимостьБУ КАК Стоимость
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчету
	|ГДЕ
	|	ПринятиеКУчету.Ссылка В(&МассивОбъектов)
	|	И ПринятиеКУчету.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПринятиеКУчету.Дата,
	|	ПринятиеКУчету.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		// Шапка
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ПараметрыЗаполнения = Новый Структура;
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			Выборка.Организация, Выборка.ДатаДокумента);
		ПараметрыЗаполнения.Вставить(
			"ПредставлениеОрганизации",
			ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,"));
		ПараметрыЗаполнения.Вставить("ПредставлениеПодразделения", Выборка.ПредставлениеПодразделения);
		ПараметрыЗаполнения.Вставить("ДатаДокумента", Выборка.ДатаДокумента);
		
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(
			Выборка.Организация, Выборка.ДатаДокумента, Выборка.ПодразделениеОрганизации);
		ПараметрыЗаполнения.Вставить("ФИОРуководителя", Руководители.РуководительПредставление);
		
		ЭтоИндивидуальныйПредприниматель = 
			СведенияОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
			И СведенияОбОрганизации.ИндивидуальныйПредприниматель = Руководители.Руководитель;
		
		ДолжностьРуководителя = ?(ЭтоИндивидуальныйПредприниматель,
			НСтр("ru = 'Индивидуальный предприниматель'"),
			Руководители.РуководительДолжностьПредставление);
		ПараметрыЗаполнения.Вставить("ДолжностьРуководителя", ДолжностьРуководителя);
		
		НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.НомерДокумента, Истина, Ложь);
		ПараметрыЗаполнения.Вставить("НомерДокумента", НомерДокумента);
		ПараметрыЗаполнения.Вставить("ИнвентарныйНомер", Выборка.ИнвентарныйНомер);
		
		ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Данные объекта НМА.
		ОбластьМакета = Макет.ПолучитьОбласть("НаименованиеОбъекта");
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Наименование", Выборка.Наименование);
		ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ОписаниеОтсутствует");
		Если ЗначениеЗаполнено(Выборка.Описание) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ОписаниеЗаполнено");
			ПараметрыЗаполнения.Вставить("Описание", Выборка.Описание);
			ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ДанныеОбъекта");
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("СпособПоступления", Выборка.СпособПоступления);
		ПараметрыЗаполнения.Вставить("ПервоначальнаяСтоимость", Выборка.Стоимость);
		ПараметрыЗаполнения.Вставить("Группа", УчетНМА.ПредставлениеГруппыУчета(Выборка.ВидНематериальногоАктива, Выборка.ВидНМАПоПБУ14));
		Если Выборка.ДатаДокумента < УчетНМА.НачалоОбязательногоПримененияФСБУ14() Тогда
			// Для сохранения поведения с предыдущими версиями
			ВидНМА = Выборка.ВидНМАПоПБУ14;
		Иначе
			ВидНМА = Выборка.ВидНематериальногоАктива;
		КонецЕсли;
		ПараметрыЗаполнения.Вставить("Вид", ВидНМА);
		Если ЗначениеЗаполнено(Выборка.КодОКОФ) Тогда
			ПараметрыЗаполнения.Вставить("КодПоКлассификатору", 
				СтрШаблон("%1 - %2", Выборка.КодОКОФ, Выборка.КодОКОФНаименование));
		КонецЕсли;
		ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Если Выборка.НачислятьАмортизациюБУ Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ПараметрыАмортизации");
			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("СпособАмортизации", Выборка.СпособНачисленияАмортизацииБУ);
			ПараметрыЗаполнения.Вставить("СрокПолезногоИспользования", Выборка.СрокПолезногоИспользованияБУ);
			ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("ОбъектНеАмортизируется");
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Подпись бухгалтера
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		Если Не ЭтоИндивидуальныйПредприниматель Тогда
			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("ФИОБухгалтер", Руководители.ГлавныйБухгалтерПредставление);
			ПараметрыЗаполнения.Вставить("ДолжностьБухгалтер", Руководители.ГлавныйБухгалтерДолжность);
			ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

Функция ТекстЗапросаПринятиеКУчетуНМА(НомераТаблиц)

	НомераТаблиц.Вставить("ПринятиеКУчетуНМА", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаСпособОтраженияСсылка", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СпособОтраженияРасходов", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.СтатьяДоходов КАК СтатьяДоходов,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеКт,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеДт,
	|	Реквизиты.СчетУчета КАК СчетУчета,
	|	Реквизиты.СчетУчетаВнеоборотногоАктива КАК СчетУчетаВнеоборотногоАктива,
	|	Реквизиты.НематериальныйАктив КАК НематериальныйАктив,
	|	Реквизиты.СтоимостьБУ КАК СтоимостьБУ,
	|	Реквизиты.СтоимостьНУ КАК СтоимостьНУ,
	|	Реквизиты.СтоимостьПР КАК СтоимостьПР,
	|	Реквизиты.СтоимостьВР КАК СтоимостьВР,
	|	Реквизиты.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	Реквизиты.ПорядокСписанияНИОКРНаРасходыНУ КАК ПорядокСписанияНИОКРНаРасходыНУ,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходовНУ,
	|	НЕ НематериальныеАктивы.ЭтоИсключительноеПраво
	|		И НематериальныеАктивы.ВключенВРеестрРоссийскогоПО
	|		И НематериальныеАктивы.НомерРеестрРоссийскогоПО <> """"
	|		И НематериальныеАктивы.ДатаРеестрРоссийскогоПО <> ДАТАВРЕМЯ(1, 1, 1)
	|		И НематериальныеАктивы.ДатаРеестрРоссийскогоПО <= Реквизиты.Дата КАК ЭтоЛицензияИзРеестраРоссийскогоПО,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПрименяетсяИнвестиционныйВычет
	|			ТОГДА &ПроцентИнвестиционногоВычета
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроцентИнвестиционногоВычета
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|		ПО Реквизиты.НематериальныйАктив = НематериальныеАктивы.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|				И Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|			ТОГДА Реквизиты.СпособОтраженияРасходовПриВключенииВСтоимость
	|		ИНАЧЕ Реквизиты.СпособОтраженияРасходов
	|	КОНЕЦ КАК СпособОтраженияРасходов
	|ПОМЕСТИТЬ ВременнаяТаблицаСпособОтраженияСсылка
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Направления.НомерСтроки КАК НомерСтроки,
	|	Направления.СчетЗатрат КАК СчетЗатрат,
	|	Направления.ПодразделениеОрганизации КАК Подразделение,
	|	Направления.Субконто1 КАК Субконто1,
	|	Направления.Субконто2 КАК Субконто2,
	|	Направления.Субконто3 КАК Субконто3,
	|	Направления.Коэффициент КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА Направления.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(Направления.Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|		КОГДА Направления.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(Направления.Субконто2 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|		КОГДА Направления.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(Направления.Субконто3 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|		КОГДА Направления.Субконто1 ССЫЛКА Справочник.НазначенияИспользованияНМА
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто1 КАК Справочник.НазначенияИспользованияНМА).Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто1 КАК Справочник.НазначенияИспользованияНМА).Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто1 КАК Справочник.НазначенияИспользованияНМА).Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто1 КАК Справочник.НазначенияИспользованияНМА).Субконто2 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто1 КАК Справочник.НазначенияИспользованияНМА).Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто1 КАК Справочник.НазначенияИспользованияНМА).Субконто3 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПустаяСсылка)
	|				КОНЕЦ
	|		КОГДА Направления.Субконто2 ССЫЛКА Справочник.НазначенияИспользованияНМА
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто2 КАК Справочник.НазначенияИспользованияНМА).Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто2 КАК Справочник.НазначенияИспользованияНМА).Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто2 КАК Справочник.НазначенияИспользованияНМА).Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто2 КАК Справочник.НазначенияИспользованияНМА).Субконто2 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто2 КАК Справочник.НазначенияИспользованияНМА).Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто2 КАК Справочник.НазначенияИспользованияНМА).Субконто3 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПустаяСсылка)
	|				КОНЕЦ
	|		КОГДА Направления.Субконто3 ССЫЛКА Справочник.НазначенияИспользованияНМА
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто3 КАК Справочник.НазначенияИспользованияНМА).Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто3 КАК Справочник.НазначенияИспользованияНМА).Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто3 КАК Справочник.НазначенияИспользованияНМА).Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто3 КАК Справочник.НазначенияИспользованияНМА).Субконто2 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					КОГДА ВЫРАЗИТЬ(Направления.Субконто3 КАК Справочник.НазначенияИспользованияНМА).Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
	|						ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(Направления.Субконто3 КАК Справочник.НазначенияИспользованияНМА).Субконто3 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПустаяСсылка)
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПустаяСсылка)
	|	КОНЕЦ КАК ВидРасходовНУ
	|ИЗ
	|	ВременнаяТаблицаСпособОтраженияСсылка КАК ВременнаяТаблицаСпособОтраженияСсылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК Направления
	|		ПО ВременнаяТаблицаСпособОтраженияСсылка.СпособОтраженияРасходов = Направления.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции


Функция ТекстЗапросаСостоянияНМА(НомераТаблиц)

	НомераТаблиц.Вставить("СостоянияНМА", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Номер КАК Номер,
	|	0 КАК НомерСтроки,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.НематериальныйАктив КАК НематериальныйАктив,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету) КАК Состояние,
	|	"""" КАК ИмяСписка,
	|	""НематериальныйАктив"" КАК ИмяПоля
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСчетаБухгалтерскогоУчетаНМА(НомераТаблиц)

	НомераТаблиц.Вставить("СчетаБухгалтерскогоУчетаНМА", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.НематериальныйАктив,
	|	Реквизиты.СчетУчета,
	|	Реквизиты.СчетНачисленияАмортизации
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСпособыОтраженияРасходовПоАмортизацииНМАБУ(НомераТаблиц)

	НомераТаблиц.Вставить("СпособыОтраженияРасходовПоАмортизацииНМАБУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.НематериальныйАктив,
	|	Реквизиты.СпособОтраженияРасходов
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияНМАБУ(НомераТаблиц)

	НомераТаблиц.Вставить("ПервоначальныеСведенияНМАБУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.НематериальныйАктив КАК НематериальныйАктив,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтоимостьБУ КАК СтоимостьБУ,
	|	Реквизиты.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	Реквизиты.СпособНачисленияАмортизацииБУ КАК СпособНачисленияАмортизацииБУ,
	|	Реквизиты.СпособПоступления КАК СпособПоступления,
	|	Реквизиты.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	Реквизиты.КоэффициентБУ КАК КоэффициентБУ,
	|	Реквизиты.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияНМАНУ(НомераТаблиц)
	
	НомераТаблиц.Вставить("ПервоначальныеСведенияНМАНУ", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	&ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	Реквизиты.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ,
	|	Реквизиты.НематериальныйАктив КАК НематериальныйАктив,
	|	Реквизиты.СтоимостьНУ КАК СтоимостьНУ,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	Реквизиты.СпособОтраженияРасходовПриВключенииВСтоимость КАК СпособОтраженияРасходовПриВключенииВСтоимость,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|			ТОГДА Реквизиты.НачислятьАмортизациюНУ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.ПорядокСписанияНИОКРНаРасходыНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокСписанияНИОКРНУ.Равномерно)
	|					ТОГДА Реквизиты.НачислятьАмортизациюНУ
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК НачислятьАмортизациюНУ
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияНМАУСН(НомераТаблиц)

	НомераТаблиц.Вставить("ПервоначальныеСведенияНМАУСН", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаОплатНМАУСН", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.НематериальныйАктив КАК НематериальныйАктив,
	|	Реквизиты.СтоимостьУСН КАК СтоимостьУСН,
	|	Реквизиты.СрокПолезногоИспользованияУСН КАК СрокПолезногоИспользованияУСН,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовУСН КАК ПорядокВключенияСтоимостиВСоставРасходовУСН,
	|	Реквизиты.ДатаПриобретения КАК ДатаПриобретения,
	|	Реквизиты.СпособУчетаНДС КАК СпособУчетаНДС
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОплатНМАУСН.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОплатНМАУСН.ДатаОплаты КАК ДатаОплаты,
	|	ТаблицаОплатНМАУСН.СуммаОплаты КАК СуммаОплаты,
	|	ТаблицаОплатНМАУСН.СуммаНДС КАК СуммаНДС
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА.ОплатаНМА КАК ТаблицаОплатНМАУСН
	|ГДЕ
	|	ТаблицаОплатНМАУСН.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНачислениеАмортизацииНМАСпециальныйКоэффициентНУ(НомераТаблиц)
	
	НомераТаблиц.Вставить("НачислениеАмортизацииНМАСпециальныйКоэффициентНУ", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка                   КАК Регистратор,
	|	Реквизиты.Дата                     КАК Период,
	|	Реквизиты.Организация              КАК Организация,
	|	&ПлательщикНалогаНаПрибыль         КАК ПлательщикНалогаНаПрибыль,
	|	Реквизиты.НематериальныйАктив      КАК НематериальныйАктив,
	|	Реквизиты.СпециальныйКоэффициентНУ КАК СпециальныйКоэффициентНУ
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВключениеВРасходыПриПринятииКУчетуНУ(НомераТаблиц)

	НомераТаблиц.Вставить("ВключениеВРасходыПриПринятииКУчетуНУ", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	&ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	Реквизиты.СпособОтраженияРасходовПриВключенииВСтоимость КАК СпособыОтраженияРасходовПоАмортизации,
	|	Реквизиты.СчетУчета КАК СчетУчета,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.НематериальныйАктив КАК НематериальныйАктив,
	|	Реквизиты.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	""Включение стоимости в состав расходов (НУ)"" КАК Содержание,
	|	""НМА"" КАК ИмяСписка
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНДС(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НМАНДС",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписанныеНМАНДС",    НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РеквизитыНДС.Ссылка КАК Регистратор,
	|	РеквизитыНДС.Дата КАК Период,
	|	РеквизитыНДС.Организация КАК Организация,
	|	РеквизитыНДС.НематериальныйАктив КАК НематериальныйАктив,
	|	ЗНАЧЕНИЕ(Перечисление.НДССостоянияОСНМА.ОжидаетсяВводВЭксплуатацию) КАК СостояниеНМА,
	|	1 КАК КоэффициентРаспределения
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК РеквизитыНДС
	|ГДЕ
	|	РеквизитыНДС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""НМА"" КАК ИмяСписка,
	|	1 КАК НомерСтрокиДокумента,
	|	НМАНДС.НематериальныйАктив КАК Номенклатура,
	|	1 КАК Количество,
	|	НМАНДС.СчетУчетаВнеоборотногоАктива КАК СчетУчета,
	|	НМАНДС.СпособУчетаНДС КАК НовыйСпособУчетаНДС,
	|	НМАНДС.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК НМАНДС
	|ГДЕ
	|	НМАНДС.Ссылка = &Ссылка
	|	И НМАНДС.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНМА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""НМА"" КАК ИмяСписка,
	|	""Нематериальные активы"" КАК СинонимСписка,
	|	1 КАК НомерСтроки,
	|	СписанныеНМАНДС.СчетУчетаВнеоборотногоАктива КАК СчетУчета,
	|	СписанныеНМАНДС.НематериальныйАктив КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК Партия,
	|	1 КАК Количество,
	|	СписанныеНМАНДС.СчетУчета КАК КорСчетСписания,
	|	1 КАК ВидКорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто3,
	|	СписанныеНМАНДС.НематериальныйАктив КАК КорСубконто1,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	СписанныеНМАНДС.ПодразделениеОрганизации КАК КорПодразделение,
	|	СписанныеНМАНДС.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК СписанныеНМАНДС
	|ГДЕ
	|	СписанныеНМАНДС.Ссылка = &Ссылка
	|	И СписанныеНМАНДС.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНМА)";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПервоначальныеСведенияНМАИП(НомераТаблиц)

	НомераТаблиц.Вставить("ПервоначальныеСведенияНМАИП",	НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.НематериальныйАктив КАК НематериальныйАктив,
	|	Реквизиты.СчетУчета КАК СчетУчета,
	|	Реквизиты.СтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	Реквизиты.ПорядокВключенияСтоимостиВСоставРасходовНУ КАК ПорядокВключенияСтоимостиВСоставРасходовНУ,
	|	Реквизиты.ДатаПриобретения КАК ДатаПриобретения,
	|	Реквизиты.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользования,
	|	Реквизиты.НачислятьАмортизациюНУ КАК НачислятьАмортизацию,
	|	ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) КАК МетодНачисленияАмортизации,
	|	Реквизиты.СпециальныйКоэффициентНУ КАК СпециальныйКоэффициент,
	|	Реквизиты.РеквизитыДокументаОплаты КАК РеквизитыДокументаОплаты,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы) КАК СчетЗатрат,
	|	Реквизиты.СтатьяПрочихРасходов КАК СтатьяЗатрат,
	|	Реквизиты.СтатьяПрочихРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|	Реквизиты.СтатьяПрочихРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|	И Реквизиты.СтоимостьНУ > 0";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции


Функция ПодготовитьПараметрыТаблицПринятиеКУчетуНМА(ТаблицаПринятияКУчетуНМА, ТаблицаСписания)
	
	Параметры = Новый Структура;
	// Подготовка таблицы Параметры.ТаблицаПринятияКУчетуНМА

	СписокОбязательныхКолонок = ""
	+ "Период,"                       // <Дата>
	+ "НематериальныйАктив,"          // <СправочникСсылка.НематериальныеАктивы>
	+ "Организация,"                  // <СправочникСсылка.Организации>
	+ "ВидОперации,"                  // <ПеречислениеСсылка.ВидыОперацийПринятияКУчетуНМА>
	+ "СтатьяДоходов,"                // <СправочникСсылка.ПрочиеДоходыИРасходы>
	+ "ПодразделениеДт,"              // <Ссылка на справочник подразделений>
	+ "ПодразделениеКт,"              // <Ссылка на справочник подразделений>
	+ "Регистратор,"                  // <ДокументСсылка.*>
	+ "СчетУчета,"                    // <ПланСчетовСсылка.Хозрасчетный> - счет на который принимается к учету НМА
	+ "СчетУчетаВнеоборотногоАктива," // <ПланСчетовСсылка.Хозрасчетный> - счет учета нематериального актива
	+ "СтоимостьБУ,"
	+ "СтоимостьНУ,"
	+ "СтоимостьПР,"
	+ "СтоимостьВР,"
	+ "ВидОбъектаУчета,"
	+ "ПорядокСписанияНИОКРНаРасходыНУ,"
	+ "ПорядокВключенияСтоимостиВСоставРасходовНУ,"
	+ "ЭтоЛицензияИзРеестраРоссийскогоПО"
	;

	Параметры.Вставить("ТаблицаПринятияКУчетуНМА",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаПринятияКУчетуНМА, СписокОбязательныхКолонок));

	СписокОбязательныхКолонок = ""
	+ "НомерСтроки,"                  // <Число>
	+ "СчетЗатрат,"                   // <ПланСчетовСсылка.Хозрасчетный>
	+ "Подразделение,"                // <Ссылка на справочник подразделений>
	+ "Субконто1,"                    //
	+ "Субконто2,"                    //
	+ "Субконто3,"                    //
	+ "Коэффициент,"                  // <Число>
	+ "ВидРасходовНУ"                 // <ПеречислениеСсылка.ВидыРасходовНУ>
	;

	Параметры.Вставить("ТаблицаСписания",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаСписания, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

// Список операций, при которых возможно применение инвестиционного вычета.
// 
// Возвращаемое значение:
//  СписокЗначений из ПеречислениеСсылка.ВидыОперацийПринятияКУчетуНМА
//
Функция СписокОперацийПрименяетсяИнвестиционныйВычет()
	
	СписокОпераций = Новый СписокЗначений;
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНМА);
	Возврат СписокОпераций;
	
КонецФункции

#Область ОбработчикиОбновления

Процедура ЗаполнитьВидОперации(ПараметрыОбработчика) Экспорт
	
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ПринятиеКУчетуНМА.Ссылка КАК Ссылка,
	|	ПринятиеКУчетуНМА.МоментВремени КАК Курсор
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|ГДЕ
	|	ПринятиеКУчетуНМА.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПринятияКУчетуНМА.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Курсор УБЫВ";
	
	Если Не ПараметрыОбработчика.Свойство("Курсор") Тогда
		Курсор = Неопределено;
		ТекстЗапроса = ШаблонТекстаЗапроса;
	Иначе
		Курсор = ПараметрыОбработчика.Курсор;
		Запрос = СхемыЗапросов.НайтиТаблицуПоПсевдониму(ШаблонТекстаЗапроса, "ПринятиеКУчетуНМА");
		Запрос.Оператор.Отбор.Добавить("ПринятиеКУчетуНМА.МоментВремени < &Курсор");
		ТекстЗапроса = Запрос.Схема.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Курсор", Курсор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ПараметрыОбработчика.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Ложь;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПараметрыОбработчика.Вставить("Курсор", Выборка.Курсор);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПринятиеКУчетуНМА");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		
		НачатьТранзакцию();
		
		Попытка
			
			ПринятиеКУчетуСсылка =  Выборка.Ссылка;
			ПринятиеКУчетуПредставление = Строка(Выборка.Ссылка);
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
			Если ДокументОбъект = Неопределено Или ЗначениеЗаполнено(ДокументОбъект.ВидОперации) Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПринятияКУчетуНМА.ПринятиеКУчетуНМА;
		
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение = СтрШаблон(
				НСтр("ru = 'Ошибка при выполнении обработчика Документы.ПринятиеКУчетуНМА.ЗаполнитьВидОперации.
                      |%1'"),
				ПредставлениеОшибки);
			
			ОбновлениеИнформационнойБазы.ЗаписатьОшибкуВЖурналРегистрации(
				ПринятиеКУчетуСсылка,
				ПринятиеКУчетуПредставление,
				Сообщение);
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Функция проверяет заполненность вида операции документа.
// Определена в свойстве ПроцедураПроверки соответствующего обработчика обновления.
//
// Параметры:
//  МетаданныеИОтбор - см. ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным
// 
// Возвращаемое значение:
//   - Булево - Признак, что объект не нуждается в обработке обновления.
//
Функция ВидОперацииЗаполнен(МетаданныеИОтбор) Экспорт

	Документ = МетаданныеИОтбор.Отбор;
	
	Если Не ЗначениеЗаполнено(Документ) Или МетаданныеИОтбор.ЭтоНовый Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПринятиеКУчетуНМА.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|ГДЕ
	|	ПринятиеКУчетуНМА.ВидОперации = &ПустойВидОперации
	|	И ПринятиеКУчетуНМА.Ссылка = &Документ";
	
	Запрос.УстановитьПараметр("ПустойВидОперации", Перечисления.ВидыОперацийПринятияКУчетуНМА.ПустаяСсылка());
	Запрос.УстановитьПараметр("Документ", Документ);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли