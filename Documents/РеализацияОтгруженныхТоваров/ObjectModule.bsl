#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура") 
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	КонецЕсли;
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

	// Специфические для конкретного документа действия
	Если ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(Контрагент)
		И (ЗначениеЗаполнено(ДоговорКонтрагента) ИЛИ НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам")) Тогда
		Документы.РеализацияОтгруженныхТоваров.ЗаполнитьСчетаУчетаРасчетов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект);
	
	ОтчетМаркетплейса = Документы.ОтчетМаркетплейса.ПустаяСсылка();
	
	ЗачетАвансов.Очистить();
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	// В формах документа счет расчетов и счет авансов редактируются в специальной форме.
	// В случае, если они не заполнены, покажем сообщение возле соответствующей гиперссылки.
	МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКонтрагентом");
	МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовПоАвансам");
	МассивНепроверяемыхРеквизитов.Добавить("Комиссионер");
	МассивНепроверяемыхРеквизитов.Добавить("ДоговорКомиссионера");
	МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКомиссионером");
	МассивНепроверяемыхРеквизитов.Добавить("СчетУчетаРасчетовСКомиссионеромПоАвансам");
	
	Если НЕ ЗначениеЗаполнено(СчетУчетаРасчетовСКонтрагентом) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,,
			НСтр("ru = 'Счет учета расчетов с контрагентом'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,
			"ПорядокУчетаРасчетов", Отказ);
	КонецЕсли;
	
	Если ТребуетсяУчетАвансов() И Не ОплатаЧерезКомиссионера Тогда
		Если НЕ ЗначениеЗаполнено(СчетУчетаРасчетовПоАвансам) Тогда
			ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,,
				НСтр("ru = 'Счет учета расчетов по авансам'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,
				"ПорядокУчетаРасчетов", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ОплатаЧерезКомиссионера Тогда
		Если Не ЗначениеЗаполнено(Комиссионер) Тогда
			ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,,
				НСтр("ru = 'Оплата через платежного агента (комиссионера)'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,
				"ПорядокУчетаРасчетов", Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДоговорКомиссионера) Тогда
			ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,,
				НСтр("ru = 'Договор с платежным агентом (комиссионером)'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,
				"ПорядокУчетаРасчетов", Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СчетУчетаРасчетовСКомиссионером) Тогда
			ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,,
				НСтр("ru = 'Счет учета расчетов с платежным агентом (комиссионером)'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,
				"ПорядокУчетаРасчетов", Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СчетУчетаРасчетовСКомиссионеромПоАвансам) Тогда
			ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,,
				НСтр("ru = 'Счет учета расчетов по авансам с платежным агентом (комиссионером)'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,
				"ПорядокУчетаРасчетов", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЧастичнаяПродажа Тогда
		ДокументЧастичнойПродажи = Документы.РеализацияОтгруженныхТоваров.ЧастичнаяПродажаПоДокументуОтгрузки(ДокументОтгрузки, Ссылка);
		
		Если ЗначениеЗаполнено(ДокументЧастичнойПродажи) Тогда
	        ТекстСообщения = НСтр("ru = 'Нельзя ввести полную отгрузку по документу передачи, который ранее уже был частично отгружен.'");
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", 
				НСтр("ru = 'Документ отгрузки'"),,, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, 
				"ДокументОтгрузки", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОтгрузки) Тогда
		
		Если ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
			ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОтгрузки, "ВидОперации");
		    Если ВидОперации <> Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности Тогда
		        ТекстСообщения = НСтр("ru = 'Указанный документ отгрузки не регистрирует отгрузку без перехода права собственности.'");
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", 
					НСтр("ru = 'Документ отгрузки'"),,, ТекстСообщения);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, 
					"ДокументОтгрузки", "Объект", Отказ);
			КонецЕсли;
			
			Если ЧастичнаяПродажа Тогда
				//Проверим остатки отгруженных товаров
				ТаблицаОстатков = Документы.РеализацияОтгруженныхТоваров.ОстатокОтгруженныхТоваров(ДокументОтгрузки, Ссылка);
				ДокументБезНДС  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОтгрузки, "ДокументБезНДС");
				Если ДокументБезНДС Тогда
					ОтборСтрокиТаблицы = Новый Структура( "Номенклатура, Цена,
						|ПереданныеСчетУчета, СчетДоходов, СчетРасходов");
				Иначе
					ОтборСтрокиТаблицы = Новый Структура( "Номенклатура, Цена, СтавкаНДС, 
						|ПереданныеСчетУчета, СчетДоходов, СчетРасходов, Субконто");
				КонецЕсли;
				
				Для Каждого СтрокаТовары Из Товары Цикл
					
					ЗаполнитьЗначенияСвойств(ОтборСтрокиТаблицы, СтрокаТовары);
					Остатки = ТаблицаОстатков.НайтиСтроки(ОтборСтрокиТаблицы);
					КоличествоОтгруженных = СтрокаТовары.Количество;
					
					Для Каждого СтрокаОстатки Из Остатки Цикл
						
						Отгружено = Мин(КоличествоОтгруженных, СтрокаОстатки.Количество); 
						КоличествоОтгруженных = КоличествоОтгруженных - Отгружено;
						СтрокаОстатки.Количество = СтрокаОстатки.Количество - Отгружено;
						
						Если КоличествоОтгруженных = 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
					
					Если КоличествоОтгруженных > 0 Тогда
						Префикс = "Товары[" + Формат(СтрокаТовары.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].";						
						
						ТекстСообщения = НСтр("ru = 'Документом ""%1"" было отгружено ""%2"" по цене ""%3"" на ""%4"" ед. меньше, чем реализуется.'");
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ТекстСообщения,
							СокрЛП(ДокументОтгрузки),
							СтрокаТовары.Номенклатура,
							СтрокаТовары.Цена,
							КоличествоОтгруженных);
						
						ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Корректность",
							НСтр("ru = 'Количество'") , СтрокаТовары.НомерСтроки, НСтр("ru = 'Товары'"), ТекстСообщения);
						Поле = Префикс + "Количество";
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.ПередачаОС") Тогда
			
			ПравоСобственностиПереходитПослеГосРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОтгрузки, "ПравоСобственностиПереходитПослеГосРегистрации");
		    Если НЕ ПравоСобственностиПереходитПослеГосРегистрации Тогда
		        ТекстСообщения = НСтр("ru = 'Указанный документ отгрузки не регистрирует передачу ОС без перехода права собственности.'");
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", 
					НСтр("ru = 'Документ отгрузки'"),,, ТекстСообщения);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, 
					"ДокументОтгрузки", "Объект", Отказ);
				КонецЕсли;
				
		КонецЕсли;
				
	КонецЕсли; 
	
	// Табличная часть "Зачет авансов"
	Если СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	ИначеЕсли ЗачетАвансов.Количество() = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	
		ТекстСообщения = НСтр("ru = 'Не введено ни одной строки с документом аванса!'");
		Поле = "ПорядокУчетаРасчетов";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , Поле, Отказ);
	КонецЕсли;
	
	// Эти реквизиты проверяются в документе с помощью специального, нетипового механизма. Проверка размещена в ПроверитьЗаполнениеСубконто()
	МассивНепроверяемыхРеквизитов.Добавить("Товары.Субконто");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	РеквизитыЗаСсылками = Документы.РеализацияОтгруженныхТоваров.РеквизитыЗаСсылками(ЧастичнаяПродажа);
	Если Не СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками) Тогда
		УчетДоходовИРасходовПредпринимателя.ПроверитьЗаполнениеСубконтоНоменклатурныеГруппы(ЭтотОбъект, "СчетДоходов", "Субконто", 
			НСтр("ru = 'Субконто'"), "Товары", НСтр("ru = 'Товары'"), Отказ);
	КонецЕсли;
	
	ПроверкаЗаполненияДокументов.ПроверитьРеквизитыЗаСсылками(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументОтгрузки) Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияОДублях = ИнформацияОДубляхРеализацииОтгруженныхТоваров();
	
	Если ИнформацияОДублях.ЕстьДубли Тогда
		ОбщегоНазначения.СообщитьПользователю(ИнформацияОДублях.СообщениеПользователю, ЭтотОбъект, , , Отказ);
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	
	НачислятьНДСПоОтгрузке = УчетнаяПолитика.НачислятьНДСПоОтгрузке(Организация, Дата);
	ЕстьУслуги             = (ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.РеализацияТоваровУслуг")) И УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ДокументОтгрузки, "Услуги") > 0;
	ПараметрыДействия      = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ЭтотОбъект);
	
	Если НачислятьНДСПоОтгрузке И НЕ ЕстьУслуги Тогда
		ПараметрыДействия.СостояниеФлага = Истина;
	КонецЕсли;
	
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);

	ДополнительныеСвойства.Вставить("НачислятьНДСПоОтгрузке", НачислятьНДСПоОтгрузке);  
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(ЭтотОбъект, РежимЗаписи);

	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	Если ЧастичнаяПродажа Тогда
		СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОтгрузки, "СуммаВключаетНДС");
		СуммаДокумента = Товары.Итог("Сумма") + ?(СуммаВключаетНДС, 0,  Товары.Итог("СуммаНДС"));
	ИначеЕсли ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОтгрузки, "СуммаВключаетНДС");
		ТаблицаТоваров = Документы.РеализацияОтгруженныхТоваров.ОстатокОтгруженныхТоваров(ДокументОтгрузки, Ссылка);
		СуммаДокумента    = ТаблицаТоваров.Итог("Сумма") 
										  + ?(СуммаВключаетНДС, 0, ТаблицаТоваров.Итог("СуммаНДС")) 
										  + УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ДокументОтгрузки, "Услуги");
	Иначе
		ИмяТабличнойЧасти = ?(ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.ПередачаОС"), "ОС", "Товары");
		СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ДокументОтгрузки, ИмяТабличнойЧасти);
	КонецЕсли;
		
	Документы.КорректировкаРеализации.ОбновитьРеквизитыСвязанныхДокументовКорректировки(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументОтгрузки)
		И НЕ ДополнительныеСвойства.НачислятьНДСПоОтгрузке Тогда
		
		УчетНДСПереопределяемый.ПроверитьСоответствиеРеквизитовСчетаФактурыВыданного(ЭтотОбъект);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.РеализацияОтгруженныхТоваров.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ
	
	// Таблица списанных товаров
	ТаблицаСписанныеТовары = УчетТоваров.ПодготовитьТаблицуСписанныеТовары(
		ПараметрыПроведения.СписаниеТоваровТовары,
		ПараметрыПроведения.СписаниеТоваровШапка, Отказ);
	
	// Таблица взаиморасчетов с учетом зачета авансов
	ТаблицаВзаиморасчетов = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента, ПараметрыПроведения.ЗачетАвансовАвансы, 
		ПараметрыПроведения.ЗачетАвансовШапка, Отказ);
	
	// Таблица взаиморасчетов с учетом зачета авансов
	ТаблицаВзаиморасчетовПлатежногоАгента = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		ПараметрыПроведения.ЗачетАвансовПлатежногоАгентаТаблицаДокумента, ПараметрыПроведения.ЗачетАвансовПлатежногоАгентаАвансы,
		ПараметрыПроведения.ЗачетАвансовПлатежногоАгентаШапка, Отказ);
	
	// Таблицы выручки от реализации: собственных товаров и услуг и отдельно комиссионных 
	ТаблицыРеализация = УчетДоходовРасходов.ПодготовитьТаблицыВыручкиОтРеализации(
		ПараметрыПроведения.РеализацияТаблицаДокумента, ТаблицаВзаиморасчетов, 
		ТаблицаСписанныеТовары, ПараметрыПроведения.РеализацияШапка, Отказ);
	
	Документы.РеализацияТоваровУслуг.ДобавитьКолонкуСодержание(ТаблицыРеализация.СобственныеТоварыУслуги);
	
	// Таблица остаточной стомиости ОС
	ТаблицаОстаточнойСтоимостиОС = УчетОС.ПодготовитьТаблицуОстаточнойСтоимостиГосРегистрация(
		ПараметрыПроведения.СписаниеОстаточнойСтоимостиОСТаблица,
		ПараметрыПроведения.СписаниеОстаточнойСтоимостиОСРеквизиты, Отказ);
	
	// Структура таблиц для отражения в налоговом учете УСН
	СтруктураТаблицУСН = Новый Структура("ТаблицаТМЦ, ТаблицаРасчетов", ТаблицаСписанныеТовары, ТаблицаВзаиморасчетов);
	
	// Учет доходов и расходов ИП
	СписанныеМПЗ = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуСписанныеМПЗ(
		ТаблицаСписанныеТовары, ПараметрыПроведения.РеализацияТаблицаДокумента, ПараметрыПроведения.РеализацияШапка);
	
	ТаблицыСписанияТоваровИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыСписанияМПЗ(
		СписанныеМПЗ, ПараметрыПроведения.РеализацияШапка, Отказ);
	ТаблицаОказаниеУслугИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОказаниеУслуг(
		ТаблицыРеализация.СобственныеТоварыУслуги, ПараметрыПроведения.РеализацияШапка);
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	УчетТоваров.СформироватьДвиженияСписаниеТоваров(
		ТаблицаСписанныеТовары, ПараметрыПроведения.РеализацияШапка, Движения, Отказ);
	
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(
		ТаблицаВзаиморасчетов, ПараметрыПроведения.ЗачетАвансовШапка, Движения, Отказ);
	
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(
		ТаблицаВзаиморасчетовПлатежногоАгента, ПараметрыПроведения.ЗачетАвансовШапка, Движения, Отказ);
	
	// Перенос задолженности при оплате курьеру
	ТаблицаВзаиморасчетовПереносЗадолженности = Документы.РеализацияТоваровУслуг.ПодготовитьТаблицуПереносаЗадолженности(
		ТаблицаВзаиморасчетов, 
		ПараметрыПроведения.РеквизитыПереносЗадолженностиДебиторскаяЗадолженность,
		Отказ);
	
	УчетВзаиморасчетов.СформироватьДвиженияПереносЗадолженности(
		ТаблицаВзаиморасчетовПереносЗадолженности,
		ПараметрыПроведения.РеквизитыПереносЗадолженностиДебиторскаяЗадолженность, Движения, Отказ);
	
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансовКомитентов(
		ТаблицыРеализация.ТоварыУслугиКомитентов, ПараметрыПроведения.ЗачетАвансовШапка, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияРеализация(
		ТаблицыРеализация.СобственныеТоварыУслуги, ТаблицыРеализация.ТоварыУслугиКомитентов, 
		ТаблицыРеализация.РеализованныеТоварыКомитентов, ПараметрыПроведения.РеализацияШапка, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияПереоценкаТоваровВРознице(
		ПараметрыПроведения.ПереоценкаВРозницеТовары, ТаблицаСписанныеТовары, 
		ПараметрыПроведения.ПереоценкаВРозницеШапка, Движения, Отказ);
		
	РегистрыНакопления.РеализацияУслуг.ДобавитьДвижения(
		Движения.РеализацияУслуг,
		ПараметрыПроведения.ТаблицаРеализацияУслуг,
		ТаблицыРеализация.СобственныеТоварыУслуги,
		ПараметрыПроведения.РеализацияШапка);
		
	УчетОС.СформироватьДвиженияСписаниеОстаточнойСтоимостиОСГосРегистрация(
		ТаблицаОстаточнойСтоимостиОС,
		ПараметрыПроведения.СписаниеОстаточнойСтоимостиОСРеквизиты,
		Движения, Отказ);  
		
	УчетОС.ОпределитьСчетаУчетаОС(ТаблицыРеализация.СобственныеТоварыУслуги, Дата, Организация, "Номенклатура");
	
	// Учет НДС
	УчетНДС.СформироватьДвиженияРеализацияОтгруженныхТоваров(
		ТаблицыРеализация.СобственныеТоварыУслуги, ПараметрыПроведения.НДСТовары, ТаблицаСписанныеТовары,  
		ПараметрыПроведения.РеализацияШапка, Движения, Отказ);

	// Формирует движения по раздельному учету НДС
	УчетНДСРаздельный.СформироватьДвиженияРеализацияОтгруженныхТоваров(
		ПараметрыПроведения.НДСТовары, ТаблицаСписанныеТовары,
		ПараметрыПроведения.РеализацияШапка, Движения, Отказ);
		
	// Погашение задолженности по отгрузке
	УчетВзаиморасчетов.СформироватьДвиженияПогашениеЗадолженностиПоОтгрузке(ТаблицаВзаиморасчетов, 
		ПараметрыПроведения.РеализацияШапка, Движения, Отказ);
	
	// Движения регистра "Рублевые суммы документов в валюте"
	УчетНДСБП.ПодготовитьТаблицуРублевыеСуммыПоОтгрузкеВУЕ(ТаблицыРеализация.СобственныеТоварыУслуги, 
		ПараметрыПроведения.РеализацияШапка);
	
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ТаблицыРеализация.СобственныеТоварыУслуги, 
		ПараметрыПроведения.РеализацияШапка, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ТаблицыРеализация.ТоварыУслугиКомитентов, 
		ПараметрыПроведения.РеализацияШапка, Движения, Отказ);
		
	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект, СтруктураТаблицУСН);
		
	// Учет доходов и расходов ИП
	ТаблицаИПМПЗОтгруженные	= УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияСписаниеМПЗ(
		ТаблицыСписанияТоваровИП,
		ПараметрыПроведения.РеализацияШапка, Движения, Отказ);
		
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияЗачетОплатыПокупателя(
		ТаблицаИПМПЗОтгруженные, Документы.РеализацияОтгруженныхТоваров.КорректировкаДокументовРасчетаАвансовИП(ТаблицаВзаиморасчетов, Организация),
		ПараметрыПроведения.РеализацияШапка, Движения, Отказ);
		
	// ПЕРЕОЦЕНКА ВАЛЮТНЫХ ОСТАТКОВ - после формирования проводок всеми другими механизмами
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкаВалютныхОстатковПоПроводкамДокумента(
		ПараметрыПроведения.ПереоценкаВалютныхОстатков, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(
		ТаблицаПереоценка, ПараметрыПроведения.ПереоценкаВалютныхОстатков, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияРасчетПереоценкиВалютныхСредств(
		ТаблицаПереоценка, ПараметрыПроведения.ПереоценкаВалютныхОстатков, Движения, Отказ);
	
	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(
		ТаблицаПереоценка, ПараметрыПроведения.ПереоценкаВалютныхОстатков, Движения, Отказ);

	// Отложенные расчеты с контрагентами.
	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентами(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);

	// Регистрация в последовательности.
	Документы.РеализацияОтгруженныхТоваров.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(ЭтотОбъект, Отказ,
		ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение,
		ТаблицаСписанныеТовары);

	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
		
	Движения.Записать();
	
	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект);	
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
		
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект);	
	ПараметрыДействия.СостояниеФлага = Ложь;
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);
	
	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИнформацияОДубляхРеализацииОтгруженныхТоваров()
	
	Результат = Новый Структура("ЕстьДубли, СообщениеПользователю");
	
	ЕстьДубли = Ложь;
	СообщенияПользователю = Новый Массив;
	
	// При отключении контроля отрицательных остатков возможен ввод нескольких документов
	// реализации к одной отгрузке, в случае поэтапного перехода права собственности 
	// (сценарий не автоматизирван, проводки необходимо скорректировать вручную).
	Если БухгалтерскийУчетПереопределяемый.ОтключитьКонтрольОтрицательныхОстатков()
		ИЛИ ЧастичнаяПродажа Тогда
		Результат.ЕстьДубли             = ЕстьДубли;
		Результат.СообщениеПользователю = "";
		Возврат Результат;
	КонецЕсли;
	
	СуществующиеРеализации = НайтиРеализацииПоДокументуОтгрузки();
	Для Каждого СтрокаТЗ Из СуществующиеРеализации Цикл
		ТекстСообщения = СтрШаблон(
			НСтр("ru='На основании документа %1 уже введена полная %2'"),
			ДокументОтгрузки,
			СтрокаТЗ.Реализация);
		СообщенияПользователю.Добавить(ТекстСообщения);
		ЕстьДубли = Истина;
	КонецЦикла;
		
	СообщениеПользователю = СтрСоединить(СообщенияПользователю, ". ");
	
	Результат.ЕстьДубли             = ЕстьДубли;
	Результат.СообщениеПользователю = СообщениеПользователю;
	
	Возврат Результат;
	
КонецФункции

Функция НайтиРеализацииПоДокументуОтгрузки()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОтгрузки", ДокументОтгрузки);
	Запрос.УстановитьПараметр("ЭтаРеализация",    Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияОтгруженныхТоваров.Ссылка КАК Реализация
	|ИЗ
	|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
	|ГДЕ
	|	РеализацияОтгруженныхТоваров.ДокументОтгрузки = &ДокументОтгрузки
	|	И НЕ РеализацияОтгруженныхТоваров.Ссылка = &ЭтаРеализация
	|	И РеализацияОтгруженныхТоваров.Проведен
	|	И НЕ РеализацияОтгруженныхТоваров.ЧастичнаяПродажа";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаполнитьПоДокументуОснованию(Основание)
	
	ДокументРеализации = Документы.РеализацияОтгруженныхТоваров.ЧастичнаяПродажаПоДокументуОтгрузки(Основание, Ссылка);
	ЧастичнаяПродажа = ЗначениеЗаполнено(ДокументРеализации);
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Если Основание.ВидОперации <> Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности Тогда
			ВызватьИсключение НСтр("ru='Документ ""Реализация (акт, накладная, УПД)"" должен иметь вид операции ""Отгрузка без перехода права собственности"".'");
		КонецЕсли;
		
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
		ДокументОтгрузки 				= Основание;
		СчетУчетаРасчетовСКонтрагентом 	= Основание.СчетУчетаРасчетовСКонтрагентом;
		СчетУчетаРасчетовПоАвансам 		= Основание.СчетУчетаРасчетовПоАвансам;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПередачаОС") Тогда
		
		Если НЕ Основание.ПравоСобственностиПереходитПослеГосРегистрации Тогда
			ВызватьИсключение НСтр("ru='В документе ""Передача ОС"" должен быть установлен флаг ""Право собственности переходит после государственной регистрации"".'");
		КонецЕсли;
		
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
		ДокументОтгрузки 				= Основание;
		СчетУчетаРасчетовСКонтрагентом 	= Основание.СчетУчетаРасчетовСКонтрагентом;
		СчетУчетаРасчетовПоАвансам 		= Основание.СчетУчетаРасчетовПоАвансам;
		
    Иначе
        Возврат;
    КонецЕсли;
	
	СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.Автоматически;
	
    СчетУчетаРасчетовСКонтрагентом = Основание.СчетУчетаРасчетовСКонтрагентом;
    Если НЕ ЗначениеЗаполнено(СчетУчетаРасчетовСКонтрагентом) Тогда
        СчетаУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
        СчетУчетаРасчетовСКонтрагентом = СчетаУчета.СчетРасчетовПокупателя;
    КонецЕсли;
    
    СчетУчетаРасчетовПоАвансам = Основание.СчетУчетаРасчетовПоАвансам;
    Если НЕ ЗначениеЗаполнено(СчетУчетаРасчетовПоАвансам) Тогда
        СчетаУчета = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
        СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетАвансовПокупателя;
    КонецЕсли;

КонецПроцедуры

Функция ТребуетсяУчетАвансов()
	
	НачислятьНДСПоОтгрузке = Ложь;
	Если ЗначениеЗаполнено(ДокументОтгрузки) Тогда
		ДатаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОтгрузки, "Дата");
		
		НачислятьНДСПоОтгрузке = ?(ТипЗнч(ДокументОтгрузки) = Тип("ДокументСсылка.ПередачаОС"),
			УчетнаяПолитика.НачислятьНДСПриПередачеНедвижимости(Организация, ДатаОснования),
			(УчетнаяПолитика.НачислятьНДСПоОтгрузке(Организация, ДатаОснования)
			И УчетНДСПереопределяемый.ПолучитьНДСДокумента(ДокументОтгрузки, "Услуги")=0));
	КонецЕсли;
	
	ОсобенностиЗачетаАванса = Документы.РеализацияОтгруженныхТоваров.ОсобенностиЗачетаАванса(
		ДоговорКонтрагента,
		ДокументОтгрузки,
		СпособЗачетаАвансов,
		НачислятьНДСПоОтгрузке);
		
	Возврат ОсобенностиЗачетаАванса.ТребуетсяУчетАвансов;
	
КонецФункции

#КонецОбласти

#КонецЕсли