#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ФизическиеЛица.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.Премия;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Приказ о поощрении сотрудников (Т-11а).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Отчет.ПечатнаяФормаТ11";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т11а";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о поощрении сотрудников (Т-11а)'");
	
	// Приказ о поощрении сотрудника (Т-11).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Отчет.ПечатнаяФормаТ11";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т11";
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВсехПриказов", Истина);
	КомандаПечати.Представление = НСтр("ru = 'Приказы на каждого сотрудника в отдельности (Т-11)'");
	КомандаПечати.ДополнительныеПараметры.Вставить("РеквизитыДетализации", "РаботаСотрудник");
	
КонецПроцедуры	

Процедура ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента) Экспорт
	
	ЗарплатаКадры.ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента, "ПериодРегистрации");
	
КонецПроцедуры

Процедура ЗаполнитьДатыЗапрета(ПараметрыОбновления) Экспорт
	
	ОбновлениеВыполнено = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 100
		|	Премия.Ссылка КАК Ссылка,
		|	Премия.Дата КАК Дата
		|ИЗ
		|	Документ.Премия КАК Премия
		|ГДЕ
		|	Премия.ДатаЗапрета = ДАТАВРЕМЯ(1, 1, 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Премия.Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ОбновлениеВыполнено = Ложь;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
				ПараметрыОбновления, Выборка.Ссылка.Метаданные().ПолноеИмя(), "Ссылка", Выборка.Ссылка) Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
			
			МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Ссылка);
			МенеджерДокумента.ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента);
			
			ОбъектДокумента.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектДокумента);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбновлениеВыполнено);
	
КонецПроцедуры

// Производит перерасчет НДФЛ и взносов по списку сотрудников.
//
//  Объект      - ДанныеФормыСтруктура.
//  Сотрудники  - Массив, сотрудники, по которым требуется провести перерасчет.
//  НДФЛ        - Булево, признак пересчета только НДФЛ.
//  Взносы      - Булево, признак пересчета только взносов.
//
Процедура ОбновитьНДФЛСотрудников(Объект, Сотрудники) Экспорт
	
	Если Объект.КорректироватьНДФЛ Тогда
		// Не пересчитываем, т.к. и НДФЛ в режиме корректировки.
		Возврат;
	КонецЕсли;
	
	// Составляем временные таблицы для расчета НДФЛ и взносов.
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РасчетЗарплатыБазовый.СоздатьВТДляРасчетаНДФЛиВзносов(МенеджерВременныхТаблиц, Объект, Сотрудники, "ПериодРегистрации", "ВидПремии");
	
	// Составляем массив физических лиц.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо
	|ИЗ
	|	ВТФизическиеЛица КАК ФизическиеЛица";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ФизическиеЛицаМассив = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	
	ФизическиеЛицаМассивНДФЛ = Новый Массив;
	
	Для Каждого ФизическоеЛицо ИЗ ФизическиеЛицаМассив Цикл
		ФиксРасчет = Объект.НДФЛ.НайтиСтроки(Новый Структура("ФизическоеЛицо, ФиксРасчет", ФизическоеЛицо, Истина));
		Если ФиксРасчет.Количество() = 0 Тогда
			ФиксРасчет = Объект.КорректировкиВыплаты.НайтиСтроки(Новый Структура("ФизическоеЛицо, ФиксРасчет", ФизическоеЛицо, Истина));
		КонецЕсли;
		Если ФиксРасчет.Количество() = 0 Тогда
			ФизическиеЛицаМассивНДФЛ.Добавить(ФизическоеЛицо);
		КонецЕсли;
	КонецЦикла;
	
	// Рассчитываем НДФЛ и взносы
	ДатаОперации = Мин(Объект.Дата, КонецДня(Объект.ПериодРегистрации));
	
	НачатьТранзакцию();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Объект.КорректироватьНДФЛ Тогда
		Если ФизическиеЛицаМассивНДФЛ.Количество() > 0 Тогда
			ОтборСтрок = Новый Структура("ФизическоеЛицо", ФизическиеЛицаМассивНДФЛ);
			РезультатРасчетаНДФЛ = РасчетЗарплатыБазовый.РезультатРасчетаНДФЛ(МенеджерВременныхТаблиц, Объект, ДатаОперации, Истина, "ПериодРегистрации");
			СдвигИдентификатора = УчетНДФЛФормы.МаксимальныйИдентификаторСтрокиНДФЛ(Объект.НДФЛ) + 1;
			// Перенумеруем строки новых коллекций.
			УчетНДФЛФормы.НазначитьИдентификаторыНовымСтрокамТаблицамНДФЛИПримененныеВычетыНаДетейИИмущественные(
				СдвигИдентификатора, РезультатРасчетаНДФЛ.НДФЛ, РезультатРасчетаНДФЛ.ПримененныеВычетыНаДетейИИмущественные);
			// Выполняем замену прежних строк на новые.
			РасчетЗарплаты.ЗаменитьСтрокиНаНовыеДанные(Объект.НДФЛ, РезультатРасчетаНДФЛ.НДФЛ, "ФизическоеЛицо", , ОтборСтрок);
			// Обновление строк НДФЛ
			ОтборСтрок = Новый Структура("ИдентификаторСтрокиНДФЛ", ОбщегоНазначения.ВыгрузитьКолонку(РезультатРасчетаНДФЛ.НДФЛ, "ИдентификаторСтрокиНДФЛ", Истина));
			РасчетЗарплаты.ЗаменитьСтрокиНаНовыеДанные(Объект.ПримененныеВычетыНаДетейИИмущественные, РезультатРасчетаНДФЛ.ПримененныеВычетыНаДетейИИмущественные, "ИдентификаторСтрокиНДФЛ", , ОтборСтрок);
			// Обновление строк корректировки выплат
			ОтборСтрок = Новый Структура("ФизическоеЛицо", ФизическиеЛицаМассивНДФЛ);
			РезультатРасчетаКорректировкиВыплаты = РасчетЗарплатыБазовый.РезультатРасчетаКорректировкиВыплаты(Объект, "ПериодРегистрации", ФизическиеЛицаМассивНДФЛ);
			РасчетЗарплаты.ЗаменитьСтрокиНаНовыеДанные(Объект.КорректировкиВыплаты, РезультатРасчетаКорректировкиВыплаты, "ФизическоеЛицо", , ОтборСтрок);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОтменитьТранзакцию();
	
КонецПроцедуры

// Процедура инициирует перерасчет НДФЛ конкретного физического лица.
// Вызывается из вспомогательной формы документа
//
// Параметры:
//  Объект        - ДанныеФормыСтруктура
//  ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//
Процедура ПересчитатьНДФЛ(Объект, ФизическоеЛицо) Экспорт
	
	СотрудникиОрганизации = СотрудникиФизическогоЛица(Объект, ФизическоеЛицо);
	Если СотрудникиОрганизации.Количество() > 0 Тогда
		ОбновитьНДФЛСотрудников(Объект, СотрудникиОрганизации.ВыгрузитьКолонку("Сотрудник"));
	КонецЕсли;
	
КонецПроцедуры

Функция СотрудникиФизическогоЛица(Объект, ФизическоеЛицо)
	
	Возврат СотрудникиФизическихЛиц(Объект, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
	
КонецФункции

Функция СотрудникиФизическихЛиц(Объект, ФизическиеЛица) Экспорт
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.Организация             = Объект.Организация;
	ПараметрыПолученияСотрудников.НачалоПериода           = Объект.ПериодРегистрации;
	ПараметрыПолученияСотрудников.ОкончаниеПериода        = КонецМесяца(Объект.ПериодРегистрации);
	ПараметрыПолученияСотрудников.СписокФизическихЛиц     = ФизическиеЛица;
	
	Возврат КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудников);
	
КонецФункции

Функция НовыеПараметрыМотиваПоощрения() Экспорт
	
	ПараметрыМотиваПоощрения = Новый Структура;
	ПараметрыМотиваПоощрения.Вставить("ТипПремии", Перечисления.ТипыПремии.ПустаяСсылка());
	ПараметрыМотиваПоощрения.Вставить("ВидПремии", ПланыВидовРасчета.Начисления.ПустаяСсылка());
	ПараметрыМотиваПоощрения.Вставить("ДатаНачалаБазовогоПериода", '00010101');
	ПараметрыМотиваПоощрения.Вставить("ДатаОкончанияБазовогоПериода", '00010101');
	
	Возврат ПараметрыМотиваПоощрения;
	
КонецФункции

Процедура СформироватьТекстМотиваПоощрения(Объект, АвтоТекстМотиваПоощрения, МотивПоощренияБылИзмененВручную) Экспорт
	
	ТекстМотиваПоощрения = Объект.МотивПоощрения;
	
	ПараметрыМотиваПоощрения = НовыеПараметрыМотиваПоощрения();
	ЗаполнитьЗначенияСвойств(ПараметрыМотиваПоощрения, Объект);
	ТекстМотиваПоощренияАвто = ТекстМотиваПоощрения(ПараметрыМотиваПоощрения);
	
	Если НЕ МотивПоощренияБылИзмененВручную ИЛИ ПустаяСтрока(Объект.МотивПоощрения) Тогда
		МотивПоощренияБылИзмененВручную = Ложь;
		Если ЗначениеЗаполнено(АвтоТекстМотиваПоощрения) Тогда
			// Менять только если значение назначения платежа не было ранее введено вручную
			ТекстМотиваПоощрения = АвтоТекстМотиваПоощрения;
		ИначеЕсли ЗначениеЗаполнено(ТекстМотиваПоощренияАвто) Тогда
			ТекстМотиваПоощрения = ТекстМотиваПоощренияАвто;
		Иначе
			МотивПоощренияБылИзмененВручную =
				Объект.ТипПремии = Перечисления.ТипыПремии.ПоРезультатамРаботы И НЕ ПустаяСтрока(ТекстМотиваПоощрения);
		КонецЕсли;
	Иначе
		АвтоТекстМотиваПоощрения = ТекстМотиваПоощренияАвто;
		МотивПоощренияБылИзмененВручную = МотивПоощренияИзмененВручную(СокрЛП(ТекстМотиваПоощрения), ТекстМотиваПоощренияАвто);
	КонецЕсли;
	
	Объект.МотивПоощрения = ТекстМотиваПоощрения;
	
КонецПроцедуры

Функция ТекстМотиваПоощрения(Параметры) Экспорт
	
	Если Параметры.ТипПремии = Перечисления.ТипыПремии.РазоваяПремия Тогда
		ТекстМотиваПоощрения = "";
	Иначе
		ТекстМотиваПоощрения = СтрШаблон(НСтр("ru = 'За выполнение плановых показателей за %1'"),
			Нрег(ПредставлениеПериода(НачалоДня(Параметры.ДатаНачалаБазовогоПериода),
				КонецДня(Параметры.ДатаОкончанияБазовогоПериода), "ФП = Истина")));
	КонецЕсли;
	
	Возврат ТекстМотиваПоощрения;
	
КонецФункции

Функция МотивПоощренияИзмененВручную(Знач МотивПоощрения, МотивПоощренияАвто) Экспорт
	
	Если ПустаяСтрока(МотивПоощрения) ИЛИ ПустаяСтрока(МотивПоощренияАвто) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат СокрЛП(МотивПоощрения) <> СокрЛП(МотивПоощренияАвто);
	
КонецФункции

#КонецОбласти

#КонецЕсли