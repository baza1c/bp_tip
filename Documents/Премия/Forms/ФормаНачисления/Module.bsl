
&НаКлиенте
Перем ТипНачисленияНовойСтроки;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Параметры.АдресПараметровВХранилище) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// "Распаковываем" параметры
	ПараметрыРасчета = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВХранилище);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		ПараметрыРасчета,
		"Ссылка, Организация, МесяцНачисления, Сотрудник, ФизическоеЛицо, ДопСведенияОСотруднике, ВидПремии, ДатаДокумента");
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	СписокКадровыхДанных = "ФамилияИО";
	КадровыеДанныеФизЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, ФизическиеЛица, СписокКадровыхДанных, МесяцНачисления);
	
	ПредставлениеСотрудника = КадровыеДанныеФизЛиц[0].ФамилияИО;
	
	Заголовок = СтрШаблон(НСтр("ru='Начисления: %1'"), ПредставлениеСотрудника);
	Если ЗначениеЗаполнено(ДопСведенияОСотруднике) Тогда
		Заголовок = СтрШаблон(НСтр("ru='%1 (%2)'"), Заголовок, ДопСведенияОСотруднике);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыРасчета.Подразделение) Тогда
		Подразделение = ПараметрыРасчета.Подразделение;
	Иначе
		Подразделение = Справочники.ПодразделенияОрганизаций.ОсновноеПодразделениеОрганизации(Организация);
	КонецЕсли;
	
	ЗаполнитьКонтролируемыеНачисления();
	
	ПараметрыФО = Новый Структура("Организация", Организация);
	ОрганизацияПрименяетРайонныйКоэффициент =
		ПолучитьФункциональнуюОпцию("ПрименятьРайонныйКоэффициент", ПараметрыФО);
	ОрганизацияПрименяетСевернуюНадбавку =
		ПолучитьФункциональнуюОпцию("ПрименятьСевернуюНадбавку", ПараметрыФО);
	
	УстановитьПараметрыВыбораНачислений();
	
	КатегорииДоходовАУСН.Добавить(Перечисления.КатегорииДоходовНДФЛ.ОплатаТрудаАУСН);
	КатегорииДоходовАУСН.Добавить(Перечисления.КатегорииДоходовНДФЛ.ПрочиеДоходыАУСН);
	
	ЗаполнитьТаблицуНачислений(ПараметрыРасчета.МассивСтрок);
	ПеренумероватьСтроки(ЭтотОбъект);
	ПолучитьИтоги(ЭтотОбъект);
	
	Элементы.ГруппаКнопокПросмотр.Видимость       = ТолькоПросмотр;
	Элементы.ГруппаКнопокРедактирование.Видимость = НЕ ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ФормаОК.КнопкаПоУмолчанию      = Истина;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		ВопросСохранитьИзменения(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыНачисления

&НаКлиенте
Процедура НачисленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ДанныеТекущейСтроки = Элементы.Начисления.ТекущиеДанные;
	
	Если НЕ Копирование И НоваяСтрока Тогда
		ДанныеТекущейСтроки = Элементы.Начисления.ТекущиеДанные;
		ДанныеТекущейСтроки.ФизическоеЛицо         = ФизическоеЛицо;
		ДанныеТекущейСтроки.Сотрудник              = Сотрудник;
		ДанныеТекущейСтроки.Подразделение          = Подразделение;
		ДанныеТекущейСтроки.НомерСтроки            = Начисления.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	НачисленияПриОкончанииРедактированияНаСервере();
	ПолучитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПослеУдаления(Элемент)
	
	НачисленияПриОкончанииРедактированияНаСервере();
	ПеренумероватьСтроки(ЭтотОбъект);
	ПолучитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияРезультатПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Начисления.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьИзменениеНачислений(Элементы.Начисления.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачислениеПриИзменении(Элемент)
	
	НачислениеПриПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачислениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПередУдалением(Элемент, Отказ)
	
	ДанныеСтроки = Элементы.Начисления.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		Если ДанныеСтроки.ЭтоПремия Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ПроверитьЗаполнениеРеквизитов() Тогда
		ПеренестиИзмененияВОбъектФормыВладельца();
		Закрыть(Сотрудник);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛисток(Команда)
	
	Если Модифицированность Тогда
		ВопросСохранитьИзменения(Ложь);
	Иначе
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьКонтролируемыеНачисления()
	
	СтруктураОтбора = Новый Структура("КатегорияНачисленияИлиНеоплаченногоВремени");
	
	СтруктураОтбора.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент;
	НачисленияРК = Новый ФиксированныйМассив(ПланыВидовРасчета.Начисления.НачисленияПоОтбору(СтруктураОтбора));
	
	СтруктураОтбора.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка;
	НачисленияСН = Новый ФиксированныйМассив(ПланыВидовРасчета.Начисления.НачисленияПоОтбору(СтруктураОтбора));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуНачислений(МассивСтрок)
	
	СдвигПорядка = 1;
	
	Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
		НоваяСтрока = Начисления.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМассива);
		Если Не ЗначениеЗаполнено(НоваяСтрока.Начисление) Тогда
			НоваяСтрока.Начисление = ВидПремии;
			НоваяСтрока.ЭтоПремия = Истина;
		КонецЕсли;
		
		НоваяСтрока.ЭтоРКСН = НачисленияРК.Найти(НоваяСтрока.Начисление) <> Неопределено
			Или НачисленияСН.Найти(НоваяСтрока.Начисление) <> Неопределено;
		
		Если НоваяСтрока.Начисление = ВидПремии Тогда
			НоваяСтрока.Порядок = 1;
		ИначеЕсли НачисленияРК.Найти(НоваяСтрока.Начисление) <> Неопределено Тогда
			НоваяСтрока.Порядок = 300;
		ИначеЕсли НачисленияСН.Найти(НоваяСтрока.Начисление) <> Неопределено Тогда
			НоваяСтрока.Порядок = 400;
		Иначе
			НоваяСтрока.Порядок = 200 + СдвигПорядка;
		КонецЕсли;
		
		СдвигПорядка = СдвигПорядка + 1;
		
	КонецЦикла;
	
	Начисления.Сортировать("Порядок");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПолучитьИтоги(Форма)
	
	Форма.ВсегоНачислено =
		Форма.Начисления.Итог("Результат");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НачисленияНачисление");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Начисления.ЭтоПремия", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстПредопределенногоЗначения);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПеренумероватьСтроки(Форма)
	
	Для Сч = 0 По Форма.Начисления.Количество() - 1 Цикл
		СтрокаТаблицы = Форма.Начисления[Сч];
		СтрокаТаблицы.НомерСтроки = Сч + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НачисленияПриОкончанииРедактированияНаСервере()
	
	ИдентификаторСтроки = Элементы.Начисления.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	СтрокаНачислений = Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если СтрокаНачислений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаНачислений.Сотрудник) Тогда
		СтрокаНачислений.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаНачислений.Сотрудник, "ФизическоеЛицо");
		ОбновляемыеФизическиеЛица = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаНачислений.ФизическоеЛицо));
		
		Если НЕ ЗначениеЗаполнено(СтрокаНачислений.Подразделение) Тогда
			Если ЗначениеЗаполнено(Подразделение) Тогда
				СтрокаНачислений.Подразделение = Подразделение;
			Иначе
				ЗначенияПоУмолчанию = Новый Структура("Организация, Подразделение");
				ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗначенияПоУмолчанию);
				СтрокаНачислений.Подразделение = ЗначенияПоУмолчанию.Подразделение;
			КонецЕсли;
		КонецЕсли;
	Иначе
		СтрокаНачислений.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	КонецЕсли;
	
	ОбработатьИзменениеНачислений(ИдентификаторСтроки);
	
КонецПроцедуры

&НаСервере
Процедура НачислениеПриПриИзмененииНаСервере()
	
	ИдентификаторСтроки = Элементы.Начисления.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	СтрокаНачислений = Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если СтрокаНачислений = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДанныеНачисления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
											СтрокаНачислений.Начисление,
											"КодДоходаНДФЛ,КатегорияДохода,КатегорияНачисленияИлиНеоплаченногоВремени,ВходитВБазуРКИСН");
	
	КатегорияНачисленияИлиНеоплаченногоВремени = ДанныеНачисления.КатегорияНачисленияИлиНеоплаченногоВремени;
	Если КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда
		Или КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее
		И ДанныеНачисления.ВходитВБазуРКИСН Тогда
		СтрокаНачислений.ВходитВБазуРКиСН = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеНачислений(ИдентификаторСтроки)
	
	СтрокаНачисления = Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если СтрокаНачисления.ВходитВБазуРКиСН Тогда
		ПересчитатьНачисленияСКоэффициентом(ИдентификаторСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНачисленияСКоэффициентом(ИдентификаторСтроки)
	
	СтрокаИнициаторПересчета = Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ПересчитатьНачисленияСКоэффициентом(СтрокаИнициаторПересчета.Сотрудник, Начисления);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьИзмененияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ПроверитьЗаполнениеРеквизитов() Тогда
			ПеренестиИзмененияВОбъектФормыВладельца();
			Если ДополнительныеПараметры.Закрытие Тогда
				Закрыть(Сотрудник);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Если ДополнительныеПараметры.Закрытие Тогда
			Модифицированность = Ложь;
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ДополнительныеПараметры.Закрытие
		И НЕ Результат = КодВозвратаДиалога.Отмена Тогда
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеРеквизитов()
	
	ПроверкаПройдена = Истина;
	ДоговорыПодряда  = Новый Массив;
	Для Каждого СтрокаТаблицы ИЗ Начисления Цикл
		ИндексСтроки = Начисления.Индекс(СтрокаТаблицы);
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Начисление) И Не СтрокаТаблицы.ЭтоПремия Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",
				"Заполнение",
				"Начисление",
				ИндексСтроки + 1,
				"Начисления");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Начисления", ИндексСтроки+1, "Начисление"));
			ПроверкаПройдена = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПроверкаПройдена;
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчет()
	
	РасчетныйЛисток = ВладелецФормы.РасчетныйЛистокНаСервере("Начисления", Сотрудник);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Отчет.АнализНачисленийИУдержаний",
		"РасчетныйЛистокПодробнее",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка),
		ЭтотОбъект,
		Новый Структура("ПечатаемыйДокумент, ЗаголовокФормы",
			РасчетныйЛисток, НСтр("ru='Расчетный листок'")));
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьИзменения(Закрытие)
	
	ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
	Оповещение = Новый ОписаниеОповещения("ВопросСохранитьИзмененияЗавершение", ЭтотОбъект, Новый Структура("Закрытие", Закрытие));
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораНачислений()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ФормаДокументаУстановитьПараметрыВыбораНачислений(
			ЭтотОбъект, Организация, МесяцНачисления);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Массив(Элементы.НачисленияНачисление.ПараметрыВыбора);
	
	МассивКатегорий = Новый Массив;
	МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент);
	МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка);
	
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.КатегорияНачисленияИлиНеоплаченногоВремени",
		Новый ФиксированныйМассив(МассивКатегорий)));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НачисленияНачисление",
		"ПараметрыВыбора",
		Новый ФиксированныйМассив(ПараметрыВыбора));
	
КонецПроцедуры

#Область ПереносВОсновнуюФорму

&НаКлиенте
Процедура ПеренестиИзмененияВОбъектФормыВладельца()
	
	Если Модифицированность Тогда
		Оповестить("ИзмененыРезультатыРачетаНачисления", ПоместитьИзмененныеДанныеВоВременноеХранилище(), ЭтотОбъект);
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьИзмененныеДанныеВоВременноеХранилище()
	
	ВозвращаемыеСведения = Новый Структура;
	
	ДанныеНачислений = Начисления.Выгрузить();
	ДанныеНачислений.ЗаполнитьЗначения(Сотрудник,      "Сотрудник");
	ДанныеНачислений.ЗаполнитьЗначения(ФизическоеЛицо, "ФизическоеЛицо");
	
	НачисленияПоПремиям   = ДанныеНачислений.Скопировать(Новый Структура("ЭтоПремия", Истина));
	НачисленияПоНадбавкам = ДанныеНачислений.Скопировать(Новый Структура("ЭтоПремия", Ложь));
	
	ВозвращаемыеСведения.Вставить("Начисления",          НачисленияПоПремиям);
	ВозвращаемыеСведения.Вставить("ЗависимыеНачисления", НачисленияПоНадбавкам);
	ВозвращаемыеСведения.Вставить("ФизическоеЛицо",      ФизическоеЛицо);
	ВозвращаемыеСведения.Вставить("Сотрудник",           Сотрудник);
	
	Возврат ПоместитьВоВременноеХранилище(ВозвращаемыеСведения, Новый УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

#КонецОбласти
