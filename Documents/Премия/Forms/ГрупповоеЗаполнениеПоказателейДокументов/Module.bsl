
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоказателя = Параметры.ИмяПоказателя;
	ПремияПроцентом = ИмяПоказателя = "ПроцентПремии";
	Элементы.ГруппаСуммаПремии.Видимость   = Не ПремияПроцентом И Параметры.ФиксированноеЗначение;
	Элементы.ГруппаПроцентПремии.Видимость = ПремияПроцентом И Не Параметры.ФиксированноеЗначение;
	ЭтотОбъект[ИмяПоказателя] = Параметры.ЗначениеПоказателя;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если ПремияПроцентом Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СуммаПремии");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ПроцентПремии");
	КонецЕсли;
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	НачисленияПоказатели = ПолучитьПоказателиНаСервере();
	Оповестить("ИзмененыПоказателиДокументаПремия", НачисленияПоказатели, ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПоказателиНаСервере()
	
	ЗначенияПоказателей = Новый Соответствие;
	ЗначенияПоказателей.Вставить(ИмяПоказателя, ЭтотОбъект[ИмяПоказателя]);
	АдресОбщихПоказателей = ПоместитьВоВременноеХранилище(ЗначенияПоказателей, Новый УникальныйИдентификатор);
	НачисленияПоказатели = Новый Структура("АдресОбщихПоказателей", АдресОбщихПоказателей);
	
	Возврат НачисленияПоказатели;
	
КонецФункции

#КонецОбласти
