#Область ОписаниеПеременных

&НаКлиенте
Перем СотрудникиКРасчету Экспорт;

&НаКлиенте
Перем СтрокиКРасчету Экспорт;

&НаКлиенте
Перем ЗакрытьПослеЗаписи;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры,"ЗначениеКопирования")) Тогда
			Если НачалоМесяца(Объект.Дата) <> НачалоМесяца(Объект.ПериодРегистрации) Тогда
				Объект.ПериодРегистрации = НачалоМесяца(Объект.Дата);
			КонецЕсли;
			ДополнитьТаблицуНачисления();
			ОбновляемыеФизическиеЛица = Неопределено;
			ОбновляемыеСотрудники = Новый ФиксированныйМассив(ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Сотрудник"));
			ОбновитьСтрокуСотрудника();
		Иначе

			// Создается новый документ
			ЗначенияДляЗаполнения = Новый Структура;
			ЗначенияДляЗаполнения.Вставить("Месяц", "Объект.ПериодРегистрации");
			ЗначенияДляЗаполнения.Вставить("Организация", "Объект.Организация");
			ЗначенияДляЗаполнения.Вставить("Ответственный", "Объект.Ответственный");
			ЗначенияДляЗаполнения.Вставить("Подразделение", "Объект.Подразделение");
			
			ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
			
			Если НачалоМесяца(Объект.Дата) <> НачалоМесяца(Объект.ПериодРегистрации) Тогда
				Объект.ПериодРегистрации = НачалоМесяца(Объект.Дата);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Объект.ВидПремии) Тогда
				УстановитьВидПремии();
				Объект.ДатаНачалаБазовогоПериода    = Объект.ПериодРегистрации;
				Объект.ДатаОкончанияБазовогоПериода = КонецМесяца(Объект.ПериодРегистрации);
				УстановитьВидПремии();
			КонецЕсли;
			ЗаполнитьДатыНачислений(ЭтотОбъект);
		КонецЕсли;
		
		ПриПолученииДанныхНаСервере();
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			МодульРасчетЗарплатыДляНебольшихОрганизаций.СформироватьТабличнуюЧастьСотрудникиПремии(Объект);
		КонецЕсли;
		
		СформироватьМотивПоощренияНаСервере();
		
		ОбновитьПланируемуюДатуВыплаты();
		
		УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
		
	КонецЕсли;
	
	Если ТолькоПросмотр Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СотрудникиГруппаДобавитьНачисленияУдержания",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СотрудникиПодбор",
			"Доступность",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЗаполнитьПоказатели",
			"Видимость",
			Ложь);
		
	Иначе
		// Часть команд доступна только при наличии соответствующих прав
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДобавитьУдержание",
			"Видимость",
			ПравоДоступа("Редактирование", Метаданные.ПланыВидовРасчета.Удержания));
	КонецЕсли;
	
	ЗаполнитьСписокМотивовПоощрения();
	УстановитьУсловноеОформление();
	УстановитьДоступностьСортировкиИЗаполнения(ЭтотОбъект);
	
	Элементы.ПериодРегистрации.Видимость = НачалоМесяца(Объект.Дата) <> НачалоМесяца(Объект.ПериодРегистрации);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПользовательУправляетСчетамиУчета =
		СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	Элементы.БухучетЗарплатыСотрудниковПредставление.Видимость = ПользовательУправляетСчетамиУчета;
	ОбновитьПредставлениеУчетаРасходов(ЭтотОбъект);
	
	РассчитыватьДокументыПриРедактировании = Константы.РассчитыватьДокументыПриРедактировании.Получить();
	Если РассчитыватьДокументыПриРедактировании Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПересчитать",
			"Видимость",
			Ложь);
			
	КонецЕсли;
	
	УстановитьОтображениеЭлементовФормы();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ПриСозданииФормыДокументаПремия(ЭтотОбъект);
	КонецЕсли;
	
	ЗапомнитьРеквизитыШапки(Объект.ТипПремии);
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// ЭДОБЗК
	ЭДОБЗК.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка, Объект);
	// Конец ЭДОБЗК
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.Премия",
		"ФормаДокумента",
		НСтр("ru='Новости: Премии'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
КонецПроцедуры

&НаСервере
Процедура ЗапомнитьРеквизитыШапки(ТипПремии)
	
	РеквизитыШапкиПоТипуПремии =
		Новый Структура("Выплата, МотивПоощрения, ВидПремии, ДатаНачалаБазовогоПериода, ДатаОкончанияБазовогоПериода, МотивПоощренияБылИзмененВручную, КоличествоМесяцевБазовогоПериода");
	Если Не ЗначениеЗаполнено(РеквизитыШапкиПрежние) Тогда
		РеквизитыШапкиПрежниеВрем = Новый Соответствие;
		РеквизитыШапкиПрежниеВрем.Вставить(Перечисления.ТипыПремии.ПоРезультатамРаботы,
			ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(РеквизитыШапкиПоТипуПремии));
		РеквизитыШапкиПрежниеВрем.Вставить(Перечисления.ТипыПремии.РазоваяПремия,
			ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(РеквизитыШапкиПоТипуПремии));
	Иначе
		РеквизитыШапкиПрежниеВрем = Новый Соответствие(РеквизитыШапкиПрежние);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(РеквизитыШапкиПоТипуПремии, Объект);
	РеквизитыШапкиПоТипуПремии.МотивПоощренияБылИзмененВручную = МотивПоощренияБылИзмененВручную;
	РеквизитыШапкиПоТипуПремии.Выплата = ?(ТипПремии = Перечисления.ТипыПремии.РазоваяПремия, Неопределено, Выплата);
	РеквизитыШапкиПоТипуПремии.КоличествоМесяцевБазовогоПериода = ?(ТипПремии = Перечисления.ТипыПремии.РазоваяПремия, 0, КоличествоМесяцевБазовогоПериода);
	РеквизитыШапкиПрежниеВрем.Вставить(ТипПремии, РеквизитыШапкиПоТипуПремии);
	
	РеквизитыШапкиПрежние = Новый ФиксированноеСоответствие(РеквизитыШапкиПрежниеВрем);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПриПолученииДанныхНаСервере();
	
	ДополнитьТаблицуНачисления();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьВторичныеДанныеТабличныхЧастей();
	
	УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ПриПолученииДанныхПремии(ЭтотОбъект);
	КонецЕсли;
	
	УстановитьОтображениеЭлементовФормы();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	// ЭДОБЗК
	ЭДОБЗК.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, Объект);
	// Конец ЭДОБЗК
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ЗакрытьПослеЗаписи = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если СотрудникиКРасчету.Количество() > 0 Тогда
		ПересчитатьДокументНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ПроведениеСервер.УстановитьПризнакПроверкиРеквизитов(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПослеЗаписиНаСервере(ЭтаФорма);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	ДополнитьТаблицуНачисления();
	ЗаполнитьВторичныеДанныеТабличныхЧастей();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ПриПолученииДанныхПремии(ЭтотОбъект);
	КонецЕсли;
	РассчитатьИтогиПоДокументу();
	
	// ЭДОБЗК
	ЭДОБЗК.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Объект);
	// Конец ЭДОБЗК
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_Премия", ПараметрыЗаписи, Объект.Ссылка);
	
	Если ЗакрытьПослеЗаписи Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыПоказателиДокументаПремия" И Источник.ВладелецФормы = ЭтотОбъект Тогда
		Если Параметр.Количество() > 0 Тогда 
			ОбработатьИзменениеПоказателейНаСервере(Параметр);
			СотрудникиКРасчету.Очистить();
			УстановитьОтображениеКнопкиПересчитать();
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ФормаКлиентскогоПриложения") И Источник.ВладелецФормы = ЭтотОбъект Тогда
		Если ИмяСобытия = "ИзмененыРезультатыРасчетаНДФЛ" Тогда
			ОбновитьДанныеНДФЛНаСервере(Параметр);
		ИначеЕсли ИмяСобытия = "ИзмененыРезультатыРачетаУдержания" Тогда
			ОбновитьДанныеУдержанийНаСервере(Параметр);
		ИначеЕсли ИмяСобытия = "ИзмененыРезультатыРачетаНачисления" Тогда
			ОбновитьДанныеНачисленийНаСервере(Параметр);
		ИначеЕсли ИмяСобытия = "ДобавленоУдержание" Тогда
			ДобавитьУдержаниеНаСервере(Параметр);
		КонецЕсли;
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭДОБЗК
	ЭДОБЗККлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭДОБЗК
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		Если НачалоМесяца(Объект.ПериодРегистрации) <> НачалоМесяца(Объект.Дата) Тогда
			Объект.ПериодРегистрации = НачалоМесяца(Объект.Дата);
			ПериодРегистрацииПриИзмененииНаСервере();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	ЗаполнитьКнопкиПодменю();
	
	УстановитьВидимостьЗависимыхНачислений();
	
	ОбновитьПланируемуюДатуВыплаты();
	
	ОчиститьТабличныеЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ОчиститьТабличныеЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПремииПриИзменении(Элемент)
	
	ТипПремииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыплатаПриИзменении(Элемент)
	
	УстановитьВидПремии();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Редактирование месяца строкой.

&НаКлиенте
Процедура МесяцНачисленияСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой", Модифицированность);
	ПериодРегистрацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("МесяцНачисленияСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой", , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт
	
	ПериодРегистрацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой", Направление, Модифицированность);
	ПодключитьОбработчикОжидания("ОбработчикОжиданияМесяцНачисленияПриИзменении", 0.3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияМесяцНачисленияПриИзменении()

	ПериодРегистрацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПериодРегистрацииПриИзмененииНаСервере()
	
	УстановитьОтветственныхЛиц();
	УстановитьФункциональныеОпцииФормы();
	ЗаполнитьКнопкиПодменю();
	ОбновитьПланируемуюДатуВыплаты();
	ПересчитатьНДФЛСотрудников();
	
КонецПроцедуры

&НаКлиенте
Процедура БазовыйПериодСтрокойОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура БазовыйПериодСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("БазовыйПериодСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НачалоПериода", Объект.ДатаНачалаБазовогоПериода);
	СтруктураПараметров.Вставить("КонецПериода",  Объект.ДатаОкончанияБазовогоПериода);
	СтруктураПараметров.Вставить("Кратность",     ПредопределенноеЗначение("Перечисление.Периодичность.Месяц"));
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", 
		СтруктураПараметров,
		ЭтотОбъект,
		,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура БазовыйПериодСтрокойНачалоВыбораЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДатаНачалаБазовогоПериода    = РезультатВыбора.НачалоПериода;
	Объект.ДатаОкончанияБазовогоПериода = РезультатВыбора.КонецПериода;
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(ЭтотОбъект,
		"БазовыйПериодСтрокой",
		ПредставлениеБазовогоПериодаСтрокой(Объект.ДатаНачалаБазовогоПериода, Объект.ДатаОкончанияБазовогоПериода));
		
	БазовыйПериодСтрокойНачалоВыбораЗавершениеНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеБазовогоПериодаСтрокой(НачалоПериода, КонецПериода)
	
	Возврат ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(КонецПериода), "ФП = Истина");
	
КонецФункции

// ЗарплатаКадрыПодсистемы.ПодписиДокументов
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент) 
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтаФорма, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент) 
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтаФорма, Элемент.Имя);
КонецПроцедуры
// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов

&НаКлиенте
Процедура ПланируемаяДатаВыплатыПриИзменении(Элемент)
	
	ПересчитатьНДФЛСотрудников();
	
КонецПроцедуры

&НаКлиенте
Процедура БухучетЗарплатыСотрудниковПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("УчетРасходовРедактированиеЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СпособОтраженияЗарплатыВБухучете", Объект.СпособОтраженияЗарплатыВБухучете);
	ПараметрыФормы.Вставить("Дата",                             Объект.Дата);
	ПараметрыФормы.Вставить("ТолькоПросмотр",                   ТолькоПросмотр);
	
	ОткрытьФорму(
		"Документ.Премия.Форма.ФормаБухучетЗарплаты",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
	
КонецПроцедуры

&НаКлиенте
Процедура МотивПоощренияПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.МотивПоощрения) Тогда
		МотивПоощренияБылИзмененВручную = Ложь;
	Иначе
		МотивПоощренияБылИзмененВручную = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСотрудники

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СотрудникиСотрудник" Тогда
		
		ПоказатьЗначение(, Элементы.Сотрудники.ТекущиеДанные.Сотрудник);
		
	ИначеЕсли Поле.Имя = "СотрудникиЗависимыеНачисления" Тогда
		
		ПерейтиКНачислениям();
		
	ИначеЕсли Поле.Имя = "СотрудникиНДФЛ" Тогда
		
		ПерейтиКНДФЛ();
		
	ИначеЕсли Поле.Имя = "СотрудникиПрочееУдержано" Тогда
		
		ПерейтиКУдержаниям();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Не ЗарплатаКадрыКлиент.ОрганизацияЗаполнена(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	ПодобратьСотрудников(Ложь)
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Сотрудники") Тогда
		ДобавляемыеСотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВыбранноеЗначение);
	Иначе
		ДобавляемыеСотрудники = ВыбранноеЗначение;
	КонецЕсли;
	
	ДобавитьСотрудников(ДобавляемыеСотрудники);
	ЗаполнитьНДФЛНаКлиенте();
	УстановитьДоступностьСортировкиИЗаполнения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	
	СформироватьСписокОбновляемыхСотрудников("Сотрудники", Элемент.ВыделенныеСтроки);
	СформироватьСписокОбновляемыхФизическихЛиц("Сотрудники", Элемент.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПослеУдаления(Элемент)
	СотрудникиПослеУдаленияНаСервере();
КонецПроцедуры

&НаСервере
Процедура СотрудникиПослеУдаленияНаСервере()
	
	Если ОбновляемыеСотрудники <> Неопределено И ОбновляемыеСотрудники.Количество() > 0 Тогда
		
		Для каждого ОбновляемыйСотрудник Из ОбновляемыеСотрудники Цикл
			
			СтрокиНачислений = Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник", ОбновляемыйСотрудник));
			Для каждого СтрокаНачисления Из СтрокиНачислений Цикл
				Объект.Начисления.Удалить(СтрокаНачисления);
			КонецЦикла;
			
			СтрокиНачислений = Объект.ЗависимыеНачисления.НайтиСтроки(Новый Структура("Сотрудник", ОбновляемыйСотрудник));
			Для каждого СтрокаНачисления Из СтрокиНачислений Цикл
				Объект.ЗависимыеНачисления.Удалить(СтрокаНачисления);
			КонецЦикла;
		
		КонецЦикла;
		
		ОбновляемыеСотрудники = Неопределено;
		
	КонецЕсли; 
	
	Если ОбновляемыеФизическиеЛица <> Неопределено И ОбновляемыеФизическиеЛица.Количество() > 0 Тогда
		
		Для каждого ОбновляемоеФизическоеЛицо Из ОбновляемыеФизическиеЛица Цикл
		
			СтрокиФизическогоЛица = Объект.Сотрудники.НайтиСтроки(Новый Структура("ФизическоеЛицо", ОбновляемоеФизическоеЛицо));
			Если СтрокиФизическогоЛица.Количество() = 0 Тогда
				
				УдаляемыеСтроки = Объект.Удержания.НайтиСтроки(Новый Структура("Сотрудник", ОбновляемоеФизическоеЛицо));
				Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
					Объект.Удержания.Удалить(УдаляемаяСтрока);
				КонецЦикла;
				
				УдаляемыеСтроки = Объект.НДФЛ.НайтиСтроки(Новый Структура("ФизическоеЛицо", ОбновляемоеФизическоеЛицо));
				Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
					
					ИдентификаторСтрокиНДФЛ = УдаляемаяСтрока.ИдентификаторСтрокиНДФЛ;
					Объект.НДФЛ.Удалить(УдаляемаяСтрока);
					
					УдаляемыеСтрокиВычетов = Объект.ПримененныеВычетыНаДетейИИмущественные.НайтиСтроки(Новый Структура("ИдентификаторСтрокиНДФЛ", ИдентификаторСтрокиНДФЛ));
					Для каждого УдаляемаяСтрокаВычетов Из УдаляемыеСтрокиВычетов Цикл
						Объект.ПримененныеВычетыНаДетейИИмущественные.Удалить(УдаляемаяСтрокаВычетов);
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли; 
			
		КонецЦикла;
		
		ОбновляемыеФизическиеЛица = Неопределено;
		
	КонецЕсли;
	
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриИзменении(Элемент)
	
	УстановитьДоступностьСортировкиИЗаполнения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиНачисленоПриИзменении(Элемент)
	
	ДанныеСтроки = Элементы.Сотрудники.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		СформироватьСписокОбновляемыхРаботников(Элементы.Сотрудники.ВыделенныеСтроки);
		ПересчитатьНачислениеНаСервере(ДанныеСтроки.Сотрудник, ДанныеСтроки.Начислено);
		ДополнитьСтрокиКРасчету(ДанныеСтроки.Сотрудник, "Сотрудники");
		УстановитьОтображениеКнопкиПересчитать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ОбновитьПодключаемыеКоманды(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ПодборСотрудникаВФормеДокументаПремия");
	
	Если ЗначениеЗаполнено(Объект.ДатаНачалаБазовогоПериода) Тогда
		НачалоПериодаПримененияОтбора = Объект.ДатаНачалаБазовогоПериода;
		ОкончаниеПериодаПримененияОтбора = Объект.ДатаОкончанияБазовогоПериода;
	Иначе
		НачалоПериодаПримененияОтбора = Объект.ПериодРегистрации;
		ОкончаниеПериодаПримененияОтбора = КонецМесяца(Объект.ПериодРегистрации);
	КонецЕсли;
	
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериодеПоПараметрамОткрытияФормыСписка(
		Элементы.Сотрудники,
		Объект.Организация,
		Объект.Подразделение,
		НачалоПериодаПримененияОтбора,
		ОкончаниеПериодаПримененияОтбора,
		Истина,
		АдресСпискаПодобранныхСотрудников());
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокумент(Команда) Экспорт 
	ПересчитатьДокументНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоказатели(Команда)
	
	Если Объект.ТипПремии = ПредопределенноеЗначение("Перечисление.ТипыПремии.РазоваяПремия") Тогда
		ИмяПоказателя = "СуммаПремии";
	Иначе
		ИмяПоказателя = ?(Выплата = 1, "ПроцентПремии", "СуммаПремии");
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ИмяПоказателя, ФиксированноеЗначение, ЗначениеПоказателя",
		ИмяПоказателя, ИмяПоказателя = "СуммаПремии", ЭтотОбъект[ИмяПоказателя]);
	ОткрытьФорму("Документ.Премия.Форма.ГрупповоеЗаполнениеПоказателейДокументов",
		ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛисток(Команда)
	
	ПодключитьОбработчикОжидания("РасчетныйЛистокНаКлиенте", 0.1, Истина);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
			Команда,
			ЭтотОбъект,
			Объект);
	КонецЕсли;
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Выплатить(Команда)
	
	Если НЕ Объект.ПометкаУдаления И НЕ Объект.Проведен ИЛИ Модифицированность Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Провести");
		Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередВыплатойСледуетПровестиЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед выплатой документ следует провести'"), Кнопки,, КодВозвратаДиалога.Да);
	Иначе
		СформироватьДокументыВыплаты();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВверх(Команда)
	
	ПереместитьСтроку(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВниз(Команда)
	
	ПереместитьСтроку(1);
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоВозрастанию(Команда)
	
	СортироватьСтроки("Возр");
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоУбыванию(Команда)
	
	СортироватьСтроки("Убыв");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьУдержание(Команда)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Сотрудник      = ТекущиеДанные.Сотрудник;
	ФизическоеЛицо = ТекущиеДанные.ФизическоеЛицо;
	
	ПоказатьВсе = Ложь;
	Если СтрНайти(Команда.Имя,"ДобавитьУдержание") = 0 Тогда
		ПоказатьВсе = Истина;
		Удержание   = ПредопределенноеЗначение("ПланВидовРасчета.Удержания.ПустаяСсылка");
	Иначе
		СуффиксКоманды = СтрЗаменить(Команда.Имя,"ДобавитьУдержание","");
		
		Если ЗначениеЗаполнено(СуффиксКоманды) Тогда
			НомерКоманды = Число(СуффиксКоманды);
			Удержание = СоответствиеКомандУдержаний.НайтиПоИдентификатору(НомерКоманды-1).Значение;
		Иначе
			Удержание  = ПредопределенноеЗначение("ПланВидовРасчета.Удержания.ПустаяСсылка");
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВвода = Новый Структура();
	ПараметрыВвода.Вставить("Ссылка",          Объект.Ссылка);
	ПараметрыВвода.Вставить("Организация",     Объект.Организация);
	ПараметрыВвода.Вставить("МесяцНачисления", Объект.ПериодРегистрации);
	ПараметрыВвода.Вставить("Сотрудник",       Сотрудник);
	ПараметрыВвода.Вставить("ФизическоеЛицо",  ФизическоеЛицо);
	ПараметрыВвода.Вставить("Удержание",       Удержание);
	
	Если ПоказатьВсе Тогда
		ПоказатьВсеУдержания(ПараметрыВвода);
	Иначе
		ОткрытьФормуВводаУдержания(ПараметрыВвода);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьНДФЛ(Команда)
	
	ПересчитатьНДФЛСотрудников();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура БазовыйПериодСтрокойНачалоВыбораЗавершениеНаСервере()
	
	ЗаполнитьДатыНачислений(ЭтотОбъект);
	УстановитьВидПремии();
	ПерезаполнитьМотивПоощренияНаСервере();
	ЗаполнитьСписокМотивовПоощрения();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидПремии()
	
	КоличествоМесяцевБазовогоПериода = Месяц(Объект.ДатаОкончанияБазовогоПериода) - Месяц(Объект.ДатаНачалаБазовогоПериода) + 1;
	Если ЗначениеЗаполнено(РеквизитыШапкиПрежние)
		И ЗначениеЗаполнено(РеквизитыШапкиПрежние[Объект.ТипПремии].ВидПремии)
		И Выплата = РеквизитыШапкиПрежние[Объект.ТипПремии].Выплата
		И КоличествоМесяцевБазовогоПериода = РеквизитыШапкиПрежние[Объект.ТипПремии].КоличествоМесяцевБазовогоПериода Тогда
		Объект.ВидПремии = РеквизитыШапкиПрежние[Объект.ТипПремии].ВидПремии;
	Иначе
		Объект.ВидПремии = УчетЗарплаты.ПолучитьВидПремииПоБазовомуПериоду(Объект.ДатаНачалаБазовогоПериода,
			Объект.ДатаОкончанияБазовогоПериода, Объект.ТипПремии, Выплата);
		ЗапомнитьРеквизитыШапки(Объект.ТипПремии);
	КонецЕсли;
	УстановитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ТипПремииПриИзмененииНаСервере()
	
	ЗапомнитьРеквизитыШапки(?(Объект.ТипПремии = Перечисления.ТипыПремии.РазоваяПремия,
		Перечисления.ТипыПремии.ПоРезультатамРаботы,
		Перечисления.ТипыПремии.РазоваяПремия));
		
	Объект.МотивПоощрения = РеквизитыШапкиПрежние[Объект.ТипПремии].МотивПоощрения;
	МотивПоощренияБылИзмененВручную = РеквизитыШапкиПрежние[Объект.ТипПремии].МотивПоощренияБылИзмененВручную;
	
	УстановитьВидПремии();
	Если Объект.ТипПремии = Перечисления.ТипыПремии.РазоваяПремия Тогда
		КоличествоМесяцевБазовогоПериода = 0;
		Объект.ЗависимыеНачисления.Очистить();
		ЗаполнитьДатыНачислений(ЭтотОбъект);
	Иначе
		КоличествоМесяцевБазовогоПериода = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидПремии, "КоличествоМесяцевБазовогоПериода");
		ДополнитьТаблицуНачисления();
	КонецЕсли;
	
	ПерезаполнитьМотивПоощренияНаСервере();
	ЗаполнитьСписокМотивовПоощрения();
	
	УстановитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеЭлементовФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Шапка",
		"ТолькоПросмотр",
		Параметры.КонтекстныйВызов);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Выплата",
		"Видимость",
		Объект.ТипПремии = Перечисления.ТипыПремии.ПоРезультатамРаботы);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"БазовыйПериодСтрокой",
		"Видимость",
		Объект.ТипПремии = Перечисления.ТипыПремии.ПоРезультатамРаботы);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗаполнитьПоказатели",
		"Заголовок",
		?(Выплата = 0 Или Объект.ТипПремии = Перечисления.ТипыПремии.РазоваяПремия,
			НСтр("ru='Ввести сумму премии'"),
			НСтр("ru='Ввести процент премии'")));
	
	УстановитьВидимостьЗависимыхНачислений();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКолонок()
	
	Элементы.СотрудникиПрочееУдержано.Видимость = ИтогУдержано <> 0;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СотрудникиПрочееУдержано");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СотрудникиФиксРасчет_НДФЛ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СотрудникиНДФЛ");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Сотрудники.Совместитель", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокОбновляемыхСотрудников(ИмяТабличнойЧасти, Знач ИдентификаторыСтрок)
	
	СотрудникиКОбновлению = Новый Соответствие;
	
	Если ТипЗнч(ИдентификаторыСтрок) = Тип("Массив") Тогда
		СписокИдентификаторовСтрок = ИдентификаторыСтрок;
	Иначе
		СписокИдентификаторовСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторыСтрок);
	КонецЕсли;
	
	Для каждого ИдентификаторСтроки Из СписокИдентификаторовСтрок Цикл
		
		СтрокаСотрудника = Объект[ИмяТабличнойЧасти].НайтиПоИдентификатору(ИдентификаторСтроки);
		Если СтрокаСотрудника <> Неопределено И ЗначениеЗаполнено(СтрокаСотрудника.Сотрудник) Тогда
			СотрудникиКОбновлению.Вставить(СтрокаСотрудника.Сотрудник, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбновляемыеСотрудники <> Неопределено Тогда
		
		СписокОбновляемых = Новый Массив(ОбновляемыеСотрудники);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокОбновляемых, ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиКОбновлению, "Ключ"), Истина);
		
	Иначе
		СписокОбновляемых = ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиКОбновлению, "Ключ");
	КонецЕсли;
	
	ОбновляемыеСотрудники = Новый ФиксированныйМассив(СписокОбновляемых);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокОбновляемыхФизическихЛиц(ИмяТабличнойЧасти, Знач ИдентификаторыСтрок, ИдентификаторКолонки = "ФизическоеЛицо")
	
	ФизическиеЛицаКОбновлению = Новый Соответствие;
	
	Если ТипЗнч(ИдентификаторыСтрок) = Тип("Массив") Тогда
		СписокИдентификаторовСтрок = ИдентификаторыСтрок;
	Иначе
		СписокИдентификаторовСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторыСтрок);
	КонецЕсли;
	
	Для каждого ИдентификаторСтроки Из СписокИдентификаторовСтрок Цикл
		
		СтрокаФизическогоЛица = Объект[ИмяТабличнойЧасти].НайтиПоИдентификатору(ИдентификаторСтроки);
		Если СтрокаФизическогоЛица <> Неопределено И ЗначениеЗаполнено(СтрокаФизическогоЛица[ИдентификаторКолонки]) Тогда
			ФизическиеЛицаКОбновлению.Вставить(СтрокаФизическогоЛица[ИдентификаторКолонки], Истина);
		КонецЕсли; 
		
	КонецЦикла;
	
	Если ОбновляемыеФизическиеЛица <> Неопределено Тогда
		
		СписокОбновляемых = Новый Массив(ОбновляемыеФизическиеЛица);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокОбновляемых, ОбщегоНазначения.ВыгрузитьКолонку(ФизическиеЛицаКОбновлению, "Ключ"), Истина);
		
	Иначе
		СписокОбновляемых = ОбщегоНазначения.ВыгрузитьКолонку(ФизическиеЛицаКОбновлению, "Ключ");
	КонецЕсли;
	
	ОбновляемыеФизическиеЛица = Новый ФиксированныйМассив(СписокОбновляемых);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьСотрудников(МножественныйВыбор)
	
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериодеПоПараметрамОткрытияФормыСписка(
		Элементы.Сотрудники,
		Объект.Организация,
		Объект.Подразделение,
		Объект.ПериодРегистрации, 
		КонецМесяца(Объект.ПериодРегистрации),
		МножественныйВыбор,
		АдресСпискаПодобранныхСотрудников());
	
КонецПроцедуры
	
&НаСервере
Процедура ДобавитьСотрудников(Знач СписокСотрудников)
	
	СотрудникиКДобавлению = Новый Массив;
	Для каждого ДобавляемыйСотрудник Из СписокСотрудников Цикл
		
		Если Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник", ДобавляемыйСотрудник)).Количество() = 0 Тогда
			СотрудникиКДобавлению.Добавить(ДобавляемыйСотрудник);
		КонецЕсли; 
		
	КонецЦикла;
	
	РезультатНачисления = РасчетЗарплатыБазовый.РезультатНачисленияРасчетаПремии(
		Объект.Организация,
		Объект.ПериодРегистрации,
		Объект.Ссылка,
		Объект.Подразделение,
		СотрудникиКДобавлению);
		
	ТребуетсяПересчет = Ложь;
	Если РезультатНачисления.Количество() > 0 Тогда
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			
			РезультатНачисления.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число"));
			
			РезультатНачисления.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
			РезультатНачисления.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
			Если ЭтоПремияПроцентом Тогда
				РезультатНачисления.ЗаполнитьЗначения(НачалоМесяца(Объект.ДатаНачалаБазовогоПериода), "ДатаНачала");
				РезультатНачисления.ЗаполнитьЗначения(НачалоДня(КонецМесяца(Объект.ДатаОкончанияБазовогоПериода)), "ДатаОкончания");
				Если ПроцентПремии <> 0 Тогда
					ЗначенияПоказателей = МодульРасчетЗарплатыДляНебольшихОрганизаций.РассчитатьПремиюПоПроценту(Объект, ПроцентПремии, СотрудникиКДобавлению);
					Для Каждого СтрокаТаблицы Из РезультатНачисления Цикл
						Если Не ЗначениеЗаполнено(СтрокаТаблицы.Начисление) Тогда
							ЗначениеПоказателя = ЗначенияПоказателей[СтрокаТаблицы.Сотрудник];
							Если ЗначениеПоказателя <> Неопределено Тогда
								СтрокаТаблицы.Результат = ЗначениеПоказателя;
								ТребуетсяПересчет = Истина;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			Иначе
				РезультатНачисления.ЗаполнитьЗначения(НачалоМесяца(Объект.ПериодРегистрации), "ДатаНачала");
				РезультатНачисления.ЗаполнитьЗначения(НачалоДня(КонецМесяца(Объект.ПериодРегистрации)), "ДатаОкончания");
				Если СуммаПремии <> 0 Тогда
					Для Каждого СтрокаТаблицы Из РезультатНачисления Цикл
						СтрокаТаблицы.Результат = СуммаПремии;
						ТребуетсяПересчет = Истина;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатНачисления.Скопировать(Новый Структура("ВходитВБазуРКиСН", Истина)), Объект.Начисления);
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатНачисления.Скопировать(Новый Структура("ВходитВБазуРКиСН", Ложь)), Объект.ЗависимыеНачисления);
			
			ОбновляемыеСотрудники = Новый ФиксированныйМассив(СотрудникиКДобавлению);
			
			Если ТребуетсяПересчет Тогда
				ПересчитатьНачисленияНаСервере(ОбновляемыеСотрудники);
				ОбновитьНДФЛСотрудников(СотрудникиКДобавлению);
			КонецЕсли;
			
			МодульРасчетЗарплатыДляНебольшихОрганизаций.ЗаполнитьРезультатУдержаний(Объект, РезультатНачисления);
			МодульРасчетЗарплатыДляНебольшихОрганизаций.СформироватьТабличнуюЧастьСотрудникиПремии(Объект);
		КонецЕсли;
		ОбновитьСтрокуСотрудника();
		РассчитатьИтогиПоДокументу();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтрокуСотрудника(Очищать = Истина)
	
	Если ОбновляемыеФизическиеЛица <> Неопределено Тогда
		
		СотрудникиФизическихЛиц = Новый Соответствие;
		Для каждого ОбновляемоеФизическоеЛицо Из ОбновляемыеФизическиеЛица Цикл
			
			СтрокиПоФизическимЛицам = Объект.Начисления.НайтиСтроки(Новый Структура("ФизическоеЛицо", ОбновляемоеФизическоеЛицо));
			Если СтрокиПоФизическимЛицам.Количество() > 0 Тогда
				ОбновляемыйСотрудник = СтрокиПоФизическимЛицам[0].Сотрудник;
			Иначе
				
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
				
				КадровыйУчет.СоздатьВТОсновныеСотрудникиФизическихЛиц(
					Запрос.МенеджерВременныхТаблиц,
					Истина,
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбновляемоеФизическоеЛицо),
					Объект.Организация,
					'00010101',
					КонецМесяца(Объект.ПериодРегистрации));
				
				Запрос.Текст =
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	ОсновныеСотрудникиФизическихЛиц.Сотрудник
					|ИЗ
					|	ВТОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц";
				
				РезультатЗапроса = Запрос.Выполнить();
				Если Не РезультатЗапроса.Пустой() Тогда
					
					Выборка = РезультатЗапроса.Выбрать();
					Выборка.Следующий();
					ОбновляемыйСотрудник = Выборка.Сотрудник;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ОбновляемыйСотрудник) Тогда
				СотрудникиФизическихЛиц.Вставить(ОбновляемыйСотрудник, Истина);
			КонецЕсли;
			
		КонецЦикла;
		
		Если СотрудникиФизическихЛиц.Количество() > 0 Тогда
			
			СотрудникиКОбновлению = Новый Массив;
			Если ОбновляемыеСотрудники <> Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиКОбновлению, ОбновляемыеСотрудники, Истина);
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиКОбновлению, ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиФизическихЛиц, "Ключ"), Истина);
			
			ОбновляемыеСотрудники = Новый ФиксированныйМассив(СотрудникиКОбновлению);
			
		КонецЕсли;
		
		Если Очищать Тогда
			ОбновляемыеФизическиеЛица = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбновляемыеСотрудники <> Неопределено Тогда
		
		Для каждого ОбновляемыйСотрудник Из ОбновляемыеСотрудники Цикл
			
			СтрокиСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ОбновляемыйСотрудник));
			Если СтрокиСотрудника.Количество() = 0 Тогда
				
				СтрокаСотрудника = Объект.Сотрудники.Добавить();
				СтрокаСотрудника.Сотрудник = ОбновляемыйСотрудник;
				СтрокаСотрудника.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбновляемыйСотрудник, "ФизическоеЛицо");
				
			Иначе
				СтрокаСотрудника = СтрокиСотрудника[0];
			КонецЕсли;
			
			ТаблицаНачислений = Объект.Начисления.Выгрузить(Новый Структура("Сотрудник", СтрокаСотрудника.Сотрудник));
			ТаблицаЗависимыеНачисления = Объект.ЗависимыеНачисления.Выгрузить(Новый Структура("Сотрудник", СтрокаСотрудника.Сотрудник));
			ТаблицаУдержаний = Объект.Удержания.Выгрузить(Новый Структура("Сотрудник", СтрокаСотрудника.ФизическоеЛицо));
			ТаблицаНДФЛ = Объект.НДФЛ.Выгрузить(Новый Структура("ФизическоеЛицо", СтрокаСотрудника.ФизическоеЛицо));
			ТаблицаКорректировкиВыплаты  = Объект.КорректировкиВыплаты.Выгрузить(Новый Структура("ФизическоеЛицо", СтрокаСотрудника.ФизическоеЛицо));
			
			Если ТаблицаНачислений.Количество() = 0
				И ТаблицаЗависимыеНачисления.Количество() = 0
				И ТаблицаУдержаний.Количество() = 0
				И ТаблицаНДФЛ.Количество() = 0 Тогда
				
				Объект.Сотрудники.Удалить(СтрокаСотрудника);
				
			Иначе
				
				СтрокаСотрудника.Начислено = ТаблицаНачислений.Итог("Результат");
				СтрокаСотрудника.ЗависимыеНачисления  = ТаблицаЗависимыеНачисления.Итог("Результат");
				СтрокаСотрудника.ВсегоНачислено = СтрокаСотрудника.Начислено + СтрокаСотрудника.ЗависимыеНачисления;
				
				СтрокаСотрудника.ПрочееУдержано = ТаблицаУдержаний.Итог("Результат");
				ИменаКолонок = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("Налог");
				СтрокаСотрудника.НДФЛ = 0;
				Для каждого ИмяКолонки Из ИменаКолонок Цикл
					СтрокаСотрудника.НДФЛ = СтрокаСотрудника.НДФЛ + ТаблицаНДФЛ.Итог(ИмяКолонки);
				КонецЦикла;
				СтрокаСотрудника.ВсегоУдержано = СтрокаСотрудника.ПрочееУдержано + СтрокаСотрудника.НДФЛ;
				СтрокаСотрудника.ФиксРасчет_НДФЛ = 
					ТаблицаНДФЛ.Найти(Истина, "ФиксРасчет")<> Неопределено
					ИЛИ ТаблицаКорректировкиВыплаты.Найти(Истина, "ФиксРасчет")<> Неопределено;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Очищать Тогда
			ОбновляемыеСотрудники = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ВидыПремийПроцентом = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремииПроцентом());
	
	УстановитьФункциональныеОпцииФормы();
	ЗаполнитьКнопкиПодменю();
	
	УстановитьНастройкиРасчета();
	ЗаполнитьБазовыйПериод();
	
	Выплата = ?(ВидыПремийПроцентом.Найти(СреднийЗаработокОбщий) = Неопределено, 0, 1);
	
	ПараметрыМотиваПоощрения = Документы.Премия.НовыеПараметрыМотиваПоощрения();
	ЗаполнитьЗначенияСвойств(ПараметрыМотиваПоощрения, Объект);
	АвтоТекстМотиваПоощрения = АвтоТекстМотиваПоощрения(ПараметрыМотиваПоощрения);
	МотивПоощренияБылИзмененВручную = Документы.Премия.МотивПоощренияИзмененВручную(
		Объект.МотивПоощрения, АвтоТекстМотиваПоощрения);
	
	УстановитьОтображениеЭлементовФормы();
	РассчитатьИтогиПоДокументу();
	УстановитьВидимостьКолонок();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой");
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АвтоТекстМотиваПоощрения(Знач ПараметрыМотиваПоощрения)
	
	Возврат Документы.Премия.ТекстМотиваПоощрения(ПараметрыМотиваПоощрения);
	
КонецФункции

&НаСервере
Процедура ПерезаполнитьМотивПоощренияНаСервере()
	
	ПараметрыМотиваПоощрения = Документы.Премия.НовыеПараметрыМотиваПоощрения();
	ЗаполнитьЗначенияСвойств(ПараметрыМотиваПоощрения, Объект);
	АвтоТекстМотиваПоощрения = АвтоТекстМотиваПоощрения(ПараметрыМотиваПоощрения);
	Документы.Премия.СформироватьТекстМотиваПоощрения(Объект, АвтоТекстМотиваПоощрения, МотивПоощренияБылИзмененВручную);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьМотивПоощренияНаСервере()
	
	Документы.Премия.СформироватьТекстМотиваПоощрения(
		Объект,
		АвтоТекстМотиваПоощрения,
		МотивПоощренияБылИзмененВручную);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокМотивовПоощрения()
	
	Элементы.МотивПоощрения.СписокВыбора.Очистить();
	Если Объект.ТипПремии = Перечисления.ТипыПремии.РазоваяПремия Тогда
		Элементы.МотивПоощрения.СписокВыбора.Добавить(НСтр("ru = 'День рождения, юбилей сотрудника'"));
		Элементы.МотивПоощрения.СписокВыбора.Добавить(НСтр("ru = 'В связи с профессиональным праздником'"));
		Элементы.МотивПоощрения.СписокВыбора.Добавить(НСтр("ru = 'К Новому году'"));
		Элементы.МотивПоощрения.СписокВыбора.Добавить("", НСтр("ru = '<свой вариант>'"));
	Иначе
		Элементы.МотивПоощрения.СписокВыбора.Добавить(АвтоТекстМотиваПоощрения);
		Элементы.МотивПоощрения.СписокВыбора.Добавить("", НСтр("ru = '<свой вариант>'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЗависимыхНачислений()
	
	ЕстьЗависимыеНачисления = ПолучитьФункциональнуюОпциюФормы("ПрименятьРайонныйКоэффициент")
		Или ПолучитьФункциональнуюОпциюФормы("ПрименятьСевернуюНадбавку");
	
	Элементы.СотрудникиЗависимыеНачисления.Видимость =
		ЕстьЗависимыеНачисления И Объект.ТипПремии <> Перечисления.ТипыПремии.РазоваяПремия;
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьПланируемуюДатуВыплаты()
	
	Если Объект.ПланируемаяДатаВыплаты < Объект.Дата Или Объект.ПланируемаяДатаВыплаты < Объект.ПериодРегистрации Тогда
		Объект.ПланируемаяДатаВыплаты = Макс(Объект.Дата, Объект.ПериодРегистрации);
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			Объект.ПланируемаяДатаВыплаты = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДатаСобытияСУчетомНерабочихДней(Объект.ПланируемаяДатаВыплаты, Объект.Организация);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеПоказателейНаСервере(ЗначенияПоказателей)
	
	АдресОбщихПоказателей    = ЗначенияПоказателей.АдресОбщихПоказателей;
	ЗначенияОбщихПоказателей = ПолучитьИзВременногоХранилища(АдресОбщихПоказателей);
	Если Не ЗначениеЗаполнено(ЗначенияОбщихПоказателей) Тогда
		Возврат;
	КонецЕсли;
	
	Сотрудники = Новый Массив;
	УникальныеСотрудники = Новый Соответствие;
	
	ЗначениеПоказателя = ЗначенияОбщихПоказателей["СуммаПремии"];
	Если ЗначениеПоказателя <> Неопределено Тогда
		СуммаПремии = ЗначениеПоказателя;
		Для Каждого СтрокаСотрудника Из Объект.Начисления Цикл
			СтрокаСотрудника.Результат = ЗначениеПоказателя;
		КонецЦикла;
	КонецЕсли;
	ЗначениеПоказателя = ЗначенияОбщихПоказателей["ПроцентПремии"];
	Если ЗначениеПоказателя <> Неопределено Тогда
		ПроцентПремии = ЗначениеПоказателя;
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			ЗначенияПоказателей = МодульРасчетЗарплатыДляНебольшихОрганизаций.РассчитатьПремиюПоПроценту(Объект, ЗначениеПоказателя);
			Для Каждого СтрокаСотрудника Из Объект.Начисления Цикл
				СтрокаСотрудника.Результат = ЗначенияПоказателей[СтрокаСотрудника.Сотрудник];
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого СтрокаНачислений Из Объект.Начисления Цикл
		ИдентификаторСтроки = СтрокаНачислений.ПолучитьИдентификатор();
		
		Если ЗначениеЗаполнено(СтрокаНачислений.Сотрудник) Тогда
			СтрокаНачислений.ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаНачислений.Сотрудник, "ФизическоеЛицо");
			ОбновляемыеФизическиеЛица = Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаНачислений.ФизическоеЛицо));
		Иначе
			СтрокаНачислений.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		КонецЕсли;
		
		ОбработатьИзменениеНачислений(ИдентификаторСтроки);
	КонецЦикла;
	ОбновитьУдержанияНДФЛСотрудников();
	
	ЗаполнитьВторичныеДанныеТабличныхЧастей();
	ОбновляемыеФизическиеЛица = Неопределено;
	ОбновляемыеСотрудники = Новый ФиксированныйМассив(ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Сотрудник"));
	ОбновитьСтрокуСотрудника();
	
	РассчитатьИтогиПоДокументу();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДатыНачислений(Форма)
	
	Объект = Форма.Объект;
	
	Для Каждого СтрокаТаблицы Из Объект.Начисления Цикл
		Если Форма.ЭтоПремияПроцентом Тогда
			СтрокаТаблицы.ДатаНачала    = Объект.ДатаНачалаБазовогоПериода;
			СтрокаТаблицы.ДатаОкончания = Объект.ДатаОкончанияБазовогоПериода;
		Иначе
			СтрокаТаблицы.ДатаНачала    = НачалоМесяца(Объект.ПериодРегистрации);
			СтрокаТаблицы.ДатаОкончания = КонецМесяца(Объект.ПериодРегистрации);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из Объект.ЗависимыеНачисления Цикл
		Если Форма.ЭтоПремияПроцентом Тогда
			СтрокаТаблицы.ДатаНачала    = Объект.ДатаНачалаБазовогоПериода;
			СтрокаТаблицы.ДатаОкончания = Объект.ДатаОкончанияБазовогоПериода;
		Иначе
			СтрокаТаблицы.ДатаНачала    = НачалоМесяца(Объект.ПериодРегистрации);
			СтрокаТаблицы.ДатаОкончания = КонецМесяца(Объект.ПериодРегистрации);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтображениеКнопкиПересчитать()
	
	Если НЕ РассчитыватьДокументыПриРедактировании Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПересчитать",
			"Видимость",
			СтрокиКРасчету.Количество() > 0);
		
	КонецЕсли;
	
КонецПроцедуры

#Область РучнойПересчет

&НаКлиенте
Процедура ДополнитьСтрокиКРасчету(Сотрудник, ИмяКлюча)
	// ИмяКлюча - "Сотрудники", "НДФЛ"
	
	Если ИмяКлюча <> "Сотрудники" Тогда
		// если есть в общем списке - все равно пересчитается
		ИндексЗначения = НайтиСтрокиКРасчету(Сотрудник, "Сотрудники");
		Если ИндексЗначения <> Неопределено Тогда
			// удалим возможное дублирование
			УдалитьСтрокиКРасчету(Сотрудник, ИмяКлюча);
			Возврат;
		КонецЕсли;
	Иначе
		ИндексЗначения = НайтиСтрокиКРасчету(Сотрудник, "НДФЛ");
		Если ИндексЗначения <> Неопределено Тогда
			// удалим возможное дублирование
			УдалитьСтрокиКРасчету(Сотрудник, "НДФЛ");
		КонецЕсли;
	КонецЕсли;
	
	МассивСотрудников = СтрокиКРасчету.Получить(ИмяКлюча);
	Если МассивСотрудников = Неопределено Тогда
		МассивСотрудников = Новый Массив;
	КонецЕсли;
	МассивСотрудников.Добавить(Сотрудник);
	СтрокиКРасчету.Вставить(ИмяКлюча, МассивСотрудников);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокиКРасчету(Сотрудник, ИмяКлюча)
	// ИмяКлюча - "Сотрудники", "НДФЛ"
	
	ИндексЗначения = НайтиСтрокиКРасчету(Сотрудник, ИмяКлюча);
	Если ИндексЗначения <> Неопределено Тогда
		МассивСотрудников = СтрокиКРасчету.Получить(ИмяКлюча);
		МассивСотрудников.Удалить(ИндексЗначения);
		Если МассивСотрудников.Количество() = 0 Тогда
			СтрокиКРасчету.Удалить(ИмяКлюча);
		Иначе
			СтрокиКРасчету.Вставить(ИмяКлюча, МассивСотрудников);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиСтрокиКРасчету(Сотрудник, ИмяКлюча)
	// ИмяКлюча - "Сотрудники", "НДФЛ"
	
	МассивСотрудников = СтрокиКРасчету.Получить(ИмяКлюча);
	Если МассивСотрудников = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат МассивСотрудников.Найти(Сотрудник);
	
КонецФункции

&НаКлиенте
Процедура ВопросПередПересчетомЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПересчитатьДокументНаСервере(ДополнительныеПараметры);

КонецПроцедуры

&НаСервере
Процедура ПересчитатьДокументНаСервере(ПараметрыРасчета)
	
	Если ОбновляемыеФизическиеЛица <> Неопределено Тогда
		ОбновляемыеФизическиеЛицаВременная = ОбновляемыеФизическиеЛица;
	КонецЕсли;
	МассивФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(Объект.ФизическиеЛица, "ФизическоеЛицо", Истина);
	ОбновляемыеФизическиеЛица = Новый ФиксированныйМассив(МассивФизическихЛиц);
	Если ОбновляемыеСотрудники <> Неопределено Тогда
		ОбновляемыеСотрудникиВременная = ОбновляемыеСотрудники;
	КонецЕсли;
	МассивСотрудников = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Сотрудники, "Сотрудник", Истина);
	ОбновляемыеСотрудники = Новый ФиксированныйМассив(МассивСотрудников);
	
	Если ПараметрыРасчета.НДФЛ Тогда
		Объект.КорректироватьНДФЛ = Ложь;
		Объект.НДФЛ.Очистить();
		ОбновитьРасчетыСотрудников(Новый Массив, МассивСотрудников)
	КонецЕсли;
	
	ОбновляемыеФизическиеЛица = ОбновляемыеФизическиеЛицаВременная;
	ОбновляемыеСотрудники     = ОбновляемыеСотрудникиВременная;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область РедактированиеИОбновлениеДанных

&НаСервере
Процедура ОбновитьДанныеНДФЛНаСервере(АдресВременногоХранилища)
	
	Параметр = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	ФизическоеЛицо = Параметр.ФизическоеЛицо;
	Сотрудник      = Параметр.Сотрудник;
	
	ОтборСтрокФизическихЛиц = Новый Структура("ФизическоеЛицо",
												ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
	РасчетЗарплатыБазовый.ЗаменитьСтрокиНДФЛ(Объект.НДФЛ,
												Параметр.НДФЛ,
												Объект.ПримененныеВычетыНаДетейИИмущественные,
												Параметр.ПримененныеВычетыНаДетейИИмущественные,
												ОтборСтрокФизическихЛиц,
												Ложь);
	
	Для Каждого СтрокаНачислений Из Параметр.Начисления Цикл
		СтруктураПоиска = Новый Структура("Сотрудник, ФизическоеЛицо, Начисление, КодВычета, ВычетПримененныйКДоходам");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаНачислений);
		СтруктураПоиска.ВычетПримененныйКДоходам = Истина;
		НайденныеСтроки = Объект.Начисления.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].КодВычета = СтрокаНачислений.КодВычета;
			НайденныеСтроки[0].СуммаВычета = СтрокаНачислений.СуммаВычета;
		КонецЕсли; 
	КонецЦикла;
	
	РасчетЗарплатыБазовый.ЗаменитьСтрокиНаНовыеДанные(Объект.КорректировкиВыплаты, Параметр.КорректировкиВыплаты, "ФизическоеЛицо", ОтборСтрокФизическихЛиц);
	
	Модифицированность = Истина;
	
	ПриОкончанииРедактированияНаСервере(ФизическоеЛицо);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеУдержанийНаСервере(АдресВременногоХранилища)
	
	Параметр = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Сотрудник      = Параметр.Сотрудник;
	ФизическоеЛицо = Параметр.ФизическоеЛицо;
	
	ОтборСтрокФизическихЛиц = Новый Структура("Сотрудник", ФизическоеЛицо);
	РасчетЗарплатыБазовый.ЗаменитьСтрокиНаНовыеДанные(Объект.Удержания, Параметр.Удержания, "Сотрудник", ОтборСтрокФизическихЛиц);
	
	Модифицированность = Истина;
	
	СформироватьСписокОбновляемыхРаботников(Элементы.Сотрудники.ВыделенныеСтроки);
	
	Если РассчитыватьДокументыПриРедактировании Тогда
		МассивСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
		ДополнитьМассивСотрудниковСовместителями(МассивСотрудников,Сотрудник);
		ОбновитьНДФЛСотрудников(МассивСотрудников);
	КонецЕсли;
	
	ПриОкончанииРедактированияНаСервере(ФизическоеЛицо);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеНачисленийНаСервере(АдресВременногоХранилища)
	
	Параметр = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Сотрудник      = Параметр.Сотрудник;
	ФизическоеЛицо = Параметр.ФизическоеЛицо;
	
	ОтборСтрокСотрудники = Новый Структура("Сотрудник", Сотрудник);
	РасчетЗарплатыБазовый.ЗаменитьСтрокиНаНовыеДанные(Объект.Начисления, Параметр.Начисления, "Сотрудник", ОтборСтрокСотрудники);
	РасчетЗарплатыБазовый.ЗаменитьСтрокиНаНовыеДанные(Объект.ЗависимыеНачисления, Параметр.ЗависимыеНачисления, "Сотрудник", ОтборСтрокСотрудники);
	
	Модифицированность = Истина;
	
	СформироватьСписокОбновляемыхРаботников(Элементы.Сотрудники.ВыделенныеСтроки);
	
	ПересчитатьСотрудника(Сотрудник);
	
	ПриОкончанииРедактированияНаСервере(ФизическоеЛицо);
	
КонецПроцедуры

&НаСервере
Процедура ПриОкончанииРедактированияНаСервере(ФизическоеЛицо)
	
	ДополнитьСписокОбновляемыхФизическихЛиц(ФизическоеЛицо);
	ОбновитьСтрокуСотрудника(РассчитыватьДокументыПриРедактировании);
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУдержаниеНаСервере(АдресВременногоХранилища)
	
	Параметр = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Сотрудник      = Параметр.Сотрудник;
	ФизическоеЛицо = Параметр.ФизическоеЛицо;
	
	ЗаполнитьЗначенияСвойств(Объект.Удержания.Добавить(), Параметр.Удержание);
	
	Модифицированность = Истина;
	
	ПересчитатьСотрудника(Сотрудник);
	
	ПриОкончанииРедактированияНаСервере(ФизическоеЛицо);
	
	УстановитьВидимостьКолонок();
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура РасчетныйЛистокНаКлиенте() Экспорт
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток("Сотрудники", ТекущиеДанные.Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛистокУдержанияНаКлиенте() Экспорт
	
	УдержанияВыделенныеСтроки = Элементы.Удержания.ВыделенныеСтроки;
	Если УдержанияВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВывестиРасчетныйЛисток(УдержанияВыделенныеСтроки, "Удержания");
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиРасчетныйЛисток(ИмяТабличнойЧасти, Сотрудник = Неопределено)
	
	РасчетныйЛисток = РасчетныйЛистокНаСервере(ИмяТабличнойЧасти, Сотрудник);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Отчет.АнализНачисленийИУдержаний",
		"РасчетныйЛистокПодробнее",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка),
		ЭтотОбъект,
		Новый Структура("ПечатаемыйДокумент, ЗаголовокФормы",
			РасчетныйЛисток, НСтр("ru='Расчетные листки'")));
	
КонецПроцедуры

&НаСервере
Функция РасчетныйЛистокНаСервере(ИмяТабличнойЧасти, Сотрудник = Неопределено) Экспорт
	
	Если Сотрудник = Неопределено Тогда
		СписокСотрудников = Новый Массив;
		Для Каждого СтрокаТаблицы Из Объект[ИмяТабличнойЧасти] Цикл
			СписокСотрудников.Добавить(СтрокаТаблицы.Сотрудник);
		КонецЦикла;
	Иначе
		СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
	КонецЕсли;
	
	Если ИмяТабличнойЧасти = "Начисления" 
		ИЛИ ИмяТабличнойЧасти = "Сотрудники"
		ИЛИ ИмяТабличнойЧасти = "ЗависимыеНачисления" Тогда
		МассивФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(
									ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
										СписокСотрудников,
										"ФизическоеЛицо"),
									"Значение",
									Истина);
	КонецЕсли;
	
	Если Объект.Проведен И Не Модифицированность Тогда
		Возврат УчетНачисленнойЗарплаты.РасчетныйЛистокПоДаннымДокумента(Объект.Организация, МассивФизическихЛиц, Объект.ПериодРегистрации);
	Иначе
		ДанныеДокумента = ДанныеДокументаФизическихЛиц(МассивФизическихЛиц);
		Возврат УчетНачисленнойЗарплаты.РасчетныйЛистокПоДаннымДокумента(Объект.Организация, МассивФизическихЛиц, Объект.ПериодРегистрации, Объект.Ссылка, ДанныеДокумента);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ДанныеДокументаФизическихЛиц(ФизическиеЛица)
	
	ТабличныеЧасти = ЗарплатаКадрыОтчеты.ТабличныеЧастиДляФормированияНабораДанныхДокументов();
	
	ОписаниеТаблицыНачислений = Новый Структура;
	ОписаниеТаблицыНачислений.Вставить("ИмяТаблицы", "Начисления");
	ОписаниеТаблицыНачислений.Вставить("ПутьКДанным", "Объект.Начисления");
	ОписаниеТаблицыНачислений.Вставить("ИмяПоляРезультат", "Результат");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаНачала", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаОкончания", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаВидРасчета", "");
	
	ТабличныеЧасти.ТаблицыНачислений.Добавить(ОписаниеТаблицыНачислений);
	
	ОписаниеТаблицыНачислений = Новый Структура;
	ОписаниеТаблицыНачислений.Вставить("ИмяТаблицы", "ЗависимыеНачисления");
	ОписаниеТаблицыНачислений.Вставить("ПутьКДанным", "Объект.ЗависимыеНачисления");
	ОписаниеТаблицыНачислений.Вставить("ИмяПоляРезультат", "Результат");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаНачала", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаОкончания", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаВидРасчета", "");
	
	ТабличныеЧасти.ТаблицыНачислений.Добавить(ОписаниеТаблицыНачислений);
	
	ОписаниеТаблицыНДФЛ = Новый Структура;
	ОписаниеТаблицыНДФЛ.Вставить("ИмяТаблицы", "НДФЛ");
	ОписаниеТаблицыНДФЛ.Вставить("ПутьКДанным", "Объект.НДФЛ");
	ОписаниеТаблицыНДФЛ.Вставить("ИмяПоляРезультат", "Налог");
	ОписаниеТаблицыНДФЛ.Вставить("ИмяРеквизитаВидРасчета", "");
	
	ТабличныеЧасти.ТаблицыУдержаний.Добавить(ОписаниеТаблицыНДФЛ);
	
	ОписаниеТаблицыУдержаний = Новый Структура;
	ОписаниеТаблицыУдержаний.Вставить("ИмяТаблицы", "Удержания");
	ОписаниеТаблицыУдержаний.Вставить("ПутьКДанным", "Объект.Удержания");
	ОписаниеТаблицыУдержаний.Вставить("ИмяПоляРезультат", "Результат");
	ОписаниеТаблицыУдержаний.Вставить("ИмяРеквизитаВидРасчета", "Удержание");
	
	ОписаниеТаблицыУдержаний.Вставить("ИмяРеквизитаФизическоеЛицо", "Сотрудник");
	
	ТабличныеЧасти.ТаблицыУдержаний.Добавить(ОписаниеТаблицыУдержаний);
	
	Возврат ЗарплатаКадрыОтчеты.ДанныеДокументаФизическихЛиц(ЭтотОбъект, ФизическиеЛица, ТабличныеЧасти, "ПериодРегистрации", "ВидПремии");
	
КонецФункции

&НаСервере
Процедура ЗаполнитьБазовыйПериод()
	
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(ЭтотОбъект,
		"БазовыйПериодСтрокой",
		ПредставлениеБазовогоПериодаСтрокой(Объект.ДатаНачалаБазовогоПериода, Объект.ДатаОкончанияБазовогоПериода));

КонецПроцедуры

&НаСервере
Процедура ОпределитьГлубинуБазовогоПериода()
	ГоризонтБазовогоПериода = 0;
	Если ЗначениеЗаполнено(Объект.ВидПремии) Тогда
		ГоризонтБазовогоПериода = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидПремии, "КоличествоМесяцевБазовогоПериода");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Начисления.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПодразделениеСотрудника(Сотрудник, ДатаСведений)
	
	КадровыеДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), "Подразделение", ДатаСведений);
	Если КадровыеДанныеСотрудника.Количество() > 0 Тогда
		Возврат КадровыеДанныеСотрудника[0].Подразделение;
	КонецЕсли;
	
	Возврат Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьКнопкиПодменю()
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	// Удержания
	Если Не ЗначениеЗаполнено(СоответствиеКомандУдержаний) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Удержания.Ссылка КАК Ссылка,
		|	Удержания.Наименование КАК Наименование
		|ИЗ
		|	ПланВидовРасчета.Удержания КАК Удержания
		|ГДЕ
		|	НЕ Удержания.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
		Выборка = Запрос.Выполнить().Выбрать();
		НомерКонтейнера = 1;
		Пока Выборка.Следующий() Цикл
			
			ТипИсточника = ТипЗнч(Элементы.ДобавитьУдержание);
			Приемник = Элементы.Вставить(Элементы.ДобавитьУдержание.Имя + НомерКонтейнера, ТипИсточника, Элементы.ГруппаКнопокУдержать, Элементы.ДобавитьУдержание);
			ИмяКоманды = Элементы.ДобавитьУдержание.ИмяКоманды + НомерКонтейнера;
			Если Команды.Найти(ИмяКоманды) = Неопределено Тогда
				НоваяКоманда = Команды.Добавить(ИмяКоманды);
				ЗаполнитьЗначенияСвойств(НоваяКоманда, Команды[Элементы.ДобавитьУдержание.ИмяКоманды]);
			КонецЕсли;
			Приемник.ИмяКоманды = ИмяКоманды;
			ЗаполнитьЗначенияСвойств(Приемник, Элементы.ДобавитьУдержание, , "Имя, ИмяКоманды");
			Приемник.Заголовок = Выборка.Наименование;
			Приемник.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
			
			СоответствиеКомандУдержаний.Добавить(Выборка.Ссылка);
			
			НомерКонтейнера = НомерКонтейнера + 1;
			Если НомерКонтейнера = 14 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		// Все удержания
		Элементы.СотрудникиГруппаКнопокУдержания.Видимость = НомерКонтейнера = 14;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПараметрыФО = Новый Структура("Организация, Период", Объект.Организация, НачалоДня(Объект.ПериодРегистрации));
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
	ПоддержкаСовместительства =
		ПолучитьФункциональнуюОпцию("ПоддержкаСовместительства");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьСортировкиИЗаполнения(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	СотрудникиЗаполнены = Объект.Сотрудники.Количество() <> 0;
	Элементы.СотрудникиПереместитьВверх.Доступность                = СотрудникиЗаполнены;
	Элементы.СотрудникиПереместитьВниз.Доступность                 = СотрудникиЗаполнены;
	Элементы.СотрудникиСортироватьПоВозрастанию.Доступность        = СотрудникиЗаполнены;
	Элементы.СотрудникиСортироватьПоУбыванию.Доступность           = СотрудникиЗаполнены;
	Элементы.СотрудникиКонтекстноеМенюПереместитьВверх.Доступность = СотрудникиЗаполнены;
	Элементы.СотрудникиКонтекстноеМенюПереместитьВниз.Доступность  = СотрудникиЗаполнены;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗаполнитьПоказатели",
		"Доступность",
		СотрудникиЗаполнены);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеНачислений(ИдентификаторСтроки)
	
	СтрокаНачисления = Объект.Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если ЗначениеЗаполнено(Объект.ВидПремии) Тогда
		ВходитВБазуРКИСН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидПремии, "ВходитВБазуРКИСН");
		СтрокаНачисления.ВходитВБазуРКиСН = ВходитВБазуРКИСН;
	КонецЕсли; 
	
	ПересчитатьНачисленияСКоэффициентом(ИдентификаторСтроки);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНачисленияСКоэффициентом(ИдентификаторСтроки)
	
	СтрокаИнициаторПересчета = Объект.Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если Не СтрокаИнициаторПересчета.ВходитВБазуРКиСН Тогда
		Возврат;
	КонецЕсли;
	
	ОкладТариф = 0;
	СтрокиНачисленийОкладТариф = Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник,ВходитВБазуРКиСН", СтрокаИнициаторПересчета.Сотрудник, Истина));
	Для Каждого СтрокОкладТариф Из СтрокиНачисленийОкладТариф Цикл
		ОкладТариф = ОкладТариф + СтрокОкладТариф.Результат;
	КонецЦикла;
	
	СтрокиНачислений = Объект.ЗависимыеНачисления.НайтиСтроки(Новый Структура("Сотрудник", СтрокаИнициаторПересчета.Сотрудник));
	Для Каждого НайденнаяСтрока Из СтрокиНачислений Цикл
		Если НайденнаяСтрока.КоэффициентПересчета <> 0 Тогда
			НайденнаяСтрока.Результат = ОкладТариф * НайденнаяСтрока.КоэффициентПересчета;
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьТаблицуНачисления()
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Формируем массив сотрудников.
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Сотрудник", Истина);
	
	// Получаем кадровые данные
	КадровыйУчет.СоздатьНаДатуВТКадровыеДанныеСотрудников(
		МенеджерВременныхТаблиц, Истина, Сотрудники, "ГоловнаяОрганизация,Организация,Подразделение,ПроцентСевернойНадбавки", Объект.ПериодРегистрации);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ПодразделенияОрганизаций.РайонныйКоэффициент > 1
	|			ТОГДА ПодразделенияОрганизаций.РайонныйКоэффициент
	|		КОГДА Организации.РайонныйКоэффициент > 1
	|			ТОГДА Организации.РайонныйКоэффициент
	|		КОГДА ГоловныеОрганизации.РайонныйКоэффициент > 1
	|			ТОГДА ГоловныеОрганизации.РайонныйКоэффициент
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК РайонныйКоэффициент,
	|	ВТКадровыеДанныеСотрудников.ПроцентСевернойНадбавки КАК ПроцентСевернойНадбавки
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|		ПО ВТКадровыеДанныеСотрудников.Подразделение = ПодразделенияОрганизаций.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ГоловныеОрганизации
	|		ПО ВТКадровыеДанныеСотрудников.ГоловнаяОрганизация = ГоловныеОрганизации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ВТКадровыеДанныеСотрудников.Организация = Организации.Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ДанныеСотрудников = Запрос.Выполнить().Выгрузить();
	
	ДанныеНачисления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ВидПремии, "КатегорияНачисленияИлиНеоплаченногоВремени,ВходитВБазуРКИСН");
	Для Каждого СтрокаНачисления Из Объект.Начисления Цикл
		ДанныеСотрудника = ДанныеСотрудников.Найти(СтрокаНачисления.Сотрудник, "Сотрудник");
		Если ДанныеСотрудника = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтрокаНачисления.ВходитВБазуРКиСН = ДанныеНачисления.ВходитВБазуРКИСН;
	КонецЦикла;
	
	Начисления = ОбщегоНазначения.ВыгрузитьКолонку(Объект.ЗависимыеНачисления, "Начисление", Истина);
	ДанныеНачислений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Начисления, "КатегорияНачисленияИлиНеоплаченногоВремени");
	
	Для Каждого СтрокаНачисления Из Объект.ЗависимыеНачисления Цикл
		ДанныеСотрудника = ДанныеСотрудников.Найти(СтрокаНачисления.Сотрудник, "Сотрудник");
		Если ДанныеСотрудника = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаНачисления.Начисление) Тогда
			
			ДанныеНачисления = ДанныеНачислений[СтрокаНачисления.Начисление];
			КатегорияНачисления = ДанныеНачисления.КатегорияНачисленияИлиНеоплаченногоВремени;
			
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент Тогда
				СтрокаНачисления.КоэффициентПересчета = ДанныеСотрудника.РайонныйКоэффициент - 1;
			КонецЕсли;
			
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка Тогда
				СтрокаНачисления.КоэффициентПересчета = ДанныеСотрудника.ПроцентСевернойНадбавки / 100;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНачисленияНаСервере(СписокСотрудников)
	
	Для Каждого Сотрудник Из СписокСотрудников Цикл
		МассивСтрок = Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
		Если МассивСтрок.Количество() <> 0 Тогда
			ДанныеСтроки = МассивСтрок[0];
			ПересчитатьНачисленияСКоэффициентом(ДанныеСтроки.ПолучитьИдентификатор());
			ОбновитьУдержанияНДФЛСотрудника(Сотрудник);
			ОбновитьСтрокуСотрудника(РассчитыватьДокументыПриРедактировании);
			РассчитатьИтогиПоДокументу();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНачислениеНаСервере(Сотрудник, Результат)
	
	МассивСтрок = Объект.Начисления.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
	Если МассивСтрок.Количество() <> 0 Тогда
		ДанныеСтроки = МассивСтрок[0];
		ДанныеСтроки.Результат = Результат;
		ПересчитатьНачисленияСКоэффициентом(ДанныеСтроки.ПолучитьИдентификатор());
		Если РассчитыватьДокументыПриРедактировании Тогда
			ОбновитьУдержанияНДФЛСотрудника(Сотрудник);
		КонецЕсли;
		ОбновитьСтрокуСотрудника(РассчитыватьДокументыПриРедактировании);
		РассчитатьИтогиПоДокументу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьДокументНаКлиенте()
	
	Если СтрокиКРасчету.Количество() > 0 Тогда
		МассивСотрудников = СтрокиКРасчету.Получить("Сотрудники");
		МассивСотрудниковНДФЛ = СтрокиКРасчету.Получить("НДФЛ");
		ОбновитьРасчетыСотрудников(МассивСотрудников, МассивСотрудниковНДФЛ);
		СотрудникиКРасчету.Очистить();
		СтрокиКРасчету.Очистить();
		УстановитьОтображениеКнопкиПересчитать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюОкончание(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПересчитатьДокументНаКлиенте();
		Записать(Параметры);
		
	Иначе
		ЗакрытьПослеЗаписи = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтветственныхЛиц()
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
КонецПроцедуры

// ЭДОБЗК

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодключаемыеКоманды(УправляемаяФорма)
	ЭДОБЗККлиентСервер.ОбновитьКоманды(УправляемаяФорма, УправляемаяФорма.Объект, Истина);
КонецПроцедуры

// Конец ЭДОБЗК

&НаСервере
Процедура УстановитьНастройкиРасчета()
	
	ОпределитьГлубинуБазовогоПериода();
	СреднийЗаработокОбщий = Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПустаяСсылка();
	Если ЗначениеЗаполнено(Объект.ВидПремии) Тогда
		СреднийЗаработокОбщий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидПремии, "СреднийЗаработокОбщий");
	КонецЕсли;
	ЭтоПремияПроцентом = ВидыПремийПроцентом.Найти(СреднийЗаработокОбщий) <> Неопределено;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеПанелиВычеты()
	
	ОписаниеПанелиВычеты = УчетНДФЛКлиентСервер.ОписаниеПанелиВычеты();
	ОписаниеТабличнойЧастиНДФЛ = УчетНДФЛКлиентСервер.ОписаниеТабличнойЧастиНДФЛ();
	ОписаниеТабличнойЧастиНДФЛ.ИмяПоляПериод = "";
	ОписаниеПанелиВычеты.ТабличнаяЧастьНДФЛ = ОписаниеТабличнойЧастиНДФЛ;
	
	НастраиваемыеПанели = Новый Соответствие;
	НастраиваемыеПанели.Вставить("ВычетыЛичные", 				Истина);
	НастраиваемыеПанели.Вставить("ВычетыНаДетейИИмущественные", "Объект.ПримененныеВычетыНаДетейИИмущественные");
	
	ОписаниеПанелиВычеты.НастраиваемыеПанели = НастраиваемыеПанели;
	
	Возврат ОписаниеПанелиВычеты;
	
КонецФункции

&НаКлиенте
Функция ОписаниеПанелиВычетыНаКлиенте() Экспорт
	Возврат ОписаниеПанелиВычеты();
КонецФункции

&НаСервере
Функция ОписаниеПанелиВычетыНаСервере() Экспорт
	Возврат ОписаниеПанелиВычеты();
КонецФункции
 
&НаСервере
Функция НДФЛПодробнееНаСервере(ФизическоеЛицо) Экспорт
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	НДФЛПодробнее = Новый Массив;
	НДФЛПодробнее.Добавить(ДокументОбъект.Ссылка);
	НДФЛПодробнее.Добавить(УчетНДФЛФормы.РегистрНалоговогоУчетаПоНДФЛ(ДокументОбъект, Модифицированность, ФизическиеЛица, Объект.ПериодРегистрации));
	
	Возврат НДФЛПодробнее;
	
КонецФункции

&НаСервере
Процедура УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов()
	
	Если ЕстьЗаполненныеТабличныеЧасти() Тогда
		ОтображениеПредупреждения = ОтображениеПредупрежденияПриРедактировании.Отображать;
	Иначе
		ОтображениеПредупреждения = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	КонецЕсли;
	
	Элементы.Организация.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупреждения;
	Элементы.Подразделение.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупреждения;
	Элементы.ПериодРегистрации.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупреждения;
	
КонецПроцедуры

&НаСервере
Функция ЕстьЗаполненныеТабличныеЧасти()
	
	ДанныеВТЧЕсть = Ложь;
	
	СписокТабличныхЧастей = СписокТабличныхЧастейДокумента();
	
	Для каждого ИмяТабличнойЧасти Из СписокТабличныхЧастей Цикл
		Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
			ДанныеВТЧЕсть = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ДанныеВТЧЕсть;
	
КонецФункции

&НаСервере
Функция СписокТабличныхЧастейДокумента()
	
	СписокТабличныхЧастей = Новый Массив;
	
	СписокТабличныхЧастей.Добавить("Сотрудники");
	СписокТабличныхЧастей.Добавить("Начисления");
	СписокТабличныхЧастей.Добавить("ЗависимыеНачисления");
	СписокТабличныхЧастей.Добавить("Удержания");
	СписокТабличныхЧастей.Добавить("НДФЛ");
	СписокТабличныхЧастей.Добавить("ПримененныеВычетыНаДетейИИмущественные");
	
	Возврат СписокТабличныхЧастей;
	
КонецФункции

&НаСервере
Процедура ОчиститьТабличныеЧасти()
	
	СписокТабличныхЧастей = СписокТабличныхЧастейДокумента();
	
	Для каждого ИмяТабличнойЧасти Из СписокТабличныхЧастей Цикл
		Объект[ИмяТабличнойЧасти].Очистить();
	КонецЦикла;
	
	УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогНачислено(Форма)
	
	ОписанияСоставляющихНачисления = Новый Массив;
	ОписанияСоставляющихНачисления.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru='премия'")));
	
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Начисления", ОписанияСоставляющихНачисления));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогНачислено", ОписанияСоставляющих);
	
	ОписанияСоставляющихНачисления = Новый Массив;
	ОписанияСоставляющихЗависимыеНачисления = Новый Массив;
	ОписанияСоставляющихЗависимыеНачисления.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru='РК/СН'")));
	
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.ЗависимыеНачисления", ОписанияСоставляющихЗависимыеНачисления));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогЗависимыеНачисления", ОписанияСоставляющих);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогУдержано(Форма)
	
	ОписанияСоставляющихУдержания = Новый Массив;
	ОписанияСоставляющихУдержания.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru='удержания'")));
		
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Удержания", ОписанияСоставляющихУдержания));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогУдержано", ОписанияСоставляющих);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогНДФЛ(Форма)
	
	ОписанияСоставляющихУдержания = Новый Массив;
	ОписанияСоставляющихУдержания.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Налог", НСтр("ru='НДФЛ'")));
	ОписанияСоставляющихУдержания.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("НалогСПревышения", НСтр("ru='НДФЛ с прев. 15%'")));
		
	КолонкиНалогов = УчетНДФЛКлиентСервер.РесурсыИсчисленногоНалогаВМассиве("Налог", Истина, Ложь);
	ВсегоКолонок = КолонкиНалогов.Количество();
	Для Сч = 2 По ВсегоКолонок Цикл
		ИмяКолонки = КолонкиНалогов[Сч - 1];
		Ставка = Прав(ИмяКолонки, 2);
		ОписанияСоставляющихУдержания.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита(ИмяКолонки, 
				СтрШаблон(НСтр("ru='НДФЛ с прев. %1%%'"), Прав(ИмяКолонки, 2))));
	КонецЦикла;
	
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.НДФЛ", ОписанияСоставляющихУдержания));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогНДФЛ", ОписанияСоставляющих);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоДокументу()
	
	РассчитатьИтогНачислено(ЭтотОбъект);
	РассчитатьИтогУдержано(ЭтотОбъект);
	РассчитатьИтогНДФЛ(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСотрудника(Сотрудник)
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		
		Если РассчитыватьДокументыПриРедактировании Тогда
			ОбновитьУдержанияНДФЛСотрудника(Сотрудник);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьМассивСотрудниковСовместителями(МассивСотрудников,Сотрудник)
	
	СотрудникиНачисления			= Объект.Начисления.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник");
	СоответствиеСотрудниковФизЛиц	= ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СотрудникиНачисления,"ФизическоеЛицо");
	ФизическоеЛицоСотрудника 		= СоответствиеСотрудниковФизЛиц.Получить(Сотрудник);
	ДобавленСовместитель			= Ложь;
	Для Каждого СтрокаСоответствия ИЗ СоответствиеСотрудниковФизЛиц Цикл
		Если СтрокаСоответствия.Ключ = Сотрудник Тогда
			Продолжить;
		ИначеЕсли СтрокаСоответствия.Значение = ФизическоеЛицоСотрудника Тогда
			МассивСотрудников.Добавить(СтрокаСоответствия.Ключ);
			ДобавленСовместитель = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ДобавленСовместитель Тогда
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивСотрудников);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРасчетыСотрудников(МассивСотрудников, МассивСотрудниковНДФЛ)
	
	Если МассивСотрудниковНДФЛ = Неопределено Тогда
		МассивСотрудниковНДФЛ = Новый Массив;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудниковНДФЛ, МассивСотрудников, Истина);
	
	Если МассивСотрудниковНДФЛ.Количество() <> 0 Тогда
		ОбновитьНДФЛСотрудников(МассивСотрудниковНДФЛ);
	КонецЕсли;
	
	Если МассивСотрудников <> Неопределено
		И МассивСотрудников.Количество() <> 0 Тогда
		ОбновитьУдержанияСотрудников(МассивСотрудников);
	КонецЕсли;
	
	ОбновитьСтрокуСотрудника();
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьУдержанияНДФЛСотрудника(Сотрудник)
	
	Если ОбновляемыеСотрудники <> Неопределено Тогда
		МассивСотрудников = Новый Массив(ОбновляемыеСотрудники);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСотрудников, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), Истина);
	Иначе
		МассивСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник);
	КонецЕсли;
	
	ДополнитьМассивСотрудниковСовместителями(МассивСотрудников,Сотрудник);
	
	ОбновитьУдержанияНДФЛСотрудников(МассивСотрудников);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьУдержанияНДФЛСотрудников(МассивСотрудников = Неопределено)
	
	Если МассивСотрудников = Неопределено Тогда
		МассивСотрудников = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Сотрудник");
	КонецЕсли;
	
	ОбновитьНДФЛСотрудников(МассивСотрудников);
	ОбновитьУдержанияСотрудников(МассивСотрудников);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьУдержанияСотрудников(МассивСотрудников = Неопределено)
	
	РезультатНачисления = Объект.Начисления.Выгрузить(Новый Структура("НомерСтроки", 0));
	РезультатНачисления.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	
	Отбор = Новый Структура("Сотрудник");
	Для каждого Сотрудник Из МассивСотрудников Цикл
		
		Отбор.Сотрудник = Сотрудник;
		ТаблицаНачислений = Объект.Начисления.Выгрузить(Отбор);
		Для Каждого СтрокаТаблицы Из ТаблицаНачислений Цикл
			НоваяСтрока = РезультатНачисления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Начисление = Объект.ВидПремии;
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Объект.ЗависимыеНачисления.Выгрузить(Отбор), РезультатНачисления);
		
	КонецЦикла;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ЗаполнитьРезультатУдержаний(Объект, РезультатНачисления);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНДФЛСотрудников(МассивСотрудников)
	
	Документы.Премия.ОбновитьНДФЛСотрудников(Объект, ПолныйМассивСотрудников(МассивСотрудников));
	
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаСервере
Функция ПолныйМассивСотрудников(Сотрудники)
	
	Сотрудники = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Сотрудники);
	ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо");
	
	МассивФизическихЛиц = Новый Массив;
	Для Каждого СотрудникФизическогоЛица ИЗ ФизическиеЛицаСотрудников Цикл
		МассивФизическихЛиц.Добавить(СотрудникФизическогоЛица.Значение);
	КонецЦикла;
	
	ТаблицаСотрудников = Документы.Премия.СотрудникиФизическихЛиц(Объект, МассивФизическихЛиц);
	
	Возврат ТаблицаСотрудников.ВыгрузитьКолонку("Сотрудник");
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьНДФЛНаКлиенте()
	
	Если Не РассчитыватьДокументыПриРедактировании Тогда
		
		СтрокиКРасчету.Вставить("НДФЛ", Новый Массив);
		
		УстановитьОтображениеКнопкиПересчитать();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНДФЛСотрудников()
	
	ПерезаполнитьНДФЛ = Не Объект.КорректироватьНДФЛ;
	
	Если ПерезаполнитьНДФЛ Тогда
		
		ОбновитьНДФЛСотрудников(ОбщегоНазначения.ВыгрузитьКолонку(Объект.Сотрудники, "Сотрудник", Истина));
		
	КонецЕсли;
	
	ОбновитьСтрокуСотрудника();
	РассчитатьИтогиПоДокументу();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВторичныеДанныеТабличныхЧастей()
	
	ФизическиеЛицаСотрудников = Новый Соответствие;
	ВыбранныеФизическиеЛица   = Новый Соответствие;
	
	ВыбранныеСотрудники = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Сотрудник", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВыбранныеСотрудники, ОбщегоНазначения.ВыгрузитьКолонку(Объект.ЗависимыеНачисления, "Сотрудник", Истина));
	
	ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВыбранныеСотрудники, "ФизическоеЛицо");
	
	Для каждого СтрокаНачисления Из Объект.Начисления Цикл
		СтрокаНачисления.ФизическоеЛицо = ФизическиеЛицаСотрудников[СтрокаНачисления.Сотрудник];
	КонецЦикла;
	
	Для каждого СтрокаНачисления Из Объект.ЗависимыеНачисления Цикл
		СтрокаНачисления.ФизическоеЛицо = ФизическиеЛицаСотрудников[СтрокаНачисления.Сотрудник];
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УчетРасходовРедактированиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект, Результат);
		ОбновитьПредставлениеУчетаРасходов(ЭтотОбъект);
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПредставлениеУчетаРасходов(Форма)
	
	Если НЕ Форма.ПользовательУправляетСчетамиУчета Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	Если НЕ ЗначениеЗаполнено(Объект.СпособОтраженияЗарплатыВБухучете) Тогда
		Форма.БухучетЗарплатыСотрудниковПредставление = НСтр("ru = '<Авто>'");
	Иначе
		Форма.БухучетЗарплатыСотрудниковПредставление = Строка(Объект.СпособОтраженияЗарплатыВБухучете);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = 0;
	ЗарплатаКадрыПереопределяемый.СостояниеДокумента(Объект, СостояниеДокумента);
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеУдержанияОкончание(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Параметры.Вставить("Удержание", Результат);
		
		ОткрытьФормуВводаУдержания(Параметры);
			
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеУдержания(ПараметрыВвода)

	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьВсеУдержанияОкончание", ЭтотОбъект, ПараметрыВвода);
	
	ОткрытьФорму("ПланВидовРасчета.Удержания.ФормаВыбора",
		,
		ЭтотОбъект,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВводаУдержания(Параметры)
	
	ОткрытьФормуВводаРасчета(Параметры, "ФормаДобавитьУдержание");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВводаРасчета(Параметры, ИмяФормы)
	
	АдресПараметровВХранилище = ПоместитьВоВременноеХранилище(Параметры, УникальныйИдентификатор);

	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресПараметровВХранилище", АдресПараметровВХранилище);
	
	ОткрытьФорму("Документ.Премия.Форма." + ИмяФормы, 
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьСтроку(Направление)
	
	УчетЗарплатыКлиент.ПереместитьСтроку(ЭтотОбъект, Направление);
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьСтроки(Направление)
	
	Объект.Сотрудники.Сортировать("Сотрудник " + Направление + ", Совместитель");
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокОбновляемыхРаботников(Знач ИдентификаторыСтрок)
	
	СписокОбновляемыхРаботников(ИдентификаторыСтрок, "ОбновляемыеСотрудники",     "Сотрудник");
	СписокОбновляемыхРаботников(ИдентификаторыСтрок, "ОбновляемыеФизическиеЛица", "ФизическоеЛицо");
	
КонецПроцедуры

&НаСервере
Процедура СписокОбновляемыхРаботников(Знач ИдентификаторыСтрок, ИмяПриемника, ИмяРеквизита)
	
	РаботникиКОбновлению = Новый Соответствие;
	
	Если ТипЗнч(ИдентификаторыСтрок) = Тип("Массив") Тогда
		СписокИдентификаторовСтрок = ИдентификаторыСтрок;
	Иначе
		СписокИдентификаторовСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторыСтрок);
	КонецЕсли;
	
	Для Каждого ИдентификаторСтроки Из СписокИдентификаторовСтрок Цикл
		
		СтрокаРаботника = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если СтрокаРаботника <> Неопределено И ЗначениеЗаполнено(СтрокаРаботника[ИмяРеквизита]) Тогда
			РаботникиКОбновлению.Вставить(СтрокаРаботника[ИмяРеквизита], Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭтотОбъект[ИмяПриемника] <> Неопределено Тогда
		
		СписокОбновляемых = Новый Массив(ЭтотОбъект[ИмяПриемника]);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокОбновляемых, ОбщегоНазначения.ВыгрузитьКолонку(РаботникиКОбновлению, "Ключ"), Истина);
		
	Иначе
		СписокОбновляемых = ОбщегоНазначения.ВыгрузитьКолонку(РаботникиКОбновлению, "Ключ");
	КонецЕсли;
	
	ЭтотОбъект[ИмяПриемника] = Новый ФиксированныйМассив(СписокОбновляемых);
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьСписокОбновляемыхФизическихЛиц(ФизическоеЛицоКОбновлению)
	
	ФизическиеЛицаКОбновлению = Новый Соответствие;
	ФизическиеЛицаКОбновлению.Вставить(ФизическоеЛицоКОбновлению);
	
	Если ОбновляемыеФизическиеЛица <> Неопределено Тогда
		
		СписокОбновляемых = Новый Массив(ОбновляемыеФизическиеЛица);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокОбновляемых, ОбщегоНазначения.ВыгрузитьКолонку(ФизическиеЛицаКОбновлению, "Ключ"), Истина);
		
	Иначе
		СписокОбновляемых = ОбщегоНазначения.ВыгрузитьКолонку(ФизическиеЛицаКОбновлению, "Ключ");
	КонецЕсли;
	
	ОбновляемыеФизическиеЛица = Новый ФиксированныйМассив(СписокОбновляемых);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНачислениям()
	
	Оповещение = Новый ОписаниеОповещения("РедактированиеНачисленийЗавершение", ЭтотОбъект);
	
	Сотрудник      = Элементы.Сотрудники.ТекущиеДанные.Сотрудник;
	ФизическоеЛицо = Элементы.Сотрудники.ТекущиеДанные.ФизическоеЛицо;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресПараметровВХранилище",  АдресПараметровВХранилище(Сотрудник, ФизическоеЛицо, "Начисления,ЗависимыеНачисления"));
	ПараметрыОткрытия.Вставить("ТолькоПросмотр",             ТолькоПросмотр);
	
	ОткрытьФорму("Документ.Премия.Форма.ФормаНачисления", 
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНДФЛ()
	
	Сотрудник      = Элементы.Сотрудники.ТекущиеДанные.Сотрудник;
	ФизическоеЛицо = Элементы.Сотрудники.ТекущиеДанные.Сотрудник;
	
	ПараметрыФормы = Новый Структура;

	ПараметрыФормы.Вставить("ТолькоПросмотр",                           ТолькоПросмотр);
	ПараметрыФормы.Вставить("Организация",                              Объект.Организация);
	ПараметрыФормы.Вставить("СведенияОбНДФЛ",                           СведенияОбНДФЛ());
	ПараметрыФормы.Вставить("МесяцНачисления",                          Объект.ПериодРегистрации);
	ПараметрыФормы.Вставить("СотрудникФизическоеЛицо",                  Сотрудник);
	ПараметрыФормы.Вставить("ДокументСсылка",                           Объект.Ссылка);
	ПараметрыФормы.Вставить("НеРаспределятьПоИсточникамФинансирования", Истина);
	ПараметрыФормы.Вставить("ДатаПолученияДохода",                      Объект.ПланируемаяДатаВыплаты);
	
	ИндексЗначения = НайтиСтрокиКРасчету(Сотрудник, "Сотрудники");
	Если ИндексЗначения = Неопределено Тогда
		ИндексЗначения = НайтиСтрокиКРасчету(Сотрудник, "НДФЛ");
	КонецЕсли;
	Если ИндексЗначения <> Неопределено Тогда
		ПараметрыФормы.Вставить("ТребуетсяПересчет", Истина);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.ПодробнееОРасчетеНДФЛ",
		ПараметрыФормы,
		ЭтотОбъект,
		,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКУдержаниям()
	
	Сотрудник      = Элементы.Сотрудники.ТекущиеДанные.Сотрудник;
	ФизическоеЛицо = Элементы.Сотрудники.ТекущиеДанные.ФизическоеЛицо;
	
	Оповещение = Новый ОписаниеОповещения("РедактированиеУдержанийЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресПараметровВХранилище", АдресПараметровВХранилище(Сотрудник, ФизическоеЛицо, "Удержания"));
	ПараметрыОткрытия.Вставить("ТолькоПросмотр",            ТолькоПросмотр);
	
	ОткрытьФорму("ОбщаяФорма.ФормаРедактированиеУдержаний", 
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция СведенияОбНДФЛ(ВходящиеДанные = Неопределено) Экспорт
	
	Если ВходящиеДанные = Неопределено Тогда
		Форма = ЭтотОбъект;
		ОбъектФормы    = Объект;
		
		ТекущиеДанные  = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
		ФизическоеЛицо = ТекущиеДанные.ФизическоеЛицо;
		Сотрудник      = ТекущиеДанные.Сотрудник;
		
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Иначе
		Форма = ВходящиеДанные;
		ОбъектФормы    = ВходящиеДанные.Объект;
		ФизическоеЛицо = ВходящиеДанные.ФизическоеЛицо;
		Сотрудник      = ВходящиеДанные.Сотрудник;
		
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ВходящийОбъект", Новый ОписаниеТипов("ДокументОбъект.Премия")));
		МассивИменРеквизитовФормы = Новый Массив;
		ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
		ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
		КопироватьДанныеФормы(ОбъектФормы, ЭтотОбъект["ВходящийОбъект"]);
		
		ДокументОбъект = РеквизитФормыВЗначение("ВходящийОбъект");
	КонецЕсли;
	
	ПланируемаяДатаВыплатыРасчета = '00010101';
	
	ПланируемаяДатаВыплатыРасчета = Объект.ПланируемаяДатаВыплаты;
	Если Не ЗначениеЗаполнено(ПланируемаяДатаВыплатыРасчета) Тогда
		ПланируемаяДатаВыплатыРасчета = КонецМесяца(ОбъектФормы.ПериодРегистрации);
	КонецЕсли;
	
	ДополнительныеСведения = УчетНДФЛФормы.ДополнительныеДанныеДляПолученияСведенийОДоходахНДФЛДокумента();
	ДополнительныеСведения.МесяцНачисления        = ОбъектФормы.ПериодРегистрации;
	ДополнительныеСведения.ПланируемаяДатаВыплаты = ПланируемаяДатаВыплатыРасчета;
	
	
	ДопПараметрыЗапроса = Новый Структура("Начисление", ОбъектФормы.ВидПремии);
	ПараметрыЗапроса = Новый Структура("Начисления", ДопПараметрыЗапроса);
	СведенияОДоходахНДФЛ = УчетНДФЛФормы.СведенияОДоходахНДФЛДокумента(Объект, "Начисления,ЗависимыеНачисления", ДополнительныеСведения, ФизическоеЛицо, ПараметрыЗапроса);
	АдресСведенийОбНДФЛ  = УчетНДФЛФормы.СведенияОбНДФЛ(Форма, ФизическоеЛицо);
	
	ДанныеОбНДФЛ = ПолучитьИзВременногоХранилища(АдресСведенийОбНДФЛ);
	ДанныеОбНДФЛ.Вставить("СведенияОДоходах",      СведенияОДоходахНДФЛ.СведенияОДоходах);
	ДанныеОбНДФЛ.Вставить("ВычетыКДоходам",        СведенияОДоходахНДФЛ.ВычетыКДоходам);
	ДанныеОбНДФЛ.Вставить("Сотрудник",             Сотрудник);
	ДанныеОбНДФЛ.Вставить("СериализованныйОбъект", ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(ДокументОбъект));
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеОбНДФЛ, УникальныйИдентификатор);
	
КонецФункции

#Область РаботаВоВспомогательныхФормах

&НаСервере
Функция АдресПараметровВХранилище(Сотрудник, ФизическоеЛицо, ИменаТаблиц)
	
	МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаТаблиц, ",", , Истина);
	МассивСтрок = Новый Массив;
	
	Для Каждого ИмяТаблицы ИЗ МассивИмен Цикл
		
		Если ИмяТаблицы = "Начисления" Тогда
			ЗначениеОтбора = Сотрудник;
			ИмяОтбора = "Сотрудник";
		ИначеЕсли ИмяТаблицы = "ЗависимыеНачисления" Тогда
			ЗначениеОтбора = Сотрудник;
			ИмяОтбора = "Сотрудник";
		ИначеЕсли ИмяТаблицы = "НДФЛ" Тогда
			ЗначениеОтбора = ФизическоеЛицо;
			ИмяОтбора = "ФизическоеЛицо";
		ИначеЕсли ИмяТаблицы = "Удержания" Тогда
			ЗначениеОтбора = ФизическоеЛицо;
			ИмяОтбора = "Сотрудник";
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрок, Объект[ИмяТаблицы].НайтиСтроки(Новый Структура(ИмяОтбора, ЗначениеОтбора)));
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		Подразделение = Объект.Подразделение;
	Иначе
		Подразделение = ПодразделениеСотрудника(Сотрудник, Объект.ПериодРегистрации);
	КонецЕсли;
	
	ДопСведенияОСотруднике = "";
	МассивЗаписей = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
	Если МассивЗаписей.Количество() > 0 Тогда
		ДопСведенияОСотруднике = МассивЗаписей[0].СведенияОСотруднике;
	КонецЕсли;
	
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("РассчитыватьДокументыПриРедактировании", РассчитыватьДокументыПриРедактировании);
	ПараметрыРасчета.Вставить("СериализованныйОбъект",                  ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(РеквизитФормыВЗначение("Объект")));
	ПараметрыРасчета.Вставить("Ссылка",                                 Объект.Ссылка);
	ПараметрыРасчета.Вставить("Организация",                            Объект.Организация);
	ПараметрыРасчета.Вставить("Подразделение",                          Подразделение);
	ПараметрыРасчета.Вставить("МесяцНачисления",                        Объект.ПериодРегистрации);
	ПараметрыРасчета.Вставить("Сотрудник",                              Сотрудник);
	ПараметрыРасчета.Вставить("ФизическоеЛицо",                         ФизическоеЛицо);
	ПараметрыРасчета.Вставить("ДопСведенияОСотруднике",                 ДопСведенияОСотруднике);
	ПараметрыРасчета.Вставить("МассивСтрок",                            Новый ФиксированныйМассив(МассивСтрок));
	ПараметрыРасчета.Вставить("ДатаДокумента",                          Объект.Дата);
	ПараметрыРасчета.Вставить("ВидПремии",                              Объект.ВидПремии);
	
	Возврат ПоместитьВоВременноеХранилище(ПараметрыРасчета, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура РедактированиеНачисленийЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатРедактирования)
		И ТипЗнч(РезультатРедактирования) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		ДополнитьСтрокиКРасчету(РезультатРедактирования, "Сотрудники");
		УстановитьОтображениеКнопкиПересчитать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеУдержанийЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если НЕ ПустаяСтрока(РезультатРедактирования) Тогда
		ОбновитьДанныеУдержанийНаСервере(РезультатРедактирования);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ВыплатаДокумента

&НаКлиенте
Процедура ВопросПередВыплатойСледуетПровестиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		Записать();
	КонецЕсли;
	
	ДокументПроведен = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Если ДокументПроведен Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Оповестить("ВыполненаЗаписьДокумента", Новый Структура("ДокументСсылка", Объект.Ссылка));
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось провести документ'"));
		Возврат;
	КонецЕсли;
	
	СформироватьДокументыВыплаты();

КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокументыВыплаты()
	
	Если МожноСоздатьВедомостиПоРасчетномуДокументу(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",             Объект.Организация);
		ПараметрыФормы.Вставить("РасчетныйДокумент",       Объект.Ссылка);
		ПараметрыФормы.Вставить("РежимВыплатыПоДокументу", Истина);
		ПараметрыФормы.Вставить("ПериодСобытия",           Объект.ПериодРегистрации);
		ПараметрыФормы.Вставить("ДатаВыплаты",             Объект.ПланируемаяДатаВыплаты);
		
		ОткрытьФорму("Обработка.ПомощникУчетаЗарплаты.Форма.Форма", ПараметрыФормы, Объект.Ссылка); 
	КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент)
	
	МожноСоздатьВедомости = Истина;
	ВзаиморасчетыССотрудникамиПереопределяемый.МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент, МожноСоздатьВедомости);
	Возврат МожноСоздатьВедомости;
	
КонецФункции

#КонецОбласти

#Область Инициализация

СотрудникиКРасчету = Новый Соответствие;
СтрокиКРасчету = Новый Соответствие;
ЗакрытьПослеЗаписи = Ложь;

#КонецОбласти
