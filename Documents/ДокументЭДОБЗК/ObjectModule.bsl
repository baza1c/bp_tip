#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ДополнительныеСвойства.Свойство("НеПроверятьЭлектронныйДокумент") Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ЭлектронныйДокумент");
	КонецЕсли;
	
	Если Не ПроизвольнаяТаблица Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "НаименованиеДокумента");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		
		Если ДополнительныеСвойства.Свойство("СсылкаНового") Тогда
			СсылкаНового = ДополнительныеСвойства.СсылкаНового;
			УстановитьСсылкуНового(СсылкаНового);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ИдентификаторДокумента) Тогда
			ИдентификаторДокумента = Строка(ЭлектронныйДокумент.УникальныйИдентификатор());
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Ответственный) Тогда
			
			ЗаполняемыеЗначения = Новый Структура("Ответственный");
			ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗаполняемыеЗначения);
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗаполняемыеЗначения);
			
		КонецЕсли;
		
		Если ПустаяСтрока(Расширение) И ЗначениеЗаполнено(ЭлектронныйДокумент) Тогда
			УстановитьПривилегированныйРежим(Истина);
			ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
			ДополнительныеПараметры.ПолучатьСсылкуНаДвоичныеДанные = Ложь;
			ДанныеФайла = РаботаСФайлами.ДанныеФайла(ЭлектронныйДокумент, ДополнительныеПараметры);
			Расширение = ДанныеФайла.Расширение;
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
		
	Иначе
		Если ПометкаУдаления И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления") Тогда
			Если КатегорияДокумента = Перечисления.КатегорииДокументовЭДОБЗК.РасчетныйЛисток
				И ИнтеграцияКабинетСотрудника.ФайлРасчетногоЛисткаОпубликован(Ссылка) Тогда
				
				ТекстСообщения = НСтр("ru = 'Не предусмотрено удаление файлов расчетных листов, их содержимое меняется при обновлении публикации.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
			ИначеЕсли Не Пользователи.ЭтоПолноправныйПользователь(, , Ложь)
				И Не ДополнительныеСвойства.Свойство("РазрешенаПометкаУдаления") Тогда
				
				ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Пометка на удаление объектов ""%1"" не предусмотрена'"),
					Метаданные().Синоним);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
			Иначе
				Если ЭлектронныйДокумент <> Неопределено И
					ЭлектронныйДокумент.Метаданные() <> Метаданные.Документы.ДокументЭДОБЗК Тогда
					
					Если Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлектронныйДокумент, "ПометкаУдаления") Тогда
						ОбъектФайла = ЭлектронныйДокумент.ПолучитьОбъект();
						ОбъектФайла.ДополнительныеСвойства.Вставить("УдалениеПечатныхФорм", Истина);
						ОбъектФайла.УстановитьПометкуУдаления(Истина);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		БизнесПроцессыИЗадачиБЗК.ПометитьНаУдалениеБизнесПроцессыПредмета(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("ФизическоеЛицо")
			И ЗначениеЗаполнено(ДанныеЗаполнения.ФизическоеЛицо) Тогда
			
			Если ТипЗнч(ДанныеЗаполнения.ФизическоеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				СтрокаСотрудника = ВнешниеПодписанты.Добавить();
				СтрокаСотрудника.ФизическоеЛицо = ДанныеЗаполнения.ФизическоеЛицо;
			ИначеЕсли ТипЗнч(ДанныеЗаполнения.ФизическоеЛицо) = Тип("Массив")
				Или ТипЗнч(ДанныеЗаполнения.ФизическоеЛицо) = Тип("ФиксированныйМассив") Тогда
				
				Для Каждого ФизическоеЛицо Из ДанныеЗаполнения.ФизическоеЛицо Цикл
					Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
						СтрокаСотрудника = ВнешниеПодписанты.Добавить();
						СтрокаСотрудника.ФизическоеЛицо = ФизическоеЛицо;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользуетсяЭДОБЗККабинетСотрудника") Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВнешниеПодписанты", ВнешниеПодписанты.ВыгрузитьКолонку("ФизическоеЛицо"));
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
			|	ФизическиеЛица.Пол КАК Пол,
			|	ФизическиеЛица.ФИО КАК ФИО
			|ИЗ
			|	Справочник.ФизическиеЛица КАК ФизическиеЛица
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъектыКабинетСотрудника
			|		ПО ФизическиеЛица.Ссылка = ВыгружаемыеОбъектыКабинетСотрудника.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом КАК ВыгруженныеОбъектыУправлениеПерсоналом
			|		ПО ФизическиеЛица.Ссылка = ВыгруженныеОбъектыУправлениеПерсоналом.Ссылка
			|ГДЕ
			|	ФизическиеЛица.Ссылка В(&ВнешниеПодписанты)
			|	И ВыгружаемыеОбъектыКабинетСотрудника.Ссылка ЕСТЬ NULL
			|	И ВыгруженныеОбъектыУправлениеПерсоналом.Ссылка ЕСТЬ NULL
			|
			|УПОРЯДОЧИТЬ ПО
			|	ФизическиеЛица.Наименование";
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		Пока Выборка.Следующий() Цикл
			
			ПредставлениеСотрудник = НСтр("ru = 'сотрудника'");
			Пол = 1;
			Если Выборка.Пол = Перечисления.ПолФизическогоЛица.Женский Тогда
				ПредставлениеСотрудник = НСтр("ru = 'сотрудницы'");
				Пол = 2;
			КонецЕсли;
			ФИО = ЗарплатаКадрыОтчеты.ПросклоненныеФИО(Выборка.ФИО, 2, Пол);
			ВызватьИсключение СтрШаблон(НСтр("ru = 'У %1 %2 отсутсвует личный каюбинет'"), ПредставлениеСотрудник, ФИО);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли