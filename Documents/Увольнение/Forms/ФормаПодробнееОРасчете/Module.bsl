
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Параметры.АдресПараметровВХранилище) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// "Распаковываем" параметры
	ПараметрыРасчета = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВХранилище);
	
	ДесериализованныйОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(ПараметрыРасчета.СериализованныйОбъект);
	ЗначениеВДанныеФормы(ДесериализованныйОбъект, Объект);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРасчета);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Объект, , "СреднийЗаработокОбщий");
	
	ГодГодовыхПремий = РасчетЗарплатыДляНебольшихОрганизаций.ГодГодовыхПремий(ДатаНачалаСобытия);
	ВидыГодовыхПремий = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии());
	ВидыПремийПроцентом = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ПремииПроцентом());
	
	ТаблицаНачислений = Объект.СреднийЗаработокОбщий.Выгрузить();
	
	ВидыПремий = Новый ФиксированныйМассив(Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	СоставныеЧасти = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНачислений, "СоставнаяЧасть", Истина);
	Для Каждого СоставнаяЧасть Из СоставныеЧасти Цикл
		Если ВидыПремий.Найти(СоставнаяЧасть) <> Неопределено Тогда
			ЕстьПремии = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПрименятьРайонныйКоэффициент = ПолучитьФункциональнуюОпцию("ПрименятьРайонныйКоэффициент",
		Новый Структура("Организация", Организация));
	ПрименятьСевернуюНадбавку = ПолучитьФункциональнуюОпцию("ПрименятьСевернуюНадбавку",
		Новый Структура("Организация", Организация));
		
	УстановитьПараметрыВыбораНачислений();
	
	// Настройка параметров редактирования таблицы
	Если ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку Или ЕстьПремии Тогда
		Элементы.ТаблицаЗаработкаОсновныеНачисления.ТолькоПросмотр    = Истина;
		Элементы.ТаблицаЗаработкаОсновныеНачисления.ГиперссылкаЯчейки = Истина;
		Элементы.ТаблицаЗаработкаОсновныеНачисления.ЦветТекста        = ЦветаСтиля.ЦветГиперссылки;
	Иначе
		ТаблицаНачислений.ЗаполнитьЗначения(ПланыВидовРасчета.Начисления.ПустаяСсылка(), "Начисление");
		ТаблицаНачислений.Свернуть("Сотрудник,Период,Начисление,СоставнаяЧасть,Год,ДатаНачалаБазовогоПериода,КоличествоМесяцев", "Сумма");
	КонецЕсли;
	
	ТаблицаВремени = Объект.ОтработанноеВремяДляСреднегоОбщий.Выгрузить();
	
	// Заполним дату приема на работу для ограничения ввода данных о среднем заработке.
	
	ПрочитатьДанныеСотрудника();
	Если Не ЗначениеЗаполнено(ДатаПриемаНаРаботуСотрудника) Тогда
		// Проблемы с датой приема на работу.
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДополнитьТаблицуНачисления();
	
	СтруктураОтбора = Новый Структура("КатегорияНачисленияИлиНеоплаченногоВремени");
	СтруктураОтбора.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда;
	НачисленияОклад.ЗагрузитьЗначения(ПланыВидовРасчета.Начисления.НачисленияПоОтбору(СтруктураОтбора));
	
	// Инициализируем соответствие по умолчанию.
	МесяцыРасчета = Новый ФиксированноеСоответствие(Новый Соответствие);
	
	// Заполняем период расчета среднего заработка.
	ЗаполнитьПериодРасчетаПоУмолчанию();
	// Заполним соответствие месяца и номера его колонки.
	ЗаполнитьМесяцыРасчета(ЭтаФорма);
	
	УстановитьЗаголовокФормы();
	
	УстановитьОтображениеЭлементовУправления(ТаблицаВремени);
	
	СреднийЗаработокОбщий.Загрузить(ТаблицаНачислений);
	ЗаполнитьФорму(ТаблицаНачислений, ТаблицаВремени);
	
	ПолучитьДанныеРасчетаСреднегоЗаработка();
	УстановитьРасширеннуюПодсказку();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПеречитатьДанныеУчета",
		"Видимость",
		Не ТолькоПросмотр);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГодовыеПремииГруппа",
		"Заголовок",
		СтрШаблон(НСтр("ru = 'Годовые премии за %1 год'"), Формат(ГодГодовыхПремий, "ЧГ=")));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГодовыеПремииГруппа",
		"Видимость",
		ЕстьПремии);
	
	Элементы.ГруппаКнопокПросмотр.Видимость       = ТолькоПросмотр;
	Элементы.ГруппаКнопокРедактирование.Видимость = НЕ ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ФормаОК.КнопкаПоУмолчанию      = Истина;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИдентификаторВладельца = ВладелецФормы.УникальныйИдентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		ВопросСохранитьИзменения(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДнейКомпенсацииУдержанияОтпускаПриИзменении(Элемент)
	
	РассчитатьНачисления();
	
КонецПроцедуры


&НаКлиенте
Процедура ПремияПроцентомЗаГодПриИзменении(Элемент)
	
	ПремияПриИзменении("ПремияГодоваяПроцентом");
	
КонецПроцедуры

&НаКлиенте
Процедура ПремияСуммойЗаГодПриИзменении(Элемент)
	
	ПремияПриИзменении("ПремияГодоваяФиксированнойСуммой");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если НЕ Модифицированность ИЛИ ТолькоПросмотр Тогда
		Закрыть();
	Иначе
		Если ПроверитьЗаполнениеРеквизитов() Тогда
			ПодготовитьФормуКПринятиюИзменений();
			Закрыть(АдресВХранилище);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьДанныеУчета(Команда)
	
	Если Объект.СреднийЗаработок <> 0 Тогда
		
		ТекстВопроса = НСтр("ru='Перед заполнением введенные данные будут очищены.
			|Продолжить?'");
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеречитатьДанныеУчетаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		ПеречитатьДанныеУчетаЗавершение(КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйЛисток(Команда)
	
	Если Модифицированность Тогда
		ВопросСохранитьИзменения(Ложь);
	Иначе
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаЗаработка

&НаКлиенте
Процедура ТаблицаЗаработкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элементы.ТаблицаЗаработка.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ТаблицаЗаработкаОсновныеНачисления" Тогда
		Если ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку Или ЕстьПремии Тогда
			ОтредактироватьСреднийЗаработок(СтрокаТаблицы.Период);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаработкаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	СтрокаТаблицы = Элементы.ТаблицаЗаработка.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы.КалендарныхДней = Мин(СтрокаТаблицы.КалендарныхДней , СтрокаТаблицы.МаксимумДней);
	Если СтрокаТаблицы.КалендарныхДней = 0 Тогда
		СтрокаТаблицы.УчтеноДней = 0;
	КонецЕсли;
	
	Если Не ПрименятьРайонныйКоэффициент И Не ПрименятьСевернуюНадбавку Тогда
		ДанныеНачислений = Новый Структура();
		ДанныеНачислений.Вставить("Период", СтрокаТаблицы.Период);
		ДанныеНачислений.Вставить("Сумма",  СтрокаТаблицы.ОсновныеНачисления);
		ПерезаполнитьИПересчитать(СтрокаТаблицы.Период, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеНачислений));
	Иначе
		Пересчитать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНачисления

&НаКлиенте
Процедура НачисленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Начисления.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.Сотрудник <> Объект.Сотрудник Тогда
			ТекущиеДанные.Сотрудник = Объект.Сотрудник;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ТекущиеДанные.Подразделение) Тогда
			ТекущиеДанные.Подразделение = Подразделение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Начисления.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РедактируетсяКомпенсация = ТекущиеДанные.Начисление = НачислениеКомпенсацияОтпуска
		Или ТекущиеДанные.Начисление = НачислениеКомпенсацияОтпускаРК
		Или ТекущиеДанные.Начисление = НачислениеКомпенсацияОтпускаСН;
		
	Если РедактируетсяКомпенсация Тогда
		ТекущиеДанные.ДатаНачала	= Объект.ДатаУвольнения;
		ТекущиеДанные.ДатаОкончания	= Объект.ДатаУвольнения;
	Иначе
		ТекущиеДанные.ДатаНачала	= Объект.ПериодРегистрации;
		ТекущиеДанные.ДатаОкончания	= Объект.ДатаУвольнения;
	КонецЕсли;
	
	НачисленияПриОкончанииРедактированияНаСервере(Не РедактируетсяКомпенсация);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПослеУдаления(Элемент)
	
	НачисленияПриОкончанииРедактированияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура НачисленияПриОкончанииРедактированияНаСервере(ПересчитыватьСреднийЗаработок = Истина)
	
	Если КонецДня(Объект.ДатаУвольнения) = КонецМесяца(Объект.ДатаУвольнения) И ПересчитыватьСреднийЗаработок Тогда
		ПересчитатьСреднийЗаработокПоследнегоМесяца();
	КонецЕсли;
	
	Документы.Увольнение.ПересчитатьНачисленияСКоэффициентом(Сотрудник,  Объект.Начисления);
	
КонецПроцедуры

&НаСервере
Процедура НачислениеПриПриИзмененииНаСервере()
	
	ИдентификаторСтроки = Элементы.Начисления.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	СтрокаНачислений = Объект.Начисления.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если СтрокаНачислений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаНачислений.Начисление) Тогда
		ДанныеНачисления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
												СтрокаНачислений.Начисление,
												"КатегорияНачисленияИлиНеоплаченногоВремени,ВходитВБазуРКИСН");
												
			КатегорияНачисления = ДанныеНачисления.КатегорияНачисленияИлиНеоплаченногоВремени;
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда
				Или КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее
				И ДанныеНачисления.ВходитВБазуРКИСН Тогда
				СтрокаНачислений.ВходитВБазуРКиСН = Истина;
			КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияОплаченоДнейПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Начисления.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ОплаченоДней
		= Мин(ТекущиеДанные.ОплаченоДней,
			ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Объект.ДатаУвольнения));
			
	ТекущиеДанные.ОплаченоЧасов = ТекущиеДанные.ОплаченоДней * 8;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачислениеПриИзменении(Элемент)
	
	НачислениеПриПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачислениеОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьДанныеСотрудника()
	
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		
		КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Объект.Сотрудник,
			"ГоловнаяОрганизация,ДатаПриема,ПроцентСевернойНадбавки", Объект.ДатаУвольнения);
		Если КадровыеДанные.Количество() > 0 Тогда
			
			ДатаПриемаНаРаботуСотрудника = КадровыеДанные[0].ДатаПриема;
			ПроцентСевернойНадбавки      = КадровыеДанные[0].ПроцентСевернойНадбавки;
			
			РайонныйКоэффициент = 1;
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ГоловнаяОрганизация", КадровыеДанные[0].ГоловнаяОрганизация);
			Запрос.УстановитьПараметр("Организация",         Организация);
			Запрос.УстановитьПараметр("Подразделение",       Подразделение);
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ВЫБОР
			|		КОГДА ПодразделенияОрганизаций.РайонныйКоэффициент > 1
			|			ТОГДА ПодразделенияОрганизаций.РайонныйКоэффициент
			|		КОГДА Организации.РайонныйКоэффициент > 1
			|			ТОГДА Организации.РайонныйКоэффициент
			|		КОГДА ГоловныеОрганизации.РайонныйКоэффициент > 1
			|			ТОГДА ГоловныеОрганизации.РайонныйКоэффициент
			|		ИНАЧЕ 1
			|	КОНЕЦ КАК РайонныйКоэффициент
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций,
			|	Справочник.Организации КАК ГоловныеОрганизации,
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	ГоловныеОрганизации.Ссылка = &ГоловнаяОрганизация
			|	И Организации.Ссылка = &Организация
			|	И ПодразделенияОрганизаций.Ссылка = &Подразделение";
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				РайонныйКоэффициент = Выборка.РайонныйКоэффициент;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьТаблицуНачисления()
	
	Начисления = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Начисления, "Начисление", Истина);
	ДанныеНачислений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Начисления, "КатегорияНачисленияИлиНеоплаченногоВремени,ВходитВБазуРКИСН");
	
	Для Каждого СтрокаНачисления Из Объект.Начисления Цикл
		
		Если ЗначениеЗаполнено(СтрокаНачисления.Начисление) Тогда
			
			ДанныеНачисления = ДанныеНачислений[СтрокаНачисления.Начисление];
			
			КатегорияНачисления = ДанныеНачисления.КатегорияНачисленияИлиНеоплаченногоВремени;
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда
				Или КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее
						И ДанныеНачисления.ВходитВБазуРКИСН Тогда
				СтрокаНачисления.ВходитВБазуРКиСН = Истина;
			КонецЕсли;
			
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент Тогда
				СтрокаНачисления.КоэффициентПересчета = РайонныйКоэффициент - 1;
			КонецЕсли;
			Если КатегорияНачисления = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка Тогда
				СтрокаНачисления.КоэффициентПересчета = ПроцентСевернойНадбавки / 100;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область РедактированиеЗаработка

&НаКлиенте
Процедура ОтредактироватьСреднийЗаработок(Месяц)
	
	Если Не ЗначениеЗаполнено(Месяц) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыОткрытия.Вставить("ДанныеНачислений", РезультатРедактированияНачислений(Месяц));
	ПараметрыОткрытия.Вставить("Период", Месяц);
	
	ДополнительныеПараметры = Новый Структура("Месяц", Месяц);
	Оповещение = Новый ОписаниеОповещения("ПриИзмененииСреднегоЗаработка", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.ВводДанныхДляРасчетаСреднегоПодробно", ПараметрыОткрытия, ЭтотОбъект,
		Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСреднегоЗаработка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность = Истина;
		ПерезаполнитьИПересчитать(ДополнительныеПараметры.Месяц, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьИПересчитать(Месяц, ДанныеНачислений)
	
	ЗаменитьДанныеНачислений(Месяц, ДанныеНачислений);
	ЗаполнитьНачисления();
	Пересчитать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьДанныеНачислений(Месяц, ДанныеНачислений);
	НайденныеСтроки = СреднийЗаработокОбщий.НайтиСтроки(Новый Структура("Период", Месяц));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		СреднийЗаработокОбщий.Удалить(НайденнаяСтрока);
	КонецЦикла;
	Если Не ЗначениеЗаполнено(ДанныеНачислений) Тогда
		НоваяСтрока = СреднийЗаработокОбщий.Добавить();
		НоваяСтрока.Период = Месяц;
		НоваяСтрока.Сотрудник = Сотрудник;
	Иначе
		Для Каждого СтрокаНачислений Из ДанныеНачислений Цикл
			НоваяСтрока = СреднийЗаработокОбщий.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачислений);
			НоваяСтрока.Период = Месяц;
			НоваяСтрока.Сотрудник = Сотрудник;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораНачислений()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ФормаДокументаУстановитьПараметрыВыбораНачислений(
			ЭтотОбъект, Организация, Объект.ПериодРегистрации);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Массив(Элементы.НачисленияНачисление.ПараметрыВыбора);
	
	МассивКатегорий = Новый Массив;
	МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда);
	МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.Прочее);
	МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска);
	Если ПрименятьРайонныйКоэффициент Тогда
		МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке);
	КонецЕсли;
	Если ПрименятьСевернуюНадбавку Тогда
		МассивКатегорий.Добавить(Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке);
	КонецЕсли;
	
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.КатегорияНачисленияИлиНеоплаченногоВремени",
		Новый ФиксированныйМассив(МассивКатегорий)));
			
	Если ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку Тогда
		СтруктураОтбора = Новый Структура("КатегорияНачисленияИлиНеоплаченногоВремени",
			Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска);
		НачисленияКомпенсацияОтпуска = Новый ФиксированныйМассив(ПланыВидовРасчета.Начисления.НачисленияПоОтбору(СтруктураОтбора));
			
		МассивОсновныхВидовРасчета = Новый Массив;
		МассивОсновныхВидовРасчета.Добавить(ПланыВидовРасчета.Начисления.ПустаяСсылка());
		Для Каждого ЗначениеМассива Из НачисленияКомпенсацияОтпуска Цикл
			МассивОсновныхВидовРасчета.Добавить(ЗначениеМассива);
		КонецЦикла;
		
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ОсновнойВидРасчета",
			Новый ФиксированныйМассив(МассивОсновныхВидовРасчета)));
	КонецЕсли;
	
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ДоступенПолныйСписок", Истина));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НачисленияНачисление",
		"ПараметрыВыбора",
		Новый ФиксированныйМассив(ПараметрыВыбора));
	
КонецПроцедуры

#КонецОбласти

#Область РасчетСреднегоЗаработка

&НаКлиенте
Процедура ПеречитатьДанныеУчетаЗавершение(Знач Результат, Знач Параметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеречитатьДанныеУчетаНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьДанныеУчетаНаСервере()
	
	ПрочитатьДанныеУчетаСреднегоЗаработка();
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФорму(ДанныеОНачислениях, ДанныеОВремени)
	
	НомерПервогоМесяца = 1 + ДобавляемыеМесяцы;
	
	Для НомерМесяца = НомерПервогоМесяца По МесяцыРасчета.Количество() Цикл
		ИндексМесяца = НомерМесяца-1;
		ТаблицаЗаработка[ИндексМесяца].ОсновныеНачисления = 0;
		ТаблицаЗаработка[ИндексМесяца].КалендарныхДней    = 0;
		ТаблицаЗаработка[ИндексМесяца].УчтеноДней         = 0;
		ТаблицаЗаработка[ИндексМесяца].ОтработаноДнейКалендарных          = 0;
		ТаблицаЗаработка[ИндексМесяца].ОтработаноДнейПятидневка           = 0;
		ТаблицаЗаработка[ИндексМесяца].НормаДнейПроизводственныйКалендарь = 0;
	КонецЦикла;
	
	ЗаполнитьНачисления();
	ЗаполнитьВремя(ДанныеОВремени);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисления()
	
	ДанныеОНачислениях = СреднийЗаработокРасширенный();
	Для Каждого СтрокаНачислений Из ДанныеОНачислениях Цикл
		
		ИндексМесяца = МесяцыРасчета.Найти(НачалоМесяца(СтрокаНачислений.Период));
		Если ИндексМесяца <> Неопределено Тогда
			
			НомерМесяца = ИндексМесяца + ДобавляемыеМесяцы;
			
			ТаблицаЗаработка[НомерМесяца].ОсновныеНачисления = 
				СтрокаНачислений.Сумма;
				
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СреднийЗаработокРасширенный()
	
	СреднийЗаработокГодовыеПремии.Очистить();
	ПремияГодоваяПроцентом = 0;
	ПремияГодоваяФиксированнойСуммой = 0;
	
	ДанныеОНачислениях = СреднийЗаработокОбщий.Выгрузить();
	ДанныеОНачислениях.Колонки.Добавить("СуммаРК", Новый ОписаниеТипов("Число"));
	ДанныеОНачислениях.Колонки.Добавить("СуммаСН", Новый ОписаниеТипов("Число"));
	ДанныеОНачислениях.Колонки.Добавить("СуммаПремии", Новый ОписаниеТипов("Число"));
	
	НачислениеРайонныйКоэффициент = ПланыВидовРасчета.Начисления.НачислениеРайонныйКоэффициент();
	НачислениеСевернаяНадбавка = ПланыВидовРасчета.Начисления.НачислениеСевернаяНадбавка();
	Для Каждого СтрокаНачислений Из ДанныеОНачислениях Цикл
		Если ЗначениеЗаполнено(СтрокаНачислений.СоставнаяЧасть) Тогда
			СтрокаНачислений.СуммаПремии = СтрокаНачислений.Сумма;
			Если ВидыГодовыхПремий.Найти(СтрокаНачислений.СоставнаяЧасть) <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(СреднийЗаработокГодовыеПремии.Добавить(), СтрокаНачислений);
				Если ВидыПремийПроцентом.Найти(СтрокаНачислений.СоставнаяЧасть) <> Неопределено Тогда
					ПремияГодоваяПроцентом = ПремияГодоваяПроцентом + СтрокаНачислений.Сумма;
				Иначе
					ПремияГодоваяФиксированнойСуммой = ПремияГодоваяФиксированнойСуммой + СтрокаНачислений.Сумма;
				КонецЕсли;
				СтрокаНачислений.Сумма = 0;
			КонецЕсли;
		ИначеЕсли СтрокаНачислений.Начисление = НачислениеРайонныйКоэффициент Тогда
			СтрокаНачислений.СуммаРК = СтрокаНачислений.Сумма;
		ИначеЕсли СтрокаНачислений.Начисление = НачислениеСевернаяНадбавка Тогда
			СтрокаНачислений.СуммаСН = СтрокаНачислений.Сумма;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеОНачислениях.Свернуть("Период", "Сумма,СуммаРК,СуммаСН,СуммаПремии");
	
	Возврат ДанныеОНачислениях;
КонецФункции

&НаСервере
Процедура ЗаполнитьВремя(ДанныеВремени)
	
	ПоследнийИзвестныйПериод = Неопределено;
	Для Каждого СтрокаДанныхВремени Из ДанныеВремени Цикл
		
		ИндексМесяца = МесяцыРасчета.Найти(НачалоМесяца(СтрокаДанныхВремени.Период));
		Если ИндексМесяца <> Неопределено Тогда
			
			НомерМесяца = ИндексМесяца + ДобавляемыеМесяцы;
			ТаблицаЗаработка[НомерМесяца].КалендарныхДней = СтрокаДанныхВремени.ОтработаноДнейКалендарных;
			ТаблицаЗаработка[НомерМесяца].ОтработаноДнейКалендарных = СтрокаДанныхВремени.ОтработаноДнейКалендарных;
			ТаблицаЗаработка[НомерМесяца].ОтработаноДнейПятидневка = СтрокаДанныхВремени.ОтработаноДнейПятидневка;
			ТаблицаЗаработка[НомерМесяца].НормаДнейПроизводственныйКалендарь = СтрокаДанныхВремени.НормаДнейПроизводственныйКалендарь;
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеУчетаСреднегоЗаработка(ТолькоМесяцУвольнения = Ложь)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		// Установка транзакции для расчета
		УстановитьПривилегированныйРежим(Истина);
		НачатьТранзакцию();
		Если КонецДня(Объект.ДатаУвольнения) = КонецМесяца(Объект.ДатаУвольнения) Тогда
			ОбъектФормы = РеквизитФормыВЗначение("Объект");
			ОбъектФормы.ОбменДанными.Загрузка = Истина;
			ОбъектФормы.Записать(РежимЗаписиДокумента.Запись);
			ОбъектФормы.ЗарегистрироватьНачисления(,,, Истина);
			ОбъектФормы.ЗарегистрироватьОтработанноеВремя();
		КонецЕсли;
		Если ТолькоМесяцУвольнения Тогда
			ДанныеДляРасчета =
				МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника(
					Сотрудник, Организация, НачалоМесяца(Объект.ДатаУвольнения), КонецДня(Объект.ДатаУвольнения), Истина);
		Иначе
			ДанныеДляРасчета =
				МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника(
					Сотрудник, Организация, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, Истина);
		КонецЕсли;
				
		ОтменитьТранзакцию();
		УстановитьПривилегированныйРежим(Ложь);
		// Отмена расчетной транзакции
		
		Если ТолькоМесяцУвольнения Тогда
			МассивСтрок = СреднийЗаработокОбщий.НайтиСтроки(Новый Структура("Период", НачалоМесяца(Объект.ДатаУвольнения)));
			Для Каждого СтрокаМассива Из МассивСтрок Цикл
				СреднийЗаработокОбщий.Удалить(СреднийЗаработокОбщий.Индекс(СтрокаМассива));
			КонецЦикла;
			Для Каждого СтрокаТаблицы Из ДанныеДляРасчета.ДанныеОНачислениях Цикл
				ЗаполнитьЗначенияСвойств(СреднийЗаработокОбщий.Добавить(), СтрокаТаблицы);
			КонецЦикла;
			ДанныеДляРасчета.ДанныеОНачислениях.Очистить();
			Для Каждого СтрокаТаблицы Из СреднийЗаработокОбщий Цикл
				ЗаполнитьЗначенияСвойств(ДанныеДляРасчета.ДанныеОНачислениях.Добавить(), СтрокаТаблицы);
			КонецЦикла;
			Для Каждого СтрокаТаблицы Из ТаблицаЗаработка Цикл
				Если СтрокаТаблицы.Период <> НачалоМесяца(Объект.ДатаУвольнения) Тогда
					НоваяСтрока = ДанныеДляРасчета.ДанныеОВремени.Добавить();
					НоваяСтрока.Период = СтрокаТаблицы.Период;
					НоваяСтрока.ОтработаноДнейКалендарных = СтрокаТаблицы.КалендарныхДней;
				КонецЕсли;
			КонецЦикла;
		Иначе
			СреднийЗаработокОбщий.Загрузить(ДанныеДляРасчета.ДанныеОНачислениях);
		КонецЕсли;
		ТаблицаЗаработка.Очистить();
		УстановитьОтображениеЭлементовУправления(ДанныеДляРасчета.ДанныеОВремени);
		ЗаполнитьФорму(ДанныеДляРасчета.ДанныеОНачислениях, ДанныеДляРасчета.ДанныеОВремени);
		Пересчитать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьМесяцыРасчета(Форма)
	
	МесяцыРасчета = Новый Массив;
	МесяцОбхода = НачалоМесяца(Форма.НачалоПериодаРасчета);
	Пока МесяцОбхода <= Форма.ОкончаниеПериодаРасчета Цикл
		МесяцыРасчета.Добавить(МесяцОбхода);
		МесяцОбхода = ДобавитьМесяц(МесяцОбхода, 1);
	КонецЦикла;
	Форма.МесяцыРасчета = Новый ФиксированныйМассив(МесяцыРасчета);
	Форма.ДобавляемыеМесяцы = 12 - МесяцыРасчета.Количество();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеЭлементовУправления(ДанныеОВремени)
	
	СчетчикМесяцев = 1;
	
	Если ДобавляемыеМесяцы <> 0 Тогда
		
		Если МесяцыРасчета.Количество() = 0 Тогда
			Месяц = ДобавитьМесяц(НачалоМесяца(НачалоПериодаРасчета), -12);
		Иначе
			Месяц = ДобавитьМесяц(НачалоМесяца(МесяцыРасчета[0]), -ДобавляемыеМесяцы);
		КонецЕсли;
		
		Для СчетчикМесяцев = 1 По ДобавляемыеМесяцы Цикл
			НоваяСтрока = ТаблицаЗаработка.Добавить();
			НоваяСтрока.Номер  = СчетчикМесяцев;
			НоваяСтрока.Месяц  = Формат(Месяц, "ДФ='ММММ'");
			НоваяСтрока.Год    = Формат(Месяц, "ДФ='гггг'");
			НоваяСтрока.Период = НачалоДня(Месяц);
			НоваяСтрока.Пустой = Истина;
			
			Месяц = ДобавитьМесяц(НачалоМесяца(Месяц), 1);
			НоваяСтрока.МаксимумДней = ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Месяц);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого Месяц Из МесяцыРасчета Цикл
		
		НоваяСтрока = ТаблицаЗаработка.Добавить();
		НоваяСтрока.Номер = СчетчикМесяцев;
		НоваяСтрока.Месяц = Формат(Месяц, "ДФ='ММММ'");
		НоваяСтрока.Год   = Формат(Месяц, "ДФ='гггг'");
		НоваяСтрока.Период = НачалоДня(Месяц);
		
		Если ДанныеОВремени.Найти(Месяц, "Период") <> Неопределено  Тогда
			НоваяСтрока.КалендарныхДней = ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Месяц);
		КонецЕсли;
		НоваяСтрока.МаксимумДней = ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Месяц);
		
		СчетчикМесяцев = СчетчикМесяцев + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Заголовок = СтрШаблон(НСтр("ru='Расчет при увольнении (%1)%2'"),
							Сотрудник,
							?(ТолькоПросмотр, НСтр("ru=' (только просмотр)'"), ""));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеСтрокиДанныхОВремени()
	
	// Функция конструирует структуру, описывающую данные о времени 
	// для расчета среднего заработка за один месяц расчетного периода.
	// Поля структуры заполнены пустыми значениями соответствующих типов.
	
	ПоляОписания = Новый Соответствие;
	ПоляОписания.Вставить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ПоляОписания.Вставить("Период", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("ОтработаноДнейКалендарных", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("ОтработаноДнейПятидневка", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("НормаДнейПроизводственныйКалендарь", Новый ОписаниеТипов("Число"));
	
	Описание = Новый Структура;
	Для Каждого КлючИЗначение Из ПоляОписания Цикл
		Описание.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.ПривестиЗначение());
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПериодРасчетаПоУмолчанию()
	
	НачалоПериода = ДобавитьМесяц(НачалоМесяца(ДатаНачалаСобытия), -12);
	ОкончаниеПериода = НачалоМесяца(ДатаНачалаСобытия) - 1;
	
	ПериодРасчета = Новый СтандартныйПериод;
	ПериодРасчета.ДатаНачала = Макс(НачалоПериода, НачалоМесяца(ДатаПриемаНаРаботуСотрудника));
	ПериодРасчета.ДатаОкончания = ОкончаниеПериода;

	НачалоПериодаРасчета = ПериодРасчета.ДатаНачала;
	ОкончаниеПериодаРасчета = ПериодРасчета.ДатаОкончания;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСреднийЗаработок()
	
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		
		ПолучитьДанныеРасчетаСреднегоЗаработка();
		Если ЗначениеЗаполнено(ДанныеРасчетаСреднегоЗаработкаИтоги) Тогда
			Объект.СреднийЗаработок   = ДанныеРасчетаСреднегоЗаработкаИтоги.СреднедневнойЗаработок;
			Объект.СреднийЗаработокРК = ДанныеРасчетаСреднегоЗаработкаИтоги.СреднедневнойЗаработокРК;
			Объект.СреднийЗаработокСН = ДанныеРасчетаСреднегоЗаработкаИтоги.СреднедневнойЗаработокСН;
			СреднийЗаработок          = Объект.СреднийЗаработок;
			
			УстановитьРасширеннуюПодсказку();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеРасчетаСреднегоЗаработка()
	
	СуммаУчтенногоЗаработка = 0;
	ДанныеРасчетаСреднегоЗаработкаИтоги = Неопределено;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		ДанныеНачислений = РезультатРедактированияНачислений();
		ДанныеВремени = РезультатРедактированияВремени(ЭтаФорма);
		
		ТаблицыПоСотруднику = Новый Структура;
		ТаблицыПоСотруднику.Вставить("ДанныеОНачислениях", ДанныеНачислений);
		ТаблицыПоСотруднику.Вставить("ДанныеОВремени", ДанныеВремени);
		
		ПараметрыПолученияДанныхСреднего = МодульРасчетЗарплатыДляНебольшихОрганизаций.ПараметрыПолученияДанныхСреднегоОбщего();
		ПараметрыПолученияДанныхСреднего.Вставить("ТаблицыПоСотруднику", 	ТаблицыПоСотруднику);
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаПериода",  	НачалоПериодаРасчета); 
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаОкончанияПериода",	ОкончаниеПериодаРасчета); 
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаСобытия", 		ДатаНачалаСобытия);		
		
		ДанныеРасчетаСреднегоЗаработкаВрем =
			МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеРасчетаСреднегоЗаработкаОбщего(ПараметрыПолученияДанныхСреднего);
			
		Если ЗначениеЗаполнено(ДанныеРасчетаСреднегоЗаработкаВрем) Тогда
			ДанныеРасчетаСреднегоЗаработкаИтоги = Новый ФиксированнаяСтруктура(ДанныеРасчетаСреднегоЗаработкаВрем.Итоги);
			СуммаУчтенногоЗаработка = ДанныеРасчетаСреднегоЗаработкаИтоги.ВсегоЗаработка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РезультатРедактированияНачислений(Месяц = Неопределено)
	
	Для Каждого СтрокаСреднего Из СреднийЗаработокОбщий Цикл
		СтрокаСреднего.Сотрудник = Сотрудник;
	КонецЦикла;
	
	Если Месяц = Неопределено Тогда
		ДанныеНачислений = ОбщегоНазначения.ТаблицаЗначенийВМассив(СреднийЗаработокОбщий.Выгрузить());
	Иначе
		ДанныеНачислений = ОбщегоНазначения.ТаблицаЗначенийВМассив(
			СреднийЗаработокОбщий.Выгрузить(Новый Структура("Период", Месяц)));
	КонецЕсли;
		
	Возврат ДанныеНачислений;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РезультатРедактированияВремени(Форма)

	ДанныеВремени = Новый Массив;
	
	Для НомерМесяца = 1 По Форма.МесяцыРасчета.Количество() Цикл
		
		СуммаНачислений = Форма.ТаблицаЗаработка[НомерМесяца + Форма.ДобавляемыеМесяцы-1].ОсновныеНачисления;
		КалендарныхДней = Форма.ТаблицаЗаработка[НомерМесяца + Форма.ДобавляемыеМесяцы -1].КалендарныхДней;
		ОтработаноДнейПятидневка = Форма.ТаблицаЗаработка[НомерМесяца + Форма.ДобавляемыеМесяцы -1].ОтработаноДнейПятидневка;
		НормаДнейПроизводственныйКалендарь = Форма.ТаблицаЗаработка[НомерМесяца + Форма.ДобавляемыеМесяцы -1].НормаДнейПроизводственныйКалендарь;
			
		Если КалендарныхДней = 0 Тогда
			Продолжить;
		КонецЕсли; 
		
		ОписаниеСтроки = ОписаниеСтрокиДанныхОВремени();
		ОписаниеСтроки.Сотрудник = Форма.Сотрудник;
		ОписаниеСтроки.Период = Форма.МесяцыРасчета[НомерМесяца - 1];
		ОписаниеСтроки.ОтработаноДнейКалендарных = КалендарныхДней;
		ОписаниеСтроки.ОтработаноДнейПятидневка = ОтработаноДнейПятидневка;
		ОписаниеСтроки.НормаДнейПроизводственныйКалендарь = НормаДнейПроизводственныйКалендарь;

		ДанныеВремени.Добавить(ОписаниеСтроки);

	КонецЦикла;
	
	Возврат ДанныеВремени;
	
КонецФункции	

&НаСервере
Процедура УстановитьРасширеннуюПодсказку()
	
	УчетЗарплаты.УстановитьРасширеннуюПодсказкуРасчетаСреднего(ЭтотОбъект);
	
	Элементы.СреднийЗаработокДекорация.ОтображениеПодсказки =
		?(ЕстьПремии, ОтображениеПодсказки.Кнопка, ОтображениеПодсказки.Нет);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаЗаработкаКалендарныхДней");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаЗаработкаОсновныеНачисления");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТаблицаЗаработка.Пустой", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаЗаработка");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТаблицаЗаработка.Пустой", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаЗаработкаОсновныеНачисления");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТаблицаЗаработка.Пустой", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Формат", "ЧДЦ=2; ЧН=0,00");
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НачисленияОплаченоДней");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НачисленияОплаченоЧасов");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Начисления.Начисление", ВидСравненияКомпоновкиДанных.НеВСписке, НачисленияОклад);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область РасчетОтпускных

&НаСервере
Процедура Пересчитать()
	
	РассчитатьСреднийЗаработок();
	РассчитатьНачисления();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьНачисления()
	
	Документы.Увольнение.РассчитатьНачисления(Объект, РеквизитФормыВЗначение("Объект"), ДополнительныеПараметрыРасчета());
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуКПринятиюИзменений()
	
	СохранитьДанныеРасчетаСреднего();
	
	АдресВХранилище = Документы.Увольнение.АдресПараметровВХранилище(РеквизитФормыВЗначение("Объект"), ДополнительныеПараметрыРасчета());
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ДополнительныеПараметрыРасчета()
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Подразделение",                             Подразделение);
	ДополнительныеПараметры.Вставить("НачислениеКомпенсацияОтпуска",              НачислениеКомпенсацияОтпуска);
	ДополнительныеПараметры.Вставить("НачислениеКомпенсацияОтпускаРК",            НачислениеКомпенсацияОтпускаРК);
	ДополнительныеПараметры.Вставить("НачислениеКомпенсацияОтпускаСН",            НачислениеКомпенсацияОтпускаСН);
	ДополнительныеПараметры.Вставить("УчитыватьМРОТПриОплатеПоСреднемуЗаработку", УчитыватьМРОТПриОплатеПоСреднемуЗаработку);
	ДополнительныеПараметры.Вставить("УникальныйИдентификатор",                   ИдентификаторВладельца);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

&НаСервере
Процедура СохранитьДанныеРасчетаСреднего()
	
	Объект.ОтработанноеВремяДляСреднегоОбщий.Очистить();
	ДанныеОВремени = РезультатРедактированияВремени(ЭтаФорма);
	Если ЗначениеЗаполнено(ДанныеОВремени) Тогда
		Для каждого СведенияОВремени Из ДанныеОВремени Цикл
			ЗаполнитьЗначенияСвойств(Объект.ОтработанноеВремяДляСреднегоОбщий.Добавить(), СведенияОВремени);
		КонецЦикла;
	КонецЕсли; 
	
	Объект.СреднийЗаработокОбщий.Очистить();
	ДанныеОНачислениях = РезультатРедактированияНачислений();
	Если ЗначениеЗаполнено(ДанныеОНачислениях) Тогда
		Для каждого СведенияОНачислениях Из ДанныеОНачислениях Цикл
			ЗаполнитьЗначенияСвойств(Объект.СреднийЗаработокОбщий.Добавить(), СведенияОНачислениях);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСреднийЗаработокПоследнегоМесяца()
	
	ПрочитатьДанныеУчетаСреднегоЗаработка(Истина);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВопросСохранитьИзменения(Закрытие)
	
	ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
	Оповещение = Новый ОписаниеОповещения("ВопросСохранитьИзмененияЗавершение", ЭтотОбъект, Новый Структура("Закрытие", Закрытие));
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьИзмененияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ПроверитьЗаполнениеРеквизитов() Тогда
			ПодготовитьФормуКПринятиюИзменений();
			Если ДополнительныеПараметры.Закрытие Тогда
				Закрыть(АдресВХранилище);
			Иначе
				ВладелецФормы.РедактированиеРасчетаЗавершение(АдресВХранилище);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Если ДополнительныеПараметры.Закрытие Тогда
			Модифицированность = Ложь;
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ДополнительныеПараметры.Закрытие
		И НЕ Результат = КодВозвратаДиалога.Отмена Тогда
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПремияПриИзменении(СоставнаяЧастьИмя)
	
	СоставнаяЧасть = ПредопределенноеЗначение("Перечисление.УчетНачисленийВСреднемЗаработкеОбщий." + СоставнаяЧастьИмя);
	СтрокиПремии = СреднийЗаработокГодовыеПремии.НайтиСтроки(Новый Структура("СоставнаяЧасть", СоставнаяЧасть));
	
	Если СтрокиПремии.Количество() > 1 Тогда
		Для Каждого Строка Из СтрокиПремии Цикл
			СреднийЗаработокГодовыеПремии.Удалить(Строка);
		КонецЦикла;
		СтрокиПремии.Очистить();
	КонецЕсли;
	Если СтрокиПремии.Количество() = 0 Тогда
		ТекущаяСтрока = СреднийЗаработокГодовыеПремии.Добавить();
		ТекущаяСтрока.Период            = НачалоМесяца(Макс(ДатаПриемаНаРаботуСотрудника, НачалоПериодаРасчета));
		ТекущаяСтрока.Сотрудник         = Сотрудник;
		ТекущаяСтрока.Год               = ГодГодовыхПремий;
		ТекущаяСтрока.КоличествоМесяцев = 12;
		ТекущаяСтрока.ДатаНачалаБазовогоПериода = НачалоМесяца(Макс(ДатаПриемаНаРаботуСотрудника, Дата(ГодГодовыхПремий,1,1)));
	Иначе
		ТекущаяСтрока = СтрокиПремии[0];
	КонецЕсли;
	ТекущаяСтрока.СоставнаяЧасть = СоставнаяЧасть;
	ТекущаяСтрока.Сумма = ЭтотОбъект[СоставнаяЧастьИмя];
	
	РассчитатьСреднийЗаработок();
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеРеквизитов()
	
	ПроверкаПройдена = Истина;
	Для Каждого СтрокаТаблицы ИЗ Объект.Начисления Цикл
		ИндексСтроки = Объект.Начисления.Индекс(СтрокаТаблицы);
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Начисление) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",
				"Заполнение",
				"Начисление",
				ИндексСтроки + 1,
				"Начисления");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.Начисления", ИндексСтроки+1, "Начисление"));
			ПроверкаПройдена = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПроверкаПройдена;
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчет()
	
	РасчетныйЛисток = ВладелецФормы.РасчетныйЛистокНаСервере();
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Отчет.АнализНачисленийИУдержаний",
		"РасчетныйЛистокПодробнее",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка),
		ЭтотОбъект,
		Новый Структура("ПечатаемыйДокумент, ЗаголовокФормы",
			РасчетныйЛисток, НСтр("ru='Расчетный листок'")));
	
КонецПроцедуры

#КонецОбласти
