#Область ОписаниеПеременных

&НаКлиенте
Перем ЗакрытьПослеЗаписи;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МожноРедактировать = ПравоДоступа("Изменение", Метаданные.Документы.Увольнение);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец БлокировкаИзмененияОбъектов
	
	// ЭДОБЗК
	ЭДОБЗК.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка, Объект);
	// Конец ЭДОБЗК
	
	КадровыйУчетФормы.ФормаКадровогоДокументаПриСозданииНаСервере(ЭтотОбъект);
	
	Если Параметры.Ключ.Пустая() Тогда
		
		// Очистим реквизит Сотрудник, если он уже принят.
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) И Параметры.ЗначениеКопирования.Проведен Тогда
			Объект.Сотрудник = Справочники.Сотрудники.ПустаяСсылка();
			Объект.ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		КонецЕсли;
		Объект.ДатаУвольнения = ТекущаяДатаСеанса();
		
		Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда 
			Объект.Организация = Параметры.Организация;
		КонецЕсли;
		Если Параметры.Свойство("Сотрудник") И ЗначениеЗаполнено(Параметры.Сотрудник) Тогда 
			Объект.Сотрудник = Параметры.Сотрудник;
		КонецЕсли;
		Если Параметры.Свойство("СтатьяТКРФ") И ЗначениеЗаполнено(Параметры.СтатьяТКРФ) Тогда 
			Объект.СтатьяТКРФ = Параметры.СтатьяТКРФ;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Объект.СтатьяТКРФ) Тогда
			Объект.СтатьяТКРФ = Справочники.ОснованияУвольнения.Статья77п3;
			Объект.ОснованиеУвольнения = НСтр("ru = 'Заявление сотрудника'");
		КонецЕсли;
		Если Параметры.Свойство("ДатаУвольнения") И ЗначениеЗаполнено(Параметры.ДатаУвольнения) Тогда 
			Объект.ДатаУвольнения = Параметры.ДатаУвольнения;
		КонецЕсли;
		Если Не ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
			Объект.РассчитатьКомпенсациюНачислитьЗарплату = Ложь;
		КонецЕсли;
		
		УстановитьПланируемуюДатуВыплаты();
		УстановитьПериодРегистрации();
		
		// Заполнение нового документа.
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный", 
		"Объект.Организация",
		"Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
		
		ЗаполнитьДанныеФормыПоОрганизации();
		
		ПриПолученииДанныхНаСервере();
		
		// ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
		ЭлектронныеТрудовыеКнижки.ЗаполнитьНаименованиеДокумента(Объект, "Увольнение");
		// Конец ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
		
		Если Не ЗначениеЗаполнено(Объект.НаименованиеДокумента) Тогда
			Объект.НаименованиеДокумента = НСтр("ru='Приказ'");
		КонецЕсли;
		
		УправлениеФормой();
		
	КонецЕсли;
	
	ПолучитьТекстДляПриказа();
	УстановитьПредупреждениеРедактированияНомера();
	СервисЭлектронныхТрудовыхКнижек.Увольнение_ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// ИнтеграцияС1САрхивом 
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивом = ОбщегоНазначения.ОбщийМодуль("ОбменСАрхивом");
		МодульОбменСАрхивом.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1САрхивом
	
	УстановитьСостояниеДокумента();
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.Увольнение",
		"ФормаДокумента",
		НСтр("ru='Новости: Увольнение'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыРезультатыРасчетаНДФЛ" И Источник.ВладелецФормы = ЭтотОбъект Тогда
		ОбновитьДанныеНДФЛНаСервере(Параметр);
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭДОБЗК
	ЭДОБЗККлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭДОБЗК
	
	Если ИмяСобытия = "Запись_СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД" Тогда
		Если Источник <> Неопределено Тогда
			Если Параметр.Организация = Объект.Организация
				И Параметр.Сотрудники.Найти(Объект.Сотрудник) <> Неопределено Тогда
				ПолучитьСтатусЗаписиЭлектроннойТрудовойКнижки();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ОснованияУвольнения" Тогда
		Если Источник = Объект.СтатьяТКРФ Тогда
			ПолучитьТекстДляПриказа();
		КонецЕсли;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
	ЭлектронныеТрудовыеКнижки.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект,
		ПравоДоступа("Изменение", Метаданные.Документы.Увольнение)
		И ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет"));
	// Конец БлокировкаИзмененияОбъектов
	
	// ЭДОБЗК
	ЭДОБЗК.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, Объект);
	// Конец ЭДОБЗК
	
	СтатусЗаписиЭлектроннойТрудовойКнижки();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	КадровыйУчетКлиент.ОповеститьОбИзмененииРабочегоМеста(ЭтотОбъект);
	Оповестить("Запись_Увольнение", ПараметрыЗаписи, Объект.Ссылка);
	
	Если ЗакрытьПослеЗаписи И Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПослеЗаписиНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец БлокировкаИзмененияОбъектов
	
	// ЭДОБЗК
	ЭДОБЗК.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Объект);
	// Конец ЭДОБЗК
	
	СтатусЗаписиЭлектроннойТрудовойКнижки();
	УстановитьПредупреждениеРедактированияНомера();
	УправлениеФормой();
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Объект.Проведен И Модифицированность Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	КонецЕсли;
	
	УчетЗарплатыКлиент.ПередЗакрытиемКадровогоДокумента(ЭтотОбъект,
		Объект.ОтразитьВТрудовойКнижке,
		ЗакрытьПослеЗаписи,
		Отказ,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Объект.НомерПриказа)) Тогда
		Объект.НомерПриказа = ЭлектронныеТрудовыеКнижкиВызовСервера.НомерНаПечать(Объект.Номер);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	СотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаУвольненияПриИзменении(Элемент)
	
	ДатаУвольненияПриИзмененииНаСервере();
	
КонецПроцедуры

// ЗарплатаКадрыПодсистемы.ПодписиДокументов
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент)
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент)
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов

// ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки

&НаКлиенте
Процедура ОтразитьВТрудовойКнижкеПриИзменении(Элемент)
	
	УправлениеФормой();
	ОбновитьИнформациюОБудущихМероприятиях();
	
	Если Не УвольнениеСОсновногоМестаРаботы Тогда
		ЭлектронныеТрудовыеКнижкиВызовСервера.ЗапомнитьОтражениеВТрудовойКнижкеСовместительств(Объект.ОтразитьВТрудовойКнижке);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДокументаПриИзменении(Элемент)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаПриИзменении(Объект.Организация, "Увольнение", Объект.НаименованиеДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДокументаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаОбработкаВыбора(
		Объект.НаименованиеДокумента, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеВторогоДокументаОснованияПриИзменении(Элемент)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаПриИзменении(Объект.Организация, "Увольнение", Объект.НаименованиеВторогоДокументаОснования);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеВторогоДокументаОснованияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеВторогоДокументаОснованияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаОбработкаВыбора(
		Объект.НаименованиеВторогоДокументаОснования, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеВторогоДокументаОснованияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВторогоДокументаОснованияПриИзменении(Элемент)
	
	ДатаВторогоДокументаОснованияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СерияВторогоДокументаОснованияПриИзменении(Элемент)
	
	ЭлектронныеТрудовыеКнижкиКлиентСервер.УстановитьЗаголовокГруппыВторогоДокументаОснования(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерВторогоДокументаОснованияПриИзменении(Элемент)
	
	ЭлектронныеТрудовыеКнижкиКлиентСервер.УстановитьЗаголовокГруппыВторогоДокументаОснования(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупреждениеОБудущихМероприятияхТекстВторогоВариантаДекорацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ПриИзмененииБудущихМероприятий", ЭтотОбъект);
	ЭлектронныеТрудовыеКнижкиКлиент.ОткрытьРегистрациюОтменыМероприятий(
		Объект.Организация, ДанныеРегистраторовБудущихМероприятий, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупреждениеОБудущихМероприятияхТекстПервогоВариантаДекорацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ПриИзмененииБудущихМероприятий", ЭтотОбъект);
	ЭлектронныеТрудовыеКнижкиКлиент.ОткрытьРегистраторМероприятия(ДанныеРегистраторовБудущихМероприятий, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяТКРФОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	КадровыйУчетКлиент.ОбработкаВыбораОснованиеУвольнения(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПланируемаяДатаВыплатыПриИзменении(Элемент)
	
	РассчитатьНачисления();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьКомпенсациюНачислитьЗарплатуПриИзменении(Элемент)
	
	РассчитатьКомпенсациюНачислитьЗарплатуПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяТКРФПриИзменении(Элемент)
	
	ПолучитьТекстДляПриказа();
	
КонецПроцедуры

&НаСервере
Процедура ЕстьВторойДокументОснованиеПриИзмененииНаСервере()
	
	Если Не ЕстьВторойДокументОснование Тогда
		Объект.НаименованиеВторогоДокументаОснования = "";
		Объект.СерияВторогоДокументаОснования        = "";
		Объект.НомерВторогоДокументаОснования        = "";
		Объект.ДатаВторогоДокументаОснования         = "";
	КонецЕсли;
	
	УстановитьДатуПредставленияЗаписиТрудовойКнижки();
	УправлениеФормой();
	
КонецПроцедуры

// Конец ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки

&НаКлиенте
Процедура НаименованиеДокументаНажатие(Элемент, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = "";
	ЭлектронныеТрудовыеКнижкиКлиент.НаименованиеДокументаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗаписьЭлектроннойТрудовойКнижкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗаписиТрудовойКнижки.Количество() = 0 Тогда
		Возврат
	ИначеЕсли ЗаписиТрудовойКнижки.Количество() = 1 Тогда
		ПоказатьЗначение(, ЗаписиТрудовойКнижки[0].ЗаписьЭлектроннойТрудовойКнижки)
	Иначе
		СписокЗаписей = Новый СписокЗначений;
		Для Каждого СтрокаТаблицы Из ЗаписиТрудовойКнижки Цикл
			СписокЗаписей.Добавить(СтрокаТаблицы.ЗаписьЭлектроннойТрудовойКнижки);
		КонецЦикла;
		
		Оповещение = Новый ОписаниеОповещения("НадписьЗаписьЭлектроннойТрудовойКнижкиПоказатьОповещение", ЭтотОбъект);
		ПоказатьВыборИзМеню(Оповещение, СписокЗаписей, Элемент);
	КонецЕсли
		
КонецПроцедуры

&НаКлиенте
Процедура ЕстьВторойДокументОснованиеПриИзменении(Элемент)
	
	ЕстьВторойДокументОснованиеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленоРезультатНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не КлючевыеРеквизитыЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("РедактированиеРасчетаЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресПараметровВХранилище", АдресПараметровВХранилище());
	ПараметрыОткрытия.Вставить("ТолькоПросмотр",            ТолькоПросмотр);
	
	ОткрытьФорму("Документ.Увольнение.Форма.ФормаПодробнееОРасчете", 
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УдержаноРезультатаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не КлючевыеРеквизитыЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	КартинкаКарандаш = ?(Объект.КорректироватьНДФЛ, БиблиотекаКартинок.Изменить, Новый Картинка);
	СписокПунктовМеню = Новый СписокЗначений;
	СписокПунктовМеню.Добавить(0, НСтр("ru = 'НДФЛ'"), , КартинкаКарандаш);
	СписокПунктовМеню.Добавить(1, НСтр("ru = 'Удержания'"));
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИзМенюЗавершение", ЭтотОбъект);
	ПоказатьВыборИзМеню(Оповещение, СписокПунктовМеню, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИтогВзносыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не КлючевыеРеквизитыЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("РедактированиеВзносовЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресПараметровВХранилище", АдресПараметровВХранилище());
	ПараметрыОткрытия.Вставить("ТолькоПросмотр",            ТолькоПросмотр);
	
	ОткрытьФорму("ОбщаяФорма.ФормаРедактированиеВзносов", 
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// ИнтеграцияС1САрхивом

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграцииС1САрхивом(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивомКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСАрхивомКлиент");
		МодульОбменСАрхивомКлиент.ВыполнитьПодключаемуюКоманду(Команда, ЭтотОбъект, Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуИнтеграцииС1САрхивомНаСервере(Знач Параметры) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивом = ОбщегоНазначения.ОбщийМодуль("ОбменСАрхивом");
		МодульОбменСАрхивом.ВыполнитьПодключаемуюКоманду(ЭтотОбъект, Параметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИнтеграцияС1САрхивом_Нажатие(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивомКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСАрхивомКлиент");
		МодульОбменСАрхивомКлиент.Нажатие(Элемент, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ОбновитьПодключаемыеКоманды(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьОтчетПоПроблемам(ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка)
	
	КонтрольВеденияУчетаКлиентБЗК.ОткрытьОтчетПоПроблемамОбъекта(ЭтотОбъект, Объект.Ссылка, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

// БлокировкаИзмененияОбъектов

&НаКлиенте
Процедура Подключаемый_РазблокироватьФормуОбъекта(Команда)
	
	БлокировкаИзмененияОбъектовКлиент.РазблокироватьФормуОбъекта(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

// Конец БлокировкаИзмененияОбъектов

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
			Команда,
			ЭтотОбъект,
			Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.КонтрольВеденияУчета

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ЗакрытьПослеЗаписи = Истина;
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура ЗарегистрироватьЗаписьЭлектроннойТрудовойКнижки(Команда)
	
	Если Объект.ПометкаУдаления Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Нельзя оформить запись электронной трудовой книжки на основании документа, помеченного на удаление.'"));
		Возврат;
	Иначе
		Если НЕ Объект.Проведен ИЛИ Модифицированность Тогда
			Кнопки = Новый СписокЗначений;
			Кнопки.Вставить(0, КодВозвратаДиалога.Да,     НСтр("ru = 'Провести'"));
			Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
			
			Оповещение = Новый ОписаниеОповещения("ВопросПередРегистрациейСледуетПровестиЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед регистрацией записи электронной трудовой книжки документ следует провести'"), Кнопки,, КодВозвратаДиалога.Да);
		Иначе
			СоздатьЗаписьЭлектроннойТрудовойКнижки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выплатить(Команда)
	
	Если НЕ Объект.ПометкаУдаления И НЕ Объект.Проведен ИЛИ Модифицированность Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Провести");
		Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередВыплатойСледуетПровестиЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед выплатой документ следует провести'"), Кнопки,, КодВозвратаДиалога.Да);
	Иначе
		СформироватьДокументыВыплаты();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьДанныеФормыПоОрганизации();
	
	// ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
	ЭлектронныеТрудовыеКнижки.ЗаполнитьНаименованиеДокумента(Объект, "Увольнение");
	// Конец ЗарплатаКадрыПодсистемы.КадровыйУчет.ЭлектронныеТрудовыеКнижки
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ИспользоватьДляМероприятийПриемПереводУвольнениеДваДокументаОснования =
		ЭлектронныеТрудовыеКнижкиВызовСервера.ИспользоватьДляМероприятийПриемПереводУвольнениеДваДокументаОснования();
		
	ЕстьВторойДокументОснование = ЗначениеЗаполнено(Объект.НаименованиеВторогоДокументаОснования)
		Или ЗначениеЗаполнено(Объект.ДатаВторогоДокументаОснования)
		Или ЗначениеЗаполнено(Объект.СерияВторогоДокументаОснования)
		Или ЗначениеЗаполнено(Объект.НомерВторогоДокументаОснования);
	
	Отбор = Новый Структура("КатегорияНачисленияИлиНеоплаченногоВремени", Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска);
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ДополнитьОтборНачислений(Отбор, Объект.Организация, Объект.ПериодРегистрации);
	КонецЕсли;
	НачислениеКомпенсацияОтпуска = ПланыВидовРасчета.Начисления.НачислениеПоУмолчанию(Отбор);

	Отбор = Новый Структура("КатегорияНачисленияИлиНеоплаченногоВремени", Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке);
	Отбор.Вставить("ОсновнойВидРасчета", НачислениеКомпенсацияОтпуска);
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ДополнитьОтборНачислений(Отбор, Объект.Организация, Объект.ПериодРегистрации);
	КонецЕсли;
	НачислениеКомпенсацияОтпускаРК = ПланыВидовРасчета.Начисления.НачислениеПоУмолчанию(Отбор);
	
	Отбор = Новый Структура("КатегорияНачисленияИлиНеоплаченногоВремени", Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке);
	Отбор.Вставить("ОсновнойВидРасчета", НачислениеКомпенсацияОтпуска);
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ДополнитьОтборНачислений(Отбор, Объект.Организация, Объект.ПериодРегистрации);
	КонецЕсли;
	НачислениеКомпенсацияОтпускаСН = ПланыВидовРасчета.Начисления.НачислениеПоУмолчанию(Отбор);
	
	ПрочитатьДанныеСотрудника();
	
	УстановитьТипОснованияУвольнения();
	
	РассчитатьИтогиПоДокументу();
	УстановитьРасшифровкуНачислений();
	
	ОтобразитьПризнакиКорректировки();
	
	ОбновитьСреднийЗаработок(ЭтотОбъект);
	
	УстановитьДатуПредставленияЗаписиТрудовойКнижки();
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	ПрочитатьДанныеСотрудника();
	
	Если ИспользуетсяСервисЭлектронныхТрудовыхКнижек Тогда
		Объект.ОтразитьВТрудовойКнижке = Истина;
	ИначеЕсли Не УвольнениеСОсновногоМестаРаботы Тогда
		Объект.ОтразитьВТрудовойКнижке = ЭлектронныеТрудовыеКнижкиВызовСервера.ОтражениеВТрудовойКнижкеСовместительств();
	КонецЕсли;
	
	Объект.ФизическоеЛицо = ФизическоеЛицо;
	
	Если Не ИспользуетсяСервисЭлектронныхТрудовыхКнижек Тогда
		РассчитатьКомпенсациюНачислитьЗарплатуПриНеобходимости();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеСотрудника()
	
	УчитыватьМРОТПриОплатеПоСреднемуЗаработку = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		
		КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Объект.Сотрудник, "ВидЗанятости,Подразделение,ФизическоеЛицо,ДатаПриема,ДоляНеполногоРабочегоВремени", Объект.ДатаУвольнения);
		Если КадровыеДанные.Количество() > 0 Тогда
			
			УвольнениеСОсновногоМестаРаботы = (КадровыеДанные[0].ВидЗанятости = Перечисления.ВидыЗанятости.ОсновноеМестоРаботы
				Или КадровыеДанные[0].ВидЗанятости = Перечисления.ВидыЗанятости.Совместительство);
				
			Подразделение = КадровыеДанные[0].Подразделение;
			ФизическоеЛицо = КадровыеДанные[0].ФизическоеЛицо;
			ДатаПриемаНаРаботу = КадровыеДанные[0].ДатаПриема;
			
			УчитыватьМРОТПриОплатеПоСреднемуЗаработку = УвольнениеСОсновногоМестаРаботы
				И КадровыеДанные[0].ДоляНеполногоРабочегоВремени >= 1;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьИнформациюОБудущихМероприятиях();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюОБудущихМероприятиях()
	
	ЭлектронныеТрудовыеКнижки.ОбновитьГруппуИнформацииОБудущихМероприятиях(ЭтотОбъект, Объект.ДатаУвольнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииБудущихМероприятий(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьИнформациюОБудущихМероприятиях();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипОснованияУвольнения()
	
	ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ОснованияУвольнения");
	КадровыйУчет.УстановитьОграничениеТиповПолюВводаОснованияУвольнения(
		ЭтотОбъект, "СтатьяТКРФ", ОграничениеТипа, ТипЗнч(Объект.СтатьяТКРФ));
	
КонецПроцедуры

// ЭДОБЗК

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодключаемыеКоманды(УправляемаяФорма)
	ЭДОБЗККлиентСервер.ОбновитьКоманды(УправляемаяФорма, УправляемаяФорма.Объект, Истина);
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	УчетЗарплаты.УправлениеФормойКадровогоМероприятия(ЭтотОбъект);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЕстьВторойДокументОснование",
		"Видимость",
		ИспользоватьДляМероприятийПриемПереводУвольнениеДваДокументаОснования);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВторойДокументОснованиеГруппа",
		"Видимость",
		ЕстьВторойДокументОснование
		И ИспользоватьДляМероприятийПриемПереводУвольнениеДваДокументаОснования);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаРасчет",
		"Видимость",
		Объект.РассчитатьКомпенсациюНачислитьЗарплату);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ФормаВыплатить",
		"Видимость",
		Объект.РассчитатьКомпенсациюНачислитьЗарплату);
		
	Если Не ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		Если Не Объект.РассчитатьКомпенсациюНачислитьЗарплату Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"РассчитатьКомпенсациюНачислитьЗарплату",
				"Видимость",
				Ложь);
		КонецЕсли;
	КонецЕсли;
	
	СформироватьОписаниеПодсказкиЭТК();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьКомпенсациюНачислитьЗарплатуПриИзмененииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.ПланируемаяДатаВыплаты) Тогда
		Объект.ПланируемаяДатаВыплаты = РасчетнаяПланируемаяДатаВыплаты();
	КонецЕсли;
	РассчитатьКомпенсациюНачислитьЗарплатуПриНеобходимости();
	УстановитьРасшифровкуНачислений();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ДатаУвольненияПриИзмененииНаСервере()
	
	УстановитьПериодРегистрации();
	УстановитьПланируемуюДатуВыплаты();
	ПрочитатьДанныеСотрудника();
	РассчитатьКомпенсациюНачислитьЗарплатуПриИзмененииНаСервере();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПериодРегистрации()
	
	Если ЗначениеЗаполнено(Объект.ДатаУвольнения) Тогда
		Объект.ПериодРегистрации = НачалоМесяца(Объект.ДатаУвольнения);
	Иначе
		Объект.ПериодРегистрации = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПланируемуюДатуВыплаты()
	
	Объект.ПланируемаяДатаВыплаты = РасчетнаяПланируемаяДатаВыплаты();
	
КонецПроцедуры

&НаСервере
Функция РасчетнаяПланируемаяДатаВыплаты()
	
	ПланируемаяДатаВыплаты = Объект.ДатаУвольнения;
	Если Не ЗначениеЗаполнено(ПланируемаяДатаВыплаты) Тогда
		ПланируемаяДатаВыплаты = НачалоДня(ТекущаяДатаСеанса() + 86400);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		ПланируемаяДатаВыплаты = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДатаСобытияСУчетомНерабочихДней(ПланируемаяДатаВыплаты, Объект.Организация);
	КонецЕсли;
	
	Возврат ПланируемаяДатаВыплаты;
	
КонецФункции

&НаСервере
Процедура РассчитатьКомпенсациюНачислитьЗарплатуПриНеобходимости()
	
	Если Объект.РассчитатьКомпенсациюНачислитьЗарплату Тогда
		РассчитатьКоличествоДнейКомпенсацииУдержанияОтпускаНаСервере();
		РассчитатьСреднийЗаработокИНачисления();
	Иначе
		УдалитьДанныеРасчета();
	КонецЕсли;
	ОтобразитьПризнакиКорректировки();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДанныеРасчета()
	
	Объект.КорректироватьНДФЛ   = Ложь;
	Объект.КорректироватьВзносы = Ложь;
	
	Объект.ДнейКомпенсацииУдержанияОтпуска = 0;
	
	Объект.Начисления.Очистить();
	Объект.Взносы.Очистить();
	Объект.НДФЛ.Очистить();
	Объект.ПримененныеВычетыНаДетейИИмущественные.Очистить();
	Объект.ОтработанноеВремяДляСреднегоОбщий.Очистить();
	Объект.СреднийЗаработокОбщий.Очистить();
	Объект.Удержания.Очистить();
	Объект.КорректировкиВыплаты.Очистить();
	
	РассчитатьИтогиПоДокументу();
	УстановитьРасшифровкуНачислений();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьНачисления()
	
	УстановитьПериодРегистрации();
	ОбъектФормы = РеквизитФормыВЗначение("Объект");
	Объект.Начисления.Очистить();
	Документы.Увольнение.РассчитатьНачисления(Объект, ОбъектФормы, ДополнительныеПараметрыРасчета());
	ВыполнитьРасчетНачислений();
	Если Не Объект.КорректироватьНДФЛ Тогда
		Документы.Увольнение.ОбновитьНДФЛиВзносыСотрудника(Объект, ОбъектФормы, Ложь, Истина);
	Иначе
		ОбновитьНДФЛиВзносыСотрудника();
	КонецЕсли;
	ОбновитьНДФЛиВзносыСотрудника();
	ЗаполнитьРезультатУдержаний();
	РассчитатьИтогиПоДокументу();
	УстановитьРасшифровкуНачислений();
	ОбновитьСреднийЗаработок(ЭтотОбъект);
	УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьКоличествоДнейКомпенсацииУдержанияОтпускаНаСервере()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") 
		И ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		СведенияПоОтпуску =
			МодульРасчетЗарплатыДляНебольшихОрганизаций.КоличествоДнейКомпенсацииУдержанияОтпуска(Объект.Организация, Объект.Сотрудник, Объект.ДатаУвольнения);
		ЗаполнитьЗначенияСвойств(Объект, СведенияПоОтпуску);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСреднийЗаработокИНачисления()
	
	УстановитьПериодРегистрации();
	
	Если КонецДня(Объект.ДатаУвольнения) = КонецМесяца(Объект.ДатаУвольнения) Тогда
		Объект.Начисления.Очистить();
		ВыполнитьРасчетНачислений();
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты")
		И ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		// Установка транзакции для расчета
		УстановитьПривилегированныйРежим(Истина);
		РасчетВыполняетсяОтдельнойТранзакцией = Не ТранзакцияАктивна();
		Если РасчетВыполняетсяОтдельнойТранзакцией Тогда
			НачатьТранзакцию();
		КонецЕсли;
		
		Если КонецДня(Объект.ДатаУвольнения) = КонецМесяца(Объект.ДатаУвольнения) Тогда
			ОбъектФормы = РеквизитФормыВЗначение("Объект");
			ОбъектФормы.ОбменДанными.Загрузка = Истина;
			ОбъектФормы.Записать(РежимЗаписиДокумента.Запись);
			ОбъектФормы.ЗарегистрироватьНачисления(,,, Истина);
			ОбъектФормы.ЗарегистрироватьОтработанноеВремя();
		КонецЕсли;
		
		// Расчет среднего заработка
		Объект.ОтработанноеВремяДляСреднегоОбщий.Очистить();
		Объект.СреднийЗаработокОбщий.Очистить();
		
		НачалоПериода = ДобавитьМесяц(НачалоМесяца(Объект.ДатаУвольнения + 86400), -12);
		НачалоПериода = Макс(НачалоПериода, НачалоМесяца(ДатаПриемаНаРаботу));
		
		ОкончаниеПериода = Макс(НачалоМесяца(Объект.ДатаУвольнения + 86400) - 1, НачалоПериода);
		
		ДанныеДляРасчета = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника(
			Объект.Сотрудник, Объект.Организация, НачалоПериода, ОкончаниеПериода, Истина);
		
		Для каждого ДанныеОВремени Из ДанныеДляРасчета.ДанныеОВремени Цикл
			СтрокаДанныхОВремени = Объект.ОтработанноеВремяДляСреднегоОбщий.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанныхОВремени, ДанныеОВремени);
			СтрокаДанныхОВремени.Сотрудник = Объект.Сотрудник;
		КонецЦикла;
		
		Для каждого ДанныеОНачислениях Из ДанныеДляРасчета.ДанныеОНачислениях Цикл
			СтрокаДанныеОНачислениях = Объект.СреднийЗаработокОбщий.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанныеОНачислениях, ДанныеОНачислениях);
			СтрокаДанныеОНачислениях.Сотрудник = Объект.Сотрудник;
		КонецЦикла;
		
		ТаблицыПоСотруднику = Новый Структура;
		ТаблицыПоСотруднику.Вставить("ДанныеОНачислениях", Объект.СреднийЗаработокОбщий);
		ТаблицыПоСотруднику.Вставить("ДанныеОВремени", Объект.ОтработанноеВремяДляСреднегоОбщий);
		
		ПараметрыПолученияДанныхСреднего = МодульРасчетЗарплатыДляНебольшихОрганизаций.ПараметрыПолученияДанныхСреднегоОбщего();
		ПараметрыПолученияДанныхСреднего.Вставить("ТаблицыПоСотруднику", 	ТаблицыПоСотруднику); 
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаПериода",  	НачалоПериода); 
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаОкончанияПериода",	ОкончаниеПериода); 
		ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаСобытия", 		Объект.ДатаУвольнения + 86400);
		
		ДанныеРасчетаСреднегоЗаработка = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеРасчетаСреднегоЗаработкаОбщего(ПараметрыПолученияДанныхСреднего);
		
		Объект.СреднийЗаработок = ДанныеРасчетаСреднегоЗаработка.Итоги.СреднедневнойЗаработок;
		Объект.СреднийЗаработокРК = ДанныеРасчетаСреднегоЗаработка.Итоги.СреднедневнойЗаработокРК;
		Объект.СреднийЗаработокСН = ДанныеРасчетаСреднегоЗаработка.Итоги.СреднедневнойЗаработокСН;
		
		Если РасчетВыполняетсяОтдельнойТранзакцией Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		// Отмена расчетной транзакции
		
	КонецЕсли;
	
	РассчитатьНачисления();
	
КонецПроцедуры

&НаСервере
Функция СведенияОбНДФЛ(ВходящиеДанные = Неопределено, ФизическоеЛицо = Неопределено) Экспорт
	
	Если ВходящиеДанные = Неопределено Тогда
		Форма = ЭтотОбъект;
		ОбъектФормы    = Объект;
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Иначе
		Форма          = ВходящиеДанные;
		ОбъектФормы    = ВходящиеДанные.Объект;
		
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ВходящийОбъект", Новый ОписаниеТипов("ДокументОбъект.Увольнение")));
		МассивИменРеквизитовФормы = Новый Массив;
		ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
		ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
		КопироватьДанныеФормы(ОбъектФормы, ЭтотОбъект["ВходящийОбъект"]);
		
		ДокументОбъект = РеквизитФормыВЗначение("ВходящийОбъект");
	КонецЕсли;
	
	ДополнительныеСведения = УчетНДФЛФормы.ДополнительныеДанныеДляПолученияСведенийОДоходахНДФЛДокумента();
	ДополнительныеСведения.МесяцНачисления = ОбъектФормы.ПериодРегистрации;
	ДополнительныеСведения.ПланируемаяДатаВыплаты = ОбъектФормы.ПланируемаяДатаВыплаты;
	
	СведенияОДоходахНДФЛ = УчетНДФЛФормы.СведенияОДоходахНДФЛДокумента(ОбъектФормы, "Начисления", ДополнительныеСведения);
	АдресСведенийОбНДФЛ = УчетНДФЛФормы.СведенияОбНДФЛ(Форма);
	
	ДанныеОбНДФЛ = ПолучитьИзВременногоХранилища(АдресСведенийОбНДФЛ);
	ДанныеОбНДФЛ.Вставить("СведенияОДоходах",      СведенияОДоходахНДФЛ.СведенияОДоходах);
	ДанныеОбНДФЛ.Вставить("ВычетыКДоходам",        СведенияОДоходахНДФЛ.ВычетыКДоходам);
	ДанныеОбНДФЛ.Вставить("Сотрудник",             ОбъектФормы.Сотрудник);
	ДанныеОбНДФЛ.Вставить("СериализованныйОбъект", ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(ДокументОбъект));
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеОбНДФЛ, УникальныйИдентификатор);
	
КонецФункции

// Конец ЭДОБЗК

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСреднийЗаработок(УправляемаяФорма)
	
	Подсказки = Новый Массив;
	УправляемаяФорма.СреднийЗаработок = 0;
	Если УправляемаяФорма.Объект.СреднийЗаработок > 0 Тогда
		УправляемаяФорма.СреднийЗаработок = УправляемаяФорма.Объект.СреднийЗаработок;
		Подсказки.Добавить(СтрШаблон(НСтр("ru = 'Средний заработок: %1'"), Формат(УправляемаяФорма.Объект.СреднийЗаработок, "ЧДЦ=2")));
	КонецЕсли;
	Если УправляемаяФорма.Объект.СреднийЗаработокРК > 0 Или УправляемаяФорма.Объект.СреднийЗаработокСН > 0 Тогда
		Подсказки.Добавить(НСтр("ru = 'В том числе:'"));
	КонецЕсли;
	Если УправляемаяФорма.Объект.СреднийЗаработокРК > 0 Тогда
		Подсказки.Добавить(СтрШаблон(НСтр("ru = '- Районный коэффициент: %1'"), Формат(УправляемаяФорма.Объект.СреднийЗаработокРК, "ЧДЦ=2")));
	КонецЕсли;
	Если УправляемаяФорма.Объект.СреднийЗаработокСН > 0 Тогда
		Подсказки.Добавить(СтрШаблон(НСтр("ru = '- Северная надбавка: %1'"), Формат(УправляемаяФорма.Объект.СреднийЗаработокСН, "ЧДЦ=2")));
	КонецЕсли;
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		УправляемаяФорма, "СреднийЗаработок", СтрСоединить(Подсказки, "," + Символы.ПС));
КонецПроцедуры

&НаСервере
Процедура ВыполнитьРасчетНачислений()
	
	УстановитьПериодРегистрации();
	ОбъектФормы = РеквизитФормыВЗначение("Объект");
	Документы.Увольнение.ВыполнитьРасчетНачислений(Объект, ОбъектФормы, ДополнительныеПараметрыРасчета());
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНДФЛиВзносыСотрудника()
	
	ОбъектФормы = РеквизитФормыВЗначение("Объект");
	Документы.Увольнение.ОбновитьНДФЛиВзносыСотрудника(Объект, ОбъектФормы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогУдержано(Форма)
	
	ОписанияСоставляющихУдержания = Новый Массив;
	ОписанияСоставляющихУдержания.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Результат", НСтр("ru='Удержания'")));
		
	ОписанияСоставляющихНДФЛ = Новый Массив;
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("Налог", НСтр("ru='НДФЛ'")));
	ОписанияСоставляющихНДФЛ.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита("НалогСПревышения", НСтр("ru='НДФЛ с прев. 15%'")));
		
	КолонкиНалогов = УчетНДФЛКлиентСервер.РесурсыИсчисленногоНалогаВМассиве("Налог", Истина, Ложь);
	ВсегоКолонок = КолонкиНалогов.Количество();
	Для Сч = 2 По ВсегоКолонок Цикл
		ИмяКолонки = КолонкиНалогов[Сч - 1];
		Ставка = Прав(ИмяКолонки, 2);
		ОписанияСоставляющихНДФЛ.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита(ИмяКолонки, 
				СтрШаблон(НСтр("ru='НДФЛ с прев. %1%%'"), Прав(ИмяКолонки, 2))));
	КонецЦикла;
	
	ОписанияСоставляющих = Новый Массив;
	
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.НДФЛ", ОписанияСоставляющихНДФЛ));
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогНДФЛ", ОписанияСоставляющих);
	
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Удержания", ОписанияСоставляющихУдержания));
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогУдержано", ОписанияСоставляющих);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтогВзносы(Форма)
	
	ПоляВзносов = УчетСтраховыхВзносовКлиентСервер.ОписаниеПолейВзносовВСоответствии(Форма.Объект.ПериодРегистрации);
	
	ОписанияСоставляющихВзносы = Новый Массив;
	Для каждого ОписаниеПоля Из ПоляВзносов Цикл
		ОписанияСоставляющихВзносы.Добавить(
			ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеСуммируемогоРеквизита(ОписаниеПоля.Ключ, ОписаниеПоля.Значение));
	КонецЦикла;
	
	ОписанияСоставляющих = Новый Массив;
	ОписанияСоставляющих.Добавить(
		ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейОписаниеТабличнойЧасти("Объект.Взносы", ОписанияСоставляющихВзносы));
	
	ЗарплатаКадрыКлиентСервер.ИтогиТабличныхЧастейРассчитатьИтог(Форма, "ИтогВзносы", ОписанияСоставляющих);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиПоДокументу()
	
	РассчитатьИтогУдержано(ЭтотОбъект);
	РассчитатьИтогВзносы(ЭтотОбъект);
	
	КВыплате = Объект.Начисления.Итог("Результат") - ИтогУдержано;
	
КонецПроцедуры

&НаСервере
Функция РасчетныйЛистокНаСервере() Экспорт
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ФизическоеЛицо);
	Если Объект.Проведен И Не Модифицированность Тогда
		Возврат УчетНачисленнойЗарплаты.РасчетныйЛистокПоДаннымДокумента(Объект.Организация, ФизическиеЛица, Объект.ПериодРегистрации);
	Иначе
		Для Каждого СтрокаТаблицы Из Объект.Начисления Цикл
			СтрокаТаблицы.ФизическоеЛицо = Объект.ФизическоеЛицо;
		КонецЦикла;
		ДанныеДокумента = ДанныеДокументаФизическихЛиц(ФизическиеЛица);
		Возврат УчетНачисленнойЗарплаты.РасчетныйЛистокПоДаннымДокумента(Объект.Организация, ФизическиеЛица, Объект.ПериодРегистрации, Объект.Ссылка, ДанныеДокумента);
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ДанныеДокументаФизическихЛиц(ФизическиеЛица)
	
	ТабличныеЧасти = ЗарплатаКадрыОтчеты.ТабличныеЧастиДляФормированияНабораДанныхДокументов();
	
	ОписаниеТаблицыНачислений = Новый Структура;
	ОписаниеТаблицыНачислений.Вставить("ИмяТаблицы", "Начисления");
	ОписаниеТаблицыНачислений.Вставить("ПутьКДанным", "Объект.Начисления");
	ОписаниеТаблицыНачислений.Вставить("ИмяПоляРезультат", "Результат");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаНачала", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаДатаОкончания", "");
	ОписаниеТаблицыНачислений.Вставить("ИмяРеквизитаВидРасчета", "Начисление");
	
	ОписаниеТаблицыНачислений.Вставить("ЗаполнятьОплаченноеВремяОтработанным", Истина);
	
	ТабличныеЧасти.ТаблицыНачислений.Добавить(ОписаниеТаблицыНачислений);
	
	ОписаниеТаблицыНДФЛ = Новый Структура;
	ОписаниеТаблицыНДФЛ.Вставить("ИмяТаблицы", "НДФЛ");
	ОписаниеТаблицыНДФЛ.Вставить("ПутьКДанным", "Объект.НДФЛ");
	ОписаниеТаблицыНДФЛ.Вставить("ИмяПоляРезультат", "Налог");
	ОписаниеТаблицыНДФЛ.Вставить("ИмяРеквизитаВидРасчета", "");
	
	ТабличныеЧасти.ТаблицыУдержаний.Добавить(ОписаниеТаблицыНДФЛ);
	
	ОписаниеТаблицыУдержаний = Новый Структура;
	ОписаниеТаблицыУдержаний.Вставить("ИмяТаблицы", "Удержания");
	ОписаниеТаблицыУдержаний.Вставить("ПутьКДанным", "Объект.Удержания");
	ОписаниеТаблицыУдержаний.Вставить("ИмяПоляРезультат", "Результат");
	ОписаниеТаблицыУдержаний.Вставить("ИмяРеквизитаВидРасчета", "Удержание");
	
	ОписаниеТаблицыУдержаний.Вставить("ИмяРеквизитаФизическоеЛицо", "Сотрудник");
	
	ТабличныеЧасти.ТаблицыУдержаний.Добавить(ОписаниеТаблицыУдержаний);
	
	Возврат ЗарплатаКадрыОтчеты.ДанныеДокументаФизическихЛиц(ЭтотОбъект, ФизическиеЛица, ТабличныеЧасти, "ПериодРегистрации");
	
КонецФункции

&НаСервере
Процедура НачисленияПриОкончанииРедактированияНаСервере()
	
	ОбновитьНДФЛиВзносыСотрудника();
	ЗаполнитьРезультатУдержаний();
	РассчитатьИтогиПоДокументу();
	УстановитьРасшифровкуНачислений();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыПоОрганизации()
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура ВопросПередРегистрациейСледуетПровестиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроведен = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Если НЕ ДокументПроведен Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось провести документ'"));
		Возврат;
	КонецЕсли;
	
	СоздатьЗаписьЭлектроннойТрудовойКнижки();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаписьЭлектроннойТрудовойКнижки()
	
	ДатаСобытия = Макс(?(ЕстьВторойДокументОснование,
		Макс(Объект.ДатаВторогоДокументаОснования, Объект.Дата),
			Объект.Дата),
		ОбщегоНазначенияКлиент.ДатаСеанса());
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Сотрудник",   Объект.ФизическоеЛицо);
	ПараметрыОткрытия.Вставить("ДатаСобытия", ДатаСобытия);
	ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", Новый Структура("Организация", Объект.Организация));
	
	ОткрытьФорму("Документ.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД.ФормаОбъекта", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияЗаписиЭТКЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеДокументаНажатиеЗавершение(Результат, ДополнительныеДанные) Экспорт
	
	Объект.НаименованиеДокумента = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЗаписьЭлектроннойТрудовойКнижкиПоказатьОповещение(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПоказатьЗначение(, ВыбранныйЭлемент.Значение)
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтатусЗаписиЭлектроннойТрудовойКнижки()
	
	СтатусЗаписиЭлектроннойТрудовойКнижки();
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, Объект.Ссылка, Новый Структура);
	// Конец БлокировкаИзмененияОбъектов
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура СтатусЗаписиЭлектроннойТрудовойКнижки()
	
	УчетЗарплаты.СтатусЗаписиЭлектроннойТрудовойКнижки(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьТекстДляПриказа()
	
	ТекстДляПриказа = "";
	Если ЗначениеЗаполнено(Объект.СтатьяТКРФ) Тогда
		ПредставлениеОбъекта = РегистрыСведений.ПредставленияОснованийУвольнения.СоздатьМенеджерЗаписи();
		ПредставлениеОбъекта.Объект = Объект.СтатьяТКРФ;
		ПредставлениеОбъекта.Прочитать();
		Если ПустаяСтрока(ПредставлениеОбъекта.Наименование) Тогда
			ТекстДляПриказа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СтатьяТКРФ, "ТекстОснования");
		Иначе
			ТекстДляПриказа = ПредставлениеОбъекта.ТекстОснования;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	УстановитьДатуПредставленияЗаписиТрудовойКнижки();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ДатаВторогоДокументаОснованияПриИзмененииНаСервере()
	
	ЭлектронныеТрудовыеКнижкиКлиентСервер.УстановитьЗаголовокГруппыВторогоДокументаОснования(ЭтотОбъект);
	УстановитьДатуПредставленияЗаписиТрудовойКнижки();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДатуПредставленияЗаписиТрудовойКнижки()
	
	ОписаниеРеквизитов = УчетЗарплаты.ОписаниеРеквизитовКадровогоДокумента(Объект);
	ДатаПредставленияЗаписиТрудовойКнижки = УчетЗарплаты.ДатаПредставленияЗаписиТрудовойКнижки(ОписаниеРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОписаниеПодсказкиЭТК()
	
	УчетЗарплаты.ОписаниеПодсказкиЭТК(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредупреждениеРедактированияНомера()
	
	Если ПустаяСтрока(Объект.НомерПриказа) Тогда
		ПредупреждениеПриРедактированииРеквизита = НСтр("ru = 'Номер заполняется при записи автоматически'");
	Иначе
		ПредупреждениеПриРедактированииРеквизита = "";
	КонецЕсли;
	
	Элементы.НомерПриказа.ПредупреждениеПриРедактировании = ПредупреждениеПриРедактированииРеквизита;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРезультатУдержаний()
	
	ОбъектФормы = РеквизитФормыВЗначение("Объект");
	Документы.Увольнение.ЗаполнитьРезультатУдержаний(Объект, ОбъектФормы);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьПризнакиКорректировки()
	
	Элементы.ДекорацияУдержано.Видимость = Объект.КорректироватьНДФЛ;
	Элементы.ДекорацияВзносы.Видимость = Объект.КорректироватьВзносы;
	
КонецПроцедуры

&НаСервере
Функция АдресПараметровВХранилище()
	
	Возврат Документы.Увольнение.АдресПараметровВХранилище(РеквизитФормыВЗначение("Объект"), ДополнительныеПараметрыРасчета());
	
КонецФункции

&НаСервере
Функция ДополнительныеПараметрыРасчета()
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Подразделение",                             Подразделение);
	ДополнительныеПараметры.Вставить("НачислениеКомпенсацияОтпуска",              НачислениеКомпенсацияОтпуска);
	ДополнительныеПараметры.Вставить("НачислениеКомпенсацияОтпускаРК",            НачислениеКомпенсацияОтпускаРК);
	ДополнительныеПараметры.Вставить("НачислениеКомпенсацияОтпускаСН",            НачислениеКомпенсацияОтпускаСН);
	ДополнительныеПараметры.Вставить("УчитыватьМРОТПриОплатеПоСреднемуЗаработку", УчитыватьМРОТПриОплатеПоСреднемуЗаработку);
	ДополнительныеПараметры.Вставить("УникальныйИдентификатор",                   УникальныйИдентификатор);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

&НаКлиенте
Процедура РедактированиеРасчетаЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ ПустаяСтрока(РезультатРедактирования) Тогда
		ЗаполнитьНачисленияПересчитатьНалог(РезультатРедактирования);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзМенюЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если РезультатРедактирования = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Если РезультатРедактирования.Значение = 0 Тогда
		УчетНДФЛКлиент.ОткрытьФормуПодробнееОРасчетеНДФЛ(Объект.Организация, ЭтотОбъект, Объект.ПериодРегистрации,
			Объект.Сотрудник, Объект.ПланируемаяДатаВыплаты);
	ИначеЕсли РезультатРедактирования.Значение = 1 Тогда
		Оповещение = Новый ОписаниеОповещения("РедактированиеУдержанийЗавершение", ЭтотОбъект);
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("АдресПараметровВХранилище", АдресПараметровВХранилище());
		ПараметрыОткрытия.Вставить("ТолькоПросмотр",            ТолькоПросмотр);
		
		ОткрытьФорму("ОбщаяФорма.ФормаРедактированиеУдержаний", 
			ПараметрыОткрытия,
			ЭтотОбъект,
			УникальныйИдентификатор,
			,
			,
			Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеУдержанийЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если НЕ ПустаяСтрока(РезультатРедактирования) Тогда
		ЗаполнитьУдержания(РезультатРедактирования);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеВзносовЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ ПустаяСтрока(РезультатРедактирования) Тогда
		ЗаполнитьВзносы(РезультатРедактирования);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисленияПересчитатьНалог(АдресВХранилище)
	
	ПараметрыОповещения = ПолучитьИзВременногоХранилища(АдресВХранилище);
	
	ДесериализованныйОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(ПараметрыОповещения.СериализованныйОбъект);
	
	ЗаполнитьЗначенияСвойств(Объект, ДесериализованныйОбъект,
		"СреднийЗаработок, СреднийЗаработокРК, СреднийЗаработокСН, ДнейКомпенсацииУдержанияОтпуска, РабочийГодС, РабочийГодПо");
	
	Объект.ОтработанноеВремяДляСреднегоОбщий.Загрузить(ДесериализованныйОбъект.ОтработанноеВремяДляСреднегоОбщий.Выгрузить());
	Объект.СреднийЗаработокОбщий.Загрузить(ДесериализованныйОбъект.СреднийЗаработокОбщий.Выгрузить());
	Объект.Начисления.Загрузить(ДесериализованныйОбъект.Начисления.Выгрузить());
	
	НачисленияПриОкончанииРедактированияНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУдержания(АдресВХранилище)
	
	ПараметрыОповещения = ПолучитьИзВременногоХранилища(АдресВХранилище);
	Объект.Удержания.Загрузить(ПараметрыОповещения.Удержания);
	РассчитатьИтогиПоДокументу();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВзносы(АдресВХранилище)
	
	ПараметрыОповещения = ПолучитьИзВременногоХранилища(АдресВХранилище);
	Объект.Взносы.Загрузить(ПараметрыОповещения.Взносы);
	Объект.КорректироватьВзносы = ПараметрыОповещения.КорректироватьРасчет;;
	ОтобразитьПризнакиКорректировки();
	РассчитатьИтогиПоДокументу();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРасшифровкуНачислений()
	
	ТекстПодсказки = "";
	СуммаКомпенсации = 0;
	СуммаЗаработка   = 0;
	
	Для Каждого СтрокаНачисления Из Объект.Начисления Цикл
		Если СтрокаНачисления.Начисление = НачислениеКомпенсацияОтпуска
			Или СтрокаНачисления.Начисление = НачислениеКомпенсацияОтпускаРК
			Или СтрокаНачисления.Начисление = НачислениеКомпенсацияОтпускаСН Тогда
			СуммаКомпенсации = СуммаКомпенсации + СтрокаНачисления.Результат;
		Иначе
			СуммаЗаработка = СуммаЗаработка + СтрокаНачисления.Результат;
		КонецЕсли;
	КонецЦикла;
	
	СписокПодсказок = Новый СписокЗначений;
	МаксимальнаяДлиннаПояснения = 0;
	МаксимальнаяДлиннаПредставлениеИтога = 0;
	Если СуммаКомпенсации <> 0 Или Объект.ДнейКомпенсацииУдержанияОтпуска <> 0 Тогда
		ПредставлениеИтогаПоКолонке = Формат(СуммаКомпенсации, "ЧЦ=15; ЧДЦ=2");
		
		ОписаниеПодсказки = Новый Структура;
		ОписаниеПодсказки.Вставить("ПредставлениеИтога", ПредставлениеИтогаПоКолонке);
		
		Пояснение = СтрШаблон(НСтр("ru = 'Компенсация отпуска (%1 дн.)'"), Объект.ДнейКомпенсацииУдержанияОтпуска);
		ДлинаПояснения = СтрДлина(Пояснение);
		ДлинаПредставленияИтога = СтрДлина(ПредставлениеИтогаПоКолонке);
		
		ОписаниеПодсказки.Вставить("ОбщаяДлина",  ДлинаПояснения + ДлинаПредставленияИтога);
		
		МаксимальнаяДлиннаПояснения = Макс(МаксимальнаяДлиннаПояснения, ДлинаПояснения);
		МаксимальнаяДлиннаПредставлениеИтога = Макс(МаксимальнаяДлиннаПредставлениеИтога, ДлинаПредставленияИтога);
		
		СписокПодсказок.Добавить(ОписаниеПодсказки, Пояснение);
	КонецЕсли;
	
	Если СуммаЗаработка <> 0 Тогда
		ПредставлениеИтогаПоКолонке = Формат(СуммаЗаработка, "ЧЦ=15; ЧДЦ=2");
		
		ОписаниеПодсказки = Новый Структура;
		ОписаниеПодсказки.Вставить("ПредставлениеИтога", ПредставлениеИтогаПоКолонке);
		
		Пояснение = НСтр("ru = 'Заработная плата'");
		ДлинаПояснения = СтрДлина(Пояснение);
		ДлинаПредставленияИтога = СтрДлина(ПредставлениеИтогаПоКолонке);
		
		ОписаниеПодсказки.Вставить("ОбщаяДлина",  ДлинаПояснения + ДлинаПредставленияИтога);
		
		МаксимальнаяДлиннаПояснения = Макс(МаксимальнаяДлиннаПояснения, ДлинаПояснения);
		МаксимальнаяДлиннаПредставлениеИтога = Макс(МаксимальнаяДлиннаПредставлениеИтога, ДлинаПредставленияИтога);
		
		СписокПодсказок.Добавить(ОписаниеПодсказки, Пояснение);
	КонецЕсли;
	
	МаксимальнаяОбщаяДлинаПредставления = МаксимальнаяДлиннаПояснения + МаксимальнаяДлиннаПредставлениеИтога;
	ШаблонПробелов = "                                        ";
	Для каждого ОписаниеПодсказки Из СписокПодсказок Цикл
		
		ТекстПодсказки = ?(ПустаяСтрока(ТекстПодсказки), "", ТекстПодсказки + Символы.ПС)
			+ ОписаниеПодсказки.Представление + ": "
			+ Лев(ШаблонПробелов, (МаксимальнаяОбщаяДлинаПредставления - ОписаниеПодсказки.Значение.ОбщаяДлина))
			+ ОписаниеПодсказки.Значение.ПредставлениеИтога;
			
	КонецЦикла;
	
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(ЭтотОбъект, "НачисленоРезультат", ТекстПодсказки);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = 0;
	ЗарплатаКадрыПереопределяемый.СостояниеДокумента(Объект, СостояниеДокумента);
	
КонецПроцедуры

&НаКлиенте
Функция КлючевыеРеквизитыЗаполнены()
	
	Если Не ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Необходимо заполнить обязательные для заполнения поля'"));
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СписокТабличныхЧастейДокумента()
	
	СписокТабличныхЧастей = Новый Массив;
	
	СписокТабличныхЧастей.Добавить("Начисления");
	СписокТабличныхЧастей.Добавить("Удержания");
	СписокТабличныхЧастей.Добавить("Взносы");
	СписокТабличныхЧастей.Добавить("НДФЛ");
	СписокТабличныхЧастей.Добавить("ПримененныеВычетыНаДетейИИмущественные");
	
	Возврат СписокТабличныхЧастей;
	
КонецФункции

&НаСервере
Функция ЕстьЗаполненныеТабличныеЧасти()
	
	ДанныеВТЧЕсть = Ложь;
	
	СписокТабличныхЧастей = СписокТабличныхЧастейДокумента();
	
	Для Каждого ИмяТабличнойЧасти Из СписокТабличныхЧастей Цикл
		Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
			ДанныеВТЧЕсть = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ДанныеВТЧЕсть;
	
КонецФункции

&НаСервере
Процедура УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов()
	
	Если ЕстьЗаполненныеТабличныеЧасти() Тогда
		ОтображениеПредупреждения = ОтображениеПредупрежденияПриРедактировании.Отображать;
	Иначе
		ОтображениеПредупреждения = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	КонецЕсли;
	
	Элементы.ПланируемаяДатаВыплаты.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупреждения;
	
КонецПроцедуры

#КонецОбласти

#Область ПодготовкаОтчетов

&НаСервере
Функция КарточкаУчетаПоСтраховымВзносамНаСервере(ФизическоеЛицо) Экспорт
	
	ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	КарточкаУчета = УчетСтраховыхВзносов.КарточкаУчетаПоСтраховымВзносамПодробнее(
		ДокументОбъект,
		Модифицированность,
		ФизическиеЛица,
		Объект.ПериодРегистрации);
	
	Возврат КарточкаУчета;
		
КонецФункции

&НаСервере
Функция НДФЛПодробнееНаСервере(ФизическиеЛица) Экспорт
	
	Если ТипЗнч(ФизическиеЛица) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическиеЛица);
	Иначе
		СписокФизическихЛиц = ФизическиеЛица;
	КонецЕсли;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	НДФЛПодробнее = Новый Массив;
	НДФЛПодробнее.Добавить(ДокументОбъект.Ссылка);
	НДФЛПодробнее.Добавить(УчетНДФЛФормы.РегистрНалоговогоУчетаПоНДФЛ(ДокументОбъект, Модифицированность, СписокФизическихЛиц, Объект.ПериодРегистрации));
	
	Возврат НДФЛПодробнее;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеНДФЛНаСервере(АдресВременногоХранилища)
	
	Параметр = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Объект.НДФЛ.Загрузить(Параметр.НДФЛ);
	Объект.ПримененныеВычетыНаДетейИИмущественные.Загрузить(Параметр.ПримененныеВычетыНаДетейИИмущественные);
	Объект.КорректировкиВыплаты.Загрузить(Параметр.КорректировкиВыплаты);
	Объект.КорректироватьНДФЛ = Параметр.КорректироватьРасчет;;
	ОтобразитьПризнакиКорректировки();
	РассчитатьИтогиПоДокументу();
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ВыплатаДокумента

&НаКлиенте
Процедура ВопросПередВыплатойСледуетПровестиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		Записать();
	КонецЕсли;
	
	ДокументПроведен = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Если ДокументПроведен Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Оповестить("ВыполненаЗаписьДокумента", Новый Структура("ДокументСсылка", Объект.Ссылка));
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось провести документ'"));
		Возврат;
	КонецЕсли;
	
	СформироватьДокументыВыплаты();

КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокументыВыплаты()
	
	Если МожноСоздатьВедомостиПоРасчетномуДокументу(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",             Объект.Организация);
		ПараметрыФормы.Вставить("РасчетныйДокумент",       Объект.Ссылка);
		ПараметрыФормы.Вставить("РежимВыплатыПоДокументу", Истина);
		ПараметрыФормы.Вставить("ПериодСобытия",           Объект.ПериодРегистрации);
		ПараметрыФормы.Вставить("ФизическоеЛицо",          Объект.ФизическоеЛицо);
		ПараметрыФормы.Вставить("ДатаВыплаты",             Объект.ПланируемаяДатаВыплаты);
		
		ОткрытьФорму("Обработка.ПомощникУчетаЗарплаты.Форма.Форма", ПараметрыФормы, Объект.Ссылка); 
	КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент)
	
	МожноСоздатьВедомости = Истина;
	ВзаиморасчетыССотрудникамиПереопределяемый.МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент, МожноСоздатьВедомости);
	Возврат МожноСоздатьВедомости;
	
КонецФункции

#КонецОбласти

#Область Инициализация

ЗакрытьПослеЗаписи = Ложь;

#КонецОбласти