#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект, , ,
		ДополнительныеСвойства.Свойство("ОтключитьПроверкуДатыЗапретаИзменения"));
	
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	// Кадровый учет
	КадровыйУчет.СформироватьКадровыеДвижения(ЭтотОбъект, Движения, ДанныеДляПроведения.КадровыеДвижения);
	
	Если ЗагруженИзВнешнегоИсточника Тогда
		Возврат;
	КонецЕсли;
	
	УчетСтажаПФР.ЗарегистрироватьПериодыВУчетеСтажаПФР(Движения, ДанныеДляРегистрацииВУчетаСтажаПФР());
	
	Если Не ПрименятьВычетыПослеУвольнения Тогда
		
		КадровыйУчет.СформироватьПрекращениеВычетовСтандартныхИНаДетей(
			Движения, ФизическоеЛицо, Организация, КонецМесяца(ДатаУвольнения) + 1, Отказ, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка));
		
	КонецЕсли;
	
	Документы.Увольнение.СформироватьДвиженияМероприятийТрудовойДеятельности(
		Движения.МероприятияТрудовойДеятельности, ДанныеДляПроведения.МероприятияТрудовойДеятельности);
		
	Если Не РассчитатьКомпенсациюНачислитьЗарплату Тогда
		Возврат;
	КонецЕсли;
	
	// Заполним описание данных для проведения в учете начисленной зарплаты.
	ДанныеДляПроведенияУчетЗарплаты = ДанныеДляПроведенияУчетЗарплаты(ДанныеДляПроведения);
	
	ЗарегистрироватьНачисления(Отказ, ДанныеДляПроведения, ДанныеДляПроведенияУчетЗарплаты);
	ЗарегистрироватьОтработанноеВремя(Отказ, ДанныеДляПроведения, ДанныеДляПроведенияУчетЗарплаты);
	
	// Подготовка данных для регистрации удержаний, НДФЛ и Корректировок выплаты в учете начисленной зарплаты.
	УчетНачисленнойЗарплаты.СоздатьВТРаспределениеНачисленийТекущегоДокумента(ДанныеДляПроведенияУчетЗарплаты);
	
	УчетНачисленнойЗарплатыБазовый.ЗарегистрироватьУдержания(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.УдержанияПоСотрудникам);
	
	// НДФЛ
	СформироватьДоходыНДФЛ(Отказ, ДанныеДляПроведения);
	// Дополняем доходы НДФЛ сведениями о распределении по статьям финансирования и/или статьям расходов.
	ОтражениеЗарплатыВУчетеБазовый.ДополнитьСведенияОДоходахНДФЛСведениямиОРаспределенииПоСтатьям(Движения);
	
	ДатаОперации = ДатаОперацииПоНалогомИВзносам();
	УчетНДФЛ.СформироватьНалогиВычеты(Движения, Отказ, Организация, ДатаОперации, ДанныеДляПроведения.НДФЛ);
	МесяцСоцВычета = ?(УчетНДФЛ.ДоходыУчитываютсяТолькоПоДатеВыплаты(ПериодРегистрации, -1), ДанныеДляПроведенияУчетЗарплаты.ПланируемаяДатаВыплаты, ПериодРегистрации);
	УчетНДФЛ.СформироватьСоциальныеВычетыПоУдержаниям(Ссылка, Движения, Отказ, Организация, ДатаОперации, МесяцСоцВычета, ДанныеДляПроведения.УдержанияПоСотрудникам);
	УчетНДФЛ.СформироватьДокументыУчтенныеПриРасчетеДляМежрасчетногоДокументаПоВременнойТаблице(Движения, Отказ, Организация, ДанныеДляПроведения.МенеджерВременныхТаблиц, Ссылка);
	
	УчетНДФЛ.ДополнитьДвиженияСписаниемАванса(Ссылка, Движения, Организация, ДатаОперации);
	
	УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(ДанныеДляПроведения.НДФЛПоСотрудникам, Организация, ДатаОперации);
	УчетНачисленнойЗарплаты.СформироватьСтрокиНДФЛКакЗачетАванса(Движения, ДанныеДляПроведения.НДФЛПоСотрудникам, ПериодРегистрации);
	УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛПоСотрудникам(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.НДФЛПоСотрудникам);
	УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛКЗачетуВозврату(ДанныеДляПроведенияУчетЗарплаты, Отказ,
			ДанныеДляПроведения.КорректировкиВыплатыПоСотрудникам, ДанныеДляПроведения.НДФЛПоСотрудникам);
	
	// Страховые взносы
	УчетСтраховыхВзносов.СформироватьСведенияОДоходахСтраховыеВзносы(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения.МенеджерВременныхТаблиц, , Истина);
	
	УчетСтраховыхВзносов.СформироватьИсчисленныеВзносы(Движения, Отказ, Организация, КонецМесяца(ПериодРегистрации), ДанныеДляПроведения.СтраховыеВзносы);
	УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, Организация, ПериодРегистрации, Ссылка, ДанныеДляПроведения.СтраховыеВзносы);
		
	// Бух.учет
	Если Не Отказ Тогда
		
		Движения.НачисленияУдержанияПоСотрудникам.Записать();
		Движения.НачисленияУдержанияПоСотрудникам.Записывать = Ложь;
		
		// формирование проводок
		ДанныеДляПроведения = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
		ДанныеДляПроведения.НачисленияУдержания = Движения.НачисленияУдержанияПоСотрудникам.Выгрузить();
		ДанныеДляПроведения.СтраховыеВзносы = Движения.СтраховыеВзносыПоФизическимЛицам.Выгрузить();
		СтрокаСписокТаблиц = "НачисленнаяЗарплатаИВзносы, НачисленныйНДФЛ, УдержаннаяЗарплата";
		ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения, СтрокаСписокТаблиц);
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		Сотрудник = ДанныеЗаполнения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, Сотрудник)
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	КадровыйУчет.ПроверитьВозможностьПроведенияПоКадровомуУчету(
		ДатаУвольнения,
		Сотрудник,
		Ссылка,
		Отказ,
		Перечисления.ВидыКадровыхСобытий.Увольнение);
		
	Если НЕ ДокументЗаполненПравильно() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не РассчитатьКомпенсациюНачислитьЗарплату Или ЗагруженИзВнешнегоИсточника Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПланируемаяДатаВыплаты");
	КонецЕсли;
	
	Если ЗагруженИзВнешнегоИсточника Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПериодРегистрации");
	КонецЕсли;
	
	Если ОтразитьВТрудовойКнижке И Не ЗагруженИзВнешнегоИсточника Тогда
		
		ЭлектронныеТрудовыеКнижки.ПроверкаЗаполненияВторогоДокументаОснования(ЭтотОбъект, Ссылка, Отказ);
		
		СотрудникиДаты = ЭлектронныеТрудовыеКнижки.ПустаяТаблицаСотрудникиДатыДляПроверкиПоследовательностиМероприятий();
		ЭлектронныеТрудовыеКнижки.ДобавитьСотрудникаВТаблицуСотрудникиДаты(
			СотрудникиДаты, Сотрудник, ДатаУвольнения);
		
		ИсключаемыеРегистраторы = Новый Массив;
		ИсключаемыеРегистраторы.Добавить(Ссылка);
		
		ЭлектронныеТрудовыеКнижки.ПроверкаВозможностиРегистрацииМероприятияУвольнения(
			СотрудникиДаты, Ссылка, ИсключаемыеРегистраторы, Отказ);
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗагруженИзВнешнегоИсточника Тогда
		ПериодРегистрации = НачалоМесяца(Дата);
	КонецЕсли;
	
	Документы.Увольнение.ЗаполнитьДатуЗапретаРедактирования(ЭтотОбъект);
	
	Если НЕ Отказ И ЭтоНовый() Тогда
		УстановитьНовыйНомер();
		Если ПустаяСтрока(НомерПриказа) Тогда
			НомерПриказа = ЭлектронныеТрудовыеКнижкиВызовСервера.НомерНаПечать(Номер);
		КонецЕсли;
	КонецЕсли;
	Если ЗагруженИзВнешнегоИсточника Тогда
		Возврат;
	КонецЕсли;
	Начислено = Начисления.Итог("Результат");
	Удержано  = Удержания.Итог("Результат");
	КолонкиНалогов = УчетНДФЛКлиентСервер.РесурсыИсчисленногоНалогаВМассиве("Налог", Ложь, Ложь);
	Для Каждого Колонка Из КолонкиНалогов Цикл
		Удержано = Удержано + НДФЛ.Итог(Колонка)
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	НомерПриказа = "";
	ИдентификаторИсточника = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведенияУчетЗарплаты(ДанныеДляПроведения = Неопределено);
	
	Если ДанныеДляПроведения = Неопределено Тогда
		ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	КонецЕсли;
	
	ДанныеДляПроведенияУчетЗарплаты = ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения();
	ДанныеДляПроведенияУчетЗарплаты.Движения 				= Движения;
	ДанныеДляПроведенияУчетЗарплаты.Организация 			= Организация;
	ДанныеДляПроведенияУчетЗарплаты.ПериодРегистрации 		= ПериодРегистрации;
	ДанныеДляПроведенияУчетЗарплаты.ПланируемаяДатаВыплаты 	= ПланируемаяДатаВыплаты;
	ДанныеДляПроведенияУчетЗарплаты.ПорядокВыплаты 			= Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
	ДанныеДляПроведенияУчетЗарплаты.ОкончательныйРасчет		= Истина;
	ДанныеДляПроведенияУчетЗарплаты.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	
	Возврат ДанныеДляПроведенияУчетЗарплаты;
	
КонецФункции

Функция ПолучитьДанныеДляПроведения()
	
	ДанныеДляПроведения = РасчетЗарплаты.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
	
	РасчетЗарплаты.ЗаполнитьНачисления(ДанныеДляПроведения, Ссылка, , "ДатаНачала");
	РасчетЗарплаты.ЗаполнитьУдержания(ДанныеДляПроведения, Ссылка, , "ДатаНачала");
	РасчетЗарплаты.ЗаполнитьСписокФизическихЛиц(ДанныеДляПроведения, Ссылка);
	РасчетЗарплаты.ЗаполнитьДанныеНДФЛ(ДанныеДляПроведения, Ссылка);
	РасчетЗарплаты.ЗаполнитьДанныеКорректировкиВыплаты(ДанныеДляПроведения, Ссылка);
	РасчетЗарплаты.ЗаполнитьДанныеСтраховыхВзносов(ДанныеДляПроведения, Ссылка);
	
	ОтражениеЗарплатыВБухучете.СоздатьВТНачисленияСДаннымиЕНВД(Организация, ПериодРегистрации,
			ДанныеДляПроведения.МенеджерВременныхТаблиц, ДанныеДляПроведения.НачисленияПоСотрудникам);
	
	ДатаСобытия = КонецДня(ДатаУвольнения) + 1;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УсловияОтбора = Новый Массив;
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(УсловияОтбора, "Регистратор", "<>", Ссылка);
	
	ПоляОтбораПериодическихДанных = Новый Структура;
	ПоляОтбораПериодическихДанных.Вставить("КадроваяИсторияСотрудников", УсловияОтбора);
	
	КадровыйУчет.СоздатьНаДатуВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		Сотрудник,
		"ГоловнаяОрганизация,Организация,Подразделение,Должность,ВидЗанятости,ФизическоеЛицо,КоличествоСтавок",
		ДатаСобытия,
		ПоляОтбораПериодическихДанных);
		
	Запрос.УстановитьПараметр("ДатаСобытия", ДатаСобытия);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ДатаСобытия КАК ДатаСобытия,
	|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	КадровыеДанныеСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	КадровыеДанныеСотрудников.Организация КАК Организация,
	|	КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
	|	КадровыеДанныеСотрудников.Должность КАК Должность,
	|	КадровыеДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение) КАК ВидСобытия,
	|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	|	КадровыеДанныеСотрудников.КоличествоСтавок КАК КоличествоСтавок
	|ИЗ
	|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	// Первый набор данных для проведения - таблица для формирования кадровых движений, истории графиков, авансов.
	ДанныеДляПроведения.Вставить("КадровыеДвижения", РезультатыЗапроса[0].Выгрузить());
	
	ДанныеДляПроведения.Вставить("МероприятияТрудовойДеятельности",
		Документы.Увольнение.ДанныеДляПроведенияМероприятияТрудовойДеятельности(Ссылка).Получить(Ссылка));
		
	Возврат ДанныеДляПроведения;

КонецФункции

Функция ДокументЗаполненПравильно(ТихийРежим = Ложь) Экспорт
	
	ТекстСообщения = "";
	СтруктураСообщений  = Новый Соответствие;
	ДокументЗаполненПравильно = Истина;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Организация"" обязательно к заполнению.'");
		СтруктураСообщений.Вставить("Организация",ТекстСообщения);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Сотрудник"" обязательно к заполнению.'");
		СтруктураСообщений.Вставить("Сотрудник",ТекстСообщения);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаУвольнения) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Дата увольнения"" обязательно к заполнению.'");
		СтруктураСообщений.Вставить("ДатаУвольнения",ТекстСообщения);
	КонецЕсли;
	
	ДокументЗаполненПравильно = СтруктураСообщений.Количество() = 0;
	
	Если НЕ ТихийРежим И НЕ ДокументЗаполненПравильно Тогда
		Для каждого Сообщение Из СтруктураСообщений Цикл
			ОбщегоНазначения.СообщитьПользователю(Сообщение.Значение,,"Объект" + ?(Сообщение.Ключ = "","",".") + Сообщение.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДокументЗаполненПравильно;	
	
КонецФункции

Функция ДанныеДляРегистрацииВУчетаСтажаПФР()
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Ссылка);
	
	ДанныеДляРегистрацииВУчете = Документы.Увольнение.ДанныеДляРегистрацииВУчетаСтажаПФР(МассивСсылок);
		
	Возврат ДанныеДляРегистрацииВУчете[Ссылка];
														
КонецФункции

Процедура СформироватьДоходыНДФЛ(Отказ = Ложь, ДанныеДляПроведения = Неопределено) Экспорт
	
	Если ДанныеДляПроведения = Неопределено Тогда
		ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	КонецЕсли;
	
	ДатаОперации = ДатаОперацииПоНалогомИВзносам();
	УчетНДФЛ.СформироватьДоходыНДФЛПоНачислениям(
		Движения, Отказ, Организация, ДатаОперации, ПланируемаяДатаВыплаты, ДанныеДляПроведения.МенеджерВременныхТаблиц, ПериодРегистрации, Ложь, Ложь, , Ссылка);
	
КонецПроцедуры

Функция ДатаОперацииПоНалогомИВзносам()
	Возврат УчетНДФЛ.ДатаОперацииПоДокументу(Дата, ПериодРегистрации);
КонецФункции

Процедура ЗарегистрироватьНачисления(Отказ = Ложь, ДанныеДляПроведения = Неопределено, ДанныеДляПроведенияУчетЗарплаты = Неопределено, ЗаписыватьДвижения = Ложь) Экспорт
	
	Если ДанныеДляПроведения = Неопределено Тогда
		ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	КонецЕсли;
	
	Если ДанныеДляПроведенияУчетЗарплаты = Неопределено Тогда
		ДанныеДляПроведенияУчетЗарплаты = ДанныеДляПроведенияУчетЗарплаты(ДанныеДляПроведения);
	КонецЕсли;
	
	УчетНачисленнойЗарплаты.ЗарегистрироватьНачисления(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.НачисленияПоСотрудникам, Неопределено, ЗаписыватьДвижения);
	
КонецПроцедуры

Процедура ЗарегистрироватьОтработанноеВремя(Отказ = Ложь, ДанныеДляПроведения = Неопределено, ДанныеДляПроведенияУчетЗарплаты = Неопределено) Экспорт
	
	Если ДанныеДляПроведения = Неопределено Тогда
		ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	КонецЕсли;
	
	Если ДанныеДляПроведенияУчетЗарплаты = Неопределено Тогда
		ДанныеДляПроведенияУчетЗарплаты = ДанныеДляПроведенияУчетЗарплаты(ДанныеДляПроведения);
	КонецЕсли;
	
	УчетНачисленнойЗарплаты.ЗарегистрироватьОтработанноеВремя(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.ОтработанноеВремяПоСотрудникам, Истина);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли