#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// ЭДОБЗК

Функция ОбъектыСтороннихПечатныхФорм(ОбъектыТипа, ИдентификаторыПечатныхФорм = Неопределено) Экспорт
	
	Возврат Новый Соответствие;
	
КонецФункции

// Конец ЭДОБЗК

#КонецОбласти

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;

	ВнешниеДанные = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчетПриВнешнейЗарплате");
	СохраненныйДокумент = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Ключ");
	Если ЗначениеЗаполнено(СохраненныйДокумент)Тогда
		ВнешниеДанные = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "ЗагруженИзВнешнегоИсточника");
	КонецЕсли;
	
	Если ВнешниеДанные Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "ФормаДокументаВнешниеДанные";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке();
КонецФункции

#Область УчетПособий

// Возникает при заполнении документа УведомлениеОПрекращенииОтпускаПоУходуЗаРебенком на основании текущего документа.
Процедура ЗаполнитьУведомлениеОПрекращенииОтпускаПоУходуЗаРебенкомПоОснованию(УвольнениеСсылка, РеквизитыУведомления) Экспорт
	РеквизитыУвольнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УвольнениеСсылка, "Организация, Сотрудник, Номер, Дата, ДатаУвольнения");
	
	РеквизитыУведомления.Вставить("ТипПриказа", Перечисления.ОснованияПрекращенияПособийПоУходу.ПриказОбУвольнении);
	РеквизитыУведомления.Вставить("Организация", РеквизитыУвольнения.Организация);
	РеквизитыУведомления.Вставить("Сотрудник", РеквизитыУвольнения.Сотрудник);
	РеквизитыУведомления.Вставить("ДатаПриказа", РеквизитыУвольнения.Дата);
	РеквизитыУведомления.Вставить("НомерПриказа", РеквизитыУвольнения.Номер);
	РеквизитыУведомления.Вставить("ДатаПрекращенияОплаты", РеквизитыУвольнения.ДатаУвольнения);
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Возврат;
	КонецЕсли;
	
	КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
	КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Уведомление о заключении договора с иностранным гражданином'");
	КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
	КомандаСоздатьНаОсновании.Обработчик = "КадровыйУчетКлиент.ВвестиУведомлениеОЗаключенииДоговораСИностранцем";
	КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьКадровыйУчетОсновнойИнтерфейс";
	
	КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
	КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
	КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Уведомление о расторжении договора с иностранным гражданином'");
	КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
	КомандаСоздатьНаОсновании.Обработчик = "КадровыйУчетКлиент.ВвестиУведомлениеОРасторженииДоговораСИностранцем";
	КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьКадровыйУчетОсновнойИнтерфейс";
	
	КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
	КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Уведомлениие об исполнении обязательств перед иностранным гражданином'");
	КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
	КомандаСоздатьНаОсновании.Обработчик = "КадровыйУчетКлиент.ВвестиУведомлениеОбИсполненииОбязательствПередИностраннымСпециалистом";
	КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьКадровыйУчетОсновнойИнтерфейс";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Приказ об увольнении
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т8";
	КомандаПечати.Представление = НСтр("ru = 'Приказ об увольнении (Т-8)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 10;
	КомандаПечати.ФункциональныеОпции = "ИспользоватьКадровыйУчет";
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "КадровыйДокументВнешнегоИсточника", Ложь);
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т61";
	КомандаПечати.Представление = НСтр("ru = 'Записка-расчет при увольнении (Т-61)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 20;
	КомандаПечати.ФункциональныеОпции = "ИспользоватьКадровыйУчет";
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "КадровыйДокументВнешнегоИсточника", Ложь);
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьРасчетаСреднегоЗаработка";
	КомандаПечати.Идентификатор = "РасчетСреднегоЗаработка";
	КомандаПечати.Представление = НСтр("ru = 'Расчет среднего заработка'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 30;
	КомандаПечати.ФункциональныеОпции = "ИспользоватьКадровыйУчет";
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "КадровыйДокументВнешнегоИсточника", Ложь);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчетПриВнешнейЗарплате") Тогда
		Отчеты.ПечатнаяФормаСправкаОСреднемЗаработке.ДобавитьКомандуПечати(КомандыПечати);
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеДляРегистрацииВУчетаСтажаПФР(МассивСсылок) Экспорт
	ДанныеДляРегистрацииВУчете = Новый Соответствие;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Увольнение.Ссылка,
	|	Увольнение.Сотрудник,
	|	Увольнение.ДатаУвольнения КАК ДатаНачала,
	|	Увольнение.Организация КАК Организация
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|ГДЕ
	|	Увольнение.Ссылка В(&МассивСсылок)";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеДляРегистрацииВУчетеПоДокументу = УчетСтажаПФР.ДанныеДляРегистрацииВУчетеСтажаПФР();
		ДанныеДляРегистрацииВУчете.Вставить(Выборка.Ссылка, ДанныеДляРегистрацииВУчетеПоДокументу); 
		
		ОписаниеПериода = УчетСтажаПФР.ОписаниеРегистрируемогоПериода();
		ОписаниеПериода.Сотрудник = Выборка.Сотрудник;
		ОписаниеПериода.ДатаНачалаПериода = КонецДня(Выборка.ДатаНачала) + 1;
		ОписаниеПериода.Состояние = Перечисления.СостоянияСотрудника.Увольнение;
			
		РегистрируемыйПериод = УчетСтажаПФР.ДобавитьЗаписьВДанныеДляРегистрацииВУчета(ДанныеДляРегистрацииВУчетеПоДокументу, ОписаниеПериода);
		УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "Организация", Выборка.Организация);
		УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "ВидСтажаПФР", Перечисления.ВидыСтажаПФР2014.НеВключаетсяВСтраховойСтаж);
	КонецЦикла;	

	Возврат ДанныеДляРегистрацииВУчете;														
КонецФункции

Процедура ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента) Экспорт
	
	ЗарплатаКадры.ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента, "ДатаУвольнения");
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, СписокСотрудников = Неопределено) Экспорт
	
	ПроверитьДокументыДляПечати(МассивОбъектов);
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Т8") Тогда
		ПечатьТ8(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Т61") Тогда
		ПечатьТ61(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПечатьТ8(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	ДанныеСсылок = ОписаниеДокументовКадровогоУчетаУвольнений(МассивОбъектов);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	КадровыйУчет.ВывестиНаПечатьТ8(ПараметрыПечати, ДокументРезультат, ДанныеСсылок.Строки,
		УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_Т8"), , ОбъектыПечати);
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_MXL_Т8",
		НСтр("ru = 'Приказ об увольнении (Т-8)'"),
		ДокументРезультат,,);
		
КонецПроцедуры

Процедура ПечатьТ61(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
	Иначе
		Возврат;
	КонецЕсли;
	
	ДанныеСсылок = ОписаниеДокументовКадровогоУчетаУвольнений(МассивОбъектов);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_ЗапискаРасчетПриУвольнении";
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВидыНДФЛ", МодульРасчетЗарплатыДляНебольшихОрганизаций.ВидыНДФЛДополненияРасчетнойБазыУдержаний());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Увольнение.Ссылка КАК Ссылка,
	|	Увольнение.ПериодРегистрации КАК ПериодРегистрации,
	|	ВЫБОР
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска)
	|			ТОГДА ИСТИНА
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке)
	|			ТОГДА ИСТИНА
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОплатаОтпуска,
	|	НачисленияУвольнения.ДатаНачала КАК ДатаНачала,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке)
	|				ТОГДА 0
	|			КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке)
	|				ТОГДА 0
	|			ИНАЧЕ НачисленияУвольнения.ОплаченоДней
	|		КОНЕЦ) КАК ОплаченоДней,
	|	СУММА(НачисленияУвольнения.Результат) КАК Результат
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Увольнение.Начисления КАК НачисленияУвольнения
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыРасчетов
	|			ПО НачисленияУвольнения.Начисление = ВидыРасчетов.Ссылка
	|		ПО Увольнение.Ссылка = НачисленияУвольнения.Ссылка
	|ГДЕ
	|	НачисленияУвольнения.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	Увольнение.Ссылка,
	|	ВЫБОР
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска)
	|			ТОГДА ИСТИНА
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке)
	|			ТОГДА ИСТИНА
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	НачисленияУвольнения.ДатаНачала,
	|	Увольнение.ПериодРегистрации
	|ИТОГИ
	|	МИНИМУМ(ПериодРегистрации),
	|	СУММА(Результат)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НДФЛУвольнения.Регистратор КАК Ссылка,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛУвольнения.ЗачетАвансаНДФЛ
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛУвольнения.Сумма
	|		КОНЕЦ) КАК Налог,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛУвольнения.ЗачетАвансаНДФЛ
	|				ТОГДА НДФЛУвольнения.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЗачтеноАвансовыхПлатежей
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НДФЛУвольнения
	|ГДЕ
	|	НДФЛУвольнения.Регистратор В(&МассивОбъектов)
	|	И НДФЛУвольнения.НачислениеУдержание В(&ВидыНДФЛ)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛУвольнения.Регистратор
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Увольнение.Ссылка КАК Ссылка,
	|	УдержанияУвольнения.Удержание КАК Удержание,
	|	СУММА(УдержанияУвольнения.Результат) КАК Результат
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Увольнение.Удержания КАК УдержанияУвольнения
	|		ПО Увольнение.Ссылка = УдержанияУвольнения.Ссылка
	|ГДЕ
	|	УдержанияУвольнения.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	Увольнение.Ссылка,
	|	УдержанияУвольнения.Удержание";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	Начисления = МассивРезультатов[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	НДФЛ       = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Удержания  = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_Т61");
	
	ОбластьШапка                         = Макет.ПолучитьОбласть("ЛицеваяСторона");
	ОбластьРасчетСреднего                = Макет.ПолучитьОбласть("РасчетСреднего");
	ОбластьРасчетСреднегоИтого           = Макет.ПолучитьОбласть("РасчетСреднегоИтого");
	ОбластьДополнительнаяСтрокаЗаработка = Макет.ПолучитьОбласть("ДополнительнаяСтрокаЗаработка");
	ОбластьПодвалРасчета                 = Макет.ПолучитьОбласть("Подвал");
	
	ВалютаУчета = ЗарплатаКадры.ВалютаУчетаЗаработнойПлаты();
	
	ПервыйПриказ = Истина;
	
	Менеджер                = ОбщегоНазначения.МенеджерОбъектаПоСсылке(МассивОбъектов[0]);
	ДанныеДокументов        = Обработки.ПечатьРасчетаСреднегоЗаработка.ДанныеДокументовРасчетаСреднегоЗаработка(Менеджер, МассивОбъектов, "");
	ОписаниеДокумента       = Обработки.ПечатьРасчетаСреднегоЗаработка.ОписаниеДокументаРассчитывающегоСреднийЗаработок(Менеджер, "");
	ДанныеОСреднемЗаработке = Обработки.ПечатьРасчетаСреднегоЗаработка.ТаблицыДанныхОСреднемЗаработкеДокументов(ОписаниеДокумента, МассивОбъектов);
	
	ПараметрыПолученияДанныхСреднего = МодульРасчетЗарплатыДляНебольшихОрганизаций.ПараметрыПолученияДанныхСреднегоОбщего();
	
	Для Каждого ДанныеСотрудников Из ДанныеСсылок.Строки Цикл
		
		Для Каждого ДанныеНаПечать Из ДанныеСотрудников.Строки Цикл
			
			НомерСтрокиНачалаОбласти = ДокументРезультат.ВысотаТаблицы + 1;
			
			Параметры = СтруктураПараметровЗапискиРасчетаТ61();
			
			//////////////////////////////////////////////////////////////////////
			// Первая страница
			// Основной отпуск
			
			Параметры.НазваниеОрганизации  = ДанныеНаПечать.РаботаОрганизацияНаименованиеСокращенное;
			Параметры.КодПоОКПО            = ДанныеНаПечать.РаботаОрганизацияКодПоОКПО;
			Параметры.ДатаДок              = ДанныеНаПечать.СсылкаНаОбъектДатаНаПечать;
			Параметры.НомерДок             = ДанныеНаПечать.СсылкаНаОбъектНомерНаПечать;
			Параметры.ТрудовойДоговорНомер = "";
			Параметры.Работник             = ДанныеНаПечать.ЛичныеДанныеФИОФамилияИмяОтчествоВДательномПадеже;
			Параметры.ТабельныйНомер       = ДанныеНаПечать.РаботаСотрудникТабельныйНомерНаПечать;
			Параметры.Подразделение        = ДанныеНаПечать.РаботаПодразделениеНаПечать;
			Параметры.Должность            = ДанныеНаПечать.РаботаДолжность;
			Параметры.СтатьяТКРФ           = ДанныеНаПечать.РаботаСтатьяТКРФНаПечать;
			
			Параметры.БухгалтерРасшифровкаПодписи              = ДанныеНаПечать.СсылкаНаОбъектБухгалтерРасшифровкаПодписи;
			Параметры.РаботникКадровойСлужбыРасшифровкаПодписи = ДанныеНаПечать.СсылкаНаОбъектРаботникКадровойСлужбыРасшифровкаПодписи;
			Параметры.ДолжностьРаботникаКадровойСлужбы         = ДанныеНаПечать.СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы;
			
			ПредставлениеДатыУвольнения = Формат(ДанныеНаПечать.РаботаДатаУвольнения, "ДЛФ=DD");
			Параметры.ДатаУвольнения    = ?(ЗначениеЗаполнено(ДанныеНаПечать.РаботаДатаУвольненияНаПечать), ПредставлениеДатыУвольнения, "      ");
			
			ПредставлениеДатыНачалаПериодаОтпуска = Формат(ДанныеНаПечать.СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск, "ДЛФ=DD");
			Параметры.РабочийГодС = ?(ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск), ПредставлениеДатыНачалаПериодаОтпуска, "      ");
			
			ПредставлениеДатыОкончанияПериодаОтпуска = Формат(ДанныеНаПечать.СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск, "ДЛФ=DD");
			Параметры.РабочийГодПо = ?(ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск), ПредставлениеДатыОкончанияПериодаОтпуска, "      ");
			
			Параметры.ДнейКомпенсации = ДанныеНаПечать.СсылкаНаОбъектДнейКомпенсацииУдержанияОтпуска;
			
			//////////////////////////////////////////////////////////////////////
			// Подвал первой страницы
			
			НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
			Если Не ПервыйПриказ Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			Иначе
				ПервыйПриказ = Ложь;
			КонецЕсли;
			
			ОбластьШапка.Параметры.Заполнить(Параметры);
			
			ДокументРезультат.Вывести(ОбластьШапка);
			
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			
			//////////////////////////////////////////////////////////////////////
			// Вторая страница
			// Расчет среднего заработка
			
			ДанныеДокумента                = ДанныеДокументов.Найти(ДанныеНаПечать.СсылкаНаОбъект, "Ссылка");
			ТаблицыДанныхОСреднемЗаработке = ДанныеОСреднемЗаработке[ДанныеНаПечать.СсылкаНаОбъект];
			ТаблицыПоСотруднику            = Обработки.ПечатьРасчетаСреднегоЗаработка.ТаблицыДанныхОСреднемЗаработкеПоСотруднику(ДанныеНаПечать.РаботаСотрудник, ТаблицыДанныхОСреднемЗаработке);
			
			ПараметрыПолученияДанныхСреднего.Вставить("ТаблицыПоСотруднику",  ТаблицыПоСотруднику); 
			ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаПериода",    ДанныеДокумента.НачалоРасчетногоПериода);
			ПараметрыПолученияДанныхСреднего.Вставить("ДатаОкончанияПериода", ДанныеДокумента.ОкончаниеРасчетногоПериода); 
			ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаСобытия",    ДанныеДокумента.ДатаНачалаСобытия);
			
			ДанныеРасчетаСреднегоЗаработка = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеРасчетаСреднегоЗаработкаОбщего(ПараметрыПолученияДанныхСреднего);
			
			ОбщаяТаблицаДанныхОСреднем = Новый ТаблицаЗначений;
			ОбщаяТаблицаДанныхОСреднем.Колонки.Добавить("Месяц", Новый ОписаниеТипов("Дата"));
			ОбщаяТаблицаДанныхОСреднем.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
		
			Для каждого Элемент Из ДанныеРасчетаСреднегоЗаработка["Заработок"] Цикл
				НоваяСтрока = ОбщаяТаблицаДанныхОСреднем.Добавить();
				НоваяСтрока.Месяц = Элемент["Месяц"];
				НоваяСтрока.Сумма = Элемент["Учтено"];
			КонецЦикла;
			
			ОбщаяТаблицаДанныхОСреднем.Свернуть("Месяц", "Сумма");
			ОбщаяТаблицаДанныхОСреднем.Сортировать("Месяц");
			
			МесяцСреднегоЗаработка = 1;
			Для каждого ПериодСреднего Из ОбщаяТаблицаДанныхОСреднем Цикл
				Если МесяцСреднегоЗаработка > 12 Тогда
					Прервать;
				КонецЕсли;
				СтрокаГод = "Год" + МесяцСреднегоЗаработка;
				СтрокаМесяц = "Месяц" + МесяцСреднегоЗаработка;
				СтрокаСумма = "Сумма" + МесяцСреднегоЗаработка;
				Параметры.Вставить(СтрокаГод, Формат(ПериодСреднего.Месяц, "ДФ=yyyy"));
				Параметры.Вставить(СтрокаМесяц, Формат(ПериодСреднего.Месяц, "ДФ=MMMM"));
				Параметры.Вставить(СтрокаСумма, ПериодСреднего.Сумма);
				МесяцСреднегоЗаработка = МесяцСреднегоЗаработка + 1;
			КонецЦикла;
			
			МассивДополнительныхСтрокЗаработка = Новый Массив; 
			
			МесяцСреднегоЗаработка = 0;
			Для каждого ПериодСреднего Из ОбщаяТаблицаДанныхОСреднем Цикл
				МесяцСреднегоЗаработка = МесяцСреднегоЗаработка + 1;
				Если МесяцСреднегоЗаработка < 13 Тогда
					Продолжить;
				КонецЕсли;
				МассивДополнительныхСтрокЗаработка.Добавить(Новый Структура("Год,Месяц,Сумма", Формат(ПериодСреднего.Месяц, "ДФ=yyyy"), Формат(ПериодСреднего.Месяц, "ДФ=MMMM"), ПериодСреднего.Сумма));
			КонецЦикла;
			
			ВсегоВремени   = МодульРасчетЗарплатыДляНебольшихОрганизаций.ИтогиПоПолю(ДанныеРасчетаСреднегоЗаработка["ОтработанноеВремя"], "Учтено");
			ВсегоЗаработок = МодульРасчетЗарплатыДляНебольшихОрганизаций.ИтогиПоПолю(ДанныеРасчетаСреднегоЗаработка["Заработок"], "Учтено");
			
			Параметры.Вставить("ВсегоДнейРасчетногоПериода", ВсегоВремени);
			Параметры.Вставить("ВсегоУчтено",                ВсегоЗаработок);
			Параметры.Вставить("ЗаработокЗаЕдиницуВремени",  ?(ВсегоВремени = 0, 0, Окр(ВсегоЗаработок / ВсегоВремени, 2)));
			
			//////////////////////////////////////////////////////////////////////
			// Начисления
			
			Начисления.Сбросить();
			
			СтрТекущийМесяц = "";
			СтрБудущиеМесяцы = "";
			НачисленоОтпускныхЗаТекущийМесяц = 0;
			ДругиеНачисленияЗаТекущийМесяц = 0;
			ВсегоНачислено = 0;
			
			Если Начисления.НайтиСледующий(Новый Структура("Ссылка", ДанныеНаПечать.СсылкаНаОбъект)) Тогда
				
				ВсегоНачислено = Начисления.Результат;
				ВыборкаНачислений = Начисления.Выбрать();
				Пока ВыборкаНачислений.Следующий() Цикл
					Если ВыборкаНачислений.ОплатаОтпуска Тогда
						НачисленоОтпускныхЗаТекущийМесяц = НачисленоОтпускныхЗаТекущийМесяц + ВыборкаНачислений.Результат;
					Иначе
						ДругиеНачисленияЗаТекущийМесяц = ДругиеНачисленияЗаТекущийМесяц + ВыборкаНачислений.Результат;
					КонецЕсли;
				КонецЦикла;
				
				СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СтрБудущиеМесяцы, 2);
				
				Параметры.Вставить("НачисленоКомпенсации", НачисленоОтпускныхЗаТекущийМесяц);
				Параметры.Вставить("ВсеДругиеНачисления",  ДругиеНачисленияЗаТекущийМесяц);
				Параметры.Вставить("ВсегоВыплат",          ВсегоНачислено);
				
			КонецЕсли;
			
			ОбластьРасчетСреднего.Параметры.Заполнить(Параметры);
			ДокументРезультат.Вывести(ОбластьРасчетСреднего);
			
			Для Каждого ДополнительнаяСтрокаЗаработка Из МассивДополнительныхСтрокЗаработка Цикл
				ОбластьДополнительнаяСтрокаЗаработка.Параметры.Заполнить(ДополнительнаяСтрокаЗаработка);
				ДокументРезультат.Вывести(ОбластьДополнительнаяСтрокаЗаработка);
			КонецЦикла;	
			
			ОбластьРасчетСреднегоИтого.Параметры.Заполнить(Параметры);
			ДокументРезультат.Вывести(ОбластьРасчетСреднегоИтого);
			
			ВсегоУдержано = 0;
			
			//////////////////////////////////////////////////////////////////////
			// НДФЛ
			
			НДФЛ.Сбросить();
			СуммаНДФЛ = 0;
			Если НДФЛ.НайтиСледующий(Новый Структура("Ссылка", ДанныеНаПечать.СсылкаНаОбъект)) Тогда 
				СуммаНДФЛ = НДФЛ.Налог + НДФЛ.ЗачтеноАвансовыхПлатежей;
				Параметры.Вставить("СуммаНДФЛ", СуммаНДФЛ);
			КонецЕсли;
			ВсегоУдержано = ВсегоУдержано + СуммаНДФЛ;
			
			//////////////////////////////////////////////////////////////////////
			// Удержания
			Удержания.Сбросить(); 
			СуммаПрочихУдержаний = 0;
			
			Если Удержания.НайтиСледующий(Новый Структура("Ссылка", ДанныеНаПечать.СсылкаНаОбъект)) Тогда
				СуммаПрочихУдержаний = СуммаПрочихУдержаний + Удержания.Результат;
				Параметры.Вставить("ПрочиеУдержания", СуммаПрочихУдержаний);
			КонецЕсли;
			ВсегоУдержано = ВсегоУдержано + СуммаПрочихУдержаний;
			
			Параметры.Вставить("ВсегоУдержано", ВсегоУдержано);
			
			КВыплате = ?(ВсегоНачислено - ВсегоУдержано > 0, ВсегоНачислено - ВсегоУдержано, 0);
			Параметры.Вставить("КВыплате", КВыплате);
			
			Параметры.Вставить("КВыплатеПрописью", РаботаСКурсамиВалют.СформироватьСуммуПрописью(КВыплате, ВалютаУчета));
			Параметры.КВыплатеРублиКопейки =
				"(" + Формат(Цел(Параметры.КВыплате),"ЧДЦ=0; ЧН=-") 
				+ Символы.НПП + НСтр("ru = 'руб'") + Символы.НПП 
				+ Формат((Параметры.КВыплате - Цел(Параметры.КВыплате)) * 100,"ЧЦ=2; ЧДЦ=0; ЧН=-")
				+ Символы.НПП + НСтр("ru = 'коп'") + ")";
			
			//////////////////////////////////////////////////////////////////////
			// Подвал второй страницы
			
			ОбластьПодвалРасчета.Параметры.Заполнить(Параметры);
			ДокументРезультат.Вывести(ОбластьПодвалРасчета);
			
			ЭДОБЗК.ЗадатьДетальнуюОбластьПечати(ПараметрыПечати, ДокументРезультат, "ПФ_MXL_Т61", НомерСтрокиНачалаОбласти,
				ДанныеНаПечать, ДанныеНаПечать.СсылкаНаОбъект);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеНаПечать.СсылкаНаОбъект);
			
		КонецЦикла;
		
	КонецЦикла;
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_MXL_Т61",
		НСтр("ru = 'Записка-расчет при увольнении (Т-61)'"),
		ДокументРезультат,,);
	
КонецПроцедуры

Функция ОписаниеДокументовКадровогоУчетаУвольнений(МассивОбъектов);
	
	ДанныеСсылок = Новый ДеревоЗначений;
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъект");
	ДанныеСсылок.Колонки.Добавить("РаботаОрганизацияНаименованиеСокращенное");
	ДанныеСсылок.Колонки.Добавить("РаботаОрганизацияКодПоОКПО");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДата");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДатаНаПечать");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектНомерНаПечать");
	ДанныеСсылок.Колонки.Добавить("РаботаДатаУвольнения");
	ДанныеСсылок.Колонки.Добавить("РаботаДатаУвольненияНаПечать");
	ДанныеСсылок.Колонки.Добавить("ЛичныеДанныеФИОФамилияИмяОтчествоВВинительномПадеже");
	ДанныеСсылок.Колонки.Добавить("ЛичныеДанныеФИОФамилияИмяОтчествоВДательномПадеже");
	ДанныеСсылок.Колонки.Добавить("РаботаСотрудникТабельныйНомерНаПечать");
	ДанныеСсылок.Колонки.Добавить("РаботаПодразделениеНаПечать");
	ДанныеСсылок.Колонки.Добавить("РаботаДолжность");
	ДанныеСсылок.Колонки.Добавить("РаботаСотрудник");
	ДанныеСсылок.Колонки.Добавить("РаботаСтатьяТКРФНаПечать");
	ДанныеСсылок.Колонки.Добавить("РаботаОснованиеУвольнения");
	ДанныеСсылок.Колонки.Добавить("РаботаТрудовойДоговорДатаОформленияНаПечать");
	ДанныеСсылок.Колонки.Добавить("РаботаТрудовойДоговорНомер");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДолжностьРуководителя");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектРуководительРасшифровкаПодписи");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДатаОзнакомленияРаботника");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДнейКомпенсацииУдержанияОтпуска");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДолжностьБухгалтера");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектБухгалтерРасшифровкаПодписи");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектРаботникКадровойСлужбыРасшифровкаПодписи");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Увольнение.Ссылка КАК Ссылка,
		|	Увольнение.Дата КАК Дата,
		|	Увольнение.Ссылка КАК СсылкаНаОбъект,
		|	Увольнение.Организация.НаименованиеСокращенное КАК РаботаОрганизацияНаименованиеСокращенное,
		|	Увольнение.Организация.КодПоОКПО КАК РаботаОрганизацияКодПоОКПО,
		|	Увольнение.ДатаУвольнения КАК РаботаДатаУвольнения,
		|	Увольнение.Сотрудник КАК РаботаСотрудник,
		|	Увольнение.Сотрудник.Код КАК РаботаСотрудникКод,
		|	Увольнение.Номер КАК СсылкаНаОбъектНомер,
		|	Увольнение.НомерПриказа КАК СсылкаНаОбъектНомерПриказа,
		|	Увольнение.Дата КАК СсылкаНаОбъектДата,
		|	Увольнение.СтатьяТКРФ КАК СсылкаНаОбъектСтатьяТКРФ,
		|	Увольнение.РабочийГодС КАК СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	Увольнение.РабочийГодПо КАК СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	Увольнение.ДнейКомпенсацииУдержанияОтпуска КАК СсылкаНаОбъектДнейКомпенсацииУдержанияОтпуска,
		|	Увольнение.Руководитель КАК СсылкаНаОбъектРуководитель,
		|	Увольнение.ДолжностьРуководителя КАК СсылкаНаОбъектДолжностьРуководителя,
		|	Увольнение.ОснованиеУвольнения КАК РаботаОснованиеУвольнения,
		|	Увольнение.Бухгалтер КАК СсылкаНаОбъектБухгалтер,
		|	Увольнение.ДолжностьБухгалтера КАК СсылкаНаОбъектДолжностьБухгалтера,
		|	Увольнение.РаботникКадровойСлужбы КАК СсылкаНаОбъектРаботникКадровойСлужбы,
		|	Увольнение.ДолжностьРаботникаКадровойСлужбы КАК СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы,
		|	Увольнение.ГлавныйБухгалтер КАК СсылкаНаОбъектГлавныйБухгалтер
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|ГДЕ
		|	Увольнение.Ссылка В(&МассивОбъектов)";
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц,
		"ВТДанныеДокументов", "РаботаСотрудник,РаботаДатаУвольнения");
	
	ОписательВременныхТаблиц.ИмяВТКадровыеДанныеСотрудников = "ВТКадровыеДанныеСотрудниковДляТ1Т8";
	
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(
		ОписательВременныхТаблиц,
		Истина,
		"ФИОПолные,Пол,Подразделение,Должность");
	
	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("СсылкаНаОбъектРуководитель");
	ИменаПолейОтветственныхЛиц.Добавить("СсылкаНаОбъектБухгалтер");
	ИменаПолейОтветственныхЛиц.Добавить("СсылкаНаОбъектРаботникКадровойСлужбы");
	
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Ложь, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументов");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеДокументов.СсылкаНаОбъект КАК СсылкаНаОбъект,
		|	ДанныеДокументов.РаботаОрганизацияНаименованиеСокращенное КАК РаботаОрганизацияНаименованиеСокращенное,
		|	ДанныеДокументов.РаботаОрганизацияКодПоОКПО КАК РаботаОрганизацияКодПоОКПО,
		|	ДанныеДокументов.СсылкаНаОбъектДата КАК СсылкаНаОбъектДата,
		|	ДанныеДокументов.СсылкаНаОбъектНомер КАК СсылкаНаОбъектНомер,
		|	ДанныеДокументов.СсылкаНаОбъектНомерПриказа КАК СсылкаНаОбъектНомерПриказа,
		|	КадровыеДанныеСотрудников.ФИОПолные КАК ЛичныеДанныеФИОПолные,
		|	КадровыеДанныеСотрудников.Пол КАК ЛичныеДанныеПол,
		|	ДанныеДокументов.РаботаСотрудникКод КАК РаботаСотрудникКод,
		|	КадровыеДанныеСотрудников.Подразделение КАК РаботаПодразделение,
		|	ДанныеДокументов.РаботаДатаУвольнения КАК РаботаДатаУвольнения,
		|	КадровыеДанныеСотрудников.Должность КАК РаботаДолжность,
		|	ДанныеДокументов.РаботаОснованиеУвольнения КАК РаботаОснованиеУвольнения,
		|	ДанныеДокументов.РаботаСотрудник КАК РаботаСотрудник,
		|	ДанныеДокументов.СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск КАК СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	ДанныеДокументов.СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск КАК СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	ДанныеДокументов.СсылкаНаОбъектДнейКомпенсацииУдержанияОтпуска КАК СсылкаНаОбъектДнейКомпенсацииУдержанияОтпуска,
		|	ДанныеДокументов.СсылкаНаОбъектСтатьяТКРФ КАК СсылкаНаОбъектСтатьяТКРФ,
		|	ДанныеДокументов.СсылкаНаОбъектДолжностьРуководителя КАК СсылкаНаОбъектДолжностьРуководителя,
		|	ФИООтветственныхЛиц.РасшифровкаПодписи КАК СсылкаНаОбъектРуководительРасшифровкаПодписи,
		|	ДанныеДокументов.СсылкаНаОбъектДолжностьБухгалтера КАК СсылкаНаОбъектДолжностьБухгалтера,
		|	ФИОБухгалтера.РасшифровкаПодписи КАК СсылкаНаОбъектБухгалтерРасшифровкаПодписи,
		|	ДанныеДокументов.СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы КАК СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы,
		|	ФИОРаботникаКадровойСлужбы.РасшифровкаПодписи КАК СсылкаНаОбъектРаботникКадровойСлужбыРасшифровкаПодписи
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудниковДляТ1Т8 КАК КадровыеДанныеСотрудников
		|		ПО ДанныеДокументов.РаботаСотрудник = КадровыеДанныеСотрудников.Сотрудник
		|			И ДанныеДокументов.РаботаДатаУвольнения = КадровыеДанныеСотрудников.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИООтветственныхЛиц
		|		ПО ДанныеДокументов.СсылкаНаОбъектРуководитель = ФИООтветственныхЛиц.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИООтветственныхЛиц.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОБухгалтера
		|		ПО ДанныеДокументов.Дата = ФИОБухгалтера.Дата
		|			И ДанныеДокументов.Ссылка = ФИОБухгалтера.Ссылка
		|			И ДанныеДокументов.СсылкаНаОбъектБухгалтер = ФИОБухгалтера.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОРаботникаКадровойСлужбы
		|		ПО ДанныеДокументов.Дата = ФИОРаботникаКадровойСлужбы.Дата
		|			И ДанныеДокументов.Ссылка = ФИОРаботникаКадровойСлужбы.Ссылка
		|			И ДанныеДокументов.СсылкаНаОбъектРаботникКадровойСлужбы = ФИОРаботникаКадровойСлужбы.ФизическоеЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	СсылкаНаОбъект";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("СсылкаНаОбъект") Цикл
		
		СтрокаСсылки = ДанныеСсылок.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСсылки, Выборка);
		
		СтрокаСсылки.СсылкаНаОбъектДатаНаПечать = Формат(Выборка.СсылкаНаОбъектДата, "ДЛФ=D");
		СтрокаСсылки.СсылкаНаОбъектНомерНаПечать = ЗарплатаКадрыОтчеты.НомерНаПечать(
			Выборка.СсылкаНаОбъектНомер, Выборка.СсылкаНаОбъектНомерПриказа);
			
		СтрокаСсылки.РаботаДатаУвольненияНаПечать = ЗарплатаКадрыОтчеты.ФорматДатыЧислоВКавычкахМесяцПрописью(Выборка.РаботаДатаУвольнения);
		
		СтрокаСсылки.РаботаСотрудникТабельныйНомерНаПечать = ЗарплатаКадрыОтчеты.ТабельныйНомерНаПечать(Выборка.РаботаСотрудникКод);
		СтрокаСсылки.ЛичныеДанныеФИОФамилияИмяОтчествоВВинительномПадеже =
			ЗарплатаКадрыОтчеты.ПросклоненныеФИО(Выборка.ЛичныеДанныеФИОПолные, 4, Выборка.ЛичныеДанныеПол);
		СтрокаСсылки.ЛичныеДанныеФИОФамилияИмяОтчествоВДательномПадеже =
			ЗарплатаКадрыОтчеты.ПросклоненныеФИО(Выборка.ЛичныеДанныеФИОПолные, 3, Выборка.ЛичныеДанныеПол);
		
		СтрокаСсылки.РаботаПодразделениеНаПечать = ЗарплатаКадрыОтчеты.ПодразделениеНаПечать(Выборка.РаботаПодразделение);
		
		СтрокаСсылки.РаботаСтатьяТКРФНаПечать = КадровыйУчет.ПредставлениеСтатьиУвольненияДляТ8(Выборка.СсылкаНаОбъектСтатьяТКРФ);
		
		СтрокаСсылки.РаботаТрудовойДоговорДатаОформленияНаПечать = ЗарплатаКадрыОтчеты.ФорматДатыЧислоВКавычкахМесяцПрописью('00010101');
		СтрокаСсылки.РаботаТрудовойДоговорНомер = "_____";
		
		СтрокаСсылки.СсылкаНаОбъектДатаОзнакомленияРаботника = ЗарплатаКадрыОтчеты.ФорматДатыЧислоВКавычкахМесяцПрописью(СтрокаСсылки.СсылкаНаОбъектДата);
		
		Пока Выборка.Следующий() Цикл
			
			СтрокаСотрудника = СтрокаСсылки.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСотрудника, СтрокаСсылки);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеСсылок;
	
КонецФункции

Функция СтруктураПараметровЗапискиРасчетаТ61()
	
	Параметры = КадровыйУчет.ПараметрыКадровогоПриказа();
	
	Параметры.Вставить("РаботникКадровойСлужбыРасшифровкаПодписи");
	Параметры.Вставить("ДолжностьРаботникаКадровойСлужбы");
	Параметры.Вставить("БухгалтерРасшифровкаПодписи");
	
	Параметры.Вставить("ДатаУвольнения");
	Параметры.Вставить("СтатьяТКРФ");
	Параметры.Вставить("ТекстОснования");
	Параметры.Вставить("ВсегоДнейРасчетногоПериода"); 
	Параметры.Вставить("ЗаработокЗаЕдиницуВремени"); 
	Параметры.Вставить("ВсегоУчтено");
	
	Параметры.Вставить("ДнейУдержания");
	Параметры.Вставить("ДнейКомпенсации");
	Параметры.Вставить("РабочийГодС");
	Параметры.Вставить("РабочийГодПо");
	
	Параметры.Вставить("НачисленоКомпенсации");
	Параметры.Вставить("ВсеДругиеНачисления");
	Параметры.Вставить("ВсегоВыплат");
	Параметры.Вставить("СуммаНДФЛ");
	Параметры.Вставить("ПрочиеУдержания");
	Параметры.Вставить("ВсегоУдержано");
	Параметры.Вставить("ДолгЗаОрганизацией");
	Параметры.Вставить("ДолгЗаРаботником");
	Параметры.Вставить("КВыплате");
	Параметры.Вставить("КВыплатеПрописью");
	Параметры.Вставить("КВыплатеРублиКопейки");
	
	Сч  = 1;
	Для Сч = 1 По 12 Цикл
		СтрокаГод = "Год" + Сч;
		СтрокаМесяц = "Месяц" + Сч;
		СтрокаСумма = "Сумма" + Сч;
		Параметры.Вставить(СтрокаГод);
		Параметры.Вставить(СтрокаМесяц);
		Параметры.Вставить(СтрокаСумма);
	КонецЦикла;	
	
	Параметры.Вставить("МассивДополнительныхСтрокЗаработка", Новый Массив);

	Возврат Параметры;
	
КонецФункции 

#Область ПечатьРасчетаСреднегоЗаработка

// Заполняет структуру - описание документа для формирования печатной формы расчета среднего заработка.
//
// Параметры:
//   ОписаниеДокумента - структура, определяется в
//                       Обработки.ПечатьРасчетаСреднегоЗаработка.ОписаниеДокументаРассчитывающегоСреднийЗаработок.
//
Процедура ЗаполнитьОписаниеДокументаРассчитывающегоСреднийЗаработок(ОписаниеДокумента, ИмяМакета) Экспорт
	МетаданныеДокумента = ПустаяСсылка().Метаданные();
	ОписаниеДокумента.Вставить("ИмяДокумента", МетаданныеДокумента.Имя);
	ОписаниеДокумента.Вставить("СинонимДокумента", МетаданныеДокумента.Синоним);
КонецПроцедуры 

// Заполняет таблицу значений - параметры формирования печатной формы расчета среднего заработка.
//
// Параметры:
//	 МассивСсылок 		- массив, печатаемые документы.
//   ДанныеДокумента 	- таблица значений, определяется в
//                      Обработки.ПечатьРасчетаСреднегоЗаработка.ДанныеДокументовРасчетаСреднегоЗаработка.
//   ИмяМакета          - строка, вариант печати расчета среднего заработка.
//
Процедура ЗаполнитьДанныеДокументовДляПечатиРасчетаСреднегоЗаработка(МассивСсылок, ДанныеДокументов, ИмяМакета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Документ.Сотрудник КАК Сотрудник,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, ДЕНЬ), ДЕНЬ, 1) КАК ДатаНачалаСобытия,
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Организация КАК Организация,
	|	Документ.Дата КАК ДатаДокумента,
	|	Документ.Номер КАК НомерДокумента,
	|	Документ.ПериодРегистрации КАК ПериодРегистрации,
	|	ДОБАВИТЬКДАТЕ(ВЫБОР
	|			КОГДА КОНЕЦПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ) = КОНЕЦПЕРИОДА(Документ.ДатаУвольнения, ДЕНЬ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ), МЕСЯЦ, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ)
	|		КОНЕЦ, МЕСЯЦ, -12) КАК НачалоРасчетногоПериода,
	|	ДОБАВИТЬКДАТЕ(ВЫБОР
	|			КОГДА КОНЕЦПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ) = КОНЕЦПЕРИОДА(Документ.ДатаУвольнения, ДЕНЬ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ), МЕСЯЦ, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ)
	|		КОНЕЦ, СЕКУНДА, -1) КАК ОкончаниеРасчетногоПериода,
	|	Документ.ДнейКомпенсацииУдержанияОтпуска КАК ДнейОсновногоОтпуска
	|ИЗ
	|	Документ.Увольнение КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&МассивСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	Документ.Ссылка,
	|	Документ.Сотрудник,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, ДЕНЬ), ДЕНЬ, 1),
	|	Документ.Организация,
	|	Документ.Дата,
	|	Документ.Номер,
	|	Документ.ПериодРегистрации,
	|	Документ.ДнейКомпенсацииУдержанияОтпуска,
	|	ДОБАВИТЬКДАТЕ(ВЫБОР
	|			КОГДА КОНЕЦПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ) = КОНЕЦПЕРИОДА(Документ.ДатаУвольнения, ДЕНЬ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ), МЕСЯЦ, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ)
	|		КОНЕЦ, МЕСЯЦ, -12),
	|	ДОБАВИТЬКДАТЕ(ВЫБОР
	|			КОГДА КОНЕЦПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ) = КОНЕЦПЕРИОДА(Документ.ДатаУвольнения, ДЕНЬ)
	|				ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ), МЕСЯЦ, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Документ.ДатаУвольнения, МЕСЯЦ)
	|		КОНЕЦ, СЕКУНДА, -1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		ДанныеДокумента = ДанныеДокументов.Добавить(); 
		
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка);
		ДанныеДокумента.ДнейОтпускаВсего 	= Выборка.ДнейОсновногоОтпуска;

		ОписанияСотрудников = Новый Соответствие;
		МассивСотрудников = Новый Массив;
		Пока Выборка.Следующий() Цикл
			МассивСотрудников.Добавить(Выборка.Сотрудник);
			ОписанияСотрудников.Вставить(Выборка.Сотрудник, Новый Структура("ИспользоватьСреднечасовойЗаработок", Ложь));
		КонецЦикла;
		ДанныеДокумента.МассивСотрудников 	= МассивСотрудников;
		ДанныеДокумента.ОписанияСотрудников = ОписанияСотрудников;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьДатыЗапрета(ПараметрыОбновления) Экспорт
	
	ОбновлениеВыполнено = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 100
		|	Увольнение.Ссылка КАК Ссылка,
		|	Увольнение.Дата КАК Дата
		|ИЗ
		|	Документ.Увольнение КАК Увольнение
		|ГДЕ
		|	Увольнение.ДатаЗапрета = ДАТАВРЕМЯ(1, 1, 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Увольнение.Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ОбновлениеВыполнено = Ложь;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
				ПараметрыОбновления, Выборка.Ссылка.Метаданные().ПолноеИмя(), "Ссылка", Выборка.Ссылка) Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
			
			МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Ссылка);
			МенеджерДокумента.ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента);
			
			ОбъектДокумента.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектДокумента);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбновлениеВыполнено);
	
КонецПроцедуры

Функция ДанныеДляПроведенияМероприятияТрудовойДеятельности(СсылкаНаДокумент, ТолькоПроведенные = Ложь) Экспорт
	
	ДанныеДляПроведения = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Номер КАК Номер,
		|	ТаблицаДокумента.НомерПриказа КАК НомерПриказа,
		|	ТаблицаДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
		|	ТаблицаДокумента.ДатаУвольнения КАК ДатаМероприятия,
		|	ТаблицаДокумента.НаименованиеДокумента КАК НаименованиеДокументаОснования,
		|	ТаблицаДокумента.Номер КАК НомерДокументаОснования,
		|	ТаблицаДокумента.Дата КАК ДатаДокументаОснования,
		|	ТаблицаДокумента.НаименованиеВторогоДокументаОснования КАК НаименованиеВторогоДокументаОснования,
		|	ТаблицаДокумента.СерияВторогоДокументаОснования КАК СерияВторогоДокументаОснования,
		|	ТаблицаДокумента.НомерВторогоДокументаОснования КАК НомерВторогоДокументаОснования,
		|	ТаблицаДокумента.ДатаВторогоДокументаОснования КАК ДатаВторогоДокументаОснования,
		|	ТаблицаДокумента.СтатьяТКРФ КАК ОснованиеУвольнения,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыМероприятийТрудовойДеятельности.Увольнение) КАК ВидМероприятия
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка В(&Ссылка)
		|	И ТаблицаДокумента.ОтразитьВТрудовойКнижке
		|	И &ТолькоПроведенные
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
	
	Если ТолькоПроведенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТолькоПроведенные", "ТаблицаДокумента.Ссылка.Проведен");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТолькоПроведенные", "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		ДвиженияДокумента = Новый Массив;
		ДанныеДляПроведения.Вставить(Выборка.Ссылка, ДвиженияДокумента);
		
		Пока Выборка.Следующий() Цикл
			ДвиженияДокумента.Добавить(ЭлектронныеТрудовыеКнижки.ЗаписьДвиженияМероприятияТрудовойДеятельности(Выборка));
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура СформироватьНаборыЗаписейМероприятияТрудовойДеятельности(МероприятияТрудовойДеятельности, ПараметрыОбновления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ТаблицаДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.Увольнение КАК ТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МероприятияТрудовойДеятельности КАК МероприятияТрудовойДеятельности
		|		ПО ТаблицаДокумента.Ссылка = МероприятияТрудовойДеятельности.Регистратор
		|ГДЕ
		|	НЕ ТаблицаДокумента.Ссылка В (&МассивОбновленных)
		|	И ТаблицаДокумента.Проведен
		|	И ТаблицаДокумента.ОтразитьВТрудовойКнижке
		|	И МероприятияТрудовойДеятельности.Регистратор ЕСТЬ NULL";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ОбрабатываемыеДокументы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	ДанныеДляПроведенияДокументов = ДанныеДляПроведенияМероприятияТрудовойДеятельности(ОбрабатываемыеДокументы, Истина);
	Для Каждого Регистратор Из ОбрабатываемыеДокументы Цикл
		
		МассивОбновленных.Добавить(Регистратор);
		ДанныеДляПроведения = ДанныеДляПроведенияДокументов.Получить(Регистратор);
		Если ДанныеДляПроведения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.МероприятияТрудовойДеятельности.НаборЗаписей", "Регистратор", Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.МероприятияТрудовойДеятельности.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
		
		РегистрыСведений.МероприятияТрудовойДеятельности.ЗаполнитьЗаписиМероприятий(
			ДанныеДляПроведения, МероприятияТрудовойДеятельности,
			"ОснованиеУвольнения");
		
		СформироватьДвиженияМероприятийТрудовойДеятельности(НаборЗаписей, ДанныеДляПроведения);
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияМероприятийТрудовойДеятельности(НаборЗаписей, ДанныеДляПроведения) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеДляПроведения) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормирования = ЭлектронныеТрудовыеКнижки.ПараметрыФормированияДвиженийМероприятийТрудовойДеятельности();
	ПараметрыФормирования.ДополнитьСведениямиОЗанятости = Истина;
	Если ЗначениеЗаполнено(ДанныеДляПроведения[0].ДатаДокументаОснования)
		И ДанныеДляПроведения[0].ДатаДокументаОснования >= ЗарплатаКадрыПовтИсп.ДатаВступленияВСилуНА("ДатаНачалаПриемаСведенийОМобилизованных") Тогда
		
		ПараметрыФормирования.ДополнитьСведениямиОДолжности = Истина;
		ПараметрыФормирования.ПолучатьИсточникДанныхОТерриториальныхУсловиях = Истина;
		ПараметрыФормирования.ДополнитьСведениямиОКодахПоОКЗ = Истина;
	Иначе
		ПараметрыФормирования.ДополнитьСведениямиОКодахПоОКЗ = Ложь;
	КонецЕсли;
	
	ЭлектронныеТрудовыеКнижки.СформироватьДвиженияМероприятийТрудовойДеятельности(
		НаборЗаписей, ДанныеДляПроведения, ПараметрыФормирования);
	
КонецПроцедуры

Процедура ПроверитьДокументыДляПечати(МассивОбъектов)
	
	МассивПроверенныхДокументов = Новый Массив;
	Для Каждого ПроверяемыйДокумент ИЗ МассивОбъектов Цикл
		ДокументОбъект = ПроверяемыйДокумент.ПолучитьОбъект();
		Если ДокументОбъект.ПроверитьЗаполнение() Тогда
			МассивПроверенныхДокументов.Добавить(ПроверяемыйДокумент);
		КонецЕсли;
	КонецЦикла;
	
	МассивОбъектов = МассивПроверенныхДокументов;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("НомерПриказа");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	Поля.Добавить("Ссылка");
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = СтрШаблон(НСтр("ru = 'Увольнение %1 от %2'"), Данные.НомерПриказа, Формат(Данные.Дата, "ДЛФ=D"));
	
КонецПроцедуры

#КонецОбласти

#Область РасчетДокумента

// Функция формирует набор параметров для передачи во вспомогательные формы,
// помещает их во временное хранилище и возвращает адрес этого хранилища.
//
// Параметры:
//	Объект                  - ДокументОбъект.Увольнение - исходый документ.
//	ДополнительныеПараметры - Структура - дополнительные параметры из формы документа.
//
// Возвращаемое значение:
//  Строка - адрес во временном хранилище.
//
Функция АдресПараметровВХранилище(Объект, ДополнительныеПараметры) Экспорт
	
	ПараметрыРасчетаСреднегоЗаработка = Новый Структура;
	ПараметрыРасчетаСреднегоЗаработка.Вставить("СериализованныйОбъект",             ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(Объект));
	ПараметрыРасчетаСреднегоЗаработка.Вставить("Сотрудник",                         Объект.Сотрудник);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("Организация",                       Объект.Организация);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("ФизическоеЛицо",                    Объект.ФизическоеЛицо);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("ДатаНачалаСобытия",                 Объект.ДатаУвольнения + 86400);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("МесяцНачисления",                   Объект.ПериодРегистрации);
	
	ПараметрыРасчетаСреднегоЗаработка.Вставить("Подразделение",                  ДополнительныеПараметры.Подразделение);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("НачислениеКомпенсацияОтпуска",   ДополнительныеПараметры.НачислениеКомпенсацияОтпуска);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("НачислениеКомпенсацияОтпускаРК", ДополнительныеПараметры.НачислениеКомпенсацияОтпускаРК);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("НачислениеКомпенсацияОтпускаСН", ДополнительныеПараметры.НачислениеКомпенсацияОтпускаСН);
	
	ПараметрыРасчетаСреднегоЗаработка.Вставить("УчитыватьМРОТПриОплатеПоСреднемуЗаработку", ДополнительныеПараметры.УчитыватьМРОТПриОплатеПоСреднемуЗаработку);
	
	Возврат ПоместитьВоВременноеХранилище(ПараметрыРасчетаСреднегоЗаработка, ДополнительныеПараметры.УникальныйИдентификатор);
	
КонецФункции

// Производит перерасчет начисления компенсации отпуска.
// 
// Параметры:
//  Объект                  - ДокументОбъект.Увольнение - исходый документ.
//  ОбъектФормы             - ДокументОбъект.
//  ДополнительныеПараметры - Структура - дополнительные параметры из формы документа.
//
Процедура РассчитатьНачисления(Объект, ОбъектФормы, ДополнительныеПараметры) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		НачисленияДокумента = Объект.Начисления.Выгрузить();
		
		Объект.Начисления.Очистить();
		
		СтрокаНачислений = Объект.Начисления.Добавить();
		
		СтрокаНачислений.Начисление    = ДополнительныеПараметры.НачислениеКомпенсацияОтпуска;
		СтрокаНачислений.Сотрудник     = Объект.Сотрудник;
		СтрокаНачислений.Подразделение = ДополнительныеПараметры.Подразделение;
		СтрокаНачислений.ДатаНачала    = Объект.ДатаУвольнения;
		СтрокаНачислений.ДатаОкончания = Объект.ДатаУвольнения;
		
		СтрокаНачислений.ОплаченоДней = Объект.ДнейКомпенсацииУдержанияОтпуска;
		
		НачислениеКомпенсацияОтпускаРК = ДополнительныеПараметры.НачислениеКомпенсацияОтпускаРК;
		НачислениеКомпенсацияОтпускаСН = ДополнительныеПараметры.НачислениеКомпенсацияОтпускаСН;
		
		ДатаЗаконаПроРКиЕАЭС = УчетНДФЛ.ДатаЗаконаПроРКиЕАЭС();
		
		СреднийЗаработокСН = 0;
		СреднийЗаработокРК = 0;
		Если Объект.ПланируемаяДатаВыплаты >= ДатаЗаконаПроРКиЕАЭС Тогда
			Если ЗначениеЗаполнено(НачислениеКомпенсацияОтпускаСН) Тогда
				СреднийЗаработокСН = Объект.СреднийЗаработокСН;
			КонецЕсли;
			Если ЗначениеЗаполнено(НачислениеКомпенсацияОтпускаРК) Тогда
				СреднийЗаработокРК = Объект.СреднийЗаработокРК;
			КонецЕсли;
		КонецЕсли;
		
		ПроизводственныйКалендарь = РасчетЗарплатыДляНебольшихОрганизацийПовтИсп.ПроизводственныйКалендарьОрганизации(Объект.Организация);
		КалендарныеДниМесяцаБезПраздников = МодульРасчетЗарплатыДляНебольшихОрганизаций.КалендарныеДниМесяцаБезПраздников(ПроизводственныйКалендарь, Объект.ДатаУвольнения);
		МРОТ = ЗарплатаКадрыПовтИсп.МинимальныйРазмерОплатыТрудаРФ(Объект.ДатаУвольнения);
		
		Если ДополнительныеПараметры.УчитыватьМРОТПриОплатеПоСреднемуЗаработку И Объект.СреднийЗаработок * КалендарныеДниМесяцаБезПраздников <= МРОТ Тогда
			КомпенсацияОтпуска = МРОТ / КалендарныеДниМесяцаБезПраздников * СтрокаНачислений.ОплаченоДней;
			КомпенсацияОтпускаРК = 0;
			КомпенсацияОтпускаСН = 0;
		Иначе
			КомпенсацияОтпуска = (Объект.СреднийЗаработок - СреднийЗаработокРК - СреднийЗаработокСН) * СтрокаНачислений.ОплаченоДней;
			КомпенсацияОтпускаРК = СреднийЗаработокРК * СтрокаНачислений.ОплаченоДней;
			КомпенсацияОтпускаСН = СреднийЗаработокСН * СтрокаНачислений.ОплаченоДней;
		КонецЕсли;
		
		СтрокаНачислений.Результат = КомпенсацияОтпуска;
		
		Если Объект.ПланируемаяДатаВыплаты >= ДатаЗаконаПроРКиЕАЭС Тогда
			
			Если ЗначениеЗаполнено(НачислениеКомпенсацияОтпускаРК) И КомпенсацияОтпускаРК > 0 Тогда
				
				СтрокаНачисленийРК					= Объект.Начисления.Добавить();
				СтрокаНачисленийРК.Начисление		= НачислениеКомпенсацияОтпускаРК;
				СтрокаНачисленийРК.Сотрудник 		= СтрокаНачислений.Сотрудник;
				СтрокаНачисленийРК.Подразделение 	= СтрокаНачислений.Подразделение;
				СтрокаНачисленийРК.Результат 		= КомпенсацияОтпускаРК;
				СтрокаНачисленийРК.ДатаНачала		= Объект.ДатаУвольнения;
				СтрокаНачисленийРК.ДатаОкончания	= Объект.ДатаУвольнения;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НачислениеКомпенсацияОтпускаСН) И КомпенсацияОтпускаСН > 0 Тогда
				
				СтрокаНачисленийСН					= Объект.Начисления.Добавить();
				СтрокаНачисленийСН.Начисление		= НачислениеКомпенсацияОтпускаСН;
				СтрокаНачисленийСН.Сотрудник 		= СтрокаНачислений.Сотрудник;
				СтрокаНачисленийСН.Подразделение 	= СтрокаНачислений.Подразделение;
				СтрокаНачисленийСН.Результат 		= КомпенсацияОтпускаСН;
				СтрокаНачисленийСН.ДатаНачала		= Объект.ДатаУвольнения;
				СтрокаНачисленийСН.ДатаОкончания	= Объект.ДатаУвольнения;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из НачисленияДокумента Цикл
			Если СтрокаТаблицы.Начисление = ДополнительныеПараметры.НачислениеКомпенсацияОтпуска
				Или СтрокаТаблицы.Начисление = НачислениеКомпенсацияОтпускаРК
				Или СтрокаТаблицы.Начисление = НачислениеКомпенсацияОтпускаСН Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Объект.Начисления.Добавить();
			НоваяСтрока.ДатаНачала		= Объект.ПериодРегистрации;
			НоваяСтрока.ДатаОкончания	= Объект.ДатаУвольнения;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Производит перерасчет начисления за отработанные дни.
// 
// Параметры:
//  Объект                  - ДокументОбъект.Увольнение - исходый документ.
//  ОбъектФормы             - ДокументОбъект.
//  ДополнительныеПараметры - Структура - дополнительные параметры из формы документа.
//
Процедура ВыполнитьРасчетНачислений(Объект, ОбъектФормы, ДополнительныеПараметры) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	
	МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
	
	// Установка транзакции для расчета
	УстановитьПривилегированныйРежим(Истина);
	РасчетВыполняетсяОтдельнойТранзакцией = Не ТранзакцияАктивна();
	Если РасчетВыполняетсяОтдельнойТранзакцией Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	ДокументОбъект = ОбъектФормы.Скопировать();
	ДокументОбъект.ОбменДанными.Загрузка = Истина;
	ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	РезультатНачисления = РасчетЗарплаты.РезультатНачисленияРасчетаЗарплаты(
		Объект.Организация,
		Объект.ПериодРегистрации,
		Объект.Ссылка,
		ДополнительныеПараметры.Подразделение,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Сотрудник));
		
	Если РезультатНачисления.Количество() > 0 Тогда
		
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатНачисления.Колонки, "ОтработаноДней")
			И Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатНачисления.Колонки, "ОтработаноЧасов") Тогда
			РезультатНачисления.Колонки.Добавить("ОтработаноДней");
			РезультатНачисления.Колонки.Добавить("ОтработаноЧасов");
		КонецЕсли;
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатНачисления.Колонки, "ОплаченоДней")
			И Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатНачисления.Колонки, "ОплаченоЧасов") Тогда
			РезультатНачисления.Колонки.Добавить("ОплаченоДней");
			РезультатНачисления.Колонки.Добавить("ОплаченоЧасов");
		КонецЕсли;
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатНачисления.Колонки, "ДатаНачала")
			И Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(РезультатНачисления.Колонки, "ДатаОкончания") Тогда
			РезультатНачисления.Колонки.Добавить("ДатаНачала");
			РезультатНачисления.Колонки.Добавить("ДатаОкончания");
		КонецЕсли;
		
		КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Объект.Сотрудник, "ДатаПриема,ДатаЗавершенияРаботы,ДоляНеполногоРабочегоВремени", Объект.ДатаУвольнения);
		Если КадровыеДанные.Количество() <> 0 Тогда
			КадровыеДанные[0].ДатаЗавершенияРаботы = Объект.ДатаУвольнения;
		КонецЕсли;
		РасчетЗарплатыБазовый.ЗаполнитьОтработанноеВремя(РезультатНачисления, Объект.ПериодРегистрации,
			Объект.Организация, Ложь, КадровыеДанные);
		
		Для Каждого СтрокаТаблицы Из РезультатНачисления Цикл
			СтрокаТаблицы.ОплаченоДней = СтрокаТаблицы.ОтработаноДней;
			СтрокаТаблицы.ОплаченоЧасов = СтрокаТаблицы.ОтработаноЧасов;
			Если СтрокаТаблицы.Начисление = ДополнительныеПараметры.НачислениеКомпенсацияОтпуска
				Или СтрокаТаблицы.Начисление = ДополнительныеПараметры.НачислениеКомпенсацияОтпускаРК
				Или СтрокаТаблицы.Начисление = ДополнительныеПараметры.НачислениеКомпенсацияОтпускаСН Тогда
				СтрокаТаблицы.ДатаНачала	= Объект.ДатаУвольнения;
				СтрокаТаблицы.ДатаОкончания	= Объект.ДатаУвольнения;
			Иначе
				СтрокаТаблицы.ДатаНачала	= Объект.ПериодРегистрации;
				СтрокаТаблицы.ДатаОкончания	= Объект.ДатаУвольнения;
			КонецЕсли;
		КонецЦикла;
		
		НачисленияДокумента = Объект.Начисления.Выгрузить();
		Объект.Начисления.Очистить();
		Для Каждого СтрокаТаблицы Из НачисленияДокумента Цикл
			Если СтрокаТаблицы.Начисление <> ДополнительныеПараметры.НачислениеКомпенсацияОтпуска
				И СтрокаТаблицы.Начисление <> ДополнительныеПараметры.НачислениеКомпенсацияОтпускаРК
				И СтрокаТаблицы.Начисление <> ДополнительныеПараметры.НачислениеКомпенсацияОтпускаСН Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Объект.Начисления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		КонецЦикла;
		
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ЗаполнитьРезультатНачисления(Объект, РезультатНачисления);
		Для Каждого СтрокаТаблицы Из РезультатНачисления Цикл
			СтрокаТаблицы.ОплаченоДней = Мин(СтрокаТаблицы.ОплаченоДней, СтрокаТаблицы.ОтработаноДней);
			СтрокаТаблицы.ОплаченоЧасов = Мин(СтрокаТаблицы.ОплаченоЧасов, СтрокаТаблицы.ОтработаноЧасов);
		КонецЦикла;
		
	КонецЕсли;
	
	Если РасчетВыполняетсяОтдельнойТранзакцией Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	// Отмена расчетной транзакции
	
	Если РезультатНачисления.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатНачисления, Объект.Начисления);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьНачисленияСКоэффициентом(Сотрудник, Начисления) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		МодульРасчетЗарплатыДляНебольшихОрганизаций.ПересчитатьНачисленияСКоэффициентом(Сотрудник, Начисления);
	КонецЕсли;
	
КонецПроцедуры

// Производит заполнение по заданным удержаниям.
// 
// Параметры:
//  Объект                  - ДокументОбъект.Увольнение - исходый документ.
//  ОбъектФормы             - ДокументОбъект.
//
Процедура ЗаполнитьРезультатУдержаний(Объект, ОбъектФормы) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("РасчетЗарплатыДляНебольшихОрганизаций") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		Возврат;
	КонецЕсли;
	
	МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
	
	МодульРасчетЗарплатыДляНебольшихОрганизаций.ЗаполнитьРезультатУдержаний(Объект);
	
КонецПроцедуры

// Производит перерасчет НДФЛ и взносов.
// 
// Параметры:
//  Объект                  - ДокументОбъект.Увольнение - исходый документ.
//  ОбъектФормы             - ДокументОбъект.
//  ДополнительныеПараметры - Структура - дополнительные параметры из формы документа.
//  НДФЛ                    - Булево, признак пересчета НДФЛ
//  Взносы                  - Булево, признак пересчета взносов
//
Процедура ОбновитьНДФЛиВзносыСотрудника(Объект, ОбъектФормы, НДФЛ = Истина, Взносы = Истина) Экспорт
	
	Если Объект.КорректироватьНДФЛ И Объект.КорректироватьВзносы Тогда
		// Не пересчитываем, т.к. и НДФЛ, и взносы либо в режиме корректировки
		Возврат;
	КонецЕсли;
	
	// Составляем временные таблицы для расчета НДФЛ и взносов.
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РасчетЗарплатыБазовый.СоздатьВТДляРасчетаНДФЛиВзносов(МенеджерВременныхТаблиц, Объект, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Сотрудник), "ПериодРегистрации");
	
	// Установка транзакции для расчета
	УстановитьПривилегированныйРежим(Истина);
	РасчетВыполняетсяОтдельнойТранзакцией = Не ТранзакцияАктивна();
	Если РасчетВыполняетсяОтдельнойТранзакцией Тогда
		НачатьТранзакцию();
	КонецЕсли;
	
	Если Не Объект.КорректироватьНДФЛ И НДФЛ Тогда
		ПересчитатьНДФЛ(Объект, ОбъектФормы);
	КонецЕсли;
	
	Если Не Объект.КорректироватьВзносы И Взносы Тогда
		РезультатРасчетаВзносов = РасчетЗарплатыБазовый.РезультатРасчетаВзносов(МенеджерВременныхТаблиц, Объект.Ссылка, Объект.Организация, Объект.ПериодРегистрации);
		Объект.Взносы.Загрузить(РезультатРасчетаВзносов);
	КонецЕсли;
	
	Если РасчетВыполняетсяОтдельнойТранзакцией Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	// Отмена расчетной транзакции
	
КонецПроцедуры

// Процедура инициирует перерасчет НДФЛ.
// Вызывается из основной или вспомогательной формы документа.
//
// Параметры:
//  Объект      - ДанныеФормыСтруктура.
//  ОбъектФормы - ДокументОбъект.
//
Процедура ПересчитатьНДФЛ(Объект, ОбъектФормы) Экспорт
	
	УчетЗарплаты.ПересчитатьНДФЛ(Объект, ОбъектФормы, Истина);
	
КонецПроцедуры

// Процедура инициирует перерасчет взносов.
// Вызывается из вспомогательной формы документа
//
// Параметры:
//  Объект         - ДанныеФормыСтруктура
//  ОбъектФормы - ДокументОбъект.
//
Процедура ПересчитатьВзносы(Объект, ОбъектФормы) Экспорт
	
	ОбновитьНДФЛиВзносыСотрудника(Объект, ОбъектФормы, Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
