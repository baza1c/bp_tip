
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Объект") Тогда
		КопироватьДанныеФормы(Параметры.Объект, Объект);
	КонецЕсли;
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	ВедомостьНаВыплатуЗарплатыФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// ИнтеграцияС1САрхивом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивом = ОбщегоНазначения.ОбщийМодуль("ОбменСАрхивом");
		МодульОбменСАрхивом.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1САрхивом

	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец БлокировкаИзмененияОбъектов
	
	СпособыВыплатыЗарплатыАванс          = Справочники.СпособыВыплатыЗарплаты.Аванс();
	СпособыВыплатыЗарплатыДоговорПодряда = Справочники.СпособыВыплатыЗарплаты.ДоговорПодряда();
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Документы.ВедомостьНаВыплатуЗарплатыВКассу);
	
	УстановитьСтатьюРасходов();
	
	ПредупреждениеВыдачаЗПНаличнымиАУСН =
		УправлениеПредупреждениямиАУСНВызовСервера.ТекстПредупрежденияВыплатаЗПНаличными();
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьТаблицуОплат();
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ВедомостьНаВыплатуЗарплатыФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// БлокировкаИзмененияОбъектов
	ФормаПроинициализирована = БлокировкаИзмененияОбъектовКлиентСервер.ФормаПроинициализирована(ЭтотОбъект);
	БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, Ложь);
	// Конец БлокировкаИзмененияОбъектов
	
	Если ФормаПроинициализирована Тогда
		УправлениеФормой();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ВедомостьНаВыплатуЗарплатыКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "ИзменениеОплатыВедомости" И Источник = Объект.Ссылка Тогда
		ОбновитьОбработатьСписокПлатежныхДокументов();
		РазблокироватьДокументПоВозможности();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ЗаполнитьДанныеОВыплатеЗначениямиПоУмолчанию();
	
	ВедомостьНаВыплатуЗарплатыФормы.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТребуетсяУточнитьПараметрыЗаписи = Ложь;
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		УчетЗарплаты.ОбработкаПроверкиЗаполненияВедомостей(ТекущийОбъект,
			ТребуетсяУточнитьПараметрыЗаписи,
			Новый Массив, Истина, Истина);
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ПослеЗаписиНаСервере(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец БлокировкаИзмененияОбъектов
	
	ВедомостьНаВыплатуЗарплатыФормы.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи); 
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ВедомостьНаВыплатуЗарплатыВКассу", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "РасшифровкаСпискаДокументов" Тогда
		ВедомостьНаВыплатуЗарплатыКлиент.ОплатаПоказать(ЭтотОбъект, ЭтотОбъект.ТекущийЭлемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	КонецЕсли;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьПлатежныеДокументы" Тогда
		СтандартнаяОбработка = Ложь;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПлатежныеДокументы;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ТребуетсяУточнитьПараметрыЗаписи Тогда
		СтандартнаяОбработка = Ложь;
		Отказ                = Истина;
		Оповещение = Новый ОписаниеОповещения("УточнитьПараметрыЗаписиЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'В документе выявлены ошибки заполнения.
			|Продолжить?'"), РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтотОбъект);
	ОрганизацияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере() Экспорт
	
	ИспользоватьРасчетПервойПоловиныМесяца =
		УчетЗарплаты.ИспользоватьРасчетПервойПоловиныМесяца(Объект.Организация, Объект.ПериодРегистрации);
	
	УправлениеФормой();
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СпособВыплатыПриИзмененииНаСервере()
	ВедомостьНаВыплатуЗарплатыФормы.СпособВыплатыПриИзмененииНаСервере(ЭтотОбъект);
	Если Объект.СпособВыплаты = СпособыВыплатыЗарплатыАванс
		И Не ИспользоватьРасчетПервойПоловиныМесяца Тогда
		Для Каждого СтрокаТаблицы ИЗ Объект.Зарплата Цикл
			СтрокаТаблицы.ДокументОснование = "";
			СтрокаТаблицы.ПериодВзаиморасчетов = Объект.ПериодРегистрации;
		КонецЦикла;
		Если Объект.НДФЛ.Количество() <> 0 Тогда
			Объект.НДФЛ.Очистить();
			Для Каждого СтрокаСостава ИЗ Объект.Состав Цикл
				ПриПолученииДанныхСтрокиСостава(СтрокаСостава);
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли Объект.СпособВыплаты = СпособыВыплатыЗарплатыДоговорПодряда Тогда
		Объект.Подразделение = "";
	КонецЕсли;
КонецПроцедуры

#Область РедактированиеМесяцаСтрокой

&НаКлиенте
Процедура ПериодРегистрацииПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтотОбъект, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтотОбъект, ЭтотОбъект, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтотОбъект, "Объект.ПериодРегистрации", "ПериодРегистрацииСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

// Обработчик подсистемы "ПодписиДокументов".
&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементПриИзменении(Элемент) 
	ПодписиДокументовКлиент.ПриИзмененииПодписывающегоЛица(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодписиДокументовЭлементНажатие(Элемент) 
	ПодписиДокументовКлиент.РасширеннаяПодсказкаНажатие(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры
// Конец Обработчик подсистемы "ПодписиДокументов".

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.КомментарийНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка)
КонецПроцедуры

&НаКлиенте
Процедура СпособВыплатыПриИзменении(Элемент)
	
	ИзменитьСпособВыплатыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Оплатить(Команда)
	
	Если Объект.ПометкаУдаления Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Нельзя оформить выплату на основании документа, помеченного на удаление.'"));
		Возврат;
	Иначе
		Если НЕ Объект.Проведен ИЛИ Модифицированность Тогда
			Кнопки = Новый СписокЗначений;
			Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Провести");
			Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
			
			Оповещение = Новый ОписаниеОповещения("ВопросПередВыплатойСледуетПровестиЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед выплатой документ следует провести'"), Кнопки,, КодВозвратаДиалога.Да);
		Иначе
			ОплатитьВедомость();
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСостав

&НаКлиенте
Процедура СоставВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВедомостьНаВыплатуЗарплатыКлиент.СоставВыбор(ЭтотОбъект, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)	
КонецПроцедуры

&НаКлиенте
Процедура СоставОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СоставОбработкаВыбораНаСервере(ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура СоставОбработкаВыбораНаСервере(ВыбранноеЗначение, СтандартнаяОбработка)
	ВедомостьНаВыплатуЗарплатыФормы.СоставОбработкаВыбораНаСервере(ЭтотОбъект, ВыбранноеЗначение, СтандартнаяОбработка)
КонецПроцедуры

&НаКлиенте
Процедура СоставПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Не Копирование Тогда
		ВедомостьНаВыплатуЗарплатыКлиент.Подобрать(ЭтотОбъект);
	КонецЕсли;	
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СоставПередУдалением(Элемент, Отказ)
	ВедомостьНаВыплатуЗарплатыКлиент.СоставПередУдалением(ЭтотОбъект, Элемент, Отказ) 
КонецПроцедуры

&НаКлиенте
Процедура СоставПослеУдаления(Элемент)
	СоставПослеУдаленияНаСервере()
КонецПроцедуры

&НаСервере
Процедура СоставПослеУдаленияНаСервере()
	ВедомостьНаВыплатуЗарплатыФормы.СоставПослеУдаленияНаСервере(ЭтотОбъект)
КонецПроцедуры

&НаКлиенте
Процедура СоставКВыплатеПриИзменении(Элемент)
	СоставКВыплатеПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоставКВыплатеПриИзмененииНаСервере()
	ВедомостьНаВыплатуЗарплатыФормы.СоставКВыплатеПриИзмененииНаСервере(ЭтотОбъект);
	ОбновитьНДФЛНаСервере(Новый ФиксированныйМассив(ЭтотОбъект.Элементы.Состав.ВыделенныеСтроки));
КонецПроцедуры

&НаКлиенте
Процедура СоставКВыплатеОткрытие(Элемент, СтандартнаяОбработка)
	ВедомостьНаВыплатуЗарплатыКлиент.СоставКВыплатеОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОплатыТаблица

&НаКлиенте
Процедура ОплатыТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение( , ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыТаблицаПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыОткрыть.Заголовок =
		СтрШаблон(НСтр("ru = 'Открыть: %1'"), ТекущиеДанные.Представление);
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыПровести.Заголовок =
		СтрШаблон(НСтр("ru = 'Провести: %1'"), ТекущиеДанные.Представление);
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыОтменитьПроведение.Заголовок =
		СтрШаблон(НСтр("ru = 'Отменить проведение: %1'"), ТекущиеДанные.Представление);
	
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыОтменитьПроведение.Доступность =
		ТекущиеДанные.СостояниеДокумента = 1 И МожноРедактировать;
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыПровести.Доступность
		= ТекущиеДанные.СостояниеДокумента <> 1 И МожноРедактировать;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// ИнтеграцияС1САрхивом

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграцииС1САрхивом(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивомКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСАрхивомКлиент");
		МодульОбменСАрхивомКлиент.ВыполнитьПодключаемуюКоманду(Команда, ЭтотОбъект, Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуИнтеграцииС1САрхивомНаСервере(Знач Параметры) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивом = ОбщегоНазначения.ОбщийМодуль("ОбменСАрхивом");
		МодульОбменСАрхивом.ВыполнитьПодключаемуюКоманду(ЭтотОбъект, Параметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИнтеграцияС1САрхивом_Нажатие(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивомКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСАрхивомКлиент");
		МодульОбменСАрхивомКлиент.Нажатие(Элемент, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// Конец ИнтеграцияС1САрхивом

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Заполнить(Команда)
	ОценкаПроизводительностиКлиент.ЗамерВремени("ЗаполнениеДокументаВедомостьНаВыплатуЗарплатыВКассу");
	ВедомостьНаВыплатуЗарплатыКлиент.Заполнить(ЭтотОбъект)
КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	ВедомостьНаВыплатуЗарплатыКлиент.Подобрать(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЗарплату(Команда)
	РедактироватьЗарплатуСтроки(Элементы.Состав.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНДФЛ(Команда)
	ВедомостьНаВыплатуЗарплатыКлиент.ОбновитьНДФЛ(ЭтотОбъект);	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОплатыОткрыть(Команда)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПоказатьЗначение( , ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОплатыПровести(Команда)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Модифицированность Тогда
		Кнопки = Новый СписокЗначений;
		Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Записать");
		Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередПроведениемДокументаОплатыСледуетПровестиЗавершение", ЭтотОбъект, ТекущиеДанные.Документ);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Данные ведомости были изменены. Перед выплатой изменения следует записать.'"), Кнопки,, КодВозвратаДиалога.Да);
	Иначе
		ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ТекущиеДанные.Документ, "Проведение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОплатыОтменитьПроведение(Команда)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Не ВопросЗадавался Тогда
		УчетЗарплатыКлиент.ПоказатьВопросПриОтменеПроведенияПлатежногоДокумента(ЭтотОбъект);
	Иначе
		ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ТекущиеДанные.Документ, "ОтменаПроведения");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОплатыПеречитать(Команда)
	
	ОбновитьОбработатьСписокПлатежныхДокументов();
	РазблокироватьДокументПоВозможности();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении()
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов()
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ОбратныеВызовы

// АПК:78-выкл Для функций, вызываемых из общей функциональности ведомостей, экспорт необходим

&НаСервере
Процедура ЗаполнитьПервоначальныеЗначения() Экспорт
	
	// ЗарплатаКадрыПодсистемы.ПодписиДокументов
	ПодписиДокументов.ЗаполнитьПодписиПоОрганизации(ЭтотОбъект);
	// Конец ЗарплатаКадрыПодсистемы.ПодписиДокументов
	
	ВедомостьНаВыплатуЗарплатыФормы.ЗаполнитьПервоначальныеЗначения(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект) Экспорт

	ИспользоватьРасчетПервойПоловиныМесяца =
		УчетЗарплаты.ИспользоватьРасчетПервойПоловиныМесяца(ТекущийОбъект.Организация, ТекущийОбъект.ПериодРегистрации);
		
	ВедомостьНаВыплатуЗарплатыФормы.ПриПолученииДанныхНаСервере(ЭтотОбъект, ТекущийОбъект);

	Если Параметры.Ключ.Пустая() Тогда
		Если НЕ ЗначениеЗаполнено(Объект.СпособВыплаты) Тогда
			Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	УстановитьСостояниеДокумента();

КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхСтрокиСостава(СтрокаСостава) Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.ПриПолученииДанныхСтрокиСостава(ЭтотОбъект, СтрокаСостава)
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхСтрокСостава() Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.ПриПолученииДанныхСтрокСостава(ЭтотОбъект)
КонецПроцедуры

&НаСервере
Процедура ОбработатьСообщенияПользователю() Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.ОбработатьСообщенияПользователю(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов() Экспорт
	
	ВедомостьНаВыплатуЗарплатыФормы.УстановитьДоступностьЭлементов(ЭтотОбъект);
	
	ЕстьОплатаПоВедомости = ВзаиморасчетыССотрудниками.ЕстьОплатаПоВедомости(Объект.Ссылка)
		Или ОплатыТаблица.Количество() <> 0;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаОплатить",
		"Доступность",
		Не ЕстьОплатаПоВедомости И МожноРедактировать);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеОплаты() Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.УстановитьПредставлениеОплаты(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере() Экспорт

	Если НЕ ЗначениеЗаполнено(Объект.Округление) Тогда
		Объект.Округление = Справочники.СпособыОкругленияПриРасчетеЗарплаты.ПоУмолчанию();
	КонецЕсли;
	
	ВедомостьНаВыплатуЗарплатыФормы.ЗаполнитьНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНДФЛНаСервере(ИдентификаторыСтрок) Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.ОбновитьНДФЛНаСервере(ЭтотОбъект, ИдентификаторыСтрок)
КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранныхСотрудников() Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.АдресСпискаПодобранныхСотрудников(ЭтотОбъект)
КонецФункции

&НаКлиенте
Процедура РедактироватьЗарплатуСтроки(ДанныеСтроки, ИмяПоля = "") Экспорт
	ВедомостьНаВыплатуЗарплатыКлиент.РедактироватьЗарплатуСтроки(ЭтотОбъект, ДанныеСтроки, ИмяПоля);
КонецПроцедуры

&НаСервере
Процедура РедактироватьЗарплатуСтрокиЗавершениеНаСервере(РезультатыРедактирования) Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.РедактироватьЗарплатуСтрокиЗавершениеНаСервере(ЭтотОбъект, РезультатыРедактирования) 
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьНДФЛСтроки(ДанныеСтроки) Экспорт
	ВедомостьНаВыплатуЗарплатыКлиент.РедактироватьНДФЛСтроки(ЭтотОбъект, ДанныеСтроки);	
КонецПроцедуры

&НаСервере
Процедура РедактироватьНДФЛСтрокиЗавершениеНаСервере(РезультатыРедактирования) Экспорт
	ВедомостьНаВыплатуЗарплатыФормы.РедактироватьНДФЛСтрокиЗавершениеНаСервере(ЭтотОбъект, РезультатыРедактирования) 
КонецПроцедуры

&НаСервере
Функция АдресВХранилищеЗарплатыПоСтроке(ИдентификаторСтроки) Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.АдресВХранилищеЗарплатыПоСтроке(ЭтотОбъект, ИдентификаторСтроки)
КонецФункции	

&НаСервере
Функция АдресВХранилищеНДФЛПоСтроке(ИдентификаторСтроки) Экспорт
	Возврат ВедомостьНаВыплатуЗарплатыФормы.АдресВХранилищеНДФЛПоСтроке(ЭтотОбъект, ИдентификаторСтроки)
КонецФункции	

// АПК:78-вкл

#КонецОбласти

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.СоставНДФЛГруппа.Видимость =
		Объект.СпособВыплаты <> СпособыВыплатыЗарплатыАванс
		Или ИспользоватьРасчетПервойПоловиныМесяца;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПодразделениеОрганизации",
		"Видимость",
		Объект.СпособВыплаты <> СпособыВыплатыЗарплатыДоговорПодряда);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СоставОбновитьНДФЛ",
		"Видимость",
		Объект.СпособВыплаты <> СпособыВыплатыЗарплатыАванс
		Или ИспользоватьРасчетПервойПоловиныМесяца);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Организация",
		"Доступность",
		Не Параметры.КонтекстныйВызов);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПериодРегистрации",
		"Доступность",
		Не Параметры.КонтекстныйВызов);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпособВыплаты",
		"Доступность",
		Не Параметры.КонтекстныйВызов);
		
	Если ОплатыТаблица.Количество() = 0 Тогда
		ТекущаяСтраницаГруппыПлатежныеДокументы = Элементы.СтраницаПлатежныеДокументыПустая;
	Иначе
		ТекущаяСтраницаГруппыПлатежныеДокументы = Элементы.СтраницаПлатежныеДокументыСписок;
	КонецЕсли;
	Элементы.СтраницыПлатежныеДокументы.ТекущаяСтраница = ТекущаяСтраницаГруппыПлатежныеДокументы;
	
	УстановитьДоступностьЭлементов();
	
	ОформитьСведенияОСуммеВыплат();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСпособВыплатыНаСервере()
	
	Объект.Округление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СпособВыплаты, "Округление");
	СпособВыплатыПриИзмененииНаСервере();
	УстановитьСтатьюРасходов();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = 0;
	ЗарплатаКадрыПереопределяемый.СостояниеДокумента(Объект, СостояниеДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередВыплатойСледуетПровестиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроведен = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Если НЕ ДокументПроведен Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось провести документ'"));
		Возврат;
	КонецЕсли;
	
	ОплатитьВедомость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьВедомость()
	
	Если Объект.СпособВыплаты = СпособыВыплатыЗарплатыДоговорПодряда Тогда
		ОткрытьФорму("Обработка.ВыплатаЗарплатыРасходнымиОрдерами.Форма", Новый Структура("ПлатежнаяВедомость", Объект.Ссылка));
	Иначе
		ОплатитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОплатитьНаСервере()
	
	ВедомостьНаВыплатуЗарплатыФормыПереопределяемый.СформироватьДокументыОплаты(ЭтотОбъект);
	УстановитьПредставлениеОплаты();
	ОбновитьОбработатьСписокПлатежныхДокументов();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатьюРасходов()
	
	СтатьяРасходов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СпособВыплаты, "СтатьяРасходов");
	
КонецПроцедуры

&НаСервере
Процедура БлокировкаИзмененияОбъектовПриЧтенииНаСервереФормыОбъекта()
	
	ДокументОбъект = ДанныеФормыВЗначение(Объект, Тип("ДокументОбъект.ВедомостьНаВыплатуЗарплатыВКассу"));
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ДокументОбъект, Ложь);
	// Конец БлокировкаИзмененияОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ОформитьСведенияОСуммеВыплат()
	
	ИтогКВыплате  = Объект.Зарплата.Итог("КВыплате");
	ИтогВыплачено = УчетЗарплаты.ОплаченнаяСуммаПоВедомости(Объект.Организация, Объект.Ссылка);
	
	Элементы.ДекорацияВыплачено.Заголовок = УчетЗарплаты.ОписаниеСостоянияОплатыВедомости(ИтогКВыплате, ИтогВыплачено);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОплат()
	
	ТаблицаОплат = ОплатыТаблица.Выгрузить();
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	УчетЗарплаты.ЗаполнитьТаблицуОплат(ДокументОбъект, ТаблицаОплат);
	ОплатыТаблица.Загрузить(ТаблицаОплат);
	КоличествоДокументовОплаты = ОплатыТаблица.Количество();
	
	ОформитьТаблицуОплат();
	
КонецПроцедуры

&НаСервере
Процедура ОформитьТаблицуОплат()
	
	ТаблицаОплат = ОплатыТаблица.Выгрузить();
	УчетЗарплаты.ОформитьТаблицуОплат(ТаблицаОплат);
	ОплатыТаблица.Загрузить(ТаблицаОплат);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОбработатьСписокПлатежныхДокументов()
	
	ЗаполнитьТаблицуОплат();
	БлокировкаИзмененияОбъектовПриЧтенииНаСервереФормыОбъекта();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура РазблокироватьДокументПоВозможности()
	
	РазблокироватьДокумент = Истина;
	
	Для Каждого Строка Из ОплатыТаблица Цикл
		Если Строка.Проведен Тогда
			РазблокироватьДокумент = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если РазблокироватьДокумент Тогда
		БлокировкаИзмененияОбъектовКлиент.РазблокироватьФормуОбъекта(ЭтотОбъект, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередПроведениемДокументаОплатыСледуетПровестиЗавершение(Ответ, ПлатежныйДокумент) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроведен = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Если Не ДокументПроведен Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось записать изменения'"));
		Возврат;
	КонецЕсли;
	
	ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ПлатежныйДокумент, "Проведение");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ДокументСсылка, ТипОбработки)
	
	ОбработатьИзменениеСостоянияПлатежногоДокумента(ДокументСсылка, ТипОбработки);
	РазблокироватьДокументПоВозможности();
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередОтменойПроведенияЗавершение(Ответ, ДопПараметры = Неопределено) Экспорт
	
	ВопросЗадавался = Истина;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ТекущиеДанные.Документ, "ОтменаПроведения");
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеСостоянияПлатежногоДокумента(ДокументСсылка, ТипОбработки)
	
	СтрокиТаблицы = ОплатыТаблица.НайтиСтроки(Новый Структура("Документ", ДокументСсылка));
	Если СтрокиТаблицы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	УчетЗарплаты.ОбработатьИзменениеСостоянияПлатежногоДокумента(Объект.Ссылка,
		ДокументСсылка,
		ТипОбработки,
		ОплатыТаблица.Индекс(СтрокиТаблицы[0]) + 1,
		Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементСписка = ОплатыСписок.НайтиПоЗначению(ДокументСсылка);
	Если ЭлементСписка = Неопределено И ТипОбработки = "Проведение" Тогда
		ОплатыСписок.Добавить(ДокументСсылка);
		УстановитьПредставлениеОплаты();
	ИначеЕсли ЭлементСписка <> Неопределено И ТипОбработки = "ОтменаПроведения" Тогда
		ОплатыСписок.Удалить(ЭлементСписка);
	КонецЕсли;
	
	БлокировкаИзмененияОбъектовПриЧтенииНаСервереФормыОбъекта();
	ОформитьТаблицуОплат();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура УточнитьПараметрыЗаписиЗавершение(Результат = Неопределено, ПараметрыЗаписи = Неопределено) Экспорт
	
	ТребуетсяУточнитьПараметрыЗаписи = Ложь;
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#Область ВызовыСервера

&НаСервере
Процедура ЗаполнитьДанныеОВыплатеЗначениямиПоУмолчанию()
	
	Если НЕ ЗначениеЗаполнено(Объект.СпособВыплаты) Тогда
		Объект.СпособВыплаты = Справочники.СпособыВыплатыЗарплаты.ПоУмолчанию();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Округление) Тогда
		Объект.Округление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СпособВыплаты, "Округление");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

