#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ВедомостьНаВыплатуЗарплаты.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	УчетЗарплаты.ОбработкаПроверкиЗаполненияВедомостей(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	УчетЗарплаты.ВедомостьНаВыплатуЗарплатыПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи); 
	
	ВедомостьНаВыплатуЗарплаты.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи); 
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ПроверитьНаличиеВалютныхНерезидентов();
	ВедомостьНаВыплатуЗарплаты.ОбработкаПроведения(ЭтотОбъект, Отказ);
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Руководитель          = "";
	ДолжностьРуководителя = "";
	ГлавныйБухгалтер      = "";
	Бухгалтер             = "";
	Кассир                = "";
	ДолжностьКассира      = "";
	
	НДФЛ.Очистить();
	Для Каждого СтрокаТаблицы ИЗ Зарплата Цикл
		СтрокаТаблицы.ДокументОснование             = "";
		СтрокаТаблицы.КВыплате                      = 0;
		СтрокаТаблицы.КомпенсацияЗаЗадержкуЗарплаты = 0;
		СтрокаТаблицы.ПериодВзаиморасчетов          = ПериодРегистрации
	КонецЦикла;
	
	СвернутьТаблицу = Ложь;
	Для Каждого СтрокаСостава ИЗ Состав Цикл
		МассивСтрок = Зарплата.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаСостава.ИдентификаторСтроки));
		Если МассивСтрок.Количество() > 1 Тогда
			СвернутьТаблицу = Истина;
			ЗаполнениеПодразделение        = Неопределено;
			ЗаполнениеСтатьяФинансирования = Неопределено;
			ЗаполнениеСтатьяРасходов       = Неопределено;
			Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
				Если ЗаполнениеПодразделение = Неопределено Тогда
					ЗаполнениеПодразделение = СтрокаМассива.Подразделение;
				КонецЕсли;
				Если ЗаполнениеСтатьяФинансирования = Неопределено Тогда
					ЗаполнениеСтатьяФинансирования = СтрокаМассива.СтатьяФинансирования;
				КонецЕсли;
				Если ЗаполнениеСтатьяРасходов = Неопределено Тогда
					ЗаполнениеСтатьяРасходов = СтрокаМассива.СтатьяРасходов;
				КонецЕсли;
			КонецЦикла;
			Для Каждого СтрокаМассива ИЗ МассивСтрок Цикл
				СтрокаМассива.Подразделение        = ЗаполнениеПодразделение;
				СтрокаМассива.СтатьяФинансирования = ЗаполнениеСтатьяФинансирования;
				СтрокаМассива.СтатьяРасходов       = ЗаполнениеСтатьяРасходов;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если СвернутьТаблицу Тогда
		ЗарплатаВременная = Зарплата.Выгрузить();
		ЗарплатаВременная.Свернуть("ИдентификаторСтроки, Сотрудник, ФизическоеЛицо, Подразделение,
										|ПериодВзаиморасчетов, СтатьяФинансирования, СтатьяРасходов, ДокументОснование",
									"КВыплате, КомпенсацияЗаЗадержкуЗарплаты");
		Зарплата.Загрузить(ЗарплатаВременная);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// АПК:299-выкл Используемость методов внутреннего API ведомостей не контролируется

#Область СценарииЗаполненияДокумента

Функция МожноЗаполнитьЗарплату() Экспорт
	Возврат ВедомостьНаВыплатуЗарплаты.МожноЗаполнитьЗарплату(ЭтотОбъект)
КонецФункции

#КонецОбласти

#Область МестоВыплаты

Функция МестоВыплаты() Экспорт
	Возврат ВедомостьНаВыплатуЗарплаты.МестоВыплатыКасса(ЭтотОбъект);
КонецФункции

Процедура УстановитьМестоВыплаты(Значение) Экспорт
	ВедомостьНаВыплатуЗарплаты.УстановитьМестоВыплатыКасса(ЭтотОбъект, Значение)
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДокумента

Процедура ОчиститьВыплаты() Экспорт
	ВедомостьНаВыплатуЗарплаты.ОчиститьВыплаты(ЭтотОбъект);
КонецПроцедуры	

Процедура ЗагрузитьВыплаты(Зарплата, НДФЛ, Финансирование = Неопределено) Экспорт
	ВедомостьНаВыплатуЗарплаты.ЗагрузитьВыплаты(ЭтотОбъект, Зарплата, НДФЛ);
КонецПроцедуры

Процедура ДобавитьВыплаты(Зарплата, НДФЛ, Финансирование = Неопределено) Экспорт
	ВедомостьНаВыплатуЗарплаты.ДобавитьВыплаты(ЭтотОбъект, Зарплата, НДФЛ);
КонецПроцедуры

Процедура УстановитьНДФЛ(НДФЛ, Знач ФизическиеЛица = Неопределено) Экспорт
	ВедомостьНаВыплатуЗарплаты.УстановитьНДФЛ(ЭтотОбъект, НДФЛ, ФизическиеЛица)
КонецПроцедуры

#КонецОбласти

// АПК:299-вкл

Процедура ПроверитьНаличиеВалютныхНерезидентов()
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, 
		ФизическиеЛица.ВыгрузитьКолонку("ФизическоеЛицо"),"ВидЗастрахованногоЛица",
		ВедомостьНаВыплатуЗарплатыКлиентСервер.ДатаВыплаты(ЭтотОбъект));
		
	ВалютныеРезиденты = Перечисления.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВалютныеРезиденты();
	
	ФизическиеЛицаВалютныеНерезиденты = Новый Массив;
	
	Для Каждого КадровыеДанныеФизическогоЛица Из КадровыеДанные Цикл
		Если ВалютныеРезиденты.Найти(КадровыеДанныеФизическогоЛица.ВидЗастрахованногоЛица) = Неопределено Тогда
			ФизическиеЛицаВалютныеНерезиденты.Добавить(КадровыеДанныеФизическогоЛица.ФизическоеЛицо);
		КонецЕсли;
	КонецЦикла;
	
	Если ФизическиеЛицаВалютныеНерезиденты.Количество() > 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Документ в нарушение норм валютного контроля ЦБ предполагает выплату наличных рублевых
			|сумм следующим иностранным гражданам:'") 
			+ Символы.ПС 
			+ СтрСоединить(ФизическиеЛицаВалютныеНерезиденты, ", ");
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

#Область ПроцедурыИФункцииЗаполненияДокумента

#КонецОбласти
