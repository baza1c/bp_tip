#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	ЗаполнитьПоДаннымОрганизации = Ложь;
	Если ЭтоЭлектронныйДокумент Тогда
		// Если по данным заполнения документ электронный, то нужно сразу заполнить по данным организации.
		ЗаполнитьПоДаннымОрганизации = Истина;
	Иначе
		// Если по данным заполнения документ неэлектронный, то дополнительно проверим, настроен ли ЭДО с контрагентом.
		ЭтоЭлектронныйДокумент = Документы.АктСверкиВзаиморасчетов.НастроенДокументооборотСКонтрагентом(Организация, Контрагент);
	КонецЕсли;
	
	// Если документ электронный, то настроим особенности электронного акта.
	Если ЭтоЭлектронныйДокумент Тогда
		ПараметрДатаНачала = Неопределено;
		ПараметрДатаОкончания = Неопределено;
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ДанныеЗаполнения.Свойство("ДатаНачала", ПараметрДатаНачала);
			ДанныеЗаполнения.Свойство("ДатаОкончания", ПараметрДатаОкончания);
		КонецЕсли;
		Документы.АктСверкиВзаиморасчетов.УстановитьПараметрыДляЭлектронногоДокумента(
			ЭтотОбъект, ПараметрДатаНачала, ПараметрДатаОкончания);
	КонецЕсли;
	
	Если ЗаполнитьПоДаннымОрганизации Тогда
		Документы.АктСверкиВзаиморасчетов.ЗаполнитьАктСверкиПоДаннымИБ(ЭтотОбъект);
	КонецЕсли;
	
	// Заполняем счета учета взаиморасчетов
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("СчетаУчетаРасчетов") Тогда
		// Из данных заполнения
		Для Каждого СчетУчетаРасчетов Из ДанныеЗаполнения.СчетаУчетаРасчетов Цикл
			СтрокаСчета = СписокСчетов.Добавить();
			СтрокаСчета.Счет = СчетУчетаРасчетов;
			СтрокаСчета.УчаствуетВРасчетах = Истина;
		КонецЦикла;
	Иначе
		// По умолчанию
		ТаблицаСчетов = УчетВзаиморасчетов.ПолучитьТаблицуСчетовУчетаВзаиморасчетов(Истина, Ложь);
		ТаблицаСчетов.Колонки.СчетРасчетов.Имя = "Счет";
		ТаблицаСчетов.Колонки.Добавить("УчаствуетВРасчетах", Новый ОписаниеТипов("Булево"));
		ТаблицаСчетов.ЗаполнитьЗначения(Истина, "УчаствуетВРасчетах");
		СписокСчетов.Загрузить(ТаблицаСчетов);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ПредставительКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ОсновноеКонтактноеЛицо");
	КонецЕсли;
	Если ЗначениеЗаполнено(Организация) Тогда
		ПредставительОрганизации = Документы.АктСверкиВзаиморасчетов.ПредставительОрганизации(Организация, Дата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) 
		И ДатаНачала > ДатаОкончания Тогда
	
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность",
			НСтр("ru = 'Период по'"), , , 
			НСтр("ru = 'Дата окончания периода сверки должна быть больше даты начала.'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаОкончания", "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСтатусДокумента();

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура УстановитьСтатусДокумента() Экспорт
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("СтатусДокумента") Тогда
		// Запись из формы документа
		СтатусДокумента = ?(ДополнительныеСвойства.СтатусДокумента,
			Перечисления.СтатусыДокументовРеализации.Подписан, 
			Перечисления.СтатусыДокументовРеализации.НеПодписан);
	КонецЕсли;
	
	УстановленСтатусДокумента = Истина;
	СтатусыДокумента          = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
	СтатусыДокумента.Статус   = СтатусДокумента;
	РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(ЭтотОбъект.Ссылка, СтатусыДокумента);
	
КонецПроцедуры

#КонецОбласти 

#КонецЕсли