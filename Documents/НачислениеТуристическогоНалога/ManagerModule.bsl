#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	Если ПравоДоступа("Использование", Метаданные.Отчеты.РеестрДокументов) Тогда
		// Реестр документов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Реестр";
		КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Начисление туристического налога""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка";
		КомандаПечати.Порядок       = 100;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Информация", "Контрагент");
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПоДокументу(Объект) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ДопУслугаРазмещения = Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд;
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Объект.Услуги.Очистить();
		
	ДанныеЗаполнения = ДанныеЗаполненияПоДокументу(Объект, ВалютаРегУчета);
	
	СуммаВключаетНДС = Объект.СуммаВключаетНДС;

	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		
		Для Каждого СтрокаУслуг Из ДанныеЗаполнения Цикл
		
			Если СтрокаУслуг.ВалютаДокумента <> ВалютаРегУчета Тогда
				ПараметрыТекущегоКурса = Новый Структура("Валюта, Курс, Кратность",
					СтрокаУслуг.ВалютаДокумента, СтрокаУслуг.КурсВзаиморасчетов, СтрокаУслуг.КратностьВзаиморасчетов);
				ПараметрыНовогоКурса = Новый Структура("Валюта, Курс, Кратность",
					ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета(), 1, 1);
				
				СтрокаУслуг.Цена = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(
					СтрокаУслуг.Цена, ПараметрыТекущегоКурса, ПараметрыНовогоКурса);
		        ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаУслуг, 1);
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаУслуг, СуммаВключаетНДС);
			КонецЕсли;
			
		КонецЦикла;
		
		Объект.Услуги.Загрузить(ДанныеЗаполнения);
		
	КонецЕсли;

	УстановитьГостиницу(Объект, ДанныеЗаполнения);
	Ставка = Справочники.Гостиницы.СтавкаНалогаНаДату(Объект.Гостиница, Объект.Дата);
		
	Услуги = Объект.Услуги.Выгрузить(); 
	ЕстьКолонкаУслугаРазмещения = Услуги.Колонки.Найти("УслугаРазмещения") <> Неопределено;

	// При заполнении на основании
	Если Не ЕстьКолонкаУслугаРазмещения Тогда 	
		Услуги.Колонки.Добавить("УслугаРазмещения", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУслугРазмещения"));
	КонецЕсли;
	
	Номенклатуры = ОбщегоНазначения.ВыгрузитьКолонку(Услуги, "Номенклатура", Истина);
	УслугиРазмещенияНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Номенклатуры, "УслугаРазмещения");
		
	Для Каждого СтрокаУслуг Из Услуги Цикл
		
		СтрокаУслуг.УслугаРазмещения = УслугиРазмещенияНоменклатуры.Получить(СтрокаУслуг.Номенклатура); 
		Если Не ЗначениеЗаполнено(СтрокаУслуг.Ставка) Тогда
			СтрокаУслуг.Ставка = Ставка;
		КонецЕсли;
		ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Услуги, СтрокаУслуг, СуммаВключаетНДС, ДопУслугаРазмещения);
				
	КонецЦикла;
	
	Объект.Услуги.Загрузить(Услуги);
	
КонецПроцедуры

Функция ДанныеЗаполненияПоДокументу(Объект, ВалютаРегУчета)
	
	ДокументОснование = Объект.ДокументОснование;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ТекстЗапроса = ТекстЗапросаДанныеЗаполненияПоРеализации();
				
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		
		ТекстЗапроса = ТекстЗапросаДанныеЗаполненияПоОтчетуКомиссионера();
			
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
	
		ТекстЗапроса = ТекстЗапросаДанныеЗаполненияПоСчету();
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		ТекстЗапроса = ТекстЗапросаДанныеЗаполненияПоОтчетуОРозничныхПродажах();
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.НачислениеТуристическогоНалога") Тогда
	
		ТекстЗапроса = ТекстЗапросаДанныеЗаполненияПоНачислениюТуристическогоНалога();
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка", Объект.ДокументОснование);
		Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
		Запрос.УстановитьПараметр("Гостиница", Объект.Гостиница); 
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		Запрос.УстановитьПараметр("Период", Объект.Дата);
        Запрос.УстановитьПараметр("ВалютаРегУчета", ВалютаРегУчета);

		Результат = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ГостиницаВШапке(Услуги, Гостиница, ДокументОснование) Экспорт
	
	Если  Не ЗначениеЗаполнено(ДокументОснование) 
		Или ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетНаОплатуПокупателю")
		Или ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		
        Возврат Истина;
		
	КонецЕсли;
	
	ТаблицаУслуг = Услуги.Выгрузить(, "Гостиница");
	ТаблицаУслуг.Свернуть("Гостиница");
	Возврат ТаблицаУслуг.Количество() = 0
		Или (ТаблицаУслуг.Количество() = 1 И ЗначениеЗаполнено(Гостиница));

КонецФункции

Функция ПолучитьИсправляемыйДокумент(ДокументОснование, ОбработанныеДокументы = Неопределено) Экспорт 

	Если Не ЗначениеЗаполнено(ДокументОснование) 
		Или ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.НачислениеТуристическогоНалога")Тогда
		
		Возврат ДокументОснование;
		
	КонецЕсли;

	РеквизитДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ДокументОснование");

	Если ОбработанныеДокументы = Неопределено Тогда
		ОбработанныеДокументы = Новый Массив;
	ИначеЕсли ОбработанныеДокументы.Найти(РеквизитДокументОснование) <> Неопределено Тогда
		Возврат ОбработанныеДокументы[0]; // Если обнаружена зацикленность, возвращаем первый документ в массиве.
	КонецЕсли;

	ОбработанныеДокументы.Добавить(ДокументОснование); // Добавляем обработанный документ, для поиска зацикленности
		
	// Если документ не первоначальный, то вызываем рекурсию.
	Возврат ПолучитьИсправляемыйДокумент(РеквизитДокументОснование, ОбработанныеДокументы);
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(Ссылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Гостиница КАК Гостиница,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Реквизиты.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.НачислениеТуристическогоНалога КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	Если Не УчетнаяПолитика.Существует(Выборка.Организация, Выборка.Период, Истина, Ссылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;

	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Выборка[Колонка.Имя]);
	КонецЦикла;	
	
	ИсправляемыйДокумент = ПолучитьИсправляемыйДокумент(Выборка.ДокументОснование);
	Если НЕ ЗначениеЗаполнено(ИсправляемыйДокумент) Тогда
		ИсправляемыйДокумент = Ссылка;
	КонецЕсли;
	Запрос.УстановитьПараметр("ДокументОснование", ИсправляемыйДокумент);
	
	ЗаполненСчетНаОплату = ЗначениеЗаполнено(Выборка.ДокументОснование) 
		И ТипЗнч(Выборка.ДокументОснование) = Тип("ДокументСсылка.СчетНаОплатуПокупателю");
	
    Запрос.УстановитьПараметр("ЗаполненСчетНаОплату", ЗаполненСчетНаОплату);
    Запрос.УстановитьПараметр("УслугиРазмещения", ТуристическийНалогСервер.МассивУслугРазмещения());

	НомераТаблиц = Новый Структура();

	Запрос.Текст = ТекстЗапросаТуристическийНалог(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();

	ПараметрыПроведения.Вставить("ТуристическийНалогКНачислению", Результат[НомераТаблиц.ТуристическийНалогКНачислению].Выгрузить());
	ПараметрыПроведения.Вставить("СторноТуристическийНалог", Результат[НомераТаблиц.СторноТуристическийНалог].Выгрузить());
		
	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаТуристическийНалог(НомераТаблиц) Экспорт
	
	НомераТаблиц.Вставить("ВТ_ТаблицаУслуги",              НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ТуристическийНалог",         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТуристическийНалогКНачислению", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СторноТуристическийНалог",      НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
	|	1 КАК КоличествоЛиц,
	|	ТаблицаУслуги.СпособРасчета КАК СпособРасчета,
	|	ТаблицаУслуги.Гостиница КАК Гостиница,
	|	ТаблицаУслуги.ВидЛьготыПоТуристическомуНалогу КАК ВидЛьготы,
	|	ТаблицаУслуги.Количество КАК КоличествоУслуг,
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &СуммаВключаетНДС
	|					ТОГДА ТаблицаУслуги.Сумма - ТаблицаУслуги.СуммаНДС
	|				ИНАЧЕ ТаблицаУслуги.Сумма
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьУслуг,
	|	&ДокументОснование КАК ДокументОснование,
	|	&Ссылка КАК РасчетныйДокумент,
	|	ТаблицаУслуги.Ставка КАК Ставка,
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаУслуги.СуммаНалога
	|	КОНЕЦ КАК СуммаНалога,
	|	0 КАК СтоимостьДопУслуг,
	|	ТаблицаУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТ_ТаблицаУслуги
	|ИЗ
	|	Документ.НачислениеТуристическогоНалога.Услуги КАК ТаблицаУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ТаблицаУслуги.Номенклатура = СпрНоменклатура.Ссылка
	|			И (ТаблицаУслуги.Ссылка = &Ссылка)
	|			И (СпрНоменклатура.УслугаРазмещения В (&УслугиРазмещения))
	|			И (ТаблицаУслуги.Количество <> 0
	|				ИЛИ &ЗаполненСчетНаОплату)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаУслуги.Номенклатура,
	|	0,
	|	ТаблицаУслуги.СпособРасчета,
	|	ТаблицаУслуги.Гостиница,
	|	ТаблицаУслуги.ВидЛьготыПоТуристическомуНалогу,
	|	0,
	|	ВЫБОР
	|		КОГДА &СуммаВключаетНДС
	|			ТОГДА ТаблицаДопУслуги.Сумма - ТаблицаДопУслуги.СуммаНДС
	|		ИНАЧЕ ТаблицаДопУслуги.Сумма
	|	КОНЕЦ,
	|	&ДокументОснование,
	|	&Ссылка,
	|	ТаблицаУслуги.Ставка,
	|	0,
	|	ВЫБОР
	|		КОГДА &СуммаВключаетНДС
	|			ТОГДА ТаблицаДопУслуги.Сумма - ТаблицаДопУслуги.СуммаНДС
	|		ИНАЧЕ ТаблицаДопУслуги.Сумма
	|	КОНЕЦ,
	|	ТаблицаУслуги.ИдентификаторСтроки
	|ИЗ
	|	Документ.НачислениеТуристическогоНалога.Услуги КАК ТаблицаУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ТаблицаУслуги.Номенклатура = СпрНоменклатура.Ссылка
	|			И (ТаблицаУслуги.Ссылка = &Ссылка)
	|			И (СпрНоменклатура.УслугаРазмещения В (&УслугиРазмещения))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеТуристическогоНалога.Услуги КАК ТаблицаДопУслуги
	|		ПО (ТаблицаДопУслуги.ИдентификаторРодительскойСтроки = ТаблицаУслуги.ИдентификаторСтроки)
	|			И (ТаблицаДопУслуги.Ссылка = &Ссылка)
	|			И (ТаблицаДопУслуги.ИдентификаторРодительскойСтроки <> """")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТуристическийНалог.Организация КАК Организация,
	|	ТуристическийНалог.Ставка КАК Ставка,
	|	ТуристическийНалог.Номенклатура КАК Номенклатура,
	|	СУММА(ТуристическийНалог.КоличествоЛиц) КАК КоличествоЛиц,
	|	ТуристическийНалог.СпособРасчета КАК СпособРасчета,
	|	ТуристическийНалог.Гостиница КАК Гостиница,
	|	ТуристическийНалог.ВидЛьготы КАК ВидЛьготы,
	|	СУММА(ТуристическийНалог.КоличествоУслуг) КАК КоличествоУслуг,
	|	СУММА(ТуристическийНалог.СтоимостьУслуг) КАК СтоимостьУслуг,
	|	ТуристическийНалог.ДокументОснование КАК ДокументОснование,
	|	ТуристическийНалог.РасчетныйДокумент КАК РасчетныйДокумент,
	|	СУММА(ТуристическийНалог.СуммаНалога) КАК СуммаНалога,
	|	ТуристическийНалог.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СУММА(ТуристическийНалог.СтоимостьДопУслуг) КАК СтоимостьДопУслуг
	|ПОМЕСТИТЬ ВТ_ТуристическийНалог
	|ИЗ
	|	РегистрНакопления.ТуристическийНалог КАК ТуристическийНалог
	|ГДЕ
	|	ТуристическийНалог.ДокументОснование = &ДокументОснование
	|	И ТуристическийНалог.Активность = ИСТИНА
	|	И ТуристическийНалог.Регистратор <> &Ссылка
	|	И ТуристическийНалог.Период <= &Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТуристическийНалог.ВидЛьготы,
	|	ТуристическийНалог.СпособРасчета,
	|	ТуристическийНалог.Гостиница,
	|	ТуристическийНалог.Организация,
	|	ТуристическийНалог.Номенклатура,
	|	ТуристическийНалог.РасчетныйДокумент,
	|	ТуристическийНалог.ДокументОснование,
	|	ТуристическийНалог.Ставка,
	|	ТуристическийНалог.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВТ_ТаблицаУслуги.Ставка КАК Ставка,
	|	ВТ_ТаблицаУслуги.Номенклатура КАК Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ТаблицаУслуги.КоличествоУслуг = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВТ_ТаблицаУслуги.КоличествоЛиц
	|		КОНЕЦ) КАК КоличествоЛиц,
	|	ВТ_ТаблицаУслуги.СпособРасчета КАК СпособРасчета,
	|	ВТ_ТаблицаУслуги.Гостиница КАК Гостиница,
	|	ВТ_ТаблицаУслуги.ВидЛьготы КАК ВидЛьготы,
	|	СУММА(ВТ_ТаблицаУслуги.КоличествоУслуг) КАК КоличествоУслуг,
	|	СУММА(ВТ_ТаблицаУслуги.СтоимостьУслуг) КАК СтоимостьУслуг,
	|	ВТ_ТаблицаУслуги.ДокументОснование КАК ДокументОснование,
	|	ВТ_ТаблицаУслуги.РасчетныйДокумент КАК РасчетныйДокумент,
	|	СУММА(ВТ_ТаблицаУслуги.СуммаНалога) КАК СуммаНалога,
	|	СУММА(ВТ_ТаблицаУслуги.СтоимостьДопУслуг) КАК СтоимостьДопУслуг,
	|	ВТ_ТаблицаУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТ_ТаблицаУслуги КАК ВТ_ТаблицаУслуги
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаУслуги.ДокументОснование,
	|	ВТ_ТаблицаУслуги.СпособРасчета,
	|	ВТ_ТаблицаУслуги.Номенклатура,
	|	ВТ_ТаблицаУслуги.РасчетныйДокумент,
	|	ВТ_ТаблицаУслуги.Гостиница,
	|	ВТ_ТаблицаУслуги.ВидЛьготы,
	|	ВТ_ТаблицаУслуги.Ставка,
	|	ВТ_ТаблицаУслуги.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВТ_ТуристическийНалог.Организация КАК Организация,
	|	ВТ_ТуристическийНалог.Ставка КАК Ставка,
	|	ВТ_ТуристическийНалог.Номенклатура КАК Номенклатура,
	|	-ВТ_ТуристическийНалог.КоличествоЛиц КАК КоличествоЛиц,
	|	ВТ_ТуристическийНалог.СпособРасчета КАК СпособРасчета,
	|	ВТ_ТуристическийНалог.Гостиница КАК Гостиница,
	|	ВТ_ТуристическийНалог.ВидЛьготы КАК ВидЛьготы,
	|	-ВТ_ТуристическийНалог.КоличествоУслуг КАК КоличествоУслуг,
	|	-ВТ_ТуристическийНалог.СтоимостьУслуг КАК СтоимостьУслуг,
	|	ВТ_ТуристическийНалог.ДокументОснование КАК ДокументОснование,
	|	ВТ_ТуристическийНалог.РасчетныйДокумент КАК РасчетныйДокумент,
	|	-ВТ_ТуристическийНалог.СуммаНалога КАК СуммаНалога,
	|	-ВТ_ТуристическийНалог.СтоимостьДопУслуг КАК СтоимостьДопУслуг,
	|	ВТ_ТуристическийНалог.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВТ_ТуристическийНалог КАК ВТ_ТуристическийНалог
	|ГДЕ
	|	ВТ_ТуристическийНалог.КоличествоУслуг <> 0";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СформироватьДвиженияТуристическийНалог(ТуристическийНалогКНачислению, СторноТуристическийНалог, Движения, Отказ) Экспорт
	
	Если ТуристическийНалогКНачислению.Количество() = 0
		И СторноТуристическийНалог.Количество() = 0 Тогда	
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из СторноТуристическийНалог Цикл 
		
		СтрокаДвижение = Движения.ТуристическийНалог.Добавить();	
		ЗаполнитьЗначенияСвойств(СтрокаДвижение, СтрокаТаблицы);
				
	КонецЦикла;

	Для Каждого СтрокаТаблицы Из ТуристическийНалогКНачислению Цикл 
		
		СтрокаДвижение = Движения.ТуристическийНалог.Добавить();	
		ЗаполнитьЗначенияСвойств(СтрокаДвижение, СтрокаТаблицы);
				
	КонецЦикла;
	
	Движения.ТуристическийНалог.Записывать = Истина;
	
КонецПроцедуры

// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Документ_НачислениеТуристическогоНалога";
	Набор.Идентификатор = Новый УникальныйИдентификатор("7b468b93-f543-410e-a5a8-731f5af97adc");
	Набор.Используется  = ПолучитьФункциональнуюОпцию("УплачиваетсяТуристическийНалог");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаДанныеЗаполненияПоОтчетуКомиссионера()
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахУслуги.НомерСтроки КАК НомерСтроки,
	|	ОтчетКомиссионераОПродажахУслуги.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ОтчетКомиссионераОПродажахУслуги.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ ОтчетКомиссионераОПродажахУслуги.Количество
	|	КОНЕЦ КАК Количество,
	|	ОтчетКомиссионераОПродажахУслуги.Цена КАК Цена,
	|	ОтчетКомиссионераОПродажахУслуги.Сумма КАК Сумма,
	|	ОтчетКомиссионераОПродажахУслуги.СтавкаНДС КАК СтавкаНДС,
	|	ОтчетКомиссионераОПродажахУслуги.СуммаНДС КАК СуммаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоТуристическомуНалогу.НеПрименяется) КАК ВидЛьготыПоТуристическомуНалогу,
	|	ОтчетКомиссионераОПродажахУслуги.Содержание КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Гостиницы.ПустаяСсылка) КАК Гостиница,
	|	ЗНАЧЕНИЕ(Справочник.Гостиницы.ПустаяСсылка) КАК ГостиницаШапка,
	|	0 КАК Ставка,
	|	ОтчетКомиссионераОПродажах.ВалютаДокумента КАК ВалютаДокумента,
	|	ОтчетКомиссионераОПродажах.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ОтчетКомиссионераОПродажах.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Услуги КАК ОтчетКомиссионераОПродажахУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ОтчетКомиссионераОПродажахУслуги.Номенклатура = СпрНоменклатура.Ссылка
	|			И (СпрНоменклатура.УслугаРазмещения <> ЗНАЧЕНИЕ(Перечисление.ВидыУслугРазмещения.ПустаяСсылка))
	|			И (ОтчетКомиссионераОПродажахУслуги.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|		ПО ОтчетКомиссионераОПродажахУслуги.Ссылка = ОтчетКомиссионераОПродажах.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеЗаполненияПоРеализации()
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугУслуги.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслугУслуги.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугУслуги.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ РеализацияТоваровУслугУслуги.Количество
	|	КОНЕЦ КАК Количество,
	|	РеализацияТоваровУслугУслуги.Цена КАК Цена,
	|	РеализацияТоваровУслугУслуги.Сумма КАК Сумма,
	|	РеализацияТоваровУслугУслуги.СтавкаНДС КАК СтавкаНДС,
	|	РеализацияТоваровУслугУслуги.СуммаНДС КАК СуммаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЛьготПоТуристическомуНалогу.НеПрименяется) КАК ВидЛьготыПоТуристическомуНалогу,
	|	РеализацияТоваровУслугУслуги.Содержание КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Гостиницы.ПустаяСсылка) КАК Гостиница,
	|	ЗНАЧЕНИЕ(Справочник.Гостиницы.ПустаяСсылка) КАК ГостиницаШапка,
	|	0 КАК Ставка,
	|	РеализацияТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваровУслуг.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	РеализацияТоваровУслуг.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО РеализацияТоваровУслугУслуги.Номенклатура = СпрНоменклатура.Ссылка
	|			И (СпрНоменклатура.УслугаРазмещения <> ЗНАЧЕНИЕ(Перечисление.ВидыУслугРазмещения.ПустаяСсылка))
	|			И (РеализацияТоваровУслугУслуги.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО РеализацияТоваровУслугУслуги.Ссылка = РеализацияТоваровУслуг.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеЗаполненияПоСчету()
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияСтавок.Период) КАК Период,
	|	ИсторияСтавок.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ПоследниеДатыСтавок
	|ИЗ
	|	Справочник.Гостиницы.ИсторияСтавок КАК ИсторияСтавок
	|ГДЕ
	|	ИсторияСтавок.Период <= &Период
	|	И ИсторияСтавок.Ссылка = &Гостиница
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияСтавок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияСтавокНалога.Ставка КАК Ставка,
	|	ИсторияСтавокНалога.Ссылка КАК Гостиница
	|ПОМЕСТИТЬ СтавкиТуристическогоНалога
	|ИЗ
	|	ВТ_ПоследниеДатыСтавок КАК ВТ_ПоследниеДатыСтавок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Гостиницы.ИсторияСтавок КАК ИсторияСтавокНалога
	|		ПО ВТ_ПоследниеДатыСтавок.Ссылка = ИсторияСтавокНалога.Ссылка
	|			И ВТ_ПоследниеДатыСтавок.Период = ИсторияСтавокНалога.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетНаОплатуПокупателюТовары.НомерСтроки КАК НомерСтроки,
	|	СчетНаОплатуПокупателюТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА СчетНаОплатуПокупателюТовары.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ СчетНаОплатуПокупателюТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА СчетНаОплатуПокупателюТовары.Количество <> 0
	|			ТОГДА (СчетНаОплатуПокупателюТовары.Сумма - СчетНаОплатуПокупателюТовары.СуммаСкидки) / СчетНаОплатуПокупателюТовары.Количество
	|		ИНАЧЕ СчетНаОплатуПокупателюТовары.Сумма - СчетНаОплатуПокупателюТовары.СуммаСкидки
	|	КОНЕЦ КАК Цена,
	|	СчетНаОплатуПокупателюТовары.Сумма - СчетНаОплатуПокупателюТовары.СуммаСкидки КАК Сумма,
	|	СчетНаОплатуПокупателюТовары.СтавкаНДС КАК СтавкаНДС,
	|	СчетНаОплатуПокупателюТовары.СуммаНДС КАК СуммаНДС,
	|	СчетНаОплатуПокупателюТовары.ВидЛьготыПоТуристическомуНалогу КАК ВидЛьготыПоТуристическомуНалогу,
	|	СчетНаОплатуПокупателюТовары.Содержание КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Гостиницы.ПустаяСсылка) КАК Гостиница,
	|	СпрГостиницы.Ссылка КАК ГостиницаШапка,
	|	ЕСТЬNULL(СтавкиТуристическогоНалога.Ставка, СпрГостиницы.СтавкаТуристическогоНалога) КАК Ставка,
	|	СчетНаОплатуПокупателю.ВалютаДокумента КАК ВалютаДокумента,
	|	СчетНаОплатуПокупателю.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	СчетНаОплатуПокупателю.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	СчетНаОплатуПокупателюТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СчетНаОплатуПокупателюТовары.ИдентификаторРодительскойСтроки КАК ИдентификаторРодительскойСтроки
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.Товары КАК СчетНаОплатуПокупателюТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СчетНаОплатуПокупателюТовары.Номенклатура = СпрНоменклатура.Ссылка
	|			И (СпрНоменклатура.УслугаРазмещения <> ЗНАЧЕНИЕ(Перечисление.ВидыУслугРазмещения.ПустаяСсылка))
	|			И (СчетНаОплатуПокупателюТовары.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|		ПО СчетНаОплатуПокупателюТовары.Ссылка = СчетНаОплатуПокупателю.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Гостиницы КАК СпрГостиницы
	|		ПО (СпрГостиницы.Ссылка = СчетНаОплатуПокупателю.Гостиница)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиТуристическогоНалога КАК СтавкиТуристическогоНалога
	|		ПО (СтавкиТуристическогоНалога.Гостиница = СчетНаОплатуПокупателю.Гостиница)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеЗаполненияПоОтчетуОРозничныхПродажах()
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ИсторияСтавок.Период) КАК Период,
	|	ИсторияСтавок.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ПоследниеДатыСтавок
	|ИЗ
	|	Справочник.Гостиницы.ИсторияСтавок КАК ИсторияСтавок
	|ГДЕ
	|	ИсторияСтавок.Период <= &Период
	|	И ИсторияСтавок.Ссылка.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсторияСтавок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияСтавокНалога.Ставка КАК Ставка,
	|	ИсторияСтавокНалога.Ссылка КАК Гостиница
	|ПОМЕСТИТЬ СтавкиТуристическогоНалога
	|ИЗ
	|	ВТ_ПоследниеДатыСтавок КАК ВТ_ПоследниеДатыСтавок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Гостиницы.ИсторияСтавок КАК ИсторияСтавокНалога
	|		ПО ВТ_ПоследниеДатыСтавок.Ссылка = ИсторияСтавокНалога.Ссылка
	|			И ВТ_ПоследниеДатыСтавок.Период = ИсторияСтавокНалога.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетОРозничныхПродажахТовары.НомерСтроки КАК НомерСтроки,
	|	ОтчетОРозничныхПродажахТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ОтчетОРозничныхПродажахТовары.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ ОтчетОРозничныхПродажахТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ОтчетОРозничныхПродажахТовары.Цена КАК Цена,
	|	ОтчетОРозничныхПродажахТовары.Сумма КАК Сумма,
	|	ОтчетОРозничныхПродажахТовары.СтавкаНДС КАК СтавкаНДС,
	|	ОтчетОРозничныхПродажахТовары.СуммаНДС КАК СуммаНДС,
	|	ОтчетОРозничныхПродажахТовары.ВидЛьготыПоТуристическомуНалогу КАК ВидЛьготыПоТуристическомуНалогу,
	|	СпрНоменклатура.НаименованиеПолное КАК Содержание,
	|	ОтчетОРозничныхПродажахТовары.Гостиница КАК Гостиница,
	|	ЗНАЧЕНИЕ(Справочник.Гостиницы.ПустаяСсылка) КАК ГостиницаШапка,
	|	ЕСТЬNULL(СтавкиТуристическогоНалога.Ставка, СпрГостиницы.СтавкаТуристическогоНалога) КАК Ставка,
	|	ОтчетОРозничныхПродажах.ВалютаДокумента КАК ВалютаДокумента,
	|	ОтчетОРозничныхПродажах.КурсДокумента КАК КурсДокумента,
	|	ОтчетОРозничныхПродажах.КратностьДокумента КАК КратностьДокумента,
	|	ОтчетОРозничныхПродажахТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ОтчетОРозничныхПродажахТовары.ИдентификаторРодительскойСтроки КАК ИдентификаторРодительскойСтроки
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ОтчетОРозничныхПродажахТовары.Номенклатура = СпрНоменклатура.Ссылка
	|			И (СпрНоменклатура.УслугаРазмещения <> ЗНАЧЕНИЕ(Перечисление.ВидыУслугРазмещения.ПустаяСсылка))
	|			И (ОтчетОРозничныхПродажахТовары.Ссылка = &Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиТуристическогоНалога КАК СтавкиТуристическогоНалога
	|		ПО ОтчетОРозничныхПродажахТовары.Гостиница = СтавкиТуристическогоНалога.Гостиница
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Гостиницы КАК СпрГостиницы
	|		ПО ОтчетОРозничныхПродажахТовары.Гостиница = СпрГостиницы.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ПО ОтчетОРозничныхПродажахТовары.Ссылка = ОтчетОРозничныхПродажах.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДанныеЗаполненияПоНачислениюТуристическогоНалога()
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НачислениеТуристическогоНалогаУслуги.НомерСтроки КАК НомерСтроки,
	|	НачислениеТуристическогоНалогаУслуги.Номенклатура КАК Номенклатура,
	|	НачислениеТуристическогоНалогаУслуги.Количество КАК Количество,
	|	НачислениеТуристическогоНалогаУслуги.Цена КАК Цена,
	|	НачислениеТуристическогоНалогаУслуги.Сумма КАК Сумма,
	|	НачислениеТуристическогоНалогаУслуги.СтавкаНДС КАК СтавкаНДС,
	|	НачислениеТуристическогоНалогаУслуги.СуммаНДС КАК СуммаНДС,
	|	НачислениеТуристическогоНалогаУслуги.ВидЛьготыПоТуристическомуНалогу КАК ВидЛьготыПоТуристическомуНалогу,
	|	НачислениеТуристическогоНалогаУслуги.Содержание КАК Содержание,
	|	НачислениеТуристическогоНалогаУслуги.Гостиница КАК Гостиница,
	|	НачислениеТуристическогоНалога.Гостиница КАК ГостиницаШапка,
	|	НачислениеТуристическогоНалогаУслуги.Ставка КАК Ставка,
	|	&ВалютаРегУчета КАК ВалютаДокумента,
	|	1 КАК КурсДокумента,
	|	1 КАК КратностьДокумента,
	|	НачислениеТуристическогоНалогаУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачислениеТуристическогоНалогаУслуги.ИдентификаторРодительскойСтроки КАК ИдентификаторРодительскойСтроки
	|ИЗ
	|	Документ.НачислениеТуристическогоНалога.Услуги КАК НачислениеТуристическогоНалогаУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО НачислениеТуристическогоНалогаУслуги.Номенклатура = СпрНоменклатура.Ссылка
	|			И (СпрНоменклатура.УслугаРазмещения <> ЗНАЧЕНИЕ(Перечисление.ВидыУслугРазмещения.ПустаяСсылка))
	|			И (НачислениеТуристическогоНалогаУслуги.Ссылка = &Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеТуристическогоНалога КАК НачислениеТуристическогоНалога
	|		ПО (НачислениеТуристическогоНалога.Ссылка = &Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьГостиницу(Объект, ДанныеЗаполнения)
	
	ДанныеЗаполнения.Свернуть("Гостиница, ГостиницаШапка");
	
	Если ДанныеЗаполнения.Количество() = 1 И ЗначениеЗаполнено(ДанныеЗаполнения[0].Гостиница) Тогда 
		Объект.Гостиница = ДанныеЗаполнения[0].Гостиница;
	ИначеЕсли ДанныеЗаполнения.Количество() > 1 И ЗначениеЗаполнено(ДанныеЗаполнения[0].Гостиница) Тогда 
		Объект.Гостиница = Неопределено;
	ИначеЕсли ДанныеЗаполнения.Количество() > 0 И ЗначениеЗаполнено(ДанныеЗаполнения[0].ГостиницаШапка) Тогда 
		Объект.Гостиница = ДанныеЗаполнения[0].ГостиницаШапка;
	Иначе
		ТуристическийНалогСервер.УстановитьГостиницу(Объект.Гостиница, Объект.Организация);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
