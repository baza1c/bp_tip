
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
		
    СтавкаГостиницы = СтавкаТурНалога(Объект.Гостиница, Объект.Дата);
	
	УстановитьУсловноеОформление(); 
	
	УстановитьПараметрыВыбораСтавкиНДС();
		
	УстановитьПараметрыВыбораНоменклатуры();
	
	УстановитьВидимостьГостиницы();
		
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.НачислениеТуристическогоНалога",
		"ФормаДокумента",
		НСтр("ru='Новости: Начисление туристического налога'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
			
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
		
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	ПроведениеСервер.УстановитьПризнакПроверкиРеквизитов(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	УстановитьСостояниеДокумента();
	
	УстановитьРеквизитыФормыТуристическийНалог();  
	ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Услуги");
	ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Услуги");
			
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		
		ОрганизацияПриИзмененииСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ПриИзмененииДаты();
				
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)

	ДокументОснованиеПриИзмененииНаКлиенте();
		
КонецПроцедуры

&НаКлиенте
Процедура ГостиницаПриИзменении(Элемент)
	
	УстановитьСтавкуТурНалога(ЭтотОбъект);
	РассчитатьТуристическийНалогТабЧасти();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	
	УслугиНоменклатураПриИзмененииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиГостиницаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	ТекущиеДанные.Ставка = СтавкаТурНалога(ТекущиеДанные.Гостиница, Объект.Дата);
	ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, ТекущиеДанные, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;

	Если ТекущиеДанные <> Неопределено И НомерАктивнойСтроки <> ТекущиеДанные.НомерСтроки Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьПредставлениеИДРодСтрок", 0.2, Истина);	
	КонецЕсли;

	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	Если НоваяСтрока Тогда
		СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
		СтрокаТабличнойЧасти.ИдентификаторСтроки = Новый УникальныйИдентификатор;
	КонецЕсли;

	Если НоваяСтрока И Не Копирование Тогда
		Элемент.ТекущиеДанные.СтавкаНДС = СтавкаНДСПриРеализации; 
		Если ЗначениеЗаполнено(Объект.Гостиница) Тогда
			Элемент.ТекущиеДанные.Гостиница = Объект.Гостиница;
			Элемент.ТекущиеДанные.Ставка = СтавкаГостиницы;
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
		
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)

	ПриИзмененииКоличествоЦена();

КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)

	ПриИзмененииКоличествоЦена();

КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)

	ПриИзмененииСумма();

КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)

	ПриИзмененииСтавкаНДС();

КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаНДСПриИзменении(Элемент)

	ПриИзмененииСуммыНДС();

КонецПроцедуры

&НаКлиенте
Процедура УслугиВидЛьготыПоТуристическомуНалогуПриИзменении(Элемент)
	
	ПриИзмененииЛьготы(); 
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиИдентификаторРодительскойСтрокиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	ТуристическийНалогКлиентСервер.ПредставлениеИДРодСтроки(ТекущиеДанные, ТаблицаОсновныхУслугРазмещения);
	РассчитатьТуристическийНалогТабЧасти();
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура УслугиИдентификаторРодительскойСтрокиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)  
	ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Услуги");
КонецПроцедуры

&НаКлиенте
Процедура УслугиИдентификаторРодительскойСтрокиОбработкаВыбора(Элемент, ВыбранноеЗначение, 
		ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
    Если ВыбранноеЗначение = "ПоказатьВсе" Тогда
		АдресТаблицыУслуг = АдресТаблицыОсновныхУслугРазмещения();
		ТуристическийНалогКлиент.ВыборСтрокиУслугиРазмещения(ЭтотОбъект, СтандартнаяОбработка, АдресТаблицыУслуг, "Услуги");
    КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура УслугиИдентификаторРодительскойСтрокиАвтоПодбор(Элемент, Текст, ДанныеВыбора, 
	ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ТуристическийНалогКлиентСервер.ДанныеВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Услуги",  Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиИдентификаторРодительскойСтрокиОкончаниеВводаТекста(Элемент, Текст, 
	ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ТуристическийНалогКлиентСервер.ДанныеВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Услуги",  Текст);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УслугиПослеУдаления(Элемент)
	ТуристическийНалогКлиентСервер.УдалитьНесвязанныеИдентификаторы(ЭтотОбъект, "Услуги");
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		НомерАктивнойСтроки = ТекущиеДанные.НомерСтроки;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
 
&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиВсего");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ДокументБезНДС", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	// УслугиСуммаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСуммаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// УслугиВидЛьготыПоТуристическомуНалогу
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиВидЛьготыПоТуристическомуНалогу");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.СанаторноКурортноеЛечение);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.УслугаРазмещения);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	// УслугиСпособРасчета
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСпособРасчета");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.СанаторноКурортноеЛечение);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.УслугаРазмещения);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	// УслугиСуммаНалога
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСуммаНалога");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.СанаторноКурортноеЛечение);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.УслугаРазмещения);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);

	// УслугиИдентификаторРодительскойСтроки
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиИдентификаторРодительскойСтроки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиИдентификаторРодительскойСтроки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.ИдентификаторРодительскойСтроки", ВидСравненияКомпоновкиДанных.НеЗаполнено, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "<...>");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиИдентификаторРодительскойСтроки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.УслугаРазмещения", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.ИдентификаторРодительскойСтроки", ВидСравненияКомпоновкиДанных.Заполнено, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.Услуги.ПредставлениеИДРодСтроки"));
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	УстановитьСостояниеДокумента();
	
	ТекущаяДатаДокумента 	= Объект.Дата;
			
	УстановитьФункциональныеОпцииФормы();
			
	ЗаполнитьДобавленныеКолонкиТаблиц();

	УправлениеФормой(ЭтотОбъект);
	
	УстановитьРеквизитыФормыТуристическийНалог();	
	ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Услуги");
	ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Услуги");

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект   = Форма.Объект; 
   	СформироватьНадписьЦеныИВалюта(Форма);
	ОбновитьИтоги(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
		
	ПлательщикНДС	= УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата);
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьПоДокументуНаСервере()
				
	Документы.НачислениеТуристическогоНалога.ЗаполнитьПоДокументу(Объект);
	
    СтавкаГостиницы = СтавкаТурНалога(Объект.Гостиница, Объект.Дата);

	УстановитьВидимостьГостиницы(); 

	ЗаполнитьДобавленныеКолонкиТаблиц();
		
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()
	
	ДанныеОбъекта = Новый Структура;
	ДанныеОбъекта.Вставить("Организация", Объект.Организация);
	ДанныеОбъекта.Вставить("Дата", Объект.Дата);
	
	Номенклатуры = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Услуги, "Номенклатура", Истина);
	
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Номенклатуры, "УслугаРазмещения");

	Для Каждого СтрокаТаблицы Из Объект.Услуги Цикл
		СведенияОНоменклатуре = РеквизитыНоменклатуры[СтрокаТаблицы.Номенклатура];
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, Объект.СуммаВключаетНДС, СведенияОНоменклатуре);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, СуммаВключаетНДС, СведенияОНоменклатуре)

	СтрокаТаблицы.Всего            = СтрокаТаблицы.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	Если СведенияОНоменклатуре <> Неопределено Тогда
		СтрокаТаблицы.УслугаРазмещения = СведенияОНоменклатуре.УслугаРазмещения;
	Иначе
		СтрокаТаблицы.УслугаРазмещения = Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)

	Объект = Форма.Объект;

	// Считаем по тем строкам, в которых ненулевое количество, т.е. их не отменили.
	ИтогВсего    = 0;
	ИтогВсегоНДС = 0;
	ИтогТурНалог = 0;

	Для каждого СтрокаТаблицы Из Объект.Услуги Цикл
		Если СтрокаТаблицы.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		ИтогВсего    = ИтогВсего    + СтрокаТаблицы.Всего;
		ИтогВсегоНДС = ИтогВсегоНДС + СтрокаТаблицы.СуммаНДС;
		ИтогТурНалог = ИтогТурНалог + СтрокаТаблицы.СуммаНалога;
	КонецЦикла;

	Форма.ИтогиВсего    = ИтогВсего;
	Форма.ИтогиВсегоНДС = ИтогВсегоНДС;
	Форма.ИтогиТурНалог = ИтогТурНалог;

КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииСервер()

	УстановитьФункциональныеОпцииФормы();
	УстановитьПараметрыВыбораСтавкиНДС();
	УстановитьСтавкуТурНалога(ЭтотОбъект);

	ЗаполнитьДобавленныеКолонкиТаблиц();	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Функция ОрганизацияПриИзмененииСервер()
	
	Если Не УчетнаяПолитика.ПлательщикТуристическогоНалога(Объект.Организация, Объект.Дата) Тогда 
		Объект.Организация = Неопределено; 
		ТекстСообщения = 
				НСтр("ru='Организация не является плательщиком туристического налога!'");
        ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли; 
	ТуристическийНалогСервер.УстановитьГостиницу(Объект.Гостиница, Объект.Организация);
	УстановитьФункциональныеОпцииФормы();
	УстановитьПараметрыВыбораСтавкиНДС(); 
	
	УстановитьСтавкуТурНалога(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);

КонецФункции

&НаСервере
Функция ДокументОснованиеПриИзмененииСервер()
	
	ОбъектЗначение = РеквизитФормыВЗначение("Объект");

	ОбъектЗначение.ЗаполнитьСвойстваШапки();

	ЗначениеВРеквизитФормы(ОбъектЗначение,"Объект");

	ЗаполнитьДобавленныеКолонкиТаблиц();

	УстановитьФункциональныеОпцииФормы();
			
	УправлениеФормой(ЭтотОбъект);
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецФункции

&НаСервере
Процедура УслугиНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта)
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		СтрокаТабличнойЧасти.Номенклатура, ДанныеОбъекта, Ложь, Истина);
	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Содержание	= СведенияОНоменклатуре.НаименованиеПолное;
	СтрокаТабличнойЧасти.Цена		= СведенияОНоменклатуре.Цена;
	СтрокаТабличнойЧасти.СтавкаНДС	= СведенияОНоменклатуре.СтавкаНДС;
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, 1);
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДанныеОбъекта.СуммаВключаетНДС);
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТабличнойЧасти, ДанныеОбъекта.СуммаВключаетНДС, СведенияОНоменклатуре);
	
	Если СтрокаТабличнойЧасти.УслугаРазмещения <> ДопУслугаРазмещения Тогда
		СтрокаТабличнойЧасти.ВидЛьготыПоТуристическомуНалогу = ЗначениеЛьготыПоУмолчанию;
		СтрокаТабличнойЧасти.СпособРасчета = СпособРасчетаПоУмолчанию; 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКоличествоЦена()

	СтрокаТаблицы = Элементы.Услуги.ТекущиеДанные;

	ЗначениеПустогоКоличества = 1;	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТаблицы, ЗначениеПустогоКоличества);
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, СтрокаТаблицы, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСумма()

	СтрокаТаблицы = Элементы.Услуги.ТекущиеДанные;
	
	Если СтрокаТаблицы.Количество = 0 Тогда
		СтрокаТаблицы.Цена = СтрокаТаблицы.Сумма;
	Иначе
		СтрокаТаблицы.Цена = СтрокаТаблицы.Сумма / СтрокаТаблицы.Количество;
	КонецЕсли;
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, СтрокаТаблицы, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСтавкаНДС()

	СтрокаТаблицы = Элементы.Услуги.ТекущиеДанные;

	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
		
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);

	ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, СтрокаТаблицы, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЛьготы()

	СтрокаТаблицы = Элементы.Услуги.ТекущиеДанные;
		
	ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, СтрокаТаблицы, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСуммыНДС()

	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма
		+ ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
		
	ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДаты()
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента);
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииСервер();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьТуристическийНалогТабЧасти()

	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл
		
		ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);

	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзмененииНаКлиенте()

	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;	
		
	ДокументОснованиеПриИзмененииСервер();

	Если Объект.Услуги.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Заполнить документ по основанию?'");
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьДокументПоОснованиюЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПоДокументуНаСервере();
		ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Услуги");
		ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Услуги");
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьДокументПоОснованиюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоДокументуНаСервере();
	КонецЕсли;
	ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Услуги");
	ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Услуги"); 
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураПриИзмененииНаКлиенте()

	ДанныеСтрокиТаблицы = Элементы.Услуги.ТекущиеДанные;
	
	ДанныеСтроки = Новый Структура("Номенклатура, Содержание, Количество, УслугаРазмещения, СпособРасчета, ВидЛьготыПоТуристическомуНалогу,
		|Цена, Сумма, СтавкаНДС, СуммаНДС, Всего");
	
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ДанныеСтрокиТаблицы);

	ДанныеОбъекта = Новый Структура("Ссылка, Дата, ВидОперации, Организация,
		|СуммаВключаетНДС, ДоговорКонтрагента");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);

	УслугиНоменклатураПриИзмененииНаСервере(ДанныеСтроки, ДанныеОбъекта);
	
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ДанныеСтроки);
		
	ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, ДанныеСтрокиТаблицы, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);
	
	Если ЗначениеЗаполнено(ДанныеСтрокиТаблицы.ИдентификаторРодительскойСтроки)
		И ДанныеСтрокиТаблицы.УслугаРазмещения <> ДопУслугаРазмещения Тогда
		ДанныеСтрокиТаблицы.ИдентификаторРодительскойСтроки = Неопределено;
	ИначеЕсли ДанныеСтрокиТаблицы.УслугаРазмещения = ДопУслугаРазмещения Тогда
		ТуристическийНалогКлиентСервер.ПодобратьСвободнуюСтрокуСУслугойРазмещения(ЭтотОбъект, "Услуги");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеСтрокиТаблицы.ИдентификаторСтроки) Тогда  
		ТуристическийНалогКлиентСервер.УдалитьСтарыеДанныеИдентификаторРодительскойСтроки(
			ЭтотОбъект, "Услуги", ДанныеСтрокиТаблицы.ИдентификаторСтроки);
	КонецЕсли;
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораСтавкиНДС()
	
	УчетНДС.УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "УслугиСтавкаНДС");
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюты()
	
	// 1. Формируем структуру параметров для заполнения формы "Цены и Валюта".
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СуммаВключаетНДС",     Объект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("ДокументБезНДС",       Объект.ДокументБезНДС);
	СтруктураПараметров.Вставить("Контрагент",           Объект.Контрагент);
	СтруктураПараметров.Вставить("Договор",              Объект.ДоговорКонтрагента);
	СтруктураПараметров.Вставить("Организация",          Объект.Организация);
	СтруктураПараметров.Вставить("ДатаДокумента",        Объект.Дата);
	СтруктураПараметров.Вставить("ТолькоПросмотр",       ТолькоПросмотр);
		
	// 2. Открываем форму "Цены и Валюта".
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтруктураПараметровКоманды", СтруктураПараметров);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение", 
		ЭтотОбъект, ДополнительныеПараметры);
	

	СписокКоманд = Новый СписокЗначений;
		
	Если Не ПлательщикНДС Тогда
		СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.ДокументБезНДС"));
	КонецЕсли;
	СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДССверху"));
	СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме")); 
	
	Если НЕ ТолькоПросмотр Тогда
		ПоказатьВыборИзМеню(ОповещениеОЗакрытии, СписокКоманд, Элементы.ЦеныИВалюта);
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.Свойство("СтруктураПараметровКоманды") Тогда
		
		СтруктураЦеныИВалюта = ДополнительныеПараметры.СтруктураПараметровКоманды;
		
		СуммаВключаетНДСДоИзменения = СтруктураЦеныИВалюта.СуммаВключаетНДС;
				
		Если РезультатЗакрытия = Неопределено Тогда 
			Возврат;
		ИначеЕсли РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.ДокументБезНДС") Тогда
			СтруктураЦеныИВалюта.ДокументБезНДС        = Истина;
		ИначеЕсли РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме") Тогда
			СтруктураЦеныИВалюта.ДокументБезНДС        = Ложь;
			СтруктураЦеныИВалюта.СуммаВключаетНДС      = Истина;
		Иначе
			СтруктураЦеныИВалюта.ДокументБезНДС   = Ложь;
			СтруктураЦеныИВалюта.СуммаВключаетНДС = Ложь;
		КонецЕсли;
		
		СтруктураЦеныИВалюта.Вставить("ПерезаполнитьЦены",    Ложь);
		СтруктураЦеныИВалюта.Вставить("ПересчитатьЦены",      Ложь);
		СтруктураЦеныИВалюта.Вставить("ПересчитатьНДС",
			СуммаВключаетНДСДоИзменения <> СтруктураЦеныИВалюта.СуммаВключаетНДС
			ИЛИ Объект.ДокументБезНДС <> СтруктураЦеныИВалюта.ДокументБезНДС);
		СтруктураЦеныИВалюта.Вставить("БылиВнесеныИзменения",
			СуммаВключаетНДСДоИзменения <> СтруктураЦеныИВалюта.СуммаВключаетНДС
			ИЛИ Объект.ДокументБезНДС <> СтруктураЦеныИВалюта.ДокументБезНДС);
	Иначе
		СтруктураЦеныИВалюта = РезультатЗакрытия;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураЦеныИВалюта) = Тип("Структура") И СтруктураЦеныИВалюта.БылиВнесеныИзменения Тогда

		ЗаполнитьЗначенияСвойств(Объект, СтруктураЦеныИВалюта);
		
		Модифицированность = Истина;

		ПриИзмененииЦеныИВалюты();
			
		ОбновитьОтображениеДанных();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦеныИВалюты()
	
	Для Каждого СтрокаТаблицы Из Объект.Услуги Цикл
		
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
			
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
		
		ТуристическийНалогКлиентСервер.РассчитатьСуммуНалога(Объект.Услуги, СтрокаТаблицы, Объект.СуммаВключаетНДС, ДопУслугаРазмещения);
				
	КонецЦикла;
	
	ОбновитьИтоги(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзмененияПоКнопкеЦеныИВалюты();

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьНадписьЦеныИВалюта(Форма) 
	
	Объект = Форма.Объект;
	СтруктураНадписи = Новый Структура("СуммаВключаетНДС, ДокументБезНДС", Объект.СуммаВключаетНДС, Объект.ДокументБезНДС);
	
	Форма.ЦеныИВалюта = ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтавкуТурНалога(Форма)
	
	Объект = Форма.Объект;
	
	СтавкиПоГостиницам = Новый Соответствие(); // Ключ - Гостиница, Значение - Ставка
	СтавкаГостиницы = 0;
	
	Если ЗначениеЗаполнено(Объект.Гостиница) Тогда
		СтавкаГостиницы = СтавкаТурНалога(Объект.Гостиница, Объект.Дата);
		Форма.СтавкаГостиницы = СтавкаГостиницы;
		СтавкиПоГостиницам.Вставить(Объект.Гостиница, СтавкаГостиницы);
	КонецЕсли;
	
	Для Каждого СтрокаУслуг Из Объект.Услуги Цикл 
		Если Не Форма.ГостиницаВШапке Тогда
			Если ЗначениеЗаполнено(СтрокаУслуг.Гостиница) Тогда
				СтавкаГостиницы = СтавкиПоГостиницам[СтрокаУслуг.Гостиница];
				Если СтавкаГостиницы = Неопределено Тогда
					СтавкаГостиницы = СтавкаТурНалога(СтрокаУслуг.Гостиница, Объект.Дата);
					СтавкиПоГостиницам.Вставить(СтрокаУслуг.Гостиница, СтавкаГостиницы);
				КонецЕсли;
			Иначе
				СтавкаГостиницы = 0;
			КонецЕсли;
		КонецЕсли;
		СтрокаУслуг.Ставка = СтавкаГостиницы;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтавкаТурНалога(Знач Гостиница, Знач ДатаПолучения)
		
	Возврат Справочники.Гостиницы.СтавкаНалогаНаДату(Гостиница, ДатаПолучения);  	
	
КонецФункции

&НаСервере
Процедура УстановитьПараметрыВыбораНоменклатуры() 
			
	УслугиРазмещения = УслугиРазмещения();
	
	НовыйМассивПараметров = Новый Массив();
	НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.УслугаРазмещения", Новый ФиксированныйМассив(УслугиРазмещения)));
	Элементы.УслугиНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассивПараметров);
		
КонецПроцедуры

&НаСервере
Функция УслугиРазмещения()
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.ВидыУслугРазмещения.УслугаРазмещения);
	Результат.Добавить(Перечисления.ВидыУслугРазмещения.СанаторноКурортноеЛечение);
    Результат.Добавить(Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд);

	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьГостиницы() 
			
	ГостиницаВШапке = 
		Документы.НачислениеТуристическогоНалога.ГостиницаВШапке(Объект.Услуги, Объект.Гостиница, Объект.ДокументОснование);
	Элементы.Гостиница.Видимость = ГостиницаВШапке;
	Элементы.УслугиГостиница.Видимость = Не ГостиницаВШапке;
		
	// Для документа Отчет о розничных продажах отключим видимость полей контрагент и договор.
	ИсправляемыйДокумент = Документы.НачислениеТуристическогоНалога.ПолучитьИсправляемыйДокумент(Объект.ДокументОснование);
	ЭтоОтчетОРозничнойПродажи = ТипЗнч(ИсправляемыйДокумент) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах");
	
	Элементы.Контрагент.Видимость = НЕ ЭтоОтчетОРозничнойПродажи;
    Элементы.ДоговорКонтрагента.Видимость = НЕ ЭтоОтчетОРозничнойПродажи;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыФормыТуристическийНалог()
	
	ЗначениеЛьготыПоУмолчанию = Перечисления.ВидыЛьготПоТуристическомуНалогу.НеПрименяется;
	СпособРасчетаПоУмолчанию = Перечисления.СпособыРасчетаТуристическогоНалога.ПоСтавке;
	ДопУслугаРазмещения = Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд;
	ГиперссылкаЦвет = ЦветаСтиля.ГиперссылкаЦвет;
 	ЦветНеактивнойСтроки = ЦветаСтиля.ЦветНеАктивнойСтроки;  
	ГиперссылкаШрифт = Новый Шрифт(, , , ,Истина);

КонецПроцедуры

&НаСервере
Функция АдресТаблицыОсновныхУслугРазмещения()

	Возврат ПоместитьВоВременноеХранилище(ТаблицаОсновныхУслугРазмещения.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбновитьПредставлениеИДРодСтрок()
	ТуристическийНалогКлиентСервер.ЗаполнитьСписокВыбораИдентификаторРодительскойСтроки(ЭтотОбъект, "Услуги");
	ТуристическийНалогКлиентСервер.ОбновитьПредставлениеИДРодСтрок(ЭтотОбъект, "Услуги"); 
	НомерАктивнойСтроки = Элементы.Услуги.ТекущиеДанные.НомерСтроки;
КонецПроцедуры

#КонецОбласти
