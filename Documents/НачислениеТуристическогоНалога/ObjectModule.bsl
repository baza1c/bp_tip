#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьСвойстваШапки() Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, ДокументОснование);

	ТуристическийНалогСервер.УстановитьГостиницу(Гостиница, Организация);
		
КонецПроцедуры

Процедура ЗаполнитьПоДокументуОснованию(Основание) Экспорт
	
	Если ЗначениеЗаполнено(Основание.Организация) И 
		Не УчетнаяПолитика.ПлательщикТуристическогоНалога(Основание.Организация, Основание.Дата) Тогда

		ТекстСообщения = 
			НСтр("ru='Организация не является плательщиком туристического налога!'");
		ВызватьИсключение ТекстСообщения;

	КонецЕсли;

	ДокументОснование = Основание;
	ЗаполнитьСвойстваШапки();	
	Документы.НачислениеТуристическогоНалога.ЗаполнитьПоДокументу(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ЗаполнитьГостиницуПередЗаписью();
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах.
	// Суммируем только неотмененные строки, как при проведении.
	СуммаДокумента = 0;
	Для каждого СтрокаТаблицы Из Услуги Цикл

		Если СтрокаТаблицы.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если СуммаВключаетНДС Тогда
			СуммаДокумента = СуммаДокумента + СтрокаТаблицы.Сумма;
		Иначе
			СуммаДокумента = СуммаДокумента + СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС;
		КонецЕсли;
		
	КонецЦикла;
	
	ИменаТабличныхЧастей = Новый Массив;
	ИменаТабличныхЧастей.Добавить("Услуги");

	ОбщегоНазначенияБПКлиентСервер.ЗаполнитьИдентификаторыСтрок(ЭтотОбъект, ИменаТабличныхЧастей);

КонецПроцедуры 

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	Иначе
		СуммаВключаетНДС = Истина;
	КонецЕсли;

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если Не УчетнаяПолитика.ПлательщикТуристическогоНалога(Организация, Дата) Тогда 
		Организация = Неопределено; 
		ТекстСообщения = 
				НСтр("ru='Организация не является плательщиком туристического налога!'");
        ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли; 

	ТуристическийНалогСервер.УстановитьГостиницу(Гостиница, Организация);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)  
	
	Если Не УчетнаяПолитика.ПлательщикТуристическогоНалога(Организация, Дата) Тогда 
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,"Корректность", НСтр("ru = 'Организация'"), , , 
			НСтр("ru = 'Организация не является плательщиком туристического налога!'"));
		Поле = "Организация";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
	КонецЕсли;

	ГостиницаВШапке = Документы.НачислениеТуристическогоНалога.ГостиницаВШапке(Услуги, Гостиница, ДокументОснование);
	ВестиУчетПоДоговорам = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ИсправляемыйДокумент = Документы.НачислениеТуристическогоНалога.ПолучитьИсправляемыйДокумент(ДокументОснование);

	Номенклатуры = ОбщегоНазначения.ВыгрузитьКолонку(Услуги, "Номенклатура", Истина);
	
	ОсновныеУслугиРазмещения = ТуристическийНалогСервер.МассивУслугРазмещения();
	УслугиРазмещенияНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Номенклатуры, "УслугаРазмещения");
	ПараметрыПроверки = ТуристическийНалогСервер.НовыйПараметрыПроверкиЛьготы();
	ПараметрыПроверки.МассивУслуг = ОсновныеУслугиРазмещения; 
	ПараметрыПроверки.ИмяТабличнойЧасти = "Услуги";

	Для Каждого СтрокаУслуги Из Услуги Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаУслуги.Номенклатура) Тогда
			// Сообщение про незаполненность выдаст платформа.
			Продолжить;
		КонецЕсли;
		
		Префикс = "Услуги[%1].";
		Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Префикс, Формат(СтрокаУслуги.НомерСтроки - 1, "ЧН=0; ЧГ="));
			
		УслугаРазмещения = УслугиРазмещенияНоменклатуры.Получить(СтрокаУслуги.Номенклатура);
		Если Не ЗначениеЗаполнено(УслугаРазмещения) Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность",
				НСтр("ru = 'Номенклатура'"),
				СтрокаУслуги.НомерСтроки,
				"Услуги",
				НСтр("ru = 'В карточке номенклатуры не установлено, что она является услугой размещения'"));

			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Услуги", СтрокаУслуги.НомерСтроки, "Номенклатура");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
		Если ОсновныеУслугиРазмещения.Найти(УслугаРазмещения) <> Неопределено 
			И Не ЗначениеЗаполнено(СтрокаУслуги.ВидЛьготыПоТуристическомуНалогу) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Льгота по тур.налогу'"),
				СтрокаУслуги.НомерСтроки, "Услуги");
			Поле = Префикс + "ВидЛьготыПоТуристическомуНалогу";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если ОсновныеУслугиРазмещения.Найти(УслугаРазмещения) <> Неопределено 
			И Не ЗначениеЗаполнено(СтрокаУслуги.СпособРасчета) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Способ расчета'"),
				СтрокаУслуги.НомерСтроки, "Услуги");
			Поле = Префикс + "СпособРасчета";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		Если УслугаРазмещения = Перечисления.ВидыУслугРазмещения.РаннийЗаездПозднийВыезд 
			И Не ЗначениеЗаполнено(СтрокаУслуги.ИдентификаторРодительскойСтроки) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Связано с'"),
				СтрокаУслуги.НомерСтроки, "Услуги");
			Поле = Префикс + "ИдентификаторРодительскойСтроки";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
		
		ПараметрыПроверки.УслугаРазмещения = УслугаРазмещения;
		ПараметрыПроверки.СтрокаУслуги = СтрокаУслуги;

		ТуристическийНалогСервер.ПроверитьКорректностьЗаполненияВидаЛьготы(ЭтотОбъект, ПараметрыПроверки, Отказ);
		
	КонецЦикла;
		
	МассивНепроверяемыхРеквизитов = Новый Массив;

	Если ГостиницаВШапке Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Услуги.Гостиница");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Гостиница");
	КонецЕсли;
	
	// Для отчета о розничных продажах не указывается контрагент и договор.
	Если ТипЗнч(ИсправляемыйДокумент) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	// Если учет по договорам отключен, то не нужно проверять заполнение договора.
	Если НЕ ВестиУчетПоДоговорам И МассивНепроверяемыхРеквизитов.Найти("ДоговорКонтрагента") = Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.НачислениеТуристическогоНалога.ПодготовитьПараметрыПроведения(Ссылка, Отказ); 
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	Документы.НачислениеТуристическогоНалога.СформироватьДвиженияТуристическийНалог(
		ПараметрыПроведения.ТуристическийНалогКНачислению, ПараметрыПроведения.СторноТуристическийНалог, Движения, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
				
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьГостиницуПередЗаписью()
	
	ТаблицаУслуг = Услуги.Выгрузить(, "Гостиница");
	ТаблицаУслуг.Свернуть("Гостиница");
	Если ТаблицаУслуг.Количество() = 1 И ЗначениеЗаполнено(ТаблицаУслуг[0].Гостиница) Тогда
		Гостиница = ТаблицаУслуг[0].Гостиница;
	ИначеЕсли ЗначениеЗаполнено(Гостиница) Тогда
		Для Каждого СтрокаТЧ Из Услуги Цикл
			СтрокаТЧ.Гостиница = Гостиница;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
