#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);

	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.НачалоСтадииРазработокНМА.ПодготовитьПараметрыПроведения(Ссылка, Отказ);

	УчетНМА.ПроверитьНачатыеРазработки(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ОбъектыНИОКР, Отказ);

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	УчетНМА.СформироватьДвиженияНачатыеРазработки(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ОбъектыНИОКР, Движения);

	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(
		ЭтотОбъект,
		 "СоздаваемыеОбъекты",
		 Новый Структура("НематериальныйАктив"),
		 Отказ);
КонецПроцедуры

#КонецОбласти

#КонецЕсли