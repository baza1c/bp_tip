
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область СтандартныеПодсистемы_ВерсионированиеОбъектов

//@skip-check module-empty-method
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы_УправлениеДоступом

// Логика ограничения доступа к данным.
// 
// Параметры:
//  Ограничение - См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы_УправлениеСвойствами

// Получает описание предопределенных наборов свойств.
//
// Параметры:
//  Наборы - ДеревоЗначений - с колонками:
//     * Имя           - Строка - Имя набора свойств. Формируется из полного имени объекта
//                       метаданных заменой символа "." на "_".
//                       Например, "Документ_ЗаказПокупателя".
//     * Идентификатор - УникальныйИдентификатор - Идентификатор ссылки предопределенного элемента.
//     * Используется  - Неопределено, Булево - Признак того, что набор свойств используется.
//                       Например, можно использовать для скрытия набора по функциональным опциям.
//                       Значение по умолчанию - Неопределено, соответствует значению Истина.
//     * ЭтоГруппа     - Булево - Истина, если набор свойств является группой.
//
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Документ_НачалоСтадииРазработокНМА";
	Набор.Идентификатор = Новый УникальныйИдентификатор("e8237f59-d8c9-45d8-8e4f-c9216c1f571c");
	Набор.Используется = ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА");
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы_Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Начало стадии разработок НМА""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// Параметры:
//   МассивОбъектов        - Массив из Произвольный         - ссылки на объекты, которые нужно распечатать
//   ПараметрыПечати       - Структура                      - дополнительные настройки печати
//   КоллекцияПечатныхФорм - ТаблицаЗначений                - сформированные табличные документы (выходной параметр)
//   ОбъектыПечати         - СписокЗначений из Произвольный - имя области, в которой был выведен объект (выходной параметр)
//   ПараметрыВывода       - Структура                      - дополнительные параметры сформированных табличных документов (выходной параметр)
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

// Возвращает дополнительные данные для печати реестра документов.
// 
// Возвращаемое значение:
//  Структура - Дополнительные реквизиты для реестра
//
Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	НомераТаблиц = Новый Структура;
	
	Запрос.Текст =
		ТекстЗапросаРеквизитыДокумента(НомераТаблиц) +
		ТекстЗапросаОбъектыНИОКР(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

// Возвращает список незавершенных исследования для заполнения в документе.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Дата - Дата
//  Ссылка - ДокументСсылка.НачалоСтадииРазработокНМА
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.НематериальныеАктивы
//
Функция СписокНезавершенныхИсследований(Организация, Дата, Ссылка) Экспорт
	
	// Предполагается, что исследования ведутся из месяца в месяц, но допускаем, что возможен перерыв.
	МаксимальныйПерерывИсследований = МаксимальныйПерерывИсследований();
	Запрос = Новый Запрос;
	
	Настройки = Справочники.НастройкиУчетаЗатрат.БазовыеНастройки(Дата, Организация);
	СчетаНИОКР = Справочники.НастройкиУчетаЗатрат.СчетаРоли(Перечисления.РолиСчетовЗатрат.КалькуляцияСтоимостиНИОКР, Настройки);
	Запрос.УстановитьПараметр("СчетаНИОКР", СчетаНИОКР);
	Запрос.УстановитьПараметр("СубконтоНИОКР", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыНаНИОКР);
	
	Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(НачалоМесяца(Дата), - МаксимальныйПерерывИсследований));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйДтКт.СубконтоДт1 КАК НематериальныйАктив
	|ПОМЕСТИТЬ ОборотыПоСчету
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			СчетДт В (&СчетаНИОКР),
	|			&СубконтоНИОКР,
	|			,
	|			,
	|			Организация = &Организация
	|				И СубконтоДт1 <> ЗНАЧЕНИЕ(Справочник.НематериальныеАктивы.ПустаяСсылка)) КАК ХозрасчетныйДтКт
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОборотыПоСчету.НематериальныйАктив КАК НематериальныйАктив
	|ИЗ
	|	ОборотыПоСчету КАК ОборотыПоСчету
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыНачалаСтадииРазработокНИОКР КАК НачалоРазработок
	|		ПО (НачалоРазработок.Организация = &Организация)
	|			И ОборотыПоСчету.НематериальныйАктив = НачалоРазработок.НИОКР
	|			И (НачалоРазработок.Регистратор <> &Ссылка)
	|ГДЕ
	|	НачалоРазработок.НИОКР ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	НематериальныйАктив";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("НематериальныйАктив");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаОбъектыНИОКР(НомераТаблиц)

	НомераТаблиц.Вставить("ОбъектыНИОКР", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбъектыНИОКР.НематериальныйАктив КАК НематериальныйАктив,
	|	ОбъектыНИОКР.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.НачалоСтадииРазработокНМА.СоздаваемыеОбъекты КАК ОбъектыНИОКР
	|ГДЕ
	|	ОбъектыНИОКР.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.НачалоСтадииРазработокНМА КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция МаксимальныйПерерывИсследований()
	Возврат 3;
КонецФункции

#КонецОбласти

#КонецЕсли
