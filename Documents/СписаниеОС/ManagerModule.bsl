#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 13, 0);
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад.ТипСклада КАК ТипСклада,
	|	Реквизиты.ОсталисьМатериальныеЦенностиОтВыбытия КАК ОсталисьМатериальныеЦенностиОтВыбытия,
	|	Реквизиты.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Результат = Запрос.Выполнить();
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);
	ПрослеживаемостьРеквизиты = Результат.Выгрузить();
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	ПлательщикНДС                    = УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период);
	ПрименяетсяУСН                   = УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация, Реквизиты.Период);
	ПрименяетсяУСНДоходыМинусРасходы = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Реквизиты.Организация, Реквизиты.Период);
	ПлательщикНДФЛ                   = УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период);
	СпособОценкиТоваровВРознице      = УчетнаяПолитика.СпособОценкиТоваровВРознице(Реквизиты.Организация, Реквизиты.Период);
	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Реквизиты.Период);

	Запрос.УстановитьПараметр("ПлательщикНДС", ПлательщикНДС);
	Запрос.УстановитьПараметр("ПрименяетсяУСН", ПрименяетсяУСН);
	Запрос.УстановитьПараметр("ПлательщикНДФЛ", ПлательщикНДФЛ);
	
	Запрос.УстановитьПараметр("СинонимОС",	НСтр("ru='Основные средства'"));
	Запрос.УстановитьПараметр("СинонимЦенностиОтВыбытия",	НСтр("ru = 'Оставшиеся товары'"));
	
	Запрос.УстановитьПараметр("СпособОценкиТоваровВРознице",	СпособОценкиТоваровВРознице);
	Запрос.УстановитьПараметр("ТипСклада", Реквизиты.ТипСклада);
	
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	ОтражатьВРасходахУСН = (Реквизиты.Период >= '20130101') И ПрименяетсяУСНДоходыМинусРасходы
		И Не (Реквизиты.ТипСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка И СпособОценкиТоваровВРознице = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости);
	Запрос.УстановитьПараметр("ОтражатьВРасходахУСН", ОтражатьВРасходахУСН);
	
	Запрос.УстановитьПараметр("ВестиУчетПоВидамДеятельностиИП",	УчетнаяПолитика.ВестиУчетПоВидамДеятельностиИП(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ОсновнаяНоменклатурнаяГруппа",	УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("Счета10", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Материалы));
	Запрос.УстановитьПараметр("СодержаниеПроводокГрупповыеОС", 
		НСтр("ru='Списание ОС'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Запрос.УстановитьПараметр("МоментСписания",                 Новый МоментВремени(Реквизиты.Период, ДокументСсылка));
	Запрос.УстановитьПараметр("Организация",                    Реквизиты.Организация);
	
	НомераТаблиц = Новый Структура;

	Запрос.Текст =
		ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)
		+ ТекстЗапросаТаблицаОС(НомераТаблиц)
		+ ТекстЗапросаВыбытиеОС(НомераТаблиц)
		+ ТекстЗапросаСостоянияОС(НомераТаблиц)
		+ ТекстЗапросаСписаниеОстаточнойСтоимости(НомераТаблиц)
		+ ТекстЗапросаПроверкиПоОС(НомераТаблиц)
		+ ТекстЗапросаСписаниеОСиНМАИП(НомераТаблиц)
		+ ТекстЗапросаСписаниеГрупповыхОС(НомераТаблиц)
		+ ТекстЗапросаЗатратыНаРемонтОС(НомераТаблиц)
		+ ТекстЗапросаОприходованиеТоваров(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаОприходованиеТоваровНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаОприходованиеТоваровУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаКнигаУчетаДоходовИРасходовУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаОприходованиеТоваровИП(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовВПоследовательности(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, ВедетсяУчетПрослеживаемыхТоваров);

	Результат = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	ПараметрыПроведения.Вставить("ПрослеживаемостьРеквизиты", ПрослеживаемостьРеквизиты);
	
	Возврат ПараметрыПроведения;

КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт о списании ОС по произвольной форме
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОСписанииОС";
	КомандаПечати.Представление = НСтр("ru = 'Акт о списании основных средств'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 10;
	
	// Акт о списании ОС (ОС-4)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС4";
	КомандаПечати.Представление = НСтр("ru = 'Акт о списании ОС (ОС-4)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 20;
	
	// Акт о списании автотранспортных средств (ОС-4а)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС4а";
	КомандаПечати.Представление = НСтр("ru = 'Акт о списании автотранспортных средств (ОС-4а)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 30;
	
	// Накладная на оприходование товаров
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Накладная на оприходование материалов'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 40;
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ОсталисьМатериальныеЦенностиОтВыбытия", Истина);
	
	// Справка-расчет стоимости материалов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СправкаРасчетСтоимостиМатериалов";
	КомандаПечати.Представление = НСтр("ru = 'Справка-расчет ""Стоимость оставшихся материалов при списании ОС""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 50;
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ОсталисьМатериальныеЦенностиОтВыбытия", Истина);
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Списание ОС""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 100;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС4") Тогда

		ИмяМакета = "ОС4";
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОС4", НСтр("ru = 'ОС-4 (Акт о списании ОС)'"), 
			ПечатьОС4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета),, ИмяМакета);
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС4а") Тогда
		
		ИмяМакета = "ОС4а";
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОС4а",
			НСтр("ru = 'ОС-4а (Акт о списании автотранспортных средств)'"), 
			ПечатьОС4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета),, ИмяМакета);
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктОСписанииОС") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктОСписанииОС", 
			НСтр("ru = 'Акт о списании ОС'"),
			ПечатьАктОСписанииОС(МассивОбъектов, ОбъектыПечати), , "ОбщийМакет.ПФ_MXL_АктОСписанииОС");
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", 
			НСтр("ru = 'Накладная на оприходование материалов'"),
			ПечатьОприходованияМатериалов(МассивОбъектов, ОбъектыПечати), , "Документ.СписаниеОС.ПФ_MXL_Накладная");
			
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СправкаРасчетСтоимостиМатериалов") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СправкаРасчетСтоимостиМатериалов",
			НСтр("ru = 'Справка-расчет ""Стоимость оставшихся материалов при списании ОС""'"),
			РегистрыСведений.РасчетСтоимостиОставшихсяМатериаловПриСписанииОС.ПечатьСправкиРасчета(МассивОбъектов, ОбъектыПечати), ,
			"РегистрСведений.РасчетСтоимостиОставшихсяМатериаловПриСписанииОС.ПФ_MXL_РасчетСтоимостиОставшихсяМатериалов");
		
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	

КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "СобытиеОС");
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

Функция ПодготовитьДоходыИРасходыСУчетомТоваров(ТаблицаСтоимости, ОприходованиеТоваровТаблицаТовары) Экспорт
	
	ДоходыИРасходы = Новый Структура();
	ДоходыИРасходы.Вставить("РасходыОтЛиквидацииОС", Неопределено);
	ДоходыИРасходы.Вставить("ДоходыОтОприходованияТоваров", Неопределено);
	ДоходыИРасходы.Вставить("ОприходованиеТоваров", Неопределено);
	ДоходыИРасходы.Вставить("РасчетСтоимостиТоваров", Неопределено);
	
	Если Не ЗначениеЗаполнено(ОприходованиеТоваровТаблицаТовары) Тогда
		// Если нет оприходованных товаров, то и корректировать расходы не нужно.
		// Расходы будут равны балансовой стоимости.
		// Доходов в этом случае также не будет.
		ДоходыИРасходы.РасходыОтЛиквидацииОС = ТаблицаСтоимости.Скопировать();
		Возврат ДоходыИРасходы;
	КонецЕсли;
	
	РасходыОтЛиквидацииОС = УчетОС.НовыйТаблицаОстаточнойСтоимостиОС();
	ДоходыОтОприходованияТоваров = Документы.СписаниеОС.НовыйТаблицаДоходыОтОприходованияТоваров();
	РасчетСтоимостиТоваров = РегистрыСведений.РасчетСтоимостиОставшихсяМатериаловПриСписанииОС.НовыйТаблицаРасчетСтоимостиТоваров();
	ОприходованиеТоваров = УчетТоваров.НовыйТаблицаОприходованияТоваров();
	
	Для Каждого СтрокаСтоимости Из ТаблицаСтоимости Цикл
		
		СтрокаРасхода = РасходыОтЛиквидацииОС.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРасхода, СтрокаСтоимости);
		
		РыночнаяСтоимостьТоваров = 0;
		КоэффициентыСтоимостиТоваров = Новый Массив;
		
		ОтборСтрок = Новый Структура("ОсновноеСредство", СтрокаРасхода.ОсновноеСредство);
		СтрокиДляНомераСтрокиОС = ОприходованиеТоваровТаблицаТовары.НайтиСтроки(ОтборСтрок);
		Для Каждого СтрокаТовара Из СтрокиДляНомераСтрокиОС Цикл
			РыночнаяСтоимостьТоваров = РыночнаяСтоимостьТоваров + СтрокаТовара.Сумма;
			КоэффициентыСтоимостиТоваров.Добавить(СтрокаТовара.Сумма);
		КонецЦикла;
		
		// Стоимость оприходуемых товаров в бухгалтерском учете не может превышать
		// балансовую стоимость. В балансовую стоимость в том числе входят и затраты на демонтаж.
		// Они учтены в СтоимостьБУ таблицы стоимости ОС.
		БалансоваяСтоимость = СтрокаРасхода.СтоимостьБУ;
		СтоимостьТоваровБУ = Мин(БалансоваяСтоимость, РыночнаяСтоимостьТоваров);
		РаспределеннаяСтоимостьТоваровБУ = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
			СтоимостьТоваровБУ, КоэффициентыСтоимостиТоваров);
		
		ИндексСтроки = 0;
		Для Каждого СтрокаТовара Из СтрокиДляНомераСтрокиОС Цикл
			
			СтрокаОприходованияТоваров = ОприходованиеТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаОприходованияТоваров, СтрокаТовара, , "Сумма");
			Если ЗначениеЗаполнено(РаспределеннаяСтоимостьТоваровБУ) Тогда
				СтрокаОприходованияТоваров.Сумма = РаспределеннаяСтоимостьТоваровБУ[ИндексСтроки];
			КонецЕсли;
			
			СтрокаРасчетСтоимостиТоваров = РасчетСтоимостиТоваров.Добавить();
			СтрокаРасчетСтоимостиТоваров.ОсновноеСредство = СтрокаРасхода.ОсновноеСредство;
			СтрокаРасчетСтоимостиТоваров.НомерСтрокиМатериала = ИндексСтроки + 1;
			СтрокаРасчетСтоимостиТоваров.Количество = СтрокаТовара.Количество;
			СтрокаРасчетСтоимостиТоваров.Стоимость = СтрокаТовара.Сумма;
			СтрокаРасчетСтоимостиТоваров.СтоимостьБухгалтерскийУчет = СтрокаОприходованияТоваров.Сумма;
			Если РыночнаяСтоимостьТоваров <> 0 Тогда
				СтрокаРасчетСтоимостиТоваров.ДоляСтоимости = СтрокаТовара.Сумма / РыночнаяСтоимостьТоваров;
			КонецЕсли;
			
			СтрокаРасчетСтоимостиТоваров.БалансоваяСтоимостьОС = БалансоваяСтоимость - СтрокаСтоимости.ЗатратыНаСписаниеОС;
			СтрокаРасчетСтоимостиТоваров.ЗатратыНаСписаниеОС = СтрокаСтоимости.ЗатратыНаСписаниеОС;
			СтрокаРасчетСтоимостиТоваров.Номенклатура = СтрокаТовара.Номенклатура;
			
			ИндексСтроки = ИндексСтроки + 1;
			
		КонецЦикла;
		
		// Уменьшим расходы от ликвидации ОС в бухгалтерском учете на стоимость товаров в бух.учете.
		// В налоговом учете расходы должны соответствовать балансовой стоимости (с учетом затрат на демонтаж).
		СтрокаРасхода.СтоимостьБУ = СтрокаРасхода.СтоимостьБУ - СтоимостьТоваровБУ;
		СтрокаРасхода.СтоимостьВР = СтрокаРасхода.СтоимостьВР - СтоимостьТоваровБУ;
		
		СтрокаДоходы = ДоходыОтОприходованияТоваров.Добавить();
		СтрокаДоходы.ОсновноеСредство = СтрокаСтоимости.ОсновноеСредство;
		СтрокаДоходы.СуммаНУ = РыночнаяСтоимостьТоваров;
		
	КонецЦикла;
	
	ДоходыИРасходы.РасходыОтЛиквидацииОС = РасходыОтЛиквидацииОС;
	ДоходыИРасходы.ДоходыОтОприходованияТоваров = ДоходыОтОприходованияТоваров;
	ДоходыИРасходы.ОприходованиеТоваров = ОприходованиеТоваров;
	ДоходыИРасходы.РасчетСтоимостиТоваров = РасчетСтоимостиТоваров;
	Возврат ДоходыИРасходы;
	
КонецФункции

Функция НовыйТаблицаДоходыОтОприходованияТоваров() Экспорт
	
	ДоходыОтОприходованияТоваров = Новый ТаблицаЗначений;
	
	ДоходыОтОприходованияТоваров.Колонки.Добавить("ОсновноеСредство", Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ДоходыОтОприходованияТоваров.Колонки.Добавить("СуммаНУ", Метаданные.РегистрыБухгалтерии.Хозрасчетный.Ресурсы.СуммаНУ.Тип);
	
	Возврат ДоходыОтОприходованияТоваров;
	
КонецФункции

Процедура СформироватьДвиженияОтраженияДоходовОтОприходованияТоваров(ДоходыОтОприходованияТоваров, ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ДоходыОтОприходованияТоваров) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыОтраженияДоходовОтОприходованияТоваров(ДоходыОтОприходованияТоваров, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	// Доходы от оприходования товаров отражаются только в налоговом учете.
	// Поэтому если организация не платит налог на прибыль - то делать проводку не нужно.
	Если Не УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Параметры.ДоходыОтОприходованияТоваров Цикл
		
		Проводка = Движения.Хозрасчетный.Добавить();
		
		Проводка.Период      = Реквизиты.Период;
		Проводка.Организация = Реквизиты.Организация;
		Проводка.Содержание  = НСтр("ru = 'Доходы от поступивших ценностей при списании ОС'");
		
		Проводка.СчетДт = ПланыСчетов.Хозрасчетный.ВыбытиеОС;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "ОсновныеСредства", СтрокаТаблицы.ОсновноеСредство);
		
		СвойстваСчетаДт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт);
		Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеДт = Реквизиты.Подразделение;
		КонецЕсли;
		
		Проводка.СчетКт = Реквизиты.СчетУчетаДоходов;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 1, Реквизиты.СубконтоДоходов);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 2, СтрокаТаблицы.ОсновноеСредство);
		
		СвойстваСчетаКт = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт);
		Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
			Проводка.ПодразделениеКт = Реквизиты.Подразделение;
		КонецЕсли;
		
		Проводка.Сумма = 0;
		
		НалоговыйУчет.ЗаполнитьНалоговыеСуммыПроводки(
			СтрокаТаблицы.СуммаНУ, СтрокаТаблицы.СуммаНУ,
			0, 0,
			- СтрокаТаблицы.СуммаНУ, - СтрокаТаблицы.СуммаНУ,
			Проводка, Истина);
		
	КонецЦикла;
	
	Движения.Хозрасчетный.Записывать = Истина;
	
КонецПроцедуры

Процедура ДобавитьКолонкуСодержание(ТаблицаЗначений) Экспорт
	
	Если ТаблицаЗначений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаЗначений.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	КонецЕсли;

	Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		Содержание = НСтр("ru = 'Оприходование %1 при списании ОС'");
		Содержание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Содержание, 
			БухгалтерскийУчетПовтИсп.НазваниеОбъектаПоСчетуУчета(СтрокаТаблицы.СчетУчета));
		СтрокаТаблицы.Содержание = Содержание;
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьКолонкуСодержаниеУСН(ТаблицаЗначений) Экспорт
	
	Если ТаблицаЗначений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаЗначений.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(300));
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		
		СтрокаТаблицы.Содержание = НСтр("ru = 'Оприходованы материалы при списании ОС.'");
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаТаблицаОС(НомераТаблиц)
	
	НомераТаблиц.Вставить("ОсновныеСредства", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ГрупповыеОС", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Регистратор,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.ОсновноеСредство.ЕдиницаУчета КАК ЕдиницаУчета,
	|	ТаблицаОС.КлючСтроки КАК КлючСтроки
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.ОсновноеСредство.ЕдиницаУчета <> ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Регистратор,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.ОсновноеСредство.ЕдиницаУчета КАК ЕдиницаУчета,
	|	ТаблицаОС.Количество КАК Количество,
	|	ТаблицаОС.Ссылка.СчетСписания КАК СчетСписания,
	|	ТаблицаОС.Ссылка.Субконто КАК СубконтоСписания1,
	|	ТаблицаОС.КлючСтроки КАК КлючСтроки
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.ОсновноеСредство.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВыбытиеОС(НомераТаблиц)

	НомераТаблиц.Вставить("ВыбытиеОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка                   КАК Регистратор,
	|	Реквизиты.Дата                     КАК Период,
	|	Реквизиты.Номер                    КАК Номер,
	|	Реквизиты.Организация              КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	""ОС""                             КАК ИмяСписка,
	|	Реквизиты.СобытиеОС                КАК СобытиеОС,
	|	ИСТИНА                             КАК СписыватьТолькоЛинейныйНУ,
	|	""Списание ОС: "" + Реквизиты.ПричинаСписания.Наименование КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаЗатратыНаРемонтОС(НомераТаблиц)
	
	НомераТаблиц.Вставить("ЗатратыНаРемонтОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗатратыНаРемонтОСТаблица", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка                   КАК Регистратор,
	|	Реквизиты.Дата                     КАК Период,
	|	Реквизиты.Номер                    КАК Номер,
	|	Реквизиты.Организация              КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	""ОС""                             КАК ИмяСписка,
	|	Реквизиты.СобытиеОС                КАК СобытиеОС,
	|	""Списание затрат на ремонт ОС""   КАК Содержание
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Регистратор,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.Ссылка.СчетСписания КАК СчетВыбытия,
	|	ТаблицаОС.Ссылка.Субконто КАК Субконто1
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.ОсновноеСредство.ЕдиницаУчета <> ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСостоянияОС(НомераТаблиц)

	НомераТаблиц.Вставить("СостоянияОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Номер,
	|	Реквизиты.Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК СостояниеОС,
	|	""ОС"" КАК ИмяСписка
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСписаниеОстаточнойСтоимости(НомераТаблиц)

	НомераТаблиц.Вставить("СписаниеОстаточнойСтоимости", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеОстаточнойСтоимостиТаблица", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеОстаточнойСтоимостиТаблицаГрупповыхОС", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Номер,
	|	Реквизиты.Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	""ОС"" КАК ИмяСписка,
	|	Реквизиты.СобытиеОС,
	|	""Списание ОС: "" + Реквизиты.ПричинаСписания.Наименование КАК Содержание,
	|	ИСТИНА КАК СписыватьТолькоЛинейныйНУ
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Ссылка              КАК Регистратор,
	|	ТаблицаОС.НомерСтроки         КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство    КАК ОсновноеСредство,
	|	ТаблицаОС.Количество          КАК Количество,
	|	ЛОЖЬ                          КАК Арендованное,
	|	ТаблицаОС.Ссылка.Субконто     КАК Субконто,
	|	ТаблицаОС.Ссылка.СчетСписания КАК СчетСписания,
	|	ТаблицаОС.Ссылка.Субконто     КАК СубконтоНУ,
	|	ТаблицаОС.Ссылка.СчетСписания КАК СчетСписанияНУ
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.ОсновноеСредство.ЕдиницаУчета <> ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Ссылка              КАК Регистратор,
	|	ТаблицаОС.НомерСтроки         КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство    КАК ОсновноеСредство,
	|	ТаблицаОС.Количество          КАК Количество,
	|	ЛОЖЬ                          КАК Арендованное,
	|	ТаблицаОС.Ссылка.Субконто     КАК Субконто,
	|	ТаблицаОС.Ссылка.СчетСписания КАК СчетСписания,
	|	ТаблицаОС.Ссылка.Субконто     КАК СубконтоНУ,
	|	ТаблицаОС.Ссылка.СчетСписания КАК СчетСписанияНУ
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.ОсновноеСредство.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПроверкиПоОС(НомераТаблиц)

	НомераТаблиц.Вставить("ПроверкиПоОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	""ОС"" КАК ИмяСписка,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК МОЛ
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСписаниеОСиНМАИП(НомераТаблиц)

	НомераТаблиц.Вставить("СписаниеОСиНМАИПРеквизиты",	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеОСиНМАИПТаблица",	НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	""СписаниеОС"" КАК ВидОперации
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ОС"" КАК ИмяСписка,
	|	&СинонимОС КАК СинонимСписка,
	|	ТаблицаОС.НомерСтроки,
	|	ВЫБОР
	|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
	|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
	|		КОГДА Реквизиты.Субконто ССЫЛКА Справочник.НоменклатурныеГруппы
	|			ТОГДА Реквизиты.Субконто
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ТаблицаОС.ОсновноеСредство КАК Номенклатура,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновныеСредства) КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА Реквизиты.Субконто ССЫЛКА Справочник.СтатьиЗатрат
	|				ИЛИ Реквизиты.Субконто ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|				ИЛИ Реквизиты.Субконто ССЫЛКА Справочник.РасходыБудущихПериодов
	|			ТОГДА Реквизиты.Субконто
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяЗатрат,
	|	ВЫБОР
	|		КОГДА Реквизиты.Субконто ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(Реквизиты.Субконто КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидРасходовНУ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Субконто ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(Реквизиты.Субконто КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПринятиеКналоговомуУчету,
	|	ВЫБОР
	|		КОГДА Реквизиты.Субконто ССЫЛКА Справочник.СтатьиЗатрат
	|			ТОГДА ВЫРАЗИТЬ(Реквизиты.Субконто КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		КОГДА Реквизиты.Субконто ССЫЛКА Справочник.ПрочиеДоходыИРасходы
	|			ТОГДА ВЫРАЗИТЬ(Реквизиты.Субконто КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	Реквизиты.СчетСписания КАК СчетДоходов,
	|	0 КАК Выручка,
	|	0 КАК НДСНачисленный
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК Реквизиты
	|		ПО ТаблицаОС.Ссылка = Реквизиты.Ссылка
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.ОсновноеСредство.ЕдиницаУчета <> ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, ВедетсяУчетПрослеживаемыхТоваров)
	
	Если НЕ ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОС", Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ОстаткиМатериалов",   НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_РНПТКСписанию",       НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОС",       НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТовары",   НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписаниеОСОС.ОсновноеСредство КАК ОС,
	|	СписаниеОС.Организация КАК Организация,
	|	СписаниеОССведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СписаниеОССведенияПрослеживаемости.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(СписаниеОССведенияПрослеживаемости.Количество) КАК Количество,
	|	СУММА(СписаниеОССведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	СписаниеОССведенияПрослеживаемости.Номенклатура КАК Комплектующие,
	|	СписаниеОССведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЛОЖЬ КАК ПрослеживаемыйКомплект,
	|	ВЫБОР
	|		КОГДА ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ГрупповойОбъект,
	|	СписаниеОСОС.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар
	|ПОМЕСТИТЬ ВТ_Прослеживаемость
	|ИЗ
	|	Документ.СписаниеОС КАК СписаниеОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК СписаниеОСОС
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС.СведенияПрослеживаемости КАК СписаниеОССведенияПрослеживаемости
	|			ПО СписаниеОСОС.Ссылка = СписаниеОССведенияПрослеживаемости.Ссылка
	|				И СписаниеОСОС.КлючСтроки = СписаниеОССведенияПрослеживаемости.ИдентификаторСтроки
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	|			ПО СписаниеОСОС.ОсновноеСредство = ОсновныеСредства.Ссылка
	|		ПО СписаниеОС.Ссылка = СписаниеОСОС.Ссылка
	|ГДЕ
	|	СписаниеОС.Ссылка = &Ссылка
	|	И СписаниеОС.ОС.ПрослеживаемыйТовар = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеОСОС.ОсновноеСредство,
	|	СписаниеОС.Организация,
	|	СписаниеОССведенияПрослеживаемости.РНПТ,
	|	СписаниеОССведенияПрослеживаемости.СтранаПроисхождения,
	|	СписаниеОССведенияПрослеживаемости.Номенклатура,
	|	СписаниеОССведенияПрослеживаемости.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	СписаниеОСОС.ПрослеживаемыйТовар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеОСОС.ОсновноеСредство КАК ОС,
	|	СписаниеОСЦенностиОтВыбытия.Номенклатура КАК Номенклатура,
	|	СУММА(СписаниеОСЦенностиОтВыбытия.Количество) КАК Количество,
	|	СУММА(СписаниеОСЦенностиОтВыбытия.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	СписаниеОСЦенностиОтВыбытия.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СписаниеОСЦенностиОтВыбытия.РНПТ КАК РНПТ
	|ПОМЕСТИТЬ ОстаткиМатериалов
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК СписаниеОСОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ЦенностиОтВыбытия КАК СписаниеОСЦенностиОтВыбытия
	|		ПО СписаниеОСОС.КлючСтроки = СписаниеОСЦенностиОтВыбытия.КлючСтроки
	|ГДЕ
	|	СписаниеОСОС.Ссылка = &Ссылка
	|	И СписаниеОСОС.ПрослеживаемыйТовар = ИСТИНА
	|	И СписаниеОСЦенностиОтВыбытия.ПрослеживаемыйТовар = ИСТИНА
	|	И СписаниеОСЦенностиОтВыбытия.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеОСЦенностиОтВыбытия.Номенклатура,
	|	СписаниеОСОС.ОсновноеСредство,
	|	СписаниеОСЦенностиОтВыбытия.СтранаПроисхождения,
	|	СписаниеОСЦенностиОтВыбытия.РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.ОС КАК ОС,
	|	ВТ_Прослеживаемость.Организация КАК Организация,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ВТ_Прослеживаемость.Количество - ЕСТЬNULL(ОстаткиМатериалов.Количество, 0)) КАК Количество,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости - ЕСТЬNULL(ОстаткиМатериалов.КоличествоПрослеживаемости, 0)) КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.Комплектующие КАК Комплектующие,
	|	ВТ_Прослеживаемость.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	ВТ_Прослеживаемость.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_Прослеживаемость.ГрупповойОбъект КАК ГрупповойОбъект
	|ПОМЕСТИТЬ РНПТПоОСКСписанию
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиМатериалов КАК ОстаткиМатериалов
	|		ПО ВТ_Прослеживаемость.ОС = ОстаткиМатериалов.ОС
	|			И (ВЫБОР
	|				КОГДА ВТ_Прослеживаемость.Комплектующие = ЗНАЧЕНИЕ(Справочник.Номенклатура.пустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВТ_Прослеживаемость.Комплектующие = ОстаткиМатериалов.Номенклатура
	|			КОНЕЦ)
	|			И ВТ_Прослеживаемость.СтранаПроисхождения = ОстаткиМатериалов.СтранаПроисхождения
	|			И ВТ_Прослеживаемость.РНПТ = ОстаткиМатериалов.РНПТ
	|ГДЕ
	|	ВТ_Прослеживаемость.ПрослеживаемыйТовар = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.ОС,
	|	ВТ_Прослеживаемость.РНПТ,
	|	ВТ_Прослеживаемость.СтранаПроисхождения,
	|	ВТ_Прослеживаемость.Комплектующие,
	|	ВТ_Прослеживаемость.Организация,
	|	ВТ_Прослеживаемость.ПрослеживаемыйКомплект,
	|	ВТ_Прослеживаемость.ИдентификаторСтроки,
	|	ВТ_Прослеживаемость.ГрупповойОбъект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РНПТПоОСКСписанию.Организация КАК Организация,
	|	РНПТПоОСКСписанию.РНПТ КАК РНПТ,
	|	РНПТПоОСКСписанию.СтранаПроисхождения КАК СтранаПроисхождения,
	|	НАЧАЛОПЕРИОДА(СписаниеОС.Дата, КВАРТАЛ) КАК ПериодОперации,
	|	СписаниеОС.Ссылка КАК ДокументОперации,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях) КАК ОтражениеВОтчетности,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт) КАК ТипДокументаВПрослеживаемости,
	|	СписаниеОС.КодОперацииПрослеживаемости КАК КодОперации,
	|	ВЫБОР
	|		КОГДА РНПТПоОСКСписанию.Комплектующие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА РНПТПоОСКСписанию.ОС
	|		ИНАЧЕ РНПТПоОСКСписанию.Комплектующие
	|	КОНЕЦ КАК Номенклатура,
	|	РНПТПоОСКСписанию.Количество КАК Количество,
	|	РНПТПоОСКСписанию.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	0 КАК СуммаБезНДС,
	|	РНПТПоОСКСписанию.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РНПТПоОСКСписанию.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	РНПТПоОСКСписанию.ГрупповойОбъект КАК ГрупповойОбъект
	|ИЗ
	|	РНПТПоОСКСписанию КАК РНПТПоОСКСписанию,
	|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК СписаниеОС
	|		ПО ДанныеПервичныхДокументов.Документ = СписаниеОС.Ссылка
	|			И ДанныеПервичныхДокументов.Организация = СписаниеОС.Организация
	|ГДЕ
	|	СписаниеОС.Ссылка = &Ссылка
	|	И РНПТПоОСКСписанию.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.ОС КАК ОсновноеСредство,
	|	ВТ_Прослеживаемость.Организация КАК Организация,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВТ_Прослеживаемость.Количество КАК Количество,
	|	ВТ_Прослеживаемость.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.Комплектующие КАК Комплектующие
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|ГДЕ
	|	ВТ_Прослеживаемость.ПрослеживаемыйТовар = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиМатериалов.ОС КАК ОсновноеСредство,
	|	ОстаткиМатериалов.Номенклатура КАК Номенклатура,
	|	ОстаткиМатериалов.Количество КАК Количество,
	|	ОстаткиМатериалов.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ОстаткиМатериалов.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ОстаткиМатериалов.РНПТ КАК РНПТ,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК Комиссионер,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДляВозврата,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|	НЕОПРЕДЕЛЕНО КАК Комплектующие
	|ИЗ
	|	ОстаткиМатериалов КАК ОстаткиМатериалов";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСписаниеГрупповыхОС(НомераТаблиц)
	
	НомераТаблиц.Вставить("СписаниеГрупповыхОС", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	&СодержаниеПроводокГрупповыеОС КАК СодержаниеПроводок,
	|	ЛОЖЬ КАК ЭтоПеремещение,
	|	""ОС"" КАК ИмяСписка
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ПечатьОС4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ИмяМакета)

	Перем ПодразделениеОтветственныхЛиц;

	Если МассивОбъектов.Количество() = 0 Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	Автотранспорт = ИмяМакета = "ОС4а";
	
	УстановитьПривилегированныйРежим(Истина);

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеОС_ОС4";

	КоличествоОС4 = 0;
	КоличествоОС4а = 0;
	КоличествоОС4б = 0;
	МакетОС4  = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС4");
	МакетОС4а = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС4а");
	МакетОС4б = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС4б");

	// Области макетов
	ОбластьЗаголовок = МакетОС4.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок4а = МакетОС4а.ПолучитьОбласть("Заголовок");

	Шапка0 = МакетОС4б.ПолучитьОбласть("Шапка0");
	Шапка1 = МакетОС4б.ПолучитьОбласть("Шапка1");
	Шапка2 = МакетОС4б.ПолучитьОбласть("Шапка2");
	Строка1 = МакетОС4б.ПолучитьОбласть("Строка1");

	// Дополняет регистры сведений колонками ДатаНачала и ДатаОкончания
	//  для более удобного объединения в общем запросе
	МВТ = Новый МенеджерВременныхТаблиц;
	СоздатьВременныеТаблицыРегистров(МВТ, МассивОбъектов);

	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МВТ;

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);

	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаОС.Ссылка КАК Ссылка,
	               |	ТаблицаОС.Ссылка.Номер КАК Номер,
	               |	ТаблицаОС.Ссылка.Дата КАК Дата,
	               |	ТаблицаОС.Ссылка.СобытиеОС КАК Состояние,
	               |	ТаблицаОС.Ссылка.ПричинаСписания КАК ПричинаСписания,
	               |	ТаблицаОС.Ссылка.Организация КАК Организация,
	               |	ТаблицаОС.Ссылка.Организация.КодПоОКПО КАК КодОКПО,
	               |	ТаблицаОС.Ссылка.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	               |	ТаблицаОС.Ссылка.МоментВремени КАК МоментВремени,
	               |	ТаблицаОС.НомерСтроки КАК Нп,
	               |	ТаблицаОС.Количество КАК Количество,
	               |	ТаблицаОС.ОсновноеСредство КАК ОС,
	               |	ТаблицаОС.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	               |	ТаблицаОС.ОсновноеСредство.ДатаВыпуска КАК ГодВыпуска,
	               |	ТаблицаОС.ОсновноеСредство.НаименованиеПолное КАК НаимОС,
	               |	ТаблицаОС.ОсновноеСредство.ГруппаОС КАК Группа,
	               |	ТаблицаОС.ОсновноеСредство.ЕдиницаУчета КАК ЕдиницаУчета,
	               |	ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.ПервоначальнаяСтоимость КАК НачСтоимость,
	               |	ВЫБОР
	               |		КОГДА ТаблицаОС.ОсновноеСредство.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	               |			ТОГДА ""-""
	               |		ИНАЧЕ ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.ИнвентарныйНомер
	               |	КОНЕЦ КАК ИнвНомер,
	               |	ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
	               |	ВТ_МестонахождениеОСБухгалтерскийУчет.Местонахождение КАК Подразделение,
	               |	ВТ_МестонахождениеОСБухгалтерскийУчет.МОЛ КАК МОЛ,
	               |	ЕСТЬNULL(ВТ_СтоимостьОС.СуммаОборот, 0) КАК Стоимость,
	               |	ВЫБОР
	               |		КОГДА ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
	               |				ИЛИ ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса)
	               |			ТОГДА ЕСТЬNULL(ВТ_ИзносОС.СуммаОборот, 0)
	               |		ИНАЧЕ ЕСТЬNULL(ВТ_АмортизацияОС.СуммаОборот, 0)
	               |	КОНЕЦ КАК НачАмортизация
	               |ИЗ
	               |	Документ.СписаниеОС.ОС КАК ТаблицаОС
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет КАК ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет
	               |		ПО (ТаблицаОС.Ссылка.Дата МЕЖДУ ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.ДатаНачала И ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.ДатаОкончания)
	               |			И ТаблицаОС.ОсновноеСредство = ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство
	               |			И ТаблицаОС.Ссылка.Организация = ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет.Организация
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаБухгалтерскогоУчетаОС КАК ВТ_СчетаБухгалтерскогоУчетаОС
	               |		ПО (ТаблицаОС.Ссылка.Дата МЕЖДУ ВТ_СчетаБухгалтерскогоУчетаОС.ДатаНачала И ВТ_СчетаБухгалтерскогоУчетаОС.ДатаОкончания)
	               |			И ТаблицаОС.Ссылка.Организация = ВТ_СчетаБухгалтерскогоУчетаОС.Организация
	               |			И ТаблицаОС.ОсновноеСредство = ВТ_СчетаБухгалтерскогоУчетаОС.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОСБухгалтерскийУчет КАК ВТ_МестонахождениеОСБухгалтерскийУчет
	               |		ПО (ТаблицаОС.Ссылка.Дата МЕЖДУ ВТ_МестонахождениеОСБухгалтерскийУчет.ДатаНачала И ВТ_МестонахождениеОСБухгалтерскийУчет.ДатаОкончания)
	               |			И ТаблицаОС.Ссылка.Организация = ВТ_МестонахождениеОСБухгалтерскийУчет.Организация
	               |			И ТаблицаОС.ОсновноеСредство = ВТ_МестонахождениеОСБухгалтерскийУчет.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС КАК ВТ_СтоимостьОС
	               |		ПО ТаблицаОС.Ссылка = ВТ_СтоимостьОС.Регистратор
	               |			И (ВТ_СчетаБухгалтерскогоУчетаОС.СчетУчета = ВТ_СтоимостьОС.СчетКт)
	               |			И ТаблицаОС.ОсновноеСредство = ВТ_СтоимостьОС.СубконтоКт1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС КАК ВТ_АмортизацияОС
	               |		ПО ТаблицаОС.Ссылка = ВТ_АмортизацияОС.Регистратор
	               |			И (ВТ_СчетаБухгалтерскогоУчетаОС.СчетНачисленияАмортизации = ВТ_АмортизацияОС.СчетДт)
	               |			И ТаблицаОС.ОсновноеСредство = ВТ_АмортизацияОС.СубконтоДт1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИзносОС КАК ВТ_ИзносОС
	               |		ПО ТаблицаОС.Ссылка = ВТ_ИзносОС.Регистратор
	               |			И (ВТ_СчетаБухгалтерскогоУчетаОС.СчетНачисленияИзносаАмортизации = ВТ_ИзносОС.СчетКт)
	               |			И ТаблицаОС.ОсновноеСредство = ВТ_ИзносОС.СубконтоКт1
	               |ГДЕ
	               |	ТаблицаОС.Ссылка В(&МассивОбъектов)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТаблицаОС.Ссылка.Дата,
	               |	ТаблицаОС.Ссылка,
	               |	Нп
	               |ИТОГИ ПО
	               |	Ссылка";

	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда

		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		Возврат ТабличныйДокумент;

	КонецЕсли;

	ВыборкаДокументов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	ПервыйДокумент = Истина;

	// Цикл по документам
	Пока ВыборкаДокументов.Следующий() Цикл

		// Выборка по табличной части
		ВыборкаСтрок = ВыборкаДокументов.Выбрать(ОбходРезультатаЗапроса.Прямой);

		ТекстСообщения = "";
		Если ВыборкаСтрок.Количество() = 0 Тогда
			ТекстШаблона = НСтр("ru = 'Нет инвентарных объектов основных средств для формирования печатной формы № ОС-4.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстШаблона, Строка(ВыборкаДокументов.Ссылка));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ВыборкаДокументов.Ссылка);
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		СведенияОбОрганизации    = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаДокументов.Организация, ВыборкаДокументов.Дата);
		ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");

		ПодразделениеОтветственныхЛиц = ВыборкаДокументов.ПодразделениеОрганизации;

		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(ВыборкаДокументов.Организация, ВыборкаДокументов.Дата, ПодразделениеОтветственныхЛиц);

		Если ВыборкаСтрок.Количество() = 1 Тогда

			ВыборкаСтрок.Следующий();
			
			Если Не ЗначениеЗаполнено(ВыборкаСтрок.ОС) Тогда
				ТабличныйДокумент.Вывести(ОбластьЗаголовок);
				Продолжить;
			КонецЕсли;

			Если Автотранспорт Тогда
				КоличествоОС4а = КоличествоОС4 + 1;
				Область = ОбластьЗаголовок4а;
			Иначе
				КоличествоОС4 = КоличествоОС4 + 1;
				Область = ОбластьЗаголовок;
			КонецЕсли;

			ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаДокументов.Организация, ВыборкаСтрок.МОЛ, ВыборкаДокументов.Дата, Истина);

			Область.Параметры.Заполнить(ВыборкаСтрок);
			Область.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаСтрок.Номер, Истина, Ложь);
			Область.Параметры.Организация = ПредставлениеОрганизации;
			Область.Параметры.ТабНомерМОЛ = ДанныеФизЛица.ТабельныйНомер;

			НаименованиеОС = ?(ПустаяСтрока(ВыборкаСтрок.НаимОС),
				СокрЛП(ВыборкаСтрок.ОС), ВыборкаСтрок.НаимОС);
				
			Область.Параметры.НаимОС = НаименованиеОС;

			Если ВыборкаСтрок.ЕдиницаУчета = Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект Тогда
				Область.Параметры.НаимОС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1, %2 шт.'", ОбщегоНазначения.КодОсновногоЯзыка()),
					НаименованиеОС,
					ВыборкаСтрок.Количество);
			КонецЕсли;

			ДокументПринятияКУчету     = "";
			ДокументВводаВЭксплуатацию = "";
			ПринятоКУчету              = "";
			ВведеноВЭксплуатацию       = "";

			УчетОС.ПолучитьДокументБухСостоянияОС(
				ВыборкаСтрок.ОС,
				ВыборкаСтрок.Организация,
				Перечисления.СостоянияОС.ПринятоКУчету,
				ДокументВводаВЭксплуатацию,
				ВведеноВЭксплуатацию);
			УчетОС.ПолучитьДокументБухСостоянияОС(
				ВыборкаСтрок.ОС,
				ВыборкаСтрок.Организация,
				Перечисления.СостоянияОС.ПринятоКУчету,
				ДокументПринятияКУчету,
				ПринятоКУчету);

			Если Автотранспорт Тогда
				Область.Параметры.ВведеноВЭксплуатацию = ВведеноВЭксплуатацию;
				Если ДокументВводаВЭксплуатацию = Неопределено Тогда
					Область.Параметры.Пробег = ПолучитьПробегАвто(ВыборкаСтрок.ОС, ВыборкаСтрок.Дата, ВыборкаСтрок.Дата, ВыборкаСтрок.Организация);
				Иначе
					Область.Параметры.Пробег = ПолучитьПробегАвто(ВыборкаСтрок.ОС, ДокументВводаВЭксплуатацию.Дата, ВыборкаСтрок.Дата, ВыборкаСтрок.Организация);
				КонецЕсли;
			ИначеЕсли ВыборкаСтрок.ЕдиницаУчета <> Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект Тогда
				
				СрокЭкспл = УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВведеноВЭксплуатацию, ВыборкаСтрок.Дата);
				Область.Параметры.СрокЭкспл = ?(НЕ ЗначениеЗаполнено(СрокЭкспл), "-",
					УправлениеВнеоборотнымиАктивами.РасшифровкаСрокаПолезногоИспользования(СрокЭкспл));
					
			КонецЕсли;

			СтоимостьОС = ?(ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				ВыборкаСтрок.НачСтоимость,
				ВыборкаСтрок.Стоимость);

			АмортизацияОС = ?(ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				0,
				ВыборкаСтрок.НачАмортизация);

			Область.Параметры.ГодВыпуска     = ?(ЗначениеЗаполнено(ВыборкаСтрок.ГодВыпуска), Год(ВыборкаСтрок.ГодВыпуска), 0);
			Область.Параметры.ПринятоКУчету  = ПринятоКУчету;
			Область.Параметры.НачСтоимость   = СтоимостьОС;
			Область.Параметры.НачАмортизация = АмортизацияОС;

			Область.Параметры.ОстСтоимость = ?(ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
				0,
				СтоимостьОС - АмортизацияОС);

			Область.Параметры.ГлавБух               = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
			Область.Параметры.Руководитель          = ОтветственныеЛица.РуководительПредставление;
			Область.Параметры.ДолжностьРуководителя = ОтветственныеЛица.РуководительДолжностьПредставление;

			ТабличныйДокумент.Вывести(Область);

		Иначе // Табличная часть состоит их нескольких строк

			ПерваяИтерация = Истина;
			Пока ВыборкаСтрок.Следующий() Цикл
				
				Если ПерваяИтерация Тогда 
					
					ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаДокументов.Организация, ВыборкаСтрок.МОЛ, ВыборкаДокументов.Дата, Истина);
					
					Шапка0.Параметры.Заполнить(ВыборкаСтрок);
					Шапка0.Параметры.Номер                 = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаСтрок.Номер, Истина, Ложь);
					Шапка0.Параметры.Организация           = ПредставлениеОрганизации;
					Шапка0.Параметры.Руководитель          = ОтветственныеЛица.РуководительПредставление;
					Шапка0.Параметры.ДолжностьРуководителя = ОтветственныеЛица.РуководительДолжностьПредставление;
					Шапка0.Параметры.ТабНомерМОЛ           = ДанныеФизЛица.ТабельныйНомер;

					ТабличныйДокумент.Вывести(Шапка0);

					Шапка1.Параметры.Заполнить(ВыборкаСтрок);
					ТабличныйДокумент.Вывести(Шапка1);
					ПерваяИтерация = Ложь;
					
				КонецЕсли;

				Строка1.Параметры.Заполнить(ВыборкаСтрок);
					
				НаименованиеОС = ?(ПустаяСтрока(ВыборкаСтрок.НаимОС),
				СокрЛП(ВыборкаСтрок.ОС), ВыборкаСтрок.НаимОС);
				
				Строка1.Параметры.НаимОС = НаименованиеОС;

				Если ВыборкаСтрок.ЕдиницаУчета = Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект Тогда
					Строка1.Параметры.НаимОС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1, %2 шт.'", ОбщегоНазначения.КодОсновногоЯзыка()),
						НаименованиеОС,
						ВыборкаСтрок.Количество);
				КонецЕсли;

				СтоимостьОС = ?(ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					ВыборкаСтрок.НачСтоимость,
					ВыборкаСтрок.Стоимость);

				АмортизацияОС = ?(ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					0,
					ВыборкаСтрок.НачАмортизация);

				Строка1.Параметры.НачСтоимость = СтоимостьОС;
				Строка1.Параметры.НачАмортизация = АмортизацияОС;
				Строка1.Параметры.ОстСтоимость = ?(ВыборкаСтрок.ПорядокПогашенияСтоимости = Перечисления.ПорядокПогашенияСтоимостиОС.СписаниеПриПринятииКУчету,
					0,
					СтоимостьОС - АмортизацияОС);

				Строка1.Параметры.Причина = ВыборкаСтрок.ПричинаСписания;

				ДокументПринятияКУчету     = "";
				ДокументВводаВЭксплуатацию = "";
				ПринятоКУчету              = "";
				ВведеноВЭксплуатацию       = "";

				УчетОС.ПолучитьДокументБухСостоянияОС(
					ВыборкаСтрок.ОС,
					ВыборкаСтрок.Организация,
					Перечисления.СостоянияОС.ПринятоКУчету,
					ДокументВводаВЭксплуатацию,
					ВведеноВЭксплуатацию);

				Если ВыборкаСтрок.ЕдиницаУчета <> Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект Тогда
					СрокЭкспл = УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВведеноВЭксплуатацию, ВыборкаСтрок.Дата);
					Строка1.Параметры.СрокЭкспл = ?(НЕ ЗначениеЗаполнено(СрокЭкспл), "-",
						УправлениеВнеоборотнымиАктивами.РасшифровкаСрокаПолезногоИспользования(СрокЭкспл));
				КонецЕсли;
					
				ТабличныйДокумент.Вывести(Строка1);

			КонецЦикла;

			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			Шапка2.Параметры.Заполнить(ВыборкаСтрок);
			Шапка2.Параметры.ГлавБух = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
			ТабличныйДокумент.Вывести(Шапка2);
			
			Если Не ПерваяИтерация Тогда 
				КоличествоОС4б = КоличествоОС4б + 1;
			КонецЕсли;

		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументов.Ссылка);

	КонецЦикла;
	
	Если КоличествоОС4 = 0 И КоличествоОС4а = 0 Тогда 
		ИмяМакета = "ОбщийМакет.ПФ_MXL_ОС4б";
	ИначеЕсли КоличествоОС4 = 0 И КоличествоОС4б = 0 Тогда 
		ИмяМакета = "ОбщийМакет.ПФ_MXL_ОС4а";
	ИначеЕсли КоличествоОС4а = 0 И КоличествоОС4б = 0 Тогда 
		ИмяМакета = "ОбщийМакет.ПФ_MXL_ОС4";
	КонецЕсли;

	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("СписаниеОС.Печать.ОС4");
	
	Возврат ТабличныйДокумент;

КонецФункции

// Процедура создает временные "виртуальные" таблицы регистров сведений для дальнейшей работы с ними
//
// Параметры:
//  МВТ            - МенеджерВременныхТаблиц, где будут храниться временные таблицы
//  МассивОбъектов - Массив документов
//
Процедура СоздатьВременныеТаблицыРегистров(МВТ, МассивОбъектов)

	// 1. Определим "границы" регистров сведений - список организаций, ОС, период, ...
	//    чтобы ограничить выборку из регистров сведений
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|// 1. Период дат
	|ВЫБРАТЬ
	|	МИНИМУМ(Док.Дата)  КАК ДатаОТ,
	|	МАКСИМУМ(Док.Дата) КАК ДатаДО
	|ИЗ
	|	Документ.СписаниеОС КАК Док
	|ГДЕ
	|	Док.Ссылка В (&МассивОбъектов)
	|;
	|
	|// 2. Список организаций
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Док.Организация КАК Организация
	|ИЗ
	|	Документ.СписаниеОС КАК Док
	|ГДЕ
	|	Док.Ссылка В (&МассивОбъектов)
	|;
	|
	|// 3. Список ОС
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Док.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК Док
	|ГДЕ
	|	Док.Ссылка В (&МассивОбъектов)
	|
	|";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);

	Результат = Запрос.ВыполнитьПакет();
	// 1.1. Интервал дат
	Выборка = Результат[0].Выбрать(ОбходРезультатаЗапроса.Прямой);
	Выборка.Следующий();
	ДатаОТ = Выборка.ДатаОТ;
	ДатаДО = Выборка.ДатаДО;

	// 1.2. Список организаций
	МассивОрганизаций = Результат[1].Выгрузить(ОбходРезультатаЗапроса.Прямой).ВыгрузитьКолонку("Организация");

	// 1.3. Список ОС
	МассивОС = Результат[2].Выгрузить(ОбходРезультатаЗапроса.Прямой).ВыгрузитьКолонку("ОсновноеСредство");

	// 2. Создание временных таблиц
	СчетаВыбытияОС = Новый Массив;
	СчетаВыбытияОС.Добавить(ПланыСчетов.Хозрасчетный.ВыбытиеОС);
	СчетаВыбытияОС.Добавить(ПланыСчетов.Хозрасчетный.ВыбытиеМЦ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("ДатаОТ",            ДатаОТ);
	Запрос.УстановитьПараметр("ДатаДО",            ДатаДО);
	Запрос.УстановитьПараметр("МассивОбъектов",    МассивОбъектов);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("МассивОС",          МассивОС);
	Запрос.УстановитьПараметр("СчетаВыбытияОС",    СчетаВыбытияОС);
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ТаблицаОС.Ссылка КАК Ссылка,
	               |	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	               |ПОМЕСТИТЬ ВТ_ВсеОС
	               |ИЗ
	               |	Документ.СписаниеОС.ОС КАК ТаблицаОС
	               |ГДЕ
	               |	ТаблицаОС.Ссылка В(&МассивОбъектов)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Ссылка,
	               |	ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Рег1.Период КАК Период,
	               |	Рег1.Организация КАК Организация,
	               |	МИНИМУМ(Рег2.Период) КАК ПервыйПериод,
	               |	Рег1.ОсновноеСредство КАК ОсновноеСредство
	               |ПОМЕСТИТЬ ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет_Рег2
	               |ИЗ
	               |	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК Рег1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК Рег2
	               |		ПО Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	               |			И Рег1.Период < Рег2.Период
	               |ГДЕ
	               |	Рег1.Период <= &ДатаДО
	               |	И Рег1.ОсновноеСредство В(&МассивОС)
	               |	И Рег2.Период <= &ДатаДО
	               |	И Рег2.ОсновноеСредство В(&МассивОС)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Рег1.Период,
	               |	Рег1.Организация,
	               |	Рег1.ОсновноеСредство
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Рег1.Период,
	               |	Рег1.Организация,
	               |	Рег1.ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА Рег2.ПервыйПериод ЕСТЬ NULL
	               |			ТОГДА ДАТАВРЕМЯ(3999, 12, 31)
	               |		ИНАЧЕ ДОБАВИТЬКДАТЕ(Рег2.ПервыйПериод, СЕКУНДА, -1)
	               |	КОНЕЦ КАК ДатаОкончания,
	               |	Рег1.Период КАК ДатаНачала,
	               |	Рег1.ОсновноеСредство КАК ОсновноеСредство,
	               |	Рег1.Организация КАК Организация,
	               |	Рег1.ИнвентарныйНомер КАК ИнвентарныйНомер,
	               |	Рег1.СпособПоступления КАК СпособПоступления,
	               |	Рег1.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	               |	Рег1.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	               |	Рег1.ПараметрВыработки КАК ПараметрВыработки,
	               |	Рег1.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости
	               |ПОМЕСТИТЬ ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет
	               |ИЗ
	               |	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК Рег1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОСБухгалтерскийУчет_Рег2 КАК Рег2
	               |		ПО Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	               |			И Рег1.Период = Рег2.Период
	               |ГДЕ
	               |	Рег1.Период <= &ДатаДО
	               |	И Рег1.ОсновноеСредство В(&МассивОС)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ДатаНачала,
	               |	ДатаОкончания,
	               |	ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Рег1.Период КАК Период,
	               |	МИНИМУМ(Рег2.Период) КАК ПервыйПериод,
	               |	Рег1.Организация КАК Организация,
	               |	Рег1.ОсновноеСредство КАК ОсновноеСредство
	               |ПОМЕСТИТЬ ВТ_СчетаБухгалтерскогоУчетаОС_Рег2
	               |ИЗ
	               |	РегистрСведений.СчетаБухгалтерскогоУчетаОС КАК Рег1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС КАК Рег2
	               |		ПО Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	               |			И Рег1.Организация = Рег2.Организация
	               |			И Рег1.Период < Рег2.Период
	               |ГДЕ
	               |	Рег1.Период <= &ДатаДО
	               |	И Рег1.Организация В(&МассивОрганизаций)
	               |	И Рег1.ОсновноеСредство В(&МассивОС)
	               |	И Рег2.Период <= &ДатаДО
	               |	И Рег2.Организация В(&МассивОрганизаций)
	               |	И Рег2.ОсновноеСредство В(&МассивОС)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Рег1.Период,
	               |	Рег1.Организация,
	               |	Рег1.ОсновноеСредство
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Рег1.Период,
	               |	Рег1.Организация,
	               |	Рег1.ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА Рег2.ПервыйПериод ЕСТЬ NULL
	               |			ТОГДА ДАТАВРЕМЯ(3999, 12, 31)
	               |		ИНАЧЕ ДОБАВИТЬКДАТЕ(Рег2.ПервыйПериод, СЕКУНДА, -1)
	               |	КОНЕЦ КАК ДатаОкончания,
	               |	Рег1.Период КАК ДатаНачала,
	               |	Рег1.Организация КАК Организация,
	               |	Рег1.ОсновноеСредство КАК ОсновноеСредство,
	               |	Рег1.СчетУчета КАК СчетУчета,
	               |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетНачисленияИзноса,
	               |	Рег1.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
	               |	Рег1.СчетНачисленияАмортизации КАК СчетНачисленияИзносаАмортизации
	               |ПОМЕСТИТЬ ВТ_СчетаБухгалтерскогоУчетаОС
	               |ИЗ
	               |	РегистрСведений.СчетаБухгалтерскогоУчетаОС КАК Рег1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаБухгалтерскогоУчетаОС_Рег2 КАК Рег2
	               |		ПО Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	               |			И Рег1.Организация = Рег2.Организация
	               |			И Рег1.Период = Рег2.Период
	               |ГДЕ
	               |	Рег1.Период <= &ДатаДО
	               |	И Рег1.Организация В(&МассивОрганизаций)
	               |	И Рег1.ОсновноеСредство В(&МассивОС)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ДАТАВРЕМЯ(3999, 12, 31),
	               |	ДАТАВРЕМЯ(1, 1, 1),
	               |	ДанныеГрупповыхОС.Организация,
	               |	ДанныеГрупповыхОС.ОсновноеСредство,
	               |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГрупповыеОбъектыОС),
	               |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка),
	               |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АмортизацияГрупповыхОбъектовОС),
	               |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АмортизацияГрупповыхОбъектовОС)
	               |ИЗ
	               |	РегистрСведений.СпособыАмортизацииГрупповыхОбъектовОС КАК ДанныеГрупповыхОС
	               |ГДЕ
	               |	ДанныеГрупповыхОС.Организация В(&МассивОрганизаций)
	               |	И ДанныеГрупповыхОС.ОсновноеСредство В(&МассивОС)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ДатаНачала,
	               |	ДатаОкончания,
	               |	Организация,
	               |	ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Рег1.Период КАК Период,
	               |	МИНИМУМ(Рег2.Период) КАК ПервыйПериод,
	               |	Рег1.Организация КАК Организация,
	               |	Рег1.ОсновноеСредство КАК ОсновноеСредство
	               |ПОМЕСТИТЬ ВТ_МестонахождениеОСБухгалтерскийУчет_Рег2
	               |ИЗ
	               |	РегистрСведений.МестонахождениеОСБухгалтерскийУчет КАК Рег1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет КАК Рег2
	               |		ПО Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	               |			И Рег1.Организация = Рег2.Организация
	               |			И Рег1.Период < Рег2.Период
	               |ГДЕ
	               |	Рег1.Период <= &ДатаДО
	               |	И Рег1.Организация В(&МассивОрганизаций)
	               |	И Рег1.ОсновноеСредство В(&МассивОС)
	               |	И Рег2.Период <= &ДатаДО
	               |	И Рег2.Организация В(&МассивОрганизаций)
	               |	И Рег2.ОсновноеСредство В(&МассивОС)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Рег1.Период,
	               |	Рег1.Организация,
	               |	Рег1.ОсновноеСредство
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Рег1.Период,
	               |	Рег1.Организация,
	               |	Рег1.ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА Рег2.ПервыйПериод ЕСТЬ NULL
	               |			ТОГДА ДАТАВРЕМЯ(3999, 12, 31)
	               |		ИНАЧЕ ДОБАВИТЬКДАТЕ(Рег2.ПервыйПериод, СЕКУНДА, -1)
	               |	КОНЕЦ КАК ДатаОкончания,
	               |	Рег1.Период КАК ДатаНачала,
	               |	Рег1.Организация КАК Организация,
	               |	Рег1.ОсновноеСредство КАК ОсновноеСредство,
	               |	Рег1.МОЛ КАК МОЛ,
	               |	Рег1.Местонахождение КАК Местонахождение
	               |ПОМЕСТИТЬ ВТ_МестонахождениеОСБухгалтерскийУчет
	               |ИЗ
	               |	РегистрСведений.МестонахождениеОСБухгалтерскийУчет КАК Рег1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОСБухгалтерскийУчет_Рег2 КАК Рег2
	               |		ПО Рег1.ОсновноеСредство = Рег2.ОсновноеСредство
	               |			И Рег1.Организация = Рег2.Организация
	               |			И Рег1.Период = Рег2.Период
	               |ГДЕ
	               |	Рег1.Период <= &ДатаДО
	               |	И Рег1.Организация В(&МассивОрганизаций)
	               |	И Рег1.ОсновноеСредство В(&МассивОС)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ДатаНачала,
	               |	ДатаОкончания,
	               |	Организация,
	               |	ОсновноеСредство
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	               |	ХозрасчетныйОборотыДтКт.СчетКт КАК СчетКт,
	               |	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК СубконтоКт1,
	               |	ХозрасчетныйОборотыДтКт.СуммаОборот КАК СуммаОборот
	               |ПОМЕСТИТЬ ВТ_СтоимостьОС
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(ДОБАВИТЬКДАТЕ(&ДатаОТ, СЕКУНДА, -1), ДОБАВИТЬКДАТЕ(&ДатаДО, СЕКУНДА, 1), Регистратор, СчетДт В (&СчетаВыбытияОС), , , ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация В (&МассивОрганизаций)) КАК ХозрасчетныйОборотыДтКт
	               |ГДЕ
	               |	ХозрасчетныйОборотыДтКт.Регистратор В(&МассивОбъектов)
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Регистратор,
	               |	СчетКт,
	               |	СубконтоКт1
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	               |	ХозрасчетныйОборотыДтКт.СчетДт КАК СчетДт,
	               |	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК СубконтоДт1,
	               |	СУММА(ХозрасчетныйОборотыДтКт.СуммаОборот) КАК СуммаОборот
	               |ПОМЕСТИТЬ ВТ_АмортизацияОС
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(ДОБАВИТЬКДАТЕ(&ДатаОТ, СЕКУНДА, -1), ДОБАВИТЬКДАТЕ(&ДатаДО, СЕКУНДА, 1), Регистратор, , ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), СчетКт В (&СчетаВыбытияОС), , Организация В (&МассивОрганизаций)) КАК ХозрасчетныйОборотыДтКт
	               |ГДЕ
	               |	ХозрасчетныйОборотыДтКт.Регистратор В(&МассивОбъектов)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОборотыДтКт.Регистратор,
	               |	ХозрасчетныйОборотыДтКт.СчетДт,
	               |	ХозрасчетныйОборотыДтКт.СубконтоДт1
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Регистратор,
	               |	СчетДт,
	               |	СубконтоДт1
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	               |	ХозрасчетныйОборотыДтКт.СчетКт КАК СчетКт,
	               |	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК СубконтоКт1,
	               |	СУММА(ХозрасчетныйОборотыДтКт.Сумма) КАК СуммаОборот
	               |ПОМЕСТИТЬ ВТ_ИзносОС
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	               |			ДОБАВИТЬКДАТЕ(&ДатаОТ, СЕКУНДА, -1),
	               |			ДОБАВИТЬКДАТЕ(&ДатаДО, СЕКУНДА, 1),
	               |			Организация В (&МассивОрганизаций)
	               |				И СубконтоКт1 В (&МассивОС)
	               |				И СчетКт В
	               |					(ВЫБРАТЬ
	               |						ВТ_СчетаБухгалтерскогоУчетаОС.СчетНачисленияИзносаАмортизации
	               |					ИЗ
	               |						ВТ_СчетаБухгалтерскогоУчетаОС),
	               |			,
	               |			) КАК ХозрасчетныйОборотыДтКт
	               |ГДЕ
	               |	ХозрасчетныйОборотыДтКт.Регистратор В(&МассивОбъектов)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОборотыДтКт.Регистратор,
	               |	ХозрасчетныйОборотыДтКт.СчетКт,
	               |	ХозрасчетныйОборотыДтКт.СубконтоКт1
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Регистратор,
	               |	СчетКт,
	               |	СубконтоКт1";
	Запрос.Выполнить();

КонецПроцедуры

// Функция возвращает параметры ОС
//
Функция ПолучитьПробегАвто(ОбъектОС, НачДата, КонДата, Организация)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонДата", КонДата);
	Запрос.УстановитьПараметр("НачГраница", Новый Граница(НачДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонГраница", Новый Граница(КонДата, ВидГраницы.Включая));
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВыработкаОС.КоличествоОборот КАК Пробег
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
		|		&КонДата,
		|		ОсновноеСредство = &ОС
		|			И Организация = &Организация
		|	) КАК РегСведенияОС
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|
		|	РегистрНакопления.ВыработкаОС.Обороты(
		|		&НачГраница,
		|		&КонГраница,
		|		,
		|		ОсновноеСредство = &ОС
		|	) КАК ВыработкаОС
		|
		|	ПО
		|		РегСведенияОС.ОсновноеСредство = ВыработкаОС.ОсновноеСредство
		|		И РегСведенияОС.ПараметрВыработки = ВыработкаОС.ПараметрВыработки
		|";

	Запрос.УстановитьПараметр("ОС", ОбъектОС);
	Запрос.УстановитьПараметр("Организация", Организация);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	Возврат Выборка.Пробег;

КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	СписаниеОСОС.НомерСтроки КАК НомерСтрокиОС,
	|	СписаниеОСОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Цена КАК Цена,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка) КАК СтавкаНДСВРознице,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.СчетУчета.Забалансовый КАК СчетУчетаЗабалансовый,
	|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются) КАК ОтражениеВУСН,
	|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка) КАК СтранаПроисхождения,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.СписаниеОС.ЦенностиОтВыбытия КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК СписаниеОСОС
	|		ПО ТаблицаТовары.Ссылка = СписаниеОСОС.Ссылка
	|			И ТаблицаТовары.КлючСтроки = СписаниеОСОС.КлючСтроки
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаОприходованиеТоваров(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ОсталисьМатериальныеЦенностиОтВыбытия Тогда
		ПараметрыПроведения.Вставить("ОприходованиеТоваровРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ОприходованиеТоваровТаблицаТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	// Структура таблицы реквизитов должна содержать все поля, которые есть в таблице рекивизитов
	// для метода УчетТоваров.СформироватьДвиженияОприходованиеТоваров()
	НомераТаблиц.Вставить("ОприходованиеТоваровРеквизиты",		НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОприходованиеТоваровТаблицаТовары",	НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Документ.ИнвентаризацияТоваровНаСкладе.ПустаяСсылка) КАК ИнвентаризацияТоваровНаСкладе,
	|	Реквизиты.СчетУчетаДоходов КАК СчетУчетаДоходов,
	|	Реквизиты.СубконтоДоходов КАК СубконтоДоходов
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ЦенностиОтВыбытия"" КАК ИмяСписка,
	|	&СинонимЦенностиОтВыбытия КАК СинонимСписка,
	|	ТаблицаТовары.НомерСтрокиОС КАК НомерСтрокиОС,
	|	ТаблицаТовары.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ОсновноеСредство.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ КАК СуммаНУ,
	|	ТаблицаТовары.СтавкаНДСВРознице КАК СтавкаНДСВРознице,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыбытиеОС) КАК СчетДоходов,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеДоходов,
	|	ТаблицаТовары.ОсновноеСредство КАК СубконтоДоходов1
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = &Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиОС,
	|	ТаблицаТовары.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаОприходованиеТоваровНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ОсталисьМатериальныеЦенностиОтВыбытия Тогда
		ПараметрыПроведения.Вставить("РеквизитыНДС", Неопределено);
		ПараметрыПроведения.Вставить("ТоварыНДС", Неопределено);
		ПараметрыПроведения.Вставить("НомераГТД", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РеквизитыНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТоварыНДС",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НомераГТД",    НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.Количество КАК Количество
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	&ПлательщикНДС
	|	И НЕ ТаблицаТовары.СчетУчета.Забалансовый
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.НомерГТД КАК НомерГТД,
	|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаТовары.Количество КАК Количество
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И НЕ ТаблицаТовары.СчетУчета.Забалансовый
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаОприходованиеТоваровУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ОсталисьМатериальныеЦенностиОтВыбытия Тогда
		ПараметрыПроведения.Вставить("РеквизитыПоступлениеРасходовУСН", Неопределено);
		ПараметрыПроведения.Вставить("УСНТаблицаРасходов", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РеквизитыПоступлениеРасходовУСН", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКонтрагента,
	|	&ВалютаРеглУчета КАК Валюта,
	|	ЛОЖЬ КАК УчетАгентскогоНДС,
	|	ИСТИНА КАК ЭтоВозврат,
	|	ЛОЖЬ КАК РасходыПредпринимателя
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	&ОтражатьВРасходахУСН
	|	И Реквизиты.Ссылка = &Ссылка"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	НомераТаблиц.Вставить("УСНТаблицаРасходов", НомераТаблиц.Количество());
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Номенклатура) КАК ВидРасхода,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
	|	ТаблицаТовары.СчетУчета,
	|	ТаблицаТовары.Ссылка КАК Партия,
	|	ТаблицаТовары.Номенклатура КАК ЭлементРасхода,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СчетУчета В (&Счета10)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СчетУчета В (&Счета10)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоМатериал,
	|	ТаблицаТовары.ОтражениеВУСН,
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.Сумма,
	|	0 КАК НДС
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	НЕ ТаблицаТовары.СчетУчетаЗабалансовый
	|	И &ОтражатьВРасходахУСН"
	;

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаКнигаУчетаДоходовИРасходовУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Не Реквизиты.ОсталисьМатериальныеЦенностиОтВыбытия Тогда
		ПараметрыПроведения.Вставить("РеквизитыУСН", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РеквизитыУСН", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	0 КАК СтрокаДокумента,
	|	СУММА(ТаблицаТовары.Сумма) КАК Графа4,
	|	СУММА(ТаблицаТовары.Сумма) КАК Графа5,
	|	0 КАК Графа6,
	|	0 КАК Графа7,
	|	0 КАК НДС,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Документ.ИнвентаризацияТоваровНаСкладе.ПустаяСсылка) КАК ИнвентаризацияТоваровНаСкладе
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = &Ссылка)
	|ГДЕ
	|	&ПрименяетсяУСН
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.Ссылка,
	|	Реквизиты.Дата,
	|	Реквизиты.Организация,
	|	Реквизиты.Номер";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаОприходованиеТоваровИП(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ОсталисьМатериальныеЦенностиОтВыбытия Тогда
		ПараметрыПроведения.Вставить("ОприходованиеТоваровИПРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ОприходованиеТоваровИПТаблица", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ОприходованиеТоваровИПРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОприходованиеТоваровИПТаблица",   НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад.ТипСклада КАК ТипСклада,
	|	Реквизиты.СубконтоДоходов КАК СтатьяДоходов,
	|	Реквизиты.СубконтоДоходов.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
	|	ЕСТЬNULL(Реквизиты.СубконтоДоходов.ПринятиеКналоговомуУчету, ЛОЖЬ) КАК ПринятиеКналоговомуУчету
	|ИЗ
	|	Документ.СписаниеОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ЦенностиОтВыбытия"" КАК ИмяСписка,
	|	&СинонимЦенностиОтВыбытия КАК СинонимСписка,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
	|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
	|		ИНАЧЕ ТаблицаТовары.Номенклатура.НоменклатурнаяГруппа
	|	КОНЕЦ КАК НоменклатурнаяГруппа,
	|	ТаблицаТовары.Ссылка КАК Партия,
	|	ТаблицаТовары.Ссылка КАК ДокументОплаты,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Сумма КАК Сумма,
	|	ТаблицаТовары.СчетУчета
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	&ПлательщикНДФЛ
	|	И НЕ(&СпособОценкиТоваровВРознице = ЗНАЧЕНИЕ(Перечисление.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости)
	|				И &ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.НеавтоматизированнаяТорговаяТочка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТовары.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовВПоследовательности(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Реквизиты.Организация, Реквизиты.Период) Тогда
		ПараметрыПроведения.Вставить("ТаблицаРегистрации", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ТаблицаРегистрации", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчетСписания
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

// Функция формирует табличный документ с печатной формой накладной
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьОприходованияМатериалов(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеОС_Накладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СписаниеОС.ПФ_MXL_Накладная");
	
	ДополнительнаяКолонкаПечатныхФормДокументов = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если НЕ ЗначениеЗаполнено(ДополнительнаяКолонкаПечатныхФормДокументов) Тогда
		ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить;
	КонецЕсли;
	ВыводитьКоды = ДополнительнаяКолонкаПечатныхФормДокументов <> Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить;
	
	ДанныеДляПечатиНакладной = ДанныеДляПечатиНакладной(МассивОбъектов, ДополнительнаяКолонкаПечатныхФормДокументов);
	
	ПервыйДокумент = Истина;
	
	Для Каждого Шапка Из ДанныеДляПечатиНакладной Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выводим шапку накладной
		
		СведенияОбОрганизации    = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата);
		ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
		
		ЗначенияПечатнойФормы = Новый Структура();
		ЗначенияПечатнойФормы.Вставить("ТекстЗаголовка", ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(
			Шапка, НСтр("ru = 'Оприходование материалов при списании ОС'")));
		ЗначенияПечатнойФормы.Вставить("ПредставлениеПолучателя", ПредставлениеОрганизации);
		ЗначенияПечатнойФормы.Вставить("Получатель", Шапка.Организация);
		ЗначенияПечатнойФормы.Вставить("Склад", Шапка.Склад);
		ЗначенияПечатнойФормы.Вставить("ПредставлениеСклада", Шапка.ПредставлениеСклада);
		
		Если ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
			ЗначенияПечатнойФормы.Вставить("ИмяКодАртикул", "Артикул");
		ИначеЕсли ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Код Тогда
			ЗначенияПечатнойФормы.Вставить("ИмяКодАртикул", "Код");
		КонецЕсли;
		
		ЗначенияПечатнойФормы.Вставить("Всего", Шапка.Материалы.Итог("Сумма"));
		ЗначенияПечатнойФормы.Вставить("ИтоговаяСтрока",
			СтрШаблон(НСтр("ru = 'Всего наименований %1 , на сумму %2'"),
				Шапка.Материалы.Количество(),
				ОбщегоНазначенияБПВызовСервера.ФорматСумм(ЗначенияПечатнойФормы.Всего, Шапка.ВалютаДокумента)));
		ЗначенияПечатнойФормы.Вставить("СуммаПрописью",
			ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(ЗначенияПечатнойФормы.Всего, Шапка.ВалютаДокумента));
		
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.Заполнить(ЗначенияПечатнойФормы);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
		ОбластьМакета.Параметры.Заполнить(ЗначенияПечатнойФормы);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Если Справочники.Склады.ИспользуетсяНесколькоСкладов() Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Склад");
			ОбластьМакета.Параметры.Заполнить(ЗначенияПечатнойФормы);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		ИмяОбластиШапки = ?(ВыводитьКоды, "ШапкаСКодом", "ШапкаТаблицы");
		ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбластиШапки);
		ОбластьМакета.Параметры.Заполнить(ЗначенияПечатнойФормы);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ИмяОбластиСтроки = ?(ВыводитьКоды, "СтрокаСКодом", "Строка");
		ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбластиСтроки);
		
		Для Каждого СтрокаМатериала Из Шапка.Материалы Цикл
			ОбластьМакета.Параметры.Заполнить(СтрокаМатериала);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
		// Вывести Итого
		ОбластьМакета = Макет.ПолучитьОбласть("Итого");
		ОбластьМакета.Параметры.Заполнить(ЗначенияПечатнойФормы);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Вывести Сумму прописью
		ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
		ОбластьМакета.Параметры.Заполнить(ЗначенияПечатнойФормы);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Вывести подписи
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	
	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

Функция ДанныеДляПечатиНакладной(МассивОбъектов, ДополнительнаяКолонкаПечатныхФормДокументов)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", ДополнительнаяКолонкаПечатныхФормДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеОС.Ссылка КАК Ссылка,
	|	СписаниеОС.Номер КАК Номер,
	|	СписаниеОС.Дата КАК Дата,
	|	СписаниеОС.Организация КАК Организация,
	|	СписаниеОС.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Константы.ВалютаРегламентированногоУчета КАК ВалютаДокумента,
	|	СписаниеОС.Склад КАК Склад,
	|	СписаниеОС.Склад.Представление КАК ПредставлениеСклада
	|ИЗ
	|	Документ.СписаниеОС КАК СписаниеОС,
	|	Константы КАК Константы
	|ГДЕ
	|	СписаниеОС.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписаниеОС.Дата,
	|	СписаниеОС.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеОСЦенностиОтВыбытия.Ссылка КАК Ссылка,
	|	СписаниеОСЦенностиОтВыбытия.Номенклатура КАК Номенклатура,
	|	СписаниеОСЦенностиОтВыбытия.Номенклатура.НаименованиеПолное КАК Товар,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА СписаниеОСЦенностиОтВыбытия.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА СписаниеОСЦенностиОтВыбытия.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ КАК КодАртикул,
	|	СписаниеОСЦенностиОтВыбытия.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаХранения,
	|	СписаниеОСЦенностиОтВыбытия.Цена КАК Цена,
	|	СУММА(СписаниеОСЦенностиОтВыбытия.Количество) КАК Количество,
	|	МИНИМУМ(СписаниеОСЦенностиОтВыбытия.НомерСтроки) КАК НомерСтроки,
	|	СУММА(СписаниеОСЦенностиОтВыбытия.Сумма) КАК Сумма
	|ИЗ
	|	Документ.СписаниеОС.ЦенностиОтВыбытия КАК СписаниеОСЦенностиОтВыбытия
	|ГДЕ
	|	СписаниеОСЦенностиОтВыбытия.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеОСЦенностиОтВыбытия.Ссылка,
	|	СписаниеОСЦенностиОтВыбытия.Номенклатура,
	|	СписаниеОСЦенностиОтВыбытия.Номенклатура.НаименованиеПолное,
	|	СписаниеОСЦенностиОтВыбытия.Номенклатура.ЕдиницаИзмерения.Представление,
	|	СписаниеОСЦенностиОтВыбытия.Цена,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА СписаниеОСЦенностиОтВыбытия.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА СписаниеОСЦенностиОтВыбытия.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписаниеОСЦенностиОтВыбытия.Ссылка,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечати = РезультатЗапроса[0].Выгрузить();
	ДанныеДляПечати.Колонки.Добавить("Материалы", Новый ОписаниеТипов("ТаблицаЗначений"));
	ДанныеСтрок = РезультатЗапроса[1].Выгрузить();
	
	Для Каждого СтрокаПечати Из ДанныеДляПечати Цикл
		ОтборСтрок = Новый Структура("Ссылка", СтрокаПечати.Ссылка);
		СтрокаПечати.Материалы = ДанныеСтрок.Скопировать(ОтборСтрок);
		СтрокаПечати.Материалы.Сортировать("НомерСтроки");
		
		// Т.к. используется группировка по строкам,
		// то в номерах строк из запроса могут быть пробелы.
		// Поэтому перенумеруем строки заново.
		НомерСтроки = 1;
		Для Каждого СтрокаМатериала Из СтрокаПечати.Материалы Цикл
			СтрокаМатериала.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеДляПечати;
	
КонецФункции

Функция ПодготовитьПараметрыОтраженияДоходовОтОприходованияТоваров(ДоходыОтОприходованияТоваров, ТаблицаРеквизиты)

	Параметры = Новый Структура;

	// Подготовка таблицы Параметры.ДоходыОтОприходованияТоваров

	СписокОбязательныхКолонок = ""
	+ "ОсновноеСредство,"  // <СправочникСсылка.ОсновныеСредства> - основное средство, по которому получен доход от оприходования ценностей.
	+ "СуммаНУ";           // <Число,15,2> - сумма в рублях в налоговом учете
	
	Параметры.Вставить("ДоходыОтОприходованияТоваров",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ДоходыОтОприходованияТоваров, СписокОбязательныхКолонок));

	// Подготовка таблицы Параметры.Реквизиты

	СписокОбязательныхКолонок = ""
	+ "Регистратор,"                   // <ДокументСсылка.*> - документ-регистратор движений
	+ "Период,"                        // <Дата> - период движений - дата документа
	+ "Организация,"                   // <СправочникСсылка.Организация> - организация, в которую приходуется товар
	+ "Подразделение,"                 // <Ссылка на справочник подразделений> - подразделение, в которое приходуется товар
	+ "СчетУчетаДоходов,"              // <ПланСчетовСсылка.Хозрасчетный> - счет, по которому отражаются доходы.
	+ "СубконтоДоходов";               // <ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Значение> - аналитика доходов.

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	Возврат Параметры;

КонецФункции

Функция ТекстЗапросаАктОСписанииОС()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписаниеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписаниеОС.Ссылка.Организация КАК Организация
	|ПОМЕСТИТЬ ОсновныеСредства
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК СписаниеОС
	|ГДЕ
	|	СписаниеОС.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеОС.Номер КАК НомерДокумента,
	|	СписаниеОС.Дата КАК ДатаДокумента,
	|	СписаниеОС.Организация КАК Организация,
	|	СписаниеОС.Ссылка КАК Ссылка,
	|	СписаниеОС.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА СписаниеОС.ПодразделениеОрганизации.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА СписаниеОС.ПодразделениеОрганизации.Наименование
	|		ИНАЧЕ СписаниеОС.ПодразделениеОрганизации.НаименованиеПолное
	|	КОНЕЦ КАК ПредставлениеПодразделения,
	|	СписаниеОС.ПричинаСписания КАК ПричинаСписания,
	|	СписаниеОС.ОС.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ОсновноеСредство КАК ОсновноеСредство,
	|		ВЫБОР
	|			КОГДА СписаниеОС.ОС.ОсновноеСредство.НаименованиеПолное ПОДОБНО """"
	|				ТОГДА СписаниеОС.ОС.ОсновноеСредство.Наименование
	|			ИНАЧЕ СписаниеОС.ОС.ОсновноеСредство.НаименованиеПолное
	|		КОНЕЦ КАК ОсновноеСредствоНаименование,
	|		&ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество
	|	) КАК ОсновныеСредства
	|ИЗ
	|	Документ.СписаниеОС КАК СписаниеОС
	|ГДЕ
	|	СписаниеОС.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписаниеОС.Дата,
	|	СписаниеОС.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Проводки01.Регистратор КАК Регистратор,
	|	Проводки01.НомерСтроки КАК НомерСтроки,
	|	Проводки01.Сумма КАК Сумма,
	|	Проводки01.КоличествоКт КАК Количество
	|ПОМЕСТИТЬ Проводки01
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Проводки01
	|ГДЕ
	|	Проводки01.Регистратор В(&МассивОбъектов)
	|	И Проводки01.СчетДт = &Счет0109
	|	И Проводки01.СчетКт В(&Счета01)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Проводки02.Регистратор КАК Регистратор,
	|	Проводки02.НомерСтроки КАК НомерСтроки,
	|	Проводки02.Сумма КАК Сумма
	|ПОМЕСТИТЬ Проводки02
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Проводки02
	|ГДЕ
	|	Проводки02.Регистратор В(&МассивОбъектов)
	|	И Проводки02.СчетКт = &Счет0109
	|	И Проводки02.СчетДт В(&Счета02)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Проводки01.Регистратор КАК Регистратор,
	|	Субконто01.Значение КАК ОсновноеСредство,
	|	Проводки01.Сумма КАК Сумма01,
	|	0 КАК Сумма02,
	|	Проводки01.Количество КАК Количество
	|ПОМЕСТИТЬ ДанныеПоОС
	|ИЗ
	|	Проводки01 КАК Проводки01
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК Субконто01
	|		ПО Проводки01.Регистратор = Субконто01.Регистратор
	|			И Проводки01.НомерСтроки = Субконто01.НомерСтроки
	|			И (Субконто01.Вид = &ВидСубконтоОС)
	|			И (Субконто01.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Проводки02.Регистратор,
	|	Субконто02.Значение,
	|	0,
	|	Проводки02.Сумма,
	|	0
	|ИЗ
	|	Проводки02 КАК Проводки02
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК Субконто02
	|		ПО Проводки02.Регистратор = Субконто02.Регистратор
	|			И Проводки02.НомерСтроки = Субконто02.НомерСтроки
	|			И (Субконто02.Вид = &ВидСубконтоОС)
	|			И (Субконто02.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоОС.Регистратор КАК Ссылка,
	|	ДанныеПоОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ДанныеПоОС.Сумма01) КАК ПервоначальнаяСтоимость,
	|	СУММА(ДанныеПоОС.Сумма01) - СУММА(ДанныеПоОС.Сумма02) КАК ОстаточнаяСтоимость,
	|	СУММА(ДанныеПоОС.Количество) КАК Количество
	|ИЗ
	|	ДанныеПоОС КАК ДанныеПоОС
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоОС.Регистратор,
	|	ДанныеПоОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОС.ИнвентарныйНомер КАК ИнвентарныйНомер
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|			,
	|			(ОсновноеСредство, Организация) В
	|				(ВЫБРАТЬ
	|					ОсновныеСредства.ОсновноеСредство,
	|					ОсновныеСредства.Организация
	|				ИЗ
	|					ОсновныеСредства)) КАК ПервоначальныеСведенияОС";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПечатьАктОСписанииОС(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АктОСписанииОС";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_АктОСписанииОС");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапросаАктОСписанииОС();
	ОсновнаяЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ПолучитьЕдиницуИзмеренияПоУмолчанию();
	Запрос.УстановитьПараметр("ОсновнаяЕдиницаИзмерения", ОсновнаяЕдиницаИзмерения);
	Запрос.УстановитьПараметр("Счет0109", ПланыСчетов.Хозрасчетный.ВыбытиеОС);
	Счета01 = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	Запрос.УстановитьПараметр("Счета01", Счета01);
	Счета02 = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.АмортизацияОсновныхСредств);
	Запрос.УстановитьПараметр("Счета02", Счета02);
	Запрос.УстановитьПараметр("ВидСубконтоОС", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаСумм = РезультатыЗапроса[5].Выгрузить();
	ТаблицаСумм.Индексы.Добавить("Ссылка,ОсновноеСредство");
	
	ИнвентарныеНомера = РезультатыЗапроса[6].Выгрузить();
	ИнвентарныеНомера.Индексы.Добавить("ОсновноеСредство");
	
	ПервыйДокумент = Истина;
	ВыборкаДокумент = РезультатыЗапроса[1].Выбрать();
	Пока ВыборкаДокумент.Следующий() Цикл
		
		ОсновныеСредства = ВыборкаДокумент.ОсновныеСредства.Выгрузить();
		ОсновныеСредства.Сортировать("НомерСтроки");
		
		Если ОсновныеСредства.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		
		ОбластьМакета.Параметры.Заполнить(ВыборкаДокумент);
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаДокумент.Организация, ВыборкаДокумент.ДатаДокумента);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации);
		ОбластьМакета.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокумент.НомерДокумента, Истина, Ложь);
		ОбластьМакета.Параметры.ДатаДокумента = Формат(ВыборкаДокумент.ДатаДокумента, "ДФ='dd.MM.yyyy ""г.""'");
		
		ПодразделениеОтветственныхЛиц = ВыборкаДокумент.ПодразделениеОрганизации;
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(ВыборкаДокумент.Организация, ВыборкаДокумент.ДатаДокумента, ПодразделениеОтветственныхЛиц);
		ОбластьМакета.Параметры.ФИОРуководителя = Руководители.РуководительПредставление;
		
		Если СведенияОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
			И СведенияОбОрганизации.ИндивидуальныйПредприниматель = Руководители.Руководитель Тогда
			ЭтоИндивидуальныйПредприниматель = Истина;
			ОбластьМакета.Параметры.ДолжностьРуководителя = НСтр("ru = 'Индивидуальный предприниматель'");
		Иначе
			ЭтоИндивидуальныйПредприниматель = Ложь;
			ОбластьМакета.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжностьПредставление;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		ИтогоПервоначальнаяСтоимость = 0;
		ИтогоОстаточнаяСтоимость = 0;
		
		Для каждого СтрокаОС Из ОсновныеСредства Цикл
			
			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("НомерСтроки", СтрокаОС.НомерСтроки);
			ПараметрыЗаполнения.Вставить("ОсновноеСредствоНаименование", СтрокаОС.ОсновноеСредствоНаименование);
			ПараметрыЗаполнения.Вставить("ЕдиницаИзмерения", СтрокаОС.ЕдиницаИзмерения);
			ПараметрыЗаполнения.Вставить("Количество", СтрокаОС.Количество);
			
			СтрокаСведений = ИнвентарныеНомера.Найти(СтрокаОС.ОсновноеСредство, "ОсновноеСредство");
			Если СтрокаСведений = Неопределено Тогда
				ПараметрыЗаполнения.Вставить("ИнвНомер", "-");
			Иначе
				ПараметрыЗаполнения.Вставить("ИнвНомер", СтрокаСведений.ИнвентарныйНомер);
			КонецЕсли;
			
			ОтборСтрок = Новый Структура("Ссылка,ОсновноеСредство", СтрокаОС.Ссылка, СтрокаОС.ОсновноеСредство);
			СтрокиСумм = ТаблицаСумм.НайтиСтроки(ОтборСтрок);
			Если СтрокиСумм.Количество() = 0 Тогда
				ПервоначальнаяСтоимость = 0;
				ОстаточнаяСтоимость = 0;
			Иначе
				Коэффициент = СтрокаОС.Количество / ?(СтрокиСумм[0].Количество = 0, 1, СтрокиСумм[0].Количество);
				ПервоначальнаяСтоимость = Окр(СтрокиСумм[0].ПервоначальнаяСтоимость * Коэффициент, 2);
				ОстаточнаяСтоимость = Окр(СтрокиСумм[0].ОстаточнаяСтоимость * Коэффициент, 2);
			КонецЕсли;
			ПараметрыЗаполнения.Вставить("ПервоначальнаяСтоимость", ПервоначальнаяСтоимость);
			ПараметрыЗаполнения.Вставить("ОстаточнаяСтоимость", ОстаточнаяСтоимость);
			
			ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ИтогоПервоначальнаяСтоимость = ИтогоПервоначальнаяСтоимость + ПервоначальнаяСтоимость;
			ИтогоОстаточнаяСтоимость = ИтогоОстаточнаяСтоимость + ОстаточнаяСтоимость;
			
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("КонецТаблицы");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим итоги по документу
		ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
		ОбластьМакета.Параметры.ИтогоПервоначальнаяСтоимость = Формат(ИтогоПервоначальнаяСтоимость, "ЧДЦ=2");
		ОбластьМакета.Параметры.ИтогоОстаточнаяСтоимость = Формат(ИтогоОстаточнаяСтоимость, "ЧДЦ=2");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		СоставКомиссии = Новый ТаблицаЗначений;
		ФиоФизЛиц = Неопределено;
		
		// Для предпринимателя, которому комиссия не нужна, выводим специальный подвал документа
		Если ЭтоИндивидуальныйПредприниматель И СоставКомиссии.Количество() = 0 Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Предприниматель");
			ОбластьМакета.Параметры.ФИОПредпринимателя = СведенияОбОрганизации.ФамилияИнициалыФизлица;
			ТабличныйДокумент.Вывести(ОбластьМакета);
		Иначе
			ВывестиКомиссию(ТабличныйДокумент, Макет, СоставКомиссии, ФиоФизЛиц);
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокумент.Ссылка);
	КонецЦикла;
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("СписаниеОС.Печать.АктОСписанииОС");
	
	Возврат ТабличныйДокумент;

КонецФункции

Процедура ВывестиКомиссию(ТабличныйДокумент, Макет, СоставКомиссии, ФиоФизЛиц)

	//комиссия не заполнена: вариант по умолчанию (4 члена комиссии)
	Если СоставКомиссии.Количество() = 0 Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ПредседательКомиссии");
		ОбластьМакета.Параметры.ПредседательКомиссии = НСтр("ru = 'Председатель комиссии:'");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ЧленКомиссии");
		ОбластьМакета.Параметры.ЧленыКомиссии = НСтр("ru = 'Члены комиссии:'");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета.Параметры.ЧленыКомиссии = "";
		Для Сч = 2 По 3 Цикл
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		Возврат;
	КонецЕсли; 
	
	//Комиссия заполнена
	ОбластьМакета = Макет.ПолучитьОбласть("ПредседательКомиссии");
	Если СоставКомиссии.Количество() <> 1 Тогда
		ОбластьМакета.Параметры.ПредседательКомиссии = НСтр("ru = 'Председатель комиссии:'");
	КонецЕсли;
	ОбластьМакета.Параметры.ДолжностьПредседателя = СоставКомиссии[0].ДолжностьНаименование;
	ОбластьМакета.Параметры.ФИОПредседателя = ПредставлениеФИОЧленаКомиссии(ФИОФизЛиц, СоставКомиссии[0].ФизЛицо);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ЧленКомиссии");
	Если СоставКомиссии.Количество() <> 1 Тогда
		ОбластьМакета.Параметры.ЧленыКомиссии = НСтр("ru = 'Члены комиссии:'");
	КонецЕсли;
			
	Для ИндексСтроки = 1 По СоставКомиссии.Количество() - 1 Цикл
		Строка = СоставКомиссии[ИндексСтроки];
		Если ИндексСтроки > 1 Тогда
			ОбластьМакета.Параметры.ЧленыКомиссии = ""; 
		КонецЕсли;
		ОбластьМакета.Параметры.ДолжностьЧленКомиссии = Строка.ДолжностьНаименование;
		ОбластьМакета.Параметры.ФИОЧленКомиссии = ПредставлениеФИОЧленаКомиссии(ФИОФизЛиц, Строка.ФизЛицо);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
КонецПроцедуры

Функция ПредставлениеФИОЧленаКомиссии(ФИОФизЛиц, ФизЛицо)
	
	ФИОФизЛица = ФИОФизЛиц.Найти(ФизЛицо, "ФизическоеЛицо");
	Если ФИОФизЛица = Неопределено Тогда
		Представление = "";
	Иначе
		Представление = ФИОФизЛица.ФамилияИО;
	КонецЕсли;
	Возврат Представление;
	
КонецФункции

#КонецОбласти

#Область СчетаУчета

Функция УстановитьПравилаЗаполненияСчетовУчета(Правила) Экспорт
	
	// Шапка
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетСписания", "РасходыОтСписанияОС");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто", "СубконтоЗатрат1");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаДоходов", "ДоходыОтСписанияОС");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СубконтоДоходов", "СубконтоПрочихДоходов1");
	
	// Табличная часть ЦенностиОтВыбытия
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ЦенностиОтВыбытия", "СчетУчета", "ЗапасыКромеЗабалансовых");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхЗаполнения(Правила, "Склад", "Склад");
	
	// Данные заполнения
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Дата");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Организация");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Склад");
	
	Возврат Правила;
	
КонецФункции

Функция РеквизитыЗаСсылками() Экспорт
	
	РеквизитыЗаСсылками = Новый Соответствие;
	
	// Прячутся за "Расходы и доходы"
	РеквизитыЗаСсылками.Вставить("СчетСписания", "ОписаниеДоходовИРасходов");
	РеквизитыЗаСсылками.Вставить("Субконто", "ОписаниеДоходовИРасходов");
	РеквизитыЗаСсылками.Вставить("СчетУчетаДоходов", "ОписаниеДоходовИРасходов");
	РеквизитыЗаСсылками.Вставить("СубконтоДоходов", "ОписаниеДоходовИРасходов");
	
	// Прячутся за "ЦенностиОтВыбытия"
	РеквизитыЗаСсылками.Вставить("ЦенностиОтВыбытия.Номенклатура", "ЦенностиОтВыбытия");
	РеквизитыЗаСсылками.Вставить("ЦенностиОтВыбытия.Количество", "ЦенностиОтВыбытия");
	РеквизитыЗаСсылками.Вставить("ЦенностиОтВыбытия.СчетУчета", "ЦенностиОтВыбытия");
	
	Возврат РеквизитыЗаСсылками;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьПустоеКоличество(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	СписаниеОС.Ссылка КАК Ссылка,
	|	СписаниеОС.Дата КАК Дата
	|ИЗ
	|	Документ.СписаниеОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС КАК СписаниеОС
	|		ПО ТаблицаОС.Ссылка = СписаниеОС.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК СправочникОС
	|		ПО ТаблицаОС.ОсновноеСредство = СправочникОС.Ссылка
	|ГДЕ
	|	ТаблицаОС.Количество = 0
	|	И СправочникОС.ЕдиницаУчета <> ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ,
	|	Ссылка УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектовОбработано = ОбъектовОбработано + 1;
		
		НачатьТранзакцию();
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.СписаниеОС");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		
		Попытка
		
			Блокировка.Заблокировать();
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Для Каждого СтрокаТЧ Из ДокументОбъект.ОС Цикл
				СтрокаТЧ.Количество = ?(СтрокаТЧ.Количество = 0, 1, СтрокаТЧ.Количество);
			КонецЦикла;
		
			// Запись обработанного объекта.
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо объект, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре Документы.СписаниеОС.ЗаполнитьПустоеКоличество() не удалось обработать элемент по причине:
					|%1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.СписаниеОС, Выборка.Ссылка, ТекстСообщения);
				
		КонецПопытки; 
		
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В процедуре Документы.СписаниеОС.ЗаполнитьПустоеКоличество() не удалось обработать все элементы: в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.СписаниеОС, ,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Документы.СписаниеОС.ЗаполнитьПустоеКоличество() обработала очередную порцию: %1 элементов'"), 
					ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиДанныеПоРНПТВТаблицуСведенияПрослеживаемости(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	Если Не Константы.ВестиУчетПрослеживаемыхТоваров.Получить() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрослеживаемыеОсновныеСредстваОбороты.Регистратор КАК Регистратор,
	|	ПрослеживаемыеОсновныеСредстваОбороты.РНПТ КАК РНПТ,
	|	ПрослеживаемыеОсновныеСредстваОбороты.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПрослеживаемыеОсновныеСредстваОбороты.ОсновноеСредство КАК ОсновноеСредство,
	|	ПрослеживаемыеОсновныеСредстваОбороты.Комплектующие КАК Номенклатура,
	|	ПрослеживаемыеОсновныеСредстваОбороты.КоличествоРасход КАК Количество,
	|	ПрослеживаемыеОсновныеСредстваОбороты.КоличествоПрослеживаемостиРасход КАК КоличествоПрослеживаемости
	|ПОМЕСТИТЬ СписанияОСПоРегистру
	|ИЗ
	|	РегистрНакопления.ПрослеживаемыеОсновныеСредства.Обороты(, , Регистратор, ) КАК ПрослеживаемыеОсновныеСредстваОбороты
	|ГДЕ
	|	ПрослеживаемыеОсновныеСредстваОбороты.Регистратор ССЫЛКА Документ.СписаниеОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеОС.Ссылка КАК Ссылка,
	|	СписаниеОСОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписаниеОСОС.КлючСтроки КАК КлючСтроки,
	|	СписаниеОССведенияПрослеживаемости.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СписаниеОССведенияПрослеживаемости.Номенклатура КАК Номенклатура,
	|	СписаниеОССведенияПрослеживаемости.Количество КАК Количество,
	|	СписаниеОССведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	СписаниеОССведенияПрослеживаемости.РНПТ КАК РНПТ
	|ПОМЕСТИТЬ ДокументыСписанияОС
	|ИЗ
	|	Документ.СписаниеОС КАК СписаниеОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК СписаниеОСОС
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеОС.СведенияПрослеживаемости КАК СписаниеОССведенияПрослеживаемости
	|			ПО СписаниеОСОС.Ссылка = СписаниеОССведенияПрослеживаемости.Ссылка
	|				И СписаниеОСОС.КлючСтроки = СписаниеОССведенияПрослеживаемости.ИдентификаторСтроки
	|		ПО СписаниеОС.Ссылка = СписаниеОСОС.Ссылка
	|ГДЕ
	|	СписаниеОС.Проведен = ИСТИНА
	|	И СписаниеОС.Дата >= &ДатаВступленияВСилуПрослеживаемости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписанияОСПоРегистру.Регистратор КАК Регистратор,
	|	СписанияОСПоРегистру.РНПТ КАК РНПТ,
	|	СписанияОСПоРегистру.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СписанияОСПоРегистру.ОсновноеСредство КАК ОсновноеСредство,
	|	СписанияОСПоРегистру.Номенклатура КАК Номенклатура,
	|	СписанияОСПоРегистру.Количество КАК Количество,
	|	СписанияОСПоРегистру.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ДокументыСписанияОС.РНПТ КАК РНПТИзТЧ
	|ПОМЕСТИТЬ ДаннныеПоСписаниям
	|ИЗ
	|	СписанияОСПоРегистру КАК СписанияОСПоРегистру
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыСписанияОС КАК ДокументыСписанияОС
	|		ПО СписанияОСПоРегистру.Регистратор = ДокументыСписанияОС.Ссылка
	|			И СписанияОСПоРегистру.ОсновноеСредство = ДокументыСписанияОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ДаннныеПоСписаниям.Регистратор КАК Регистратор,
	|	ДаннныеПоСписаниям.РНПТ КАК РНПТ,
	|	ДаннныеПоСписаниям.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ДаннныеПоСписаниям.ОсновноеСредство КАК ОсновноеСредство,
	|	ДаннныеПоСписаниям.Номенклатура КАК Номенклатура,
	|	ДаннныеПоСписаниям.Количество КАК Количество,
	|	ДаннныеПоСписаниям.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости
	|ИЗ
	|	ДаннныеПоСписаниям КАК ДаннныеПоСписаниям
	|ГДЕ
	|	ДаннныеПоСписаниям.РНПТИзТЧ ЕСТЬ NULL
	|ИТОГИ ПО
	|	Регистратор,
	|	ОсновноеСредство";
	
	Запрос.УстановитьПараметр("ДатаВступленияВСилуПрослеживаемости", ПрослеживаемостьБРУ.ДатаНачалаУчетаПрослеживаемыхТоваров());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.СписаниеОС");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Регистратор);
		
		Попытка
		
			Блокировка.Заблокировать();
			ДокументОбъект = Выборка.Регистратор.ПолучитьОбъект();
			
			ВыборкаПоОС = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоОС.Следующий() Цикл
				Если ВыборкаПоОС.ОсновноеСредство <> Неопределено 
					И ВыборкаПоОС.ОсновноеСредство <> Справочники.ОсновныеСредства.ПустаяСсылка() Тогда
					СтрокаОС = ДокументОбъект.ОС.Найти(ВыборкаПоОС.ОсновноеСредство, "ОсновноеСредство");
					Если СтрокаОС = Неопределено Тогда
						Продолжить;
					Иначе
						Если Не ЗначениеЗаполнено(СтрокаОС.КлючСтроки) Тогда
							СтрокаОС.КлючСтроки = Новый УникальныйИдентификатор;
						КонецЕсли;
						ВыборкаПоСведениям = ВыборкаПоОС.Выбрать();
						Пока ВыборкаПоСведениям.Следующий() Цикл
							НоваяСтрока = ДокументОбъект.СведенияПрослеживаемости.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСведениям);
							НоваяСтрока.ИдентификаторСтроки = СтрокаОС.КлючСтроки;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
					
			// Запись обработанного объекта.
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось сохранить данные об РНПТ в таблице ""Сведения прослеживаемости"":
					|%1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.СписаниеОС,
				Выборка.Регистратор,
				ТекстСообщения);
				
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

