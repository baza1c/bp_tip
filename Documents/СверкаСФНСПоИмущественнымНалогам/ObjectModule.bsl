
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		 И ДанныеЗаполнения.Свойство("ЗаполнениеИзОбъекта")
		 И ДанныеЗаполнения.ЗаполнениеИзОбъекта Тогда
		// Заполнение выполняется из переданного объекта при создании формы 
		Возврат; 
	КонецЕсли; 

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("КодыНалоговыхОрганов") Тогда
		КодыНалоговыхОрганов = ДанныеЗаполнения.КодыНалоговыхОрганов;
	Иначе
		КодыНалоговыхОрганов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодОтправителя);
	КонецЕсли;

	ЗаполнитьОбъектыСверки(КодыНалоговыхОрганов);
	
	Если Не ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		// Ищем регистрацию, которая соответствует коду отправителя
		РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(
			Организация,
			,
			КодОтправителя);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(РегистрацияВНалоговомОргане) И Сверка.Количество() > 0 Тогда
		// В базе может не быть регистрации с кодом отправителя. Но если сверка заполнялась по переданному массиву кодов НО,
		// то в таблице могут быть другие коды. Поэтому ищем регистрацию по первому коду налогового органа из сверки - 
		// такая регистрация точно есть, т.к. на ней зарегистрирован объект.
		РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(
			Организация,
			,
			Сверка[0].КодНалоговогоОргана);
	КонецЕсли;

	РасчетФНС.Очистить();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("СообщениеФНС") Тогда
			ЗагрузитьРасчетФНСИзСообщения(ДанныеЗаполнения.СообщениеФНС);
			ЗагруженРасчетФНС = Истина;
			СравнениеЗавершено = РасчетыСовпадают();
		ИначеЕсли ДанныеЗаполнения.Свойство("Файл") Тогда
			// Сбросим признак наличия объекта в расчете ФНС - это будет установлено при сверке
			Для Каждого СтрокаСверки Из Сверка Цикл
				СтрокаСверки.ОбъектЕстьВРасчетеФНС = Ложь;
			КонецЦикла;
			ЗагрузитьРасчетФНСИзФайла(ДанныеЗаполнения.Файл.Данные, ДанныеЗаполнения.Файл.ИмяФайла);
			ЗагруженРасчетФНС = Истина;
			СравнениеЗавершено = РасчетыСовпадают();
		КонецЕсли;
	КонецЕсли;

	Если Не ЗагруженРасчетФНС Тогда
		// При ручной сверке по умолчанию считаем, что расчеты сходятся.
		// Поэтому в строках сверки заполним суммы расчета ФНС по расчету программы.
		// Детали расчета ФНС не заполняем - это сделает пользователь, если обнаружит, что расчет по объекту отличается.
		Для Каждого СтрокаСверки Из Сверка Цикл
			СтрокаСверки.СуммаЛьготФНС = СтрокаСверки.СуммаЛьгот;
			СтрокаСверки.СуммаНалогаФНС = СтрокаСверки.СуммаНалога;
			СтрокаСверки.ОбъектЕстьВРасчетеФНС = Истина;
		КонецЦикла;
	КонецЕсли;

	// При автоматической сверке расчетов есть смысл отсортировать строки по ключевым полям сопоставления.
	// Это полезно в том случае, если один и тот же объект не сопоставился из-за расхождений в каком-то из этих полей -
	// тогда в отсортированном списке это будет заметно.
	Если ЗагруженРасчетФНС Тогда
		ТаблицаСверки = Сверка.Выгрузить();
		ТаблицаСверки.Колонки.Добавить("Сортировка", ОбщегоНазначения.ОписаниеТипаСтрока(100));
		Для каждого СтрокаСверки Из ТаблицаСверки Цикл
			Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
				СтрокаСверки.Сортировка = Врег(СтрЗаменить(СтрокаСверки.РегистрационныйЗнак, " ", ""));
			Иначе
				СтрокаСверки.Сортировка = Врег(СтрЗаменить(СтрокаСверки.КадастровыйНомер, " ", ""));
			КонецЕсли;
		КонецЦикла;
		ТаблицаСверки.Сортировать("Сортировка, КодНалоговогоОргана, КодПоОКТМО");
		Сверка.Загрузить(ТаблицаСверки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ПометкаУдаления Тогда 
	
		ДокументСверкаСФНС = Документы.СверкаСФНСПоИмущественнымНалогам.НайтиСуществующий(
			Организация, Налог, НалоговыйПериод, КодОтправителя, Ссылка);
			
		Если ЗначениеЗаполнено(ДокументСверкаСФНС) Тогда
			ВызватьИсключение Нстр("ru='Такая сверка расчета уже существует. Дублирование запрещено.'");
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьОбъектыСверки(КодыНалоговыхОрганов) 

	Сверка.Очистить();
	Расчет.Очистить();
	
	Запрос = Новый Запрос;
	
	ПериодРасчета = СверкаСФНСПоИмущественнымНалогамФормыКлиентСервер.ПериодРасчетаНалога(НалоговыйПериод);
	
	Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		Запрос.Текст = ТекстЗапросаТранспортныеСредства();
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		Запрос.Текст = ТекстЗапросаЗемельныеУчастки();
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		Запрос.Текст = ТекстЗапросаИмущество();
		ИспользоватьПрежнийРасчетНалога = Ложь;
		Если ПериодРасчета < РасчетИмущественныхНалогов.НачалоПримененияНовогоРасчетаНалогаНаИмущество()
		 Или РасчетИмущественныхНалоговПереопределяемый.ИспользоватьПрежнийРасчетНалогаНаИмуществоВПереходныйПериод(Организация, ПериодРасчета) Тогда
		 	ИспользоватьПрежнийРасчетНалога = Истина;
		КонецЕсли;
		Запрос.УстановитьПараметр("ИспользоватьПрежнийРасчетНалога", ИспользоватьПрежнийРасчетНалога);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
	Запрос.УстановитьПараметр("КодыНалоговыхОрганов", КодыНалоговыхОрганов);
	Запрос.УстановитьПараметр("ПериодОсобогоКонтроля", 
		СверкаСФНСПоИмущественнымНалогамФормыКлиентСервер.ПериодИзмененийДляОсобогоКонтроля(НалоговыйПериод));
	
	ВыборкаОбъектыСверки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбъектыСверки.Следующий() Цикл

		СтрокаСверки = Сверка.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСверки, ВыборкаОбъектыСверки);
		СтрокаСверки.ОбъектЕстьВРасчете = Истина;

		ИдентификаторСтроки = Новый УникальныйИдентификатор();
		СтрокаСверки.ИдентификаторСтроки = ИдентификаторСтроки;

		ВыборкаРасчет = ВыборкаОбъектыСверки.Выбрать();

		Пока ВыборкаРасчет.Следующий() Цикл

			СтрокаРасчета = Расчет.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасчета, ВыборкаРасчет);
			СтрокаРасчета.ИдентификаторСтроки = ИдентификаторСтроки;

			Если Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
				СтрокаРасчета.ДоляВПравеЧислитель = 
					?(ЗначениеЗаполнено(ВыборкаРасчет.ДоляВПравеОбщейСобственностиЧислитель), ВыборкаРасчет.ДоляВПравеОбщейСобственностиЧислитель, 1)
					* ?(ЗначениеЗаполнено(ВыборкаРасчет.ДоляСтоимостиЧислитель), ВыборкаРасчет.ДоляСтоимостиЧислитель, 1);
				СтрокаРасчета.ДоляВПравеЗнаменатель = 
					?(ЗначениеЗаполнено(ВыборкаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель), ВыборкаРасчет.ДоляВПравеОбщейСобственностиЗнаменатель, 1)
					* ?(ЗначениеЗаполнено(ВыборкаРасчет.ДоляСтоимостиЗнаменатель), ВыборкаРасчет.ДоляСтоимостиЗнаменатель, 1);
				СтрокаРасчета.КоличествоМесяцевВладения = ВыборкаРасчет.КоличествоМесяцевИспользования;
			КонецЕсли;

		КонецЦикла; 

	КонецЦикла; 

КонецПроцедуры

Функция ТекстЗапросаТранспортныеСредства()

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетТранспортногоНалога.Организация КАК Организация,
	|	РасчетТранспортногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетТранспортногоНалога.ОсновноеСредство.Наименование КАК Наименование,
	|	РасчетТранспортногоНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетТранспортногоНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РасчетТранспортногоНалога.ВидТранспортногоСредства КАК ВидТранспортногоСредства,
	|	РасчетТранспортногоНалога.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасчетТранспортногоНалога.ИФНС.Код КАК КодНалоговогоОргана,
	|	РасчетТранспортногоНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетТранспортногоНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетТранспортногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВЫБОР
	|		КОГДА РасчетТранспортногоНалога.ПовышающийКоэффициент = 0
	|			ТОГДА 1
	|		ИНАЧЕ РасчетТранспортногоНалога.ПовышающийКоэффициент
	|	КОНЕЦ КАК ПовышающийКоэффициент,
	|	РасчетТранспортногоНалога.КоличествоМесяцевИспользования КАК КоличествоМесяцевВладения,
	|	РасчетТранспортногоНалога.ДатаРегистрации КАК ДатаПостановкиНаУчет,
	|	РасчетТранспортногоНалога.ДатаСнятияСУчета КАК ДатаСнятияСУчета,
	|	РасчетТранспортногоНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетТранспортногоНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетТранспортногоНалога.СуммаЛьгот <> 0 КАК ЕстьЛьготы
	|ПОМЕСТИТЬ РасчетНалога
	|ИЗ
	|	РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
	|ГДЕ
	|	РасчетТранспортногоНалога.Организация = &Организация
	|	И РасчетТранспортногоНалога.ПериодРасчета = &ПериодРасчета
	|	И РасчетТранспортногоНалога.ИФНС.Код В(&КодыНалоговыхОрганов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(РегистрацияТранспортныхСредств.Период) КАК ДатаПоследнегоИзменения
	|ПОМЕСТИТЬ ДатыПоследнихИзменений
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетНалога КАК РасчетНалога
	|		ПО РегистрацияТранспортныхСредств.Организация = РасчетНалога.Организация
	|			И РегистрацияТранспортныхСредств.ОсновноеСредство = РасчетНалога.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	АВТОНОМЕРЗАПИСИ() КАК Ключ
	|ПОМЕСТИТЬ ГруппировкаПоОбъектам
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	РегистрационныйЗнак,
	|	ИдентификационныйНомер,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.Наименование КАК НаименованиеОбъекта,
	|	РасчетНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РасчетНалога.ВидТранспортногоСредства КАК ВидТранспортногоСредства,
	|	РасчетНалога.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	РасчетНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетНалога.ДатаПостановкиНаУчет КАК ДатаПостановкиНаУчет,
	|	РасчетНалога.ДатаСнятияСУчета КАК ДатаСнятияСУчета,
	|	ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоИзменения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) >= &ПериодОсобогоКонтроля
	|			ТОГДА 0
	|		КОГДА РасчетНалога.ЕстьЛьготы
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок,
	|	ГруппировкаПоОбъектам.Ключ КАК КлючГруппировки
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппировкаПоОбъектам КАК ГруппировкаПоОбъектам
	|		ПО РасчетНалога.ОсновноеСредство = ГруппировкаПоОбъектам.ОсновноеСредство
	|			И РасчетНалога.РегистрационныйЗнак = ГруппировкаПоОбъектам.РегистрационныйЗнак
	|			И РасчетНалога.ИдентификационныйНомер = ГруппировкаПоОбъектам.ИдентификационныйНомер
	|			И РасчетНалога.КодНалоговогоОргана = ГруппировкаПоОбъектам.КодНалоговогоОргана
	|			И РасчетНалога.КодПоОКТМО = ГруппировкаПоОбъектам.КодПоОКТМО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПоследнихИзменений КАК ДатыПоследнихИзменений
	|		ПО РасчетНалога.ОсновноеСредство = ДатыПоследнихИзменений.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ДатаПостановкиНаУчет,
	|	ОсновноеСредство
	|ИТОГИ
	|	МАКСИМУМ(ОсновноеСредство),
	|	МАКСИМУМ(НаименованиеОбъекта),
	|	МАКСИМУМ(РегистрационныйЗнак),
	|	МАКСИМУМ(ИдентификационныйНомер),
	|	МАКСИМУМ(ВидТранспортногоСредства),
	|	МАКСИМУМ(ЕдиницаИзмерения),
	|	МАКСИМУМ(КодНалоговогоОргана),
	|	МАКСИМУМ(КодПоОКТМО),
	|	СУММА(СуммаЛьгот),
	|	СУММА(СуммаНалога),
	|	МИНИМУМ(ДатаПостановкиНаУчет),
	|	МИНИМУМ(ДатаСнятияСУчета),
	|	МАКСИМУМ(ДатаПоследнегоИзменения),
	|	МИНИМУМ(Порядок)
	|ПО
	|	КлючГруппировки"; 

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаЗемельныеУчастки()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетЗемельногоНалога.Организация КАК Организация,
	|	РасчетЗемельногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетЗемельногоНалога.ОсновноеСредство.Наименование КАК НаименованиеОбъекта,
	|	РасчетЗемельногоНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетЗемельногоНалога.ИФНС.Код КАК КодНалоговогоОргана,
	|	РасчетЗемельногоНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетЗемельногоНалога.КадастроваяСтоимость КАК НалоговаяБаза,
	|	ВЫБОР
	|		КОГДА РасчетЗемельногоНалога.ДоляВПравеОбщейСобственностиЧислитель = 0
	|			ТОГДА 1
	|		ИНАЧЕ РасчетЗемельногоНалога.ДоляВПравеОбщейСобственностиЧислитель
	|	КОНЕЦ КАК ДоляВПравеЧислитель,
	|	ВЫБОР
	|		КОГДА РасчетЗемельногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель = 0
	|			ТОГДА 1
	|		ИНАЧЕ РасчетЗемельногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель
	|	КОНЕЦ КАК ДоляВПравеЗнаменатель,
	|	РасчетЗемельногоНалога.НеоблагаемаяКадастроваяСтоимость КАК НалоговыйВычет,
	|	РасчетЗемельногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетЗемельногоНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетЗемельногоНалога.КоличествоМесяцевИспользования КАК КоличествоМесяцевВладения,
	|	РасчетЗемельногоНалога.ДатаРегистрации КАК ДатаПостановкиНаУчет,
	|	РасчетЗемельногоНалога.ДатаСнятияСУчета КАК ДатаСнятияСУчета,
	|	РасчетЗемельногоНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетЗемельногоНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетЗемельногоНалога.СуммаЛьгот <> 0 КАК ЕстьЛьготы
	|ПОМЕСТИТЬ РасчетНалога
	|ИЗ
	|	РегистрСведений.РасчетЗемельногоНалога КАК РасчетЗемельногоНалога
	|ГДЕ
	|	РасчетЗемельногоНалога.Организация = &Организация
	|	И РасчетЗемельногоНалога.ПериодРасчета = &ПериодРасчета
	|	И РасчетЗемельногоНалога.ИФНС.Код В (&КодыНалоговыхОрганов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияЗемельныхУчастков.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(РегистрацияЗемельныхУчастков.Период) КАК ДатаПоследнегоИзменения
	|ПОМЕСТИТЬ ДатыПоследнихИзменений
	|ИЗ
	|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетНалога КАК РасчетНалога
	|		ПО РегистрацияЗемельныхУчастков.Организация = РасчетНалога.Организация
	|		И РегистрацияЗемельныхУчастков.ОсновноеСредство = РасчетНалога.ОсновноеСредство
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияЗемельныхУчастков.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	АВТОНОМЕРЗАПИСИ() КАК Ключ
	|ПОМЕСТИТЬ ГруппировкаПоОбъектам
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КадастровыйНомер,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.НаименованиеОбъекта КАК НаименованиеОбъекта,
	|	РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетНалога.ДоляВПравеЧислитель КАК ДоляВПравеЧислитель,
	|	РасчетНалога.ДоляВПравеЗнаменатель КАК ДоляВПравеЗнаменатель,
	|	РасчетНалога.НалоговыйВычет КАК НалоговыйВычет,
	|	РасчетНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	РасчетНалога.ДатаПостановкиНаУчет КАК ДатаПостановкиНаУчет,
	|	РасчетНалога.ДатаСнятияСУчета КАК ДатаСнятияСУчета,
	|	ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоИзменения,
	|	РасчетНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетНалога.СуммаНалога КАК СуммаНалога,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) >= &ПериодОсобогоКонтроля
	|			ТОГДА 0
	|		КОГДА РасчетНалога.ЕстьЛьготы
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок,
	|	ГруппировкаПоОбъектам.Ключ КАК КлючГруппировки
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппировкаПоОбъектам КАК ГруппировкаПоОбъектам
	|		ПО РасчетНалога.ОсновноеСредство = ГруппировкаПоОбъектам.ОсновноеСредство
	|		И РасчетНалога.КадастровыйНомер = ГруппировкаПоОбъектам.КадастровыйНомер
	|		И РасчетНалога.КодНалоговогоОргана = ГруппировкаПоОбъектам.КодНалоговогоОргана
	|		И РасчетНалога.КодПоОКТМО = ГруппировкаПоОбъектам.КодПоОКТМО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПоследнихИзменений КАК ДатыПоследнихИзменений
	|		ПО РасчетНалога.ОсновноеСредство = ДатыПоследнихИзменений.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ДатаПостановкиНаУчет,
	|	ОсновноеСредство
	|ИТОГИ
	|	МАКСИМУМ(ОсновноеСредство),
	|	МАКСИМУМ(НаименованиеОбъекта),
	|	МАКСИМУМ(КадастровыйНомер),
	|	МАКСИМУМ(КодНалоговогоОргана),
	|	МАКСИМУМ(КодПоОКТМО),
	|	МИНИМУМ(ДатаПостановкиНаУчет),
	|	МИНИМУМ(ДатаСнятияСУчета),
	|	МАКСИМУМ(ДатаПоследнегоИзменения),
	|	СУММА(СуммаЛьгот),
	|	СУММА(СуммаНалога),
	|	МИНИМУМ(Порядок)
	|ПО
	|	КлючГруппировки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаИмущество()

	// Основную часть данных об имуществе берем из расчета налога. Сверяем только кадастровую недвижимость (недвижимость
	// по среднегодовой стоимости декларируется, и ФНС по ней расчет не присылает).
	// Но там отсутствуют данные о снятии с учета.
	// Для получения этих данных используем такой алгоритм:
	// 1. Выбираем данные из регистра расчета налога (из нового или из прежнего в зависимости от периода и наличия данных)
	//    См. врем таблицу РасчетНалога
	// 2. Для каждого отдельного периода действия набора параметров (т.е для каждой ДатаСведений) определяем дату прекращения
	//    его действия: это либо дата прекращения права собственности, либо следующая ДатаСведений
	//    См. врем таблицу ДатыПрекращенияРасчета
	// 3. Определяем даты последних изменений по объектам - это период последней записи в регистре настроек. Не отбираем
	//    по виду налоговой базы, т.к. объект теоретически мог быть переквалифицирован в облагаемый по среднегодовой стоимости.
	//    См. врем таблицу ДатыПоследнихИзменений 
	// 4. Под объекто сверки понимается сочетание ОсновноеСредство - код налогового органа - код ОКТМО. Для группировки
	//    итогового результата по сочетанию этих полей, используем временную таблицу группировок
	//    См. врем таблицу ГруппировкаПоОбъектам
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ОсновноеСредство.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА РасчетНалогаНаИмуществоПоКадастровойСтоимости.КадастровыйНомерПомещения <> """"
	|			ТОГДА РасчетНалогаНаИмуществоПоКадастровойСтоимости.КадастровыйНомерПомещения
	|		ИНАЧЕ РасчетНалогаНаИмуществоПоКадастровойСтоимости.КадастровыйНомер
	|	КОНЕЦ КАК КадастровыйНомер,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ИФНС.Код КАК КодНалоговогоОргана,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДатаСведений КАК ДатаСведений,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЧислитель КАК
	|		ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЗнаменатель КАК
	|		ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетНалогаНаИмуществоПоКадастровойСтоимости.СуммаНалога КАК СуммаНалога,
	|	НЕ РасчетНалогаНаИмуществоПоКадастровойСтоимости.ОснованиеЛьготы = ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка) КАК ЕстьЛьгота
	|ПОМЕСТИТЬ РасчетНалога
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмуществоПоКадастровойСтоимости КАК РасчетНалогаНаИмуществоПоКадастровойСтоимости
	|ГДЕ
	|	Не &ИспользоватьПрежнийРасчетНалога
	|	И РасчетНалогаНаИмуществоПоКадастровойСтоимости.Организация = &Организация
	|	И РасчетНалогаНаИмуществоПоКадастровойСтоимости.ПериодРасчета = &ПериодРасчета
	|	И РасчетНалогаНаИмуществоПоКадастровойСтоимости.ИФНС.Код В (&КодыНалоговыхОрганов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетНалогаНаИмущество.ОсновноеСредство,
	|	РасчетНалогаНаИмущество.ОсновноеСредство.Наименование,
	|	ВЫБОР
	|		КОГДА РасчетНалогаНаИмущество.КадастровыйНомерПомещения <> """"
	|			ТОГДА РасчетНалогаНаИмущество.КадастровыйНомерПомещения
	|		ИНАЧЕ РасчетНалогаНаИмущество.КадастровыйНомер
	|	КОНЕЦ,
	|	РасчетНалогаНаИмущество.ИФНС.Код,
	|	РасчетНалогаНаИмущество.КодПоОКТМО,
	|	РасчетНалогаНаИмущество.ДатаСведений,
	|	РасчетНалогаНаИмущество.КадастроваяСтоимость,
	|	РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетНалогаНаИмущество.ДоляСтоимостиЧислитель,
	|	РасчетНалогаНаИмущество.ДоляСтоимостиЗнаменатель,
	|	РасчетНалогаНаИмущество.НалоговаяСтавка,
	|	РасчетНалогаНаИмущество.КоличествоМесяцевИспользования,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА РасчетНалогаНаИмущество.НеоблагаемаяКадастроваяСтоимость >= РасчетНалогаНаИмущество.КадастроваяСтоимость
	|			ТОГДА ВЫРАЗИТЬ(РасчетНалогаНаИмущество.КадастроваяСтоимость КАК ЧИСЛО(15, 0))
	|		КОГДА РасчетНалогаНаИмущество.НеоблагаемаяКадастроваяСтоимость > 0
	|			ТОГДА ВЫРАЗИТЬ(РасчетНалогаНаИмущество.НеоблагаемаяКадастроваяСтоимость КАК ЧИСЛО(15, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ * ВЫБОР
	|		КОГДА РасчетНалогаНаИмущество.ДоляСтоимостиЧислитель > 0
	|		И РасчетНалогаНаИмущество.ДоляСтоимостиЗнаменатель > 0
	|			ТОГДА РасчетНалогаНаИмущество.ДоляСтоимостиЧислитель / РасчетНалогаНаИмущество.ДоляСтоимостиЗнаменатель
	|		ИНАЧЕ 1
	|	КОНЕЦ * ВЫБОР
	|		КОГДА РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЧислитель > 0
	|		И РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЗнаменатель > 0
	|			ТОГДА РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЧислитель /
	|				РасчетНалогаНаИмущество.ДоляВПравеОбщейСобственностиЗнаменатель
	|		ИНАЧЕ 1
	|	КОНЕЦ * РасчетНалогаНаИмущество.КоличествоМесяцевВладения / 12 * ВЫБОР
	|		КОГДА РасчетНалогаНаИмущество.КоличествоМесяцевВладения > 0
	|			ТОГДА РасчетНалогаНаИмущество.КоличествоМесяцевИспользования / РасчетНалогаНаИмущество.КоличествоМесяцевВладения
	|		ИНАЧЕ 1
	|	КОНЕЦ * РасчетНалогаНаИмущество.НалоговаяСтавка / 100 КАК ЧИСЛО(15, 0)) +
	|		РасчетНалогаНаИмущество.СуммаУменьшенияСуммыНалога,
	|	РасчетНалогаНаИмущество.СуммаНалогаКУплате + РасчетНалогаНаИмущество.СуммаАвансовыхПлатежей,
	|	НЕ РасчетНалогаНаИмущество.ОснованиеЛьготы = ЗНАЧЕНИЕ(Справочник.ОснованияЛьготПоИмущественнымНалогам.ПустаяСсылка)
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
	|ГДЕ
	|	&ИспользоватьПрежнийРасчетНалога
	|	И РасчетНалогаНаИмущество.Организация = &Организация
	|	И РасчетНалогаНаИмущество.ПериодРасчета = &ПериодРасчета
	|	И РасчетНалогаНаИмущество.ИФНС.Код В (&КодыНалоговыхОрганов)
	|	И РасчетНалогаНаИмущество.ВидНалоговойБазы = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.ДатаСведений КАК ДатаСведений
	|ПОМЕСТИТЬ ВсеДатыСведений
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиПрекращенияРасчета.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗаписиПрекращенияРасчета.ДатаСведений КАК ДатаСведений,
	|	МИНИМУМ(ЗаписиПрекращенияРасчета.ДатаПрекращенияРасчета) КАК ДатаПрекращенияРасчета
	|ПОМЕСТИТЬ ДатыПрекращенияРасчета
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|		ВсеДатыСведений.ДатаСведений КАК ДатаСведений,
	|		ВЫБОР
	|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности < ВсеДатыСведений.ДатаСведений
	|				ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
	|			ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности
	|		КОНЕЦ КАК ДатаПрекращенияРасчета
	|	ИЗ
	|		РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеДатыСведений КАК ВсеДатыСведений
	|			ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = ВсеДатыСведений.ОсновноеСредство
	|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период >= ВсеДатыСведений.ДатаСведений
	|	ГДЕ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация = &Организация
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности <= &ПериодРасчета
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности <> ДАТАВРЕМЯ(1, 1, 1)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство,
	|		ВсеДатыСведений.ДатаСведений,
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
	|	ИЗ
	|		РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеДатыСведений КАК ВсеДатыСведений
	|			ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = ВсеДатыСведений.ОсновноеСредство
	|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период >= ВсеДатыСведений.ДатаСведений
	|	ГДЕ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация = &Организация
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОС.НеПодлежитНалогообложению
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВсеДатыСведений.ОсновноеСредство,
	|		ВсеДатыСведений.ДатаСведений,
	|		СледующиеДатыСведений.ДатаСведений
	|	ИЗ
	|		ВсеДатыСведений КАК ВсеДатыСведений
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеДатыСведений КАК СледующиеДатыСведений
	|			ПО ВсеДатыСведений.ОсновноеСредство = СледующиеДатыСведений.ОсновноеСредство
	|			И ВсеДатыСведений.ДатаСведений < СледующиеДатыСведений.ДатаСведений) КАК ЗаписиПрекращенияРасчета
	|СГРУППИРОВАТЬ ПО
	|	ЗаписиПрекращенияРасчета.ОсновноеСредство,
	|	ЗаписиПрекращенияРасчета.ДатаСведений
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ДатаСведений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК ДатаПоследнегоИзменения
	|ПОМЕСТИТЬ ДатыПоследнихИзменений
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
	|ГДЕ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация = &Организация
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период <= &ПериодРасчета
	|	И СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство В
	|		(ВЫБРАТЬ
	|			РасчетНалога.ОсновноеСредство
	|		ИЗ
	|			РасчетНалога КАК РасчетНалога)
	|СГРУППИРОВАТЬ ПО
	|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	АВТОНОМЕРЗАПИСИ() КАК Ключ
	|ПОМЕСТИТЬ ГруппировкаПоОбъектам
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КадастровыйНомер,
	|	КодНалоговогоОргана,
	|	КодПоОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетНалога.Наименование КАК НаименованиеОбъекта,
	|	РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	РасчетНалога.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетНалога.КадастроваяСтоимость КАК НалоговаяБаза,
	|	РасчетНалога.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетНалога.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетНалога.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	РасчетНалога.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	РасчетНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетНалога.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	РасчетНалога.СуммаЛьгот КАК СуммаЛьгот,
	|	РасчетНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетНалога.ДатаСведений КАК ДатаПостановкиНаУчет,
	|	ЕСТЬNULL(ДатыПрекращенияРасчета.ДатаПрекращенияРасчета, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСнятияСУчета,
	|	ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПоследнегоИзменения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДатыПоследнихИзменений.ДатаПоследнегоИзменения, ДАТАВРЕМЯ(1, 1, 1)) >= &ПериодОсобогоКонтроля
	|			ТОГДА 0
	|		КОГДА РасчетНалога.ЕстьЛьгота
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Порядок,
	|	ГруппировкаПоОбъектам.Ключ КАК КлючГруппировки
	|ИЗ
	|	РасчетНалога КАК РасчетНалога
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппировкаПоОбъектам КАК ГруппировкаПоОбъектам
	|		ПО РасчетНалога.ОсновноеСредство = ГруппировкаПоОбъектам.ОсновноеСредство
	|		И РасчетНалога.КадастровыйНомер = ГруппировкаПоОбъектам.КадастровыйНомер
	|		И РасчетНалога.КодНалоговогоОргана = ГруппировкаПоОбъектам.КодНалоговогоОргана
	|		И РасчетНалога.КодПоОКТМО = ГруппировкаПоОбъектам.КодПоОКТМО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПрекращенияРасчета КАК ДатыПрекращенияРасчета
	|		ПО РасчетНалога.ОсновноеСредство = ДатыПрекращенияРасчета.ОсновноеСредство
	|		И РасчетНалога.ДатаСведений = ДатыПрекращенияРасчета.ДатаСведений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПоследнихИзменений КАК ДатыПоследнихИзменений
	|		ПО РасчетНалога.ОсновноеСредство = ДатыПоследнихИзменений.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ДатаПостановкиНаУчет,
	|	ОсновноеСредство
	|ИТОГИ
	|	МАКСИМУМ(ОсновноеСредство),
	|	МАКСИМУМ(НаименованиеОбъекта),
	|	МАКСИМУМ(КадастровыйНомер),
	|	МАКСИМУМ(КодНалоговогоОргана),
	|	МАКСИМУМ(КодПоОКТМО),
	|	СУММА(СуммаЛьгот),
	|	СУММА(СуммаНалога),
	|	МИНИМУМ(ДатаПостановкиНаУчет),
	|	МИНИМУМ(ДатаСнятияСУчета),
	|	МАКСИМУМ(ДатаПоследнегоИзменения),
	|	МИНИМУМ(Порядок)
	|ПО
	|	КлючГруппировки";

	Возврат ТекстЗапроса;

КонецФункции

Процедура ЗагрузитьРасчетФНСИзСообщения(Сообщение)
		
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	Вложения = КонтекстЭДОСервер.ПолучитьФайлыДокументовРеализацииПолномочийНалоговыхОрганов(Сообщение);
	
	ТаблицаПоиска = Неопределено; // кэш таблицы для сопоставления объектов расчетов
	ИдентификаторыОбъектов = Неопределено; // кэш таблицы поиска элементов справочника ОС (если объект остуствует в расчете программы, но есть в ФНС)
	
	Для Каждого Файл Из Вложения Цикл
		Если СтрЗаканчиваетсяНа(Файл.ИмяФайла, ".xml") Тогда
			ЗагрузитьРасчетФНСИзФайла(Файл.Данные, Файл.ИмяФайла, ТаблицаПоиска, ИдентификаторыОбъектов);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ЗагрузитьРасчетФНСИзФайла(Данные, ИмяФайла, ТаблицаПоиска = Неопределено, ИдентификаторыОбъектов = Неопределено) 

	Если ЗначениеЗаполнено(ИмяФайла) Тогда

		ЭтоРасчетТранспортногоНалога = СверкаСФНСПоИмущественнымНалогамФормыКлиентСервер.ЭтоФайлРасчетаТранспортногоНалога(ИмяФайла);
		ЭтоРасчетЗемельногоНалога = СверкаСФНСПоИмущественнымНалогамФормыКлиентСервер.ЭтоФайлРасчетаЗемельногоНалога(ИмяФайла);
		ЭтоРасчетИмущественныхНалогов = СверкаСФНСПоИмущественнымНалогамФормыКлиентСервер.ЭтоФайлРасчетаИмущественныхНалогов(ИмяФайла);

		Если Не ЭтоРасчетТранспортногоНалога
			 И Не ЭтоРасчетЗемельногоНалога
			 И Не ЭтоРасчетИмущественныхНалогов Тогда
			Возврат; 
		КонецЕсли;

	КонецЕсли;

	Попытка
		ПредставлениеXML = Справочники.ДокументыРеализацииПолномочийНалоговыхОрганов.ТекстСообщения(Данные);
		ДеревоЗагрузки = УведомлениеОСпецрежимахНалогообложения.СформироватьДеревоЗагрузки(ПредставлениеXML);
		ВерсияФормата = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПути(ДеревоЗагрузки, "Файл/ВерсФорм", "ИмяЭлемента").ЗначениеЭлемента;
		КНД = УведомлениеОСпецрежимахНалогообложения.НайтиУзелВДеревеПоПути(ДеревоЗагрузки, "Файл/Документ/КНД", "ИмяЭлемента").ЗначениеЭлемента;
		СтрокиРасчета = СверкаСФНСПоИмущественнымНалогам.СтрокиРасчетаНалога(ДеревоЗагрузки, Налог);
	Исключение
		ВызватьИсключение СтрШаблон(НСтр("ru='Ошибка при чтении файла %1. Возможно, файл имеет неверный формат:
			|%2'"),
			ИмяФайла,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки; 

	// В расчете транспортного налога ФНС может добавлять VIN в поле регистрационного знака в формате "[гос номер] (VIN [номер VIN])".
	// Понять, в каком формате заполнен конкретный файл можно только непосредственно проанализировав содержание записей (VIN может быть
	// как во всех строках расчета, так и только в некоторых).
	// Поэтому здесь определяем, как именно заполнен рег знак, и если указан VIN, то далее учитываем это при парсинге рег знака,
	// а также в сопоставлении ТС учитываем VIN.
	// Если ТаблицаПоиска уже инициализирована, то значит, что формат рег знака уже был проанализирован или не требуется
	Если ТаблицаПоиска = Неопределено И Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог И СтрокиРасчета.Количество() > 0 Тогда 
		Для Каждого СтрокаРасчетаФНС Из СтрокиРасчета Цикл
			РегЗнак = СверкаСФНСПоИмущественнымНалогам.ЗначениеПараметраРасчета(СтрокаРасчетаФНС, "РегЗнак");
			СопоставлениеСУчетомVIN = (СтрНайти(РегЗнак, "VIN") > 0);
			Если СопоставлениеСУчетомVIN Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТаблицаПоиска = Неопределено Тогда
		ТаблицаПоиска = ТаблицаПоиска();
	КонецЕсли;
	
	КлючевыеПоляСверки = КлючевыеПоляСверки();

	Для Каждого СтрокаРасчетаФНС Из СтрокиРасчета Цикл
		
		// Пропускаем строки, в которых период расчета не совпадает с периодом сверки
		НалоговыйПериодФНС = Дата(СверкаСФНСПоИмущественнымНалогам.ЗначениеПараметраРасчета(СтрокаРасчетаФНС, "НалПер"), 1, 1);
		Если НалоговыйПериодФНС <> НалоговыйПериод Тогда
			Продолжить;
		КонецЕсли;

		Поиск = Новый Структура;
		
		Для Каждого ОписаниеПоля Из КлючевыеПоляСверки Цикл
			
			Если ОписаниеПоля.Ключ = "ИдентификационныйНомер" Тогда
				// VIN заполняется при парсинге поля регистрационного знака
				Продолжить;
			КонецЕсли;

			ЗначениеИзРасчетаФНС = СверкаСФНСПоИмущественнымНалогам.ЗначениеПараметраРасчета(СтрокаРасчетаФНС, ОписаниеПоля.Значение);
			Если ОписаниеПоля.Ключ = "РегистрационныйЗнак" Тогда
				// ФНС иногда в одно поле с гос номером ТС (РегЗнак) вставляет VIN.
				// Если для конкретной строки расчета указан VIN, то ищем по регистрационному знаку и VIN,
				// а если VIN не указан, то только по регистрационному знаку.
				Для каждого Свойство Из СоставРегистрационногоЗнака(ЗначениеИзРасчетаФНС) Цикл
					Поиск.Вставить(Свойство.Ключ, Врег(СтрЗаменить(Свойство.Значение, " ", "")));
				КонецЦикла; 
			Иначе
				Поиск.Вставить(ОписаниеПоля.Ключ, Врег(СтрЗаменить(ЗначениеИзРасчетаФНС, " ", "")));
			КонецЕсли;

		КонецЦикла; 

		НайденнаяСтрокаСверки = Неопределено;
		НайденныеСтроки = ТаблицаПоиска.НайтиСтроки(Поиск);
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденнаяСтрокаСверки = НайденныеСтроки[0].СтрокаСверки;
		КонецЕсли;

		ДополнитьСверку(НайденнаяСтрокаСверки, СтрокаРасчетаФНС, ИдентификаторыОбъектов, Поиск, ВерсияФормата, КНД);

	КонецЦикла;

КонецПроцедуры

// Проверяет, все ли объекты есть в расчетах и совпадают ли суммы
// 
// Возвращаемое значение:
//   - Истина - по составу объектов и суммам расчеты совпадают
//
Функция РасчетыСовпадают() Экспорт 

	КоличествоСовпавших = Сверка.НайтиСтроки(Новый Структура("ПравильныйРасчет", 1)).Количество();
	КоличествоВсего = Сверка.Количество();

	Возврат КоличествоСовпавших = КоличествоВсего;

КонецФункции

Функция ТаблицаПоиска()

	КлючевыеПоляСверки = КлючевыеПоляСверки();

	ПоляПоиска = Новый Массив;
	Для Каждого ОписаниеПоля Из КлючевыеПоляСверки Цикл
		ПоляПоиска.Добавить(ОписаниеПоля.Ключ);
	КонецЦикла; 
	ПоляПоискаСтрока = СтрСоединить(ПоляПоиска, ",");

	ТаблицаПоиска = Сверка.ВыгрузитьКолонки(ПоляПоискаСтрока);
	ТаблицаПоиска.Колонки.Добавить("СтрокаСверки");

	// Заполняем таблицу поиска. При этом исключаем влияние регистра и пробелов в значениях ключевых полей. 
	Для Каждого СтрокаСверки Из Сверка Цикл
		НоваяСтрока = ТаблицаПоиска.Добавить();
		Для Каждого ОписаниеПоля Из КлючевыеПоляСверки Цикл
			НоваяСтрока[ОписаниеПоля.Ключ] = Врег(СтрЗаменить(СтрокаСверки[ОписаниеПоля.Ключ], " ", ""));
		КонецЦикла; 
		НоваяСтрока.СтрокаСверки = СтрокаСверки;
	КонецЦикла; 

	ТаблицаПоиска.Индексы.Добавить(ПоляПоискаСтрока);
	
	// Для транспортного налога VIN может быть указан только в части строк. В этом случае
	// добавим индекс без поля VIN, чтобы выполнять поиск для тех строк, где ФНС не указала VIN
	Если СопоставлениеСУчетомVIN Тогда
		ПолеVIN = ПоляПоиска.Найти("ИдентификационныйНомер");
		Если ПолеVIN <> Неопределено Тогда
			ПоляПоиска.Удалить(ПолеVIN);
			ТаблицаПоиска.Индексы.Добавить(СтрСоединить(ПоляПоиска, ","));
		КонецЕсли;
	КонецЕсли;

	Возврат ТаблицаПоиска;

КонецФункции

Функция КлючевыеПоляСверки()

	КлючевыеПоляСверки = Новый Структура;

	Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		КлючевыеПоляСверки.Вставить("РегистрационныйЗнак", "РегЗнак");
		Если СопоставлениеСУчетомVIN Тогда
			КлючевыеПоляСверки.Вставить("ИдентификационныйНомер", "РегЗнак");
		КонецЕсли;
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		КлючевыеПоляСверки.Вставить("КадастровыйНомер", "НомКадастрЗУ");
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		КлючевыеПоляСверки.Вставить("КадастровыйНомер", "НомКадастрНИ");
	КонецЕсли;

	КлючевыеПоляСверки.Вставить("КодНалоговогоОргана", "КодНО");
	КлючевыеПоляСверки.Вставить("КодПоОКТМО", "ОКТМО");

	Возврат КлючевыеПоляСверки;

КонецФункции

Функция ПрочиеПоляСверки()

	ПоляСверки = Новый Структура;

	ПоляСверки.Вставить("СуммаЛьготФНС", "РазмНалЛьг");
	ПоляСверки.Вставить("СуммаНалогаФНС", "СумИсчисл");
	Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		ПоляСверки.Вставить("НаименованиеОбъекта", "НаимОбъект");
	КонецЕсли;

	Возврат ПоляСверки;

КонецФункции

Функция ПоляРасчета(КНД)

	ПоляСверки = Новый Структура;

	ПоляСверки.Вставить("НалоговаяБаза", "НалБаза");
	
	Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		ПоляСверки.Вставить("НалоговаяСтавка", "НалСтавка");
		ПоляСверки.Вставить("КоличествоМесяцевВладения", "ВладенТС");
		ПоляСверки.Вставить("ПовышающийКоэффициент", "ПовКоэф");
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		ПоляСверки.Вставить("НалоговаяСтавка", "НалСтав");
		// В форме КНД 1153007 (расчет земельного налога), которая применяется до конца 2022 года, предусмотрена передача
		// суммы налогового высчета. С 2023 года применяется форма КНД 1152029 (все три налога вместе), и там уже нет
		// суммы налогового вычета.
		Если КНД = СверкаСФНСПоИмущественнымНалогамФормыКлиентСервер.КНДРасчетаЗемельногоНалога() Тогда
			ПоляСверки.Вставить("НалоговыйВычет", "НалВычет");
		КонецЕсли;
		ПоляСверки.Вставить("КоличествоМесяцевВладения", "ВладенЗУ");
		ПоляСверки.Вставить("ПовышающийКоэффициент", "ПовКоэф");
		ПоляСверки.Вставить("ДоляВПраве", "ДоляЗУ");
	ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		ПоляСверки.Вставить("НалоговаяСтавка", "НалСтав");
		ПоляСверки.Вставить("КоличествоМесяцевВладения", "ВладенИмОрг");
		ПоляСверки.Вставить("ДоляВПраве", "ДоляИмОрг");
	КонецЕсли;

	Возврат ПоляСверки;

КонецФункции

Процедура ДополнитьСверку(СтрокаСверки, СтрокаРасчетаФНС, ИдентификаторыОбъектов, ЗначенияКлюча, ВерсияФормата, КНД)

	Если СтрокаСверки = Неопределено Тогда
		СтрокаСверки = Сверка.Добавить();
		СтрокаСверки.ИдентификаторСтроки = Новый УникальныйИдентификатор();
		Для Каждого ОписаниеПоля Из КлючевыеПоляСверки() Цикл
			Если ОписаниеПоля.Ключ = "ИдентификационныйНомер" Тогда
				// VIN указывается в одном поле с регистрационным знаком
				Продолжить;
			КонецЕсли;
			ЗначениеИзРасчетаФНС = СверкаСФНСПоИмущественнымНалогам.ЗначениеПараметраРасчета(СтрокаРасчетаФНС, ОписаниеПоля.Значение);
			Если ОписаниеПоля.Ключ = "РегистрационныйЗнак" Тогда
				// ФНС иногда в одно поле с гос номером ТС (РегЗнак) вставляет VIN.
				// Если указан VIN, то кроме поля РегистрационныйЗнак заполняется также ИдентификационныйНомер.
				ЗаполнитьЗначенияСвойств(СтрокаСверки, СоставРегистрационногоЗнака(ЗначениеИзРасчетаФНС)); 
			Иначе
				СтрокаСверки[ОписаниеПоля.Ключ] = ЗначениеИзРасчетаФНС;
			КонецЕсли;
		КонецЦикла;
		// Поищем объект налогообложения по гос. номеру (и по VIN, если он указан в расчете ФНС) или кадастровому номеру.
		// Возможно, ранее объект был учтен, но уже снят с учета, а ФНС продолжает начислять налог.
		// В этом случае ссылка на объект (ОС) потребуется для того, чтобы при необходимости направить 
		// пользователя к настройкам налога по этому объекту.
		Если ИдентификаторыОбъектов = Неопределено Тогда
			// Встретив первый объект, которого нет в расчете программы, инициализируем таблицу
			ИдентификаторыОбъектов = СверкаСФНСПоИмущественнымНалогам.ИдентификаторыОбъектовНалогообложения(Организация, Налог, СопоставлениеСУчетомVIN); 
		КонецЕсли;
		СверкаСФНСПоИмущественнымНалогам.УстановитьОсновноеСредствоПоИдентификатору(СтрокаСверки, ИдентификаторыОбъектов, ЗначенияКлюча, Налог, СопоставлениеСУчетомVIN); 
	КонецЕсли;

	Для каждого ОписаниеПоля Из ПрочиеПоляСверки() Цикл
		ЗначениеИзРасчетаФНС = СверкаСФНСПоИмущественнымНалогам.ЗначениеПараметраРасчета(СтрокаРасчетаФНС, ОписаниеПоля.Значение);
		Если ОписаниеПоля.Ключ = "НаименованиеОбъекта" Тогда
			Если Не ЗначениеЗаполнено(СтрокаСверки.НаименованиеОбъекта) Тогда
				СтрокаСверки.НаименованиеОбъекта = ЗначениеИзРасчетаФНС;
			КонецЕсли;
		Иначе
			СтрокаСверки[ОписаниеПоля.Ключ] = ЗначениеИзРасчетаФНС;
		КонецЕсли;
	КонецЦикла;

	СтрокаСверки.ОбъектЕстьВРасчетеФНС = Истина;

	ОписаниеТиповКоличествоМесяцевВладения = Метаданные.Документы.СверкаСФНСПоИмущественнымНалогам.ТабличныеЧасти.РасчетФНС.Реквизиты.КоличествоМесяцевВладения.Тип;
	ОписаниеТиповДоляВПравеЧислитель = Метаданные.Документы.СверкаСФНСПоИмущественнымНалогам.ТабличныеЧасти.РасчетФНС.Реквизиты.ДоляВПравеЧислитель.Тип;
	ОписаниеТиповДоляВПравеЗнаменатель = Метаданные.Документы.СверкаСФНСПоИмущественнымНалогам.ТабличныеЧасти.РасчетФНС.Реквизиты.ДоляВПравеЗнаменатель.Тип;

	Если ВерсияФормата = "5.03" Тогда
		// детали расчета содержатся в подчиненных строках
		ИмяЭлементаДеталиРасчета = "";
		Если Налог = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
			ИмяЭлементаДеталиРасчета = "НачислТС";
		ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
			ИмяЭлементаДеталиРасчета = "НачислЗУ";
		ИначеЕсли Налог = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
			ИмяЭлементаДеталиРасчета = "НачислНИ";
		КонецЕсли;
		СтрокиИсточника = СтрокаРасчетаФНС.Строки.НайтиСтроки(Новый Структура("ИмяЭлемента", ИмяЭлементаДеталиРасчета));
	Иначе // 5.01 и 5.02
		// все данные расчета содержатся непосредственно в строке расчета ФНС
		СтрокиИсточника = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаРасчетаФНС);
	КонецЕсли;
	
	ПоляРасчета = ПоляРасчета(КНД);
	
	Для Каждого СтрокаИсточника Из СтрокиИсточника Цикл
		СтрокаРасчета = РасчетФНС.Добавить();
		СтрокаРасчета.ИдентификаторСтроки = СтрокаСверки.ИдентификаторСтроки;
		Для каждого ОписаниеПоля Из ПоляРасчета Цикл
			ЗначениеИзРасчетаФНС = СверкаСФНСПоИмущественнымНалогам.ЗначениеПараметраРасчета(СтрокаИсточника, ОписаниеПоля.Значение);
			Если ОписаниеПоля.Ключ = "КоличествоМесяцевВладения" Тогда
				// В расчете ФНС значение записано в формате XX/12, где XX - количество месяцев владения в году в виде 01, 02 и т.д.
				СтрокаРасчета[ОписаниеПоля.Ключ] = ОписаниеТиповКоличествоМесяцевВладения.ПривестиЗначение(Лев(ЗначениеИзРасчетаФНС, 2));
			ИначеЕсли ОписаниеПоля.Ключ = "ПовышающийКоэффициент" Тогда
				// В расчете ФНС отсуствует атрибут, если коэффициент = 1. В этом случае значение будет пустым - нужно подменить на 1.
				Если Не ЗначениеЗаполнено(ЗначениеИзРасчетаФНС) Тогда
					СтрокаРасчета.ПовышающийКоэффициент = 1;
				Иначе
					СтрокаРасчета.ПовышающийКоэффициент = ЗначениеИзРасчетаФНС;
				КонецЕсли;
			ИначеЕсли ОписаниеПоля.Ключ = "ДоляВПраве" Тогда
				Если Не ЗначениеЗаполнено(ЗначениеИзРасчетаФНС) Тогда
					СтрокаРасчета.ДоляВПравеЧислитель = 1;
					СтрокаРасчета.ДоляВПравеЗнаменатель = 1;
				Иначе
					// В расчете ФНС значение записано в виде простой правильной дроби без лидирующих нулей в числителе и знаменателе.
					// Формат записи: (от 1 до 20 знаков)/(от 1 до 20 знаков)
					ДоляВПраве = СтрРазделить(ЗначениеИзРасчетаФНС, "/", Истина);
					СтрокаРасчета.ДоляВПравеЧислитель = ОписаниеТиповДоляВПравеЧислитель.ПривестиЗначение(ДоляВПраве[0]);
					СтрокаРасчета.ДоляВПравеЗнаменатель = ОписаниеТиповДоляВПравеЗнаменатель.ПривестиЗначение(ДоляВПраве[1]);
				КонецЕсли;
			Иначе
				СтрокаРасчета[ОписаниеПоля.Ключ] = ЗначениеИзРасчетаФНС;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Если СтрокаСверки.ОбъектЕстьВРасчете 
		 И СтрокаСверки.ОбъектЕстьВРасчетеФНС
		 И СтрокаСверки.СуммаНалога = СтрокаСверки.СуммаНалогаФНС Тогда
		СтрокаСверки.ПравильныйРасчет = 1; // расчет сошелся
	КонецЕсли;

КонецПроцедуры

Функция СоставРегистрационногоЗнака(Знач РегистрационныйЗнак)

	СоставРегистрационногоЗнака = Новый Структура;
	
	НачалоVIN = СтрНайти(РегистрационныйЗнак, "VIN");
	
	Если НачалоVIN = 0 Тогда
		СоставРегистрационногоЗнака.Вставить("РегистрационныйЗнак", РегистрационныйЗнак);
		Возврат СоставРегистрационногоЗнака;
	КонецЕсли;
	
	РегистрационныйНомер = Лев(РегистрационныйЗнак, НачалоVIN - 1);
	РегистрационныйНомер = СтрЗаменить(РегистрационныйНомер, "(", ""); // учтено, что VIN может быть в скобках
	СоставРегистрационногоЗнака.Вставить("РегистрационныйЗнак", СокрЛП(РегистрационныйНомер));
	
	ИдентификационныйНомер = Сред(РегистрационныйЗнак, НачалоVIN + 3);
	ИдентификационныйНомер = СтрЗаменить(ИдентификационныйНомер, ")", ""); // учтено, что VIN может быть в скобках
	СоставРегистрационногоЗнака.Вставить("ИдентификационныйНомер", СокрЛП(ИдентификационныйНомер));

	Возврат СоставРегистрационногоЗнака;
	
КонецФункции

#КонецОбласти

#КонецЕсли