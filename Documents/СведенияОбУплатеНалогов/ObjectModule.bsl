#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	НачалоПростогоУчетаЕНС = ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	Если Дата < НачалоПростогоУчетаЕНС Тогда
		 Дата = НачалоПростогоУчетаЕНС;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.СведенияОбУплатеНалогов.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	ТаблицыУплатыВзносовФОТ = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыУплатыВзносовФОТ(
		ПараметрыПроведения, ПараметрыПроведения.Реквизиты);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект, ПараметрыПроведения, Ложь);
	
	УчетУСН.СформироватьДвиженияКУДиРРаздел5ПоТаблицам(ПараметрыПроведения,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияУплатаВзносовФОТ(
		ТаблицыУплатыВзносовФОТ, ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	НачалоПростогоУчетаЕНС = ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	Если Дата < НачалоПростогоУчетаЕНС Тогда
		 Дата = НачалоПростогоУчетаЕНС;
	КонецЕсли;
	Ответственный = Пользователи.ТекущийПользователь();
	СозданАвтоматически = Ложь;
	Если ОбъектКопирования.СозданАвтоматически Тогда
		Комментарий = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Сведения.Итог("Сумма");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнениеЗадачБухгалтераБП.АктуализироватьСтатусыЗадачПоФиксированнымВзносамИП(Организация, Дата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли