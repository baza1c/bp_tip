#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьФорматСохраненияРегистров() Экспорт
	
	ФорматРегистра = Константы.ФорматСохраненияРегистровУчета.Получить();
	Возврат ?(ЗначениеЗаполнено(ФорматРегистра), ФорматРегистра, Перечисления.ФорматыСохраненияОтчетов.PDF);
	
КонецФункции

Функция ПолучитьПрисоединенныеФайлаРегистра(РегистрУчета) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("ВладелецФайла", РегистрУчета);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрУчетаПрисоединенныеФайлы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РегистрУчетаПрисоединенныеФайлы КАК РегистрУчетаПрисоединенныеФайлы
	|ГДЕ
	|	РегистрУчетаПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Новый Массив;
	
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выгрузить();
	
	Возврат Выборка.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ПолучитьСвойстваПрисоединенныхФайловРегистра(РегистрУчета, ПолучитьДанныеФайла) Экспорт
	
	ПрисоединенныеФайлаРегистра = ПолучитьПрисоединенныеФайлаРегистра(РегистрУчета);
	
	СвойстваПрисоединенныхФайлов = 
		ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ПрисоединенныеФайлаРегистра, 
			"ПодписанЭП, Расширение, Описание", Истина); 
	
	Если ПолучитьДанныеФайла Тогда
		
		Для Каждого ПрисоединенныйФайл Из СвойстваПрисоединенныхФайлов Цикл
			
			ПрисоединенныйФайлСсылка = ПрисоединенныйФайл.Ключ;
			ПрисоединенныйФайлСвойства = ПрисоединенныйФайл.Значение;
			ПрисоединенныйФайлСвойства.Вставить("ДанныеФайла",
				РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайлСсылка, Новый УникальныйИдентификатор));

		КонецЦикла;
			
	КонецЕсли;
	
	Возврат СвойстваПрисоединенныхФайлов;
	
КонецФункции

#КонецОбласти

#КонецЕсли