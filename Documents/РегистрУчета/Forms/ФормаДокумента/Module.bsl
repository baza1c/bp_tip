#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ПодготовитьФормуНаСервере();
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Объект");
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидРегистра) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Вид регистра"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.ВидРегистра", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Дата"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Дата", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.НачалоПериода) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Начало периода"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.НачалоПериода", , Отказ);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.КонецПериода) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Конец периода"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.КонецПериода", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Организация"" не заполнено'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "ПолеОрганизация", Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьПризнакПодписания();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Файл"
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("ВладелецФайла")
		И Параметр.ВладелецФайла = Объект.Ссылка Тогда

		ЗаполнитьПризнакПодписания();
		УправлениеФормой(ЭтаФорма);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
	
&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
	
&НаКлиенте
Процедура ОткрытьПрисоединенныйФайл(Команда)
	
	РегистрыУчетаКлиент.ОткрытьРегистр(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьЭП(Команда)
	РегистрыУчетаКлиент.ПодписатьРегистрУчета(Объект.Ссылка, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВместеСЭП(Команда)
	РегистрыУчетаКлиент.СохранитьВместеСЭП(Объект.Ссылка, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ОбщегоНазначенияБПВызовСервера.ЗаполнитьСписокОрганизаций(Элементы.ПолеОрганизация, СоответствиеОрганизаций);
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьЗначениеПолеОрганизация(
			ПолеОрганизация, Объект.Организация, Объект.ВключатьОбособленныеПодразделения);
	
	ЗаполнитьПризнакПодписания();
	
	Заголовок = Объект.Описание;
	
	УправлениеФормой(ЭтаФорма);
	
	Элементы.ПодразделениеОрганизации.Видимость = ЗначениеЗаполнено(Объект.ПодразделениеОрганизации);
	
	ИспользуетсяЭП = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи");
    
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакПодписания()
	
	ПодписанЭП = Ложь;
	
	СвойстваПрисоединенныхФайлов = РегистрыУчетаВызовСервера.ПолучитьСвойстваПрисоединенныхФайловРегистра(Объект.Ссылка);
	
	ФайлРегистраУчетаПрисоединен = СвойстваПрисоединенныхФайлов.Количество() > 0;
	
	Для Каждого ПрисоединенныйФайл Из СвойстваПрисоединенныхФайлов Цикл
		
		СвойстваФайла = ПрисоединенныйФайл.Значение;
		
		Если СвойстваФайла.ПодписанЭП Тогда
			
			ПодписанЭП = СвойстваФайла.ПодписанЭП;
			
			Прервать;
			
		Конецесли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ДекорацияПодпись.Доступность = Форма.ФайлРегистраУчетаПрисоединен;
	
КонецПроцедуры

#КонецОбласти
