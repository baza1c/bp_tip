#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользуетсяНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	РегистрыУчетаВызовСервера.УстановитьВидимостьКомандСохранения(ЭтотОбъект);
	
	ИспользуетсяЭП = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи");

	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзменитьОтборРегистровУчета" Тогда

		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметр.Отбор);
		
		ОбновитьОтбор();
		
	ИначеЕсли ИмяСобытия = "Запись_Файл" Тогда

		ОбновитьОтбор();
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ОбновитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	ОбновитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбновитьОтбор();

КонецПроцедуры

&НаКлиенте
Процедура ВидРегистраПриИзменении(Элемент)
	
	ОбновитьОтбор();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РегистрыУчетаКлиент.ОткрытьРегистр(ВыбраннаяСтрока);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", НачалоПериода, КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьЭП(Команда)
	
	ВыбраннаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ВыбраннаяСтрока <> Неопределено Тогда
		
		РегистрыУчетаКлиент.ПодписатьРегистрУчета(Элементы.Список.ТекущиеДанные.Ссылка, ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьВместеСЭП(Команда)
	
	ВыбраннаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ВыбраннаяСтрока <> Неопределено Тогда
		
		РегистрыУчетаКлиент.СохранитьВместеСЭП(Элементы.Список.ТекущиеДанные.Ссылка, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРегистрУчета(Команда)
	
	ВыбраннаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ВыбраннаяСтрока <> Неопределено Тогда
		
		РегистрыУчетаКлиент.ОткрытьРегистр(Элементы.Список.ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьОтбор()
	
	// Обработка быстрых отборов
	Список.Отбор.Элементы.Очистить();
	
	Если ЗначениеЗаполнено(Организация) И (ИспользуетсяНесколькоОрганизаций) Тогда
	
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Организация;
		
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(НачалоПериода) Тогда
	
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НачалоПериода");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
		Отбор.ПравоеЗначение = НачалоПериода;
		
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда
	
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КонецПериода");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
		Отбор.ПравоеЗначение = КонецПериода;
		
	КонецЕсли; 
		
	Если ЗначениеЗаполнено(ВидРегистра) Тогда
	
		Отбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидРегистра");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = ВидРегистра;
		
	КонецЕсли; 
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	ОбновитьОтбор();

КонецПроцедуры

#КонецОбласти 
