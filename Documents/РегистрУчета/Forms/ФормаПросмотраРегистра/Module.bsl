#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользуетсяЭП = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи");
	
	Документ = Параметры.ДокументРегистрУчета;
	
	Организация = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Организация").Организация;
	
	ОбновитьФорму(Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СохраненВСтаройВерсии Тогда
	
		Отказ = Истина;
		
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайлаРегистра);
		
		Возврат;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	БухгалтерскиеОтчетыКлиент.НачатьРасчетСуммыВыделенныхЯчеек(
		Элементы.Результат,
		ЭтотОбъект,
		"Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодписатьЭП(Команда)
	ОбработчикЗавершенияПодписания = 
		Новый ОписаниеОповещения("ПослеПодписанияФайла", ЭтотОбъект);
	РегистрыУчетаКлиент.ПодписатьРегистрУчета(Документ, ЭтотОбъект, ОбработчикЗавершенияПодписания);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВместеСЭП(Команда)
	РегистрыУчетаКлиент.СохранитьВместеСЭП(Документ, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеПодписанияФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
	
		ОбновитьФорму();
	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере()
	
	ПолеСумма = БухгалтерскиеОтчетыВызовСервера.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		Результат, КэшВыделеннойОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РезультатПриАктивизацииПодключаемый()
	
	НеобходимоВычислятьНаСервере = Ложь;
	БухгалтерскиеОтчетыКлиент.ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(
		ПолеСумма, Результат, Элементы.Результат, КэшВыделеннойОбласти, НеобходимоВычислятьНаСервере);
	
	Если НеобходимоВычислятьНаСервере Тогда
		ВычислитьСуммуВыделенныхЯчеекТабличногоДокументаВКонтекстеНаСервере();
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_РезультатПриАктивизацииПодключаемый");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФорму(Отказ = Неопределено)
	
	СоответствиеПрисоединенныеФайлы = РегистрыУчетаВызовСервера.ПолучитьСвойстваПрисоединенныхФайловРегистра(Документ, Истина);
	
	СвойстваПрисоединенныхФайлов = Новый ФиксированноеСоответствие(СоответствиеПрисоединенныеФайлы);
	
	Если СвойстваПрисоединенныхФайлов.Количество() = 0 Тогда
		
		Отказ = Истина;
		
		СообщениеОбОшибке = СтрШаблон(НСтр("ru='%1 не содержит присоединенного файла'"), Документ);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, Документ);
		Возврат;
		
	КонецЕсли;

	НайденФайлВMXL = Ложь;
	НайденФайлСПодписью = Ложь;
	
	Для Каждого ПрисоединенныйФайл Из СвойстваПрисоединенныхФайлов Цикл
		
		СвойстваФайла = ПрисоединенныйФайл.Значение;
		
		Если СвойстваФайла.Расширение = "mxl" Тогда
			НайденФайлВMXL = Истина;
			СвойстваФайлаMXL = СвойстваФайла;
		КонецЕсли;
			
		Если СвойстваФайла.ПодписанЭП = Истина Тогда
			НайденФайлСПодписью = Истина;
			СвойстваФайлаСПодписью = СвойстваФайла;
		КонецЕсли;
		
		ДанныеФайлаРегистра = СвойстваФайла.ДанныеФайла;

	КонецЦикла;
	
	Если Не НайденФайлВMXL Тогда // Регистр сохранен в старой версии программы.
	
		СохраненВСтаройВерсии = Истина;
	
	ИначеЕсли НЕ НайденФайлСПодписью Тогда
	
		ДокументБезШтампа =	РегистрыУчетаВызовСервера.ДокументБезШтампа(СвойстваФайлаMXL.ДанныеФайла);
		
		Результат = ДокументБезШтампа;
		ЕстьШтампЭП = Ложь;
		Заголовок = СвойстваФайлаMXL.Описание;
		Элементы.ПодписатьЭП.Видимость = Истина;
		Элементы.СохранитьВместеСЭП.Видимость = Ложь;
		
	Иначе
		
		ДокументСоШтампом =	РегистрыУчетаВызовСервера.ДокументСоШтампом(СвойстваФайлаMXL.ДанныеФайла, СвойстваФайлаСПодписью.ДанныеФайла, Организация);
		
		Результат = ДокументСоШтампом;
		ЕстьШтампЭП = Истина;
		Заголовок = СвойстваФайлаMXL.Описание;
		Элементы.ПодписатьЭП.Видимость = Ложь;
		Элементы.СохранитьВместеСЭП.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
