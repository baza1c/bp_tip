#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПодготовитьПараметрыПроведения(ДокументСсылка) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Процедура СформироватьДвиженияРасходыНаОнлайнКассыУменьшающиеНалоги(ТаблицаРеквизиты, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыРегистрацияРасходовУменьшающихНалоги(ТаблицаРеквизиты);
	
	ДанныеРасхода = Параметры.Реквизиты[0];
	
	Движение = Движения.РасходыНаОнлайнКассыУменьшающиеНалоги.ДобавитьПриход();
	ЗаполнитьЗначенияСвойств(Движение, ДанныеРасхода);
	Движение.СистемаНалогообложения = ДанныеРасхода.УменьшаемыйНалог;
	
	Движения.РасходыНаОнлайнКассыУменьшающиеНалоги.Записывать = Истина;
	
КонецПроцедуры

Функция ПределРасходовНаОнлайнКассу() Экспорт
	
	Возврат 28000;
	
КонецФункции

Функция МаксимальнаяДатаРегистрацииКассы(ДатаРасхода = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаРасхода) Тогда
		ДатаРасхода = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ДатаРасхода < Дата(2024, 1, 1) Тогда
		Возврат КонецДня(Дата(2019, 7, 1));
	Иначе
		Возврат КонецДня(Дата(2025, 12, 31));
	КонецЕсли;
	
КонецФункции

Функция МинимальнаяДатаРегистрацииКассы(ДатаРасхода = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаРасхода) Тогда
		ДатаРасхода = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ДатаРасхода < Дата(2024, 1, 1) Тогда
		Возврат Дата(2017, 2, 1);
	Иначе
		Возврат Дата(2022, 1, 1);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДокументРасходыНаОнлайнКассы.Ссылка КАК Регистратор,
	|	ДокументРасходыНаОнлайнКассы.Дата КАК Период,
	|	ДокументРасходыНаОнлайнКассы.УменьшаемыйНалог КАК УменьшаемыйНалог,
	|	ДокументРасходыНаОнлайнКассы.Организация КАК Организация,
	|	ДокументРасходыНаОнлайнКассы.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ДокументРасходыНаОнлайнКассы.Ссылка КАК ДокументРасхода,
	|	ДокументРасходыНаОнлайнКассы.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.РасходыНаОнлайнКассыУменьшающиеНалоги КАК ДокументРасходыНаОнлайнКассы
	|ГДЕ
	|	ДокументРасходыНаОнлайнКассы.Ссылка = &Ссылка"
	;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ПодготовитьПараметрыРегистрацияРасходовУменьшающихНалоги(ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы реквизитов для регистрации расходов на онлайн-кассы.
	СписокОбязательныхКолонок = ""
	+ "Период,"                      // <Дата> - дата документа, записывающего движения
	+ "Регистратор,"                 // <ДокументСсылка.РасходыНаОнлайнКассыУменьшающиеНалоги> - документ, записывающий движения в регистры
	+ "УменьшаемыйНалог,"            // <ПеречислениеСсылка.ВидыНалоговУменьшаемыхНаРасходыККТ> - вид уменьшаемого налога
	+ "Организация,"                 // <СправочникСсылка.Организации> - организация документа
	+ "РегистрацияВНалоговомОргане," // <СправочникСсылка.РегистрацииВНалоговомОргане> - регистрация в ИФНС, к которой относятся расходы
	+ "ДокументРасхода,"             // <ДокументСсылка.РасходыНаОнлайнКассыУменьшающиеНалоги> - документ регистрации расхода на онлайн-кассу
	+ "Сумма"                        // <Число, 10, 0> - сумма расходов на онлайн-кассу
	;
	
	Параметры.Вставить("Реквизиты", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

#Область ОбработчикиОбновления

Процедура ЗаполнитьУменьшаемыйНалог() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходыНаОнлайнКассыУменьшающиеНалоги.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РасходыНаОнлайнКассыУменьшающиеНалоги КАК РасходыНаОнлайнКассыУменьшающиеНалоги
	|ГДЕ
	|	РасходыНаОнлайнКассыУменьшающиеНалоги.УменьшаемыйНалог = ЗНАЧЕНИЕ(Перечисление.ВидыНалоговУменьшаемыхНаРасходыККТ.ПустаяСсылка)"
	;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		// До появления реквизита документ регистрировал только расходы, уменьшающие ЕНВД.
		ДокументОбъект.УменьшаемыйНалог = Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.ЕНВД;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли