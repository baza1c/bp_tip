#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	МинимальнаяДата = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.МинимальнаяДатаРегистрацииКассы(Дата);
	Если Дата < МинимальнаяДата Тогда
		Дата = МинимальнаяДата;
	КонецЕсли;
	
	Если УменьшаемыйНалог = Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.ЕНВД Тогда
		// Для ЕНВД документ регистрируется в конце квартала.
		Дата = КонецКвартала(Дата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Проведен Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	РасходыНаОнлайнКассыУменьшающиеНалоги.Дата КАК Дата
			|ИЗ
			|	Документ.РасходыНаОнлайнКассыУменьшающиеНалоги КАК РасходыНаОнлайнКассыУменьшающиеНалоги
			|ГДЕ
			|	РасходыНаОнлайнКассыУменьшающиеНалоги.Ссылка = &Ссылка
			|	И РасходыНаОнлайнКассыУменьшающиеНалоги.Проведен";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.Дата < Дата Тогда
			ДополнительныеСвойства.Вставить("ПредыдущаяДата", Выборка.Дата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	// Заводской номер и регистрационные данные ККМ уникальны и не копируются.
	ЗаводскойНомер       = "";
	ДатаРегистрации      = '00010101';
	РегистрационныйНомер = "";
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// Проверка реквизитов шапки документа
	
	ПределРасходов = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.ПределРасходовНаОнлайнКассу();
	Если СуммаДокумента > ПределРасходов Тогда
		ШаблонТекста = НСтр("ru = 'Неверно указана сумма расходов.
			|Максимально допустимая сумма для уменьшения налога - %1 руб.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, ПределРасходов);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "СуммаРасходов", , Отказ);
	КонецЕсли;
	
	МаксимальнаяДатаРегистрации = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.МаксимальнаяДатаРегистрацииКассы(Дата);
	МинимальнаяДатаРегистрации = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.МинимальнаяДатаРегистрацииКассы(Дата);
	Если ДатаРегистрации > МаксимальнаяДатаРегистрации Или ДатаРегистрации < МинимальнаяДатаРегистрации Тогда
		ШаблонТекста = НСтр("ru = 'Неверно указана дата регистрации.
			|Для уменьшения налога касса должна быть зарегистрирована в период с %1 до %2'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста,
			Формат(МинимальнаяДатаРегистрации, "ДЛФ=DD"), Формат(МаксимальнаяДатаРегистрации, "ДЛФ=DD"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаРегистрации", , Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.ПодготовитьПараметрыПроведения(Ссылка);
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	Документы.РасходыНаОнлайнКассыУменьшающиеНалоги.СформироватьДвиженияРасходыНаОнлайнКассыУменьшающиеНалоги(
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
	СдвинутьГраницуАктуальностиРегламентныхОпераций();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
	СдвинутьГраницуАктуальностиРегламентныхОпераций();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СдвинутьГраницуАктуальностиРегламентныхОпераций()
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если УменьшаемыйНалог = Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.УСН Тогда
		ВидРегламентнойОперации = Перечисления.ВидыРегламентныхОпераций.РасчетНалогаУСН;
	Иначе
		ВидРегламентнойОперации = Перечисления.ВидыРегламентныхОпераций.РасчетРасходовУменьшающихОтдельныеНалоги;
	КонецЕсли;
	
	НомерГруппыРегламентнойОперации = Перечисления.ВидыРегламентныхОпераций.НомерГруппы(ВидРегламентнойОперации);
	
	ГраницаАктуальности = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства, "ПредыдущаяДата", Дата);
	
	РегистрыСведений.НеактуальныеРегламентныеОперации.СдвинутьГраницуАктуальностиНазад(
		Организация, ГраницаАктуальности, НомерГруппыРегламентнойОперации, ВидРегламентнойОперации, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли