#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстВыбора;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		Взаимодействия.УстановитьПредметПоДаннымЗаполнения(Параметры, Предмет);
	КонецЕсли;
	
	Взаимодействия.ПодготовитьОповещения(ЭтотОбъект, Параметры);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СписокВыборкаАдресовЭлектроннойПочты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Параметры, "СписокАдресовЭлектроннойПочты");

	Если Параметры.Свойство("ДанныеУчастника") И Объект.Контрагент <> Параметры.ДанныеУчастника.Контакт Тогда
		ЗаполнитьНаОснованииУчастника(Параметры.ДанныеУчастника);
	КонецЕсли;
	
	Если Параметры.Свойство("ЗначенияЗаполнения") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
		ЗначенияЗаполнения = Параметры.ЗначенияЗаполнения;
		Если ТипЗнч(ЗначенияЗаполнения) = Тип("Структура") Тогда
			
			ИсключемыеСвойства = "";
			Если ЗначенияЗаполнения.Свойство("Участники") Тогда
				ИсключемыеСвойства = "Участники";
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Объект, ЗначенияЗаполнения, , ИсключемыеСвойства);
			
			Если ЗначенияЗаполнения.Свойство("Участники") И ЗначениеЗаполнено(ЗначенияЗаполнения.Участники) Тогда
				Участники = ПолучитьИзВременногоХранилища(ЗначенияЗаполнения.Участники);
				Объект.Участники.Загрузить(Участники);
			КонецЕсли;
			Если ЗначенияЗаполнения.Свойство("ПредметВзаимодействия") 
				И ЗначениеЗаполнено(ЗначенияЗаполнения.ПредметВзаимодействия) Тогда
				Предмет = ЗначенияЗаполнения.ПредметВзаимодействия;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	ВзаимодействияФормыБП.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

	Если СписокВыборкаАдресовЭлектроннойПочты <> Неопределено Тогда
		Элементы.ЭлектроннаяПочтаКонтактногоЛица.СписокВыбора.ЗагрузитьЗначения(
			СписокВыборкаАдресовЭлектроннойПочты.ВыгрузитьЗначения());
		Элементы.ЭлектроннаяПочтаКонтактногоЛица.КнопкаВыпадающегоСписка = СписокВыборкаАдресовЭлектроннойПочты.Количество() > 1;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодменюСоздатьНаОсновании", "Видимость", Ложь);
	
	Элементы.ДокументОснование.ТолькоПросмотр = ТолькоПросмотр;
	
	ИзменилисьКонтакты = Ложь;
	
	ВзаимодействияФормыБП.УстановитьОтборКонтактногоЛица(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	УправлениеФормой(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения	

	Предмет = Объект.Предмет;

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПроверитьЗаполнениеРеквизитов(Отказ);
	
	Если Объект.Участники.Количество() = 0 Тогда
		Объект.Участники.Добавить();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Участники[0].Контакт) Тогда
		СтрокаУчастники = Объект.Участники[0];
		Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
			СтрокаУчастники.Контакт = Объект.КонтактноеЛицо;
		ИначеЕсли ЗначениеЗаполнено(Объект.Контрагент) Тогда
			СтрокаУчастники.Контакт = Объект.Контрагент;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПредставлениеКонтактаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзмененоСостояниеЛидов") Тогда
		ПараметрыЗаписи.Вставить("ИзмененоСостояниеЛидов", ТекущийОбъект.ДополнительныеСвойства.ИзмененоСостояниеЛидов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИзмененоСостояниеЛидов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ПараметрыЗаписи, "ИзмененоСостояниеЛидов", Ложь);
	Если ИзмененоСостояниеЛидов Тогда
		ОповеститьОбИзменении(Тип("СправочникСсылка.КонтактныеЛица"));
		Оповестить("ИзмененоСостояниеЛидов", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.КонтактноеЛицо));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	ТекущийОбъект.Предмет = Предмет;
	
	Взаимодействия.ПередЗаписьюВзаимодействияИзФормы(ЭтотОбъект, ТекущийОбъект, ИзменилисьКонтакты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ВзаимодействияКлиент.ОтработатьОповещение(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ВзаимодействияКлиентБП.ФормаОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора, КонтекстВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ОчиститьКонтактнуюИнформацию();
	
	УстановитьКонтактноеЛицо();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактноеЛицоПриИзменении(Элемент)
	
	ОчиститьКонтактнуюИнформацию();
	
	Если ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		КонтактноеЛицоПриИзмененииНаСервере();
	КонецЕсли;
		
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВзаимодействияКлиентБП.ПредметНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте

Процедура ОтправитьПротоколВстречи(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ПроверкаДокументаЗавершение",
			ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Документ не записан. Записать?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ПараметрыФормы = ПараметрыФормыВзаимодействия();
		ВзаимодействияКлиентБП.СоздатьПисьмо(ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КонтактноеЛицо");
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Контрагент");
	
	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,	
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Объект.Контрагент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Объект.КонтактноеЛицо", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КонтактноеЛицо");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Контрагент");
	
	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,	
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Объект.КонтактноеЛицо", ВидСравненияКомпоновкиДанных.Заполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
		"Объект.Контрагент", ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.Контрагент.Видимость = Не ЭтоЛид(Объект.КонтактноеЛицо);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКонтактноеЛицо()
	
	ВзаимодействияФормыБП.УстановитьОтборКонтактногоЛица(ЭтотОбъект);
	ВзаимодействияФормыБП.УстановитьКонтактноеЛицо(ЭтотОбъект, Объект.Участники);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьКонтактнуюИнформацию()
	
	ВзаимодействияФормыБП.УстановитьКонтактнуюИнформацию(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКонтактнуюИнформацию()
	
	Объект.Участники.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаДокументаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Записать() Тогда
		
		ПараметрыФормы = ПараметрыФормыВзаимодействия();
		ВзаимодействияКлиентБП.СоздатьПисьмо(ПараметрыФормы);
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыФормыВзаимодействия()
	
	ЭлектроннаяПочта = "";
	
	Если Объект.Участники.Количество() > 0 Тогда
		ЭлектроннаяПочта = Объект.Участники[0].КакСвязаться;
	КонецЕсли;
	
	ПараметрыФормыВзаимодействия = ВзаимодействияКлиентБП.ПараметрыФормыНовогоПисьмаНаОсновании(ЭтотОбъект, ЭлектроннаяПочта);
	
	Возврат ПараметрыФормыВзаимодействия;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоЛид(КонтактноеЛицо)
	Возврат Справочники.КонтактныеЛица.ЭтоЛид(КонтактноеЛицо);
КонецФункции

&НаСервере
Процедура ЗаполнитьНаОснованииУчастника(ДанныеУчастника)
	
	Если ТипЗнч(ДанныеУчастника.Контакт) = Тип("СправочникСсылка.Контрагенты") Тогда
		Объект.Контрагент = ДанныеУчастника.Контакт;
		Объект.КонтактноеЛицо = Справочники.КонтактныеЛица.КонтактноеЛицоПоУмолчанию(ДанныеУчастника.Контакт);
	ИначеЕсли ТипЗнч(ДанныеУчастника.Контакт) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
		Объект.КонтактноеЛицо = ДанныеУчастника.Контакт;
		Объект.Контрагент = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеУчастника.Контакт, "ОбъектВладелец");
	КонецЕсли;
		
	Если Объект.Участники.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Участник = Объект.Участники.Добавить();
	
	Если ПустаяСтрока(ДанныеУчастника.КакСвязаться) Тогда
		
		УстановитьКонтактнуюИнформацию();
		
	Иначе
		Участник.Контакт = ДанныеУчастника.Контакт;
		Участник.ПредставлениеКонтакта = Строка(ДанныеУчастника.Контакт);
		Участник.ЭлектроннаяПочта = ДанныеУчастника.КакСвязаться;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеКонтактаНаСервере()
	
	Для Каждого Участник Из Объект.Участники Цикл
		Если ЗначениеЗаполнено(Участник.Контакт) Тогда
			Участник.ПредставлениеКонтакта = Строка(Участник.Контакт);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура КонтактноеЛицоПриИзмененииНаСервере()
	
	ВзаимодействияФормыБП.КонтактноеЛицоПриИзмененииНаСервере(ЭтотОбъект);
	УстановитьКонтактнуюИнформацию();
		
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитов(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Тема) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Тема'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"Тема",
				"Объект",
				Отказ);
			КонецЕсли; 
			
	Если Не ЗначениеЗаполнено(Объект.Описание) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Описание события'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"Описание",
				"Объект",
				Отказ);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.Контрагент)
		И Не ЗначениеЗаполнено(Объект.КонтактноеЛицо) Тогда
		
		ТекстСообщения = НСтр("ru = 'Поля ""Контрагент"" и ""Контактное лицо"" не заполнены. Хотя бы одно из них должно быть заполнено'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ТекстСообщения,
				,
				"Контрагент",
				"Объект",
				Отказ);
				
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаГлобальныеКоманды.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ЛеваяКолонка);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ПраваяКолонка);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаДополнительныеРеквизиты);
	
	Элементы.ГруппаКонтактнаяИнформацияПолеЭлектроннаяПочтаКонтактныеЛица.Объединенная = Истина;
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

