#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ОбменСБанкамиДепозиты.ДоступенОбменДепозитами() Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;

	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Организация") Тогда
		Организация = Параметры.Отбор["Организация"];
		Параметры.Отбор.Удалить("Организация");
	Иначе
		Организация = ОбщегоНазначенияБЭД.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(Организация);
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		Список.Отбор, 
		"Организация");
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор,
		"Организация",
		Организация,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ОтборОрганизацияИспользование);
		
	СостояниеОтклонено = "Отклонено";
	СостоянияЭДОтклонено = ОбменСБанкамиДепозитыСлужебный.СостоянияЭДЗаявкиНаДепозитПоСтроке(СостояниеОтклонено);
	Список.Параметры.УстановитьЗначениеПараметра("СостоянияЭДОтклонено", СостоянияЭДОтклонено);
	
	СостояниеОтклоненоПредставление = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ПредставлениеСостоянияЗаявкиНаДепозит(
		СостояниеОтклонено);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеОтклонено", СостояниеОтклоненоПредставление);
	
	СостояниеПодготовлено = "Подготовлено";
	СостоянияЭДПодготовлено = ОбменСБанкамиДепозитыСлужебный.СостоянияЭДЗаявкиНаДепозитПоСтроке(СостояниеПодготовлено);
	Список.Параметры.УстановитьЗначениеПараметра("СостоянияЭДПодготовлено", СостоянияЭДПодготовлено);
	
	СостояниеПодготовленоПредставление = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ПредставлениеСостоянияЗаявкиНаДепозит(
		СостояниеПодготовлено);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеПодготовлено", СостояниеПодготовленоПредставление);
	
	СостояниеНаПодписи = "НаПодписи";
	СостоянияЭДНаПодписи = ОбменСБанкамиДепозитыСлужебный.СостоянияЭДЗаявкиНаДепозитПоСтроке(СостояниеНаПодписи);
	Список.Параметры.УстановитьЗначениеПараметра("СостоянияЭДНаПодписи", СостоянияЭДНаПодписи);
	
	СостояниеНаПодписиПредставление = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ПредставлениеСостоянияЗаявкиНаДепозит(
		СостояниеНаПодписи);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеНаПодписи", СостояниеНаПодписиПредставление);
	
	СостояниеИсполнено = "Исполнено";
	СостояниеИсполненоПредставление = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ПредставлениеСостоянияЗаявкиНаДепозит(
		СостояниеИсполнено);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеИсполнено", СостояниеИсполненоПредставление);
	
	СостояниеВОбработке = "ВОбработке";
	СостояниеВОбработкеПредставление = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ПредставлениеСостоянияЗаявкиНаДепозит(
		СостояниеВОбработке);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеВОбработке", СостояниеВОбработкеПредставление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьСостояниеОбменСБанками" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	УстановитьОтборПоОрганизации();
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(Организация);
	УстановитьОтборПоОрганизации();
КонецПроцедуры

#КонецОбласти

#Область СллужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтборПоОрганизации()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		Список.Отбор, 
		"Организация");
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор,
		"Организация",
		Организация,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ОтборОрганизацияИспользование);
	
КонецПроцедуры

#КонецОбласти

