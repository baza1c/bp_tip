#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыПредложений;

&НаКлиенте
Перем СтраницыПомощника;

&НаКлиенте
Перем ПараметрыПользователя;

&НаКлиенте
Перем ПараметрыРасчетаДепозита;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступенОбменДепозитами = ОбменСБанкамиДепозиты.ДоступенОбменДепозитами();
	ЕстьПравоОбработкиЭД = ОбменСБанкамиСлужебный.ПравоОбработкиЭД();
	ИнформационнаяБазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	ЭтоНоваяЗаявка = Параметры.Ключ.Пустая(); 
	НастройкаОбменаПоддерживаетДепозиты = Истина;
	Заголовок = СтрШаблон(НСтр("ru = 'Заявка на депозит от %1'"), Формат(Объект.Дата, "ДЛФ=D"));
	
	Если ЗначениеЗаполнено(Параметры.Сумма) Тогда
		Объект.СуммаДепозита = Параметры.Сумма;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Срок) Тогда
		Объект.СрокДепозита = Параметры.Срок;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Объект.Организация = Параметры.Организация;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.КлючПродукта) Тогда
		КлючПродукта = Параметры.КлючПродукта;
	КонецЕсли;
	
	Если Не ЭтоНоваяЗаявка Тогда
		
		НастройкаОбмена = ОбменСБанками.НастройкаОбмена(Объект.Организация, Объект.Банк);
		
		Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
			НастройкаОбменаПоддерживаетДепозиты = 
				ОбменСБанкамиДепозитыСлужебный.НастройкаПоддерживаетОбменДепозитами(
					НастройкаОбмена);
		КонецЕсли;
		
		ИтогоДоход = Объект.СуммаДепозита + Объект.Доход;
		СостояниеЗаявкиНаДепозит = СостояниеЗаявкиНаДепозитПоЭД(Объект.Ссылка);
		ЗаявкаИсполнена = Объект.Подтвержден;
		
	КонецЕсли;
	
	Если Не ДоступенОбменДепозитами Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеПодготовлено = "Подготовлено";
	СостояниеНаПодписи = "НаПодписи";
	
	Если Не ЗначениеЗаполнено(СостояниеЗаявкиНаДепозит) 
		Или СостояниеЗаявкиНаДепозит = СостояниеПодготовлено 
		Или СостояниеЗаявкиНаДепозит = СостояниеНаПодписи Тогда
		
		ИмяРеквизитаБанк = ОбменСБанкамиСлужебный.ИмяПрикладногоРеквизита("БанковскийСчетОрганизации.Банк");
		ИмяРеквизитаОрганизация = ОбменСБанкамиСлужебный.ИмяПрикладногоРеквизита("БанковскийСчетОрганизации.Организация");
		
		МассивПараметров = Новый Массив;
		Если ЗначениеЗаполнено(ИмяРеквизитаБанк) Тогда
			ИмяПараметраСвязиБанк = СтрШаблон("Отбор.%1", ИмяРеквизитаБанк);
			МассивПараметров.Добавить(Новый СвязьПараметраВыбора(ИмяПараметраСвязиБанк, "Объект.Банк"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИмяРеквизитаОрганизация) Тогда
			ИмяПараметраСвязиОрганизация = СтрШаблон("Отбор.%1", ИмяРеквизитаОрганизация);
			МассивПараметров.Добавить(Новый СвязьПараметраВыбора(ИмяПараметраСвязиОрганизация, "Объект.Организация"));
		КонецЕсли;
		
		Если МассивПараметров.Количество() > 0 Тогда
			Элементы.СтраницаПредложения_СчетОткрытия.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивПараметров);
			Элементы.СтраницаПодтверждение_СчетОткрытия.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивПараметров);
		КонецЕсли;
		
		РезультатЗапускаЗадания = ОбменСБанкамиДепозитыВызовСервера.ЗапускЗаданияОбновленияДанныхДепозитныхПродуктов(Ложь);
		ДлительнаяОперация = РезультатЗапускаЗадания.ДлительнаяОперация;
		
		СоздатьЭлементыПредложенийБанка(10);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СтраницыПомощника = СписокСтраницПомощника();
	ПараметрыПредложений = НовыйПараметрыПредложений();
	ПараметрыПользователя = НовыйПараметрыПользователя();
	ПараметрыРасчетаДепозита = НовыйПараметрыРасчетаДепозита();
	
	Если ЭтоНоваяЗаявка И ЗначениеЗаполнено(КлючПродукта) Тогда
		ПараметрыПредложений.ПользовательУказалПараметры = Истина;
		ПараметрыПользователя.Сумма = Объект.СуммаДепозита;
		ПараметрыПользователя.Срок = Объект.СрокДепозита;
	КонецЕсли;
	
	УстановитьВидимостьНавигации(Ложь);
	
	Если ЗначениеЗаполнено(ДлительнаяОперация) И Не ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		
		ТекстСтраницы = ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияОбновлениеПредложений();
		НастроитьСтраницуОжидание(ТекстСтраницы);
		ПоказатьСтраницу("Ожидание");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеОбновленияДанныхДепозитныхПродуктов", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	Иначе
		
		ЗавершитьОбновлениеДанныхДепозитныхПродуктов();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ЗаявкаИсполнена Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОжидаетсяСтатусЗаявки Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "ОтправленоDirectBank" Или ИмяСобытия = "ОбновитьСостояниеОбменСБанками" 
		Или ИмяСобытия = "ЗавершеноЗаданиеОбновленияСессииОбменСБанками" Тогда
		
		ТекущееСостояниеЗаявки = СостояниеЗаявкиНаДепозит;
		НовоеСостояниеЗаявки = СостояниеЗаявкиНаДепозитПоЭД(Объект.Ссылка);
		СостояниеИсполнено = "Исполнено";
		
		Если ЗначениеЗаполнено(НовоеСостояниеЗаявки) Тогда
			СостояниеЗаявкиНаДепозит = НовоеСостояниеЗаявки;
		КонецЕсли;
		
		Если НовоеСостояниеЗаявки = СостояниеИсполнено Тогда
			
			ЗаявкаИсполнена = Истина;
			ОжидаетсяСтатусЗаявки = Ложь;
			ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеЗаявки", 1, Истина);
			
		ИначеЕсли ЗначениеЗаполнено(НовоеСостояниеЗаявки) И
			Не НовоеСостояниеЗаявки = ТекущееСостояниеЗаявки Тогда
			
			ОбработатьСостояниеЗаявкиНаДепозит();
			
		КонецЕсли;
		
		Если НовоеСостояниеЗаявки = "ВОбработке" И ЗапуститьОжиданиеОбработкиЗаявкиНаДепозит Тогда
			ЗапуститьОжиданиеОбработкиЗаявкиНаДепозит = Ложь;
			ПодключитьОбработчикОжидания("Подключаемый_ОжиданиеЗавершениеОбработкиОтправленныхЗаявокНаДепозит", 10, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПользователя.Срок = ВыбранноеЗначение.Срок;
	ПараметрыПользователя.Сумма = ВыбранноеЗначение.Сумма;
	
	Объект.СрокДепозита = ВыбранноеЗначение.Срок;
	Объект.СуммаДепозита = ВыбранноеЗначение.Сумма;
	
	СрокДепозитаПриИзменении();
	
	Для каждого ТекущаяСтрокаПродукта Из ПродуктыБанков Цикл
		Если ТекущаяСтрокаПродукта.КлючПродукта = ВыбранноеЗначение.КлючПродукта Тогда
			ПредложениеБанка = ТекущаяСтрокаПродукта.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПредложений.ПользовательВыбралПредложение = Истина;
	ПараметрыПредложений.ПользовательУказалПараметры = Истина;
	ОбновитьРасчетДоходностиСпискаПродуктов(ВыбранноеЗначение.КлючПродукта, ВыбранноеЗначение.ИдентификаторУсловия);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	МожноПовторитьЗапросПредложений = Ложь;
	Объект.СчетОрганизации = Неопределено;
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		
		Если Объект.Организация = ПараметрыПредложений.Организация Тогда
			ПолученыДанныеПредложений = Истина;
		Иначе
			РезультатПолученияДанныхПредложений = ЗаполнитьПредложенияБанков();
			Если ЗначениеЗаполнено(РезультатПолученияДанныхПредложений.ОшибкаПолученияПредложений) Тогда
				МожноПовторитьЗапросПредложений = Истина;
				ПараметрыРасчетаДепозита.РезультатПредварительногоРасчета = Неопределено;
				НастроитьСтраницуПредложения();
				СтраницаПредложенияПоказатьСообщение("Ошибка", РезультатПолученияДанныхПредложений.ОшибкаПолученияПредложений);
				ПоказатьСтраницу("Предложения");
				Возврат;
			КонецЕсли;
			ПолученыДанныеПредложений = РезультатПолученияДанныхПредложений.ЕстьПредложенияБанков;
		КонецЕсли;
		
	КонецЕсли;
	ПараметрыРасчетаДепозита.РезультатПредварительногоРасчета = Неопределено;
	НастроитьСтраницуПредложения();
	ПоказатьСтраницу("Предложения");
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаПредложения_СуммаДепозитаПриИзменении(Элемент)
	
	ПараметрыПредложений.ПользовательУказалПараметры = Истина;
	ПараметрыПользователя.Сумма = Объект.СуммаДепозита;
	ПриИзмененииПараметраДепозита();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаПредложения_СрокПриИзменении(Элемент)
	
	СрокДепозитаПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаПредложения_ДатаЗакрытияПриИзменении(Элемент)
	
	ДлинаСуток = 24 * 60 * 60;
	Объект.СрокДепозита = Окр((Объект.ДатаЗакрытия - Объект.ДатаОткрытия) / (ДлинаСуток), 0);
	ПараметрыПредложений.ПользовательУказалПараметры = Истина;
	ПараметрыПользователя.Срок = Объект.СрокДепозита;
	
	ПриИзмененииПараметраДепозита();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеБанкаПриИзменении(Элемент)
	
	ПараметрыПредложений.ПользовательВыбралПредложение = Истина;
	ЗаполнитьПараметрыДепозита();

	Если ПараметрыПредложений.ПользовательУказалПараметры Тогда
		ПриИзмененииПараметраДепозита();
	КонецЕсли;
	
	УстановитьДоступностьКомандыСтраницаПредложения_Далее();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаПредложения_СчетОткрытияПриИзменении(Элемент)
	
	УстановитьДоступностьКомандыСтраницаПредложения_Далее();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаПодтверждение_СчетОткрытияПриИзменении(Элемент)
	
	Элементы.СтраницаПодтверждение_Далее.Доступность = Истина;
	Элементы.ГруппаСтраницаПодтверждение_Информация.Видимость = Ложь;
	ПараметрыРасчетаДепозита.РезультатРасчетаВБанке = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура НавигацияДопИнформацияПриИзменении(Элемент)
	ПоказатьИнформациюСтраницыЗаявка(НавигацияДопИнформация);
КонецПроцедуры

&НаКлиенте
Процедура ВложенныеФайлыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПрисоединенныеФайлыТаблица.ТекущиеДанные; 
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФайлСсылка = ТекущиеДанные.Ссылка;
	Если ЗначениеЗаполнено(ФайлСсылка) Тогда
		ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(ФайлСсылка, УникальныйИдентификатор);
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСрокиИУсловияНажатие(Элемент)
	
	ИндексЭлементаПредложение = СтрЗаменить(Элемент.Имя, "СрокиИУсловияПредложение_", "");
	ИмяЭлементаПредложение = СтрШаблон("ЗаголовокПредложение_%1", ИндексЭлементаПредложение);
	
	ЭлементПредложение = Элементы[ИмяЭлементаПредложение];
	
	Если ПродуктыБанков.Количество() = 1 Тогда
		СтрокаПродукта = ПродуктыБанков[0];
	Иначе
		ИдентификаторСтрокиПродукта = ЭлементПредложение.СписокВыбора[0].Значение;
		СтрокаПродукта = ПродуктыБанков.НайтиПоИдентификатору(ИдентификаторСтрокиПродукта);
	КонецЕсли;
	
	ПараметрыПродукта = ОбменСБанкамиДепозитыКлиент.НовыеПараметрыПродукта();
	
	ПараметрыПродукта.КлючПродукта = СтрокаПродукта.КлючПродукта;
	ПараметрыПродукта.Наименование = СтрокаПродукта.Наименование;
	ПараметрыПродукта.НаименованиеБанка = СтрокаПродукта.НаименованиеБанка;
	ПараметрыПродукта.ПодробноеОписание = СтрокаПродукта.ПодробноеОписание;
	ПараметрыПродукта.ОдинБанк = ПараметрыПредложений.ОдинБанк;
	ПараметрыПродукта.РасчетПриОформлении = СтрокаПродукта.РасчетПриОформлении;
	
	ОтборПараметрыПользователя = ОбменСБанкамиДепозитыКлиент.НовыеПараметрыПользователя();
	ОтборПараметрыПользователя.Сумма = Объект.СуммаДепозита;
	ОтборПараметрыПользователя.Срок = Объект.СрокДепозита;
	
	ПараметрыОткрытия = ОбменСБанкамиДепозитыКлиент.НовыеПараметрыОткрытияФормыСрокиИУсловия();
	ПараметрыОткрытия.Вставить("АдресХранилищаПредложенийБанков", АдресХранилищаПредложенийБанков);
	ПараметрыОткрытия.Вставить("ПараметрыПродукта", ПараметрыПродукта);
	ПараметрыОткрытия.Вставить("ПараметрыПользователя", ОтборПараметрыПользователя);
	
	ОткрытьФорму("Документ.ЗаявкаНаДепозитОбменСБанками.Форма.СрокиИУсловия",
		ПараметрыОткрытия, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЗаявкиНаДепозитНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСБанкамиКлиент.ОткрытьСписокЭД(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	ОчиститьСообщения();
	СледующийШаг = СтрШаблон("Шаг%1", НавигацияНомерШага+1);
	ОбработатьИзменениеШага(СледующийШаг);
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	ОчиститьСообщения();
	
	СледующийШаг = "Шаг1";
	Если НавигацияНомерШага > 1 Тогда
		СледующийШаг = СтрШаблон("Шаг%1", НавигацияНомерШага - 1);
	КонецЕсли;
	
	Если Не ЭтоНоваяЗаявка И СледующийШаг = "Шаг1" Тогда
		ПараметрыРасчетаДепозита.РезультатПредварительногоРасчета = Неопределено;
		ПараметрыРасчетаДепозита.РезультатРасчетаВБанке = Неопределено;
		ПоказатьСтраницу("Подтверждение");
		РазместитьНавигацию("Подтверждение");
		Возврат;
	КонецЕсли;
	
	Если НавигацияНомерШага = 2 Тогда
		
		РезультатПредварительногоРасчета = ПараметрыРасчетаДепозита.РезультатПредварительногоРасчета;
		Объект.СуммаДепозита = РезультатПредварительногоРасчета.СуммаДепозита;
		Объект.СрокДепозита = РезультатПредварительногоРасчета.Срок;
		Объект.Ставка = РезультатПредварительногоРасчета.Ставка;
		Объект.Доход = РезультатПредварительногоРасчета.СуммаДоход;
		Объект.ДатаЗакрытия = РезультатПредварительногоРасчета.ДатаЗакрытия;
		ИтогоДоход = Объект.СуммаДепозита + Объект.Доход;
		
	КонецЕсли;
	
	ОбработатьИзменениеШага(СледующийШаг);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписок(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ПараметрыОтбора.Вставить("Организация", Объект.Организация);
		ПараметрыОткрытияФормы.Вставить("Отбор", ПараметрыОтбора);
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЗаявкаНаДепозитОбменСБанками.Форма.ФормаСписка", ПараметрыОткрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСостояниеЗаявкиНаДепозит(Команда)
	
	ОчиститьСообщения();
	ОжидаетсяСтатусЗаявки = Истина;
	ЗапуститьОжиданиеОбработкиЗаявкиНаДепозит = ИнформационнаяБазаФайловая;
	СообщениеОбменаЗапрос = СформироватьЗапросСостоянияЗаявкиНаДепозит(Объект.Ссылка);
	ОбменСбанкамиКлиент.СформироватьПодписатьОтправитьЭД(Объект.Ссылка, СообщениеОбменаЗапрос);
	Элементы.СтраницаЗаявкаНаДепозит_ОжиданиеКартинка.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторитьЗапросПредложений(Команда)
	
	РезультатЗапускаЗадания = ОбменСБанкамиДепозитыВызовСервера.ЗапускЗаданияОбновленияДанныхДепозитныхПродуктов(Истина);
		ДлительнаяОперация = РезультатЗапускаЗадания.ДлительнаяОперация;
	ЗапущеноЗадание = ЗначениеЗаполнено(ДлительнаяОперация);
	
	Если ЗапущеноЗадание Тогда
		УстановитьВидимостьНавигации(Ложь);
	КонецЕсли;
	
	Если ЗапущеноЗадание И Не ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ТекстСтраницы = ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияОбновлениеПредложений();
		НастроитьСтраницуОжидание(ТекстСтраницы);
		ПоказатьСтраницу("Ожидание");
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеОбновленияДанныхДепозитныхПродуктов", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	Иначе
		
		ЗавершитьОбновлениеДанныхДепозитныхПродуктов();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьСообщениеНедоступенОбмен()
	
	ДанныеПодсказки = Новый Массив;
	ДанныеПодсказки.Добавить(Новый ФорматированнаяСтрока(
		НСтр("ru = 'Недоступен обмен с банком по депозитам. Проверьте настройки обмена.'")));
	Элементы.ДекорацияЗаголовокНеДоступенОбмен.Заголовок = Новый ФорматированнаяСтрока(ДанныеПодсказки);
	Элементы.ГруппаПредупреждениеНеДоступенОбмен.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СрокДепозитаПриИзменении()
	
	ПараметрыПредложений.ПользовательУказалПараметры = Истина;
	ПараметрыПользователя.Срок = Объект.СрокДепозита;
	ДлинаСуток = 24 * 60 * 60;
	Объект.ДатаЗакрытия = Объект.ДатаОткрытия + (Объект.СрокДепозита * ДлинаСуток);
	
	ПриИзмененииПараметраДепозита();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбновленияДанныхДепозитныхПродуктов(Результат, ДополнительныеПараметры) Экспорт
	
	ТекстСообщения = "";
	
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда // задание было отменено
		ТекстСообщения =  НСтр("ru = 'Не удалось отправить запрос в банк.'");
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		
		НастроитьСтраницуОжидание(ТекстСообщения, Истина);
		ПоказатьСтраницу("Ожидание");
		
	Иначе
		
		ЗавершитьОбновлениеДанныхДепозитныхПродуктов();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОбновлениеДанныхДепозитныхПродуктов()
	
	ДлительнаяОперация = Неопределено;
	ПоказатьСтраницу(Неопределено);
	
	РезультатПолученияДанныхПредложений = Неопределено;
	НастроитьПоСостоянию = Ложь;
	
	Если ЗначениеЗаполнено(СостояниеЗаявкиНаДепозит) Тогда
		НастроитьПоСостоянию = Истина;
	КонецЕсли;
	
	Если ДоступенОбменДепозитами И НастройкаОбменаПоддерживаетДепозиты Тогда
		
		Если НастроитьПоСостоянию Тогда
			РезультатПолученияДанныхПредложений = ЗаполнитьТаблицуПродуктовНаСервере();
		ИначеЕсли ЗначениеЗаполнено(Объект.Организация) Тогда
			РезультатПолученияДанныхПредложений = ЗаполнитьПредложенияБанков();
		КонецЕсли;
		
	КонецЕсли;
		
	Если НастроитьПоСостоянию Тогда
		
		Если ПродуктыБанков.Количество() > 0 Тогда
			ПолученыДанныеПредложений = Истина;
			СтруктураПоиска = Новый Структура("Банк, ИдентификаторПродукта", 
				Объект.Банк, Объект.ИдентификаторПродукта);
			НайденныеСтроки = ПродуктыБанков.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() > 0 Тогда
				ТекущаяСтрокаПродукта = НайденныеСтроки[0];
				ПредложениеБанка = ТекущаяСтрокаПродукта.ПолучитьИдентификатор();
				КлючПродукта = ТекущаяСтрокаПродукта.КлючПродукта;
			КонецЕсли;
		КонецЕсли;
		
		НастроитьФормуПоСостояниюЗаявки();
		
	Иначе
		
		УстановитьВидимостьНавигации();
		ОбработатьИзменениеШага("Шаг1");
		
	КонецЕсли;
	
	МожноПовторитьЗапросПредложений = Ложь;
	ОшибкаПолученияПредложений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		РезультатПолученияДанныхПредложений, "ОшибкаПолученияПредложений", "");
	Если ЗначениеЗаполнено(ОшибкаПолученияПредложений) Тогда
		МожноПовторитьЗапросПредложений = Истина;
		НастроитьСтраницуОжидание(ОшибкаПолученияПредложений, Истина);
		ПоказатьСтраницу("Ожидание");
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьПараметрыПродуктаИЗаписатьЗаявку()
	
	ТекущийПродукт = ПродуктыБанков.НайтиПоИдентификатору(ПредложениеБанка);
	Объект.Валюта = ТекущийПродукт.Валюта;
	Объект.НаименованиеПродукта = ТекущийПродукт.Наименование;
	КлючПродукта = ТекущийПродукт.КлючПродукта;
	Объект.ИдентификаторПродукта = ТекущийПродукт.ИдентификаторПродукта;
	Объект.ОписаниеПродукта = ТекущийПродукт.ПодробноеОписание;
	Объект.Ответственный = ПользователиКлиент.АвторизованныйПользователь();
	
	Возврат Записать();
	
КонецФункции

&НаКлиенте
Функция ПроверитьИЗаписатьЗаявкуНаДепозит()
	
	ПараметрыЗаявки = Новый Структура;
	
	ПараметрыЗаявки.Вставить("СуммаДепозита", Объект.СуммаДепозита);
	ПараметрыЗаявки.Вставить("СрокДепозита", Объект.СрокДепозита);
	ПараметрыЗаявки.Вставить("ДатаЗакрытия", Объект.ДатаЗакрытия);
	ПараметрыЗаявки.Вставить("Доход", Объект.Доход);
	ПараметрыЗаявки.Вставить("Ответственный", ПользователиКлиент.АвторизованныйПользователь());
	
	ИзменилисьДанныеЗавки = ДанныеЗаявкиИзменены(Объект.Ссылка, ПараметрыЗаявки);
	
	Результат = Истина;

	Если ИзменилисьДанныеЗавки Тогда
		Результат = ЗаполнитьПараметрыПродуктаИЗаписатьЗаявку();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеЗаявкиИзменены(СсылкаНаЗаявку, ПараметрыЗаявки)
	
	РеквизитыЗаявки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаЗаявку,
		"СуммаДепозита, СрокДепозита, ДатаЗакрытия, Доход, Ответственный");
	Возврат Не ОбщегоНазначения.ДанныеСовпадают(ПараметрыЗаявки, РеквизитыЗаявки);
	
КонецФункции

&НаКлиенте
Функция ЗаполнитьПредложенияБанков()
	
	РезультатПолученияДанных = ЗаполнитьПредложенияБанковНаСервере(ПараметрыПользователя, Ложь, КлючПродукта);
	
	Если РезультатПолученияДанных.ЕстьПредложенияБанков Тогда
		ПолученыДанныеПредложений = Истина;
		ЗаполнитьПараметрыПредложений(РезультатПолученияДанных);
		ЗаполнитьПараметрыДепозита();
	КонецЕсли;
		
	Возврат РезультатПолученияДанных;
	
КонецФункции

// Выполняет расчет предложений и обновляет данные предложений банков на форме.
// При этом:
//   - если передан параметры КлючПродукта уставнавливает продукт первым в таблице.
//   - если переданы параметры КлючПродукта и ВыбранноеУсловие уставливает выбранное условие
//        как текущее условие продукта.
// 
// Параметры:
//  ПараметрыПользователя - Структура: см. НовыйПараметрыПользователя.
//  ПрочитатьИзХранилища - Булево - если Истина, данные продуктов банков используются из временного хранилища.
//  ВыбранныйПродукт - Строка, Неопределено - уникальный идентификатор депозитного продукта.
//  ВыбранноеУсловие - Строка, Неопределено - уникальный идентификатор условия депозитного продукта.
// Возвращаемое значение:
//  Структура:
//   * ЕстьПредложенияБанков - Булево - если Истина, были получены данные предложений банков.
//   * ОшибкаПолученияПредложений - Строка - содержит текст ошибки, если не удалось получить предложения ни от одного банка.
//   * ОдинБанк - Булево - если Истина, все предложения от одного банка.
//   * УсловияТекущегоПродукта - Массив из Структура - Список условий текущего продукта.
//
&НаСервере
Функция ЗаполнитьПредложенияБанковНаСервере(ПараметрыПользователя, ПрочитатьИзХранилища, 
	Знач ВыбранныйПродукт = Неопределено, Знач ВыбранноеУсловие = Неопределено)
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьПредложенияБанков", Ложь);
	Результат.Вставить("ОшибкаПолученияПредложений", "");
	Результат.Вставить("ОдинБанк", Ложь);
	Результат.Вставить("УсловияТекущегоПродукта", Новый Массив);
	
	Если ПрочитатьИзХранилища И ЗначениеЗаполнено(АдресХранилищаПредложенийБанков) Тогда
		ДанныеПродуктыБанков = ПолучитьИзВременногоХранилища(АдресХранилищаПредложенийБанков);
	Иначе
		ДанныеПродуктыБанков = ОбменСБанкамиДепозитыСлужебный.ДанныеДепозитныхПродуктов(Объект.Организация);
	КонецЕсли;
	
	Результат.ОшибкаПолученияПредложений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеПродуктыБанков, "ОшибкаПолученияПредложений", "");
		
	Если Не ЗначениеЗаполнено(ДанныеПродуктыБанков.Продукты) Или Не ЗначениеЗаполнено(ДанныеПродуктыБанков.Условия) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.ЕстьПредложенияБанков = Истина;
	
	Если Не ПрочитатьИзХранилища Тогда
		Если Не ЗначениеЗаполнено(АдресХранилищаПредложенийБанков) Тогда
			АдресХранилищаПредложенийБанков = ПоместитьВоВременноеХранилище(
				ДанныеПродуктыБанков, УникальныйИдентификатор);
		Иначе
			ПоместитьВоВременноеХранилище(ДанныеПродуктыБанков, АдресХранилищаПредложенийБанков);
		КонецЕсли;
	КонецЕсли;
	
	ДанныеПредложений = ОбменСБанкамиДепозитыСлужебный.НовыеДанныеПредложенийБанка();
	ДанныеПредложений.Продукты = ДанныеПродуктыБанков.Продукты;
	ДанныеПредложений.Условия = ДанныеПродуктыБанков.Условия;
	
	Срок = 0;
	Сумма = 0;
	
	Если ЗначениеЗаполнено(ПараметрыПользователя) Тогда
		
		Срок = ПараметрыПользователя.Срок;
		Сумма = ПараметрыПользователя.Сумма;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбранныйПродукт) Тогда
		ПараметрПоиска = Новый Структура();
		ПараметрПоиска.Вставить("КлючПродукта", ВыбранныйПродукт);
		НайденныеСтроки = ДанныеПродуктыБанков.Продукты.НайтиСтроки(ПараметрПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			ВыбранныйПродукт = Неопределено;
			ВыбранноеУсловие = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ДатаРасчета = ТекущаяДатаСеанса();
	ОбменСБанкамиДепозитыСлужебный.ПроверитьПродуктыИРассчитатьДоходность(ДанныеПредложений,
		ДатаРасчета, Сумма, Срок);
	
	ПродуктыБанков.Загрузить(ДанныеПредложений.Продукты);
	
	// Проверяем позицию выбранного пользователем продукта в таблице продуктов
	// если он не первый, сдвигаем в начало
	
	ТекущийИндекс = 0;
	Если ЗначениеЗаполнено(ВыбранныйПродукт) Тогда
		Для каждого ТекущаяСтрокаПродукта Из ПродуктыБанков Цикл
			Если ТекущаяСтрокаПродукта.КлючПродукта = ВыбранныйПродукт И ТекущийИндекс <> 0 Тогда
				ПродуктыБанков.Сдвинуть(ТекущийИндекс, -ТекущийИндекс);
				Прервать;
			КонецЕсли;
			ТекущийИндекс = ТекущийИндекс + 1;
		КонецЦикла;
	КонецЕсли;
	
	ВыбранныйПродукт = ПродуктыБанков[0];
	УсловияТекущегоПродукта = ДанныеПродуктыБанков.Условия[ВыбранныйПродукт.КлючПродукта];
	
	// Устанавливаем выбранное условие как текущее условие продукта
	
	Если ЗначениеЗаполнено(ВыбранноеУсловие) Тогда
		
		Для каждого ТекущееУсловие из УсловияТекущегоПродукта Цикл
			
			Если ТекущееУсловие.ИдентификаторУсловия = ВыбранноеУсловие Тогда
				
				ВыбранныйПродукт.Ставка = ТекущееУсловие.Ставка;
				Если Не ВыбранныйПродукт.РасчетПриОформлении Тогда
					ВыбранныйПродукт.Доход = ОбменСбанкамиДепозитыСлужебныйКлиентСервер.РассчитатьДоход(ДатаРасчета,
						Сумма, ТекущееУсловие.Ставка, Срок);
				КонецЕсли;
				ВыбранныйПродукт.Продвигаемый = ТекущееУсловие.Продвигаемый;
				ВыбранныйПродукт.ИдентификаторУсловия = ТекущееУсловие.ИдентификаторУсловия;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьЭлементыПредложенийБанка(ДанныеПродуктыБанков.Условия, ДанныеПредложений.ОдинБанк);
	
	КлючПродукта = ВыбранныйПродукт.КлючПродукта;
	ПредложениеБанка = ВыбранныйПродукт.ПолучитьИдентификатор();
	Результат.Вставить("ОдинБанк", ДанныеПредложений.ОдинБанк);
	Результат.Вставить("УсловияТекущегоПродукта", УсловияТекущегоПродукта);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗаполнитьТаблицуПродуктовНаСервере()
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьПредложенияБанков", Ложь);
	Результат.Вставить("ОшибкаПолученияПредложений", "");
	
	ДанныеПродуктыБанков = ОбменСБанкамиДепозитыСлужебный.ДанныеДепозитныхПродуктов(Объект.Организация);
	
	Продукты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеПродуктыБанков, "Продукты", Неопределено);
	Условия = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеПродуктыБанков, "Условия", Неопределено);
	Результат.ОшибкаПолученияПредложений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДанныеПродуктыБанков, "ОшибкаПолученияПредложений", "");
	
	Если Не ЗначениеЗаполнено(Продукты) Или Не ЗначениеЗаполнено(Условия) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.ЕстьПредложенияБанков = Истина;
	ДанныеПредложений = ОбменСБанкамиДепозитыСлужебный.НовыеДанныеПредложенийБанка();
	ДанныеПредложений.Продукты = Продукты;
	ДанныеПредложений.Условия = Условия;
		
	ДатаРасчета = ТекущаяДатаСеанса();
	ОбменСБанкамиДепозитыСлужебный.ПроверитьПродуктыИРассчитатьДоходность(ДанныеПредложений,
		ДатаРасчета, Объект.СуммаДепозита, Объект.СрокДепозита);
	
	ПродуктыБанков.Загрузить(ДанныеПредложений.Продукты);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеУсловия(УсловияПродукта, ИдентификаторУсловия)
	
	Результат = Неопределено;
	
	Для Каждого ТекущиеУсловие Из УсловияПродукта Цикл
		Если ТекущиеУсловие.ИдентификаторУсловия = ИдентификаторУсловия Тогда
			Результат = ТекущиеУсловие;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НовыйПараметрыУсловияПродукта()
	
	Результат = Новый Структура();
	Результат.Вставить("Валюта", Неопределено);
	Результат.Вставить("Сумма", 0);
	Результат.Вставить("СуммаМинимум", 0);
	Результат.Вставить("СуммаМаксимум", 0);
	Результат.Вставить("Срок", 0);
	Результат.Вставить("СрокМинимум", 0);
	Результат.Вставить("СрокМаксимум", 0);
	Результат.Вставить("Ставка", 0);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПараметрыДепозита()
	
	Предложение = Неопределено;
	
	Если ПродуктыБанков.Количество() = 1 Тогда
		Предложение = ПродуктыБанков[0];
	КонецЕсли;
	
	Если Предложение = Неопределено Тогда
		Предложение = ПродуктыБанков.НайтиПоИдентификатору(ПредложениеБанка);
	КонецЕсли;
	
	Если Предложение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Предложение.ИдентификаторУсловия) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыПредложений.ПользовательУказалПараметры Тогда
		
		Объект.СуммаДепозита = Предложение.Сумма;
		Объект.СрокДепозита = Предложение.Срок;
		ДлинаСуток = 24 * 60 * 60;
		Объект.ДатаЗакрытия = Объект.ДатаОткрытия + (Предложение.Срок * ДлинаСуток);
		
	КонецЕсли;
	
	Объект.Ставка = Предложение.Ставка;
	Объект.Валюта = Предложение.Валюта;
	Элементы.СтраницаПредложения_ГруппаДоходность.Видимость = Истина;
	Элементы.ГруппаДекорацияРасчетПриОформлении.Видимость = Ложь;
	Если Предложение.РасчетПриОформлении Тогда
		Элементы.СтраницаПредложения_ГруппаДоходность.Видимость = Ложь;
		Элементы.ГруппаДекорацияРасчетПриОформлении.Видимость = Истина;
	КонецЕсли;
	Объект.Доход = Предложение.Доход;
	ИтогоДоход = Объект.СуммаДепозита + Предложение.Доход;
	Объект.Банк = Предложение.Банк;
	Объект.СчетОрганизации = ЗаполнитьСчетОрганизацииПоУмолчанию(Объект.Организация,
		Объект.Банк, Объект.Валюта);
	УстановитьДоступностьКомандыСтраницаПредложения_Далее();
		
	Объект.Капитализация = Предложение.Капитализация;
	Объект.СпособВыплатыПроцентов = Предложение.ВыплатаПроцентов;
	
	УстановитьФорматЭлементов();
	
	ПараметрыРасчетаДепозита.РезультатРасчетаВБанке = Новый Структура;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыПредложений(ДанныеПредложений)
	
	ПараметрыПредложений.ОдинБанк = ДанныеПредложений.ОдинБанк;
	ПараметрыПредложений.Организация = Объект.Организация;
	ПараметрыПредложений.УсловияТекущегоПродукта = ДанныеПредложений.УсловияТекущегоПродукта;
	ПараметрыПредложений.ПользовательУказалПараметры = Ложь;
	ПараметрыПредложений.ПользовательВыбралПредложение = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФорматЭлементов()
	
	ФорматВалюты = СтрШаблон("ЧФ='Ч %1'", Объект.Валюта);
	
	Элементы.СтраницаПредложения_Доход.Формат = ФорматВалюты;
	Элементы.СтраницаПредложения_ИтогоДоход.Формат = ФорматВалюты;
	Элементы.СтраницаПодтверждение_ДепозитыСумма.ФорматРедактирования = ФорматВалюты;
	Элементы.СтраницаПодтверждение_Доход.ФорматРедактирования = ФорматВалюты;
	Элементы.СтраницаПодтверждение_Итог.ФорматРедактирования = ФорматВалюты;
	Элементы.СтраницаЗаявкаНаДепозит_СуммаДепозита.Формат = ФорматВалюты;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьФормуПоСостояниюЗаявки()
	
	СостояниеИсполнено = "Исполнено";
	СостояниеОтклонено = "Отклонено";
	
	Если ЕстьПравоОбработкиЭД
		И (Не ДоступенОбменДепозитами Или Не НастройкаОбменаПоддерживаетДепозиты)
		И (Не СостояниеЗаявкиНаДепозит = СостояниеИсполнено И Не СостояниеЗаявкиНаДепозит = СостояниеОтклонено) Тогда
		ПоказатьСообщениеНедоступенОбмен();
	КонецЕсли;
	
	СостояниеПодготовлено = "Подготовлено";
	СостояниеНаПодписи = "НаПодписи";
	УстановитьФорматЭлементов();
	СостояниеЗаявкиНаДепозитПредставление = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ПредставлениеСостоянияЗаявкиНаДепозит(СостояниеЗаявкиНаДепозит);
	УстановитьВидимостьНавигации();
	Если СостояниеЗаявкиНаДепозит = СостояниеПодготовлено
		Или СостояниеЗаявкиНаДепозит = СостояниеНаПодписи Тогда
		
		ЗаполнитьРезультатПредварительногоРасчета();
		НастроитьСтраницуПодтверждение();
		Если ЗначениеЗаполнено(КлючПродукта) Тогда
			ТекстСообщения = НСтр("ru = 'Для отправки заявки требуется расчет депозита.'");
			СтраницаПодтверждениеПоказатьСообщение("Информация", ТекстСообщения);
			Элементы.СтраницаПодтверждение_Далее.Заголовок = ТекстКнопкиДалееСтраницыПодтверждение("Рассчитать");
		КонецЕсли;
		
		ПоказатьСтраницу("Подтверждение");
		РазместитьНавигацию("Подтверждение");
		НавигацияНомерШага = НавигацияНомерШага - 1;
		
	Иначе
		
		Если Объект.Подтвержден Тогда
			
			ЗаявкаИсполнена = Истина;
			
			Если Объект.ЕстьВложение Тогда
				ПрочитатьПрисоединенныеФайлы();
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗаявкаИсполнена Тогда
			ОжидаетсяСтатусЗаявки = Истина;
			РазместитьНавигацию("Открытие");
		КонецЕсли;
		
		НастроитьСтраницуЗаявкаНаДепозит();
		ПоказатьСтраницу("ЗаявкаНаДепозит");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СообщениеОбменаЗаявкиНаДепозит(СсылкаНаЗаявку)
	
	Возврат ОбменСБанкамиСлужебный.СообщениеОбменаПоВладельцу(СсылкаНаЗаявку);
	
КонецФункции

&НаСервереБезКонтекста
Функция СостояниеЗаявкиНаДепозитПоЭД(СсылкаНаЗаявку)
	Возврат ОбменСБанкамиДепозитыСлужебный.СостояниеЗаявкиНаДепозитПоЭД(СсылкаНаЗаявку);
КонецФункции

&НаКлиенте
Процедура ОбработатьСостояниеЗаявкиНаДепозит()
	
	СостояниеПодготовлено = "Подготовлено";
	СостояниеНаПодписи = "НаПодписи";
	СостояниеЗаявкиНаДепозитПредставление = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ПредставлениеСостоянияЗаявкиНаДепозит(СостояниеЗаявкиНаДепозит);
		
	Если СостояниеЗаявкиНаДепозит = СостояниеПодготовлено Тогда
		Элементы.СтраницаПодтверждение_Назад.Доступность = Истина;
	ИначеЕсли СостояниеЗаявкиНаДепозит = СостояниеНаПодписи Тогда
		Элементы.СтраницаПодтверждение_Назад.Доступность = Ложь;
	Иначе
		
		НастроитьСтраницуЗаявкаНаДепозит();
		РазместитьНавигацию("Открытие");
		ПоказатьСтраницу("ЗаявкаНаДепозит");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьДанныеЗаявки()
	
	ОбновитьДанныеЗаявкиНаСервере();
	СостояниеЗаявкиНаДепозитПредставление = 
		ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ПредставлениеСостоянияЗаявкиНаДепозит(СостояниеЗаявкиНаДепозит);
	НастроитьСтраницуЗаявкаНаДепозит();
	РазместитьНавигацию("Открытие");
	ПоказатьСтраницу("ЗаявкаНаДепозит");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеЗаявкиНаСервере()
	
	Прочитать();
	СостояниеЗаявкиНаДепозит = "Исполнено";
	
	Если Объект.ЕстьВложение Тогда
		ПрочитатьПрисоединенныеФайлы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОшибкуПоЭД(СсылкаНаДокумент)
	
	Возврат ОбменСБанками.ПолучитьПричинуОтклонениияДокументаБанком(СсылкаНаДокумент);
	
КонецФункции

&НаСервереБезКонтекста
Функция СформироватьЗапросСостоянияЗаявкиНаДепозит(СсылкаНаЗаявку)
	
	СформироватьИзвещение = Ложь;
	Состояние = ОбменСБанкамиСлужебный.СостояниеЭД(СсылкаНаЗаявку);
	
	Если Состояние = Перечисления.СостоянияОбменСБанками.ПлатежИсполнен Тогда
		СформироватьИзвещение = Истина;
	КонецЕсли;
	
	СсылкаНаЭДЗаявки = ОбменСБанкамиСлужебный.СообщениеОбменаПоВладельцу(СсылкаНаЗаявку);
	СсылкаЗапросаЭД = ОбменСБанкамиДепозитыСлужебный.СформироватьЗапросСостоянияЗаявкиНаДепозит(СсылкаНаЭДЗаявки, СформироватьИзвещение);
	
	Возврат СсылкаЗапросаЭД;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьСчетОрганизацииПоУмолчанию(Организация, Банк, Валюта)
	Возврат ОбменСБанкамиСлужебный.СчетОрганизацииВБанкеПоУмолчанию(Организация, Банк, Валюта);
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОжиданиеЗавершениеОбработкиОтправленныхЗаявокНаДепозит()
	
	ПараметрыПриложенияНастройкиСДаннымиСессии = ОбменСБанкамиСлужебныйКлиент.ЗначениеИзКеша("НастройкиОбменаСДаннымиСессии");
	Если ПараметрыПриложенияНастройкиСДаннымиСессии = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложенияДанныеСессии = ПараметрыПриложенияНастройкиСДаннымиСессии.Получить(НастройкаОбмена);
	
	Если Не ЗначениеЗаполнено(ПараметрыПриложенияДанныеСессии) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСессии = ПараметрыПриложенияДанныеСессии.ИдентификаторСессии;
	Если Не ЗначениеЗаполнено(УниверсальнаяДатаОбновленияСессии) Тогда
		УниверсальнаяДатаОбновленияСессии = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КонецЕсли;
	ВремяЖизниСессии = ПараметрыПриложенияДанныеСессии.ВремяЖизниСессии;
	ТолькоОбменДепозитами = ПараметрыПриложенияДанныеСессии.ТолькоОбменДепозитами;
	
	Если Не ЗначениеЗаполнено(ИдентификаторСессии)
		Или Не ЗначениеЗаполнено(УниверсальнаяДатаОбновленияСессии) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВремяЖизниСессии = 0 
		И Не ТолькоОбменДепозитами Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСессии = ОбменСБанкамиСлужебныйКлиентСервер.НовыеДанныеСессииОбменаСБанком();
	
	ИдентификаторЗаданияОжидания = "";
	
	РезультатЗапускаЗаданияПроверкиСтатуса = Неопределено;
	
	ДанныеСессии = ОбменСБанкамиСлужебныйКлиентСервер.НовыеДанныеСессииОбменаСБанком();
	ДанныеСессии.НастройкаОбмена = НастройкаОбмена;
	ДанныеСессии.ИдентификаторСессии = ИдентификаторСессии;
	ДанныеСессии.УниверсальнаяДатаОбновленияСессии = УниверсальнаяДатаОбновленияСессии;
	ДанныеСессии.ВремяЖизниСессии = ВремяЖизниСессии;
	ДанныеСессии.ТолькоОбменДепозитами = ТолькоОбменДепозитами;
	
	РезультатЗапускаЗаданияПроверкиСтатуса = 
		ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОжиданиеЗавершенияОбработкиДокументовБанком(
			ДанныеСессии, ИдентификаторЗаданияОжидания);
			
	Если Не ЗначениеЗаполнено(РезультатЗапускаЗаданияПроверкиСтатуса) Тогда
		
		Возврат;
		
	КонецЕсли;
		
	ПараметрыПриложенияДанныеСессии.УниверсальнаяДатаОбновленияСессии = УниверсальнаяДатаОбновленияСессии;
	
	ПараметрыПриложенияДанныеСессии.ИдентификаторЗаданияОжидания =
		РезультатЗапускаЗаданияПроверкиСтатуса.ИдентификаторЗадания;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверкаЗавершенияОбработкиОтправленныхЗаявокНаДепозит",
	ЭтотОбъект);

  	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатЗапускаЗаданияПроверкиСтатуса,
		ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

// Проверяет наличие документов, отправленных в банк и находящихся в состоянии обработки.
// Запускает задание ожидания завершения обработки отправленных в банк документов, пока 
// все отправленные документы не перейдут в состояния завершения обработки, или время ожидания
// не превысит лимит в 75 минут.
//
// Параметры:
//  Результат - Структура, Неопределено - Структура - структура со свойствами или Неопределено, 
//    если задание было отменено. Результат.АдресРезультата - адрес временного хранилища в которое
//    помещен результат выполнения функции см. ОбменСБанкамиСлужебный.ОбновитьДанныеОткрытойСесииОбмена.
//  ДополнительныеПараметры - Структура, Неопределено - дополнительные параметры переданные при вызове оповещения.
//
&НаКлиенте
Процедура ПроверкаЗавершенияОбработкиОтправленныхЗаявокНаДепозит(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда;
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПриложенияНастройкиСДаннымиСессии = ОбменСБанкамиСлужебныйКлиент.ЗначениеИзКеша("НастройкиОбменаСДаннымиСессии");
	
	Если ПараметрыПриложенияНастройкиСДаннымиСессии = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Результат.НастройкаОбмена;
	ПараметрыПриложенияДанныеСессии = ПараметрыПриложенияНастройкиСДаннымиСессии.Получить(НастройкаОбмена);
	
	Если ПараметрыПриложенияДанныеСессии = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДлительностьСессии = (Результат.УниверсальнаяДатаОбновленияСессии
		- ПараметрыПриложенияДанныеСессии.УниверсальнаяДатаОбновленияСессии)
		/ 1000;
	
	Если ДлительностьСессии > ОбменСБанкамиСлужебныйКлиентСервер.ДлительностьОткрытойСессииОбменаСБанком() Тогда
		Возврат;
	КонецЕсли;
	
	ВремяЖизниСессии = ПараметрыПриложенияДанныеСессии.ВремяЖизниСессии;
	Если ВремяЖизниСессии = 0 И ПараметрыПриложенияДанныеСессии.ТолькоОбменДепозитами Тогда
		ВремяЖизниСессии = ОбменСБанкамиСлужебныйКлиентСервер.ВремяЖизниСессииПоУмолчанию();
	КонецЕсли;
	
	ДлительностьТекущейСессии = (ТекущаяУниверсальнаяДатаВМиллисекундах()
		- Результат.УниверсальнаяДатаОбновленияСессии) / 1000;
		
	Если ДлительностьТекущейСессии >= ВремяЖизниСессии Тогда
		Возврат;
	КонецЕсли;
	
	НовоеСостояниеЗаявки = СостояниеЗаявкиНаДепозитПоЭД(Объект.Ссылка);
	ДлительностьПаузы = Окр(ВремяЖизниСессии * 0.50 - ДлительностьТекущейСессии);
	
	Если НовоеСостояниеЗаявки = "ВОбработке" Тогда
		
		ПодключитьОбработчикОжидания("Подключаемый_ОжиданиеЗавершениеОбработкиОтправленныхЗаявокНаДепозит", ДлительностьПаузы, Истина);
		ПараметрыПриложенияДанныеСессии.ИдентификаторЗаданияОжидания = "";
		
	ИначеЕсли НовоеСостояниеЗаявки = "Исполнено" Тогда
		
		ЗаявкаИсполнена = Истина;
		ОжидаетсяСтатусЗаявки = Ложь;
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеЗаявки", 1, Истина);
		
	ИначеЕсли НовоеСостояниеЗаявки = "Отклонено" Тогда
		
		ОжидаетсяСтатусЗаявки = Ложь;
		ОбработатьСостояниеЗаявкиНаДепозит();
		
	КонецЕсли;
	
КонецПроцедуры

#Область ОтправкаЗаявкиНаДепозит

&НаКлиенте
Процедура НачатьОтправкуЗаявкиНаДепозитВБанк() Экспорт
	
	СообщениеОбмена = Неопределено;
	
	Если СостояниеЗаявкиНаДепозит = "Подготовлено"
		Или СостояниеЗаявкиНаДепозит = "НаПодписи" Тогда
		СообщениеОбмена = СообщениеОбменаЗаявкиНаДепозит(Объект.Ссылка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбмена) И СостояниеЗаявкиНаДепозит = "НаПодписи" Тогда
		МассивСообщенийОбмена = Новый Массив;
		МассивСообщенийОбмена.Добавить(СообщениеОбмена);
		ОбменСБанкамиСлужебныйКлиент.ПодготовитьОкружениеИПодписатьДокументыКодомИзSMS(НастройкаОбмена, МассивСообщенийОбмена);
	Иначе
		ОбменСБанкамиКлиент.СформироватьПодписатьОтправитьЭД(Объект.Ссылка, СообщениеОбмена);
	КонецЕсли;
	
	ОжидаетсяСтатусЗаявки = Истина;
	ЗапуститьОжиданиеОбработкиЗаявкиНаДепозит = ИнформационнаяБазаФайловая;
	
КонецПроцедуры

#КонецОбласти

#Область РасчетДепозитаВБанке

&НаКлиенте
Функция НужноЗапрашиватьРасчетДепозита()
	
	РезультатПредварительногоРасчета = ПараметрыРасчетаДепозита.РезультатПредварительногоРасчета;
	
	Если Не ЗначениеЗаполнено(РезультатПредварительногоРасчета) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ТекущийПродуктБанка = ПродуктыБанков.НайтиПоИдентификатору(ПредложениеБанка);
	Если ТекущийПродуктБанка = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	Если Не ТекущийПродуктБанка.КлючПродукта = РезультатПредварительногоРасчета.КлючПродукта
		Или Не Объект.СуммаДепозита = РезультатПредварительногоРасчета.СуммаДепозита
		Или Не Объект.СрокДепозита = РезультатПредварительногоРасчета.Срок
		Или Не Объект.Ставка = РезультатПредварительногоРасчета.Ставка Тогда
		Возврат Истина;
	КонецЕсли;
	
	РезультатРасчетаВБанке = ПараметрыРасчетаДепозита.РезультатРасчетаВБанке;
	Возврат Не ЗначениеЗаполнено(РезультатРасчетаВБанке);
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьРезультатПредварительногоРасчета()
	
	РезультатПредварительногоРасчета = Новый Структура;
	ПродуктБанка = ПродуктыБанков.НайтиПоИдентификатору(ПредложениеБанка);
	ТекущийКлючПродукта = "";
	
	Если Не ПродуктБанка = Неопределено Тогда
		ТекущийКлючПродукта = ПродуктБанка.КлючПродукта;
	Иначе
		ТекущийКлючПродукта = КлючПродукта;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийКлючПродукта) Тогда
		
		РезультатПредварительногоРасчета.Вставить("КлючПродукта", ТекущийКлючПродукта);
		РезультатПредварительногоРасчета.Вставить("СуммаДепозита", Объект.СуммаДепозита);
		РезультатПредварительногоРасчета.Вставить("Срок", Объект.СрокДепозита);
		РезультатПредварительногоРасчета.Вставить("Ставка", Объект.Ставка);
		РезультатПредварительногоРасчета.Вставить("СуммаДоход", Объект.Доход);
		РезультатПредварительногоРасчета.Вставить("ДатаЗакрытия", Объект.ДатаЗакрытия);
		ПараметрыРасчетаДепозита.РезультатПредварительногоРасчета = РезультатПредварительногоРасчета;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьРасчетДепозита()
	
	ПараметрыДепозита = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.НовыеПараметрыРасчетаДепозитаВБанке();
	ИдентификаторПредложения = Неопределено;
	
	Если ПродуктыБанков.Количество() > 0 Тогда
		ТекущееПредложение = ПродуктыБанков.НайтиПоИдентификатору(ПредложениеБанка);
		ИдентификаторПредложения = ТекущееПредложение.ИдентификаторПродукта;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИдентификаторПредложения) Тогда
		ИдентификаторПредложения = Объект.ИдентификаторПродукта;
	КонецЕсли;
	
	ПараметрыДепозита.ИдентификаторПродукта = ИдентификаторПредложения;
	ПараметрыДепозита.СчетОткрытия = Объект.СчетОрганизации;
	ПараметрыДепозита.СуммаДепозита = Объект.СуммаДепозита;
	ПараметрыДепозита.Срок = Объект.СрокДепозита;
	ДанныеЗапросаРасчета = ПолучитьДанныеДляЗапросаРасчетаДепозита(Объект.Организация, Объект.Банк, ПараметрыДепозита);
	
	ОтправитьЗапросРасчетаДепозитаВБанк(ДанныеЗапросаРасчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросРасчетаДепозитаВБанк(ДанныеЗапросаРасчета)
	
	НастройкаОбмена = ДанныеЗапросаРасчета.НастройкаОбмена;
	ДанныеАвторизации = Неопределено;
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросРасчетаДепозитаВБанкЛогинПароль", ЭтотОбъект, 
		ДанныеЗапросаРасчета);
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросРасчетаДепозитаВБанкЛогинПароль(ДанныеАутентификации,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеАутентификации) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = ДополнительныеПараметры.НастройкаОбмена;
	
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("ДанныеАвторизации", ДанныеАутентификации);
	ПараметрыПолучения.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
	ПараметрыПолучения.Вставить("ДанныеСообщенияОбмена", ДополнительныеПараметры.ДанныеСообщенияОбмена);
	ПараметрыПолучения.Вставить("РеквизитыНастройкиОбмена", ДополнительныеПараметры.РеквизитыНастройкиОбмена);
	РеквизитыНастройкиОбмена = ДополнительныеПараметры.РеквизитыНастройкиОбмена;
	
	Обработчик = Новый ОписаниеОповещения("ОтправитьЗапросРасчетаДепозитаВБанкПослеПолученияМаркера", ЭтотОбъект, 
		ПараметрыПолучения);
		
	ОбменСБанкамиСлужебныйКлиент.ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
		РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации, РеквизитыНастройкиОбмена.ВерсияФормата,
		НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросРасчетаДепозитаВБанкПослеПолученияМаркера(Маркер, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Маркер) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("ИдентификаторСессии", Маркер);
	ПараметрыОтправки.Вставить("ВидОперации", "ПолучениеРасчетаДепозита");
	
	ТекстСообщения = НСтр("ru = 'Ожидание завершения операции в банке'");
	НастроитьСтраницуОжидание(ТекстСообщения);
	ПоказатьСтраницу("Ожидание");
	
	Результат = ОбменСбанкамиДепозитыВызовСервера.ЗапроситьРасчетДепозитаВБанке(Маркер, УникальныйИдентификатор,
		ДополнительныеПараметры);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатЗапросаРасчетаДепозита", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаРасчетаДепозита(РезультатЗадания, ДополнительныеПараметры = Неопределено) Экспорт
	
	ТекстСообщения = "";
	Если Не ТипЗнч(РезультатЗадания) = Тип("Структура") Тогда // задание было отменено
		ТекстСообщения =  НСтр("ru = 'Не удалось отправить запрос в банк.'");
	ИначеЕсли РезультатЗадания.Статус = "Ошибка" Тогда
		ТекстСообщения = РезультатЗадания.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		НастроитьСтраницуОжидание(ТекстСообщения, Истина);
		Возврат;
	КонецЕсли;
	
	ТекущийПродуктБанка = ПродуктыБанков.НайтиПоИдентификатору(ПредложениеБанка);

	Результат = ПолучитьРезультатРасчетаДепозита(РезультатЗадания.АдресРезультата);
	РезультатРасчетаВБанке = Результат.ПараметрыДепозита;
	РезультатРасчетаВБанке.Вставить("КлючПродукта", ТекущийПродуктБанка.КлючПродукта);
	РезультатРасчетаВБанке.Вставить("СчетОткрытия", Объект.СчетОрганизации);
	
	ПараметрыРасчетаДепозита.РезультатРасчетаВБанке = РезультатРасчетаВБанке;
	РезультатРасчетаОшибки = Результат.ОшибкиДепозита;
	УстановитьРезультатРасчетаДепозита();
	
	НастроитьСтраницуПодтверждение(РезультатРасчетаОшибки);
	ПоказатьСтраницу("Подтверждение");
	РазместитьНавигацию("Подтверждение");
	
	Если Не ЗначениеЗаполнено(ПараметрыРасчетаДепозита.РезультатРасчетаВБанке) Тогда
		НавигацияНомерШага = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеДляЗапросаРасчетаДепозита(Организация, Банк, ПараметрыДепозита)
	Возврат ОбменСБанкамиДепозитыСлужебный.СформироватьДанныеЗапросаРасчетаДепозита(Организация, Банк, ПараметрыДепозита);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьРезультатРасчетаДепозита(АдресРезультата)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура УстановитьРезультатРасчетаДепозита()
	
	Если ЗначениеЗаполнено(ПараметрыРасчетаДепозита.РезультатРасчетаВБанке) Тогда
		
		РезультатРасчетаВБанке = ПараметрыРасчетаДепозита.РезультатРасчетаВБанке;
		Объект.СуммаДепозита = РезультатРасчетаВБанке.СуммаДепозита;
		Объект.СрокДепозита = РезультатРасчетаВБанке.Срок;
		Объект.Ставка = РезультатРасчетаВБанке.Ставка;
		Объект.Доход = РезультатРасчетаВБанке.СуммаДоход;
		Объект.ДатаЗакрытия = РезультатРасчетаВБанке.ДатаЗакрытия;
		ИтогоДоход = Объект.СуммаДепозита + Объект.Доход;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Параметры

// Инициализация пользовательских параметров отбора предложений банков.
// 
// Возвращаемое значение:
//  Структура:
//   * Срок - Число - срок депозита в днях.
//   * Сумма - Число - сумма депозита.
//
&НаКлиенте
Функция НовыйПараметрыПользователя()
	
	Результат = Новый Структура();
	Результат.Вставить("Срок", 0);
	Результат.Вставить("Сумма", 0);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция НовыйПараметрыРасчетаДепозита()
	
	Результат = Новый Структура;
	Результат.Вставить("РезультатРасчетаВБанке", Неопределено);
	Результат.Вставить("РезультатПредварительногоРасчета", Неопределено);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров предложений банков.
// 
// Возвращаемое значение:
//  Структура - Новый параметры предложений:
// * ОдинБанк - Булево - Истина, если се предложения от одного банка
// * Организация - ОпределяемыйТип.Организация, Неопределено - Ссылка на организацю
// * УсловияТекущегоПродукта - Массив из Структура - Список условий текущего продукта
// * ПользовательУказалПараметры - Булево - Если Истина, пользователь менял параметры депозита
// * ПользовательВыбралПредложение - Булево - Если Истина, пользователь выбрал предложение из списка
//
&НаКлиенте
Функция НовыйПараметрыПредложений()
	
	Результат = Новый Структура;
	Результат.Вставить("ОдинБанк", Ложь);
	Результат.Вставить("Организация", Неопределено);
	Результат.Вставить("УсловияТекущегоПродукта", Новый Массив);
	Результат.Вставить("ПользовательУказалПараметры", Ложь);
	Результат.Вставить("ПользовательВыбралПредложение", Ложь);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СтраницыПомощника

// Показать страницу помощника, имя которой указано в параметре.
// 
// Параметры:
//  ИмяСтраницы - Неопределено, Строка - Имя страницы, если Неопределено скроет все страницы помощника.
//    Возможные ключи:
//       - "Предложения" - страница выбора предложений;
//       - "Подтверждение" - страница подтверждения расчета депозита;
//       - "Ожидание" - страница ожидания завершения операции;
//       - "ЗаявкаНаДепозит" - страница заявки на депозит;
//       - "Неопределено" - скрыть все страницы.
//
&НаКлиенте
Процедура ПоказатьСтраницу(ИмяСтраницы = Неопределено)
	
	Если СтраницыПомощника = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Для каждого КлючИЗначение Из СтраницыПомощника Цикл
		ЭлементСтраница = КлючИЗначение.Значение;
		ЭлементСтраница.Видимость = Ложь;
	КонецЦикла;
	
	Если Не ИмяСтраницы = Неопределено Тогда
		ЭлементСтраница = СтраницыПомощника[ИмяСтраницы];
		ЭлементСтраница.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#Область Ожидание

&НаКлиенте
Процедура НастроитьСтраницуОжидание(Текст, Ошибка = Ложь)
	
	ТекстОжидания = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(Текст);
	
	Если Не Ошибка Тогда
		Элементы.СтраницаОжидание_ОжиданиеПояснение.Заголовок = ТекстОжидания;
	Иначе
		Элементы.СтраницаОжидание_ОшибкаТекст.Заголовок = ТекстОжидания;
	КонецЕсли;
	
	Элементы.СтраницаОжидание_ПовторитьЗапросПредложений.Видимость = Ложь;
	Если Ошибка И МожноПовторитьЗапросПредложений Тогда
		Элементы.СтраницаОжидание_ПовторитьЗапросПредложений.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ГруппаСтраницаОжидание_Ожидание.Видимость = Не Ошибка;
	Элементы.ГруппаСтраницаОжидание_Ошибка.Видимость = Ошибка;
	
КонецПроцедуры

#КонецОбласти

#Область ВыборПредложения

&НаКлиенте
Процедура НастроитьСтраницуПредложения()
	
	ДоступностьЭлементов = Ложь;
	ОрганизацияЗаполнена = ЗначениеЗаполнено(Объект.Организация);
	
	Если ОрганизацияЗаполнена И ПолученыДанныеПредложений Тогда
		ДоступностьЭлементов = Истина;
	КонецЕсли;
	
	УстановитьДоступностьЭлементовСтраницыПредложение(ДоступностьЭлементов);
	УстановитьДоступностьКомандыСтраницаПредложения_Далее();
	
	Если Не ОрганизацияЗаполнена Тогда
		
		ТестСообщения = ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияНеЗаполненаОрганизация();
		СтраницаПредложенияПоказатьСообщение("Информация", ТестСообщения);
		Возврат;
		
	КонецЕсли;
	
	Если Не ПолученыДанныеПредложений Тогда
		
		ТестСообщения = ОбменСБанкамиДепозитыКлиентСервер.ТекстСообщенияНетПредложенийБанковДляОрганизации();
		Если ДоступенОбменДепозитами И НастройкаОбменаПоддерживаетДепозиты Тогда
			МожноПовторитьЗапросПредложений = Истина;
		КонецЕсли;
		СтраницаПредложенияПоказатьСообщение("Информация", ТестСообщения);
		Возврат;
		
	КонецЕсли;
	
	СтраницаПредложенияСкрытьСообщения();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовСтраницыПредложение(ДоступностьЭлемента)
	
	Элементы.СтраницаПредложения_СуммаДепозита.Доступность = ДоступностьЭлемента;
	Элементы.СтраницаПредложения_Срок.Доступность = ДоступностьЭлемента;
	Элементы.СтраницаПредложения_ДатаЗакрытия.Доступность = ДоступностьЭлемента;
	Элементы.СтраницаПредложения_Ставка.Доступность = ДоступностьЭлемента;
	Элементы.СтраницаПредложения_Доход.Доступность = ДоступностьЭлемента;
	Элементы.СтраницаПредложения_ИтогоДоход.Доступность = ДоступностьЭлемента;
	Элементы.СтраницаПредложения_ДоходИтогРазделитель.Доступность = ДоступностьЭлемента;
	Элементы.СтраницаПредложения_СчетОткрытия.Доступность = ДоступностьЭлемента;
	Элементы.СтраницаПредложения_СчетОткрытия.АвтоОтметкаНезаполненного = ДоступностьЭлемента;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандыСтраницаПредложения_Далее()
	
	ДоступностьЭлемента = Ложь;
	
	ПродуктБанка = ПродуктыБанков.НайтиПоИдентификатору(ПредложениеБанка);
	
	Если ЗначениеЗаполнено(Объект.СчетОрганизации) 
		И Не ПродуктБанка = Неопределено И ЗначениеЗаполнено(ПродуктБанка.ИдентификаторУсловия) Тогда
		
		ДоступностьЭлемента =  Истина;
		
	КонецЕсли;
	
	Элементы.СтраницаПредложения_Далее.Доступность = ДоступностьЭлемента;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметраДепозита()
	
	Элементы.СтраницаПредложения_Далее.Доступность = Ложь;
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьРасчетДоходностиСпискаПродуктов", 0.5, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаПредложенияСкрытьСообщения()
	
	Элементы.ГруппаСтраница_Информация.Видимость = Ложь;
	Элементы.ГруппаСтраница_Предложения.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаПредложенияПоказатьСообщение(ВидСообщения, Текст)
	
	Элементы.ГруппаСтраница_Предложения.Видимость = Ложь;
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(Текст);
	
	Если ВидСообщения = "Ошибка" Тогда
		Элементы.ДекорацияКартинкаОшибка.Видимость = Истина;
		Элементы.ГруппаСообщениеОжидание.Видимость = Ложь;
		Элементы.ГруппаСообщениеИнформация.Видимость = Истина;
		Элементы.ДекорацияИнформацияТекст.Заголовок = ТекстСообщения;
	ИначеЕсли ВидСообщения = "Информация" Тогда
		Элементы.ДекорацияКартинкаОшибка.Видимость = Ложь;
		Элементы.ГруппаСообщениеОжидание.Видимость = Ложь;
		Элементы.ГруппаСообщениеИнформация.Видимость = Истина;
		Элементы.ДекорацияИнформацияТекст.Заголовок = ТекстСообщения;
	ИначеЕсли ВидСообщения = "Ожидание" Тогда
		Элементы.ГруппаСообщениеОжидание.Видимость = Истина;
		Элементы.ГруппаСообщениеИнформация.Видимость = Ложь;
		Элементы.ДекорацияОжиданиеТекст.Заголовок = ТекстСообщения;
	КонецЕсли;
	
	Элементы.ПредложенияИнформация_ПовторитьЗапросПредложений.Видимость = Ложь;
	Если Не ВидСообщения = "Ожидание" И МожноПовторитьЗапросПредложений Тогда
		Элементы.ПредложенияИнформация_ПовторитьЗапросПредложений.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ГруппаСтраница_Информация.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыПредложенийБанка(КоличествоЭлементов)
	
	НачальныйИндекс = 1;
	Если ВсегоЭлементовПредложений > 0 Тогда
		НачальныйИндекс = ВсегоЭлементовПредложений+1;
	КонецЕсли;
	
	ПоследнийИндекс = ВсегоЭлементовПредложений + КоличествоЭлементов;
	
	Для ТекущийИндекс = НачальныйИндекс по ПоследнийИндекс Цикл
		
		НовыйЭлементыПредложенийБанка(ТекущийИндекс);
		
	КонецЦикла;
	
	ВсегоЭлементовПредложений = ПоследнийИндекс;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьЭлементыПредложений(НомерЭлемента)
	
	Для ТекущийИндекс = НомерЭлемента по ВсегоЭлементовПредложений Цикл
		
		ИмяЭлемента = СтрШаблон("ГруппаПредложение_%1", ТекущийИндекс);
		ЭлементГруппаПредложения = Элементы[ИмяЭлемента];
		ЭлементГруппаПредложения.Видимость = Ложь;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НовыйЭлементыПредложенийБанка(Индекс)
	
	ИндексСтрокой = Формат(Индекс, "ЧДЦ=0; ЧГ=");
	
	ГруппаРазмещенияПредложений = Элементы.ГруппаСтраница_Предложения;
	
	ГруппаПредложение = Элементы.Добавить(СтрШаблон("ГруппаПредложение_%1", ИндексСтрокой), Тип("ГруппаФормы"),
		ГруппаРазмещенияПредложений);
	ГруппаПредложение.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПредложение.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаПредложение.Заголовок = СтрШаблон(НСтр("ru = 'Предложение %1'"), ИндексСтрокой);
	ГруппаПредложение.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаПредложение.РастягиватьПоГоризонтали = Ложь;
	ГруппаПредложение.ОтображатьЗаголовок = Ложь;
	ГруппаПредложение.Ширина = 50;
	
	Предложение = Элементы.Добавить(СтрШаблон("ЗаголовокПредложение_%1", ИндексСтрокой), Тип("ПолеФормы"), 
		ГруппаПредложение);
	Предложение.Заголовок = СтрШаблон(НСтр("ru = 'Предложение %1'"), ИндексСтрокой);
	Предложение.ПутьКДанным = "ПредложениеБанка";
	Предложение.Вид = ВидПоляФормы.ПолеПереключателя;
	Предложение.ВидПереключателя =  ВидПереключателя.Переключатель;
	Предложение.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Предложение.Шрифт = ШрифтыСтиля.КрупныйШрифтТекста;
	Предложение.УстановитьДействие("ПриИзменении", "ПредложениеБанкаПриИзменении");
	
	Если Индекс = 1 Тогда
		
		Предложение = Элементы.Добавить(СтрШаблон("ЗаголовокПредложениеНадпись_%1", ИндексСтрокой), Тип("ПолеФормы"), 
			ГруппаПредложение);
		Предложение.Заголовок = СтрШаблон(НСтр("ru = 'Предложение %1'"), ИндексСтрокой);
		Предложение.ПутьКДанным = "ОдинПродуктЗаголовок";
		Предложение.Вид = ВидПоляФормы.ПолеНадписи;
		Предложение.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Предложение.Шрифт = ШрифтыСтиля.КрупныйШрифтТекста;
		Предложение.Видимость = Ложь;
		
	КонецЕсли;
	
	ГруппаДетали = Элементы.Добавить(СтрШаблон("ГруппаДетали_%1", ИндексСтрокой), Тип("ГруппаФормы"), ГруппаПредложение);
	ГруппаДетали.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаДетали.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаДетали.Заголовок = НСтр("ru = 'Детали'");
	ГруппаДетали.ОтображатьЗаголовок = Ложь;
	ГруппаДетали.Отображение = ОтображениеОбычнойГруппы.Нет;
	
	ДекорацияОтступОписания = Элементы.Добавить(СтрШаблон("ДекорацияОтступОписания_%1", ИндексСтрокой), Тип("ДекорацияФормы"),
		ГруппаДетали);
	ДекорацияОтступОписания.Заголовок = "";
	ДекорацияОтступОписания.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияОтступОписания.Ширина = 1;
	
	ГруппаОписание = Элементы.Добавить(СтрШаблон("ГруппаОписание_%1", ИндексСтрокой), Тип("ГруппаФормы"), ГруппаДетали);
	ГруппаОписание.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОписание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаОписание.Заголовок = НСтр("ru = 'Описание'");
	ГруппаОписание.ОтображатьЗаголовок = Ложь;
	ГруппаОписание.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаОписание.РастягиватьПоГоризонтали = Истина;
	
	ГруппаОписание = Элементы.Добавить(СтрШаблон("ГруппаОписаниеДетали_%1", ИндексСтрокой), Тип("ГруппаФормы"), ГруппаДетали);
	ГруппаОписание.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОписание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаОписание.Заголовок = НСтр("ru = 'Описание продукта и условия'");
	ГруппаОписание.ОтображатьЗаголовок = Ложь;
	ГруппаОписание.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаОписание.РастягиватьПоГоризонтали = Истина;
	
	ДекорацияОписаниеУсловия = Элементы.Добавить(СтрШаблон("ОписаниеУсловия_%1", ИндексСтрокой), Тип("ДекорацияФормы"),
		ГруппаОписание);
	ДекорацияОписаниеУсловия.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияОписаниеУсловия.АвтоМаксимальнаяШирина = Ложь;
	ДекорацияОписаниеУсловия.РастягиватьПоГоризонтали = Истина;
	
	ДекорацияОписание = Элементы.Добавить(СтрШаблон("ОписаниеПредложение_%1", ИндексСтрокой), Тип("ДекорацияФормы"),
		ГруппаОписание);
	ДекорацияОписание.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияОписание.АвтоМаксимальнаяШирина = Ложь;
	ДекорацияОписание.РастягиватьПоГоризонтали = Истина;
	
	ДекорацияСрокиИУсловия = Элементы.Добавить(СтрШаблон("СрокиИУсловияПредложение_%1", ИндексСтрокой), Тип("ДекорацияФормы"),
		ГруппаОписание);
	ДекорацияСрокиИУсловия.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияСрокиИУсловия.Заголовок = НСтр("ru = 'Сроки и условия'");
	ДекорацияСрокиИУсловия.Гиперссылка = Истина;
	ДекорацияСрокиИУсловия.УстановитьДействие("Нажатие", "ДекорацияСрокиИУсловияНажатие");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементыПредложенийБанка(УсловияПродуктов, ОдинБанк = Ложь)
	
	НомерПредложения = 1;
	
	Если ОдинБанк Тогда
		
		НаименованиеБанка = ПродуктыБанков[0].НаименованиеБанка;
		Элементы.ГруппаПраваяПанель.Заголовок = СтрШаблон(НСтр("ru = 'Предложения банка %1'"), НаименованиеБанка);
		
	Иначе
		
		Элементы.ГруппаПраваяПанель.Заголовок = НСтр("ru = 'Предложения банков:'");
		
	КонецЕсли;
	
	КоличествоПродуктов = ПродуктыБанков.Количество();
	
	Если КоличествоПродуктов > ВсегоЭлементовПредложений Тогда
		
		КоличествоЭлементов = КоличествоПродуктов - ВсегоЭлементовПредложений;
		СоздатьЭлементыПредложенийБанка(КоличествоЭлементов);
		
	КонецЕсли;
	
	Если КоличествоПродуктов < ВсегоЭлементовПредложений Тогда
		
		НомерЭлемента = КоличествоПродуктов+1;
		СкрытьЭлементыПредложений(НомерЭлемента);
		
	КонецЕсли;
	
	ПараметрыТекущегоУсловия = НовыйПараметрыУсловияПродукта();
	
	ОдноПредложение = Ложь;
	Если ПродуктыБанков.Количество() = 1 Тогда
		ОдноПредложение = Истина;
	КонецЕсли;
	
	Для каждого ТекСтрока Из ПродуктыБанков Цикл
		
		ИмяЭлемента = СтрШаблон("ГруппаПредложение_%1", НомерПредложения);
		ЭлементГруппаПредложения = Элементы[ИмяЭлемента];
		
		ЕстьУсловие = ЗначениеЗаполнено(ТекСтрока.ИдентификаторУсловия);
		
		Если ЕстьУсловие Тогда
			НаименованиеПродукта = СтрШаблон(НСтр("ru = '%1%% %2'"), ТекСтрока.Ставка, ТекСтрока.Наименование);
		Иначе
			НаименованиеПродукта = СтрШаблон(НСтр("ru = '%1%% до %2'"), ТекСтрока.СтавкаМаксимум, ТекСтрока.Наименование);
		КонецЕсли;
		
		Если Не ОдинБанк Тогда
			НаименованиеПродукта = СтрШаблон(НСтр("ru = '%1: %2'"), ТекСтрока.НаименованиеБанка, НаименованиеПродукта);
		КонецЕсли;
		
		ДекорацияОтступОписания = Элементы[СтрШаблон("ДекорацияОтступОписания_%1", НомерПредложения)];
		
		Если ОдноПредложение Тогда
			
			ЗаголовокПредложения = Элементы[СтрШаблон("ЗаголовокПредложениеНадпись_%1", НомерПредложения)];
			ЗаголовокПредложенияПереключатель = Элементы[СтрШаблон("ЗаголовокПредложение_%1", НомерПредложения)];
			ЗаголовокПредложенияПереключатель.Видимость = Ложь;
			ОдинПродуктЗаголовок = НаименованиеПродукта;
			ДекорацияОтступОписания.Видимость = Ложь;
			
		Иначе
			
			ЗаголовокПредложения = Элементы[СтрШаблон("ЗаголовокПредложение_%1", НомерПредложения)];
			ЗаголовокПредложения.СписокВыбора.Очистить();
			ЗаголовокПредложения.СписокВыбора.Добавить(ТекСтрока.ПолучитьИдентификатор(), НаименованиеПродукта);
			ДекорацияОтступОписания.Видимость = Истина;
			
		КонецЕсли;
		
		ЗаголовокПредложения.Видимость = Истина;
		
		ЕстьУсловие = ЗначениеЗаполнено(ТекСтрока.ИдентификаторУсловия);
		
		ТекстОписаниеУсловия = "";
		
		Если ЕстьУсловие Тогда
			
			ТекущиеУсловияПродукта = УсловияПродуктов.Получить(ТекСтрока.КлючПродукта);
			ВыбранноеУсловиеПродукта = ПолучитьДанныеУсловия(ТекущиеУсловияПродукта, ТекСтрока.ИдентификаторУсловия);
			
			ПараметрыТекущегоУсловия.Валюта = ТекСтрока.Валюта;
			ПараметрыТекущегоУсловия.Сумма = ТекСтрока.Сумма;
			ПараметрыТекущегоУсловия.СуммаМинимум = ВыбранноеУсловиеПродукта.СуммаМинимум;
			ПараметрыТекущегоУсловия.СуммаМаксимум = ВыбранноеУсловиеПродукта.СуммаМаксимум;
			ПараметрыТекущегоУсловия.Срок = ТекСтрока.Срок;
			ПараметрыТекущегоУсловия.СрокМинимум = ВыбранноеУсловиеПродукта.СрокМинимум;
			ПараметрыТекущегоУсловия.СрокМаксимум = ВыбранноеУсловиеПродукта.СрокМаксимум;
			ПараметрыТекущегоУсловия.Ставка = ВыбранноеУсловиеПродукта.Ставка;
			
			ТекстОписаниеУсловия = ОбменСБанкамиДепозитыКлиентСервер.СформироватьСтрокуИнформацииПоУсловию(
				ПараметрыТекущегоУсловия, Ложь);
		
		Иначе
			
			ТекстОписаниеУсловия = НСтр("ru = 'Не выполнены условия продукта'");
			
		КонецЕсли;
		
		ОписаниеУсловия = Элементы[СтрШаблон("ОписаниеУсловия_%1", НомерПредложения)];
		ОписаниеУсловия.Заголовок = ТекстОписаниеУсловия;
		
		ОписаниеПродукта = Элементы[СтрШаблон("ОписаниеПредложение_%1", НомерПредложения)];
		ОписаниеПродукта.Заголовок = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ТекСтрока.ПодробноеОписание);
		
		ЭлементГруппаПредложения.Видимость = Истина;
		ЗаголовокПредложения.Доступность = ЕстьУсловие;
		НомерПредложения = НомерПредложения + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьРасчетДоходностиСпискаПродуктов()
	
	ОбновитьРасчетДоходностиСпискаПродуктов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРасчетДоходностиСпискаПродуктов(ВыбранныйПродукт = Неопределено, ВыбранноеУсловие = Неопределено)
	
	ТекущийПродукт = ПродуктыБанков.НайтиПоИдентификатору(ПредложениеБанка);
	
	Если Не ЗначениеЗаполнено(ВыбранныйПродукт)
		И ПараметрыПредложений.ПользовательВыбралПредложение
		И Не ТекущийПродукт = Неопределено Тогда
		ВыбранныйПродукт = ТекущийПродукт.КлючПродукта;
	КонецЕсли;
	
	РезультатПолученияДанных = ЗаполнитьПредложенияБанковНаСервере(ПараметрыПользователя, Истина, ВыбранныйПродукт, ВыбранноеУсловие);
	Если Не РезультатПолученияДанных.ЕстьПредложенияБанков Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПредложений.ОдинБанк = РезультатПолученияДанных.ОдинБанк;
	ПараметрыПредложений.Организация = Объект.Организация;
	ПараметрыПредложений.УсловияТекущегоПродукта = РезультатПолученияДанных.УсловияТекущегоПродукта;
	
	ЗаполнитьПараметрыДепозита();
	УстановитьДоступностьЭлементовСтраницыПредложение(Истина);
	УстановитьДоступностьКомандыСтраницаПредложения_Далее();
	
КонецПроцедуры

#КонецОбласти

#Область Подтверждение

&НаКлиенте
Процедура НастроитьСтраницуПодтверждение(Ошибка = Неопределено)
	
	ЕстьОшибка = Ложь;
	ЕстьПредупреждение = Ложь;
	БлокироватьОтправку = Ложь;
	ПовторитьРасчет = Ложь;
	ВидимостьКомандыНазад = Истина;
	УстарелиДанныеЗаявки = Ложь;
	ПоказатьИнформацию = Ложь;
	
	Элементы.СтраницаПодтверждение_СчетОткрытия.ТолькоПросмотр = Истина;
	Элементы.СтраницаПодтверждение_СтавкаПредупреждение.Видимость = Ложь;
	Элементы.СтраницаПодтверждение_СрокПредупреждение.Видимость = Ложь;
	Элементы.СтраницаПодтверждение_ДатаЗакрытияПредупреждение.Видимость = Ложь;
	Элементы.СтраницаПодтверждение_ДоходПредупреждение.Видимость = Ложь;
	Элементы.СтраницаПодтверждение_ИтогПредупреждение.Видимость = Ложь;
	
	Если ЗначениеЗаполнено(Ошибка) Тогда
		
		ОписаниеОшибки = "";
		ЭлементОписанияОшибки = Неопределено;
		
		Если Ошибка.КодОшибки = "2301" Тогда
			
			ОписаниеОшибки = НСтр("ru = '2301 У пользователя нет прав на открытие депозита.'");
			ЭлементОписанияОшибки = Элементы.СтраницаПодтверждение_ПредупреждениеОписание;
			ЕстьПредупреждение = Истина;
			ПовторитьРасчет = Истина;
			
		ИначеЕсли Ошибка.КодОшибки = "2302" Тогда
			
			ОписаниеОшибки =  НСтр("ru = '2302 На указанном счёте недостаточно средств,'")
				+ НСтр("ru = 'пожалуйста, выберите другой счет.'");
			ЭлементОписанияОшибки = Элементы.СтраницаПодтверждение_ПредупреждениеОписание;
			Элементы.СтраницаПодтверждение_СчетОткрытия.ТолькоПросмотр = Ложь;
			ЕстьПредупреждение = Истина;
			ПовторитьРасчет = Истина;
			БлокироватьОтправку = Истина;
			
		ИначеЕсли Ошибка.КодОшибки = "2303" Тогда
			
			ОписаниеОшибки =  НСтр("ru = '2303 Время заявки не соответствует регламенту.'");
			ЭлементОписанияОшибки = Элементы.СтраницаПодтверждение_ПредупреждениеОписание;
			ЕстьПредупреждение = Истина;
			БлокироватьОтправку = Истина;
			
		Иначе
			
			ОписаниеОшибки = СтрШаблон(НСтр("ru = '%1 Ошибка расчета депозита.'"), Ошибка.КодОшибки);
			ЭлементОписанияОшибки = Элементы.СтраницаПодтверждение_ОшибкаОписание;
			ЕстьОшибка = Истина;
			БлокироватьОтправку = Истина;
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(Ошибка.Описание) Тогда
			ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Сообщение банка:
					|%1'"), Ошибка.Описание);
		КонецЕсли;
		
		ТекстОшибки = СтрШаблон(НСтр("ru = 'При запросе расчета депозита в банке произошла ошибка.
			|%1
			|%2'"), ОписаниеОшибки, Ошибка.ДополнительнаяИнформация);
			
		ЭлементОписанияОшибки.Заголовок = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ТекстОшибки);
		
	КонецЕсли;
	
	Если Не ЕстьОшибка И Не ЕстьПредупреждение И ЗначениеЗаполнено(
		ПараметрыРасчетаДепозита.РезультатРасчетаВБанке) Тогда
	
		РезультатРасчетаВБанке = ПараметрыРасчетаДепозита.РезультатРасчетаВБанке;
		РезультатПредварительногоРасчета = ПараметрыРасчетаДепозита.РезультатПредварительногоРасчета;
		
		Если Не РезультатПредварительногоРасчета.Ставка = РезультатРасчетаВБанке.Ставка Тогда
			Элементы.СтраницаПодтверждение_СтавкаПредупреждение.Видимость = Истина;
			ЕстьПредупреждение = Истина;
		КонецЕсли;
		
		Если Не РезультатПредварительногоРасчета.Срок = РезультатРасчетаВБанке.Срок Тогда
			Элементы.СтраницаПодтверждение_СрокПредупреждение.Видимость = Истина;
			ЕстьПредупреждение = Истина;
		КонецЕсли;
		
		Если Не РезультатПредварительногоРасчета.ДатаЗакрытия = РезультатРасчетаВБанке.ДатаЗакрытия Тогда
			Элементы.СтраницаПодтверждение_ДатаЗакрытияПредупреждение.Видимость = Истина;
			ЕстьПредупреждение = Истина;
		КонецЕсли;
		
		Если Не РезультатПредварительногоРасчета.СуммаДоход = РезультатРасчетаВБанке.СуммаДоход Тогда
			Элементы.СтраницаПодтверждение_ДоходПредупреждение.Видимость = Истина;
			Элементы.СтраницаПодтверждение_ИтогПредупреждение.Видимость = Истина;
			ЕстьПредупреждение = Истина;
		КонецЕсли;
		
		Если ЕстьПредупреждение И ЗначениеЗаполнено(СостояниеЗаявкиНаДепозит) Тогда
			УстарелиДанныеЗаявки = Истина;
		ИначеЕсли ЕстьПредупреждение Тогда
			ТекстПредупреждения = 
				НСтр("ru = 'Пожалуйста, обратите внимание: банк скорректировал параметры вашей заявки'");
			Элементы.СтраницаПодтверждение_ПредупреждениеОписание.Заголовок = ТекстПредупреждения;
		КонецЕсли;
	КонецЕсли;
	
	Если Не УстарелиДанныеЗаявки И ЗначениеЗаполнено(СостояниеЗаявкиНаДепозит) Тогда
		
		УстарелиДанныеЗаявки = Истина;
		
		Если ПродуктыБанков.Количество() > 0 Тогда
			
			СтруктураПоиска = Новый Структура("Банк, ИдентификаторПродукта", 
				Объект.Банк, Объект.ИдентификаторПродукта);
			НайденныеСтроки = ПродуктыБанков.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() > 0 Тогда
				УстарелиДанныеЗаявки = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если УстарелиДанныеЗаявки Тогда
		Если ДоступенОбменДепозитами И НастройкаОбменаПоддерживаетДепозиты Тогда
			ТекстОшибки = 
				НСтр("ru = 'Данные расчета не актуальны, невозможно отправить заявку. Создайте и отправьте новую заявку'");
		Иначе
			ТекстОшибки = "";
		КонецЕсли;
		
		ЭлементОписанияОшибки = Элементы.СтраницаПодтверждение_ОшибкаОписание;
		ЭлементОписанияОшибки.Заголовок = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ТекстОшибки);
		
		ЕстьПредупреждение = Ложь;
		
		Элементы.СтраницаПодтверждение_СтавкаПредупреждение.Видимость = Ложь;
		Элементы.СтраницаПодтверждение_СрокПредупреждение.Видимость = Ложь;
		Элементы.СтраницаПодтверждение_ДатаЗакрытияПредупреждение.Видимость = Ложь;
		Элементы.СтраницаПодтверждение_ИтогПредупреждение.Видимость = Ложь;
		Элементы.СтраницаПодтверждение_ДоходПредупреждение.Видимость = Ложь;
		
		ЕстьОшибка = Истина;
		БлокироватьОтправку = Истина;
		ВидимостьКомандыНазад = Ложь;
		
	КонецЕсли;
	
	Если ДоступенОбменДепозитами Тогда
	
		ТекстКнопкиОтправки = ТекстКнопкиДалееСтраницыПодтверждение("Отправить");
		
		Если ПовторитьРасчет Тогда
			
			ТекстКнопкиОтправки  = ТекстКнопкиДалееСтраницыПодтверждение("Рассчитать");
			
		КонецЕсли;
		
		Если БлокироватьОтправку Или ПовторитьРасчет Тогда
			
			ПараметрыРасчетаДепозита.РезультатРасчетаВБанке = Неопределено;
			
		Иначе
			
			ПоказатьИнформацию = Истина;
			ТекстСообщения = НСтр("ru = 'Банк подтвердил расчет депозита. Отправьте заявку для продолжения.'");
			Если СостояниеЗаявкиНаДепозит = "НаПодписи" Тогда
				ТекстСообщения = НСтр("ru = 'Банк подтвердил расчет депозита. Подпишите заявку для продолжения.'");
				ТекстКнопкиОтправки = ТекстКнопкиДалееСтраницыПодтверждение("Подписать");
			КонецЕсли;
			Элементы.СтраницаПодтверждение_ОжиданиеКартинка.Видимость = Ложь;
			Элементы.СтраницаПодтверждение_ОжиданиеОписание.Заголовок = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ТекстСообщения);
			
		КонецЕсли;
		
		Элементы.СтраницаПодтверждение_Далее.Заголовок = ТекстКнопкиОтправки;
	
	КонецЕсли;
	
	Элементы.СтраницаПодтверждение_Далее.Видимость = ДоступенОбменДепозитами;
	Элементы.СтраницаПодтверждение_Далее.Доступность = Не БлокироватьОтправку;
	
	Если Не ЭтоНоваяЗаявка Тогда
		ВидимостьКомандыНазад = Ложь;
	КонецЕсли;
	Элементы.СтраницаПодтверждение_Назад.Видимость = ВидимостьКомандыНазад;
	Элементы.ГруппаСтраницаПодтверждение_Информация.Видимость = Ложь;
	
	Если ЕстьОшибка Или ЕстьПредупреждение Тогда
		ПоказатьИнформацию = Ложь;
	КонецЕсли;
	
	Если ЕстьОшибка Или ЕстьПредупреждение Или ПоказатьИнформацию Тогда
		Элементы.ГруппаСтраницаПодтверждение_Информация.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ГруппаСтраницаПодтверждение_Ошибка.Видимость = Ложь;
	Если ЕстьОшибка И ЗначениеЗаполнено(Элементы.СтраницаПодтверждение_ОшибкаОписание.Заголовок) Тогда
		Элементы.ГруппаСтраницаПодтверждение_Ошибка.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ГруппаСтраницаПодтверждение_Предупреждение.Видимость = ЕстьПредупреждение;
	Элементы.ГруппаСтраницаПодтверждение_Ожидание.Видимость = ПоказатьИнформацию;
	
КонецПроцедуры

&НаКлиенте
Функция ТекстКнопкиДалееСтраницыПодтверждение(Действие)
	
	ТекстКнопки = "";
	
	Если Действие = "Отправить" Тогда
		ТекстКнопки = НСтр("ru = 'Отправить заявку'");
	ИначеЕсли Действие = "Рассчитать" Тогда
		ТекстКнопки = НСтр("ru = 'Запросить расчет'");
	ИначеЕсли Действие = "Подписать" Тогда
		ТекстКнопки = НСтр("ru = 'Подписать'");
	КонецЕсли;
	
	Возврат ТекстКнопки;
	
КонецФункции

&НаКлиенте
Процедура СтраницаПодтверждениеПоказатьСообщение(ВидСообщения, Текст)
	
	Элементы.ГруппаСтраницаПодтверждение_Ошибка.Видимость = Ложь;
	Элементы.ГруппаСтраницаПодтверждение_Предупреждение.Видимость = Ложь;
	Элементы.ГруппаСтраницаПодтверждение_Ожидание.Видимость = Ложь;
	Элементы.НавигацияДопИнформация.Видимость = Ложь;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_ОписаниеПродукта.Видимость = Ложь;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_ГрафикВыплатыПроцентов.Видимость = Ложь;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_Вложения.Видимость = Ложь;
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(Текст);
	
	Если ВидСообщения = "Ошибка" Тогда
		
		Элементы.СтраницаПодтверждение_ОшибкаКартинка.Видимость = Истина;
		Элементы.СтраницаПодтверждение_ОшибкаОписание.Видимость = Истина;
		Элементы.ГруппаСтраницаПодтверждение_Ошибка.Видимость = Истина;
		Элементы.СтраницаПодтверждение_ОшибкаОписание.Заголовок = ТекстСообщения;
		
	ИначеЕсли ВидСообщения = "Предупреждение" Тогда
		
		Элементы.СтраницаПодтверждение_ПредупреждениеКартинка.Видимость = Истина;
		Элементы.СтраницаПодтверждение_ПредупреждениеОписание.Видимость = Истина;
		Элементы.ГруппаСтраницаПодтверждение_Предупреждение.Видимость = Истина;
		Элементы.СтраницаПодтверждение_ПредупреждениеОписание.Заголовок = ТекстСообщения;
		
	ИначеЕсли ВидСообщения = "Ожидание" Тогда
		
		Элементы.СтраницаПодтверждение_ОжиданиеКартинка.Видимость = Истина;
		Элементы.СтраницаПодтверждение_ОжиданиеОписание.Видимость = Истина;
		Элементы.ГруппаСтраницаПодтверждение_Ожидание.Видимость = Истина;
		Элементы.СтраницаПодтверждение_ОжиданиеОписание.Заголовок = ТекстСообщения;
		
	ИначеЕсли ВидСообщения = "Информация" Тогда
		
		Элементы.СтраницаПодтверждение_ОжиданиеКартинка.Видимость = Ложь;
		Элементы.СтраницаПодтверждение_ОжиданиеОписание.Видимость = Истина;
		Элементы.ГруппаСтраницаПодтверждение_Ожидание.Видимость = Истина;
		Элементы.СтраницаПодтверждение_ОжиданиеОписание.Заголовок = ТекстСообщения;
		
	КонецЕсли;
	
	Элементы.ГруппаСтраницаПодтверждение_Информация.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаПодтверждениеСкрытьСообщения()
	
	Элементы.ГруппаСтраницаПодтверждение_Ошибка.Видимость = Ложь;
	Элементы.ГруппаСтраницаПодтверждение_Предупреждение.Видимость = Ложь;
	Элементы.ГруппаСтраницаПодтверждение_Ожидание.Видимость = Ложь;
	Элементы.ГруппаСтраницаПодтверждение_Информация.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаявкаНаДепозит

&НаКлиенте
Процедура НастроитьСтраницуЗаявкаНаДепозит()
	
	НавигацияДопИнформация = "Описание";
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_ОписаниеПродукта.Видимость = Ложь;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_ГрафикВыплатыПроцентов.Видимость = Ложь;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_Вложения.Видимость = Ложь;
	Элементы.СтраницаЗаявкаНаДепозит_ДатаВозвратаСредств.Видимость = Ложь;
	Элементы.СтраницаЗаявкаНаДепозит_ДатаЗакрытия.Видимость = Ложь;
	Элементы.СтраницаЗаявкаНаДепозит_СчетаДепозита.Видимость = Ложь;
	
	Если СостояниеЗаявкиНаДепозит = "ВОбработке" Тогда
		
		Текст = НСтр("ru = 'Заявка доставлена и обрабатывается банком.'");
		СтраницаЗаявкаНаДепозитПоказатьСообщение("Ожидание", Текст, ОжидаетсяСтатусЗаявки);
		Элементы.ЗапроситьСостояниеЗаявкиНаДепозит.Видимость = Истина;
		
	ИначеЕсли СостояниеЗаявкиНаДепозит = "Отклонено" Тогда
		
		ТекстОшибки = ПолучитьОшибкуПоЭД(Объект.Ссылка);
		Текст = НСтр("ru = 'Заявка отклонена банком.'"); 
		
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			Текст = ТекстОшибки;
		КонецЕсли;
		
		СтраницаЗаявкаНаДепозитПоказатьСообщение("Ошибка", Текст);
		Элементы.ЗапроситьСостояниеЗаявкиНаДепозит.Видимость = Ложь;
		
	ИначеЕсли СостояниеЗаявкиНаДепозит = "Исполнено" Тогда
		
		Элементы.ГруппаНавигация.Видимость = Ложь;
		СтраницаПодтверждениеСкрытьСообщения();
		Элементы.СтраницаЗаявкаНаДепозит_ДатаВозвратаСредств.Видимость = Истина;
		Элементы.СтраницаЗаявкаНаДепозит_СчетаДепозита.Видимость = Истина;
		
		Если Не Объект.ДатаВозвратаСредств = Объект.ДатаЗакрытия Тогда
			Элементы.СтраницаЗаявкаНаДепозит_ДатаЗакрытия.Видимость = Истина;
		КонецЕсли;
		
		ЕстьОписание = ЗначениеЗаполнено(Объект.ОписаниеПродукта);
		ЕстьКомментарийБанка = ЗначениеЗаполнено(Объект.Комментарий);
		
		Если ЕстьОписание Тогда
			Элементы.СтраницаЗаявкаНаДепозит_ПодробноеОписаниеПродукта.Заголовок = 
				СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(Объект.ОписаниеПродукта);
		КонецЕсли;
		Элементы.СтраницаЗаявкаНаДепозит_ПодробноеОписаниеПродукта.Видимость = ЕстьОписание;
		
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_КомментарийБанка.ОтображатьЗаголовок = Ложь;
		Если ЕстьОписание И ЕстьКомментарийБанка Тогда
			Элементы.ГруппаСтраницаЗаявкаНаДепозит_КомментарийБанка.ОтображатьЗаголовок = Истина;
		КонецЕсли;
		
		Если ЕстьКомментарийБанка Тогда
			Элементы.СтраницаЗаявкаНаДепозит_КомментарийБанка.Заголовок = 
				СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(Объект.Комментарий);
		КонецЕсли;
		Элементы.СтраницаЗаявкаНаДепозит_КомментарийБанка.Видимость = ЕстьКомментарийБанка;
		
		НавигацияДопИнформация = "Описание";
		
		ПоказатьИнформациюСтраницыЗаявка(НавигацияДопИнформация);
		
	КонецЕсли;
	
	Если Не ДоступенОбменДепозитами Или Не НастройкаОбменаПоддерживаетДепозиты Тогда
		Элементы.ЗапроситьСостояниеЗаявкиНаДепозит.Видимость = Ложь;
	КонецЕсли;
	
	ТекстЗаявкаНаДепозитЗаголвок = СтрШаблон(НСтр("ru = 'Депозит: %1'"), Объект.НаименованиеПродукта);
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_ДеталиПродукта.Заголовок = ТекстЗаявкаНаДепозитЗаголвок;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаЗаявкаНаДепозитПоказатьСообщение(ВидСообщения, Текст, ПоказыватьКартинку = Истина)
	
	Элементы.НавигацияДопИнформация.Видимость = Ложь;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_ОписаниеПродукта.ОтображатьЗаголовок = Ложь;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_СтраницаОшибка.Видимость = Ложь;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_СтраницаОжидание.Видимость = Ложь;
	
	ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(Текст);
	
	Если ВидСообщения = "Ошибка" Тогда
		
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_СтраницаОшибка.Видимость = Истина;
		Элементы.СтраницаЗаявкаНаДепозит_ОшибкаКартинка.Видимость = ПоказыватьКартинку;
		Элементы.СтраницаЗаявкаНаДепозит_ОшибкаТекст.Заголовок = ТекстЗаголовка;
		
	ИначеЕсли ВидСообщения = "Ожидание" Тогда
		
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_СтраницаОжидание.Видимость = Истина;
		Элементы.СтраницаЗаявкаНаДепозит_ОжиданиеКартинка.Видимость = ПоказыватьКартинку;
		Элементы.СтраницаЗаявкаНаДепозит_ОжиданиеТекст.Заголовок = ТекстЗаголовка;
		
	КонецЕсли;
	
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_Информация.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИнформациюСтраницыЗаявка(ВыбранныйПереключатель)
	
	ЕстьГрафик = Объект.ГрафикВыплатыПроцентов.Количество() > 1;
	ЕстьВложения = Объект.ЕстьВложение;
	
	СписокНавигации = Элементы.НавигацияДопИнформация.СписокВыбора;
	СписокНавигации.Очистить();
	СписокНавигации.Добавить("Описание", "Описание");
	
	Если ЕстьГрафик Тогда
		СписокНавигации.Добавить("График", "График начисления");
	КонецЕсли;
	
	Если ЕстьВложения Тогда
		СписокНавигации.Добавить("Вложения", "Вложения");
	КонецЕсли;
	
	ОтображатьНавигацию = Ложь;
	Если ЕстьГрафик Или ЕстьВложения Тогда
		ОтображатьНавигацию = Истина;
	КонецЕсли;
	
	Элементы.НавигацияДопИнформация.Видимость = ОтображатьНавигацию;
	Элементы.ГруппаСтраницаЗаявкаНаДепозит_ОписаниеПродукта.ОтображатьЗаголовок = Не ОтображатьНавигацию;
	
	Если ВыбранныйПереключатель = "Описание" Тогда
		
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_Информация.Видимость = Ложь;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_ОписаниеПродукта.Видимость = Истина;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_ГрафикВыплатыПроцентов.Видимость = Ложь;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_Вложения.Видимость = Ложь;
		
	ИначеЕсли ВыбранныйПереключатель = "График" И ЕстьГрафик Тогда
		
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_Информация.Видимость = Ложь;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_ОписаниеПродукта.Видимость = Ложь;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_ГрафикВыплатыПроцентов.Видимость = Истина;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_Вложения.Видимость = Ложь;
		
	ИначеЕсли ВыбранныйПереключатель = "Вложения" И ЕстьВложения Тогда 
		
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_Информация.Видимость = Ложь;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_ОписаниеПродукта.Видимость = Ложь;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_ГрафикВыплатыПроцентов.Видимость = Ложь;
		Элементы.ГруппаСтраницаЗаявкаНаДепозит_Вложения.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПрисоединенныеФайлы()
	
	ПрисоединенныеФайлыТаблица.Очистить();
	
	МассивФайлов = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Объект.Ссылка, МассивФайлов);
	
	ДанныеФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивФайлов, 
		"Ссылка, Наименование, Расширение, ПометкаУдаления");
	
	Для каждого ЭлементКоллекции Из ДанныеФайлов Цикл
		
		ДанныеФайла = ЭлементКоллекции.Значение;
		
		Если ДанныеФайла.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ПрисоединенныеФайлыТаблица.Добавить();
		НоваяСтрока.Ссылка = ДанныеФайла.Ссылка;
		НоваяСтрока.Наименование = ОбменСБанкамиСлужебныйКлиентСервер.ПредставлениеПрисоединенногоФайла(
			ДанныеФайла.Наименование, ДанныеФайла.Расширение);
		НоваяСтрока.ИндексКартинки = РаботаСФайламиБЭДКлиентСервер.ИндексКартинкиПоРасширениюФайла(ДанныеФайла.Расширение);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииСтраницыПомощника

// Список страниц помощника.
// 
// Возвращаемое значение:
//  Структура - Список страниц помощника:
//  * Предложения - ГруппаФормы - страница выбора предложений;
//  * Подтверждение - ГруппаФормы - страница подтверждения расчета депозита;
//  * Ожидание - ГруппаФормы - страница ожидания завершения операции;
//  * ЗаявкаНаДепозит - ГруппаФормы - страница заявки на депозит.
//
&НаКлиенте
Функция СписокСтраницПомощника()
	
	Результат = Новый Структура();
	Результат.Вставить("Предложения", Элементы.СтраницаВыборПредложения);
	Результат.Вставить("Подтверждение", Элементы.СтраницаПодтверждение);
	Результат.Вставить("Ожидание", Элементы.СтраницаОжидание);
	Результат.Вставить("ЗаявкаНаДепозит", Элементы.СтраницаЗаявкаНаДепозит);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область НавигацияПомощников

&НаКлиенте
Процедура УстановитьВидимостьНавигации(ВидимостьНавигации = Истина)
	Элементы.ГруппаНавигация.Видимость = ВидимостьНавигации;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеШага(ИмяШага)
	
	Если ИмяШага = "Шаг1" Тогда
		
		РазместитьНавигацию("Начало");
		НастроитьСтраницуПредложения();
		ПоказатьСтраницу("Предложения");
		
	ИначеЕсли ИмяШага = "Шаг2" Тогда
		
		Если НужноЗапрашиватьРасчетДепозита() Тогда
			ЗаполнитьРезультатПредварительногоРасчета();
			ЗапроситьРасчетДепозита();
			Если Не ЗначениеЗаполнено(СостояниеЗаявкиНаДепозит) Тогда
				ЗаписатьЗаявку = Истина;
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		УстановитьРезультатРасчетаДепозита();
		ПоказатьСтраницу("Подтверждение");
		РазместитьНавигацию("Подтверждение");
		
	ИначеЕсли ИмяШага = "Шаг3" Тогда
		
		ЗаявкаГотоваКОтправке = Ложь;
		
		Если ЗаписатьЗаявку Тогда
			ЗаявкаГотоваКОтправке = ЗаполнитьПараметрыПродуктаИЗаписатьЗаявку();
			ЗаписатьЗаявку = Ложь;
		ИначеЕсли ЗначениеЗаполнено(СостояниеЗаявкиНаДепозит) Тогда
			ЗаявкаГотоваКОтправке = ПроверитьИЗаписатьЗаявкуНаДепозит();
			ЗаписатьЗаявку = Ложь;
		КонецЕсли;
		
		Если ЗаявкаГотоваКОтправке Тогда
			НачатьОтправкуЗаявкиНаДепозитВБанк();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область СтруктураШагов

&НаСервере
Функция НавигацияПомощника()
	
	СтруктураНавигации = Новый Структура;
	СтруктураНавигации.Вставить(ИмяШага(1), СтруктураШагаНачало(1));
	СтруктураНавигации.Вставить(ИмяШага(2), СтруктураШагаПодтверждение(2));
	СтруктураНавигации.Вставить(ИмяШага(3), СтруктураШагаРезультат(3));
	
	Возврат СтруктураНавигации;
	
КонецФункции

&НаСервере
Функция СтруктураШагаНачало(НомерШага)
	
	
	СтруктураШага                = НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Начало'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "Начало";
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаПодтверждение(НомерШага)
	
	
	СтруктураШага                = НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Подтверждение'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "Подтверждение";
	Возврат СтруктураШага;
	
КонецФункции

&НаСервере
Функция СтруктураШагаРезультат(НомерШага)
	
	СтруктураШага                = НовыйСтруктураШага();
	СтруктураШага.ИмяПомощника   = ИмяПомощника();
	СтруктураШага.НомерШага      = НомерШага;
	СтруктураШага.ТекстНавигации = НСтр("ru='Открытие'");
	СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = "Открытие";
	Возврат СтруктураШага;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииНавигацииПомощников

&НаСервере
Функция ИмяПомощника()

	Возврат "ЗаявкаНаОткрытиеДепозита";

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяШага(НомерШага)
	
	Возврат "Шаг" + НомерШага;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МаксимальноеЧислоШагов()
	
	Возврат 3;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НовыйСтруктураШага()
	
	СтруктураШага = Новый Структура;
	СтруктураШага.Вставить("ИмяПомощника", "");
	СтруктураШага.Вставить("СтруктураПараметровФормы", НовыйСтруктураПараметровФормы());
	СтруктураШага.Вставить("НомерШага", 0);
	СтруктураШага.Вставить("ТекстНавигации", "");
	
	Возврат СтруктураШага;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НовыйСтруктураПараметровФормы()
	
	СтруктураПараметровФормы = Новый Структура;
	СтруктураПараметровФормы.Вставить("НавигацияПараметрФормы", "");
	
	Возврат СтруктураПараметровФормы;
	
КонецФункции

&НаСервере
Процедура РазместитьНавигацию(ИмяТекущейСтраницы)
	
	НавигацияПараметрФормы = ИмяТекущейСтраницы;
	СтруктураНавигацииПомощника = НавигацияПомощника();
	НомерШага = 0;
	
	Для Каждого Шаг Из СтруктураНавигацииПомощника Цикл
		СтруктураШага = Шаг.Значение;
		Если СтруктураШага.СтруктураПараметровФормы.НавигацияПараметрФормы = НавигацияПараметрФормы Тогда
			НомерШага = СтруктураШага.НомерШага;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НомерШага = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НавигацияНомерШага = НомерШага;
	Если НавигацияНомерШага = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РазместитьКартинки(СтруктураНавигацииПомощника, Ложь);
	РазместитьТексты(СтруктураНавигацииПомощника);
	
	ГруппаНавигация = Элементы.Найти("ГруппаНавигация");
	Если ГруппаНавигация <> Неопределено Тогда
		ГруппаНавигация.Видимость = Истина;
	КонецЕсли;
	
	КомандаНазад = Элементы.Найти("КомандаНазад");
	Если НавигацияНомерШага = 1 И КомандаНазад <> Неопределено Тогда
		КомандаНазад.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РазместитьКартинки(СтруктураНавигацииПомощника, Навигация = Истина)
	
	КартинкаОтступ = ?(НавигацияНомерШага = 1,
		БиблиотекаКартинок.НавигацияОтступТекущийШагОбменСБанками,
		БиблиотекаКартинок.НавигацияОтступВыполненныйШагОбменСБанками);
	
	СтруктураКартинокНавигации = СтруктураКартинокНавигации();
	
	МассивФорматированныхСтрок = Новый Массив;
	МассивФорматированныхСтрок.Добавить(КартинкаОтступ);
	
	Для НомерШага = 1 По СтруктураНавигацииПомощника.Количество() Цикл
		
		ВыводитьЛинию = (НомерШага <> 1);
		
		// Шаги
		Если НомерШага < НавигацияНомерШага Тогда
			ИдентификаторШага = "Выполненный";
		ИначеЕсли НомерШага = НавигацияНомерШага Тогда
			ИдентификаторШага = "Текущий";
		Иначе
			ИдентификаторШага = "НеВыполненный";
		КонецЕсли;
		ИмяКартинкиШага = СтрШаблон("Шаг%1", ИдентификаторШага);
		
		// Линия
		ЦветЛинии   = ?(НомерШага <= НавигацияНомерШага, "Выполненный", "НеВыполненный");
		РазмерЛинии = ?(НомерШага = НавигацияНомерШага Или НомерШага = НавигацияНомерШага + 1, "Текущий", "");
		ИмяЛинии    = СтрШаблон("ЛинияШаг%1%2", ЦветЛинии, РазмерЛинии);
		
		// Ссылка
		ИмяСсылка = Неопределено;
		Если Навигация Тогда
			ИмяСсылки = ?(НомерШага < НавигацияНомерШага, ИмяШага(НомерШага), "");
		КонецЕсли;
		
		Если ВыводитьЛинию Тогда
			МассивФорматированныхСтрок.Добавить(СтруктураКартинокНавигации[ИмяЛинии]);
		КонецЕсли;
		МассивФорматированныхСтрок.Добавить(Новый ФорматированнаяСтрока(СтруктураКартинокНавигации[ИмяКартинкиШага],,,,
			ИмяСсылки));
		
	КонецЦикла;
	
	Элементы.НавигацияКартинка.Заголовок = Новый ФорматированнаяСтрока(МассивФорматированныхСтрок);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктураКартинокНавигации()

	СтруктураКартинокНавигации = Новый Структура;
	СтруктураКартинокНавигации.Вставить("ЛинияШагВыполненный", 
		БиблиотекаКартинок.НавигацияЛинияШагВыполненныйОбменСБанками);
	СтруктураКартинокНавигации.Вставить("ЛинияШагНеВыполненный", 
		БиблиотекаКартинок.НавигацияЛинияШагНеВыполненныйОбменСБанками);
	СтруктураКартинокНавигации.Вставить("ЛинияШагВыполненныйТекущий", 
		БиблиотекаКартинок.НавигацияЛинияШагВыполненныйТекущийОбменСБанками);
	СтруктураКартинокНавигации.Вставить("ЛинияШагНеВыполненныйТекущий", 
		БиблиотекаКартинок.НавигацияЛинияШагНеВыполненныйТекущийОбменСБанками);
	СтруктураКартинокНавигации.Вставить("ШагВыполненный", БиблиотекаКартинок.НавигацияШагВыполненныйОбменСБанками);
	СтруктураКартинокНавигации.Вставить("ШагТекущий", БиблиотекаКартинок.НавигацияШагТекущийОбменСБанками);
	СтруктураКартинокНавигации.Вставить("ШагНеВыполненный", БиблиотекаКартинок.НавигацияШагНеВыполненныйОбменСБанками);
	Возврат СтруктураКартинокНавигации;

КонецФункции

&НаСервере
Процедура РазместитьТексты(СтруктураНавигацииПомощника)
	
	КоличествоШагов  = СтруктураНавигацииПомощника.Количество();
	МинимальнаяШиринаШаг = 14;
	
	Для НомерШага = 1 По КоличествоШагов Цикл
		
		СтруктураШага = СтруктураНавигацииПомощника[ИмяШага(НомерШага)];
		
		ИмяСсылки   = ?(НомерШага < НавигацияНомерШага, ИмяШага(НомерШага), "");
		ШрифтТекста = ?(НомерШага = НавигацияНомерШага, ШрифтыСтиля.ШрифтТекущийШагРегистрацииОрганизации, Неопределено);
		ЦветТекста  = ?(НомерШага > НавигацияНомерШага,
			ЦветаСтиля.ЦветНедоступногоТекста, ЦветаСтиля.ЦветШрифтаВыполненныйШагРегистрацииОрганизации);
		
		ЭлементФормы = Элементы[ИмяШага(НомерШага)];
		ЭлементФормы.Заголовок = Новый ФорматированнаяСтрока(
			СтруктураШага.ТекстНавигации, ШрифтТекста, ЦветТекста, , );
		ЭлементФормы.Ширина = Макс(МинимальнаяШиринаШаг, СтрДлина(СтруктураШага.ТекстНавигации));
		
	КонецЦикла;
	
	//Очистка неиспользуемых шагов навигации
	МаксимальноеЧислоШагов = МаксимальноеЧислоШагов();
	Если КоличествоШагов < МаксимальноеЧислоШагов Тогда
		Для НомерШага = КоличествоШагов + 1 По МаксимальноеЧислоШагов Цикл
			
			ЭлементФормы = Элементы.Найти(ИмяШага(НомерШага));
			Если ЭлементФормы <> Неопределено Тогда
				ЭлементФормы.Заголовок = "";
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти
