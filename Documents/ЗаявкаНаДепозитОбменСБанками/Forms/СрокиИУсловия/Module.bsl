#Область ОписаниеПеременных

&НаКлиенте
Перем УсловияТекущегоПродукта;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресХранилищаПредложенийБанков = Неопределено;
	ПродуктБанка = Неопределено;
	
	Если Не Параметры.Свойство("АдресХранилищаПредложенийБанков", АдресХранилищаПредложенийБанков) 
		Или Не Параметры.Свойство("ПараметрыПродукта", ПродуктБанка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	КлючПродукта = ПродуктБанка.КлючПродукта;
	РасчетПриОформлении = ПродуктБанка.РасчетПриОформлении;
	
	Если РасчетПриОформлении Тогда
		Элементы.ДекорацияРасчетПриОформлении.Видимость = Истина;
		Элементы.Доход.Видимость = Ложь;
	КонецЕсли;
	
	ОдинБанк = ПродуктБанка.ОдинБанк;
	УсловияТекущегоПродукта = ОбменСбанкамиДепозитыВызовСервера.ПолучитьУсловияПродукта(
		АдресХранилищаПредложенийБанков, КлючПродукта);
		
	Если Параметры.Свойство("ПараметрыПользователя") 
		И ТипЗнч(Параметры.ПараметрыПользователя) = Тип("Структура") Тогда
		
		ПараметрыОтбора = Параметры.ПараметрыПользователя;
		ПараметрыОтбора.Свойство("Сумма", ОтборСумма);
		ПараметрыОтбора.Свойство("Срок", ОтборСрок);
		ПараметрыПользователяСумма = ОтборСумма;
		ПараметрыПользователяСрок = ОтборСрок;
		
	КонецЕсли;
	
	ИспользоватьОтборСрок = ЗначениеЗаполнено(ОтборСрок);
	ИспользоватьОтборСумма = ЗначениеЗаполнено(ОтборСумма);
	
	ИспользуетсяОтбор = Ложь;
	Если ИспользоватьОтборСрок Или ИспользоватьОтборСумма Тогда
		ИспользуетсяОтбор = Истина;
	КонецЕсли;
	
	ВыбраннаяСтрокаУсловия = Новый Структура;
	ВыбраннаяСтрокаУсловия.Вставить("ИндексСтроки", 0);
	ВыбраннаяСтрокаУсловия.Вставить("Ставка", 0);
	
	Для каждого ТекущаяСтрока из УсловияТекущегоПродукта Цикл
		
		НоваяСтрокаУсловия = УсловияПродукта.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаУсловия, ТекущаяСтрока);
		ВыполняютсяУсловия = Истина;
		
		Если ИспользуетсяОтбор Тогда 
			
			ВыполняютсяУсловия = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ВыполняютсяУсловияПродукта(
				НоваяСтрокаУсловия, ОтборСумма, ОтборСрок);
				
			Если ВыполняютсяУсловия
				И ВыбраннаяСтрокаУсловия.Ставка < НоваяСтрокаУсловия.Ставка Тогда
				
				ВыбраннаяСтрокаУсловия.ИндексСтроки = УсловияПродукта.Индекс(НоваяСтрокаУсловия);
				ВыбраннаяСтрокаУсловия.Ставка = НоваяСтрокаУсловия.Ставка;
				
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяСтрокаУсловия.ВыполняютсяУсловия = ВыполняютсяУсловия;
		
	КонецЦикла;
	
	Если УсловияПродукта.Количество() > 0 Тогда
		Элементы.УсловияПродукта.ТекущаяСтрока = ВыбраннаяСтрокаУсловия.ИндексСтроки;
	КонецЕсли;
	
	Если ОдинБанк Или ПустаяСтрока(ПродуктБанка.НаименованиеБанка) Тогда
		Заголовок = ПродуктБанка.Наименование;
	Иначе
		Заголовок = СтрШаблон("%1: %2", ПродуктБанка.НаименованиеБанка, ПродуктБанка.Наименование);
	КонецЕсли;

	Элементы.ДекорацияОписаниеТекст.Заголовок = 
		СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ПродуктБанка.ПодробноеОписание);
		
	УстановитьУсловноеОформление();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСуммаПриИзменении(Элемент)
	
	ИспользоватьОтборСумма = ЗначениеЗаполнено(ОтборСумма);
	ПараметрыПользователяСумма = ОтборСумма;
	
	Если ИспользоватьОтборСумма Тогда
		ДанныеСтроки = Элементы.УсловияПродукта.ТекущиеДанные;
		Доход = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.РассчитатьДоход(ДатаРасчета,
			ОтборСумма, ОтборСрок, ДанныеСтроки.Ставка);
	КонецЕсли;
	
	ПроверитьВыполнениеУсловийПриИзмененииОтбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСрокПриИзменении(Элемент)

	ИспользоватьОтборСрок = ЗначениеЗаполнено(ОтборСрок);
	ПараметрыПользователяСрок = ОтборСрок;
	
	Если ИспользоватьОтборСрок Тогда
		ДанныеСтроки = Элементы.УсловияПродукта.ТекущиеДанные;
		Доход = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.РассчитатьДоход(ДатаРасчета,
			ОтборСумма, ОтборСрок, ДанныеСтроки.Ставка);
	КонецЕсли;
	
	ПроверитьВыполнениеУсловийПриИзмененииОтбора();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУсловияПродукта

&НаКлиенте
Процедура ПроверитьВыполнениеУсловийПриИзмененииОтбора()
	
	ИспользуетсяОтбор = Ложь;
	Если ИспользоватьОтборСрок Или ИспользоватьОтборСумма Тогда
		ИспользуетсяОтбор = Истина;
	КонецЕсли;
	
	ВыбраннаяСтрокаУсловия = Новый Структура;
	ВыбраннаяСтрокаУсловия.Вставить("ИндексСтроки", 0);
	ВыбраннаяСтрокаУсловия.Вставить("Ставка", 0);
	
	Для каждого ТекущаяСтрока из УсловияПродукта Цикл
		
		ВыполняютсяУсловия = Истина;
		
		Если ИспользуетсяОтбор Тогда
			ВыполняютсяУсловия = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ВыполняютсяУсловияПродукта(
				ТекущаяСтрока, ОтборСумма, ОтборСрок);
				
			Если ВыполняютсяУсловия
				И ВыбраннаяСтрокаУсловия.Ставка < ТекущаяСтрока.Ставка Тогда
				
				ВыбраннаяСтрокаУсловия.ИндексСтроки = УсловияПродукта.Индекс(ТекущаяСтрока);
				ВыбраннаяСтрокаУсловия.Ставка = ТекущаяСтрока.Ставка;
				
			КонецЕсли;
				
		КонецЕсли;
		
		ТекущаяСтрока.ВыполняютсяУсловия = ВыполняютсяУсловия;
		
	КонецЦикла;
	
	Если УсловияПродукта.Количество() > 0 Тогда
		Элементы.УсловияПродукта.ТекущаяСтрока = ВыбраннаяСтрокаУсловия.ИндексСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УсловияПродуктаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработатьВыборУсловия();
КонецПроцедуры

&НаКлиенте
Процедура УсловияПродуктаПриАктивизацииСтроки(Элемент)
	
	Если РасчетПриОформлении Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Элементы.УсловияПродукта.ТекущиеДанные;
	
	Если Не ЗначениеЗаполнено(ПараметрыПользователяСумма) Тогда
		Если ДанныеСтроки.СуммаМинимум = 0 Тогда
			ОтборСумма = ДанныеСтроки.СуммаМаксимум;
		Иначе
			ОтборСумма = ДанныеСтроки.СуммаМинимум;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыПользователяСрок) Тогда
		Если ДанныеСтроки.СрокМинимум = 0 Тогда
			ОтборСрок = ДанныеСтроки.СрокМаксимум;
		Иначе
			ОтборСрок = ДанныеСтроки.СрокМинимум;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ВыполняютсяУсловияПродукта(ДанныеСтроки, 
		ОтборСумма, ОтборСрок) Тогда
		
		Доход = ОбменСБанкамиДепозитыСлужебныйКлиентСервер.РассчитатьДоход(ДатаРасчета,
			ОтборСумма, ОтборСрок, ДанныеСтроки.Ставка);
			
	Иначе
			
		Доход = 0;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыборУсловия();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборУсловия()
	
	ДанныеСтроки = Элементы.УсловияПродукта.ТекущиеДанные;
	ОчиститьСообщения();
	
	Если Не ОбменСБанкамиДепозитыСлужебныйКлиентСервер.ВыполняютсяУсловияПродукта(
		ДанныеСтроки, ОтборСумма, ОтборСрок) Тогда
		
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Сумма депозита %1 на срок %2 не соответствует выбранному интервалу процентной ставки.
				|Укажите другую сумму и/или срок или выберите другой интервал.'"),
			ОтборСумма,
			ОтборСрок);
		ПоказатьПредупреждение(, ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Доход", Доход);
	Результат.Вставить("Сумма", ОтборСумма);
	Результат.Вставить("Срок", ОтборСрок);
	Результат.Вставить("КлючПродукта", КлючПродукта);
	Результат.Вставить("ИдентификаторУсловия", ДанныеСтроки.ИдентификаторУсловия);
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ОграничениеСуммы = 9999999999999.99;
	ОграничениеСрока = 9999;
	
	// Оформление поля СуммаМаксимум
	
	ИмяПоляОформления = "УсловияПродуктаСуммаМаксимум";
	ИмяПоляОтбора = "УсловияПродукта.СуммаМаксимум";
	Элемент = УсловноеОформление.Элементы.Добавить();
	ДобавитьЭлементОформления(Элемент, ИмяПоляОформления);
	ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
	ДобавитьОтборУсловногоОформления(Элемент, ЛевоеЗначение, , ОграничениеСуммы);
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Текст", НСтр("ru = 'Не ограничено'"));
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.НедоступныйДляВыбораЭлементБЭД);
	
	// Оформление поля СрокМаксимум
	
	ИмяПоляОформления = "УсловияПродуктаСрокМаксимум";
	ИмяПоляОтбора = "УсловияПродукта.СрокМаксимум";
	Элемент = УсловноеОформление.Элементы.Добавить();
	ДобавитьЭлементОформления(Элемент, ИмяПоляОформления);
	ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
	ДобавитьОтборУсловногоОформления(Элемент, ЛевоеЗначение, , ОграничениеСрока);
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Текст", НСтр("ru = 'Не ограничено'"));
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"ЦветТекста", ЦветаСтиля.НедоступныйДляВыбораЭлементБЭД);
	
	// Оформление полей УсловияПродуктаСуммаМинимум, УсловияПродуктаСуммаМаксимум, 
	// УсловияПродуктаСрокМинимум, УсловияПродуктаСрокМаксимум, УсловияПродуктаСтавка.
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ДобавитьЭлементОформления(Элемент, "УсловияПродуктаСуммаМинимум");
	ДобавитьЭлементОформления(Элемент, "УсловияПродуктаСуммаМаксимум");
	ДобавитьЭлементОформления(Элемент, "УсловияПродуктаСрокМинимум");
	ДобавитьЭлементОформления(Элемент, "УсловияПродуктаСрокМаксимум");
	ДобавитьЭлементОформления(Элемент, "УсловияПродуктаСтавка");
	
	ИмяПоляОтбора = "УсловияПродукта.ВыполняютсяУсловия";
	ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбора);
	ДобавитьОтборУсловногоОформления(Элемент, ЛевоеЗначение, , Истина);
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Шрифт", ШрифтыСтиля.ЖирныйШрифтБЭД);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементОформления(Элемент, ИмяПоля)
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ПолеЭлемента.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОтборУсловногоОформления(ЭлементОформления, ЛевоеЗначение, ВидСравнения = Неопределено, ПравоеЗначение = Неопределено)
	
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = ЛевоеЗначение;
	
	ОтборЭлемента.ВидСравнения = ВидСравнения;
	Если Не ПравоеЗначение = Неопределено Тогда
		ОтборЭлемента.ПравоеЗначение = ПравоеЗначение;
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти


