#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обновляет данные заявки на депозит по данным полученным из извещения о состоянии депозита
// 
// Параметры:
//  СсылкаНаЗаявку - ДокументСсылка.ЗаявкаНаДепозитОбменСБанками - ссылка на заявку для которой получено извещение
//  ДанныеЗаявки - Структура: см. ОбменСБанкамиДепозитыСлужебный.НовыеРеквизитыЗаявкиНаДепозитДляИзменения
//
Процедура ЗаполнитьДанныеЗаявкиПоСостоянию(СсылкаНаЗаявку, ДанныеЗаявки) Экспорт
	
	ЗаявкаОбъект = СсылкаНаЗаявку.ПолучитьОбъект();
	ИсключаемыеСвойства = "СпособВыплатыПроцентов, ГрафикВыплатыПроцентов";
	
	ЗаполнитьЗначенияСвойств(ЗаявкаОбъект, ДанныеЗаявки, , ИсключаемыеСвойства);
	ЗаявкаОбъект.ГрафикВыплатыПроцентов.Очистить();
	
	Для каждого ТекущийПлатеж Из ДанныеЗаявки.ГрафикВыплатыПроцентов Цикл
		ТекСтрока = ЗаявкаОбъект.ГрафикВыплатыПроцентов.Добавить();
		ЗаполнитьЗначенияСвойств(ТекСтрока, ТекущийПлатеж);
	КонецЦикла;
	
	ЗаявкаОбъект.Комментарий = ДанныеЗаявки.КомментарийБанка;

	СпособВыплатыПроцентов = ОбменСБанкамиДепозитыСлужебный.СпособыВыплатыПроцентовОбменСБанкамиДепозиты(
		ДанныеЗаявки.СпособВыплатыПроцентов);
	ЗаявкаОбъект.СпособВыплатыПроцентов = СпособВыплатыПроцентов;
	
	Валюта = ОбщегоНазначенияБЭД.НайтиСсылку("Валюты", ДанныеЗаявки.КодВалюты);
	БанковскийСчетОрганизации = ОбменСБанкамиСлужебный.БанковскийСчетОрганизацииПоНомеру(ЗаявкаОбъект.Организация,
		ДанныеЗаявки.НомерСчета);
	
	Если ЗначениеЗаполнено(БанковскийСчетОрганизации) И БанковскийСчетОрганизации <> ЗаявкаОбъект.СчетОрганизации Тогда
		ЗаявкаОбъект.СчетОрганизации = БанковскийСчетОрганизации;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Валюта) Тогда
		ЗаявкаОбъект.Валюта = Валюта;
	КонецЕсли;
	ЗаявкаОбъект.Подтвержден = Истина;
	ЗаявкаОбъект.Записать();
	
КонецПроцедуры

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецЕсли
