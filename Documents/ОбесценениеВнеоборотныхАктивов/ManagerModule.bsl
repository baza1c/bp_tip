#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 6, 0);
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();

	Результат = Запрос.Выполнить();
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьСтруктуруИзРезультатаЗапроса(Результат);

	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;

	НомераТаблиц = Новый Структура;

	Запрос.УстановитьПараметр("МоментВремени", Реквизиты.МоментВремени);
	Запрос.УстановитьПараметр("Организация",   Реквизиты.Организация);
	Запрос.УстановитьПараметр("СчетаНМА",      АмортизацияАктивов.СчетаАктивов(Тип("СправочникСсылка.НематериальныеАктивы")));
	
	Запрос.Текст =
		ТекстЗапросаТаблицаОС(НомераТаблиц)
		+ ТекстЗапросаОбесценениеОС(НомераТаблиц)
		+ ТекстЗапросаСобытияОС(НомераТаблиц)
		+ ТекстЗапросаПроверкиПоОС(НомераТаблиц)
		+ ТекстЗапросаНематериальныеАктивы(НомераТаблиц);

	Результат = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции

// Определяет списки (табличные части с основными данными) документа.
// Документ работает с несколькими классами объектов, перечень которых может увеличиваться в будущих версиях.
// 
// Возвращаемое значение:
//  Массив из Строка - имена табличных частей
//
Функция СпискиДокумента() Экспорт
	
	ОсновныеСписки = Новый Массив;
	ОсновныеСписки.Добавить("ОС");
	ОсновныеСписки.Добавить("НМА");
	Возврат ОсновныеСписки;
	
КонецФункции

// Определяет списки (табличные части с основными данными) документа,
// использование которых не допускается набором функциональных опций.
// 
// Возвращаемое значение:
//  Массив из Строка - имена табличных частей
//
Функция НеиспользуемыеСписки() Экспорт
	
	НеиспользуемыеСписки = Новый Массив;
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредств") Тогда
		НеиспользуемыеСписки.Добавить("ОС");
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА") Тогда
		НеиспользуемыеСписки.Добавить("НМА");
	КонецЕсли;
	
	Возврат НеиспользуемыеСписки;
	
КонецФункции

// Определяет учетные данные активов всех видов, информация о которых фиксируется в документе -
// балансовую стоимость, накопленное обесценение.
//
// Параметры:
//  Объекты  - Соответствие:
//     * Ключ - Тип (СправочникСсылка.ОсновныеСредства, СправочникСсылка.НематериальныеАктивы) - класс объектов, по которым запрашиваются сведения
//     * Значение - Массив из СправочникСсылка.ОсновныеСредства, СправочникСсылка.НематериальныеАктивы - перечень объектов, по которым запрашиваются сведения
//  Контекст - Структура - 
//     * Дата - Дата - указывает на месяц, на конец которого определяются данные
//     * Организация - СправочникСсылка.Организации
//     * МежотчетныйПериод - Булево
// 
// Возвращаемое значение:
//  Соответствие - 
//    * Ключ: Тип - класс активов (СправочникСсылка.ОсновныеСредства, СправочникСсылка.НематериальныеАктивы)
//    * Значение: ТаблицаЗначений
//       ** Объект - СправочникСсылка.ОсновныеСредства, СправочникСсылка.НематериальныеАктивы - в зависимости от списка
//       ** ИнвентарныйНомер - см. ТипИнвентарныйНомер
//       ** БалансоваяСтоимость - Число
//       ** СуммаОбесценения - Число, обесценение, доступное к восстановлению
//
Функция УчетныеДанные(Объекты, Контекст) Экспорт
	
	УчетныеДанные = Новый Соответствие;
	
	Для Каждого СоставКлассаОбъектов Из Объекты Цикл
	
		УчетныеДанныеКлассаОбъектов = УчетныеДанныеАктивов(
			СоставКлассаОбъектов.Ключ,
			СоставКлассаОбъектов.Значение,
			Контекст.Дата,
			Контекст.Организация,
			Контекст.МежотчетныйПериод);
		УчетныеДанные.Вставить(СоставКлассаОбъектов.Ключ, УчетныеДанныеКлассаОбъектов);
		
	КонецЦикла;
	
	Возврат УчетныеДанные;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Обесценение";
	КомандаПечати.Представление = НСтр("ru = 'Обесценение'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр),
//                                            см. УправлениеПечатью.ПодготовитьКоллекциюПечатныхФорм.
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Обесценение") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Обесценение",
			НСтр("ru = 'Обесценение внеоборотных активов'"),
			ПечатьОбесценение(МассивОбъектов, ОбъектыПечати),
			, // пиктограмма печатной формы отсутствует
			"Документ.ОбесценениеВнеоборотныхАктивов.ПФ_MXL_Обесценение");
		
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(
		МассивОбъектов,
		КоллекцияПечатныхФорм,
		ОбъектыПечати,
		ПараметрыВывода);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРеквизитыДокумента()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.СтатьяПрочихДоходовРасходов КАК СтатьяПрочихДоходовИРасходов,
	|	Реквизиты.СобытиеОС КАК ОтражатьВНалоговомУчете,
	|	Реквизиты.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаОС(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОсновныеСредства", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Регистратор,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.ИзменениеОбесценения КАК ИзменениеОбесценения,
	|	ТаблицаОС.Ссылка.ПодразделениеОрганизации КАК Подразделение
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор КАК Регистратор,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаОбесценениеОС(НомераТаблиц)

	НомераТаблиц.Вставить("РеквизитыУбытокОбесценения", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ОбесценениеОСТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.СтатьяПрочихДоходовРасходов КАК СтатьяПрочихДоходовИРасходов,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.МежотчетныйПериод КАК МежотчетныйПериод
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор КАК Регистратор,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК Объект,
	|	ТаблицаОС.Подразделение КАК Подразделение,
	|	ТаблицаОС.ИзменениеОбесценения КАК ИзменениеОбесценения
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСобытияОС(НомераТаблиц)

	НомераТаблиц.Вставить("СобытияОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СобытияОСТаблица", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка      КАК Регистратор,
	|	Реквизиты.Дата        КАК Период,
	|	Реквизиты.Номер       КАК Номер,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.СобытиеОС   КАК СобытиеОС
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор      КАК Регистратор,
	|	ТаблицаОС.НомерСтроки      КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	0                          КАК СуммаЗатратБУ,
	|	0                          КАК СуммаЗатратНУ,
	|	0                          КАК СуммаЗатратУСН
	|ИЗ
	|	ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаПроверкиПоОС(НомераТаблиц)

	НомераТаблиц.Вставить("ПроверкиПоОС", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка      КАК Регистратор,
	|	Реквизиты.Дата        КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	""ОС""                КАК ИмяСписка,
	|	НЕОПРЕДЕЛЕНО КАК МОЛ
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНематериальныеАктивы(НомераТаблиц)

	НомераТаблиц.Вставить("ВТ_НематериальныеАктивы",               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ПодразделенияНематериальныхАктивов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("НематериальныеАктивы",                  НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МАКСИМУМ(НематериальныеАктивы.НомерСтроки) КАК НомерСтроки,
	|	НематериальныеАктивы.НематериальныйАктив КАК Объект,
	|	СУММА(НематериальныеАктивы.ИзменениеОбесценения) КАК ИзменениеОбесценения
	|ПОМЕСТИТЬ НематериальныеАктивы
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов.НМА КАК НематериальныеАктивы
	|ГДЕ
	|	НематериальныеАктивы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НематериальныеАктивы.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПервоначальнаяСтоимость.Субконто1 КАК Объект,
	|	МАКСИМУМ(ПервоначальнаяСтоимость.Подразделение) КАК Подразделение
	|ПОМЕСТИТЬ ПодразделенияНематериальныхАктивов
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментВремени, Счет В (&СчетаНМА), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы), Организация = &Организация) КАК ПервоначальнаяСтоимость
	|
	|СГРУППИРОВАТЬ ПО
	|	ПервоначальнаяСтоимость.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""НМА"" КАК ИмяСписка,
	|	НематериальныеАктивы.НомерСтроки КАК НомерСтроки,
	|	НематериальныеАктивы.Объект КАК Объект,
	|	ЕСТЬNULL(ПодразделенияНематериальныхАктивов.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
	|	НематериальныеАктивы.ИзменениеОбесценения КАК ИзменениеОбесценения
	|ИЗ
	|	НематериальныеАктивы КАК НематериальныеАктивы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодразделенияНематериальныхАктивов КАК ПодразделенияНематериальныхАктивов
	|		ПО НематериальныеАктивы.Объект = ПодразделенияНематериальныхАктивов.Объект
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

#Область Печать

Функция ПечатьОбесценение(Ссылки, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Обесценение.Ссылка КАК Ссылка,
	|	Обесценение.Дата КАК Дата,
	|	Обесценение.СобытиеОС КАК СобытиеОС,
	|	Обесценение.Организация КАК Организация,
	|	Обесценение.Организация.НаименованиеСокращенное КАК ОрганизацияПредставление,
	|	Обесценение.Ответственный.ФизическоеЛицо КАК ОтветственныйФизическоеЛицо,
	|	Обесценение.ОС.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ОсновноеСредство КАК Объект,
	|		ОсновноеСредство КАК ОсновноеСредство,
	|		БалансоваяСтоимость КАК БалансоваяСтоимость,
	|		ВозмещаемаяСтоимость КАК ВозмещаемаяСтоимость,
	|		СуммаОбесценения КАК СуммаОбесценения,
	|		ИзменениеОбесценения КАК ИзменениеОбесценения
	|	) КАК ОС,
	|	Обесценение.НМА.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		НематериальныйАктив КАК Объект,
	|		НематериальныйАктив.Код КАК ИнвентарныйНомер,
	|		БалансоваяСтоимость КАК БалансоваяСтоимость,
	|		ВозмещаемаяСтоимость КАК ВозмещаемаяСтоимость,
	|		СуммаОбесценения КАК СуммаОбесценения,
	|		ИзменениеОбесценения КАК ИзменениеОбесценения
	|	) КАК НМА
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов КАК Обесценение
	|ГДЕ
	|	Обесценение.Ссылка В(&Ссылки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	Результат = Новый ТабличныйДокумент;
	Результат.АвтоМасштаб = Истина;
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	Результат.КлючПараметровПечати = "Документ.ОбесценениеВнеоборотныхАктивов.ПФ_MXL_Обесценение";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ОбесценениеВнеоборотныхАктивов.ПФ_MXL_Обесценение");
	
	ОбластиМакета = Новый Структура("Шапка, Подпись");
	Для Каждого ОписаниеОбласти Из ОбластиМакета Цикл
		ОбластиМакета[ОписаниеОбласти.Ключ] = Макет.ПолучитьОбласть(ОписаниеОбласти.Ключ);
	КонецЦикла;
	
	ВыборкаДокумент = РезультатЗапроса.Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДокумент.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			Результат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НачалоОбластиПечати = Результат.ВысотаТаблицы + 1;
		
		// Начало документа
		ОбластиМакета.Шапка.Параметры.Заполнить(ВыборкаДокумент);
		ФорматированныеПараметры = Новый Структура;
		ФорматированныеПараметры.Вставить("ДатаДокумента", НРег(Формат(ВыборкаДокумент.Дата, "ДЛФ=D")));
		ФорматированныеПараметры.Вставить("ДействуетС", Формат(КонецМесяца(ВыборкаДокумент.Дата) + 1, "ДФ='MMMM yyyy ""г.""'"));
		ОбластиМакета.Шапка.Параметры.Заполнить(ФорматированныеПараметры);
		Результат.Вывести(ОбластиМакета.Шапка);
		
		// Таблицы
		
		НеиспользуемыеСписки = НеиспользуемыеСписки();
		
		Если НеиспользуемыеСписки.Найти("ОС") = Неопределено Тогда
			
			ТаблицаСтрокОС = ВыборкаДокумент.ОС.Выгрузить();
			ТаблицаСтрокОС = УчетОС.ПолучитьТаблицуИнвентарныхНомеровОС(ТаблицаСтрокОС,
				ВыборкаДокумент.Организация, ВыборкаДокумент.Дата);
				
			ПечатьОбесценениеТаблица(
				Результат,
				ТаблицаСтрокОС,
				НСтр("ru = 'Основные средства'"),
				НСтр("ru = 'Основное средство'"),
				Макет);
			
		КонецЕсли;
		
		Если НеиспользуемыеСписки.Найти("НМА") = Неопределено Тогда
			
			ТаблицаСтрокНМА = ВыборкаДокумент.НМА.Выгрузить();
			ПечатьОбесценениеТаблица(
				Результат,
				ТаблицаСтрокНМА,
				НСтр("ru = 'Нематериальные активы'"),
				НСтр("ru = 'Нематериальный актив'"),
				Макет);
			
		КонецЕсли;
		
		// Конец документа
		ОбластиМакета.Подпись.Параметры.Заполнить(ВыборкаДокумент);
		
		Полномочия = ОтветственныеЛицаБП.ПолномочияОтветственного(
			ВыборкаДокумент.ОтветственныйФизическоеЛицо,
			ВыборкаДокумент.Организация,
			ВыборкаДокумент.Дата);
		СведенияПодписи = Новый Структура;
		СведенияПодписи.Вставить("ДолжностьПодписавшегоЛица", Полномочия.ДолжностьПредставление);
		СведенияПодписи.Вставить("ДокументПодписал",          Полномочия.ФИО.Представление);
		ОбластиМакета.Подпись.Параметры.Заполнить(СведенияПодписи);
		
		Результат.Вывести(ОбластиМакета.Подпись);
		
		// В табличном документе зададим имя области, в которую был выведен объект.
		// Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			Результат,
			НачалоОбластиПечати,
			ОбъектыПечати,
			ВыборкаДокумент.Ссылка);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ПечатьОбесценениеТаблица(Результат, Данные, ВидАктивов, ВидОбъекта, Макет)
	
	Если Не ЗначениеЗаполнено(Данные) Тогда
		Возврат;
	КонецЕсли;
	
	// Шапка таблицы
	
	ДанныеЗаголовка = Новый Структура;
	ДанныеЗаголовка.Вставить("ВидАктивов", ВидАктивов);
	ДанныеЗаголовка.Вставить("ВидОбъекта", ВидОбъекта);
	
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьЗаголовокТаблицы.Параметры.Заполнить(ДанныеЗаголовка);
	Результат.Вывести(ОбластьЗаголовокТаблицы);
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьШапкаТаблицы.Параметры.Заполнить(ДанныеЗаголовка);
	Результат.Вывести(ОбластьШапкаТаблицы);
	
	ПараметрыУбытка = Новый Структура;
	ПараметрыУбытка.Вставить("СуммаУбыткаПризнаваемая",      0);
	ПараметрыУбытка.Вставить("СуммаУбыткаВосстанавливаемая", 0);
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	Для Каждого СтрокаТаблицы Из Данные Цикл
		// Основная часть
		ОбластьСтрока.Параметры.Заполнить(СтрокаТаблицы);
		
		Если СтрокаТаблицы.ИзменениеОбесценения > 0 Тогда
			ПараметрыУбытка.СуммаУбыткаПризнаваемая      = СтрокаТаблицы.ИзменениеОбесценения;
			ПараметрыУбытка.СуммаУбыткаВосстанавливаемая = 0;
		ИначеЕсли СтрокаТаблицы.ИзменениеОбесценения < 0 Тогда
			ПараметрыУбытка.СуммаУбыткаПризнаваемая      = 0;
			ПараметрыУбытка.СуммаУбыткаВосстанавливаемая = -СтрокаТаблицы.ИзменениеОбесценения;
		Иначе
			ПараметрыУбытка.СуммаУбыткаПризнаваемая      = 0;
			ПараметрыУбытка.СуммаУбыткаВосстанавливаемая = 0;
		КонецЕсли;
		
		ОбластьСтрока.Параметры.Заполнить(ПараметрыУбытка);
		
		Результат.Вывести(ОбластьСтрока);
		
	КонецЦикла;
	
	// Конец таблицы
	ОбластьКонецТаблицы = Макет.ПолучитьОбласть("КонецТаблицы");
	Результат.Вывести(ОбластьКонецТаблицы);

КонецПроцедуры

#КонецОбласти

#Область УчетныеДанныеАктивов

Функция УчетныеДанныеАктивов(ТипОбъектов, Объекты, Период, Организация, МежотчетныйПериод)
	
	ТипСумма = БухгалтерскийУчетКлиентСервер.ТипСуммаНеотрицательная();
	
	УчетныеДанные = Новый ТаблицаЗначений;
	УчетныеДанные.Колонки.Добавить("Объект", Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипОбъектов))); // далее индексируется
	УчетныеДанные.Колонки.Добавить("ИнвентарныйНомер",    ТипИнвентарныйНомер(ТипОбъектов));
	УчетныеДанные.Колонки.Добавить("БалансоваяСтоимость", ТипСумма);
	УчетныеДанные.Колонки.Добавить("СуммаОбесценения",    ТипСумма); // доступного к восстановлению
	
	ЗаполнитьУчетныеДанныеАктивов(УчетныеДанные, ТипОбъектов, Объекты, Период, Организация, МежотчетныйПериод);
	
	УчетныеДанные.Индексы.Добавить("Объект");
	
	Возврат УчетныеДанные;
	
КонецФункции

Процедура ЗаполнитьУчетныеДанныеАктивов(УчетныеДанные, ТипОбъектов, Объекты, Знач Период, Организация, МежотчетныйПериод)

	Если Не ЗначениеЗаполнено(Объекты) Тогда
		Возврат;
	КонецЕсли;
	
	Период = КонецМесяца(Период);
	ПериодСтоимости = Период;
	Если МежотчетныйПериод Тогда
		ПериодСтоимости = Новый МоментВремениСУточнениемПериода(Период, БухгалтерскийУчет.ВерхняяГраницаУточненияПериода());
		// Для межотчетного периода считаем, что все необходимые расчеты уже выполнены
	КонецЕсли;
	
	Если МежотчетныйПериод Или РассчитанаБалансоваяСтоимость(ТипОбъектов, Период, Организация) Тогда
		Стоимость = АмортизацияАктивов.Стоимость(ТипОбъектов, ПериодСтоимости, Организация, Объекты);
	Иначе
		Стоимость = АмортизацияАктивов.РассчитатьСтоимость(ТипОбъектов, ПериодСтоимости, Организация, Объекты);
	КонецЕсли;
	
	ИнвентарныеНомера = ИнвентарныеНомера(ТипОбъектов, Объекты, Период, Организация);
	
	Для Каждого Объект Из Объекты Цикл
		
		Запись = УчетныеДанные.Добавить();
		Запись.Объект = Объект;
		Запись.ИнвентарныйНомер = ИнвентарныеНомера[Объект];
		
		СтоимостьОбъекта = Стоимость.Найти(Объект, "Объект");
		Если СтоимостьОбъекта <> Неопределено Тогда
			Запись.БалансоваяСтоимость = СтоимостьОбъекта.БалансоваяСтоимость;
			Запись.СуммаОбесценения    = СтоимостьОбъекта.ДоступноеОбесценение;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция ТипИнвентарныйНомер(ТипОбъектов)
	
	Если ТипОбъектов = Тип("СправочникСсылка.НематериальныеАктивы") Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(Метаданные.Справочники.НематериальныеАктивы.ДлинаКода);
	Иначе
		Возврат Метаданные.РегистрыСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.Ресурсы.ИнвентарныйНомер.Тип;
	КонецЕсли;
	
КонецФункции

Функция ИнвентарныеНомера(ТипОбъектов, Объекты, Период, Организация)
	
	Если ТипОбъектов = Тип("СправочникСсылка.НематериальныеАктивы") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Объекты, "Код");
	Иначе
		
		ОсновныеСредства = Новый ТаблицаЗначений;
		ОсновныеСредства.Колонки.Добавить("ОсновноеСредство", Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
		Для Каждого Объект Из Объекты Цикл
			ОсновныеСредства.Добавить().ОсновноеСредство = Объект;
		КонецЦикла;
		ИнвентарныеНомераТаблица = УчетОС.ПолучитьТаблицуИнвентарныхНомеровОС(ОсновныеСредства, Организация, Период);
		
		ИнвентарныеНомера = Новый Соответствие;
		Для Каждого ОписаниеОсновногоСредства Из ИнвентарныеНомераТаблица Цикл
			ИнвентарныеНомера.Вставить(ОписаниеОсновногоСредства.ОсновноеСредство, ОписаниеОсновногоСредства.ИнвентарныйНомер);
		КонецЦикла;
		
		Возврат ИнвентарныеНомера;
		
	КонецЕсли;
	
КонецФункции

Функция РассчитанаБалансоваяСтоимость(ТипОбъектов, Период, Организация)
	
	ВидОперации = Перечисления.ВидыРегламентныхОпераций.АмортизацияИИзносОС;
	Если ТипОбъектов = Тип("СправочникСсылка.НематериальныеАктивы") Тогда
		ВидОперации = Перечисления.ВидыРегламентныхОпераций.АмортизацияНМАИсписаниеРасходовПоНИОКР;
	КонецЕсли;
	
	РеглОперация = Документы.РегламентнаяОперация.НайтиДокумент(Период, Организация, ВидОперации);
		
	Если Не ЗначениеЗаполнено(РеглОперация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Состояние = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеглОперация, "Состояние");
	Возврат Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Выполнено;
	
	// Не проверяем требование повторного выполнения ("актуальность регл. операции"),
	// так как работа с документом обесценения приводит к требованию повторного выполнения ("неактуальности").
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
