#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура") 
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	КонецЕсли;
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	СобытиеОС = Справочники.СобытияОС.СобытиеОСПоВиду(Перечисления.ВидыСобытийОС.ПризнаниеУбыткаОтОбесценения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.ОбесценениеВнеоборотныхАктивов.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ
	УчетОС.ПроверитьСоответствиеОСОрганизации(ПараметрыПроведения.ОсновныеСредства,
		ПараметрыПроведения.ПроверкиПоОС, Отказ);

	УчетОС.ПроверитьСостояниеОСПринятоКУчету(ПараметрыПроведения.ОсновныеСредства,
		ПараметрыПроведения.ПроверкиПоОС, Отказ);
		
	УчетОС.ПроверитьСоответствиеМестонахожденияОС(ПараметрыПроведения.ОсновныеСредства,
		ПараметрыПроведения.ПроверкиПоОС, Отказ);

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Алгоритмы формирования проводок этого документа рассчитывают суммы проводок налогового учета
	Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("СуммыНалоговогоУчетаЗаполнены", Истина);

	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	УчетОС.СформироватьДвиженияРегистрацияСобытияОС(
		ПараметрыПроведения.СобытияОСТаблица,
		ПараметрыПроведения.СобытияОС,
		Движения, Отказ);
		
	УправлениеВнеоборотнымиАктивами.СформироватьДвиженияУбытокОбесценения(
		Движения,
		ПараметрыПроведения.РеквизитыУбытокОбесценения,
		Тип("СправочникСсылка.ОсновныеСредства"),
		ПараметрыПроведения.ОбесценениеОСТаблица);
		
	УправлениеВнеоборотнымиАктивами.СформироватьДвиженияУбытокОбесценения(
		Движения,
		ПараметрыПроведения.РеквизитыУбытокОбесценения,
		Тип("СправочникСсылка.НематериальныеАктивы"),
		ПараметрыПроведения.НематериальныеАктивы);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ОС) И НЕ УчетнаяПолитика.ПрименяетсяФСБУ6(Организация, Дата) И НЕ МежотчетныйПериод Тогда
		ТекстСообщения = НСтр("ru = 'Обесценение ОС применяется только после перехода на ФСБУ 6 или в межотчетный период 31 декабря 2021 года.'");
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность",
				НСтр("ru = 'Дата'"),,, ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
				"Дата", "Объект", Отказ);
	КонецЕсли;
	
	ОсновныеСписки = Документы.ОбесценениеВнеоборотныхАктивов.СпискиДокумента();
	НеиспользуемыеСписки = Документы.ОбесценениеВнеоборотныхАктивов.НеиспользуемыеСписки();

	ОбщегоНазначенияБП.ИсключитьИзПроверкиНеиспользуемыеТабличныеЧасти(ПроверяемыеРеквизиты, НеиспользуемыеСписки);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ОсновныеСписки, НеиспользуемыеСписки);
	
	ОбщегоНазначенияБП.ИсключитьИзПроверкиОсновныеТабличныеЧасти(ЭтотОбъект, ОсновныеСписки, ПроверяемыеРеквизиты);
	
	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОС", Новый Структура("ОсновноеСредство"), Отказ);
	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "НМА", Новый Структура("НематериальныйАктив"), Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	МежотчетныйПериод = ?(КонецДня(Дата) = КонецГода(Дата), Истина, Ложь) И Год(Дата) = 2021;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(Основание)

	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);

	Если ТипЗнч(Основание) = Тип("СправочникСсылка.ОсновныеСредства") Тогда
		Если Основание.ЭтоГруппа Тогда
			ТекстСообщения = НСтр("ru = 'Ввод изменения состояния ОС на основании группы ОС невозможен!
				|Выберите ОС. Для раскрытия группы используйте клавиши Ctrl и стрелку вниз'");
			ВызватьИсключение(ТекстСообщения);
		КонецЕсли;

		СтрокаТабличнойЧасти = ОС.Добавить();
		СтрокаТабличнойЧасти.ОсновноеСредство = Основание.Ссылка;

	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ФункциональностьДоступна = Ложь;
	УчетОбесцененияАктивов.ОпределитьФункциональностьДоступна(ФункциональностьДоступна);
	Если Не ФункциональностьДоступна Тогда
		ВызватьИсключение НСтр("ru = 'Функциональность недоступна (отключена)'");
	КонецЕсли;
	
	ИспользуетсяМежотчетныйПериод = БухгалтерскийУчетКлиентСервер.ИспользуетсяМежотчетныйПериод();
	МежотчетныйПериод = Ложь;
	
	Если ИспользуетсяМежотчетныйПериод И НЕ ПолучитьФункциональнуюОпцию("ПрименятьФСБУ6Досрочно", Новый Структура("Организация", Организация)) Тогда
		
		МежотчетныйПериод = ?(КонецДня(Дата) = КонецГода(Дата), Истина, Ложь) И Год(Дата) = 2021;
		
		Если МежотчетныйПериод Тогда
			Дата = КонецДня(Дата);
			СтатьяПрочихДоходовРасходов = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли