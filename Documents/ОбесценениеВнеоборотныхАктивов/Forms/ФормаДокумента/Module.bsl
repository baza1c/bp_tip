#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	НастроитьОтображениеСтраницОбъектовУчета();
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Скопированные данные наверняка устарели, потому что основной сценарий копирования документа -
	// проверка на обесценение на очередную отчетную дату.
	Если Не ЗначениеЗаполнено(Параметры.Ключ) И ЗаполненЛюбойСписок() Тогда
		ЗаполнитьУчетныеДанныеВсе();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборОсновныхСредств.Форма.Форма" Тогда
		
		Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			Возврат;
		КонецЕсли;
		
		ДлительнаяОперацияУчетныеДанные = ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение);
		ОжидатьЗавершениеУчетныеДанные(ДлительнаяОперацияУчетныеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПроведениеСервер.УстановитьПризнакПроверкиРеквизитов(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененаИнформацияОС", Объект.Ссылка);// отображаемая в формах справочника ОС
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)

	ОпределитьМежотчетныйПериодДляРетроспективногоОбесценения();
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;
	
	// От даты документа зависят ранее прочитанные учетные данные - при изменении может потребоваться их заполнить
	ЗаполненныеДанныеУстарели = Ложь;
	Если ЗаполненЛюбойСписок() Тогда
		ЗаполненныеДанныеУстарели = (НачалоМесяца(Объект.Дата) <> НачалоМесяца(ТекущаяДатаДокумента));
	КонецЕсли;
	
	Если ЗаполненныеДанныеУстарели Тогда
		ЗаполнитьУчетныеДанныеВсе();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОпределитьМежотчетныйПериодДляРетроспективногоОбесценения();
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ЗаполнитьУчетныеДанныеВсе();
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОС

&НаКлиенте
Процедура ОСОсновноеСредствоПриИзменении(Элемент)
	
	ЗаполнитьУчетныеДанныеСтроки(Элементы.ОС.ТекущиеДанные, "ОС", "ОсновноеСредство");
	
КонецПроцедуры

&НаКлиенте
Процедура ОСПриИзменении(Элемент)
	
	ПриИзмененииСписка(Элементы.ОС);
	
КонецПроцедуры

&НаКлиенте
Процедура ОСВозмещаемаяСтоимостьПриИзменении(Элемент)
	
	ПриИзмененииВозмещаемойСтоимости(Элементы.ОС);
	
КонецПроцедуры

&НаКлиенте
Процедура ОССуммаУбыткаПризнаваемаяПриИзменении(Элемент)
	
	ОбновитьИзменениеОбесцененияПризнаниеУбытка(Элементы.ОС.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОССуммаУбыткаВосстанавливаемаяПриИзменении(Элемент)
	
	ОбновитьИзменениеОбесцененияВосстановлениеУбытка(Элементы.ОС.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНМА

&НаКлиенте
Процедура НМАПриИзменении(Элемент)
	
	ПриИзмененииСписка(Элементы.НМА);
	
КонецПроцедуры

&НаКлиенте
Процедура НМАНематериальныйАктивПриИзменении(Элемент)
	
	ЗаполнитьУчетныеДанныеСтроки(Элементы.НМА.ТекущиеДанные, "НМА", "НематериальныйАктив");
	
КонецПроцедуры

&НаКлиенте
Процедура НМАВозмещаемаяСтоимостьПриИзменении(Элемент)
	ПриИзмененииВозмещаемойСтоимости(Элементы.НМА);
КонецПроцедуры

&НаКлиенте
Процедура НМАСуммаУбыткаПризнаваемаяПриИзменении(Элемент)
	
	ОбновитьИзменениеОбесцененияПризнаниеУбытка(Элементы.НМА.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура НМАСуммаУбыткаВосстанавливаемаяПриИзменении(Элемент)
	
	ОбновитьИзменениеОбесцененияВосстановлениеУбытка(Элементы.НМА.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборОС(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоказатьОстатки", Истина);
	ПараметрыФормы.Вставить("ДатаОстатков", Объект.Дата);
	ПараметрыФормы.Вставить("Организация",  Объект.Организация);
	
	Если Объект.ОС.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("АдресОСВХранилище", ПоместитьОСВХранилище());
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ПодборОсновныхСредств.Форма.Форма", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОсновныеСредства(Команда)
	
	НачатьОбновлениеСтоимостныхПоказателей("ОС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНематериальныеАктивы(Команда)
	
	НачатьОбновлениеСтоимостныхПоказателей("НМА");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УстановитьСостояниеДокумента();
	
	ТекущаяДатаДокумента = Объект.Дата;
	
	ЗаполнитьИнвентарныеНомераОС();
	ЗаполнитьИнвентарныеНомераНМА();
	
	ЗаполнитьДобавленныеКолонки();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ОграничитьВосстановлениеУбытка("ОССуммаУбыткаВосстанавливаемая",  "ОС");
	ОграничитьВосстановлениеУбытка("НМАСуммаУбыткаВосстанавливаемая", "НМА");
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеСтраницОбъектовУчета()
	
	КоличествоСтраниц =
		Документы.ОбесценениеВнеоборотныхАктивов.СпискиДокумента().Количество() - 
		Документы.ОбесценениеВнеоборотныхАктивов.НеиспользуемыеСписки().Количество();
		
	Если КоличествоСтраниц > 1 Тогда
		ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	Иначе
		// Из имени колонки списка понятно, что в этом списке - заголовок не требуется
		ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	Элементы.ОбъектыУчета.ОтображениеСтраниц = ОтображениеСтраниц;
	
КонецПроцедуры

&НаСервере
Процедура ОграничитьВосстановлениеУбытка(ИмяПоля, ИмяСписка)

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, ИмяПоля);
	
	ПроверяемыйРеквизит = СтрШаблон("Объект.%1.СуммаОбесценения", ИмяСписка);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, ПроверяемыйРеквизит, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	Элементы.ОСЗаголовокСуммаВосстановления.Видимость = НЕ Объект.МежотчетныйПериод;
	Элементы.ОССуммаУбыткаВосстанавливаемая.Видимость = НЕ Объект.МежотчетныйПериод;
	Элементы.ОСЗаголовокОбесценение.Видимость = НЕ Объект.МежотчетныйПериод;
	Элементы.ОССуммаОбесценения.Видимость = НЕ Объект.МежотчетныйПериод;
	Элементы.СтатьяПрочихДоходовРасходов.Видимость = НЕ Объект.МежотчетныйПериод;
	Элементы.НМА.Видимость = НЕ Объект.МежотчетныйПериод;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФСБУ6ПрименяетсяДосрочно(Организация)
	Возврат ПолучитьФункциональнуюОпцию("ПрименятьФСБУ6Досрочно", Новый Структура("Организация", Организация));
КонецФункции

&НаКлиенте
Процедура ОпределитьМежотчетныйПериодДляРетроспективногоОбесценения()
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) ИЛИ НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		Объект.МежотчетныйПериод = Ложь;
		Возврат;
	КонецЕсли;
	
	Если НЕ ФСБУ6ПрименяетсяДосрочно(Объект.Организация) Тогда
		МежотчетныйПериод = ?(КонецДня(Объект.Дата) = КонецГода(Объект.Дата), Истина, Ложь) И Год(Объект.Дата) = 2021;
		Объект.МежотчетныйПериод = МежотчетныйПериод;
		Если МежотчетныйПериод Тогда
			Объект.Дата = КонецДня(Объект.Дата);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область ПоляСписковОбъектовУчета

&НаКлиенте
Процедура ПриИзмененииСписка(ПолеСписка)
	
	ТекущиеДанные = ПолеСписка.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьДобавленныеКолонкиСтроки(ТекущиеДанные);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВозмещаемойСтоимости(ПолеСписка)
	
	ОбновитьСуммуУбытка(ПолеСписка.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзменениеОбесцененияПризнаниеУбытка(СтрокаТЧ)
	СтрокаТЧ.СуммаУбыткаПризнаваемая = Мин(СтрокаТЧ.БалансоваяСтоимость, СтрокаТЧ.СуммаУбыткаПризнаваемая);
	СтрокаТЧ.СуммаУбыткаВосстанавливаемая = 0;
	СтрокаТЧ.ИзменениеОбесценения = СтрокаТЧ.СуммаУбыткаПризнаваемая - СтрокаТЧ.СуммаУбыткаВосстанавливаемая;
	СтрокаТЧ.ВозмещаемаяСтоимость = СтрокаТЧ.БалансоваяСтоимость - СтрокаТЧ.ИзменениеОбесценения;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзменениеОбесцененияВосстановлениеУбытка(СтрокаТЧ)
	СтрокаТЧ.СуммаУбыткаВосстанавливаемая = Мин(СтрокаТЧ.СуммаОбесценения, СтрокаТЧ.СуммаУбыткаВосстанавливаемая);
	СтрокаТЧ.СуммаУбыткаПризнаваемая = 0;
	СтрокаТЧ.ИзменениеОбесценения = СтрокаТЧ.СуммаУбыткаПризнаваемая - СтрокаТЧ.СуммаУбыткаВосстанавливаемая;
	СтрокаТЧ.ВозмещаемаяСтоимость = СтрокаТЧ.БалансоваяСтоимость - СтрокаТЧ.ИзменениеОбесценения;
КонецПроцедуры

&НаКлиенте
Процедура НачатьОбновлениеСтоимостныхПоказателей(ИмяСписка)
	
	ДлительнаяОперацияУчетныеДанные = ОбновитьСтоимостныеПоказатели(ИмяСписка);
	ОжидатьЗавершениеУчетныеДанные(ДлительнаяОперацияУчетныеДанные);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСуммуУбытка(СтрокаТЧ)
	
	// убыток - отрицательный; восстановление положительное;
	// восстановление ограничено доступным обесценением
	Убыток = Мин(СтрокаТЧ.СуммаОбесценения, СтрокаТЧ.ВозмещаемаяСтоимость - СтрокаТЧ.БалансоваяСтоимость);
	
	// признание и восстановление - беззнаковое (неотрицательное) число
	СтрокаТЧ.СуммаУбыткаПризнаваемая      = Макс(0, -Убыток);
	СтрокаТЧ.СуммаУбыткаВосстанавливаемая = Макс(0, Убыток);
	
	// "изменение" - знаковое число, инвертированное (убыток - положительный)
	СтрокаТЧ.ИзменениеОбесценения = -Убыток;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьСтоимостныеПоказатели(Знач ИмяСписка)
	
	ИмяКолонкиОбъектУчета = "ОсновноеСредство";
	Если ИмяСписка = "НМА" Тогда
		ИмяКолонкиОбъектУчета = "НематериальныйАктив";
	КонецЕсли;
	
	Возврат НачатьУчетныеДанныеСпискаОбъектов(Объект[ИмяСписка], ИмяСписка, ИмяКолонкиОбъектУчета);
	
КонецФункции

#КонецОбласти

#Область Подбор

&НаСервере
Функция ПоместитьОСВХранилище()
	
	ТаблицаОС = Объект.ОС.Выгрузить(, "НомерСтроки, ОсновноеСредство");
	Возврат ПоместитьВоВременноеХранилище(ТаблицаОС);
	
КонецФункции

&НаСервере
Функция ОбработкаВыбораПодборНаСервере(Знач ВыбранноеЗначение)
	
	ДобавленныеСтроки = УчетОС.ОбработатьПодборОсновныхСредств(Объект.ОС, ВыбранноеЗначение);
	
	Для Каждого СтрокаТаблицы Из ДобавленныеСтроки Цикл
		ЗаполнитьДобавленныеКолонкиСтроки(СтрокаТаблицы);
	КонецЦикла;
	
	Возврат НачатьУчетныеДанныеСпискаОбъектов(ДобавленныеСтроки, "ОС", "ОсновноеСредство");
	
КонецФункции

&НаСервере
Процедура ЗаполнитьИнвентарныеНомераОС()
	
	ТаблицаОС = Объект.ОС.Выгрузить();
	
	ТаблицаНомеров = УчетОС.ПолучитьТаблицуИнвентарныхНомеровОС(ТаблицаОС,
		Объект.Организация, Объект.Дата);
	
	Объект.ОС.Загрузить(ТаблицаНомеров);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнвентарныеНомераНМА()
	
	ОбъектыУчета = Объект.НМА.Выгрузить(, "НематериальныйАктив").ВыгрузитьКолонку("НематериальныйАктив");
	ИнвентарныеНомера = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ОбъектыУчета, "Код");
	Для Каждого СтрокаТЧ Из Объект.НМА Цикл
		ЗначенияРеквизитов = ИнвентарныеНомера[СтрокаТЧ.НематериальныйАктив];
		Если ЗначенияРеквизитов <> Неопределено Тогда
			СтрокаТЧ.ИнвентарныйНомер = ЗначенияРеквизитов.Код;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Заполнение

&НаКлиенте
Функция ЗаполненЛюбойСписок()
	Возврат ЗначениеЗаполнено(Объект.ОС) Или ЗначениеЗаполнено(Объект.НМА);
КонецФункции

&НаСервере
Процедура ЗаполнитьДобавленныеКолонки()

	Списки = Новый Массив;
	Списки.Добавить(Объект.ОС);
	Списки.Добавить(Объект.НМА);
	
	Для Каждого Список Из Списки Цикл
		Для Каждого СтрокаТаблицы Из Список Цикл
			ЗаполнитьДобавленныеКолонкиСтроки(СтрокаТаблицы);
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтроки(СтрокаТаблицы)
	
	СтрокаТаблицы.ЗаголовокБалансоваяСтоимость   = НСтр("ru='балансовая стоимость'");
	СтрокаТаблицы.ЗаголовокВозмещаемаяСтоимость  = НСтр("ru='возмещаемая сумма'");
	
	СтрокаТаблицы.ЗаголовокОбесценение           = НСтр("ru='накопленное обесценение'");
	СтрокаТаблицы.ЗаголовокСуммаОбесценения      = НСтр("ru='признать убыток'");
	СтрокаТаблицы.ЗаголовокСуммаВосстановления   = НСтр("ru='восстановить убыток'");
	
	ЗаполнитьКолонкиИзменениеОбесценения(СтрокаТаблицы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКолонкиИзменениеОбесценения(СтрокаТаблицы)
	
	Если СтрокаТаблицы.ИзменениеОбесценения > 0 Тогда
		
		СтрокаТаблицы.СуммаУбыткаПризнаваемая = СтрокаТаблицы.ИзменениеОбесценения;
		СтрокаТаблицы.СуммаУбыткаВосстанавливаемая = 0;
		
		//Восстановление убытка от обесценения
	ИначеЕсли СтрокаТаблицы.ИзменениеОбесценения < 0 Тогда
		
		СтрокаТаблицы.СуммаУбыткаПризнаваемая = 0;
		СтрокаТаблицы.СуммаУбыткаВосстанавливаемая = - СтрокаТаблицы.ИзменениеОбесценения;;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ЗаполнитьУчетныеДанные

&НаКлиенте
Процедура ЗаполнитьУчетныеДанныеВсе()
	
	Если Не ЗаполненЛюбойСписок() Тогда
		Возврат;
	КонецЕсли;
	
	ОжидатьЗавершениеУчетныеДанные(НачатьУчетныеДанныеВсе());
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеУчетныеДанные(ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершениеОперации = Новый ОписаниеОповещения("ЗакончитьУчетныеДанные", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ЗавершениеОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУчетныеДанныеСтроки(СтрокаТЧ, ИмяСписка, ИмяПоляОбъектУчета)
	
	ОбъектУчета = СтрокаТЧ[ИмяПоляОбъектУчета];
	
	Если Не ЗначениеЗаполнено(ОбъектУчета) Тогда
		СтрокаТЧ.ИнвентарныйНомер    = "";
		СтрокаТЧ.БалансоваяСтоимость = 0;
		СтрокаТЧ.СуммаОбесценения    = 0;
	Иначе
		УчетныеДанные = УчетныеДанные(
			ОбъектУчета,
			КонтекстУчетныхДанных(Объект),
			УникальныйИдентификатор);
		
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, УчетныеДанные);
	КонецЕсли;
	
	ОбновитьСуммуУбытка(СтрокаТЧ);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КонтекстУчетныхДанных(Объект)
	
	Контекст = Новый Структура("Дата, Организация, МежотчетныйПериод");
	ЗаполнитьЗначенияСвойств(Контекст, Объект);
	Возврат Контекст;
	
КонецФункции

&НаСервереБезКонтекста
Функция УчетныеДанные(ОбъектУчета, Контекст, ИдентификаторФормы)
	
	ТипОбъектовУчета = ТипЗнч(ОбъектУчета);
	
	ОбъектыУчета = Новый Соответствие;
	ОбъектыУчета.Вставить(ТипОбъектовУчета, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектУчета));
	
	УчетныеДанныеОбъектов = Документы.ОбесценениеВнеоборотныхАктивов.УчетныеДанные(ОбъектыУчета, Контекст);
	
	УчетныеДанные = УчетныеДанныеОбъектов[ТипОбъектовУчета];
	Если УчетныеДанные <> Неопределено И УчетныеДанные.Количество() = 1 Тогда
		Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(УчетныеДанные[0]);
	Иначе
		Возврат Новый Структура;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция НачатьУчетныеДанныеСпискаОбъектов(СтрокиТЧ, ИмяСписка, ИмяПоляОбъектУчета)
	
	Если Не ЗначениеЗаполнено(СтрокиТЧ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОбъектыСписка = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиТЧ, ИмяПоляОбъектУчета);
	ТипОбъектовУчета = ТипЗнч(ОбъектыСписка[0]);
	
	ОбъектыУчета = Новый Соответствие;
	ОбъектыУчета.Вставить(ТипОбъектовУчета, ОбъектыСписка);
	
	Возврат НачатьУчетныеДанные(ОбъектыУчета);
	
КонецФункции

&НаСервере
Функция НачатьУчетныеДанныеВсе()
	
	ОбъектыУчета = Новый Соответствие;
	ОбъектыУчета.Вставить(
		Тип("СправочникСсылка.ОсновныеСредства"),
		Объект.ОС.Выгрузить(, "ОсновноеСредство").ВыгрузитьКолонку("ОсновноеСредство"));
	ОбъектыУчета.Вставить(
		Тип("СправочникСсылка.НематериальныеАктивы"),
		Объект.НМА.Выгрузить(, "НематериальныйАктив").ВыгрузитьКолонку("НематериальныйАктив"));
	
	Возврат НачатьУчетныеДанные(ОбъектыУчета);
	
КонецФункции

&НаСервере
Функция НачатьУчетныеДанные(ОбъектыУчета)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Лимит = ЛимитРасчетаАмортизацииСинхронно();
	
	КоличествоОбъектов = 0;
	ПараметрыВыполнения.ЗапуститьНеВФоне = Истина; // для небольшого количества объектов
	
	Для Каждого КлассОбъектовУчета Из ОбъектыУчета Цикл
		
		КоличествоОбъектов = КоличествоОбъектов + КлассОбъектовУчета.Значение.Количество();
		
		Если КоличествоОбъектов > Лимит Тогда
			ПараметрыВыполнения.ЗапуститьНеВФоне = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Документы.ОбесценениеВнеоборотныхАктивов.УчетныеДанные",
		ОбъектыУчета,
		КонтекстУчетныхДанных(Объект));
	
КонецФункции

&НаКлиенте
Процедура ЗакончитьУчетныеДанные(Результат, НеиспользуемыйПараметр) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось определить сведения об объектах внеоборотных активов. Подробная информация в журнале регистрации.'");
	КонецЕсли;
	
	ЗакончитьУчетныеДанныеНаСервере(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура ЗакончитьУчетныеДанныеНаСервере(АдресРезультата)
	
	УчетныеДанные = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(УчетныеДанные) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого УчетныеДанныеСписка Из УчетныеДанные Цикл
		
		Если УчетныеДанныеСписка.Ключ = Тип("СправочникСсылка.ОсновныеСредства") Тогда
			ИмяСписка     = "ОС";
			ИмяПоляСписка = "ОсновноеСредство";
		ИначеЕсли УчетныеДанныеСписка.Ключ = Тип("СправочникСсылка.НематериальныеАктивы") Тогда
			ИмяСписка     = "НМА";
			ИмяПоляСписка = "НематериальныйАктив";
		Иначе
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из Объект[ИмяСписка] Цикл
			
			ДанныеОбъекта = УчетныеДанныеСписка.Значение.Найти(СтрокаТаблицы[ИмяПоляСписка], "Объект");
			Если ДанныеОбъекта = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеОбъекта);
			
			ОбновитьСуммуУбытка(СтрокаТаблицы);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЛимитРасчетаАмортизацииСинхронно()
	Возврат 10;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
