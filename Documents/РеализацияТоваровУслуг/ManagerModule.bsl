#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 14, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область СчетаУчета

Процедура УстановитьПравилаЗаполненияСчетовУчета(Правила) Экспорт
	
	// Шапка
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКонтрагентом", "РасчетыСПокупателем");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТребуетсяУчетРасчетов");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаРасчетовПоАвансам", "АвансыПокупателя");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТребуетсяУчетАвансов");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовПоАвансам", "АвансыПокупателя");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТребуетсяУчетАвансов");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтгрузкаБезПереходаПраваСобственности");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ТребуетсяУчетРасчетов");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовПоТаре",        "ВозвратнаяТараПереданная");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УчетТарыНаБалансе");
	
	// Табличная часть Товары
	
	//   Запасы (кроме оборудования, выкуп товаров на реализации, розницы в ценах продажи)
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",           "Запасы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "Оборудование");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ТоварыВЦенахПродажи");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ВыкупТоваровКомиссионером");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПродажаТоваровСоСкладаКомиссионера");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "ПереданныеСчетУчета", "ЗапасыОтгруженные");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОперацииСКомиссионером");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "ПереданныеСчетУчета", "ЗапасыОтгруженные");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтгрузкаБезПереходаПраваСобственности");
	
	//   Выручка (кроме оборудования, розницы в ценах продажи)
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетДоходов", "Выручка");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СчетЗапасов", "СчетУчета");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ОперацииСКомиссионером");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеПроверкиЗаполнения(Правила, "СобственныеЗапасы");
	
	СчетаУчетаВДокументах.ДобавитьУровеньПодчиненности(Правила);
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто",                 "ВидДеятельности");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаНДСПоРеализации", "ВыручкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",  "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетАкциза", "ВыручкаАкцизы");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетРасходов",             "Расходы");
	
	//   Запасы (выкуп товаров на реализации)
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",           "ЗапасыОтгруженные");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ВыкупТоваровКомиссионером");
	
	//   Выручка (выкуп товаров на реализации)
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетДоходов", "Выручка");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СчетЗапасов", "СчетУчета");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеПроверкиЗаполнения(Правила, "СобственныеЗапасы");
	
	СчетаУчетаВДокументах.ДобавитьУровеньПодчиненности(Правила);
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто",                 "ВидДеятельности");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаНДСПоРеализации", "ВыручкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",  "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетРасходов",             "Расходы");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетАкциза", "ВыручкаАкцизы");
	
	//   Запасы (продажа со склада комиссионера)
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",           "ЗапасыОтгруженные");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПродажаТоваровСоСкладаКомиссионера");
	
	//   Выручка (продажа со склада комиссионера)
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетДоходов", "Выручка");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СчетЗапасов", "СчетУчета");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеПроверкиЗаполнения(Правила, "СобственныеЗапасы");
	
	СчетаУчетаВДокументах.ДобавитьУровеньПодчиненности(Правила);
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто",                 "ВидДеятельности");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаНДСПоРеализации", "ВыручкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",  "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетРасходов",             "Расходы");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетАкциза", "ВыручкаАкцизы");
	
	
	// При отгрузке без перехода права собственности счета выручки также указываются в этом документе, но используются при проведении другого документа.
	
	//  Оборудование (тоже в табличной части Товары)
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",     "ОборудованиеНаСкладе");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "Оборудование");
	
	//   Выручка (для оборудования)
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетДоходов",  "Выручка");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СчетЗапасов", "СчетУчета");
	
	СчетаУчетаВДокументах.ДобавитьУровеньПодчиненности(Правила);
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто",     "ВидДеятельности");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаНДСПоРеализации", "ВыручкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",  "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетРасходов", "Расходы");
	
	//   Запасы (розница в ценах продажи)
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",           "ЗапасыВЦенахПродажи");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТоварыВЦенахПродажи");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "ПереданныеСчетУчета", "ЗапасыОтгруженные");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОперацииСКомиссионером");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "ПереданныеСчетУчета", "ЗапасыОтгруженные");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтгрузкаБезПереходаПраваСобственности");
	
	//   Выручка (розница в ценах продажи)
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетДоходов", "Выручка");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СчетЗапасов", "СчетУчета");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ОперацииСКомиссионером");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеПроверкиЗаполнения(Правила, "СобственныеЗапасы");
	
	СчетаУчетаВДокументах.ДобавитьУровеньПодчиненности(Правила);
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто",                 "ВидДеятельности");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаНДСПоРеализации", "ВыручкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",  "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетРасходов",             "Расходы");
	
	// Табличная часть ВозвратнаяТара
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ВозвратнаяТара", "СчетУчета", "ВозвратнаяТараНаСкладе");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УчетТарыНаБалансе");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ВозвратнаяТара", "СчетУчета", "ВозвратнаяТараЗаБалансом");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "УчетТарыНаБалансе");
	
	// Табличная часть Услуги
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СчетДоходов", "Выручка");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто",                 "ВидДеятельности");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаНДСПоРеализации", "ВыручкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",  "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетРасходов",             "Расходы");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеПроверкиЗаполнения(Правила, "ТребуетсяСчетРасходовПоОказаниюУслуг");
	
	// Табличная часть Агентские услуги
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "АгентскиеУслуги", "СчетРасчетов", "РасчетыСКомитентом");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Контрагент",         "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента", "ДоговорКонтрагента");
	
	// Данные заполнения
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Дата");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Организация");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Склад");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "ДеятельностьНаПатенте");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "ДоговорКонтрагента");   
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "СпособЗачетаАвансов");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ОтгрузкаБезПереходаПраваСобственности", "ВидОперации");
	
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ВыкупТоваровКомиссионером", "ВидОперации");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ПродажаТоваровСоСкладаКомиссионера", "ВидОперации");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "Оборудование",                          "ВидОперации");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ТребуетсяУчетРасчетов",                 "ВидОперации, ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ТребуетсяУчетАвансов",                  "ВидОперации, ДоговорКонтрагента");
	
КонецПроцедуры

Процедура ДополнитьДанныеЗаполненияСчетовУчета(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("ОтгрузкаБезПереходаПраваСобственности") Тогда
		
		ДанныеЗаполнения.ОтгрузкаБезПереходаПраваСобственности = 
			(ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности);
		
		КонецЕсли;
		
	Если ДанныеЗаполнения.Свойство("ВыкупТоваровКомиссионером") Тогда
		
		ДанныеЗаполнения.ВыкупТоваровКомиссионером = 
			(ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ПродажаТоваровСоСкладаКомиссионера") Тогда
		
		ДанныеЗаполнения.ПродажаТоваровСоСкладаКомиссионера = 
			(ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Оборудование") Тогда
		
		ДанныеЗаполнения.Оборудование = (ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Оборудование);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ТребуетсяУчетРасчетов") Или ДанныеЗаполнения.Свойство("ТребуетсяУчетАвансов") Тогда
		
		ОсобенностиДокумента = ОсобенностиУчетаРасчетов(ДанныеЗаполнения);
			
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ОсобенностиДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Устарела: Следует использовать ЗаполнитьПередЗаписью(), 
// ЗаполнитьПередОтображениемПользователю(), Заполнить(), 
// ЗаполнитьОбъектПриИзменении(), ЗаполнитьРеквизитыПриИзменении(),
// либо перед записью документа устанавливать дополнительное свойство 
// ЗаполнитьСчетаУчетаПередЗаписью
//
// Параметры:
//  Объект		- ДокументОбъект
//  СчетаУчета	- оставлен для совместимости; не используется
Процедура ЗаполнитьСчетаУчетаРасчетов(Объект, СчетаУчета = Неопределено) Экспорт
	
	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаРасчетов(Объект);
	
КонецПроцедуры

// Устарела: Следует использовать ЗаполнитьПередЗаписью(), 
// ЗаполнитьПередОтображениемПользователю(), Заполнить(), 
// ЗаполнитьОбъектПриИзменении(), ЗаполнитьСтроки(),
// либо перед записью документа устанавливать дополнительное свойство 
// ЗаполнитьСчетаУчетаПередЗаписью
//
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, ИмяТабличнойЧасти) Экспорт

	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаВТабличнойЧасти(
		Объект,
		ИмяТабличнойЧасти);

КонецПроцедуры

// Устарела: Следует использовать ЗаполнитьПередЗаписью(), 
// ЗаполнитьПередОтображениемПользователю(), Заполнить(), 
// ЗаполнитьОбъектПриИзменении(), ЗаполнитьСтроки(),
// либо перед записью документа устанавливать дополнительное свойство 
// ЗаполнитьСчетаУчетаПередЗаписью
//
// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  Объект                - ДокументОбъект или соответствующие данные формы
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - оставлен для совместимости; не используется
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(Объект, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СведенияОНоменклатуре = Неопределено) Экспорт

	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(
		Объект,
		СтрокаТабличнойЧасти,
		ИмяТабличнойЧасти);

КонецПроцедуры

#КонецОбласти

Функция РеквизитыЗаСсылками(ВидОперации) Экспорт
	
	РеквизитыЗаСсылками = Новый Соответствие;
	
	// Прячутся за "ПорядокУчетаРасчетов"
	Для Каждого ОписаниеРеквизита Из УчетВзаиморасчетовФормы.РеквизитыДокументаПорядокУчетаРасчетов() Цикл
		РеквизитыЗаСсылками.Вставить(ОписаниеРеквизита.Ключ, "ПорядокУчетаРасчетов");
	КонецЦикла;
	
	// Прячутся за "АналитикаУчета"
	АналитикаУчета = Новый Массив;
	Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги Тогда
		
		АналитикаУчета.Добавить("Услуги.СчетДоходов");
		АналитикаУчета.Добавить("Услуги.Субконто");
		АналитикаУчета.Добавить("Услуги.СчетУчетаНДСПоРеализации");
		АналитикаУчета.Добавить("Услуги.СчетРасходов");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Товары Тогда
		
		АналитикаУчета.Добавить("Товары.СчетУчета");
		АналитикаУчета.Добавить("Товары.СчетДоходов");
		АналитикаУчета.Добавить("Товары.Субконто");
		АналитикаУчета.Добавить("Товары.СчетУчетаНДСПоРеализации");
		АналитикаУчета.Добавить("Товары.СчетРасходов");
		
	КонецЕсли;
	
	Для Каждого ИмяРеквизита Из АналитикаУчета Цикл
		РеквизитыЗаСсылками.Вставить(ИмяРеквизита, "АналитикаУчета");
	КонецЦикла;
	
	Возврат РеквизитыЗаСсылками;
	
КонецФункции

Функция ИспользоватьПростуюФорму(ВидОперации) Экспорт
	
	Возврат
		(ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги)
		Или
		(ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Товары)
		Или
		(ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером)
		Или
		(ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера);
	
КонецФункции

Функция ОсобенностиУчетаРасчетов(ПараметрыДокумента) Экспорт
	
	ОсобенностиДокумента = УчетВзаиморасчетовФормы.НовыйОсобенностиУчетаРасчетовДокумента();
	
	ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеПлатежейОтПокупателей");
	ТолькоУчетАвансов = Ложь;
	
	Если ПараметрыДокумента.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности Тогда
		НачислятьНДСПриОтгрузке = УчетнаяПолитика.НачислятьНДСПоОтгрузке(ПараметрыДокумента.Организация, ПараметрыДокумента.Дата);
		Если НачислятьНДСПриОтгрузке Тогда
			ТолькоУчетАвансов = Истина;
		Иначе
			ОсобенностиДокумента.ТребуетсяУчетРасчетов = Ложь;
		КонецЕсли;
	ИначеЕсли ПараметрыДокумента.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера Тогда
		ОсобенностиДокумента.ТребуетсяУчетРасчетовСПлатежнымАгентом = Истина;
	ИначеЕсли Не ИспользоватьПростуюФорму(ПараметрыДокумента.ВидОперации) И ЗначениеЗаполнено(ПараметрыДокумента.ДоговорКонтрагента) Тогда
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыДокумента.ДоговорКонтрагента, "ВидДоговора");
		Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			ОсобенностиДокумента.ТребуетсяУчетРасчетов = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ТолькоУчетАвансов Тогда
		ОсобенностиДокумента.ТребуетсяУчетРасчетов     = Ложь;
		ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = Ложь;
		
	ИначеЕсли Не ОсобенностиДокумента.ТребуетсяУчетРасчетов Тогда
		ОсобенностиДокумента.ТребуетсяУчетАвансов = Ложь;
		ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = Ложь;
	КонецЕсли;
	
	Возврат ОсобенностиДокумента;
	
КонецФункции

Функция ТекстЗапросаДанныеДляПечатиСчетовФактур(НомераТаблиц, ЕстьПередачаТоваровВСоставеРабот = Ложь) Экспорт
	
	НомераТаблиц.Вставить("ВременнаяТаблицаНаличиеТоваров", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты",                      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ШтрихкодыУпаковок",              НомераТаблиц.Количество());

	Если ЕстьПередачаТоваровВСоставеРабот Тогда
		НомераТаблиц.Вставить("ТаблицаУслуг",                      НомераТаблиц.Количество());
	Иначе
		НомераТаблиц.Вставить("ВТ_РублевыеСуммыДокументовВВалюте", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВТ_ДанныеПоСтранеПроисхождения",    НомераТаблиц.Количество());
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СведенияОПрослеживаемости",      НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ НаличиеТоваров
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.ПодразделениеОрганизации.ЦифровойИндексОбособленногоПодразделения
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|					ТОГДА Реквизиты.Организация.ЦифровойИндексОбособленногоПодразделения
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Поставщик,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация.ИНН
	|		ИНАЧЕ Реквизиты.Организация.ИНН
	|	КОНЕЦ КАК ИННпоставщика,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.ПодразделениеОрганизации
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК ОбособленноеПодразделениеПоставщика,
	|	ВЫБОР
	|		КОГДА Реквизиты.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.Грузоотправитель
	|		КОГДА Реквизиты.ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.ПодразделениеОрганизации
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация
	|		ИНАЧЕ ""он же""
	|	КОНЕЦ КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА Реквизиты.Контрагент.ОбособленноеПодразделение
	|				И Реквизиты.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Покупатель,
	|	Реквизиты.Контрагент.ИНН КАК ИННпокупателя,
	|	ВЫБОР
	|		КОГДА Реквизиты.Грузополучатель.ОбособленноеПодразделение
	|				И Реквизиты.Грузополучатель.ГоловнойКонтрагент = Реквизиты.Контрагент
	|				И Реквизиты.Грузополучатель.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Грузополучатель
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК ОбособленноеПодразделениеПокупателя,
	|	ВЫБОР
	|		КОГДА Реквизиты.Грузополучатель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.Грузополучатель
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Грузополучатель,
	|	Реквизиты.ВалютаДокумента КАК Валюта,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ДоговорКонтрагента.Представление КАК Основание,
	|	Реквизиты.ДоговорКонтрагента.Дата КАК ОснованиеДата,
	|	Реквизиты.ДоговорКонтрагента.Номер КАК ОснованиеНомер,
	|	Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДСПокупателем КАК НДСИсчисляетсяНалоговымАгентом,
	|	Реквизиты.Руководитель КАК Исполнитель,
	|	Реквизиты.ЗаРуководителяНаОсновании КАК ИсполнительНаОсновании,
	|	Реквизиты.ОтпускПроизвел КАК ОтпускПроизвел,
	|	Реквизиты.ДоверенностьНомер КАК ДоверенностьНомер,
	|	Реквизиты.ДоверенностьДата КАК ДоверенностьДата,
	|	Реквизиты.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	Реквизиты.ДоверенностьЧерезКого КАК ДоверенностьЧерезКого,
	|	Реквизиты.ЗаЗаказчикаНаОсновании КАК ЗаЗаказчикаНаОсновании,
	|	ВЫБОР
	|		КОГДА НаличиеТоваров.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьТовары,
	|	Реквизиты.АдресДоставки КАК АдресДоставки,
	|	Реквизиты.СведенияОТранспортировкеИГрузе КАК СведенияОТранспортировкеИГрузе,
	|	Реквизиты.ОтветственныйЗаОформление КАК ОтветственныйЗаОформление,
	|	Реквизиты.СопроводительныеДокументы КАК СопроводительныеДокументы,
	|	Реквизиты.Перевозчик КАК Перевозчик,
	|	Реквизиты.ПеревозкаАвтотранспортом КАК ПеревозкаАвтотранспортом,
	|	Реквизиты.БанковскийСчетОрганизации КАК БанковскийСчетПродавца,
	|	Реквизиты.ЭтапГосконтрактаЕИС КАК ЭтапГосконтрактаЕИС,
	|	Реквизиты.ОснованиеПередачи КАК ОснованиеПередачи,
	|	Реквизиты.РежимЗаполненияОснованияПередачи КАК РежимЗаполненияОснованияПередачи
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаличиеТоваров КАК НаличиеТоваров
	|		ПО Реквизиты.Ссылка = НаличиеТоваров.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ШтрихкодыУпаковок КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДокумента.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &ДокументОснование
	|	И ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL" + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	Если Не ЕстьПередачаТоваровВСоставеРабот Тогда 
		ТекстЗапроса = Текстзапроса 
		+"ВЫБРАТЬ
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК Всего,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДС,
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДС
		|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
		|ИЗ
		|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|ГДЕ
		|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор = &ДокументОснование
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтрокиДокумента,
		|	ТабличнаяЧастьДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслугУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	МАКСИМУМ(РеализацияТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения) КАК СтранаПроисхождения,
		|	МАКСИМУМ(СтраныМира.НаименованиеПолное) КАК НаименованиеПолное
		|ПОМЕСТИТЬ ВТ_ДанныеПоСтранеПроисхождения
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
		|			ПО РеализацияТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения = СтраныМира.Ссылка
		|		ПО РеализацияТоваровУслугУслуги.ИдентификаторСтроки = РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
		|ГДЕ
		|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка = &ДокументОснование
		|	И РеализацияТоваровУслугУслуги.Ссылка = &ДокументОснование
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровУслугУслуги.ИдентификаторСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 КАК НомерТабЧасти,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Номенклатура КАК Товар,
		|	ТаблицаТовары.Номенклатура.Код КАК ТоварКод,
		|	ТаблицаТовары.Номенклатура.Артикул КАК ТоварАртикул,
		|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
		|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаТовары.СтранаПроисхождения.НаименованиеПолное КАК ПредставлениеСтраны,
		|	ТаблицаТовары.НомерГТД КАК НомерГТД,
		|	ТаблицаТовары.НомерГТД.Представление КАК ПредставлениеГТД,
		|	ТаблицаТовары.НомерГТД.РегистрационныйНомер КАК РегистрационныйНомерТД,
		|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.Цена КАК Цена,
		|	ТаблицаТовары.Сумма КАК Сумма,
		|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
		|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ЛОЖЬ КАК ЭтоУслуга,
		|	ТаблицаТовары.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ЕСТЬNULL(ТаблицаТовары.СчетУчета.Забалансовый, ЛОЖЬ) КАК ЭтоКомиссия,
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоРуб,
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСРуб,
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0) КАК СуммаБезНДСРуб,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК КонтрагентСводныйСФ,
		|	НЕОПРЕДЕЛЕНО КАК ПериодичностьУслуги,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
		|			ТОГДА ТаблицаТовары.КодТНВЭД
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
		|	КОНЕЦ КАК ТоварКодТНВЭД,
		|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаТовары.Номенклатура.КодТНВЭД КАК НоменклатураКодТНВЭД,
		|	ТаблицаТовары.ИдентификаторСтрокиГосконтрактаЕИС КАК ИдентификаторСтрокиГосконтрактаЕИС,
		|	ТаблицаТовары.НалоговаяБазаНДС КАК НалоговаяБазаНДС,
		|	ТаблицаТовары.СуммаАкциза КАК СуммаАкциза
		|ПОМЕСТИТЬ ТаблицаДокумента
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|		ПО ТаблицаТовары.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
		|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
		|			И (ТаблицаТовары.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &ДокументОснование
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ТаблицаУслуги.НомерСтроки,
		|	ТаблицаУслуги.Номенклатура,
		|	ТаблицаУслуги.Номенклатура.Код,
		|	ТаблицаУслуги.Номенклатура.КодОКВЭД2,
		|	ТаблицаУслуги.Содержание,
		|	ЕСТЬNULL(ВТ_ДанныеПоСтранеПроисхождения.СтранаПроисхождения, """"),
		|	ЕСТЬNULL(ВТ_ДанныеПоСтранеПроисхождения.НаименованиеПолное, """"),
		|	"""",
		|	"""",
		|	"""",
		|	ТаблицаУслуги.Номенклатура.ЕдиницаИзмерения,
		|	ТаблицаУслуги.Количество,
		|	ТаблицаУслуги.Цена,
		|	ТаблицаУслуги.Сумма,
		|	ТаблицаУслуги.СуммаНДС,
		|	ТаблицаУслуги.СтавкаНДС,
		|	ИСТИНА,
		|	ТаблицаУслуги.Ссылка.СуммаВключаетНДС,
		|	ТаблицаУслуги.Ссылка,
		|	ЛОЖЬ,
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
		|	ТаблицаУслуги.ИдентификаторСтроки,
		|	НЕОПРЕДЕЛЕНО,
		|	ТаблицаУслуги.ИдентификаторСтрокиГосконтрактаЕИС,
		|	0,
		|	0
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК ТаблицаУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|		ПО ТаблицаУслуги.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
		|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
		|			И (ТаблицаУслуги.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеПоСтранеПроисхождения КАК ВТ_ДанныеПоСтранеПроисхождения
		|		ПО ТаблицаУслуги.ИдентификаторСтроки = ВТ_ДанныеПоСтранеПроисхождения.ИдентификаторСтроки
		|ГДЕ
		|	ТаблицаУслуги.Ссылка = &ДокументОснование
		|	И ТаблицаУслуги.Ссылка.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ТаблицаАгентскиеУслуги.НомерСтроки,
		|	ТаблицаАгентскиеУслуги.Номенклатура,
		|	ТаблицаАгентскиеУслуги.Номенклатура.Код,
		|	ТаблицаАгентскиеУслуги.Номенклатура.КодОКВЭД2,
		|	ТаблицаАгентскиеУслуги.Содержание,
		|	"""",
		|	"""",
		|	"""",
		|	"""",
		|	"""",
		|	ТаблицаАгентскиеУслуги.Номенклатура.ЕдиницаИзмерения,
		|	ТаблицаАгентскиеУслуги.Количество,
		|	ТаблицаАгентскиеУслуги.Цена,
		|	ТаблицаАгентскиеУслуги.Сумма,
		|	ТаблицаАгентскиеУслуги.СуммаНДС,
		|	ТаблицаАгентскиеУслуги.СтавкаНДС,
		|	ИСТИНА,
		|	ТаблицаАгентскиеУслуги.Ссылка.СуммаВключаетНДС,
		|	ТаблицаАгентскиеУслуги.Ссылка,
		|	ИСТИНА,
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
		|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
		|	НЕОПРЕДЕЛЕНО,
		|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	0
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК ТаблицаАгентскиеУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
		|		ПО ТаблицаАгентскиеУслуги.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
		|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
		|			И (ТаблицаАгентскиеУслуги.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
		|ГДЕ
		|	ТаблицаАгентскиеУслуги.Ссылка = &ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ.Код КАК НомерТовара,
		|	РеализацияТоваровУслугСведенияПрослеживаемости.Количество КАК КоличествоУчетное,
		|	РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК Количество,
		|	ЕСТЬNULL(КлассификаторТНВЭД.ЕдиницаИзмерения.Наименование, ""шт"") КАК ЕдиницаИзмеренияПрослеживаемостиНаименование,
		|	ЕСТЬNULL(КлассификаторТНВЭД.ЕдиницаИзмерения.Код, ""796"") КАК ЕдиницаИзмеренияПрослеживаемостиКод,
		|	ТаблицаДокумента.Товар КАК Товар,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ТоварКодТНВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
		|			ТОГДА ""--""
		|		ИНАЧЕ ТаблицаДокумента.ТоварКодТНВЭД
		|	КОНЕЦ КАК ТоварКодТНВЭД,
		|	ТаблицаДокумента.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаДокумента.НомерГТД КАК НомерГТД,
		|	ТаблицаДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.СуммаВключаетНДС
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаДокумента.Количество = 0
		|						ТОГДА 0
		|					КОГДА ТаблицаДокумента.СуммаНДС = 0
		|						ТОГДА ТаблицаДокумента.Цена
		|					ИНАЧЕ ВЫРАЗИТЬ((ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаНДС) / ТаблицаДокумента.Количество КАК ЧИСЛО(15, 2))
		|				КОНЕЦ
		|		ИНАЧЕ ТаблицаДокумента.Цена
		|	КОНЕЦ КАК Цена,
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаДокумента.КонтрагентСводныйСФ КАК КонтрагентСводныйСФ,
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.НомерТабЧасти КАК НомерТабЧасти,
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаДокумента.НалоговаяБазаНДС КАК НалоговаяБазаНДС,
		|	ВЫБОР
		|		КОГДА РеализацияТоваровУслугСведенияПрослеживаемости.Сумма = 0
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаДокумента.СуммаВключаетНДС
		|						ТОГДА ВЫРАЗИТЬ(РеализацияТоваровУслугСведенияПрослеживаемости.Количество * (ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаНДС) / ТаблицаДокумента.Количество КАК ЧИСЛО(15, 2))
		|					ИНАЧЕ РеализацияТоваровУслугСведенияПрослеживаемости.Количество * ТаблицаДокумента.Цена
		|				КОНЕЦ
		|		ИНАЧЕ РеализацияТоваровУслугСведенияПрослеживаемости.Сумма
		|	КОНЕЦ КАК СтоимостьБезНалога
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокумента КАК ТаблицаДокумента
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
		|			ПО ТаблицаДокумента.НоменклатураКодТНВЭД = КлассификаторТНВЭД.Ссылка
		|		ПО (ТаблицаДокумента.ИдентификаторСтроки = РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки)
		|			И РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка = ТаблицаДокумента.Ссылка";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "ВЫБРАТЬ
		|	Таблица.НомерСтроки КАК НомерСтроки,
		|	Таблица.Товар КАК Товар,
		|	Таблица.ТоварКод КАК ТоварКод,
		|	Таблица.ТоварНаименование КАК ТоварНаименование,
		|	Таблица.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	Таблица.Количество КАК Количество,
		|	Таблица.Цена КАК Цена,
		|	Таблица.Сумма КАК Сумма,
		|	Таблица.СуммаНДС КАК СуммаНДС,
		|	Таблица.СтавкаНДС КАК СтавкаНДС,
		|	Таблица.СуммаВключаетНДС КАК СуммаВключаетНДС,
		|	&ДокументОснование КАК Ссылка,
		|	Таблица.ТоварКодТНВЭД КАК ТоварКодТНВЭД,
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Таблица.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Таблица.СтранаПроисхождения КАК СтранаПроисхожденияПредставление,
		|	Таблица.РНПТ КАК РНПТ,
		|	Таблица.ВсегоРуб КАК ВсегоРуб,
		|	Таблица.НДСРуб КАК НДСРуб,
		|	Таблица.СуммаБезНДСРуб КАК СуммаБезНДСРуб
		|ПОМЕСТИТЬ ТаблицаОС
		|ИЗ
		|	&ТаблицаДокумента КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 КАК НомерТабЧасти,
		|	НЕОПРЕДЕЛЕНО КАК ТоварАртикул,
		|	"""" КАК НомерГТД,
		|	"""" КАК ПредставлениеГТД,
		|	"""" КАК РегистрационныйНомерТД,
		|	ТаблицаОС.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаОС.Количество КАК Количество,
		|	ТаблицаОС.Цена КАК Цена,
		|	ТаблицаОС.Сумма КАК Сумма,
		|	ТаблицаОС.СуммаНДС КАК СуммаНДС,
		|	ТаблицаОС.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаОС.РНПТ = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоУслуга,
		|	ЛОЖЬ КАК ЭтоКомиссия,
		|	ТаблицаОС.ВсегоРуб КАК ВсегоРуб,
		|	ТаблицаОС.СуммаБезНДСРуб КАК СуммаБезНДСРуб,
		|	ТаблицаОС.НДСРуб КАК НДСРуб,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК КонтрагентСводныйСФ,
		|	НЕОПРЕДЕЛЕНО КАК ПериодичностьУслуги,
		|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаОС.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаОС.СтранаПроисхожденияПредставление КАК СтранаПроисхожденияПредставление,
		|	ТаблицаОС.Товар КАК Товар,
		|	ТаблицаОС.ТоварКод КАК ТоварКод,
		|	ТаблицаОС.ТоварНаименование КАК ТоварНаименование,
		|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОС.Ссылка КАК Ссылка,
		|	КлассификаторТНВЭД.Код КАК ТоварКодТНВЭД,
		|	ТаблицаОС.РНПТ КАК РНПТ,
		|	ТаблицаОС.СуммаВключаетНДС КАК СуммаВключаетНДС,
		|	0 КАК НалоговаяБазаНДС,
		|	ЕСТЬNULL(КлассификаторТНВЭД.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияПрослеживаемостиНаименование,
		|	ЕСТЬNULL(КлассификаторТНВЭД.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияПрослеживаемостиКод,
		|	0 КАК СуммаАкциза
		|ПОМЕСТИТЬ ТаблицаДокумента
		|ИЗ
		|	ТаблицаОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
		|		ПО ТаблицаОС.ТоварКодТНВЭД = КлассификаторТНВЭД.Ссылка
		|{УПОРЯДОЧИТЬ ПО
		|	НомерСтроки}
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ.Код КАК НомерТовара,
		|	РеализацияТоваровУслугСведенияПрослеживаемости.Количество КАК КоличествоУчетное,
		|	РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК Количество,
		|	ТаблицаДокумента.ЕдиницаИзмеренияПрослеживаемостиНаименование КАК ЕдиницаИзмеренияПрослеживаемостиНаименование,
		|	ТаблицаДокумента.ЕдиницаИзмеренияПрослеживаемостиКод КАК ЕдиницаИзмеренияПрослеживаемостиКод,
		|	ТаблицаДокумента.НомерГТД КАК НомерГТД,
		|	ТаблицаДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаДокумента.КонтрагентСводныйСФ КАК КонтрагентСводныйСФ,
		|	ТаблицаДокумента.НомерТабЧасти КАК НомерТабЧасти,
		|	ТаблицаДокумента.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.ТоварКодТНВЭД КАК ТоварКодТНВЭД,
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Товар КАК Товар,
		|	ТаблицаДокумента.Цена КАК Цена,
		|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаДокумента.Сумма КАК СтоимостьБезНалога
		|ИЗ
		|	ТаблицаДокумента КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
		|		ПО ТаблицаДокумента.Ссылка = РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка
		|			И ТаблицаДокумента.ИдентификаторСтроки = РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
		|			И ТаблицаДокумента.Товар = РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура
		|			И ТаблицаДокумента.СтранаПроисхождения = РеализацияТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения
		|			И ТаблицаДокумента.РНПТ = РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

// Возвращает текст запроса для отбора документов к печати УПД по релевантной форме.
// Добавлена во все документы, для которых доступна печать УПД.
// Параметры:
// ВерсияПостановленияНДС1137 - см.УчетНДСПереопределяемый.ВерсияПостановленияНДС1137()
// ЕстьПрослеживаемыеТовары - Булево - признак того, что в документе есть прослеживаемый товар.
//
// Возвращаемое значение: 
// ТекстЗапроса - строка с необходимым текстом запроса.
//
Функция ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов(ВерсияПостановленияНДС1137 = Неопределено, ЕстьПрослеживаемыеТовары = Ложь) Экспорт
	
	ОтбиратьВсеСФ1137 = Ложь;
	Если ВерсияПостановленияНДС1137 = Неопределено Тогда
		ОтбиратьВсеСФ1137 = Истина;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.СчетФактураВыданный.ПустаяСсылка) КАК СчетФактура,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА СОКРЛП(РеализацияТоваровУслуг.НомерВходящегоДокумента) <> """"
	|			ТОГДА СОКРЛП(РеализацияТоваровУслуг.НомерВходящегоДокумента)
	|		ИНАЧЕ РеализацияТоваровУслуг.Номер
	|	КОНЕЦ КАК Номер,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаФактуры,
	|	РеализацияТоваровУслуг.Руководитель КАК Руководитель,
	|	РеализацияТоваровУслуг.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	ИСТИНА КАК СчетФактураБезНДС,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ИСТИНА КАК УдалитьПрефиксыИзНомера,
	|	ИСТИНА КАК ЭтоСчетФактураВыданный,
	|	ИСТИНА КАК ВыводитьСуммуБезНДС,
	|	РеализацияТоваровУслуг.Ссылка КАК ДокументОснование,
	|	1 КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ВидДоговора,ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ПустаяСсылка)) КАК ВидДоговора,
	|	ЕСТЬNULL(ГосКонтракты.Код, """") КАК ИдентификаторГосКонтракта,
	|	"""" КАК КППСчетаФактуры,
	|	"""" КАК КПППродавца,
	|	РеализацияТоваровУслуг.Дата КАК ДатаСведений
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ПЕРВЫЕ 1
	|			РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка КАК Ссылка,
	|			ИСТИНА КАК ЕстьПрослеживаемыеТовары
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ГДЕ
	|			РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка В(&МассивОбъектов)) КАК ПрослеживаемыеТовары
	|		ПО РеализацияТоваровУслуг.Ссылка = ПрослеживаемыеТовары.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГосКонтракты КАК ГосКонтракты
	|			ПО ДоговорыКонтрагентов.ГосударственныйКонтракт = ГосКонтракты.Ссылка
	|		ПО РеализацияТоваровУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	И РеализацияТоваровУслуг.Дата > &УсловиеПоДате
	|	И &ЕстьПрослеживаемыеТовары";
	
	Если ОтбиратьВсеСФ1137 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			">= &НачалоПримененияПостановления1137");
	ИначеЕсли ВерсияПостановленияНДС1137 = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"< &НачалоПримененияПостановления1137");
	ИначеЕсли ВерсияПостановленияНДС1137 >= 1 И ВерсияПостановленияНДС1137 <= 3 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ &НачалоПримененияПостановления1137 И ДАТАВРЕМЯ(2017,09,30,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 4 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ ДАТАВРЕМЯ(2017,10,1) И ДАТАВРЕМЯ(2021,06,30,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 5 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ ДАТАВРЕМЯ(2021,7,1) И ДАТАВРЕМЯ(2023,09,30,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 6 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ ДАТАВРЕМЯ(2023,10,1) И ДАТАВРЕМЯ(2024,09,30,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 7 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ ДАТАВРЕМЯ(2024,10,1) И ДАТАВРЕМЯ(2025,12,31,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 8 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			">= ДАТАВРЕМЯ(2026,1,1)");
	КонецЕсли;
	
	Если ОтбиратьВсеСФ1137 Тогда
		// Не накладываем отбор.
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕстьПрослеживаемыеТовары", "ИСТИНА");
	ИначеЕсли ЕстьПрослеживаемыеТовары Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕстьПрослеживаемыеТовары", "ЕСТЬNULL(ПрослеживаемыеТовары.ЕстьПрослеживаемыеТовары, ЛОЖЬ) = ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕстьПрослеживаемыеТовары", "ЕСТЬNULL(ПрослеживаемыеТовары.ЕстьПрослеживаемыеТовары, ЛОЖЬ) = ЛОЖЬ");
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаДанныеДляОбновленияЦенДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугВозвратнаяТара.Номенклатура,
	|	РеализацияТоваровУслугВозвратнаяТара.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	ИСТИНА КАК ЦенаВключаетНДС
	|ПОМЕСТИТЬ ВозвратнаяТара
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВозвратнаяТара КАК РеализацияТоваровУслугВозвратнаяТара
	|ГДЕ
	|	РеализацияТоваровУслугВозвратнаяТара.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугТовары.Цена КАК Цена,
	|	&Валюта КАК Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|	И НЕ РеализацияТоваровУслугТовары.Номенклатура В
	|				(ВЫБРАТЬ
	|					ВозвратнаяТара.Номенклатура
	|				ИЗ
	|					ВозвратнаяТара КАК ВозвратнаяТара)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугУслуги.Номенклатура,
	|	РеализацияТоваровУслугУслуги.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|ГДЕ
	|	РеализацияТоваровУслугУслуги.Ссылка = &Ссылка
	|	И НЕ РеализацияТоваровУслугУслуги.Номенклатура В
	|				(ВЫБРАТЬ
	|					ВозвратнаяТара.Номенклатура
	|				ИЗ
	|					ВозвратнаяТара КАК ВозвратнаяТара)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВозвратнаяТара.Номенклатура,
	|	ВозвратнаяТара.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	ВозвратнаяТара.ЦенаВключаетНДС
	|ИЗ
	|	ВозвратнаяТара КАК ВозвратнаяТара
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугАгентскиеУслуги.Номенклатура,
	|	РеализацияТоваровУслугАгентскиеУслуги.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслугАгентскиеУслуги
	|ГДЕ
	|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка = &Ссылка
	|	И НЕ РеализацияТоваровУслугАгентскиеУслуги.Номенклатура В
	|				(ВЫБРАТЬ
	|					ВозвратнаяТара.Номенклатура
	|				ИЗ
	|					ВозвратнаяТара КАК ВозвратнаяТара)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Цена,
	|	Валюта";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	МассивРеквизитов = Новый Массив();
	МассивРеквизитов.Добавить("УдалитьУчитыватьНДС");
	
	Возврат МассивРеквизитов;
	
КонецФункции

Функция РекомендуемыйВидОперации(Объект) Экспорт
	
	Если НЕ (Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаКомиссия
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Товары
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорКонтрагента, "ВидДоговора");
		
		Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			Возврат Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.ВозвратнаяТара.Количество() > 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Объект.Товары.Количество() > 0 
		И Объект.Услуги.Количество() = 0 
		И Объект.АгентскиеУслуги.Количество() = 0 Тогда
		
		Возврат Перечисления.ВидыОперацийРеализацияТоваров.Товары;
		
	ИначеЕсли Объект.Товары.Количество() = 0
		И Объект.Услуги.Количество() > 0 
		И Объект.АгентскиеУслуги.Количество() = 0 Тогда
		
		Возврат Перечисления.ВидыОперацийРеализацияТоваров.Услуги;
	
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	ПолеТовары = "Таб.Товары.Ссылка";
	Результат.Вставить("ЕстьТовары",          ПолеТовары);
	Результат.Вставить("ЕстьУслуги",          СтрЗаменить(ПолеТовары, "Товары", "Услуги"));
	Результат.Вставить("ЕстьАгентскиеУслуги", СтрЗаменить(ПолеТовары, "Товары", "АгентскиеУслуги"));
	
	Возврат Результат;
	
КонецФункции

#Область ЧекиНПД

Функция ТаблицаУслугПродукцииНПД(Документ) Экспорт
	
	СчетаГотоваяПродукция =  БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("СчетаГотоваяПродукция", СчетаГотоваяПродукция);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаУслуги.Номенклатура КАК УслугаНПД,
	|	ТаблицаУслуги.Сумма КАК Сумма,
	|	ТаблицаУслуги.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаУслуги.СуммаНДС КАК СуммаНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК ТаблицаУслуги
	|ГДЕ
	|	ТаблицаУслуги.Ссылка = &Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Документ
	|	И ТаблицаТовары.СчетУчета В(&СчетаГотоваяПродукция)";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

// Возвращаяет массив документов, для которых выписка счетов-фактур не требуется
//
Функция СчетаФактурыНеТребуются(МассивДокументов) Экспорт
	
	ДокументыСчетФактураНеТребуются = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(МассивДокументов) Тогда
		Возврат ДокументыСчетФактураНеТребуются
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	ИСТИНА КАК ЕстьНДС
	|ПОМЕСТИТЬ ВТ_ДокументыСНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И (РеализацияТоваровУслуг.СуммаНДС > 0
	|			ИЛИ РеализацияТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслуг.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И (РеализацияТоваровУслуг.СуммаНДС > 0
	|			ИЛИ РеализацияТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслуг.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И (РеализацияТоваровУслуг.СуммаНДС > 0
	|			ИЛИ РеализацияТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	ЕСТЬNULL(ВТ_ДокументыСНДС.ЕстьНДС, ЛОЖЬ) КАК ЕстьНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыСНДС КАК ВТ_ДокументыСНДС
	|		ПО (ВТ_ДокументыСНДС.Ссылка = РеализацияТоваровУслуг.Ссылка)
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности)
	|	И НЕ РеализацияТоваровУслуг.ДоговорКонтрагента.УчетАгентскогоНДСПокупателем
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И РеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	ЕСТЬNULL(ВТ_ДокументыСНДС.ЕстьНДС, ЛОЖЬ) КАК ЕстьНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыСНДС КАК ВТ_ДокументыСНДС
	|		ПО (ВТ_ДокументыСНДС.Ссылка = РеализацияТоваровУслуг.Ссылка)
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И РеализацияТоваровУслуг.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности)
	|	И РеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером)
	|	И НЕ РеализацияТоваровУслуг.ДоговорКонтрагента.УчетАгентскогоНДСПокупателем";
	
	Результат = Запрос.ВыполнитьПакет();
	
	// Отгрузки без перехода права собственности
	ВыборкаБезПереходаПраваСобственности = Результат[1].Выбрать();
	Пока ВыборкаБезПереходаПраваСобственности.Следующий() Цикл
		
		Если НЕ УчетнаяПолитика.НачислятьНДСПоОтгрузке(ВыборкаБезПереходаПраваСобственности.Организация, ВыборкаБезПереходаПраваСобственности.Дата) Тогда
			ДокументыСчетФактураНеТребуются.Добавить(ВыборкаБезПереходаПраваСобственности.Ссылка);
		ИначеЕсли НЕ ВыборкаБезПереходаПраваСобственности.ЕстьНДС
			И (НЕ УчетнаяПолитика.ПлательщикНДС(ВыборкаБезПереходаПраваСобственности.Организация, ВыборкаБезПереходаПраваСобственности.Дата) 
			ИЛИ УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(ВыборкаБезПереходаПраваСобственности.Дата) >= 3 
			И Не УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(ВыборкаБезПереходаПраваСобственности.Организация, ВыборкаБезПереходаПраваСобственности.Дата)) Тогда
			ДокументыСчетФактураНеТребуются.Добавить(ВыборкаБезПереходаПраваСобственности.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	// Отгрузки по договорам комиссии
	ВыборкаКомиссия = Результат[2].Выбрать();
	Пока ВыборкаКомиссия.Следующий() Цикл
		ДокументыСчетФактураНеТребуются.Добавить(ВыборкаКомиссия.Ссылка);
	КонецЦикла;
	
	// Прочие реализации
	ВыборкаРеализация = Результат[3].Выбрать();
	Пока ВыборкаРеализация.Следующий() Цикл
		
		Если НЕ ВыборкаРеализация.ЕстьНДС
			И (НЕ УчетнаяПолитика.ПлательщикНДС(ВыборкаРеализация.Организация, ВыборкаРеализация.Дата) 
			ИЛИ УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(ВыборкаРеализация.Дата) >= 3 
			И НЕ УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(ВыборкаРеализация.Организация, ВыборкаРеализация.Дата)) Тогда
			ДокументыСчетФактураНеТребуются.Добавить(ВыборкаРеализация.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДокументыСчетФактураНеТребуются;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ИдентификаторОтслеживатьОригинал = УчетОригиналовПервичныхДокументовБП.ИдентификаторОтслеживатьОригинал();
	
	// Товарная накладная (ТОРГ-12)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ12_БезУслуг";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаПечати.Порядок = 10;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЕстьПодакцизныеТовары",    Ложь);
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	// Товарная накладная (ТОРГ-12) с услугами
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ12";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12) с услугами'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая";
	КомандаПечати.Порядок = 20;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЕстьПодакцизныеТовары",    Ложь);
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	// Акт об оказании услуг
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаУслуги";
	КомандаПечати.Порядок = 30;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	// Акт на передачу прав
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктНаПередачуПрав";
	КомандаПечати.Представление = НСтр("ru = 'Акт на передачу прав'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары,ФормаДокументаУслуги";
	КомандаПечати.ФункциональныеОпции = "ИспользоватьПередачуПрав";
	КомандаПечати.Порядок = 40;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	// Акт о приемке выполненных строительных работ
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОПриемкеВыполненныхСтроительныхРабот";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приемке выполненных строительных работ'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаСпискаСтроительныхРабот,ФормаДокументаСтроительныеРаботы";
	КомандаПечати.Порядок = 45;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ВидОперации",
		Перечисления.ВидыОперацийРеализацияТоваров.СтроительныеРаботы, ВидСравнения.Равно);
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	Если ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураВыданный) Тогда
		// Счет-фактура
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СчетФактура";
		КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
		КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиСчетовФактур";
		КомандаПечати.ФункциональныеОпции = "ИспользуетсяОСНО,ИспользуетсяНДФЛИП,ОсуществляетсяЗакупкаТоваровУслугДляКомитентов,ОсуществляетсяРеализацияТоваровУслугКомитентов,ВыписыватьСчетаФактурыСпецРежимы,УплачиватьНДССпецРежимы,ВедетсяУчетИмпортныхТоваров";
		КомандаПечати.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте","СчетФактураКомплект");
		КомандаПечати.Порядок = 50;
		УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	КонецЕсли;
	
	// Универсальный передаточный документ
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "УниверсальныйПередаточныйДокумент";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)'");
	КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиУниверсальныхПередаточныхДокументов";
	КомандаПечати.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте","УниверсальныйПередаточныйДокументКомплект");
	КомандаПечати.Порядок = 60;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ВидОперации",
		Перечисления.ВидыОперацийРеализацияТоваров.СтроительныеРаботы, ВидСравнения.НеРавно);
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	// Транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати  = "Обработка.ПечатьТранспортнойНакладной";
	КомандаПечати.Идентификатор   = "ТранспортнаяНакладная";
	КомандаПечати.Представление   = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.ФункциональныеОпции = "ИспользоватьДоставкуАвтотранспортом";
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиТранспортнойНакладной";
	КомандаПечати.СписокФорм    = "ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаПечати.Порядок = 70;
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	// Транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьТранспортнойНакладной";
	КомандаПечати.Идентификатор = "ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.ФункциональныеОпции = "ИспользоватьДоставкуАвтотранспортом";
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора";
	КомандаПечати.Порядок = 71;
	
	// Товарно-транспортная накладная (1-Т)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьТТН";
	КомандаПечати.Идентификатор = "ТТН";
	КомандаПечати.Представление = НСтр("ru = 'Товарно-транспортная накладная (1-Т)'");
	КомандаПечати.ФункциональныеОпции = "ИспользоватьДоставкуАвтотранспортом";
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";	
	КомандаПечати.Порядок = 80;
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	// Накладная на отпуск материалов на сторону (М-15)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М15";
	КомандаПечати.Представление = НСтр("ru = 'Накладная на отпуск материалов на сторону (М-15)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаПечати.Порядок = 90;
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	// Расходная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Расходная накладная'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаПечати.Порядок = 100;
	КомандаПечати.ДополнительныеПараметры.Вставить(ИдентификаторОтслеживатьОригинал, Истина);
	
	Если ПравоДоступа("Чтение", Метаданные.Документы.СчетНаОплатуПокупателю) Тогда
		// Счет на оплату
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор  = "СчетЗаказ";
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьСчетаНаОплату";
		КомандаПечати.Представление  = ПечатьТорговыхДокументовКлиентСервер.ПредставлениеПечатнойФормыСчетаПокупателю();
		КомандаПечати.СписокФорм     = "ФормаСписка,ФормаВыбора";
		КомандаПечати.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте","СчетЗаказКомплект");
		КомандаПечати.ДополнительныеПараметры.Вставить("СокращенноеСообщениеОбОшибке", Ложь);
		КомандаПечати.Порядок        = 110;
		// Счет на оплату
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор  = "СчетЗаказ";
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьСчетаНаОплату";
		КомандаПечати.Представление  = ПечатьТорговыхДокументовКлиентСервер.ПредставлениеПечатнойФормыСчетаПокупателю();
		КомандаПечати.СписокФорм     = "ФормаДокументаОбщая,ФормаДокументаТовары,ФормаДокументаУслуги";
		КомандаПечати.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте","СчетЗаказКомплект");
		КомандаПечати.ДополнительныеПараметры.Вставить("СокращенноеСообщениеОбОшибке", Истина);
		КомандаПечати.Порядок        = 110;
	КонецЕсли;
	
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьРублевыхСуммДокументовВВалюте) Тогда
		// Справка-расчет "Рублевые суммы документа в валюте"
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьРублевыхСуммДокументовВВалюте";
		КомандаПечати.Идентификатор = "РублевыеСуммыДокументаВВалюте";
		КомандаПечати.Представление = НСтр("ru = 'Справка-расчет ""Рублевые суммы документа в валюте""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
		КомандаПечати.ФункциональныеОпции = "ИспользоватьВалютныйУчет";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
		КомандаПечати.Порядок = 130;
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.Контрагенты) Тогда
		// Печать конвертов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Конверт";
		КомандаПечати.Представление = НСтр("ru = 'Конверт'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиКонверта";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте", Истина);
		КомандаПечати.Порядок       = 140;
	КонецЕсли;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ПечатьКомплектаДокументов";
	КомандаПечати.Идентификатор = "КомплектДокументов";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары,ФормаДокументаУслуги";
	КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
	КомандаПечати.Порядок = 150;
	
	// Принятие расходов УСН
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "РасходыУСН";
	КомандаПечати.Представление = НСтр("ru = 'Признание расходов УСН'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРасходовУСН";
	КомандаПечати.СписокФорм    = "ФормаДокументаОбщая,ФормаДокументаТовары,ФормаДокументаУслуги,ФормаДокументаСтроительныеРаботы";
	КомандаПечати.ФункциональныеОпции = "ПрименяетсяУСНДоходыМинусРасходы";
	КомандаПечати.Порядок       = 160;
	УчетУСН.ДобавитьУсловиеВидимостиПечатиРасходыУСН(КомандаПечати);
	
	Если ПравоДоступа("Использование", Метаданные.Отчеты.РеестрДокументов) Тогда
		// Реестр документов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Реестр";
		КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Реализация (акт, накладная, УПД)""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
		КомандаПечати.СписокФорм    = "ФормаСписка";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
		КомандаПечати.Порядок       = 170;
	КонецЕсли;
	
КонецПроцедуры

// Возвращаяет массив документов с прослеживаемыми товарами.
//
// Параметры:
// МассивДокументов - массив проверяемых документов.
//
// Возвращаемое значение:
// ДокументыСПрослеживаемымиТоварами - массив документов с прослеживаемыми товарами.
//
Функция ДокументыСПрослеживаемымиТоварами(МассивДокументов) Экспорт
	
	ДокументыСПрослеживаемымиТоварами = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(МассивДокументов) Тогда
		Возврат ДокументыСПрослеживаемымиТоварами
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка В(&МассивДокументов)
	|	И РеализацияТоваровУслугТовары.ПрослеживаемыйТовар = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугУслуги.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ПО РеализацияТоваровУслугУслуги.Ссылка = РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслугУслуги.Ссылка В(&МассивДокументов)
	|	И РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)";
	
	ДокументыСПрослеживаемымиТоварами = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат ДокументыСПрослеживаемымиТоварами;
	
КонецФункции

Функция СоздатьДокументПоОтчетуЯндекса(АдресХранилища, ПараметрыДокумента, Период) Экспорт
	
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресХранилища);
		
	Если НЕ ТипЗнч(ДанныеФайла) = Тип("Массив") Тогда
	  	Возврат Неопределено;
	КонецЕсли;
	
	ДанныеДокумента = Неопределено;
	Для Каждого СтраницаОтчета Из ДанныеФайла Цикл
		Если СтраницаОтчета.ВидДокумента = "ОтчетЯндекс_Отгружено"
			И СтраницаОтчета.НомерДокумента = ПараметрыДокумента.НомерДокумента Тогда
			ДанныеДокумента = СтраницаОтчета.ДанныеДокумента;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеДокумента = Неопределено Тогда
	  	Возврат Неопределено;
	КонецЕсли;
	   
	Если ПараметрыДокумента.Свойство("Ссылка")
		И ЗначениеЗаполнено(ПараметрыДокумента.Ссылка) Тогда
		НовыйДокумент = ПараметрыДокумента.Ссылка.ПолучитьОбъект();
		НовыйДокумент.ПометкаУдаления = Ложь;
		НовыйДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		НовыйДокумент.Товары.Очистить();
		НовыйДокумент.СуммаДокумента = 0;
		
	Иначе
		НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		НовыйДокумент.Заполнить(Неопределено);
		НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности;
		Новыйдокумент.СуммаВключаетНДС = Истина;
		Если ПараметрыДокумента.Свойство("Ссылка") Тогда
			ЗаполнитьЗначенияСвойств(Новыйдокумент, ПараметрыДокумента,, "Контрагент, ДоговорКонтрагента, Ссылка");
		Иначе
			ЗаполнитьЗначенияСвойств(Новыйдокумент, ПараметрыДокумента,, "Контрагент, ДоговорКонтрагента");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ДанныеДокумента, "Контрагент, ДоговорКонтрагента");
	КонецЕсли;
	
	НовыйДокумент.НомерВходящегоДокумента = ДанныеДокумента.НомерДокумента;
	Если ЗначениеЗаполнено(ДанныеДокумента.ДатаДокумента) Тогда
		НовыйДокумент.Дата = ДанныеДокумента.ДатаДокумента;
	Иначе
		НовыйДокумент.Дата = Период;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НовыйДокумент.ДоговорКонтрагента)
		И ЗначениеЗаполнено(НовыйДокумент.Организация)
		И ЗначениеЗаполнено(НовыйДокумент.Контрагент) Тогда
		
		МассивВидовДоговоров = Новый Массив;
		МассивВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
		
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(НовыйДокумент.ДоговорКонтрагента,
			НовыйДокумент.Контрагент, НовыйДокумент.Организация, МассивВидовДоговоров);
		
	КонецЕсли;
	
	НезаполненнаяНоменклатура = Новый Массив;
	СпособЗагрузки = ОбменСМаркетплейс.СпособЗагрузки(ДанныеДокумента);
	ЭтоАвтозагрузка = ОбменСМаркетплейс.ЭтоАвтозагрузка(СпособЗагрузки);
	
	ЗаполнитьСчетаУчетаРасчетов(НовыйДокумент);
	Если ДанныеДокумента.Свойство("ТаблицаТоваров") Тогда
		
		МассивПоискаНоменклатуры  = Новый Массив;
		СопоставлениеНоменклатуры = Новый Соответствие;
		
		ТаблицаТоваров = ДанныеДокумента.ТаблицаТоваров;
		
		Обработки.ЗагрузкаОтчетаЯндексМаркет.ДобавитьКолонкуИД(ТаблицаТоваров);
		
		Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
			СтруктураПоискаНоменклатуры = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(ПараметрыДокумента.Контрагент, СтрокаТовара.ИД);
			СтруктураПоискаНоменклатуры.ИсторияИдентификаторов = СтрокаТовара.ИсторияИдентификаторов;
			МассивПоискаНоменклатуры.Добавить(СтруктураПоискаНоменклатуры);
		КонецЦикла;
		
		МассивНайденнойНоменклатуры   = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Новый Структура("НоменклатураКонтрагента", МассивПоискаНоменклатуры), Ложь);
		
		Для Каждого СтрокаНоменклатуры Из МассивНайденнойНоменклатуры Цикл
			Идентификатор = СтрокаНоменклатуры.НоменклатураКонтрагента.Идентификатор;
			Номенклатура  = СтрокаНоменклатуры.НоменклатураИБ.Номенклатура;
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				СопоставлениеНоменклатуры.Вставить(Идентификатор, Номенклатура);
			КонецЕсли;
		КонецЦикла;
		
		Если ТаблицаТоваров.Количество() = 0 Тогда
			//ВызватьИсключение НСтр("ru = 'В строках отчета отгрузок'");
			Возврат Неопределено;
		КонецЕсли;
		
		Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
			Если НЕ НачалоМесяца(СтрокаТовара.ДатаПередачи) = НачалоМесяца(Период) Тогда
				Продолжить;
			Конецесли;

			Номенклатура = СопоставлениеНоменклатуры.Получить(СтрокаТовара.ИД);
			
			СтрокаДокумента = НовыйДокумент.Товары.Добавить();
			СтрокаДокумента.Номенклатура = Номенклатура;
			СтрокаДокумента.Количество   = СтрокаТовара.Количество;
			СтрокаДокумента.Сумма        = СтрокаТовара.Сумма;
			СтрокаДокумента.СтавкаНДС    = УчетНДС.СтавкаНДСПоПредставлению(СтрокаТовара.СтавкаНДС);
			СтрокаДокумента.НомерЗаказа  = СтрокаТовара.НомерЗаказа;
			
			Если Не ЗначениеЗаполнено(Номенклатура) И ЭтоАвтозагрузка Тогда
				ОбменСМаркетплейс.ДобавитьНезаполненнуюНоменклатуру(
					НезаполненнаяНоменклатура, СтрокаТовара, "Товары", НовыйДокумент.Товары.Индекс(СтрокаДокумента));
			КонецЕсли;
			
		КонецЦикла;
		
		ДанныеОбъекта = Новый Структура("Дата, Организация, Реализация, ДокументБезНДС, ТипЦен, СуммаВключаетНДС, ВедетсяУчетПрослеживаемыхТоваров");
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, НовыйДокумент);
		ДанныеОбъекта.Реализация = Истина;
		ДанныеОбъекта.ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
			И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(НовыйДокумент.Дата);		
		
		Если НовыйДокумент.Товары.Количество() > 0 Тогда
			ЗаполнитьСтрокиТЧЗагрузкаИзФайла(НовыйДокумент.Товары, "Товары", ДанныеОбъекта);
			СчетаУчетаВДокументах.ЗаполнитьСтроки(НовыйДокумент.Товары, "Товары", НовыйДокумент, Документы.РеализацияТоваровУслуг);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(НовыйДокумент);
	
	Попытка
		НовыйДокумент.Записать();
		
		Если ЗначениеЗаполнено(ДанныеДокумента.НомерСФ)
			И УчетнаяПолитика.НачислятьНДСПоОтгрузке(НовыйДокумент.Организация, НовыйДокумент.Дата) Тогда
			ПараметрыСоздания = УчетНДСКлиентСервер.НовыеПараметрыСозданияВыданногоСчетаФактуры();
			ПараметрыСоздания.Основание = НовыйДокумент.Ссылка;
			РеквизитыСФ = УчетНДСВызовСервера.СоздатьСчетФактуруВыданныйНаОсновании(ПараметрыСоздания);
			
			Если РеквизитыСФ <> Неопределено И ЗначениеЗаполнено(РеквизитыСФ.СчетФактура) Тогда
				СчетФактураОбъект = РеквизитыСФ.СчетФактура.ПолучитьОбъект();
				СчетФактураОбъект.ОтредактированНомерСчетаФактуры = Истина;
				СчетФактураОбъект.ПредставлениеНомера = ДанныеДокумента.НомерСФ;
				СчетФактураОбъект.НомерВходящегоДокумента = ДанныеДокумента.НомерСФ;
				СчетФактураОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	ОбменСМаркетплейс.СохранитьДанныеМаркетплейсаВПрисоединенномФайле(
		НовыйДокумент.Ссылка, ДанныеДокумента.ПервичныеДанныеМаркетплейса, СпособЗагрузки);
	
	ОбменСМаркетплейс.СохранитьНесопоставленнуюНоменклатуруЯндекс(
		НезаполненнаяНоменклатура, НовыйДокумент.Ссылка, ДанныеФайла, ПараметрыДокумента);
	
	Возврат НовыйДокумент.Ссылка;
	
КонецФункции

Функция ЭтоВыполнениеСтроительныхРабот(Документ) Экспорт
	
	Если ТипЗнч(Документ) <> Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат (ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ВидОперации")
		= Перечисления.ВидыОперацийРеализацияТоваров.СтроительныеРаботы);
	
КонецФункции

Функция ОпределитьПодписантов(СписокДокументов) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылки", СписокДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Руководитель КАК Руководитель,
	|	РеализацияТоваровУслуг.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	РеализацияТоваровУслуг.ОтпускПроизвел КАК Кладовщик,
	|	РеализацияТоваровУслуг.Склад КАК Склад,
	|	РеализацияТоваровУслуг.ОтветственныйЗаОформление КАК ОтветственныйЗаОформление,
	|	РеализацияТоваровУслуг.Дата КАК Дата
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&Ссылки)";
	
	ПодписантыДокументов = Новый Соответствие;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПодписиДокумента = Новый Структура;
		
		Если ЗначениеЗаполнено(Выборка.Руководитель) Тогда
			ПодписиДокумента.Вставить("Руководитель", Выборка.Руководитель);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ГлавныйБухгалтер) Тогда
			ПодписиДокумента.Вставить("ГлавныйБухгалтер", Выборка.ГлавныйБухгалтер);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.Кладовщик) Тогда
			ПодписиДокумента.Вставить("Кладовщик", Выборка.Кладовщик);
		ИначеЕсли ЗначениеЗаполнено(Выборка.Склад) Тогда
			ОтветственноеЛицоСклада = ОтветственныеЛицаБП.ОтветственноеЛицоНаСкладе(Выборка.Склад, Выборка.Дата);
			Если ЗначениеЗаполнено(ОтветственноеЛицоСклада) Тогда
				ПодписиДокумента.Вставить("Кладовщик", ОтветственноеЛицоСклада);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ОтветственныйЗаОформление) Тогда
			ПодписиДокумента.Вставить("ОтветственныйЗаОформление", Выборка.ОтветственныйЗаОформление);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПодписиДокумента) Тогда
			ПодписантыДокументов.Вставить(Выборка.Ссылка, ПодписиДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПодписантыДокументов;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает документ "Реализация отгруженных товаров", которым учтен переход права собственности
// по ранее отгруженным товарам.
// Параметры:
// ДокументОтгрузки - ДокуменСсылка.РеализацияТоваровУслуг - документ, которым товары отгружены 
// без перехода права собственности
// 
// Возвращаемое значение:
// ДокументСсылка.РеализацияОтгруженныхТоваров - документ, которым учтен переход права собственности
// по документу отгрузки.
Функция РеализацияПоОтгрузке(ДокументОтгрузки) Экспорт
	
	ВыполнитьПроверкуПравДоступа("Чтение", Метаданные.Документы.РеализацияОтгруженныхТоваров);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОтгрузки", ДокументОтгрузки);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеализацияОтгруженныхТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
	|ГДЕ
	|	РеализацияОтгруженныхТоваров.ДокументОтгрузки = &ДокументОтгрузки
	|	И РеализацияОтгруженныхТоваров.Проведен
	|	И НЕ РеализацияОтгруженныхТоваров.ЧастичнаяПродажа";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ТоварыКВозвратуПоРеализации(ДокументРеализация, ДатаСреза) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсходныйДокументРеализации", ДокументРеализация);
	Запрос.УстановитьПараметр("ДатаСреза",                  ДатаСреза);
	// Текущий документ в выборку не включаем
	Запрос.УстановитьПараметр("ДокументИсключение",        ?(ТипЗНЧ(ДатаСреза) = Тип("МоментВремени"),
			ДатаСреза.Ссылка, Неопределено));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка КАК Ссылка,
	|	ВозвратТоваровОтПокупателя.Дата КАК Дата,
	|	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура,
	|	ВозвратТоваровОтПокупателяТовары.Цена КАК Цена,
	|	ВозвратТоваровОтПокупателяТовары.КоличествоПослеИзменения КАК Количество,
	|	ВозвратТоваровОтПокупателяТовары.СуммаПослеИзменения КАК Сумма,
	|	ВозвратТоваровОтПокупателяТовары.СуммаНДСПослеИзменения КАК СуммаНДС,
	|	ВозвратТоваровОтПокупателяТовары.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ ВТ_ВозвратыПоДокументуРеализации
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	|		ПО ВозвратТоваровОтПокупателя.Ссылка = ВозвратТоваровОтПокупателяТовары.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО (РеализацияТоваровУслугТовары.Номенклатура = ВозвратТоваровОтПокупателяТовары.Номенклатура)
	|			И (РеализацияТоваровУслугТовары.Цена = ВозвратТоваровОтПокупателяТовары.Цена)
	|			И (РеализацияТоваровУслугТовары.НомерГТД = ВозвратТоваровОтПокупателяТовары.НомерГТД)
	|			И (РеализацияТоваровУслугТовары.Ссылка = ВозвратТоваровОтПокупателяТовары.Ссылка.Сделка)
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Сделка = &ИсходныйДокументРеализации
	|	И ВозвратТоваровОтПокупателя.МоментВремени < &ДатаСреза
	|	И НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления
	|	И ВозвратТоваровОтПокупателяТовары.ИсправляемыйДокумент <> НЕОПРЕДЕЛЕНО
	|	И ВозвратТоваровОтПокупателя.Ссылка <> &ДокументИсключение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Ссылка,
	|	КорректировкаРеализации.Дата,
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.Цена,
	|	КорректировкаРеализацииТовары.Количество,
	|	КорректировкаРеализацииТовары.Сумма,
	|	КорректировкаРеализацииТовары.СуммаНДС,
	|	КорректировкаРеализацииТовары.НомерГТД
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ПО КорректировкаРеализацииТовары.Ссылка = КорректировкаРеализации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО (РеализацияТоваровУслугТовары.Номенклатура = КорректировкаРеализацииТовары.Номенклатура)
	|			И (РеализацияТоваровУслугТовары.Цена = КорректировкаРеализацииТовары.Цена)
	|			И (РеализацияТоваровУслугТовары.НомерГТД = КорректировкаРеализацииТовары.НомерГТД)
	|			И (РеализацияТоваровУслугТовары.Ссылка = КорректировкаРеализации.ИсходныйДокументРеализации)
	|ГДЕ
	|	КорректировкаРеализацииТовары.ЕстьВДокументеРеализации
	|	И КорректировкаРеализации.ИсходныйДокументРеализации = &ИсходныйДокументРеализации
	|	И НЕ РеализацияТоваровУслугТовары.Ссылка ЕСТЬ NULL
	|	И КорректировкаРеализации.МоментВремени < &ДатаСреза
	|	И НЕ КорректировкаРеализации.ПометкаУдаления
	|	И КорректировкаРеализацииТовары.ЕстьВДокументеРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка,
	|	РеализацияТоваровУслугТовары.Ссылка.Дата,
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	РеализацияТоваровУслугТовары.Цена,
	|	РеализацияТоваровУслугТовары.Количество,
	|	РеализацияТоваровУслугТовары.Сумма,
	|	РеализацияТоваровУслугТовары.СуммаНДС,
	|	РеализацияТоваровУслугТовары.НомерГТД
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &ИсходныйДокументРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ВозвратыПоДокументуРеализации.Дата) КАК Дата,
	|	ВТ_ВозвратыПоДокументуРеализации.Номенклатура КАК Номенклатура,
	|	ВТ_ВозвратыПоДокументуРеализации.Цена КАК Цена,
	|	ВТ_ВозвратыПоДокументуРеализации.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ ВТ_СрезПоследнихПоДате
	|ИЗ
	|	ВТ_ВозвратыПоДокументуРеализации КАК ВТ_ВозвратыПоДокументуРеализации
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВозвратыПоДокументуРеализации.Номенклатура,
	|	ВТ_ВозвратыПоДокументуРеализации.Цена,
	|	ВТ_ВозвратыПоДокументуРеализации.НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_ВозвратыПоДокументуРеализации.Ссылка) КАК Ссылка,
	|	ВТ_ВозвратыПоДокументуРеализации.Номенклатура КАК Номенклатура,
	|	ВТ_ВозвратыПоДокументуРеализации.Цена КАК Цена,
	|	ВТ_ВозвратыПоДокументуРеализации.НомерГТД КАК НомерГТД
	|ПОМЕСТИТЬ ВТ_СрезПоследних
	|ИЗ
	|	ВТ_СрезПоследнихПоДате КАК ВТ_СрезПоследнихПоДате
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВозвратыПоДокументуРеализации КАК ВТ_ВозвратыПоДокументуРеализации
	|		ПО ВТ_СрезПоследнихПоДате.Дата = ВТ_ВозвратыПоДокументуРеализации.Дата
	|			И ВТ_СрезПоследнихПоДате.Номенклатура = ВТ_ВозвратыПоДокументуРеализации.Номенклатура
	|			И ВТ_СрезПоследнихПоДате.Цена = ВТ_ВозвратыПоДокументуРеализации.Цена
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВозвратыПоДокументуРеализации.Номенклатура,
	|	ВТ_ВозвратыПоДокументуРеализации.Цена,
	|	ВТ_ВозвратыПоДокументуРеализации.НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВозвратыПоДокументуРеализации.Ссылка КАК Ссылка,
	|	ВТ_ВозвратыПоДокументуРеализации.Номенклатура КАК Номенклатура,
	|	ВТ_ВозвратыПоДокументуРеализации.Количество КАК Количество,
	|	ВТ_ВозвратыПоДокументуРеализации.Цена КАК Цена,
	|	ВТ_ВозвратыПоДокументуРеализации.Сумма КАК Сумма,
	|	ВТ_ВозвратыПоДокументуРеализации.СуммаНДС КАК СуммаНДС,
	|	ВТ_ВозвратыПоДокументуРеализации.НомерГТД КАК НомерГТД
	|ИЗ
	|	ВТ_СрезПоследних КАК ВТ_СрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВозвратыПоДокументуРеализации КАК ВТ_ВозвратыПоДокументуРеализации
	|		ПО ВТ_СрезПоследних.Ссылка = ВТ_ВозвратыПоДокументуРеализации.Ссылка
	|			И ВТ_СрезПоследних.Номенклатура = ВТ_ВозвратыПоДокументуРеализации.Номенклатура
	|			И ВТ_СрезПоследних.НомерГТД = ВТ_ВозвратыПоДокументуРеализации.НомерГТД
	|			И ВТ_СрезПоследних.Цена = ВТ_ВозвратыПоДокументуРеализации.Цена";
	
	// Для некоторых ролей может быть запрещен просмотр документа "Корректировка реализации". 
	// При этом данные для возврата должны вычисляться корректно.
	УстановитьПривилегированныйРежим(Истина);
	
	ТоварыКВозвратуПоРеализации = Запрос.Выполнить().Выгрузить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТоварыКВозвратуПоРеализации;
КонецФункции


// Заполняет реквизит "ДеятельностьНаТорговомСборе" по данным учета и данным документа.
//
Процедура УстановитьДеятельностьНаТорговомСборе(Объект) Экспорт
	
	ВозможнаДеятельностьНаТорговомСборе = (Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаКомиссия
		Или Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Товары
		Или Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности);
	
	Если Не Объект.ДеятельностьНаПатенте И ВозможнаДеятельностьНаТорговомСборе Тогда
		Объект.ДеятельностьНаТорговомСборе = ТорговыйСбор.ДеятельностьНаТорговомСбореПриУСНДоходы(
			Объект.Организация, Объект.Склад, Объект.Дата);
	ИначеЕсли Объект.ДеятельностьНаТорговомСборе Тогда
		Объект.ДеятельностьНаТорговомСборе = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Функция ИспользуемаяСтатьяПрочихДоходовРасходов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	ПрочиеДоходыИРасходы.Ссылка КАК СтатьяДоходов,
	|	ПрочиеДоходыИРасходы.Код КАК Код
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрочиеДоходыИРасходы КАК ПрочиеДоходыИРасходы
	|		ПО РеализацияТоваров.Субконто = ПрочиеДоходыИРасходы.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() <> 1 Или Не Выборка.Следующий() Тогда
		Возврат Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Выборка.СтатьяДоходов;
	
КонецФункции

// Формирует пакеты электронных документов стандарта CML 2.08 содержащих в себе документы
// реализация (акт, накладная, УПД), реквизиты организации и счет-фактура.
// 
// Параметры:
//  МассивОбъектов - Массив - массив ссылок на документы РеализацияТоваровИУслуг.
// 
// Возвращаемое значение:
//  Массив - содержит структуры со свойствами:
//    * Представление - Строка - наименование пакета электронных документов
//    * АдресВоВременномХранилище - Строка - адрес данных электронного документа во временном хранилище
//
Функция СформироватьРеализациюТоваровИУслугВXML(МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	РеализацияТоваровУслугУслуги.Ссылка КАК ДокументРеализации
	                      |ИЗ
	                      |	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	                      |ГДЕ
	                      |	РеализацияТоваровУслугУслуги.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	                      |	И РеализацияТоваровУслугУслуги.Ссылка В(&СсылкиНаДокументы)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	РеализацияТоваровУслуг.Ссылка КАК ДокументРеализации,
	                      |	ЕСТЬNULL(СчетФактураВыданныйДокументыОснования.Ссылка, ЗНАЧЕНИЕ(Документ.СчетФактураВыданный.ПустаяСсылка)) КАК СчетФактура,
	                      |	РеализацияТоваровУслуг.Организация,
	                      |	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.Ссылка) КАК ДокументРеализацииПредставление,
	                      |	РеализацияТоваровУслуг.Контрагент.ИНН,
	                      |	РеализацияТоваровУслуг.Организация.ИНН
	                      |ИЗ
	                      |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	                      |		ПО (СчетФактураВыданныйДокументыОснования.ДокументОснование = РеализацияТоваровУслуг.Ссылка)
	                      |ГДЕ
	                      |	РеализацияТоваровУслуг.Ссылка В(&СсылкиНаДокументы)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	РеализацияТоваровУслугУслуги.Ссылка КАК ДокументРеализации,
	                      |	РеализацияТоваровУслугУслуги.Номенклатура,
	                      |	РеализацияТоваровУслугУслуги.Номенклатура.Представление
	                      |ИЗ
	                      |	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	                      |ГДЕ
	                      |	РеализацияТоваровУслугУслуги.Номенклатура.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)
	                      |	И РеализацияТоваровУслугУслуги.Ссылка В(&СсылкиНаДокументы)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	РеализацияТоваровУслугУслуги.Ссылка,
	                      |	РеализацияТоваровУслугУслуги.Номенклатура");
	
	Запрос.УстановитьПараметр("СсылкиНаДокументы", МассивОбъектов);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[1].Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДокументыСУслугойСтрокой = РезультатЗапроса[0].Выгрузить().ВыгрузитьКолонку("ДокументРеализации");
	НоменклатураБезЕдиницыИзмерения = РезультатЗапроса[2].Выбрать();
	ВыборкаДанных = РезультатЗапроса[1].Выбрать();
	
	ТаблицаСсылок = Новый ТаблицаЗначений;
	ТаблицаСсылок.Колонки.Добавить("Ссылка");
	СчетаФактурыРеализаций = Новый Соответствие;
	Пока ВыборкаДанных.Следующий() Цикл
		
		ДокументРеализации = ВыборкаДанных.ДокументРеализации;
		ДокументРеализацииПредставление = ВыборкаДанных.ДокументРеализацииПредставление;
		Если ДокументыСУслугойСтрокой.Найти(ДокументРеализации) <> Неопределено Тогда
			
			ШаблонСообщения = НСтр("ru = 'Сообщение будет отправлено без электронного документа %1. Имеются позиции, отсутствующие в справочнике ""Номенклатура"".'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, """" + ДокументРеализацииПредставление + """");
			ЗаписьЖурналаРегистрации("Обмен ЭД через электронную почту", УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг,, ТекстСообщения);
				
			Продолжить;
			
		КонецЕсли;
		
		НоменклатураБезЕдиницыИзмерения.Сбросить();
		СписокНоменклатурыБезЕИ = "";
		ПерваяИтерация = Истина;
		Пока НоменклатураБезЕдиницыИзмерения.НайтиСледующий(ДокументРеализации, "ДокументРеализации") Цикл
			
			СписокНоменклатурыБезЕИ = СписокНоменклатурыБезЕИ + ?(ПерваяИтерация, "", "," + Символы.ПС) +
				НоменклатураБезЕдиницыИзмерения.НоменклатураПредставление;
			ПерваяИтерация = Ложь;
			
		КонецЦикла;
		Если Не ПустаяСтрока(СписокНоменклатурыБезЕИ) Тогда
			
			ШаблонСообщения = НСтр("ru = 'Сообщение будет отправлено без электронного документа %1.
			|Для следующих номенклатурных позиций не заполнена единица измерения: '");
			ШаблонСообщения = ШаблонСообщения + Символы.ПС + СписокНоменклатурыБезЕИ;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, """" + ДокументРеализацииПредставление + """");
			ЗаписьЖурналаРегистрации("Обмен ЭД через электронную почту", УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг,, ТекстСообщения);	
				
			Продолжить;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаДанных.КонтрагентИНН) Тогда
			
			ШаблонСообщения = НСтр("ru = 'Сообщение будет отправлено без электронного документа %1. Не заполнен ИНН контрагента.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, """" + ДокументРеализацииПредставление + """");
			ЗаписьЖурналаРегистрации("Обмен ЭД через электронную почту", УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг,, ТекстСообщения);	
				
			Продолжить;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаДанных.ОрганизацияИНН) Тогда
			
			ШаблонСообщения = НСтр("ru = 'Сообщение будет отправлено без электронного документа %1. Не заполнен ИНН организации.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, """" + ДокументРеализацииПредставление + """");
			ЗаписьЖурналаРегистрации("Обмен ЭД через электронную почту", УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг,, ТекстСообщения);	
				
			Продолжить;
			
		КонецЕсли;		
		
		НоваяСтрока = ТаблицаСсылок.Добавить();
		НоваяСтрока.Ссылка = ВыборкаДанных.ДокументРеализации;
			
		Если ЗначениеЗаполнено(ВыборкаДанных.СчетФактура) Тогда
			СчетаФактурыРеализаций.Вставить(ВыборкаДанных.ДокументРеализации, ВыборкаДанных.СчетФактура);
		КонецЕсли;
		
	КонецЦикла;
	ТаблицаСсылок.Свернуть("Ссылка");
	
	ПакетыЭД = Новый Массив;
	ОписанияОбъектовУчета = ОбменСКонтрагентами.ОписанияОбъектовУчета(ТаблицаСсылок.ВыгрузитьКолонку("Ссылка"));
	Для Каждого ОписаниеОбъектаУчета Из ОписанияОбъектовУчета Цикл
		Если СчетаФактурыРеализаций[ОписаниеОбъектаУчета.ОбъектУчета] <> Неопределено Тогда
			ОписаниеОбъектаУчета.ТипДокумента = ОбменСКонтрагентами.ТипыДокументов().УПД;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыВизуализации = ОбменСКонтрагентами.НовыеПараметрыВизуализацииЭлектронногоДокумента();
	ПараметрыВизуализации.ВыводитьДопДанные           = Ложь;
	ПараметрыВизуализации.ВыводитьБанковскиеРеквизиты = Истина;
	ПараметрыВизуализации.ВыводитьКопияВерна          = Ложь;
	ДанныеДокументовДляВыгрузки = ОбменСКонтрагентами.СформироватьДанныеДляВыгрузкиЭлектронныхДокументов(ОписанияОбъектовУчета);
	Для Каждого ДанныеДокумента Из ДанныеДокументовДляВыгрузки Цикл
		// Подготовим визуализацию.
		Попытка
			ПотокПредставления = ДанныеДокумента.ДвоичныеДанныеПредставления.ОткрытьПотокДляЧтения();
			ТабличныйДокумент = Новый ТабличныйДокумент;
			ТабличныйДокумент.Прочитать(ПотокПредставления);
		Исключение
			ЗаголовокСообщения = НСтр("ru = 'Не удалось сформировать визуализацю электронного документа по причине'");
			СообщитьОбОшибкеВыгрузкиЭлектронногоДокумента(ЗаголовокСообщения);
			ВызватьИсключение;
		КонецПопытки;
		
		КаталогКонтейнера = ФайловаяСистема.СоздатьВременныйКаталог();
		// Распакуем архив для добавления визуализации.
		Попытка
			ПотокАрхива = Новый ПотокВПамяти;
			ДанныеДокумента.ДвоичныеДанныеПакета.Записать(ПотокАрхива);
			
			ЧтениеZip = Новый ЧтениеZipФайла(ПотокАрхива);
			ЧтениеZip.ИзвлечьВсе(КаталогКонтейнера);
			ПотокАрхива.Закрыть();
			ЧтениеZip.Закрыть();
		Исключение
			ЗаголовокСообщения = НСтр("ru = 'Не удалось распаковать контейнер электронного документа по причине'");
			СообщитьОбОшибкеВыгрузкиЭлектронногоДокумента(ЗаголовокСообщения);
			ВызватьИсключение;
		КонецПопытки;
		
		ИмяДокумента = ИмяСохраняемогоФайла(ДанныеДокумента.ОбъектУчета);
		
		Попытка
			ТабличныйДокумент.Записать(КаталогКонтейнера + ПолучитьРазделительПути() + ИмяДокумента + "pdf", ТипФайлаТабличногоДокумента.PDF);
		Исключение
			ЗаголовокСообщения = НСтр("ru = 'Не удалось сохранить визуализацию электронного документа по причине'");
			СообщитьОбОшибкеВыгрузкиЭлектронногоДокумента(ЗаголовокСообщения);
			ВызватьИсключение;
		КонецПопытки;
		
		Попытка
			ЗаписьZip = Новый ЗаписьZipФайла();
			ЗаписьZip.Добавить(КаталогКонтейнера + "*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути,
				РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
			ДанныеПакета = ЗаписьZip.ПолучитьДвоичныеДанные();
		Исключение
			ЗаголовокСообщения = НСтр("ru = 'Не удалось сформировать контейнер электронного документа по причине'");
			СообщитьОбОшибкеВыгрузкиЭлектронногоДокумента(ЗаголовокСообщения);
			ВызватьИсключение;
		КонецПопытки;
		
		СтруктураВложения = Новый Структура("АдресВоВременномХранилище, Представление");
		СтруктураВложения.АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДанныеПакета, Новый УникальныйИдентификатор);
		СтруктураВложения.Представление = ИмяДокумента + "zip";
		ПакетыЭД.Добавить(СтруктураВложения);
	КонецЦикла;
	
	Возврат ПакетыЭД;
	
КонецФункции

// Читает данные электронных документов вида реализация товаров и услуг стандарта CML 2.08
//
// Параметры:
//  АдресаXMLФайлов - Массив - массив строк с адресами данных электронных документов во временном хранилище
//
// Возвращаемое значение:
//  ДанныеСчетов - ТаблицаЗначений - таблица значений с данными прочитанных электронных документов.
//   описание колонок таблицы см. в функции.
//
Функция РазобратьРеализациюТоваровИУслугВXML(МассивДанныхЭД) Экспорт
	
	ДанныеРТиУ = Новый ТаблицаЗначений;
	ДанныеРТиУ.Колонки.Добавить("Показывать"       , Новый ОписаниеТипов("Булево"));
	ДанныеРТиУ.Колонки.Добавить("Наименование"     , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)));
	ДанныеРТиУ.Колонки.Добавить("ИНН"              , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(12)));
	ДанныеРТиУ.Колонки.Добавить("КПП"              , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	ДанныеРТиУ.Колонки.Добавить("УИД"              , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(32)));
	ДанныеРТиУ.Колонки.Добавить("СчетФактураУИД"   , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(32)));
	ДанныеРТиУ.Колонки.Добавить("ВидЭД"            , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(30)));
	ДанныеРТиУ.Колонки.Добавить("Документ"         , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)));
	ДанныеРТиУ.Колонки.Добавить("НомерДокумента"   , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(20)));
	ДанныеРТиУ.Колонки.Добавить("ДатаДокумента"    , Новый ОписаниеТипов("Дата"));
	ДанныеРТиУ.Колонки.Добавить("СуммаДокумента"   , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	ДанныеРТиУ.Колонки.Добавить("ДанныеДокумента"  , Новый ОписаниеТипов("Структура"));
	ДанныеРТиУ.Колонки.Добавить("ТабличныйДокумент", Новый ОписаниеТипов("ТабличныйДокумент"));
	ДанныеРТиУ.Колонки.Добавить("ФайлДанных");
	ДанныеРТиУ.Колонки.Добавить("ФайлТабличногоДокумента");
	ДанныеРТиУ.Колонки.Добавить("РасширениеФайла"   , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(4)));
	ДанныеРТиУ.Колонки.Добавить("НомерСчФ"   , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(20)));
	ДанныеРТиУ.Колонки.Добавить("ВидДокумента"   , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(10)));
	ДанныеРТиУ.Колонки.Добавить("ДатаСчФ"   , Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ДанныеРТиУ.Колонки.Добавить("ТекстОшибки"       , Новый ОписаниеТипов("Строка"));
	
	ДанныеРТиУ.Индексы.Добавить("ИНН, КПП, НомерДокумента");
	
	Для Каждого ДанныеЭД Из МассивДанныхЭД Цикл
				
		ПолученныеДанные = ПолучитьИзВременногоХранилища(ДанныеЭД);		
		Если ТипЗнч(ПолученныеДанные) = Тип("Структура") Тогда
			
			ВидДокумента = "";
			Если ПолученныеДанные.Свойство("ВидДокумента") Тогда
					
					ВидДокумента = ПолученныеДанные.ВидДокумента;
					
			КонецЕсли;
			ДанныеКарточкиЭД = ПрочитатьДанныеКарточкиЭД(ПолученныеДанные.ДвоичныеДанные, ВидДокумента);
			
		Иначе	
			
			ДанныеКарточкиЭД = ПрочитатьДанныеКарточкиЭД(ПолученныеДанные);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеКарточкиЭД) Тогда		
			
			Если ДанныеКарточкиЭД.Свойство("НомерДокумента") Тогда
				Отбор = Новый Структура("ИНН, КПП, НомерДокумента",
					ДанныеКарточкиЭД.ИНН,
					ДанныеКарточкиЭД.КПП,
					ДанныеКарточкиЭД.НомерДокумента);
				
				Если ДанныеРТиУ.НайтиСтроки(Отбор).Количество() > 0 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаДанныхРТиУ = ДанныеРТиУ.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанныхРТиУ, ДанныеКарточкиЭД);
						
			Если ТипЗнч(ПолученныеДанные) = Тип("Структура") Тогда
				
				СтрокаДанныхРТиУ.РасширениеФайла = ПолученныеДанные.РасширениеФайла;				
				СтрокаДанныхРТиУ.ВидДокумента = ВидДокумента;				
				Если ПолученныеДанные.Свойство("ФайлТабличногоДокумента") Тогда
				
					СтрокаДанныхРТиУ.ФайлТабличногоДокумента = ПолученныеДанные.ФайлТабличногоДокумента;
					Если ПолученныеДанные.РасширениеФайла = "mxl" Тогда
					
						ТабДок = Новый ТабличныйДокумент;						
						ТабДок.ОтображатьЗаголовки = Ложь;
						ТабДок.ОтображатьСетку = Истина;						
						ИмяВременногоФайла = ПолучитьИмяВременногоФайла("mxl");
						ПолученныеДанные.ФайлТабличногоДокумента.Записать(ИмяВременногоФайла);
						ТабДок.Прочитать(ИмяВременногоФайла);					
						СтрокаДанныхРТиУ.ТабличныйДокумент = ТабДок;
						УдалитьФайлы(ИмяВременногоФайла);
					
					КонецЕсли;
					
				КонецЕсли;								
				Если ПолученныеДанные.Свойство("НомерСчФ") Тогда
					
					СтрокаДанныхРТиУ.НомерСчФ = ПолученныеДанные.НомерСчФ;
					СтрокаДанныхРТиУ.ДатаСчФ = ПолученныеДанные.ДатаСчФ;				
					
				КонецЕсли;			
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеРТиУ;
	
КонецФункции

Процедура СообщитьОбОшибкеВыгрузкиЭлектронногоДокумента(Знач ЗаголовокСообщения)
	
	ШаблонСообщения = НСтр("ru = '%1:
	|%2
	|Подробности см. в Журнале регистрации.'");
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ЗаголовокСообщения,
	КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	Сообщить(ТекстСообщения);

КонецПроцедуры

Функция СтатусДокументаПеречислением(СтатусДокумента) Экспорт
	
	Возврат ?(СтатусДокумента = Истина,
		Перечисления.СтатусыДокументовРеализации.Подписан,
		Перечисления.СтатусыДокументовРеализации.НеПодписан);
	
КонецФункции

#Область ОбработчикиОбновления

Функция ТекстЗапросаДанныеДетализацииДоУслуг() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	Услуги.НомерСтроки КАК НомерСтроки,
	|	Услуги.СчетРасходов КАК СчетРасходов,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(Услуги.Субконто КАК Справочник.НоменклатурныеГруппы), ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)) КАК НоменклатурнаяГруппа,
	|	Услуги.Номенклатура КАК Услуга,
	|	Услуги.Сумма КАК Сумма
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК Услуги
	|ГДЕ
	|	Услуги.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ ПО
	|	СчетРасходов,
	|	НоменклатурнаяГруппа";
	
КонецФункции

#КонецОбласти

Функция СоздатьДокументИзСтруктуры(ДанныеСтруктуры, ТекстПрогресса = "") Экспорт
	
	Если ТипЗнч(ДанныеСтруктуры) = Тип("Строка") Тогда
		ДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеСтруктуры);
	ИначеЕсли ТипЗнч(ДанныеСтруктуры) = Тип("Структура") Тогда
		ДанныеФайла = ДанныеСтруктуры;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
	Если НЕ ТипЗнч(ДанныеФайла) = Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если НЕ ДанныеФайла.Свойство("ДанныеДокумента") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеЗаполнения = Новый Структура("Организация,Контрагент,ДоговорКонтрагента,ВидОперации");
	ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером;
	
	ДатаУведомления = Дата(1,1,1);
	НомерУведомления = "";
	ТекстУведомления = "";
	ТекстКомментария = "";
	
	Если ДанныеФайла.ДанныеДокумента.Свойство("Дата") Тогда
		
		ДатаУведомления = ДанныеФайла.ДанныеДокумента.Дата;
		НомерУведомления = СокрЛП(ДанныеФайла.ДанныеДокумента.Номер);
		Если ДанныеФайла.ВидДокумента = ЭлектронноеВзаимодействиеБП.ВидДокументаОтчетOzonВыкуп() Тогда
			ТекстУведомления = СтрШаблон(
				НСтр("ru = 'Отчет о выкупе №%1 от %2'", ОбщегоНазначения.КодОсновногоЯзыка()), 
				НомерУведомления, 
				Формат(ДатаУведомления, "ДФ=""дд.ММ.гггг"""));
				
			ТекстКомментария = ТекстУведомления;
		Иначе
			ТекстУведомления = НСтр("ru = 'Уведомление о выкупе'", ОбщегоНазначения.КодОсновногоЯзыка());
			
			ТекстКомментария = СтрШаблон(
				НСтр("ru = 'Уведомление о выкупе №%1 от %2'", ОбщегоНазначения.КодОсновногоЯзыка()), 
				НомерУведомления, 
				Формат(ДатаУведомления, "ДФ=""дд.ММ.гггг"""));
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеФайла.ДанныеДокумента.Свойство("Получатель") Тогда
		Если ДанныеФайла.ДанныеДокумента.Получатель.Свойство("Ссылка") Тогда
			ДанныеЗаполнения.Организация = ДанныеФайла.ДанныеДокумента.Получатель.Ссылка;
		Иначе
			ИНН = "";
			КПП = "";
			ДанныеФайла.ДанныеДокумента.Получатель.Свойство("ИНН", ИНН);
			ДанныеФайла.ДанныеДокумента.Получатель.Свойство("КПП", КПП);
			
			СсылкаОрганизация = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Организации", ИНН, КПП);
			Если СсылкаОрганизация = Неопределено Тогда
				СсылкаОрганизация = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Организации", ИНН);
			КонецЕсли;
			ДанныеЗаполнения.Организация = СсылкаОрганизация;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеФайла.ДанныеДокумента.Свойство("Плательщик") Тогда
		Если ДанныеФайла.ДанныеДокумента.Плательщик.Свойство("Ссылка") Тогда
			ДанныеЗаполнения.Контрагент = ДанныеФайла.ДанныеДокумента.Плательщик.Ссылка;
		Иначе
			ИНН = "";
			КПП = "";
			ДанныеФайла.ДанныеДокумента.Плательщик.Свойство("ИНН", ИНН);
			ДанныеФайла.ДанныеДокумента.Плательщик.Свойство("КПП", КПП);
			
			СсылкаКонтрагента = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ИНН, КПП);
			Если СсылкаКонтрагента = Неопределено Тогда
				СсылкаКонтрагента = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ИНН);
			КонецЕсли;
			ДанныеЗаполнения.Контрагент = СсылкаКонтрагента;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения.Контрагент) Тогда
		ВызватьИсключение НСтр("ru = 'Не найден контрагент'");
	КонецЕсли;
	
	Если ДанныеФайла.ДанныеДокумента.Свойство("Договор") Тогда
		ДанныеЗаполнения.ДоговорКонтрагента = ДанныеФайла.ДанныеДокумента.Договор;
	КонецЕсли;
	
	ЗаполнятьДоговор = Истина;
	Если ДанныеФайла.Свойство("ЗаполнятьДоговор") Тогда
		ЗаполнятьДоговор = ДанныеФайла.ЗаполнятьДоговор;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения.ДоговорКонтрагента)
		И ЗаполнятьДоговор
		И ЗначениеЗаполнено(ДанныеЗаполнения.Организация)
		И ЗначениеЗаполнено(ДанныеЗаполнения.Контрагент) Тогда
		
		МассивВидовДоговоров = Новый Массив;
		МассивВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПокупателем"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Номер", Новый Структура("ЗначениеОтбора", НомерУведомления));
		ДополнительныеПараметры.Вставить("Дата", Новый Структура("ЗначениеОтбора", ДатаУведомления));
		ДополнительныеПараметры.Вставить("Наименование", Новый Структура("ЗначениеОтбора", ТекстУведомления));
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДанныеЗаполнения.ДоговорКонтрагента,
			ДанныеЗаполнения.Контрагент,
			ДанныеЗаполнения.Организация,
			МассивВидовДоговоров, ДополнительныеПараметры, Истина);
		
	КонецЕсли;
	
	Если ДанныеФайла.ДанныеДокумента.Свойство("ТаблицаТоваров") Тогда
		
		МассивПоискаНоменклатуры  = Новый Массив;
		СопоставлениеНоменклатуры = Новый Соответствие;
		
		ТаблицаТоваров = ДанныеФайла.ДанныеДокумента.ТаблицаТоваров;
		//Обработаем строки товара перед загрузкой
		Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
			
			СтруктураПоискаНоменклатуры = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(
				ДанныеЗаполнения.Контрагент,
				СтрокаТовара.ИД);
			МассивПоискаНоменклатуры.Добавить(СтруктураПоискаНоменклатуры);
			
		КонецЦикла;
		
		МассивНайденнойНоменклатуры   = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(
			Новый Структура("НоменклатураКонтрагента", МассивПоискаНоменклатуры),
			Ложь);
		
		Для Каждого СтрокаНоменклатуры Из МассивНайденнойНоменклатуры Цикл
			Идентификатор = СтрокаНоменклатуры.НоменклатураКонтрагента.Идентификатор;
			Номенклатура  = СтрокаНоменклатуры.НоменклатураИБ.Номенклатура;
			Если ЗначениеЗаполнено(Номенклатура) Тогда
				СопоставлениеНоменклатуры.Вставить(Идентификатор, Номенклатура);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'В строках уведомления нет данных о продажах'");
	КонецЕсли;
	
	ЭлектронноеВзаимодействиеБП.ПроверитьЗапретИзмененияДанных(ДатаУведомления, ДанныеЗаполнения.Организация);
	
	СпособЗагрузки = ОбменСМаркетплейс.СпособЗагрузки(ДанныеФайла);
	ЭтоАвтозагрузка = ОбменСМаркетплейс.ЭтоАвтозагрузка(СпособЗагрузки);
	
	НачатьТранзакцию();
	
	Попытка
		
		Если ДанныеФайла.ДанныеДокумента.Свойство("Ссылка")
			И ЗначениеЗаполнено(ДанныеФайла.ДанныеДокумента.Ссылка) Тогда
			НовыйДокумент = ДанныеФайла.ДанныеДокумента.Ссылка.ПолучитьОбъект();
			НовыйДокумент.ПометкаУдаления = Ложь;
			НовыйДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			НовыйДокумент.Товары.Очистить();
			НовыйДокумент.СуммаДокумента = 0;
		Иначе
			НовыйДокумент = СоздатьДокумент();
		КонецЕсли;
		
		НовыйДокумент.Дата = КонецДня(ДатаУведомления);
		НовыйДокумент.Комментарий = ТекстКомментария;
		НовыйДокумент.ЭтоУниверсальныйДокумент = Истина;
		
		Если Не ЗначениеЗаполнено(ДанныеЗаполнения.ДоговорКонтрагента)
			И ЗаполнятьДоговор
			И ЗначениеЗаполнено(ДанныеЗаполнения.Организация)
			И ЗначениеЗаполнено(ДанныеЗаполнения.Контрагент) Тогда
			
			ДанныеЗаполненияДоговора = Новый Структура;
			ДанныеЗаполненияДоговора.Вставить("Организация", ДанныеЗаполнения.Организация);
			ДанныеЗаполненияДоговора.Вставить("Владелец", ДанныеЗаполнения.Контрагент);
			
			ЭтоПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(ДанныеЗаполнения.Организация, ТекущаяДатаСеанса());

			НовыйДоговор = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
			НовыйДоговор.Заполнить(ДанныеЗаполненияДоговора);
			НовыйДоговор.Номер = НомерУведомления;
			НовыйДоговор.Дата = ДатаУведомления;
			НовыйДоговор.Наименование = ТекстУведомления;
			НовыйДоговор.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПриРеализации(
				?(ЗначениеЗаполнено(НовыйДоговор.Дата), НовыйДоговор.Дата, ТекущаяДатаСеанса()),
				ДанныеЗаполнения.Организация);
				
			НовыйДоговор.ДополнительныеСвойства.Вставить("НеДелатьОсновным", Истина);
				
			Попытка
				НовыйДоговор.Записать();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			КонецПопытки;
			
			ДанныеЗаполнения.ДоговорКонтрагента = НовыйДоговор.Ссылка;
		КонецЕсли;
		
		// Документ создается с фиксированным значением настройки НДС.
		ДанныеЗаполнения.Вставить("СуммаВключаетНДС", Истина);
		НовыйДокумент.Заполнить(ДанныеЗаполнения);
		
		НезаполненнаяНоменклатура = Новый Массив;
		Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
			
			Номенклатура = СопоставлениеНоменклатуры.Получить(СтрокаТовара.ИД);
			
			СтрокаДокумента = НовыйДокумент.Товары.Добавить();
			СтрокаДокумента.Номенклатура = Номенклатура;
			СтрокаДокумента.Количество   = СтрокаТовара.Количество;
			СтрокаДокумента.Сумма        = СтрокаТовара.Сумма;
			СтрокаДокумента.СтавкаНДС    = ОбменСКонтрагентамиБП.ПолучитьСтавкуНДСПеречислением(СтрокаТовара.СтавкаНДС);
			
			Если Не ЗначениеЗаполнено(Номенклатура) И ЭтоАвтозагрузка Тогда
				ОбменСМаркетплейс.ДобавитьНезаполненнуюНоменклатуру(
					НезаполненнаяНоменклатура, СтрокаТовара, "Товары", НовыйДокумент.Товары.Индекс(СтрокаДокумента));
			КонецЕсли;
			
		КонецЦикла;
		
		ДанныеОбъекта = Новый Структура("Дата, Организация, Реализация, ДокументБезНДС, ТипЦен, СуммаВключаетНДС, ВедетсяУчетПрослеживаемыхТоваров");
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, НовыйДокумент);
		ДанныеОбъекта.Реализация = Истина;
		ДанныеОбъекта.ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
			И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(НовыйДокумент.Дата);
		
		ЗаполнитьСтрокиТЧЗагрузкаИзФайла(НовыйДокумент.Товары, "Товары", ДанныеОбъекта);
		
		НовыйДокумент.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
		
		НовыйДокумент.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	ПараметрыСоздания = УчетНДСКлиентСервер.НовыеПараметрыСозданияВыданногоСчетаФактуры();
	ПараметрыСоздания.Основание                = НовыйДокумент.Ссылка;
	ПараметрыСоздания.ЭтоУниверсальныйДокумент = НовыйДокумент.ЭтоУниверсальныйДокумент;
	ПараметрыСоздания.КодВидаОперации          = "01";
	УчетНДСВызовСервера.СоздатьСчетФактуруВыданныйНаОсновании(ПараметрыСоздания);
	
	ОбменСМаркетплейс.СохранитьДанныеМаркетплейсаВПрисоединенномФайле(
		НовыйДокумент.Ссылка, ДанныеФайла.ДанныеДокумента.ПервичныеДанныеМаркетплейса, СпособЗагрузки);
	
	Если ЭтоАвтозагрузка Тогда
		ДанныеФайла.ДанныеДокумента.Вставить("Ссылка", НовыйДокумент.Ссылка);
	КонецЕсли;
	
	ОбменСМаркетплейс.СохранитьНесопоставленнуюНоменклатуру(
		НезаполненнаяНоменклатура, НовыйДокумент.Ссылка, ДанныеЗаполнения.Контрагент, ДанныеФайла);
	
	Возврат НовыйДокумент.Ссылка;
	
КонецФункции

Функция СоздатьПродажуТоваровСоСкладаКомиссионера(ДанныеСтруктуры, ПротоколСоздания) Экспорт
	
	Шапка = ДанныеСтруктуры.Документ.Шапка;
	Товары = ДанныеСтруктуры.Документ.Товары;
	Организация = ДанныеСтруктуры.Организация;
	КонтрагентМаркетплейс = ДанныеСтруктуры.КонтрагентМаркетплейс;
	НомерДокумента = Шапка.НомерДокумента;
	ДатаДокумента = Шапка.ДатаДокумента;
	ДатаСФ = Шапка.ДатаСФ;
	НомерСФ = Шапка.НомерСФ;
	
	ТекстСообщения = "";
	
	ЭлектронноеВзаимодействиеБП.ПроверитьЗапретИзмененияДанных(ДатаДокумента, Организация);
	
	СпособЗагрузки = ОбменСМаркетплейс.СпособЗагрузки(ДанныеСтруктуры);
	ЭтоАвтозагрузка = ОбменСМаркетплейс.ЭтоАвтозагрузка(СпособЗагрузки);
	
	МассивПоискаНоменклатуры  = Новый Массив;
	СопоставлениеНоменклатуры = Новый Соответствие;
	
	Для Каждого СтрокаТовара Из Товары Цикл
		СтруктураПоискаНоменклатуры = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(
			КонтрагентМаркетплейс,
			СтрокаТовара.ИД);
		Если ЗначениеЗаполнено(СтрокаТовара.НатуральныйИД) Тогда
			СтруктураПоискаНоменклатуры.ИсторияИдентификаторов.Добавить(СтрокаТовара.НатуральныйИД);
		КонецЕсли;
		МассивПоискаНоменклатуры.Добавить(СтруктураПоискаНоменклатуры);
	КонецЦикла;
	
	МассивНайденнойНоменклатуры = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(
		Новый Структура("НоменклатураКонтрагента", МассивПоискаНоменклатуры),
		Ложь);
	Для Каждого СтрокаНоменклатуры Из МассивНайденнойНоменклатуры Цикл
		Идентификатор = СтрокаНоменклатуры.НоменклатураКонтрагента.Идентификатор;
		Номенклатура  = СтрокаНоменклатуры.НоменклатураИБ.Номенклатура;
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			СопоставлениеНоменклатуры.Вставить(Идентификатор, Номенклатура);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Шапка.Документ) Тогда
		НовыйДокумент = Шапка.Документ.ПолучитьОбъект();
		НовыйДокумент.ПометкаУдаления = Ложь;
		НовыйДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		НовыйДокумент.Товары.Очистить();
		НовыйДокумент.СуммаДокумента = 0;
	Иначе
		НовыйДокумент = СоздатьДокумент();
		НовыйДокумент.Дата = ДатаДокумента;
	КонецЕсли;
	
	ДанныеЗаполнения = Новый Структура("Организация,ВидОперации,Контрагент,ДоговорКонтрагента",
		Организация,
		Перечисления.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера);
	ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Шапка);
	
	ДанныеЗаполнения.Вставить("СуммаВключаетНДС", Истина);
	ДанныеЗаполнения.Вставить("Склад");
	ДанныеЗаполнения.Вставить("ПодразделениеОрганизации");
	НовыйДокумент.Заполнить(ДанныеЗаполнения);
	
	НовыйДокумент.НомерВходящегоДокумента = НомерДокумента;
	НовыйДокумент.Комиссионер = КонтрагентМаркетплейс;
	НовыйДокумент.ОплатаЧерезКомиссионера = Истина;
	НовыйДокумент.СчетУчетаРасчетовСКомиссионером = НовыйДокумент.СчетУчетаРасчетовСКонтрагентом;
	НовыйДокумент.СчетУчетаРасчетовСКомиссионеромПоАвансам = НовыйДокумент.СчетУчетаРасчетовПоАвансам;
	НовыйДокумент.ЭтоУниверсальныйДокумент = Ложь;
	НовыйДокумент.Комментарий = ДанныеСтруктуры.ОписаниеРодителя;
	
	РеквизитыДоговора = Новый Структура;
	РеквизитыДоговора.Вставить("Владелец", КонтрагентМаркетплейс);
	РеквизитыДоговора.Вставить("Организация", Организация);
	РеквизитыДоговора.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
	
	ДоговорКомиссионера = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	ЕстьДоговорКонтрагента = РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		ДоговорКомиссионера,
		РеквизитыДоговора.Владелец,
		РеквизитыДоговора.Организация,
		РеквизитыДоговора.ВидДоговора);
	Если НЕ ЕстьДоговорКонтрагента Тогда
		ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", РеквизитыДоговора);
		ДоговорКомиссионера = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
		Если ДоговорКомиссионера = Неопределено Тогда
			ТекстСообщения = ТекстСообщения
				+ СтрШаблон(НСтр("ru = 'Не удалось создать договор с комиссионером: %1.'"), КонтрагентМаркетплейс)
				+ Символы.ПС;
		КонецЕсли;
	КонецЕсли;
	НовыйДокумент.ДоговорКомиссионера = ДоговорКомиссионера;
	
	НезаполненнаяНоменклатура = Новый Массив;
	Для Каждого СтрокаТовара Из Товары Цикл
		
		Номенклатура = СопоставлениеНоменклатуры.Получить(СтрокаТовара.ИД);
		
		НоваяСтрока = НовыйДокумент.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара);
		НоваяСтрока.Номенклатура = Номенклатура;
		НоваяСтрока.СтавкаНДС = ОбменСКонтрагентамиБП.ПолучитьСтавкуНДСПеречислением(СтрокаТовара.СтавкаНДС);
		
		Если Не ЗначениеЗаполнено(Номенклатура) И ЭтоАвтозагрузка Тогда
			ОбменСМаркетплейс.ДобавитьНезаполненнуюНоменклатуру(
				НезаполненнаяНоменклатура, СтрокаТовара, "Товары", НовыйДокумент.Товары.Индекс(НоваяСтрока));
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, Реализация, ДокументБезНДС, ТипЦен, СуммаВключаетНДС, ВедетсяУчетПрослеживаемыхТоваров");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, НовыйДокумент);
	ДанныеОбъекта.Реализация = Истина;
	ДанныеОбъекта.ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(НовыйДокумент.Дата);
	ЗаполнитьСтрокиТЧЗагрузкаИзФайла(НовыйДокумент.Товары, "Товары", ДанныеОбъекта);
	
	НовыйДокумент.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
	
	НовыйДокумент.Записать();
	
	Если ЗначениеЗаполнено(НомерСФ) Тогда
		ПараметрыСоздания = УчетНДСКлиентСервер.НовыеПараметрыСозданияВыданногоСчетаФактуры();
		ПараметрыСоздания.Основание = НовыйДокумент.Ссылка;
		РеквизитыСФ = УчетНДСВызовСервера.СоздатьСчетФактуруВыданныйНаОсновании(ПараметрыСоздания);
		
		Если РеквизитыСФ <> Неопределено И ЗначениеЗаполнено(РеквизитыСФ.СчетФактура) Тогда
			СчетФактураОбъект = РеквизитыСФ.СчетФактура.ПолучитьОбъект();
			СчетФактураОбъект.ОтредактированНомерСчетаФактуры = Истина;
			СчетФактураОбъект.ПредставлениеНомера = НомерСФ;
			СчетФактураОбъект.НомерВходящегоДокумента = НомерСФ;
			СчетФактураОбъект.Записать();
		КонецЕсли;
	КонецЕсли;
	
	ПротоколСоздания.Дата  = НовыйДокумент.Дата;
	ПротоколСоздания.Сумма = НовыйДокумент.СуммаДокумента;
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ПротоколСоздания.Вставить("ТекстСообщения", ТекстСообщения);
	КонецЕсли;
	
	ОбменСМаркетплейс.СохранитьДанныеМаркетплейсаВПрисоединенномФайле(
		НовыйДокумент.Ссылка, ДанныеСтруктуры.ПервичныеДанныеМаркетплейса, СпособЗагрузки);
	
	Если ЭтоАвтозагрузка Тогда
		Шапка.Вставить("Документ", НовыйДокумент.Ссылка);
	КонецЕсли;
		
	ОбменСМаркетплейс.СохранитьНесопоставленнуюНоменклатуру(
		НезаполненнаяНоменклатура, НовыйДокумент.Ссылка, КонтрагентМаркетплейс, ДанныеСтруктуры);
	
	Возврат НовыйДокумент.Ссылка;
	
КонецФункции

Функция НайтиПродажуЮридическомуЛицу(ПараметрыПоиска) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ПараметрыПоиска.Организация);
	Запрос.УстановитьПараметр("Контрагент", ПараметрыПоиска.Контрагент);
	Запрос.УстановитьПараметр("Комиссионер", ПараметрыПоиска.Комиссионер);
	Запрос.УстановитьПараметр("Дата", НачалоДня(ПараметрыПоиска.Дата));
	Запрос.УстановитьПараметр("НомерВходящегоДокумента", ПараметрыПоиска.НомерВходящегоДокумента);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера)
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
	|	И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, ДЕНЬ) = &Дата
	|	И РеализацияТоваровУслуг.Комиссионер = &Комиссионер
	|	И РеализацияТоваровУслуг.НомерВходящегоДокумента = &НомерВходящегоДокумента
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления";
	
	Результат = Новый Структура("Ссылка, ДоговорКонтрагента, ТекстСообщения");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	Иначе
		СтрШаблон = "ru = 'Не найден документ реализации для контрагента: %1, комиссионера %2'";
		Результат.ТекстСообщения = СтрШаблон(НСтр(СтрШаблон),
			ПараметрыПоиска.Контрагент,
			ПараметрыПоиска.Комиссионер);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;

	ЗначенияЗаполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЗначенияЗаполнения");

	ВидОперации = Неопределено; 

	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВидОперации	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "ВидОперации");
	КонецЕсли;

	// Если документ копируется, то вид операции получаем из копируемого документа.
	Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
		Если Параметры.Свойство("ЗначениеКопирования")
			И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Параметры.ЗначениеКопирования, "ВидОперации");
		КонецЕсли;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
		ВидОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначенияЗаполнения, "ВидОперации");
	КонецЕсли;
	
	// При вводе на основании счета на оплату и поступления товаров и услуг, 
	// открывается форма, содержащая только ТЧ Товары или только ТЧ Услуги, если
	// у документа-основания заполнена только соответствующая таблица.
	Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
		Если Параметры.Свойство("Основание")
			И ЗначениеЗаполнено(Параметры.Основание) Тогда
			
			Если ТипЗнч(Параметры.Основание) = Тип("ФиксированныйМассив") Тогда
				СписокОснований = Параметры.Основание;
			Иначе
				СписокОснований = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.Основание);
			КонецЕсли; 
			
			ВидОперации = ОпределитьВидОперацииПоДокументуОснованию(СписокОснований);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		ДоговорКонтрагента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначенияЗаполнения, "ДоговорКонтрагента");
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			ВидОперации = РаботаСДоговорамиКонтрагентовБПВызовСервера.ВидОперацииПоДоговору(
				ДоговорКонтрагента, Тип("ДокументСсылка.РеализацияТоваровУслуг"));
		КонецЕсли;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	ФормыРеализацииТоваровУслуг = ПолучитьСоответствиеВидовОперацийФормам();
	ВыбраннаяФорма = ФормыРеализацииТоваровУслуг[ВидОперации];
	Если ВыбраннаяФорма = Неопределено Тогда
		ВыбраннаяФорма = "ФормаДокумента";
		Параметры.Вставить("ИзменитьВидОперации");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд отправки по электронной почте.
// 
// Параметры:
//  КомандыОтправки - ТаблицаЗначений - состав полей см. в функции ОтправкаПочтовыхСообщений.КомандыОтправки
//
Процедура ДобавитьКомандыОтправки(КомандыОтправки) Экспорт
	
	// Товарная накладная (ТОРГ-12)
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "ТОРГ12_БезУслуг";
	КомандаОтправки.Представление = НСтр("ru='Товарная накладная (ТОРГ-12)'");
	КомандаОтправки.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаОтправки.Порядок       = 10;
		
	// Товарная накладная (ТОРГ-12) с услугами
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "ТОРГ12";
	КомандаОтправки.Представление = НСтр("ru='Товарная накладная (ТОРГ-12) с услугами'");
	КомандаОтправки.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая";
	КомандаОтправки.Порядок       = 20;
	
	// Акт об оказании услуг
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "Акт";
	КомандаОтправки.Представление = НСтр("ru='Акт об оказании услуг'");
	КомандаОтправки.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаУслуги";
	КомандаОтправки.Порядок       = 30;
	
	// Счет-фактура
	Если ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураВыданный) Тогда
		КомандаОтправки = КомандыОтправки.Добавить();
		КомандаОтправки.Идентификатор = "СчетФактура";
		КомандаОтправки.Представление = НСтр("ru='Счет-фактура'");
		КомандаОтправки.Обработчик    = "ОтправкаПочтовыхСообщений.ОтправитьСчетФактураВыданный";
		КомандаОтправки.ФункциональныеОпции = "ИспользуетсяОСНО,ИспользуетсяНДФЛИП,ОсуществляетсяЗакупкаТоваровУслугДляКомитентов,ОсуществляетсяРеализацияТоваровУслугКомитентов,ВыписыватьСчетаФактурыСпецРежимы,УплачиватьНДССпецРежимы,ВедетсяУчетИмпортныхТоваров";
		КомандаОтправки.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте","СчетФактураКомплект");
		КомандаОтправки.Порядок       = 40;
	КонецЕсли;
	
	// Расходная накладная
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "Накладная";
	КомандаОтправки.Представление = НСтр("ru='Расходная накладная'");
	КомандаОтправки.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаОтправки.Порядок       = 50;
	
	// Универсальный передаточный документ
	КомандаОтправки = КомандыОтправки.Добавить();
	КомандаОтправки.Идентификатор = "УниверсальныйПередаточныйДокумент";
	КомандаОтправки.Представление = НСтр("ru='Универсальный передаточный документ (УПД)'");
	КомандаОтправки.Обработчик    = "ОтправкаПочтовыхСообщений.ОтправитьУПД";
	КомандаОтправки.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте","УниверсальныйПередаточныйДокументКомплект");
	КомандаОтправки.Порядок       = 60;
	
	Если ПравоДоступа("Чтение", Метаданные.Документы.СчетНаОплатуПокупателю) Тогда
		// Счет на оплату
		КомандаОтправки = КомандыОтправки.Добавить();
		КомандаОтправки.Идентификатор  = "СчетЗаказ";
		КомандаОтправки.МенеджерПечати = "Обработка.ПечатьСчетаНаОплату";
		КомандаОтправки.Представление  = ПечатьТорговыхДокументовКлиентСервер.ПредставлениеПечатнойФормыСчетаПокупателю();
		КомандаОтправки.Порядок        = 70;
		КомандаОтправки.Обработчик     = "ОтправкаПочтовыхСообщений.ОтправитьСчетНаОплатуПокупателю";
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСоответствиеВидовОперацийФормам() Экспорт

	ФормыРеализацияТоваровУслуг = Новый Соответствие;
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.Товары, "ФормаДокументаТовары");
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.Услуги, "ФормаДокументаУслуги");
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.ПродажаКомиссия, 						"ФормаДокументаОбщая");
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности, 	"ФормаДокументаОбщая");
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.Оборудование, 							"ФормаДокументаОбщая");
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров,						"ФормаДокументаОбщая");
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером,				"ФормаДокументаТовары");
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера,		"ФормаДокументаТовары");
	ФормыРеализацияТоваровУслуг.Вставить(Перечисления.ВидыОперацийРеализацияТоваров.СтроительныеРаботы,						"ФормаДокументаСтроительныеРаботы");
		
	Возврат ФормыРеализацияТоваровУслуг;

КонецФункции 

Функция ВидЭлектронногоДокумента(Документ) Экспорт
	
	ВидОперации = Неопределено;
	ТолькоУслуги = Ложь;
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "ВидОперации, Товары, ВозвратнаяТара");
		ВидОперации = ДанныеДокумента.ВидОперации;
		ТолькоУслуги = (ДанныеДокумента.Товары.Пустой() И ДанныеДокумента.ВозвратнаяТара.Пустой());
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		ВидОперации = Документ.ВидОперации;
		ТолькоУслуги = (Документ.Товары.Количество() = 0 И Документ.ВозвратнаяТара.Количество() = 0);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги Или ТолькоУслуги Тогда
		Возврат ОбменСКонтрагентами.ТипыДокументов().АктВыполненныхРабот;
	Иначе
		Возврат ОбменСКонтрагентами.ТипыДокументов().ТоварнаяНакладная;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьМассивВидовДоговоров(ВидОперации, ДеятельностьНаПатенте = Ложь) Экспорт
	СписокВидовДоговоров = Новый Массив;
	
	Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров Тогда
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);		
	Иначе
		СписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	КонецЕсли;
	
	Возврат СписокВидовДоговоров;
КонецФункции

Функция ОпределитьВидОперацииПоДокументуОснованию(СписокОснований) Экспорт
	Если СписокОснований.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СписокОснований", СписокОснований);
	
	Если ТипЗнч(СписокОснований[0]) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1 КАК ЕстьТовары,
		|	0 КАК ЕстьУслуги,
		|	0 КАК ЕстьВозвратнаяТара,
		|	0 КАК ЕстьОборудование,
		|	0 КАК ПередачаНаКомиссию
		|ПОМЕСТИТЬ ВТ_Таблицы
		|ИЗ
		|	Документ.СчетНаОплатуПокупателю.Товары КАК ДокОснование
		|ГДЕ
		|	ДокОснование.Ссылка В (&СписокОснований)
		|	И НЕ ЕСТЬNULL(ДокОснование.Номенклатура.Услуга, ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	0,
		|	0,
		|	0,
		|	0,
		|	1
		|ИЗ
		|	Документ.СчетНаОплатуПокупателю КАК ДокОснование
		|ГДЕ
		|	ДокОснование.Ссылка В (&СписокОснований)
		|	И (ЕСТЬNULL(ДокОснование.ДоговорКонтрагента.ВидДоговора, ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)) =
		|		ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	0,
		|	1,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	Документ.СчетНаОплатуПокупателю.Товары КАК ДокОснование
		|ГДЕ
		|	ДокОснование.Ссылка В (&СписокОснований)
		|	И ЕСТЬNULL(ДокОснование.Номенклатура.Услуга, ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	0,
		|	0,
		|	1,
		|	0,
		|	0
		|ИЗ
		|	Документ.СчетНаОплатуПокупателю.ВозвратнаяТара КАК ДокОснование
		|ГДЕ
		|	ДокОснование.Ссылка В (&СписокОснований)";
	
	ИначеЕсли ТипЗнч(СписокОснований[0]) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	0 КАК ЕстьТовары,
		|	0 КАК ЕстьУслуги,
		|	0 КАК ЕстьВозвратнаяТара,
		|	1 КАК ЕстьОборудование,
		|	0 КАК ПередачаНаКомиссию
		|ПОМЕСТИТЬ ВТ_Таблицы
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ДокОснование
		|ГДЕ
		|	ДокОснование.Ссылка В (&СписокОснований)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	1 КАК ЕстьТовары,
		|	0 КАК ЕстьУслуги,
		|	0 КАК ЕстьВозвратнаяТара,
		|	0 КАК ЕстьОборудование,
		|	0 КАК ПередачаНаКомиссию
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ДокОснование
		|ГДЕ
		|	ДокОснование.Ссылка В (&СписокОснований)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	0 КАК ЕстьТовары,
		|	0 КАК ЕстьУслуги,
		|	1 КАК ЕстьВозвратнаяТара,
		|	0 КАК ЕстьОборудование,
		|	0 КАК ПередачаНаКомиссию
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.ВозвратнаяТара КАК ДокОснование
		|ГДЕ
		|	ДокОснование.Ссылка В (&СписокОснований)
		|";
	ИначеЕсли ТипЗнч(СписокОснований[0]) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	0 КАК ЕстьТовары,
		|	0 КАК ЕстьУслуги,
		|	0 КАК ЕстьВозвратнаяТара,
		|	0 КАК ЕстьОборудование,
		|	1 КАК ПередачаНаКомиссию
		|ПОМЕСТИТЬ ВТ_Таблицы
		|ИЗ
		|	Документ.ОтчетКомиссионераОПродажах.Товары КАК ДокОснование
		|ГДЕ
		|	ДокОснование.Ссылка В(&СписокОснований)";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() + 
	"ВЫБРАТЬ
	|	СУММА(ВТ_Таблица.ЕстьТовары) КАК ЕстьТовары,
	|	СУММА(ВТ_Таблица.ЕстьУслуги) КАК ЕстьУслуги,
	|	СУММА(ВТ_Таблица.ЕстьВозвратнаяТара) КАК ЕстьВозвратнаяТара,
	|	СУММА(ВТ_Таблица.ЕстьОборудование) КАК ЕстьОборудование,
	|	СУММА(ВТ_Таблица.ПередачаНаКомиссию) КАК ПередачаНаКомиссию
	|ИЗ
	|	ВТ_Таблицы КАК ВТ_Таблица";
	
	Выборка = Запрос.Выполнить().Выбрать();

	ЕстьТовары 	= Ложь;
	ЕстьУслуги	= Ложь;
	ЕстьВозвратнаяТара	= Ложь;
	ЕстьОборудование 	= Ложь;
	ПередачаНаКомиссию 	= Ложь;
	Если Выборка.Следующий() Тогда
		ЕстьТовары 	= ЗначениеЗаполнено(Выборка.ЕстьТовары) И Выборка.ЕстьТовары > 0;
		ЕстьУслуги	= ЗначениеЗаполнено(Выборка.ЕстьУслуги) И Выборка.ЕстьУслуги > 0;
		ЕстьВозвратнаяТара	= ЗначениеЗаполнено(Выборка.ЕстьВозвратнаяТара) И Выборка.ЕстьВозвратнаяТара > 0;
		ЕстьОборудование	= ЗначениеЗаполнено(Выборка.ЕстьОборудование) И Выборка.ЕстьОборудование > 0;
		ПередачаНаКомиссию  = ЗначениеЗаполнено(Выборка.ПередачаНаКомиссию) И Выборка.ПередачаНаКомиссию > 0;
	КонецЕсли;

	Результат = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаКомиссия;
	
	ОсуществляетсяРеализацияТоваровУслугКомитентов			 = ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугКомитентов");
	ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров	 = ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров");
	ВедетсяОтгрузкаБезПереходаПраваСобственности   			 = ПолучитьФункциональнуюОпцию("ВедетсяОтгрузкаБезПереходаПраваСобственности");
	ИспользоватьПродажиЧерезМаркетплейс   			         = ПолучитьФункциональнуюОпцию("ИспользоватьПродажиЧерезМаркетплейс");
	
	Если ЕстьТовары И ВедетсяОтгрузкаБезПереходаПраваСобственности Тогда
		// Предложим пользователю выбрать необходимую форму.
		Результат = Неопределено;
		
	ИначеЕсли (ЕстьТовары И ЕстьУслуги)
		ИЛИ ЕстьОборудование
		ИЛИ (ЕстьВозвратнаяТара И НЕ ПередачаНаКомиссию) Тогда
		// Одновременная реализация товаров и услуг,
		// а также реализация оборудования и тары выполняется через основную форму 
			
	ИначеЕсли ПередачаНаКомиссию Тогда
	    // Используем основную формуОпределеим по виду договора
		
		Результат = Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров;
		
	ИначеЕсли ЕстьТовары Тогда
		Результат = Перечисления.ВидыОперацийРеализацияТоваров.Товары;
	
	ИначеЕсли ЕстьУслуги Тогда
		// Если осуществляется реализация товаров и услуг комитентов, то могут быть агентские услуги, которые 
		// указываются в основной форме, иначе отображаем форму только для собственных услуг.
		Если НЕ ОсуществляетсяРеализацияТоваровУслугКомитентов Тогда
		
			Результат = Перечисления.ВидыОперацийРеализацияТоваров.Услуги;
		
		КонецЕсли; 
		
	КонецЕсли;

	// Все остальные случаи - через основную форму.
	Возврат Результат;

КонецФункции

// Перечень табличных частей, данные которых не используются 
// в контексте документа, скрыты от пользователя
Функция НеИспользуемыеТабличныеЧасти(ВидОперации, Комиссия) Экспорт
	
	ТабличныеЧасти = Новый Массив;
	
	Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Товары
		Или ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером
		Или ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера Тогда

		// Все, кроме товаров
		ТабличныеЧасти.Добавить("Услуги");
		ТабличныеЧасти.Добавить("АгентскиеУслуги");
		ТабличныеЧасти.Добавить("ВозвратнаяТара");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги Тогда

		// Все, кроме услуг
		ТабличныеЧасти.Добавить("Товары");
		ТабличныеЧасти.Добавить("АгентскиеУслуги");
		ТабличныеЧасти.Добавить("ВозвратнаяТара");
	
	Иначе
	
		// Услуги не доступны при комиссии и отгрузке без перехода права собственности
		Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности 
			ИЛИ Комиссия Тогда
			ТабличныеЧасти.Добавить("Услуги");
			ТабличныеЧасти.Добавить("АгентскиеУслуги");
		КонецЕсли;
		
		// Возвратная тара доступна только, если включена соответствующая опция
		Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
			ТабличныеЧасти.Добавить("ВозвратнаяТара");
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат ТабличныеЧасти;
	
КонецФункции

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ, ДоговорДляОтложенногоПроведения = Неопределено) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	ЭтоОтложенноеПроведение = ЗначениеЗаполнено(ДоговорДляОтложенногоПроведения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.УстановитьПараметр("СчетВыручка", ПланыСчетов.Хозрасчетный.Выручка);
	Запрос.УстановитьПараметр("СчетаТоваровКомитентов", 
		БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ТоварыПринятыеНаКомиссию));
	СтавкиБезНДС = Новый Массив();
	СтавкиБезНДС.Добавить(Перечисления.СтавкиНДС.БезНДС);
	СтавкиБезНДС.Добавить(Перечисления.СтавкиНДС.НДС0);
	Запрос.УстановитьПараметр("СтавкиБезНДС", СтавкиБезНДС);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц, ЭтоОтложенноеПроведение);
	Результат    = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	Если НЕ УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	НомераТаблиц = Новый Структура;
	
	Реквизиты.Вставить("ВалютаРеглУчета", ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	Реквизиты.Вставить("РасчетыВВалюте",  Реквизиты.ВалютаВзаиморасчетов <> Реквизиты.ВалютаРеглУчета);
	Реквизиты.Вставить("ПлательщикНДС", УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период)
		ИЛИ УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Реквизиты.Организация, Реквизиты.Период));
	Реквизиты.Вставить("ПлательщикЕНВД", УчетнаяПолитика.ПлательщикЕНВД(Реквизиты.Организация, Реквизиты.Период));
	Реквизиты.Вставить("ЭтоОтложенноеПроведение", ЭтоОтложенноеПроведение);
	Реквизиты.Вставить("ВедетсяУчетПрослеживаемыхТоваров", ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Реквизиты.Период));

	Если Реквизиты.РасчетыВВалюте Тогда
		ПодготовитьТаблицыДокументаРасчетыВВалюте(Запрос, Реквизиты);
	Иначе
		Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
		Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
			Результат    = Запрос.ВыполнитьПакет();
		КонецЕсли;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("РасчетыВВалюте", Реквизиты.РасчетыВВалюте);
	
	Реквизиты.Вставить("ПрименяетсяУСН",
		УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация, Реквизиты.Период));
	Реквизиты.Вставить("ПрименяетсяУСНПатент", 
		УчетнаяПолитика.ПрименяетсяУСНПатент(Реквизиты.Организация, Реквизиты.Период));
	Реквизиты.Вставить("ПрименяетсяАУСН", 
		УчетнаяПолитика.ПрименяетсяАУСН(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ПрименяетсяУСН",        Реквизиты.ПрименяетсяУСН);
	Запрос.УстановитьПараметр("ПрименяетсяУСНПатент",  Реквизиты.ПрименяетсяУСНПатент);
	Запрос.УстановитьПараметр("УчитыватьЗадолженностьУСНПатент",
		УчетПСН.УчитыватьЗадолженностьПоДеятельностиНаПатенте(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ДеятельностьНаПатенте", Реквизиты.ДеятельностьНаПатенте);
	Запрос.УстановитьПараметр("ДеятельностьНаТорговомСборе", Реквизиты.ДеятельностьНаТорговомСборе);
	Запрос.УстановитьПараметр("ИспользуетсяОтложенноеПроведение",
		ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Реквизиты.Организация, Реквизиты.Период));
	
	Запрос.УстановитьПараметр("ИспользоватьСчет40",
		УчетПроизводства.ИспользоватьСчетВыпускПродукции(Реквизиты.Период, Реквизиты.Организация));
	
	Запрос.УстановитьПараметр("ПлательщикНДС",Реквизиты.ПлательщикНДС);

	Реквизиты.Вставить("ВедетсяУчетНДСПоФЗ134", УчетНДС.ВедетсяУчетНДСПоФЗ134(Реквизиты.Период));
	Реквизиты.Вставить("УчетПоПродажнойСтоимости", Реквизиты.ЕстьТовары
		И УчетнаяПолитика.СпособОценкиТоваровВРознице(Реквизиты.Организация, Реквизиты.Период) 
			= Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости);
	Запрос.УстановитьПараметр("УчетПоПродажнойСтоимости", Реквизиты.УчетПоПродажнойСтоимости);
	
	Реквизиты.Вставить("ЭтоДоговорСПокупателем", 
		Реквизиты.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	Реквизиты.Вставить("ЭтоДоговорСКомиссионером", 
		Реквизиты.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
	Реквизиты.Вставить("ЭтоОтгрузкаБезПереходаПраваСобственности", 
		Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности);
		
	Реквизиты.Вставить("ЭтоВыкупТоваровКомиссионером", 
		Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером);
	Запрос.УстановитьПараметр("ЭтоВыкупТоваровКомиссионером", Реквизиты.ЭтоВыкупТоваровКомиссионером);
	Реквизиты.Вставить("ЭтоПродажаТоваровСоСкладаКомиссионера",
		Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаТоваровСоСкладаКомиссионера);
	Запрос.УстановитьПараметр("ЭтоПродажаТоваровСоСкладаКомиссионера", Реквизиты.ЭтоПродажаТоваровСоСкладаКомиссионера);
	Запрос.УстановитьПараметр("ЭтоДоговорСКомиссионером", Реквизиты.ЭтоДоговорСКомиссионером);
	Запрос.УстановитьПараметр("ЭтоОтгрузкаБезПереходаПраваСобственности", Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности);
	Если Реквизиты.ЭтоДоговорСКомиссионером ИЛИ Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности Тогда
		Запрос.УстановитьПараметр("ИспользоватьСчет45", Истина);
		Запрос.УстановитьПараметр("ВидКорСубконто1",    ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
		Запрос.УстановитьПараметр("ВидКорСубконто2",    ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Иначе
		Запрос.УстановитьПараметр("ИспользоватьСчет45", Ложь);
		Запрос.УстановитьПараметр("ВидКорСубконто1",    1);
		Запрос.УстановитьПараметр("ВидКорСубконто2",    Неопределено);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Субсчета91",    БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеРасходы));
	Запрос.УстановитьПараметр("СинонимТовары", НСтр("ru='Товары'"));
	Запрос.УстановитьПараметр("СинонимТара",   НСтр("ru='Возвратная тара'"));
	Запрос.УстановитьПараметр("ПустойСчет",    ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.УстановитьПараметр("МассивКодовРеализацийЗаРубеж",
		Справочники.КодыОперацийРаздела7ДекларацииПоНДС.КодыРеализацииУслугЗаРубежом());
	Запрос.УстановитьПараметр("ЗаполнятьДокументРасчетов", 
		РегистрыНакопления.РеализованныеТоварыКомитентов.ЗаполнятьДокументРасчетов(Реквизиты.Период));
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	ПрименяетсяОсвобождениеОтУплатыНДС = УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(
		Реквизиты.Организация, Реквизиты.Период);
	// В декларации по НДС отражаются РНПТ по операциям с прослеживаемым товаром с НДС,
	// кроме случаев добровольного начисления НДС при применении освобождения от уплаты НДС.
	Запрос.УстановитьПараметр("ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС",
		Реквизиты.ЕстьНДС И Не ПрименяетсяОсвобождениеОтУплатыНДС И Реквизиты.ПлательщикНДС);
	Запрос.УстановитьПараметр("ДокументСНДС", Реквизиты.ЕстьНДС);
	Запрос.УстановитьПараметр("ЭтоЭкспорт",
		Реквизиты.СтранаРегистрации <> Справочники.СтраныМира.Россия);
	Запрос.УстановитьПараметр("ТоварыПринятыеНаКомиссию", БухгалтерскийУчетПовтИсп.СписокСчетовТоварыПринятыеНаКомиссию());
	Запрос.УстановитьПараметр("РеализацияФизЛицу", Справочники.Контрагенты.ЭтоФизическоеЛицо(Реквизиты.Контрагент));
	Запрос.УстановитьПараметр("РеквизитыПериод", Реквизиты.Период);
	Реквизиты.Вставить("ЕстьУчетПодакцизныхТоваров", ПолучитьФункциональнуюОпцию("ПроизводствоПодакцизныхТоваров"));
	
	Если ПолучитьФункциональнуюОпцию("ВедетсяУчетТаможенныхДекларацийЭкспорт") Тогда
		Запрос.УстановитьПараметр("РеализацияЧерезЭлектроннуюТорговуюПлощадкуФизлицуВЕАЭС",
			РеализацияЧерезЭлектроннуюТорговуюПлощадкуФизическомуЛицуВСтрануЕАЭС(ДокументСсылка));
	Иначе
		Запрос.УстановитьПараметр("РеализацияЧерезЭлектроннуюТорговуюПлощадкуФизлицуВЕАЭС", Ложь);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОплатаПринятаКурьером", НСтр("ru = 'Оплата принята курьером транспортной организации'"));
	Запрос.УстановитьПараметр("ОплатаПринятаПлатежнымАгентом", НСтр("ru = 'Оплата принята платежным агентом'"));
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаСписаниеТоваров(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаСписаниеТары(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРеализация(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаЗачетАвансовКомитентов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПереоценкаТоваровВРознице(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРеализацияУслуг(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаТоварыНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРублевыеСуммыДокументовВВалюте(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаДанныеРазметкиАУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРеквизитыПереносЗадолженностиДебиторскаяЗадолженность(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаОтгрузкаПоСчету(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПрослеживаемыеТоварыВРаботах(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаАкцизы(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаЗачетАвансовПлатежногоАгента(НомераТаблиц, ПараметрыПроведения, Реквизиты);
		
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц, ЭтоОтложенноеПроведение)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСоставДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());

	Если ЭтоОтложенноеПроведение Тогда
		// При отложенном проведении нет необходимости обращаться к табличным частям документа, 
		// которые могут содержать много строк. 
		// Суммы взаиморасчетов могут быть получены из ранее сформированных проводок документа.
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЛОЖЬ КАК ЕстьТовары,
		|	ЛОЖЬ КАК ЕстьТоварыКомитентов,
		|	ЛОЖЬ КАК ЕстьУслуги,
		|	ЛОЖЬ КАК ЕстьАгентскиеУслуги,
		|	ЛОЖЬ КАК ЕстьТара,
		|	ЛОЖЬ КАК ЕстьТоварыБезНДС,
		|	ЛОЖЬ КАК ЕстьУслугиБезНДС,
		|	ЛОЖЬ КАК ЕстьСчетНаОплатуПокупателю,
		|	ЛОЖЬ КАК ЕстьНДС
		|ПОМЕСТИТЬ СоставДокумента
		|ГДЕ
		|	ЛОЖЬ";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МАКСИМУМ(СоставДокумента.ЕстьТовары) КАК ЕстьТовары,
		|	МАКСИМУМ(СоставДокумента.ЕстьТоварыКомитентов) КАК ЕстьТоварыКомитентов,
		|	МАКСИМУМ(СоставДокумента.ЕстьУслуги) КАК ЕстьУслуги,
		|	МАКСИМУМ(СоставДокумента.ЕстьАгентскиеУслуги) КАК ЕстьАгентскиеУслуги,
		|	МАКСИМУМ(СоставДокумента.ЕстьТара) КАК ЕстьТара,
		|	МАКСИМУМ(СоставДокумента.ЕстьТоварыБезНДС) КАК ЕстьТоварыБезНДС,
		|	МАКСИМУМ(СоставДокумента.ЕстьУслугиБезНДС) КАК ЕстьУслугиБезНДС,
		|	МАКСИМУМ(СоставДокумента.ЕстьСчетНаОплатуПокупателю) КАК ЕстьСчетНаОплатуПокупателю,
		|	МАКСИМУМ(СоставДокумента.ЕстьНДС) КАК ЕстьНДС
		|ПОМЕСТИТЬ СоставДокумента
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 1
		|		ИСТИНА КАК ЕстьТовары,
		|		ЛОЖЬ КАК ЕстьТоварыКомитентов,
		|		ЛОЖЬ КАК ЕстьУслуги,
		|		ЛОЖЬ КАК ЕстьАгентскиеУслуги,
		|		ЛОЖЬ КАК ЕстьТара,
		|		ЛОЖЬ КАК ЕстьТоварыБезНДС,
		|		ЛОЖЬ КАК ЕстьУслугиБезНДС,
		|		ТаблицаДокумента.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка) КАК ЕстьСчетНаОплатуПокупателю,
		|		ЛОЖЬ КАК ЕстьНДС
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СчетУчета В(&СчетаТоваровКомитентов)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ТаблицаДокумента.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка),
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Услуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ТаблицаДокумента.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка),
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ТаблицаДокумента.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка),
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.ВозвратнаяТара КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС В(&СтавкиБезНДС)
		|		И НЕ ТаблицаДокумента.СчетУчета В (&СчетаТоваровКомитентов)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Услуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС В(&СтавкиБезНДС)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Услуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС))КАК СоставДокумента";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.Контрагент.СтранаРегистрации КАК СтранаРегистрации,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОплатаЧерезКомиссионера
	|			ТОГДА Реквизиты.Комиссионер
	|		ИНАЧЕ Реквизиты.Перевозчик
	|	КОНЕЦ КАК Перевозчик,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОплатаЧерезКомиссионера
	|			ТОГДА Реквизиты.ДоговорКомиссионера
	|		ИНАЧЕ ЕСТЬNULL(СпособыДоставки.ДоговорКонтрагента, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ КАК ПеревозчикДоговор,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОплатаЧерезКомиссионера
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЕСТЬNULL(СпособыДоставки.ОплатаПриПолучении, ЛОЖЬ)
	|	КОНЕЦ КАК ОплатаПриПолучении,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОплатаЧерезКомиссионера
	|			ТОГДА Реквизиты.СчетУчетаРасчетовСКомиссионером
	|		ИНАЧЕ ЕСТЬNULL(СпособыДоставки.СчетУчетаРасчетов, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ КАК СчетРасчетовСПеревозчиком,
	|	Реквизиты.ОплатаЧерезКомиссионера КАК ОплатаЧерезКомиссионера,
	|	Реквизиты.Комиссионер КАК Комиссионер,
	|	Реквизиты.ДоговорКомиссионера КАК ДоговорКомиссионера,
	|	Реквизиты.СчетУчетаРасчетовСКомиссионером КАК СчетУчетаРасчетовСКомиссионером,
	|	Реквизиты.СчетУчетаРасчетовСКомиссионеромПоАвансам КАК СчетУчетаРасчетовСКомиссионеромПоАвансам,
	|	Реквизиты.СуммаДокумента КАК СуммаДокумента,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	Реквизиты.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Реквизиты.ДокументБезНДС КАК ДокументБезНДС,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.Склад.ТипСклада КАК ТипСклада,
	|	Реквизиты.ДеятельностьНаТорговомСборе КАК ДеятельностьНаТорговомСборе,
	|	Реквизиты.ДеятельностьНаПатенте КАК ДеятельностьНаПатенте,
	|	Реквизиты.Патент КАК Патент,
	|	Реквизиты.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.СчетУчетаРасчетовПоТаре КАК СчетУчетаРасчетовПоТаре,
	|	Реквизиты.ДоговорКонтрагента.КодРаздел7ДекларацииНДС КАК КодОперацииПоСделке,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДСПокупателем, ЛОЖЬ)
	|				И Реквизиты.Дата >= ДАТАВРЕМЯ(2018, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НДСИсчисляетсяНалоговымАгентом,
	|	ЕСТЬNULL(КодыОперацийРаздела7ДекларацииПоНДС.ОперацияНеПодлежитНалогообложению, ЛОЖЬ) КАК КодОперацииПоСделкеСоответствуетСт149НКРФ,
	|	Реквизиты.ЭлектроннаяТорговаяПлощадка КАК ЭлектроннаяТорговаяПлощадка
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КодыОперацийРаздела7ДекларацииПоНДС КАК КодыОперацийРаздела7ДекларацииПоНДС
	|		ПО Реквизиты.ДоговорКонтрагента.КодРаздел7ДекларацииНДС = КодыОперацийРаздела7ДекларацииПоНДС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыДоставки КАК СпособыДоставки
	|		ПО Реквизиты.СпособДоставки = СпособыДоставки.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Реквизиты.ДокументБезНДС КАК ДокументБезНДС,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.Комиссионер КАК Комиссионер,
	|	Реквизиты.Перевозчик КАК Перевозчик,
	|	Реквизиты.ПеревозчикДоговор КАК ПеревозчикДоговор,
	|	Реквизиты.ОплатаПриПолучении КАК ОплатаПриПолучении,
	|	Реквизиты.СчетРасчетовСПеревозчиком КАК СчетРасчетовСПеревозчиком,
	|	Реквизиты.ОплатаЧерезКомиссионера КАК ОплатаЧерезКомиссионера,
	|	Реквизиты.СуммаДокумента КАК СуммаДокумента,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	Реквизиты.ДеятельностьНаТорговомСборе КАК ДеятельностьНаТорговомСборе,
	|	Реквизиты.ДеятельностьНаПатенте КАК ДеятельностьНаПатенте,
	|	Реквизиты.Патент КАК Патент,
	|	Реквизиты.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
	|	Реквизиты.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
	|		ИЛИ ЕСТЬNULL(СоставДокумента.ЕстьСчетНаОплатуПокупателю, ЛОЖЬ) КАК ЕстьСчетНаОплатуПокупателю,
	|	""Реализация"" КАК ТипСписания,
	|	Реквизиты.Ссылка КАК ДокументРеализации,
	|	ЕСТЬNULL(СоставДокумента.ЕстьТовары, ЛОЖЬ) КАК ЕстьТовары,
	|	ЕСТЬNULL(СоставДокумента.ЕстьТоварыКомитентов, ЛОЖЬ) КАК ЕстьТоварыКомитентов,
	|	ЕСТЬNULL(СоставДокумента.ЕстьУслуги, ЛОЖЬ) КАК ЕстьУслуги,
	|	ЕСТЬNULL(СоставДокумента.ЕстьАгентскиеУслуги, ЛОЖЬ) КАК ЕстьАгентскиеУслуги,
	|	ЕСТЬNULL(СоставДокумента.ЕстьТара, ЛОЖЬ) КАК ЕстьТара,
	|	ЕСТЬNULL(СоставДокумента.ЕстьТоварыБезНДС, ЛОЖЬ) КАК ЕстьТоварыБезНДС,
	|	ЕСТЬNULL(СоставДокумента.ЕстьУслугиБезНДС, ЛОЖЬ) КАК ЕстьУслугиБезНДС,
	|	ЕСТЬNULL(СоставДокумента.ЕстьНДС, ЛОЖЬ) КАК ЕстьНДС,
	|	Реквизиты.КодОперацииПоСделке КАК КодОперацииПоСделке,
	|	Реквизиты.НДСИсчисляетсяНалоговымАгентом КАК НДСИсчисляетсяНалоговымАгентом,
	|	Реквизиты.СтранаРегистрации КАК СтранаРегистрации,
	|	Реквизиты.ЭлектроннаяТорговаяПлощадка КАК ЭлектроннаяТорговаяПлощадка
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоставДокумента КАК СоставДокумента
	|		ПО (ИСТИНА)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты)

	ТекстЗапроса = "";
	
	Если Реквизиты.ЕстьТовары ИЛИ Реквизиты.ЕстьУслуги Тогда	
		НомераТаблиц.Вставить("ВременнаяТаблицаСчетаЕНВД", НомераТаблиц.Количество());
		Если Реквизиты.ПлательщикЕНВД Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	СчетаДоходовИРасходовЕНВД.Счет
			|ПОМЕСТИТЬ СчетаЕНВД
			|ИЗ
			|	РегистрСведений.СчетаДоходовИРасходовЕНВД КАК СчетаДоходовИРасходовЕНВД
			|ГДЕ
			|	СчетаДоходовИРасходовЕНВД.Счет В ИЕРАРХИИ(&СчетВыручка)
			|	И НЕ СчетаДоходовИРасходовЕНВД.Счет.ЗапретитьИспользоватьВПроводках";
		Иначе
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	NULL КАК Счет
			|ПОМЕСТИТЬ СчетаЕНВД
			|ГДЕ
			|	ЛОЖЬ";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Реквизиты.ЕстьТовары Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаТовары", НомераТаблиц.Количество());
		
		Если Реквизиты.ЕстьТоварыБезНДС И (Реквизиты.ПлательщикНДС
			ИЛИ УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Реквизиты.Организация, Реквизиты.Период)) Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ТаблицаТовары.Количество КАК Количество,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
			|			ТОГДА ТаблицаТовары.Сумма
			|		ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
			|	КОНЕЦ КАК СуммаВзаиморасчетов,
			|	ТаблицаТовары.СуммаНДС КАК СуммаНДСВзаиморасчетов,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
			|			ТОГДА ТаблицаТовары.Сумма
			|		ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
			|	КОНЕЦ КАК СуммаРуб,
			|	ТаблицаТовары.СуммаНДС КАК СуммаНДСРуб,
			|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
			|	ТаблицаТовары.НомерГТД КАК НомерГТД,
			|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ТаблицаТовары.СчетУчета КАК СчетУчета,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.СчетУчета В (&СчетаТоваровКомитентов)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоКомиссия,
			|	ТаблицаТовары.ПереданныеСчетУчета КАК ПереданныеСчетУчета,
			|	ТаблицаТовары.СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации,
			|	ТаблицаТовары.СчетДоходов КАК СчетДоходов,
			|	ВЫБОР
			|		КОГДА СчетаЕНВД.Счет ЕСТЬ НЕ NULL 
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ДоходЕНВД,
			|	ТаблицаТовары.СчетРасходов КАК СчетРасходов,
			|	ТаблицаТовары.Субконто КАК Субконто,
			|	ТаблицаТовары.ДокументОприходования КАК ДокументОприходования,
			|	ТаблицаТовары.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
			|	ТаблицаТовары.Себестоимость КАК Себестоимость,
			|	ЕСТЬNULL(КлассификаторТНВЭД.СырьевойТовар, ЛОЖЬ) КАК СырьевойТовар,
			|	ЕСТЬNULL(СправочникНоменклатура.КодРаздел7ДекларацииНДС, ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка)) КАК КодРаздел7ДекларацииНДС,
			|	ЕСТЬNULL(КодыОперацийРаздела7ДекларацииПоНДС.ОперацияНеПодлежитНалогообложению, ЛОЖЬ) КАК КодСоответствуетСт149НКРФ,
			|	ЕСТЬNULL(КодыОперацийРаздела7ДекларацииПоНДС.ВключаетсяВРеестрПодтверждающихДокументов, ЛОЖЬ) КАК КодВключаетсяВРеестр,
			|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ТаблицаТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
			|	ТаблицаТовары.НалоговаяБазаНДС КАК НалоговаяБазаНДС,
			|	ТаблицаТовары.СуммаАкциза КАК СуммаАкциза
			|ПОМЕСТИТЬ ТаблицаТовары
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
			|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаЕНВД КАК СчетаЕНВД
			|		ПО ТаблицаТовары.СчетДоходов = СчетаЕНВД.Счет
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
			|			ПО СправочникНоменклатура.КодТНВЭД = КлассификаторТНВЭД.Ссылка
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КодыОперацийРаздела7ДекларацииПоНДС КАК КодыОперацийРаздела7ДекларацииПоНДС
			|			ПО СправочникНоменклатура.КодРаздел7ДекларацииНДС = КодыОперацийРаздела7ДекларацииПоНДС.Ссылка
			|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
			|ГДЕ
			|	ТаблицаТовары.Ссылка = &Ссылка"
			+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		Иначе
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ТаблицаТовары.Количество КАК Количество,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
			|			ТОГДА ТаблицаТовары.Сумма
			|		ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
			|	КОНЕЦ КАК СуммаВзаиморасчетов,
			|	ТаблицаТовары.СуммаНДС КАК СуммаНДСВзаиморасчетов,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
			|			ТОГДА ТаблицаТовары.Сумма
			|		ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
			|	КОНЕЦ КАК СуммаРуб,
			|	ТаблицаТовары.СуммаНДС КАК СуммаНДСРуб,
			|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
			|	ТаблицаТовары.НомерГТД КАК НомерГТД,
			|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
			|	ТаблицаТовары.СчетУчета КАК СчетУчета,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.СчетУчета В (&СчетаТоваровКомитентов)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоКомиссия,
			|	ТаблицаТовары.ПереданныеСчетУчета КАК ПереданныеСчетУчета,
			|	ТаблицаТовары.СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации,
			|	ТаблицаТовары.СчетДоходов КАК СчетДоходов,
			|	ВЫБОР
			|		КОГДА СчетаЕНВД.Счет ЕСТЬ НЕ NULL 
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ДоходЕНВД,
			|	ТаблицаТовары.СчетРасходов КАК СчетРасходов,
			|	ТаблицаТовары.Субконто КАК Субконто,
			|	ТаблицаТовары.ДокументОприходования КАК ДокументОприходования,
			|	ТаблицаТовары.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
			|	ТаблицаТовары.Себестоимость КАК Себестоимость,
			|	ЛОЖЬ КАК СырьевойТовар,
			|	ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка) КАК КодРаздел7ДекларацииНДС,
			|	ЛОЖЬ КАК КодСоответствуетСт149НКРФ,
			|	ЛОЖЬ КАК КодВключаетсяВРеестр,
			|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ТаблицаТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
			|	ТаблицаТовары.НалоговаяБазаНДС КАК НалоговаяБазаНДС,
			|	ТаблицаТовары.СуммаАкциза КАК СуммаАкциза
			|ПОМЕСТИТЬ ТаблицаТовары
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
			|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаЕНВД КАК СчетаЕНВД
			|		ПО ТаблицаТовары.СчетДоходов = СчетаЕНВД.Счет
			|ГДЕ
			|	ТаблицаТовары.Ссылка = &Ссылка"
			+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		КонецЕсли;
	КонецЕсли;
	
	Если Реквизиты.ЕстьТара Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаВозвратнаяТара", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаВозвратнаяТара.Ссылка,
		|	ТаблицаВозвратнаяТара.НомерСтроки КАК НомерСтроки,
		|	ТаблицаВозвратнаяТара.Номенклатура,
		|	ТаблицаВозвратнаяТара.Количество,
		|	ТаблицаВозвратнаяТара.СчетНаОплатуПокупателю,
		|	ТаблицаВозвратнаяТара.Сумма КАК СуммаВзаиморасчетов,
		|	ТаблицаВозвратнаяТара.Сумма КАК СуммаРуб,
		|	ТаблицаВозвратнаяТара.СчетУчета
		|ПОМЕСТИТЬ ТаблицаВозвратнаяТара
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.ВозвратнаяТара КАК ТаблицаВозвратнаяТара
		|ГДЕ
		|	ТаблицаВозвратнаяТара.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаУслуги", НомераТаблиц.Количество());
		
		Если Реквизиты.ЕстьУслугиБезНДС И (Реквизиты.ПлательщикНДС
			ИЛИ УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Реквизиты.Организация, Реквизиты.Период)) Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ТаблицаУслуги.Ссылка КАК Ссылка,
			|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
			|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
			|	ТаблицаУслуги.Количество КАК Количество,
			|	ТаблицаУслуги.Содержание КАК Содержание,
			|	ВЫБОР
			|		КОГДА ТаблицаУслуги.Ссылка.СуммаВключаетНДС
			|			ТОГДА ТаблицаУслуги.Сумма
			|		ИНАЧЕ ТаблицаУслуги.Сумма + ТаблицаУслуги.СуммаНДС
			|	КОНЕЦ КАК СуммаВзаиморасчетов,
			|	ТаблицаУслуги.СуммаНДС КАК СуммаНДСВзаиморасчетов,
			|	ВЫБОР
			|		КОГДА ТаблицаУслуги.Ссылка.СуммаВключаетНДС
			|			ТОГДА ТаблицаУслуги.Сумма
			|		ИНАЧЕ ТаблицаУслуги.Сумма + ТаблицаУслуги.СуммаНДС
			|	КОНЕЦ КАК СуммаРуб,
			|	ТаблицаУслуги.СуммаНДС КАК СуммаНДСРуб,
			|	ТаблицаУслуги.СтавкаНДС КАК СтавкаНДС,
			|	ТаблицаУслуги.СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации,
			|	ТаблицаУслуги.СчетДоходов КАК СчетДоходов,
			|	ВЫБОР
			|		КОГДА СчетаЕНВД.Счет ЕСТЬ НЕ NULL 
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ДоходЕНВД,
			|	ТаблицаУслуги.СчетРасходов КАК СчетРасходов,
			|	ТаблицаУслуги.Субконто КАК Субконто,
			|	ТаблицаУслуги.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
			|	ЕСТЬNULL(СправочникНоменклатура.КодРаздел7ДекларацииНДС, ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка)) КАК КодРаздел7ДекларацииНДС,
			|	ЕСТЬNULL(КодыОперацийРаздела7ДекларацииПоНДС.ОперацияНеПодлежитНалогообложению, ЛОЖЬ) КАК КодСоответствуетСт149НКРФ,
			|	ЕСТЬNULL(КодыОперацийРаздела7ДекларацииПоНДС.ВключаетсяВРеестрПодтверждающихДокументов, ЛОЖЬ) КАК КодВключаетсяВРеестр,
			|	ТаблицаУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
			|ПОМЕСТИТЬ ТаблицаУслуги
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Услуги КАК ТаблицаУслуги
			|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаЕНВД КАК СчетаЕНВД
			|		ПО ТаблицаУслуги.СчетДоходов = СчетаЕНВД.Счет
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КодыОперацийРаздела7ДекларацииПоНДС КАК КодыОперацийРаздела7ДекларацииПоНДС
			|			ПО СправочникНоменклатура.КодРаздел7ДекларацииНДС = КодыОперацийРаздела7ДекларацииПоНДС.Ссылка
			|		ПО ТаблицаУслуги.Номенклатура = СправочникНоменклатура.Ссылка
			|ГДЕ
			|	ТаблицаУслуги.Ссылка = &Ссылка"
			+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		Иначе
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ТаблицаУслуги.Ссылка КАК Ссылка,
			|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
			|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
			|	ТаблицаУслуги.Количество КАК Количество,
			|	ТаблицаУслуги.Содержание КАК Содержание,
			|	ВЫБОР
			|		КОГДА ТаблицаУслуги.Ссылка.СуммаВключаетНДС
			|			ТОГДА ТаблицаУслуги.Сумма
			|		ИНАЧЕ ТаблицаУслуги.Сумма + ТаблицаУслуги.СуммаНДС
			|	КОНЕЦ КАК СуммаВзаиморасчетов,
			|	ТаблицаУслуги.СуммаНДС КАК СуммаНДСВзаиморасчетов,
			|	ВЫБОР
			|		КОГДА ТаблицаУслуги.Ссылка.СуммаВключаетНДС
			|			ТОГДА ТаблицаУслуги.Сумма
			|		ИНАЧЕ ТаблицаУслуги.Сумма + ТаблицаУслуги.СуммаНДС
			|	КОНЕЦ КАК СуммаРуб,
			|	ТаблицаУслуги.СуммаНДС КАК СуммаНДСРуб,
			|	ТаблицаУслуги.СтавкаНДС КАК СтавкаНДС,
			|	ТаблицаУслуги.СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации,
			|	ТаблицаУслуги.СчетДоходов КАК СчетДоходов,
			|	ВЫБОР
			|		КОГДА СчетаЕНВД.Счет ЕСТЬ НЕ NULL 
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ДоходЕНВД,
			|	ТаблицаУслуги.СчетРасходов КАК СчетРасходов,
			|	ТаблицаУслуги.Субконто КАК Субконто,
			|	ТаблицаУслуги.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
			|	ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка) КАК КодРаздел7ДекларацииНДС,
			|	ЛОЖЬ КАК КодСоответствуетСт149НКРФ,
			|	ЛОЖЬ КАК КодВключаетсяВРеестр,
			|	ТаблицаУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
			|ПОМЕСТИТЬ ТаблицаУслуги
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Услуги КАК ТаблицаУслуги
			|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаЕНВД КАК СчетаЕНВД
			|		ПО ТаблицаУслуги.СчетДоходов = СчетаЕНВД.Счет
			|ГДЕ
			|	ТаблицаУслуги.Ссылка = &Ссылка"
			+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		КонецЕсли;
	КонецЕсли;
	
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаАгентскиеУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаАгентскиеУслуги.Ссылка,
		|	ТаблицаАгентскиеУслуги.НомерСтроки КАК НомерСтроки,
		|	ТаблицаАгентскиеУслуги.Номенклатура,
		|	ТаблицаАгентскиеУслуги.Содержание,
		|	ТаблицаАгентскиеУслуги.Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаАгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаАгентскиеУслуги.Сумма
		|		ИНАЧЕ ТаблицаАгентскиеУслуги.Сумма + ТаблицаАгентскиеУслуги.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаАгентскиеУслуги.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаАгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаАгентскиеУслуги.Сумма
		|		ИНАЧЕ ТаблицаАгентскиеУслуги.Сумма + ТаблицаАгентскиеУслуги.СуммаНДС
		|	КОНЕЦ КАК СуммаРуб,
		|	ТаблицаАгентскиеУслуги.СуммаНДС КАК СуммаНДСРуб,
		|	ТаблицаАгентскиеУслуги.СтавкаНДС,
		|	ТаблицаАгентскиеУслуги.Контрагент,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ТаблицаАгентскиеУслуги.СчетРасчетов,
		|	ТаблицаАгентскиеУслуги.СчетНаОплатуПокупателю,
		|	ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка) КАК КодРаздел7ДекларацииНДС,
		|	ЛОЖЬ КАК КодСоответствуетСт149НКРФ,
		|	ЛОЖЬ КАК КодВключаетсяВРеестр,
		|	ТаблицаАгентскиеУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ТаблицаАгентскиеУслуги
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК ТаблицаАгентскиеУслуги
		|ГДЕ
		|	ТаблицаАгентскиеУслуги.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ПодготовитьТаблицыДокументаРасчетыВВалюте(Запрос, Реквизиты)
	
	НомераТаблиц = Новый Структура;
	Запрос.УстановитьПараметр("НДСИсчисляетсяНалоговымАгентом", Реквизиты.НДСИсчисляетсяНалоговымАгентом);
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокументаРасчетыВВалюте(НомераТаблиц, Реквизиты);
	Результат    = Запрос.ВыполнитьПакет();
	
	ТекстЗапроса = "";
	Если Реквизиты.ЕстьТовары Тогда
		СуммыТаблицыТовары = Результат[НомераТаблиц["СуммыТаблицыТовары"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыТовары, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыТовары", СуммыТаблицыТовары);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеТовары();
	КонецЕсли;
	Если Реквизиты.ЕстьТара Тогда
		СуммыТаблицыТара = Результат[НомераТаблиц["СуммыТаблицыТара"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыТара, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыТара", СуммыТаблицыТара);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеТара();
	КонецЕсли;
	Если Реквизиты.ЕстьУслуги Тогда
		СуммыТаблицыУслуги = Результат[НомераТаблиц["СуммыТаблицыУслуги"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыУслуги, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыУслуги", СуммыТаблицыУслуги);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеУслуги();
	КонецЕсли;
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		СуммыТаблицыАгентскиеУслуги = Результат[НомераТаблиц["СуммыТаблицыАгентскиеУслуги"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыАгентскиеУслуги, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыАгентскиеУслуги", СуммыТаблицыАгентскиеУслуги);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеАгентскиеУслуги();
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результат    = Запрос.ВыполнитьПакет();
	
КонецПроцедуры

Функция ТекстЗапросаВременныеТаблицыДокументаРасчетыВВалюте(НомераТаблиц, Реквизиты)

	ТекстЗапроса = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
	
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаТовары", "ПОМЕСТИТЬ ВременнаяТаблицаТовары");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаТовары.Ссылка = &Ссылка", "ТаблицаТовары.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыТовары", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.НомерСтроки,
		|	ВременнаяТаблицаТовары.СтавкаНДС,
		|	ВременнаяТаблицаТовары.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаТовары.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаТовары.СуммаРуб,
		|	ВременнаяТаблицаТовары.СуммаНДСРуб,
		|	ВременнаяТаблицаТовары.КодРаздел7ДекларацииНДС,
		|	ВременнаяТаблицаТовары.КодСоответствуетСт149НКРФ,
		|	ВременнаяТаблицаТовары.КодВключаетсяВРеестр,
		|	ВременнаяТаблицаТовары.ИдентификаторСтроки,
		|	ВременнаяТаблицаТовары.ПрослеживаемыйКомплект,
		|	ВременнаяТаблицаТовары.НалоговаяБазаНДС
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьТара Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаВозвратнаяТара", "ПОМЕСТИТЬ ВременнаяТаблицаТара");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаВозвратнаяТара.Ссылка = &Ссылка", "ТаблицаВозвратнаяТара.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыТара", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаТара.НомерСтроки,
		|	ВременнаяТаблицаТара.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаТара.СуммаРуб
		|ИЗ
		|	ВременнаяТаблицаТара КАК ВременнаяТаблицаТара"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаУслуги", "ПОМЕСТИТЬ ВременнаяТаблицаУслуги");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаУслуги.Ссылка = &Ссылка", "ТаблицаУслуги.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаУслуги.НомерСтроки,
		|	ВременнаяТаблицаУслуги.СтавкаНДС,
		|	ВременнаяТаблицаУслуги.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаУслуги.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаУслуги.СуммаРуб,
		|	ВременнаяТаблицаУслуги.СуммаНДСРуб,
		|	ВременнаяТаблицаУслуги.КодРаздел7ДекларацииНДС,
		|	ВременнаяТаблицаУслуги.КодСоответствуетСт149НКРФ,
		|	ВременнаяТаблицаУслуги.КодВключаетсяВРеестр,
		|	ВременнаяТаблицаУслуги.ИдентификаторСтроки
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаАгентскиеУслуги", "ПОМЕСТИТЬ ВременнаяТаблицаАгентскиеУслуги");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаАгентскиеУслуги.Ссылка = &Ссылка", "ТаблицаАгентскиеУслуги.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыАгентскиеУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаАгентскиеУслуги.НомерСтроки,
		|	ВременнаяТаблицаАгентскиеУслуги.СтавкаНДС,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаРуб,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаНДСРуб,
		|	ВременнаяТаблицаАгентскиеУслуги.КодРаздел7ДекларацииНДС,
		|	ВременнаяТаблицаАгентскиеУслуги.КодСоответствуетСт149НКРФ,
		|	ВременнаяТаблицаАгентскиеУслуги.КодВключаетсяВРеестр,
		|	ВременнаяТаблицаАгентскиеУслуги.ИдентификаторСтроки
		|ИЗ
		|	ВременнаяТаблицаАгентскиеУслуги КАК ВременнаяТаблицаАгентскиеУслуги"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРасчетыВВалютеТовары()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыТовары.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыТовары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаРуб КАК СуммаРуб,
	|	СуммыТаблицыТовары.СуммаНДСРуб КАК СуммаНДСРуб
	|ПОМЕСТИТЬ СуммыТаблицыТовары
	|ИЗ
	|	&СуммыТаблицыТовары КАК СуммыТаблицыТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаТовары.Ссылка КАК Ссылка,
	|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаТовары.Количество КАК Количество,
	|	СуммыТаблицыТовары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаРуб КАК СуммаРуб,
	|	ВЫБОР
	|		КОГДА &НДСИсчисляетсяНалоговымАгентом
	|			ТОГДА 0
	|		ИНАЧЕ СуммыТаблицыТовары.СуммаНДСРуб
	|	КОНЕЦ КАК СуммаНДСРуб,
	|	ВременнаяТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаТовары.НомерГТД КАК НомерГТД,
	|	ВременнаяТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВременнаяТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ВременнаяТаблицаТовары.ЭтоКомиссия КАК ЭтоКомиссия,
	|	ВременнаяТаблицаТовары.ПереданныеСчетУчета КАК ПереданныеСчетУчета,
	|	ВременнаяТаблицаТовары.СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации,
	|	ВременнаяТаблицаТовары.СчетДоходов КАК СчетДоходов,
	|	ВременнаяТаблицаТовары.ДоходЕНВД КАК ДоходЕНВД,
	|	ВременнаяТаблицаТовары.СчетРасходов КАК СчетРасходов,
	|	ВременнаяТаблицаТовары.Субконто КАК Субконто,
	|	ВременнаяТаблицаТовары.ДокументОприходования КАК ДокументОприходования,
	|	ВременнаяТаблицаТовары.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
	|	ВременнаяТаблицаТовары.Себестоимость КАК Себестоимость,
	|	ВременнаяТаблицаТовары.СырьевойТовар КАК СырьевойТовар,
	|	ВременнаяТаблицаТовары.КодРаздел7ДекларацииНДС КАК КодРаздел7ДекларацииНДС,
	|	ВременнаяТаблицаТовары.КодСоответствуетСт149НКРФ КАК КодСоответствуетСт149НКРФ,
	|	ВременнаяТаблицаТовары.КодВключаетсяВРеестр КАК КодВключаетсяВРеестр,
	|	ВременнаяТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВременнаяТаблицаТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	ВременнаяТаблицаТовары.НалоговаяБазаНДС КАК НалоговаяБазаНДС,
	|	ВременнаяТаблицаТовары.СуммаАкциза КАК СуммаАкциза
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыТовары КАК СуммыТаблицыТовары
	|		ПО ВременнаяТаблицаТовары.НомерСтроки = СуммыТаблицыТовары.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеТара()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыТара.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыТара.СуммаВзаиморасчетов,
	|	СуммыТаблицыТара.СуммаРуб
	|ПОМЕСТИТЬ СуммыТаблицыТара
	|ИЗ
	|	&СуммыТаблицыТара КАК СуммыТаблицыТара
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаТара.Ссылка,
	|	ВременнаяТаблицаТара.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаТара.Номенклатура,
	|	ВременнаяТаблицаТара.Количество,
	|	ВременнаяТаблицаТара.СчетНаОплатуПокупателю,
	|	СуммыТаблицыТара.СуммаВзаиморасчетов,
	|	СуммыТаблицыТара.СуммаРуб,
	|	ВременнаяТаблицаТара.СчетУчета
	|ПОМЕСТИТЬ ТаблицаВозвратнаяТара
	|ИЗ
	|	ВременнаяТаблицаТара КАК ВременнаяТаблицаТара
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыТара КАК СуммыТаблицыТара
	|		ПО ВременнаяТаблицаТара.НомерСтроки = СуммыТаблицыТара.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеУслуги()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыУслуги.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаРуб,
	|	СуммыТаблицыУслуги.СуммаНДСРуб
	|ПОМЕСТИТЬ СуммыТаблицыУслуги
	|ИЗ
	|	&СуммыТаблицыУслуги КАК СуммыТаблицыУслуги
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаУслуги.Ссылка,
	|	ВременнаяТаблицаУслуги.НомерСтроки,
	|	ВременнаяТаблицаУслуги.Номенклатура,
	|	ВременнаяТаблицаУслуги.Количество,
	|	ВременнаяТаблицаУслуги.Содержание,
	|	СуммыТаблицыУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаРуб,
	|	СуммыТаблицыУслуги.СуммаНДСРуб,
	|	ВременнаяТаблицаУслуги.СтавкаНДС,
	|	ВременнаяТаблицаУслуги.СчетУчетаНДСПоРеализации,
	|	ВременнаяТаблицаУслуги.СчетДоходов,
	|	ВременнаяТаблицаУслуги.ДоходЕНВД,
	|	ВременнаяТаблицаУслуги.СчетРасходов,
	|	ВременнаяТаблицаУслуги.Субконто,
	|	ВременнаяТаблицаУслуги.КодРаздел7ДекларацииНДС,
	|	ВременнаяТаблицаУслуги.КодСоответствуетСт149НКРФ,
	|	ВременнаяТаблицаУслуги.СчетНаОплатуПокупателю,
	|	ВременнаяТаблицаУслуги.КодВключаетсяВРеестр,
	|	ВременнаяТаблицаУслуги.ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаУслуги
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыУслуги КАК СуммыТаблицыУслуги
	|		ПО ВременнаяТаблицаУслуги.НомерСтроки = СуммыТаблицыУслуги.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеАгентскиеУслуги()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыАгентскиеУслуги.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыАгентскиеУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыАгентскиеУслуги.СуммаРуб,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСРуб
	|ПОМЕСТИТЬ СуммыТаблицыАгентскиеУслуги
	|ИЗ
	|	&СуммыТаблицыАгентскиеУслуги КАК СуммыТаблицыАгентскиеУслуги
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаАгентскиеУслуги.Ссылка,
	|	ВременнаяТаблицаАгентскиеУслуги.НомерСтроки,
	|	ВременнаяТаблицаАгентскиеУслуги.Номенклатура,
	|	ВременнаяТаблицаАгентскиеУслуги.Содержание,
	|	ВременнаяТаблицаАгентскиеУслуги.Количество,
	|	СуммыТаблицыАгентскиеУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыАгентскиеУслуги.СуммаРуб,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСРуб,
	|	ВременнаяТаблицаАгентскиеУслуги.СтавкаНДС,
	|	ВременнаяТаблицаАгентскиеУслуги.Контрагент,
	|	ВременнаяТаблицаАгентскиеУслуги.ДоговорКонтрагента,
	|	ВременнаяТаблицаАгентскиеУслуги.ВалютаВзаиморасчетов,
	|	ВременнаяТаблицаАгентскиеУслуги.ВидДоговора,
	|	ВременнаяТаблицаАгентскиеУслуги.СчетРасчетов,
	|	ВременнаяТаблицаАгентскиеУслуги.КодРаздел7ДекларацииНДС,
	|	ВременнаяТаблицаАгентскиеУслуги.КодСоответствуетСт149НКРФ,
	|	ВременнаяТаблицаАгентскиеУслуги.СчетНаОплатуПокупателю,
	|	ВременнаяТаблицаАгентскиеУслуги.КодВключаетсяВРеестр,
	|	ВременнаяТаблицаАгентскиеУслуги.ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаАгентскиеУслуги
	|ИЗ
	|	ВременнаяТаблицаАгентскиеУслуги КАК ВременнаяТаблицаАгентскиеУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыАгентскиеУслуги КАК СуммыТаблицыАгентскиеУслуги
	|		ПО ВременнаяТаблицаАгентскиеУслуги.НомерСтроки = СуммыТаблицыАгентскиеУслуги.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаСписаниеТоваров(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если НЕ Реквизиты.ЕстьТовары Тогда
		ПараметрыПроведения.Вставить("СписаниеТоваровРеквизиты",     Неопределено);
		ПараметрыПроведения.Вставить("ВыпускПродукцииРеквизиты",     Неопределено);
		ПараметрыПроведения.Вставить("СписаниеТоваровВТСведенияПрослеживаемости", Неопределено);
		ПараметрыПроведения.Вставить("СписаниеТоваровТаблицаТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("СписаниеТоваровРеквизиты",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВыпускПродукцииРеквизиты",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеТоваровВТСведенияПрослеживаемости", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеТоваровТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА &ЭтоВыкупТоваровКомиссионером
	|			ТОГДА Реквизиты.Контрагент
	|		КОГДА &ЭтоПродажаТоваровСоСкладаКомиссионера
	|			ТОГДА Реквизиты.Комиссионер
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Контрагент,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.ПодразделениеОрганизации КАК КорПодразделение,
	|	ВЫБОР
	|		КОГДА &ЭтоДоговорСКомиссионером
	|			ТОГДА ""Передача товаров на комиссию""
	|		КОГДА &ЭтоОтгрузкаБезПереходаПраваСобственности
	|			ТОГДА ""Отгрузка без перехода права собственности""
	|		ИНАЧЕ ""Реализация товаров""
	|	КОНЕЦ КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	&ИспользоватьСчет40 КАК ИспользоватьСчет40
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияПрослеживаемости.Комитент КАК Комитент,
	|	СведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СУММА(СведенияПрослеживаемости.Количество) КАК Количество
	|ПОМЕСТИТЬ СведенияПрослеживаемости
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК СведенияПрослеживаемости
	|ГДЕ
	|	СведенияПрослеживаемости.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияПрослеживаемости.Комитент,
	|	СведенияПрослеживаемости.ИдентификаторСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СведенияПрослеживаемости.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	Реквизиты.Склад КАК Склад,
	|	ТаблицаТовары.ДокументОприходования КАК ДокументОприходования,
	|	ТаблицаТовары.Себестоимость КАК Себестоимость,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ПрослеживаемыйКомплект
	|			ТОГДА ТаблицаТовары.Количество
	|		ИНАЧЕ ЕСТЬNULL(СведенияПрослеживаемости.Количество, ТаблицаТовары.Количество)
	|	КОНЕЦ КАК Количество,
	|	Реквизиты.ПодразделениеОрганизации КАК КорПодразделение,
	|	ВЫБОР
	|		КОГДА СведенияПрослеживаемости.Комитент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЕСТЬNULL(СведенияПрослеживаемости.Комитент, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКомиссии,
	|	&ПустойСчет КАК СчетАвансовСКомитентом,
	|	&ПустойСчет КАК СчетРасчетовСКомитентом,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаРасчетовСКомитентом,
	|	0 КАК СуммаРасчетовСКомитентом,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСчет45
	|			ТОГДА ТаблицаТовары.ПереданныеСчетУчета
	|		ИНАЧЕ ТаблицаТовары.СчетРасходов
	|	КОНЕЦ КАК КорСчетСписания,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСчет45
	|			ТОГДА Реквизиты.Контрагент
	|		ИНАЧЕ ТаблицаТовары.Субконто
	|	КОНЕЦ КАК КорСубконто1,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСчет45
	|			ТОГДА ТаблицаТовары.Номенклатура
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	&ВидКорСубконто1 КАК ВидКорСубконто1,
	|	&ВидКорСубконто2 КАК ВидКорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто3,
	|	ТаблицаТовары.СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПрослеживаемости КАК СведенияПрослеживаемости
	|		ПО ТаблицаТовары.ИдентификаторСтроки = СведенияПрослеживаемости.ИдентификаторСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСписаниеТары(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если НЕ Реквизиты.ЕстьТара Тогда
		ПараметрыПроведения.Вставить("ТараРеквизиты",     Неопределено);
		ПараметрыПроведения.Вставить("ТараТаблицаТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ТараРеквизиты",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТараТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Склад,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.СчетУчетаРасчетовПоТаре,
	|	""Передача тары"" КАК Содержание
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ВозвратнаяТара"" КАК ИмяСписка,
	|	&СинонимТара КАК СинонимСписка,
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаВозвратнаяТара.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВозвратнаяТара.СчетУчета,
	|	ТаблицаВозвратнаяТара.Номенклатура,
	|	Реквизиты.Склад КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
	|	ВЫБОР
	|		КОГДА &ИспользуетсяОтложенноеПроведение
	|			ТОГДА ТаблицаВозвратнаяТара.СуммаРуб
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Себестоимость,
	|	ЛОЖЬ КАК СебестоимостьУказанаВДокументе,
	|	ТаблицаВозвратнаяТара.Количество,
	|	ТаблицаВозвратнаяТара.СуммаРуб КАК СуммаРуб,
	|	ТаблицаВозвратнаяТара.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	Реквизиты.ПодразделениеОрганизации КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКомиссии,
	|	&ПустойСчет КАК СчетАвансовСКомитентом,
	|	&ПустойСчет КАК СчетРасчетовСКомитентом,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаРасчетовСКомитентом,
	|	0 КАК СуммаРасчетовСКомитентом,
	|	Реквизиты.СчетУчетаРасчетовПоТаре КАК КорСчетСписания,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконто3,
	|	НЕОПРЕДЕЛЕНО КАК СчетЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК Субконто1,
	|	НЕОПРЕДЕЛЕНО КАК Субконто2,
	|	НЕОПРЕДЕЛЕНО КАК Субконто3,
	|	Ложь КАК Малоценка
	|ИЗ
	|	ТаблицаВозвратнаяТара КАК ТаблицаВозвратнаяТара
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ТаблицаВозвратнаяТара.СчетУчета.Забалансовый
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	""ВозвратнаяТара"" КАК ИмяСписка,
	|	&СинонимТара КАК СинонимСписка,
	|	Реквизиты.Дата КАК Период,
	|	ТаблицаВозвратнаяТара.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВозвратнаяТара.СчетУчета,
	|	ТаблицаВозвратнаяТара.Номенклатура,
	|	Реквизиты.Склад КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
	|	ВЫБОР
	|		КОГДА &ИспользуетсяОтложенноеПроведение
	|			ТОГДА ТаблицаВозвратнаяТара.СуммаРуб
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Себестоимость,
	|	ЛОЖЬ КАК СебестоимостьУказанаВДокументе,
	|	ТаблицаВозвратнаяТара.Количество,
	|	ТаблицаВозвратнаяТара.СуммаРуб КАК СуммаРуб,
	|	ТаблицаВозвратнаяТара.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	Реквизиты.ПодразделениеОрганизации КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорКомиссии,
	|	&ПустойСчет КАК СчетАвансовСКомитентом,
	|	&ПустойСчет КАК СчетРасчетовСКомитентом,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаРасчетовСКомитентом,
	|	0 КАК СуммаРасчетовСКомитентом,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВозвратнаяТараУПокупателя) КАК КорСчетСписания,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	ТаблицаВозвратнаяТара.Номенклатура КАК КорСубконто2,
	|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура) КАК ВидКорСубконто2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии) КАК ВидКорСубконто3,
	|	НЕОПРЕДЕЛЕНО КАК СчетЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеЗатрат,
	|	НЕОПРЕДЕЛЕНО КАК Субконто1,
	|	НЕОПРЕДЕЛЕНО КАК Субконто2,
	|	НЕОПРЕДЕЛЕНО КАК Субконто3,
	|	Ложь КАК Малоценка
	|ИЗ
	|	ТаблицаВозвратнаяТара КАК ТаблицаВозвратнаяТара
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаВозвратнаяТара.СчетУчета.Забалансовый
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоДоговорСКомиссионером Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовРеквизиты",        Неопределено);
		ПараметрыПроведения.Вставить("ЗачетАвансовТаблицаДокумента", Неопределено);
		ПараметрыПроведения.Вставить("ЗачетАвансовТаблицаАвансов",   Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСуммВзаиморасчетов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовРеквизиты",        НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовТаблицаДокумента", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаРуб) КАК СуммаРуб,
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовКомитента) КАК СуммаВзаиморасчетовКомитента,
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовЕНВД) КАК СуммаВзаиморасчетовЕНВД,
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовТорговыйСбор) КАК СуммаВзаиморасчетовТорговыйСбор,
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовПатент) КАК СуммаВзаиморасчетовПатент
	|ПОМЕСТИТЬ ТаблицаСуммВзаиморасчетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		0 КАК СуммаВзаиморасчетов,
	|		0 КАК СуммаРуб,
	|		0 КАК СуммаВзаиморасчетовКомитента,
	|		0 КАК СуммаВзаиморасчетовЕНВД,
	|		0 КАК СуммаВзаиморасчетовТорговыйСбор,
	|		0 КАК СуммаВзаиморасчетовПатент";
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаТовары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|		ТаблицаТовары.СуммаРуб КАК СуммаРуб,
		|		ВЫБОР
		|			КОГДА ТаблицаТовары.ЭтоКомиссия
		|				ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаВзаиморасчетовКомитента,
		|		ВЫБОР
		|			КОГДА НЕ ТаблицаТовары.ЭтоКомиссия
		|					И НЕ &ДеятельностьНаПатенте
		|					И НЕ &ДеятельностьНаТорговомСборе
		|					И ТаблицаТовары.ДоходЕНВД
		|				ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаВзаиморасчетовЕНВД,
		|		ВЫБОР
		|			КОГДА НЕ ТаблицаТовары.ЭтоКомиссия
		|					И НЕ &ДеятельностьНаПатенте
		|					И &ДеятельностьНаТорговомСборе
		|				ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаВзаиморасчетовТорговыйСбор,
		|		ВЫБОР
		|			КОГДА НЕ ТаблицаТовары.ЭтоКомиссия
		|					И &ДеятельностьНаПатенте
		|				ТОГДА ТаблицаТовары.СуммаВзаиморасчетов
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаВзаиморасчетовПатент
		|	ИЗ
		|		ТаблицаТовары КАК ТаблицаТовары";
	КонецЕсли;
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаУслуги.СуммаВзаиморасчетов,
		|		ТаблицаУслуги.СуммаРуб,
		|		0,
		|		ВЫБОР
		|			КОГДА НЕ &ДеятельностьНаПатенте
		|					И ТаблицаУслуги.ДоходЕНВД
		|				ТОГДА ТаблицаУслуги.СуммаВзаиморасчетов
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		0,
		|		ВЫБОР
		|			КОГДА &ДеятельностьНаПатенте
		|				ТОГДА ТаблицаУслуги.СуммаВзаиморасчетов
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаУслуги КАК ТаблицаУслуги";
	КонецЕсли;
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаАгентскиеУслуги.СуммаВзаиморасчетов,
		|		ТаблицаАгентскиеУслуги.СуммаРуб,
		|		ТаблицаАгентскиеУслуги.СуммаВзаиморасчетов,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + ") КАК ТаблицаСуммВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.ДеятельностьНаТорговомСборе,
	|	Реквизиты.ДеятельностьНаПатенте,
	|	&ПрименяетсяУСН КАК УчитыватьЗадолженностьУСН,
	|	&УчитыватьЗадолженностьУСНПатент КАК УчитыватьЗадолженностьУСНПатент,
	|	""Выбытие"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК ДокументРасчетов,
	|	ВЫБОР
	|		КОГДА &ЭтоОтгрузкаБезПереходаПраваСобственности
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ)
	|						ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиУЕ)
	|					КОГДА ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ОплатаВВалюте, ЛОЖЬ)
	|						ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиВал)
	|					ИНАЧЕ
	|						ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиРуб)
	|				КОНЕЦ
	|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
	|	КОНЕЦ КАК СчетРасчетов,
	|	ВЫБОР 
	|		КОГДА Реквизиты.ОплатаЧерезКомиссионера
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовПоАвансам
	|	КОНЕЦ КАК СчетАвансов,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	Реквизиты.РасчетыВУсловныхЕдиницах,
	|	Реквизиты.УчетАгентскогоНДС,
	|	&РасчетыВВалюте КАК РасчетыВВалюте,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов,
	|	ТаблицаСуммВзаиморасчетов.СуммаРуб,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовКомитента,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовЕНВД,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовТорговыйСбор,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовПатент
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСуммВзаиморасчетов КАК ТаблицаСуммВзаиморасчетов
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов ЕСТЬ НЕ NULL "
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Если Реквизиты.СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовТаблицаАвансов", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ЗачетАвансовТаблицаАвансов",   НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса
		+ "ВЫБРАТЬ
		|	РеализацияТоваровУслугЗачетАвансов.НомерСтроки КАК НомерСтроки,
		|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
		|	Реквизиты.Контрагент,
		|	Реквизиты.ДоговорКонтрагента,
		|	РеализацияТоваровУслугЗачетАвансов.ДокументАванса,
		|	РеализацияТоваровУслугЗачетАвансов.СуммаЗачета
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.ЗачетАвансов КАК РеализацияТоваровУслугЗачетАвансов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	РеализацияТоваровУслугЗачетАвансов.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции 

Функция ТекстЗапросаРеализация(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности
		ИЛИ Реквизиты.ЭтоДоговорСКомиссионером 
		ИЛИ (НЕ Реквизиты.ЕстьТовары И НЕ Реквизиты.ЕстьУслуги И НЕ Реквизиты.ЕстьАгентскиеУслуги) Тогда
		ПараметрыПроведения.Вставить("РеализацияТаблицаДокумента", Неопределено);
		Возврат "";
	КонецЕсли;

	НомераТаблиц.Вставить("ВременнаяТаблицаРеализация", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РеализацияТаблицаДокумента", НомераТаблиц.Количество());

	// Для деятельности по ЕНВД суммы реализаций "Без НДС" не попадают в раздел 7 Декларации по НДС.
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	NULL КАК ИмяСписка,
	|	NULL КАК НомерСтроки,
	|	NULL КАК Номенклатура,
	|	NULL КАК Количество,
	|	NULL КАК СуммаВзаиморасчетов,
	|	NULL КАК СуммаРуб,
	|	NULL КАК СуммаНДСРуб,
	|	NULL КАК СчетУчета,
	|	NULL КАК СчетДоходов,
	|	NULL КАК ДоходЕНВД,
	|	NULL КАК Субконто,
	|	NULL КАК СтавкаНДС,
	|	NULL КАК СчетУчетаНДСПоРеализации,
	|	NULL КАК Ссылка,
	|	NULL КАК ЭтоКомиссия,
	|	NULL КАК ЭтоУслуга,
	|	NULL КАК ЭтоНесырьевойТовар,
	|	NULL КАК Комитент,
	|	NULL КАК ДоговорКомиссии,
	|	NULL КАК СчетРасчетовСКомитентом,
	|	NULL КАК СчетАвансовСКомитентом,
	|	NULL КАК ВалютаРасчетовСКомитентом,
	|	NULL КАК КодРаздел7ДекларацииНДС,
	|	NULL КАК КодСоответствуетСт149НКРФ,
	|	NULL КАК КодВключаетсяВРеестр,
	|	NULL КАК ИдентификаторСтроки,
	|	NULL КАК ПрослеживаемыйКомплект,
	|	NULL КАК НалоговаяБазаНДС,
	|	NULL КАК СуммаАкциза
	|ПОМЕСТИТЬ ВременнаяТаблицаРеализация
	|ГДЕ
	|	ЛОЖЬ";
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаТовары.СуммаРуб КАК СуммаРуб,
		|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
		|	ТаблицаТовары.СчетУчета КАК СчетУчета,
		|	ТаблицаТовары.СчетДоходов КАК СчетДоходов,
		|	ТаблицаТовары.ДоходЕНВД КАК ДоходЕНВД,
		|	ТаблицаТовары.Субконто КАК Субконто,
		|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТовары.СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.ЭтоКомиссия КАК ЭтоКомиссия,
		|	ЛОЖЬ КАК ЭтоУслуга,
		|	НЕ ТаблицаТовары.СырьевойТовар КАК ЭтоНесырьевойТовар,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
		|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК ДоговорКомиссии,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетРасчетовСКомитентом,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетАвансовСКомитентом,
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаРасчетовСКомитентом,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.ДоходЕНВД
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаТовары.КодРаздел7ДекларацииНДС
		|	КОНЕЦ КАК КодРаздел7ДекларацииНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.ДоходЕНВД
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ТаблицаТовары.КодСоответствуетСт149НКРФ
		|	КОНЕЦ КАК КодСоответствуетСт149НКРФ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.ДоходЕНВД
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ТаблицаТовары.КодВключаетсяВРеестр
		|	КОНЕЦ КАК КодВключаетсяВРеестр,
		|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
		|	ТаблицаТовары.НалоговаяБазаНДС КАК НалоговаяБазаНДС,
		|	ТаблицаТовары.СуммаАкциза КАК СуммаАкциза
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары";
	КонецЕсли;
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""Услуги"",
		|	ТаблицаУслуги.НомерСтроки,
		|	ТаблицаУслуги.Номенклатура,
		|	ТаблицаУслуги.Количество,
		|	ТаблицаУслуги.СуммаВзаиморасчетов,
		|	ТаблицаУслуги.СуммаРуб,
		|	ТаблицаУслуги.СуммаНДСРуб,
		|	ТаблицаУслуги.СчетРасходов,
		|	ТаблицаУслуги.СчетДоходов,
		|	ТаблицаУслуги.ДоходЕНВД,
		|	ТаблицаУслуги.Субконто,
		|	ТаблицаУслуги.СтавкаНДС,
		|	ТаблицаУслуги.СчетУчетаНДСПоРеализации,
		|	ТаблицаУслуги.Ссылка,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.ДоходЕНВД
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаУслуги.КодРаздел7ДекларацииНДС
		|	КОНЕЦ КАК КодРаздел7ДекларацииНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.ДоходЕНВД
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ТаблицаУслуги.КодСоответствуетСт149НКРФ
		|	КОНЕЦ КАК КодСоответствуетСт149НКРФ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.ДоходЕНВД
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ТаблицаУслуги.КодВключаетсяВРеестр
		|	КОНЕЦ КАК КодВключаетсяВРеестр,
		|	ТаблицаУслуги.ИдентификаторСтроки,
		|	Неопределено,
		|	0,
		|	0
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги";
	КонецЕсли;
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""АгентскиеУслуги"",
		|	ТаблицаАгентскиеУслуги.НомерСтроки,
		|	ТаблицаАгентскиеУслуги.Номенклатура,
		|	ТаблицаАгентскиеУслуги.Количество,
		|	ТаблицаАгентскиеУслуги.СуммаВзаиморасчетов,
		|	ТаблицаАгентскиеУслуги.СуммаРуб,
		|	ТаблицаАгентскиеУслуги.СуммаНДСРуб,
		|	&ПустойСчет,
		|	ЛОЖЬ,
		|	ТаблицаАгентскиеУслуги.СчетРасчетов,
		|	ТаблицаАгентскиеУслуги.Контрагент,
		|	ТаблицаАгентскиеУслуги.СтавкаНДС,
		|	&ПустойСчет,
		|	ТаблицаАгентскиеУслуги.Ссылка,
		|	ИСТИНА,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ТаблицаАгентскиеУслуги.Контрагент,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента,
		|	ТаблицаАгентскиеУслуги.СчетРасчетов,
		|	ТаблицаАгентскиеУслуги.СчетРасчетов,
		|	ТаблицаАгентскиеУслуги.ВалютаВзаиморасчетов,
		|	ТаблицаАгентскиеУслуги.КодРаздел7ДекларацииНДС КАК КодРаздел7ДекларацииНДС,
		|	ТаблицаАгентскиеУслуги.КодСоответствуетСт149НКРФ КАК КодСоответствуетСт149НКРФ,
		|	ТаблицаАгентскиеУслуги.КодВключаетсяВРеестр КАК КодВключаетсяВРеестр,
		|	ТаблицаАгентскиеУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Неопределено,
		|	0,
		|	0
		|ИЗ
		|	ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() + 
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРеализация.ИмяСписка КАК ИмяСписка,
	|	ВременнаяТаблицаРеализация.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаРеализация.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаРеализация.Количество КАК Количество,
	|	ВременнаяТаблицаРеализация.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВременнаяТаблицаРеализация.СуммаРуб КАК СуммаРуб,
	|	ВременнаяТаблицаРеализация.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ВременнаяТаблицаРеализация.СуммаРуб КАК СуммаБУ,
	|	ВременнаяТаблицаРеализация.СуммаРуб КАК СуммаНУ,
	|	ВременнаяТаблицаРеализация.СчетУчета КАК СчетУчета,
	|	ВременнаяТаблицаРеализация.СчетДоходов КАК СчетДоходов,
	|	ВременнаяТаблицаРеализация.ДоходЕНВД КАК ДоходЕНВД,
	|	ВременнаяТаблицаРеализация.Субконто КАК Субконто,
	|	ВременнаяТаблицаРеализация.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаРеализация.СчетУчетаНДСПоРеализации КАК СчетУчетаНДСПоРеализации,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
	|	Реквизиты.Ссылка КАК КорСубконто3,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.Контрагент КАК Покупатель,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ВременнаяТаблицаРеализация.ЭтоКомиссия КАК ЭтоКомиссия,
	|	ВременнаяТаблицаРеализация.ЭтоУслуга КАК ЭтоУслуга,
	|	ВЫБОР
	|		КОГДА Реквизиты.Дата >= ДАТАВРЕМЯ(2016, 7, 1)
	|			ТОГДА ВременнаяТаблицаРеализация.ЭтоНесырьевойТовар
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНесырьевойТовар,
	|	ВременнаяТаблицаРеализация.Комитент КАК Комитент,
	|	ВременнаяТаблицаРеализация.ДоговорКомиссии КАК ДоговорКомиссии,
	|	Реквизиты.Ссылка КАК ДокументРасчетовСКомитентом,
	|	ВЫБОР
	|		КОГДА &ЗаполнятьДокументРасчетов
	|			ТОГДА Реквизиты.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДокументРасчетов,
	|	Реквизиты.Дата КАК ДатаРеализации,
	|	ВременнаяТаблицаРеализация.СчетРасчетовСКомитентом КАК СчетРасчетовСКомитентом,
	|	ВременнаяТаблицаРеализация.СчетАвансовСКомитентом КАК СчетАвансовСКомитентом,
	|	ВременнаяТаблицаРеализация.ВалютаРасчетовСКомитентом КАК ВалютаРасчетовСКомитентом,
	|	0 КАК СуммаРасчетовСКомитентом,
	|	0 КАК СуммаПоступленияОтКомитента,
	|	ВременнаяТаблицаРеализация.КодРаздел7ДекларацииНДС КАК КодРаздел7ДекларацииНДС,
	|	ВременнаяТаблицаРеализация.КодСоответствуетСт149НКРФ КАК КодСоответствуетСт149НКРФ,
	|	ВременнаяТаблицаРеализация.КодВключаетсяВРеестр КАК КодВключаетсяВРеестр,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРеализация.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				И ВременнаяТаблицаРеализация.ЭтоУслуга
	|				И Реквизиты.Дата >= ДАТАВРЕМЯ(2019, 7, 1)
	|			ТОГДА Реквизиты.КодОперацииПоСделке В (&МассивКодовРеализацийЗаРубеж)
	|						И НЕ ВременнаяТаблицаРеализация.КодСоответствуетСт149НКРФ
	|					ИЛИ ВременнаяТаблицаРеализация.КодРаздел7ДекларацииНДС В (&МассивКодовРеализацийЗаРубеж)
	|						И НЕ Реквизиты.КодОперацииПоСделкеСоответствуетСт149НКРФ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УслугиЗаРубежом,
	|	ВЫБОР
	|		КОГДА &РеализацияЧерезЭлектроннуюТорговуюПлощадкуФизлицуВЕАЭС
	|				И ВременнаяТаблицаРеализация.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				И НЕ ВременнаяТаблицаРеализация.ЭтоУслуга
	|				И Реквизиты.Дата >= ДАТАВРЕМЯ(2024, 7, 1)
	|			ТОГДА 
	|				ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РеализацияТоваровЧерезЭлектроннуюТорговуюПлощадкуФизлицуВЕАЭС,
	|	ВременнаяТаблицаРеализация.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВременнаяТаблицаРеализация.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	ВременнаяТаблицаРеализация.НалоговаяБазаНДС КАК НалоговаяБазаНДС,
	|	ВременнаяТаблицаРеализация.СуммаАкциза КАК СуммаАкциза
	|ИЗ
	|	ВременнаяТаблицаРеализация КАК ВременнаяТаблицаРеализация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИмяСписка,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаЗачетАвансовКомитентов(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ЕстьТоварыКомитентов
		И НЕ Реквизиты.ЕстьАгентскиеУслуги Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовКомитентовРеквизиты", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ЗачетАвансовКомитентовРеквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически) КАК СпособЗачетаАвансов,
	|	ЛОЖЬ КАК УчитыватьЗадолженностьУСН,
	|	ЛОЖЬ КАК УчитыватьЗадолженностьУСНПатент,
	|	""Поступление"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаПереоценкаТоваровВРознице(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если НЕ Реквизиты.ЕстьТовары
		ИЛИ НЕ Реквизиты.УчетПоПродажнойСтоимости 
		ИЛИ Реквизиты.ТипСклада <> Перечисления.ТипыСкладов.РозничныйМагазин Тогда
		ПараметрыПроведения.Вставить("ПереоценкаТоваровВРозницеРеквизиты",     Неопределено);
		ПараметрыПроведения.Вставить("ПереоценкаТоваровВРозницеТаблицаТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПереоценкаТоваровВРозницеРеквизиты",     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПереоценкаТоваровВРозницеТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.Склад,
	|	Реквизиты.ТипСклада КАК ТипСклада
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	&СинонимТовары КАК СинонимСписка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.СчетУчета,
	|	ТаблицаТовары.Номенклатура,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	ТаблицаТовары.Количество,
	|	ТаблицаТовары.СчетРасходов КАК КорСчетСписания,
	|	ТаблицаТовары.Субконто КАК КорСубконтоСписания1,
	|	ТаблицаТовары.СуммаРуб КАК Сумма
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаРеализацияУслуг(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если НЕ Реквизиты.ЕстьУслуги
		ИЛИ Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности Тогда
		ПараметрыПроведения.Вставить("ТаблицаРеализацияУслуг", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ТаблицаРеализацияУслуг", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	""Услуги"" КАК ИмяСписка,
	|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУслуги.СчетРасходов,
	|	ТаблицаУслуги.Номенклатура КАК Услуга,
	|	ТаблицаУслуги.Субконто КАК НоменклатурнаяГруппа,
	|	ТаблицаУслуги.СуммаРуб - ТаблицаУслуги.СуммаНДСРуб КАК Сумма
	|ИЗ
	|	ТаблицаУслуги КАК ТаблицаУслуги
	|ГДЕ
	|	НЕ ТаблицаУслуги.СчетРасходов В (&Субсчета91)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИмяСписка,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции	

Функция ТекстЗапросаТоварыНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	ТекстЗапроса = "";
	
	Если НЕ Реквизиты.ПлательщикНДС Тогда
		ПараметрыПроведения.Вставить("РеквизитыНДСВыпускПродукции", Неопределено);
	Иначе
		НомераТаблиц.Вставить("РеквизитыНДСВыпускПродукции", НомераТаблиц.Количество());
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Дата КАК Период,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
		|	Реквизиты.Склад КАК Склад
		|ИЗ
		|	Реквизиты КАК Реквизиты"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
	Если Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности
		ИЛИ НЕ Реквизиты.ЭтоДоговорСПокупателем 
		ИЛИ НЕ Реквизиты.ЕстьТовары Тогда
		ПараметрыПроведения.Вставить("НДСТоварыРеализация", Неопределено);
	Иначе
		НомераТаблиц.Вставить("НДСТоварыРеализация", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса
		+ "ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимТовары КАК СинонимСписка,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	ТаблицаТовары.Номенклатура,
		|	ВЫБОР
		|		КОГДА &РеквизитыПериод >= ДАТАВРЕМЯ(2016, 7, 1)
		|			ТОГДА ТаблицаТовары.СырьевойТовар
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК СырьевойТовар,
		|	ТаблицаТовары.СтавкаНДС,
		|	ТаблицаТовары.СчетУчета,
		|	ТаблицаТовары.НомерГТД,
		|	ТаблицаТовары.СтранаПроисхождения,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|ГДЕ
		|	НЕ ТаблицаТовары.ЭтоКомиссия
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтрокиДокумента" 
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
		
	Если Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности
		ИЛИ НЕ Реквизиты.ЭтоДоговорСПокупателем 
		ИЛИ НЕ Реквизиты.ЕстьТоварыКомитентов Тогда
		ПараметрыПроведения.Вставить("НДСТоварыНаКомиссииРеализация", Неопределено);
	Иначе
		НомераТаблиц.Вставить("НДСТоварыНаКомиссииРеализация", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса
		+ "ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимТовары КАК СинонимСписка,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.СтавкаНДС,
		|	ТаблицаТовары.СчетУчета,
		|	ТаблицаТовары.НомерГТД,
		|	ТаблицаТовары.СтранаПроисхождения,
		|	ТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.ЭтоКомиссия
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтрокиДокумента"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности
		ИЛИ НЕ Реквизиты.ЭтоДоговорСКомиссионером 
		ИЛИ НЕ Реквизиты.ЕстьТовары Тогда
		ПараметрыПроведения.Вставить("НДСТоварыОтгрузкаКомиссионеру", Неопределено);
	Иначе
		НомераТаблиц.Вставить("НДСТоварыОтгрузкаКомиссионеру", НомераТаблиц.Количество());
		// Ставка НДС при передаче товаров комиссионеру может быть любой и не должна влиять на учет.
		ТекстЗапроса = ТекстЗапроса
		+ "ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимТовары КАК СинонимСписка,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	ТаблицаТовары.Номенклатура,
		|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка) КАК СтавкаНДС,
		|	ТаблицаТовары.СчетУчета,
		|	ТаблицаТовары.НомерГТД,
		|	ТаблицаТовары.СтранаПроисхождения,
		|	ТаблицаТовары.Количество КАК Количество
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|ГДЕ
		|	НЕ ТаблицаТовары.ЭтоКомиссия
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтрокиДокумента" 
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если НЕ Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности 
		ИЛИ НЕ Реквизиты.ЕстьТовары Тогда
		ПараметрыПроведения.Вставить("НДСТоварыОтгрузка", Неопределено);
	Иначе
		НомераТаблиц.Вставить("НДСТоварыОтгрузка", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса
		+ "ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимТовары КАК СинонимСписка,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтрокиДокумента,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТовары.СчетУчета КАК СчетУчета,
		|	ТаблицаТовары.НомерГТД КАК НомерГТД,
		|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ЛОЖЬ КАК ЭтоУслуга,
		|	ВЫБОР
		|		КОГДА Реквизиты.Дата >= ДАТАВРЕМЯ(2016, 7, 1)
		|			ТОГДА НЕ ТаблицаТовары.СырьевойТовар
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоНесырьевойТовар,
		|	ТаблицаТовары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаТовары.СуммаРуб КАК СуммаРуб,
		|	ТаблицаТовары.СуммаРуб КАК СуммаБУ,
		|	ТаблицаТовары.СуммаРуб КАК СуммаНУ,
		|	ТаблицаТовары.СуммаРуб - ТаблицаТовары.СуммаНДСРуб КАК СуммаБезНДСРуб,
		|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
		|	НЕОПРЕДЕЛЕНО КАК СчетДоходов,
		|	НЕОПРЕДЕЛЕНО КАК Субконто,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСНачисленныйПоОтгрузке) КАК СчетУчетаНДСПоРеализации,
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.ДоходЕНВД
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка)
		|		ИНАЧЕ ТаблицаТовары.КодРаздел7ДекларацииНДС
		|	КОНЕЦ КАК КодРаздел7ДекларацииНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.ДоходЕНВД
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ТаблицаТовары.КодСоответствуетСт149НКРФ
		|	КОНЕЦ КАК КодСоответствуетСт149НКРФ,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.ДоходЕНВД
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ТаблицаТовары.КодВключаетсяВРеестр
		|	КОНЕЦ КАК КодВключаетсяВРеестр,
		|	ЛОЖЬ КАК УслугиЗаРубежом,
		|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаТовары.НалоговаяБазаНДС КАК НалоговаяБазаНДС
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаТовары.СчетУчета.Забалансовый
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтрокиДокумента" 
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРублевыеСуммыДокументовВВалюте(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если (НЕ Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности
		И НЕ Реквизиты.ЭтоДоговорСКомиссионером) 
		ИЛИ (НЕ Реквизиты.ЕстьТовары И НЕ Реквизиты.ЕстьУслуги И НЕ Реквизиты.ЕстьАгентскиеУслуги) Тогда
		ПараметрыПроведения.Вставить("РублевыеСуммыДокументаВВалюте", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РублевыеСуммыДокументаВВалюте", НомераТаблиц.Количество());
	
	ТекстЗапроса = "";
	
	Если Реквизиты.ЕстьТовары Тогда
		
		Если Реквизиты.ЭтоДоговорСКомиссионером Тогда
	
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	""Товары"" КАК ИмяСписка,
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.СуммаРуб КАК СуммаБу,
			|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
			|	ТаблицаТовары.СуммаРуб - ТаблицаТовары.СуммаНДСРуб КАК СуммаБезНДСРуб,
			|	ТаблицаТовары.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаТовары КАК ТаблицаТовары";
			
		ИначеЕсли Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности Тогда
			
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	""Товары"" КАК ИмяСписка,
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.СуммаРуб КАК СуммаБу,
			|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
			|	ТаблицаТовары.СуммаРуб - ТаблицаТовары.СуммаНДСРуб КАК СуммаБезНДСРуб,
			|	ТаблицаТовары.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаТовары КАК ТаблицаТовары
			|ГДЕ
			|	ТаблицаТовары.ЭтоКомиссия";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги И НЕ Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности Тогда
		
		Если Реквизиты.ЕстьТовары Тогда
			
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
			
		КонецЕсли;	
		
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	""Услуги"",
		|	ТаблицаУслуги.НомерСтроки,
		|	ТаблицаУслуги.СуммаРуб,
		|	ТаблицаУслуги.СуммаНДСРуб,
		|	ТаблицаУслуги.СуммаРуб - ТаблицаУслуги.СуммаНДСРуб,
		|	ТаблицаУслуги.Ссылка
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги";
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаДанныеРазметкиАУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ПрименяетсяАУСН
		Или Реквизиты.ДокументБезНДС
		//Или Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности
		Или Реквизиты.ЭтоДоговорСКомиссионером 
		Или (Не Реквизиты.ЕстьТовары И Не Реквизиты.ЕстьУслуги И Не Реквизиты.ЕстьАгентскиеУслуги) Тогда
		ПараметрыПроведения.Вставить("РеквизитыАУСН", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСуммНДС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РеквизитыАУСН", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ТаблицаСуммНДС.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаСуммНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		0 КАК СуммаНДС
	|";
	
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТаблицаТовары.СуммаНДСВзаиморасчетов
		|	ИЗ
		|		ТаблицаТовары КАК ТаблицаТовары
		|";
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТаблицаУслуги.СуммаНДСВзаиморасчетов
		|	ИЗ
		|		ТаблицаУслуги КАК ТаблицаУслуги
		|";
	КонецЕсли;
	
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ТекстЗапроса = ТекстЗапроса +
		"
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТаблицаАгентскиеУслуги.СуммаНДСВзаиморасчетов
		|	ИЗ
		|		ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
		") КАК ТаблицаСуммНДС"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВалютаДокумента <> &ВалютаРегламентированногоУчета
	|		И НЕ Реквизиты.РасчетыВУсловныхЕдиницах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РасчетыВВалюте,
	|	Реквизиты.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.СуммаДокумента КАК СуммаДокумента,
	|	ТаблицаСуммНДС.СуммаНДС КАК СуммаНДСДокумента
	|ИЗ
	|	Реквизиты Как Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСуммНДС
	|		ПО ИСТИНА";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Процедура ДобавитьКолонкуСодержание(ТаблицаЗначений, ВидОперации = Неопределено) Экспорт
	
	Если ТаблицаЗначений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаЗначений.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		
		Если ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.СтроительныеРаботы Тогда
			СтрокаТаблицы.Содержание = НСтр("ru = 'Строительные работы'");
		ИначеЕсли СтрокаТаблицы.ЭтоУслуга Тогда
			СтрокаТаблицы.Содержание = НСтр("ru = 'Реализация услуг'");
		Иначе
			СтрокаТаблицы.Содержание = НСтр("ru = 'Реализация товаров'");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// ОТЛОЖЕННОЕ ПРОВЕДЕНИЕ

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Реквизиты.ЭтоОтгрузкаБезПереходаПраваСобственности
		ИЛИ Реквизиты.ЭтоДоговорСКомиссионером
		ИЛИ Реквизиты.ЭтоОтложенноеПроведение
		ИЛИ НЕ ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Реквизиты.Организация, Реквизиты.Период) Тогда
		ПараметрыПроведения.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", Неопределено);
		Возврат "";
	КонецЕсли;

	НомераТаблиц.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", НомераТаблиц.Количество());

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.Дата КАК Дата
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаАгентскиеУслуги.Контрагент,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента,
		|	ТаблицаАгентскиеУслуги.ВалютаВзаиморасчетов,
		|	ТаблицаАгентскиеУслуги.ВидДоговора,
		|	Реквизиты.Дата
		|ИЗ
		|	ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги
		|	ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты
		|		ПО ИСТИНА";
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура ОбработкаОтложенногоПроведения(Параметры, Отказ) Экспорт
	
	ПараметрыПроведения = ПодготовитьПараметрыПроведения(
		Параметры.Регистратор,
		Отказ,
		Параметры.ДоговорКонтрагента);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	

	ТаблицаВзаиморасчетов = УчетВзаиморасчетовОтложенноеПроведение.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		Параметры,
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
		ПараметрыПроведения.ЗачетАвансовТаблицаАвансов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Отказ);

	// Структура таблиц для отражения в налоговом учете УСН
	СтруктураТаблицУСН = Новый Структура("ТаблицаРасчетов",	ТаблицаВзаиморасчетов);

	УчетВзаиморасчетовОтложенноеПроведение.СформироватьДвиженияЗачетАвансов(
		Параметры,
		ТаблицаВзаиморасчетов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Отказ);
	
	УчетВзаиморасчетовОтложенноеПроведение.СформироватьДвиженияУСН(
		Параметры,
		СтруктураТаблицУСН);

КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", "Расходная накладная", 
			ПечатьДокумента(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,"Документ.РеализацияТоваровУслуг.ПФ_MXL_Накладная");
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Акт") Тогда
		ТаблицаСведенийАктаОбОказанииУслуг = ПолучитьТаблицуСведенийАктаОбОказанииУслуг(МассивОбъектов);
		ПараметрыПечати.Вставить("ВидДокументаПечати",  "РеализацияТоваровУслуг");
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_Акт");
		ПараметрыПечати.Вставить("ИмяМакетаПечати",     "Документ.РеализацияТоваровУслуг.ПФ_MXL_Акт");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Акт", "Акт об оказании услуг", 
			ПечатьТорговыхДокументов.ПечатьАктаОбОказанииУслуг(ТаблицаСведенийАктаОбОказанииУслуг, ОбъектыПечати, ПараметрыПечати),, 
					"Документ.РеализацияТоваровУслуг.ПФ_MXL_Акт");
		ПараметрыВывода.Вставить("ФормироватьЭД", Истина);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктНаПередачуПрав") Тогда
		ТаблицаСведенийАктаНаПередачуПрав = ПолучитьТаблицуСведенийАктаНаПередачуПрав(МассивОбъектов);
		ПараметрыПечати.Вставить("ВидДокументаПечати",  "РеализацияТоваровУслуг");
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_АктНаПередачуПрав");
		ПараметрыПечати.Вставить("ИмяМакетаПечати",     "Документ.РеализацияТоваровУслуг.ПФ_MXL_АктНаПередачуПрав");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктНаПередачуПрав", "Акт на передачу прав", 
			ПечатьТорговыхДокументов.ПечатьАктаНаПередачуПрав(ТаблицаСведенийАктаНаПередачуПрав, ОбъектыПечати, ПараметрыПечати),, 
					"Документ.РеализацияТоваровУслуг.ПФ_MXL_АктНаПередачуПрав");
		ПараметрыВывода.Вставить("ФормироватьЭД", Истина);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктОПриемкеВыполненныхСтроительныхРабот") Тогда
		ТаблицаСведенийАктаОПриемкеВыполненныхСтроительныхРабот = ДанныеДляПечатиАктаОПриемкеВыполненныхСтроительныхРабот(МассивОбъектов);
		ПараметрыПечати.Вставить("ВидДокументаПечати",  "РеализацияТоваровУслуг");
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_АктОПриемкеВыполненныхСтроительныхРабот");
		ПараметрыПечати.Вставить("ИмяМакетаПечати",     "Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОПриемкеВыполненныхСтроительныхРабот");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктОПриемкеВыполненныхСтроительныхРабот", "Акт о приемке выполненных строительных работ", 
			ПечатьАктаОПриемкеВыполненныхСтроительныхРабот(ТаблицаСведенийАктаОПриемкеВыполненныхСтроительныхРабот, ОбъектыПечати, ПараметрыПечати),, 
					"Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОПриемкеВыполненныхСтроительныхРабот");
 	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ12") Тогда
		ВключатьУслуги = Истина;
		ТаблицаСведенийТОРГ12 = ПолучитьТаблицуСведенийТОРГ12(МассивОбъектов, ВключатьУслуги);
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_ТОРГ12");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ12", "ТОРГ-12 (Товарная накладная с услугами)", 
			ПечатьТорговыхДокументов.ПечатьТОРГ12(ТаблицаСведенийТОРГ12, ОбъектыПечати, ПараметрыПечати),,"ОбщийМакет.ПФ_MXL_ТОРГ12");
		ПараметрыВывода.Вставить("ФормироватьЭД", Истина);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ12_БезУслуг") Тогда
		ВключатьУслуги = Ложь;
		ТаблицаСведенийТОРГ12 = ПолучитьТаблицуСведенийТОРГ12(МассивОбъектов, ВключатьУслуги);
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_ТОРГ12");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ12_БезУслуг", "ТОРГ-12 (Товарная накладная)",
			ПечатьТорговыхДокументов.ПечатьТОРГ12(ТаблицаСведенийТОРГ12, ОбъектыПечати, ПараметрыПечати),,"ОбщийМакет.ПФ_MXL_ТОРГ12");
		ПараметрыВывода.Вставить("ФормироватьЭД", Истина);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М15") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М15", "М-15 (Накладная)", 
			ПечатьМ15(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,"ОбщийМакет.ПФ_MXL_М15");
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактураКомплект") Тогда
		УчетНДС.ПечатьКомплектаСчетовФактур(КоллекцияПечатныхФорм, МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УниверсальныйПередаточныйДокументКомплект") Тогда
		ИменаФайлов = Неопределено;
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УниверсальныйПередаточныйДокумент", "УПД",
			УчетНДС.ПечатьКомплектаУПД(КоллекцияПечатныхФорм, МассивОбъектов, ОбъектыПечати, ИменаФайлов, ПараметрыПечати, ПараметрыВывода),,, ИменаФайлов);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктРеализация") Тогда
		МассивДокументов = Новый Массив;
		Для каждого Документ Из МассивОбъектов Цикл
			Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") 
				И Документ.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги Тогда
				МассивДокументов.Добавить(Документ);
			КонецЕсли;
		КонецЦикла;
		ТаблицаСведенийАктаОбОказанииУслуг = ПолучитьТаблицуСведенийАктаОбОказанииУслуг(МассивДокументов);
		ПараметрыПечати.Вставить("ВидДокументаПечати",  "РеализацияТоваровУслуг");
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_Акт");
		ПараметрыПечати.Вставить("ИмяМакетаПечати",     "Документ.РеализацияТоваровУслуг.ПФ_MXL_Акт");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктРеализация", "Акт (реализация)", 
			ПечатьТорговыхДокументов.ПечатьАктаОбОказанииУслуг(ТаблицаСведенийАктаОбОказанииУслуг, ОбъектыПечати, ПараметрыПечати),, 
					"Документ.РеализацияТоваровУслуг.ПФ_MXL_Акт");
		ПараметрыВывода.Вставить("ФормироватьЭД", Истина);
	КонецЕсли;
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НакладнаяРеализация") Тогда
		ВключатьУслуги = Истина;
		МассивДокументов = Новый Массив;
		Для каждого Документ Из МассивОбъектов Цикл
			Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") 
				И Документ.ВидОперации <> Перечисления.ВидыОперацийРеализацияТоваров.Услуги Тогда
				МассивДокументов.Добавить(Документ);
			КонецЕсли;
		КонецЦикла;
		ТаблицаСведенийТОРГ12 = ПолучитьТаблицуСведенийТОРГ12(МассивДокументов, ВключатьУслуги);
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_ТОРГ12");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "НакладнаяРеализация", "Накладная (реализация)", 
			ПечатьТорговыхДокументов.ПечатьТОРГ12(ТаблицаСведенийТОРГ12, ОбъектыПечати, ПараметрыПечати),,"ОбщийМакет.ПФ_MXL_ТОРГ12");
		ПараметрыВывода.Вставить("ФормироватьЭД", Истина);
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов,
		КоллекцияПечатныхФорм,
		ОбъектыПечати,
		ПараметрыВывода);
	
КонецПроцедуры

Процедура УстановитьПараметрТабличногоДокумента(ОбластьМакета, ИмяПараметра, ЗначениеПараметра)
	ОбластьМакета.Параметры.Заполнить(Новый Структура(ИмяПараметра, ЗначениеПараметра));
КонецПроцедуры


// Функция формирует табличный документ с печатной формой накладной,
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьДокумента(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_Накладная";
	
	ДополнительнаяКолонкаПечатныхФормДокументов = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если НЕ ЗначениеЗаполнено(ДополнительнаяКолонкаПечатныхФормДокументов) Тогда
		ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить;
	КонецЕсли;
	ВыводитьКоды = ДополнительнаяКолонкаПечатныхФормДокументов <> Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", ДополнительнаяКолонкаПечатныхФормДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументРеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	ДокументРеализацияТоваровУслуг.Номер КАК Номер,
	|	ДокументРеализацияТоваровУслуг.Дата КАК Дата,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДокументРеализацияТоваровУслуг.Контрагент КАК Получатель,
	|	ДокументРеализацияТоваровУслуг.Организация КАК Организация,
	|	ДокументРеализацияТоваровУслуг.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ДокументРеализацияТоваровУслуг.Организация КАК Поставщик,
	|	ДокументРеализацияТоваровУслуг.Ссылка.Склад КАК Склад,
	|	ДокументРеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	|	ДокументРеализацияТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	ДокументРеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ДокументРеализацияТоваровУслуг.ОтпускПроизвел КАК ОтпускПроизвел,
	|	РеализацияТоваровУслуг.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Цена КАК Цена,
	|	РеализацияТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|	РеализацияТоваровУслуг.СтранаПроисхождения КАК СтранаПроисхождения,
	|	РеализацияТоваровУслуг.НомерГТД КАК НомерГТД,
	|	СУММА(РеализацияТоваровУслуг.Количество) КАК Количество,
	|	СУММА(РеализацияТоваровУслуг.КоличествоМест) КАК КоличествоМест,
	|	СУММА(РеализацияТоваровУслуг.Сумма) КАК Сумма,
	|	СУММА(РеализацияТоваровУслуг.СуммаНДС) КАК СуммаНДС,
	|	МИНИМУМ(РеализацияТоваровУслуг.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_ВложенныйЗапросПоТоварам
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументРеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|		ПО (РеализацияТоваровУслуг.Ссылка = ДокументРеализацияТоваровУслуг.Ссылка)
	|ГДЕ
	|	ДокументРеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Цена,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	РеализацияТоваровУслуг.СтранаПроисхождения,
	|	РеализацияТоваровУслуг.НомерГТД,
	|	ДокументРеализацияТоваровУслуг.Ссылка,
	|	ДокументРеализацияТоваровУслуг.Номер,
	|	ДокументРеализацияТоваровУслуг.Дата,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента,
	|	ДокументРеализацияТоваровУслуг.Контрагент,
	|	ДокументРеализацияТоваровУслуг.Организация,
	|	ДокументРеализацияТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументРеализацияТоваровУслуг.СуммаДокумента,
	|	ДокументРеализацияТоваровУслуг.ВалютаДокумента,
	|	ДокументРеализацияТоваровУслуг.СуммаВключаетНДС,
	|	ДокументРеализацияТоваровУслуг.Ссылка.Склад,
	|	ДокументРеализацияТоваровУслуг.ОтпускПроизвел,
	|	ДокументРеализацияТоваровУслуг.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапросПоТоварам.Ссылка КАК Ссылка,
	|	ВложенныйЗапросПоТоварам.Номер КАК Номер,
	|	ВложенныйЗапросПоТоварам.Дата КАК Дата,
	|	ВложенныйЗапросПоТоварам.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВложенныйЗапросПоТоварам.Получатель КАК Получатель,
	|	ВложенныйЗапросПоТоварам.Организация КАК Организация,
	|	ВложенныйЗапросПоТоварам.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВложенныйЗапросПоТоварам.Организация КАК Поставщик,
	|	ВложенныйЗапросПоТоварам.Ссылка.Склад КАК Склад,
	|	ВложенныйЗапросПоТоварам.Ссылка.ОтпускПроизвел КАК ОтпускПроизвел,
	|	ВложенныйЗапросПоТоварам.Ссылка.ДоверенностьЧерезКого КАК Получил,
	|	ВложенныйЗапросПоТоварам.СуммаДокумента КАК СуммаДокумента,
	|	ВложенныйЗапросПоТоварам.ВалютаДокумента КАК ВалютаДокумента,
	|	ВложенныйЗапросПоТоварам.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ВложенныйЗапросПоТоварам.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапросПоТоварам.Номенклатура.НаименованиеПолное КАК Товар,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ВложенныйЗапросПоТоварам.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ВложенныйЗапросПоТоварам.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ КАК Артикул,
	|	ВложенныйЗапросПоТоварам.Количество КАК Количество,
	|	ВложенныйЗапросПоТоварам.КоличествоМест КАК КоличествоМест,
	|	ВложенныйЗапросПоТоварам.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапросПоТоварам.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаХранения,
	|	ВложенныйЗапросПоТоварам.Цена КАК Цена,
	|	ВложенныйЗапросПоТоварам.Сумма КАК Сумма,
	|	ВложенныйЗапросПоТоварам.СуммаНДС КАК СуммаНДС,
	|	ВложенныйЗапросПоТоварам.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВложенныйЗапросПоТоварам.НомерГТД КАК НомерГТД,
	|	ВложенныйЗапросПоТоварам.НомерСтроки КАК НомерСтроки,
	|	1 КАК ID
	|ИЗ
	|	ВТ_ВложенныйЗапросПоТоварам КАК ВложенныйЗапросПоТоварам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Ссылка.Номер,
	|	РеализацияТоваровУслуг.Ссылка.Дата,
	|	РеализацияТоваровУслуг.Ссылка.ДоговорКонтрагента,
	|	РеализацияТоваровУслуг.Ссылка.Контрагент,
	|	РеализацияТоваровУслуг.Ссылка.Организация,
	|	РеализацияТоваровУслуг.Ссылка.ПодразделениеОрганизации,
	|	РеализацияТоваровУслуг.Ссылка.Организация,
	|	РеализацияТоваровУслуг.Ссылка.Склад,
	|	РеализацияТоваровУслуг.Ссылка.ОтпускПроизвел,
	|	РеализацияТоваровУслуг.Ссылка.ДоверенностьЧерезКого,
	|	РеализацияТоваровУслуг.Ссылка.СуммаДокумента,
	|	РеализацияТоваровУслуг.Ссылка.ВалютаДокумента,
	|	РеализацияТоваровУслуг.Ссылка.СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	NULL,
	|	NULL,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Цена,
	|	РеализацияТоваровУслуг.Сумма,
	|	0,
	|	NULL,
	|	NULL,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	2
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВозвратнаяТара КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Ссылка.Номер,
	|	РеализацияТоваровУслуг.Ссылка.Дата,
	|	РеализацияТоваровУслуг.Ссылка.ДоговорКонтрагента,
	|	РеализацияТоваровУслуг.Ссылка.Контрагент,
	|	РеализацияТоваровУслуг.Ссылка.Организация,
	|	РеализацияТоваровУслуг.Ссылка.ПодразделениеОрганизации,
	|	РеализацияТоваровУслуг.Ссылка.Организация,
	|	РеализацияТоваровУслуг.Ссылка.Склад,
	|	РеализацияТоваровУслуг.Ссылка.ОтпускПроизвел,
	|	РеализацияТоваровУслуг.Ссылка.ДоверенностьЧерезКого,
	|	РеализацияТоваровУслуг.Ссылка.СуммаДокумента,
	|	РеализацияТоваровУслуг.Ссылка.ВалютаДокумента,
	|	РеализацияТоваровУслуг.Ссылка.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	NULL,
	|	NULL,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Цена,
	|	РеализацияТоваровУслуг.Сумма,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	NULL,
	|	NULL,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	3
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Ссылка.Номер,
	|	РеализацияТоваровУслуг.Ссылка.Дата,
	|	РеализацияТоваровУслуг.Ссылка.ДоговорКонтрагента,
	|	РеализацияТоваровУслуг.Ссылка.Контрагент,
	|	РеализацияТоваровУслуг.Ссылка.Организация,
	|	РеализацияТоваровУслуг.Ссылка.ПодразделениеОрганизации,
	|	РеализацияТоваровУслуг.Ссылка.Организация,
	|	РеализацияТоваровУслуг.Ссылка.Склад,
	|	РеализацияТоваровУслуг.Ссылка.ОтпускПроизвел,
	|	РеализацияТоваровУслуг.Ссылка.ДоверенностьЧерезКого,
	|	РеализацияТоваровУслуг.Ссылка.СуммаДокумента,
	|	РеализацияТоваровУслуг.Ссылка.ВалютаДокумента,
	|	РеализацияТоваровУслуг.Ссылка.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	NULL,
	|	NULL,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Цена,
	|	РеализацияТоваровУслуг.Сумма,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	NULL,
	|	NULL,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	4
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	ID,
	|	ВложенныйЗапросПоТоварам.НомерСтроки";
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Шапка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_Накладная");

		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выводим шапку накладной

		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		УстановитьПараметрТабличногоДокумента(ОбластьМакета, 
			"ТекстЗаголовка", ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(Шапка, "Расходная накладная"));
		ТабличныйДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
		СведенияОбОрганизации   = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата, , , , Истина);
		ПредставлениеПоставщика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
		УстановитьПараметрТабличногоДокумента(ОбластьМакета, "ПредставлениеПоставщика", ПредставлениеПоставщика);
		УстановитьПараметрТабличногоДокумента(ОбластьМакета, "Поставщик", Шапка.Поставщик);
		ТабличныйДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
		СведенияОКонтрагенте    = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Получатель, Шапка.Дата);
		ПредставлениеПолучателя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОКонтрагенте, "НаименованиеДляПечатныхФорм,");
		УстановитьПараметрТабличногоДокумента(ОбластьМакета, "ПредставлениеПолучателя", ПредставлениеПолучателя);
		УстановитьПараметрТабличногоДокумента(ОбластьМакета, "Получатель",               Шапка.Получатель);
		ТабличныйДокумент.Вывести(ОбластьМакета);

		ЕстьСкидки = Ложь;

		ОбластьНомера = Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
		ОбластьДанных = Макет.ПолучитьОбласть("ШапкаТаблицы|Данные");
		ОбластьСуммы  = Макет.ПолучитьОбласть("ШапкаТаблицы|Сумма");

		ТабличныйДокумент.Вывести(ОбластьНомера);
		Если ВыводитьКоды Тогда
			Если ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
				УстановитьПараметрТабличногоДокумента(ОбластьКодов, "ИмяКолонкиКодов", "Артикул");
			ИначеЕсли ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Код Тогда
				УстановитьПараметрТабличногоДокумента(ОбластьКодов, "ИмяКолонкиКодов", "Код");
			КонецЕсли;
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		ТабличныйДокумент.Присоединить(ОбластьСуммы);

		ОбластьКолонкаТовар = Макет.Область("Товар");

		Если НЕ ВыводитьКоды Тогда
			ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки + 
												Макет.Область("КолонкаКодов").ШиринаКолонки;
		КонецЕсли;
		
		Если НЕ ЕстьСкидки Тогда
			ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки + 
												Макет.Область("СуммаБезСкидки").ШиринаКолонки +
												Макет.Область("СуммаСкидки").ШиринаКолонки;
		КонецЕсли;

		ОбластьНомера = Макет.ПолучитьОбласть("Строка|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть("Строка|КолонкаКодов");
		ОбластьДанных = Макет.ПолучитьОбласть("Строка|Данные");
		ОбластьСуммы  = Макет.ПолучитьОбласть("Строка|Сумма");

		Сумма    = 0;
		СуммаНДС = 0;
		ВсегоСкидок    = 0;
		ВсегоБезСкидок = 0;
        КоличествоСтрок = 0;

		НомерСтроки = 0;

		Пока Шапка.Следующий() Цикл 
			
			Если Шапка.Номенклатура = Null Тогда 
				Продолжить;
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;
			УстановитьПараметрТабличногоДокумента(ОбластьНомера, "НомерСтроки", НомерСтроки);
			ТабличныйДокумент.Вывести(ОбластьНомера);

			Если ВыводитьКоды Тогда
				ОбластьКодов.Параметры.Заполнить(Шапка);
				ТабличныйДокумент.Присоединить(ОбластьКодов);
			КонецЕсли;
			
			ОбластьДанных.Параметры.Заполнить(Шапка);
			УстановитьПараметрТабличногоДокумента(ОбластьДанных, "Товар", СокрЛП(Шапка.Товар));
			ТабличныйДокумент.Присоединить(ОбластьДанных);

			Скидка = 0;
			
			ОбластьСуммы.Параметры.Заполнить(Шапка);
			ТабличныйДокумент.Присоединить(ОбластьСуммы);
			Сумма          = Сумма       + ?(ЗначениеЗаполнено(Шапка.Сумма), Шапка.Сумма, 0);
			СуммаНДС       = СуммаНДС    + ?(ЗначениеЗаполнено(Шапка.СуммаНДС), Шапка.СуммаНДС, 0);
			ВсегоСкидок    = ВсегоСкидок + Скидка;
			ВсегоБезСкидок = Сумма       + ВсегоСкидок;
			
			КоличествоСтрок = КоличествоСтрок + 1;

		КонецЦикла;

		// Вывести Итого
		ОбластьНомера = Макет.ПолучитьОбласть("Итого|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть("Итого|КолонкаКодов");
		ОбластьДанных = Макет.ПолучитьОбласть("Итого|Данные");
		ОбластьСуммы  = Макет.ПолучитьОбласть("Итого|Сумма");

		ТабличныйДокумент.Вывести(ОбластьНомера);
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		УстановитьПараметрТабличногоДокумента(ОбластьСуммы, "Всего", ОбщегоНазначенияБПВызовСервера.ФорматСумм(Сумма));
		ТабличныйДокумент.Присоединить(ОбластьСуммы);
		
		// Вывести ИтогоНДС
		ОбластьНомера = Макет.ПолучитьОбласть("ИтогоНДС|НомерСтроки");
		ОбластьКодов  = Макет.ПолучитьОбласть("ИтогоНДС|КолонкаКодов");
		ОбластьДанных = Макет.ПолучитьОбласть("ИтогоНДС|Данные");
		ОбластьСуммы  = Макет.ПолучитьОбласть("ИтогоНДС|Сумма");
		
		ТабличныйДокумент.Вывести(ОбластьНомера);
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		
		Если СуммаНДС <> 0 Тогда
			УстановитьПараметрТабличногоДокумента(ОбластьСуммы, "ВсегоНДС", ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаНДС));
			УстановитьПараметрТабличногоДокумента(ОбластьДанных, "НДС", ?(Шапка.СуммаВключаетНДС, "В том числе НДС", " Сумма НДС"));
		Иначе
			УстановитьПараметрТабличногоДокумента(ОбластьСуммы,  "ВсегоНДС", "-");
			УстановитьПараметрТабличногоДокумента(ОбластьДанных, "НДС",      "Без налога (НДС)");
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		ТабличныйДокумент.Присоединить(ОбластьСуммы);
				
		// Вывести Сумму прописью
		ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
		СуммаКПрописи = Сумма + ?(Шапка.СуммаВключаетНДС, 0, СуммаНДС);
		
		УстановитьПараметрТабличногоДокумента(ОбластьМакета, 
			"ИтоговаяСтрока", 
			СтрШаблон("Всего наименований %1, на сумму %2", 
				КоличествоСтрок, 
				ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаКПрописи, Шапка.ВалютаДокумента)));
				
		УстановитьПараметрТабличногоДокумента(ОбластьМакета, 
			"СуммаПрописью", 
			ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(СуммаКПрописи, Шапка.ВалютаДокумента));
		ТабличныйДокумент.Вывести(ОбластьМакета);

		// Вывести подписи
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		
		Если ЗначениеЗаполнено(Шапка.ОтпускПроизвел) Тогда
			ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Шапка.Организация, Шапка.ОтпускПроизвел, Шапка.Дата);
			Представление = ?(ЗначениеЗаполнено(ДанныеФизЛица.Должность), Строка(ДанныеФизЛица.Должность) + " ", "");
			Представление = Представление + ?(ЗначениеЗаполнено(ДанныеФизЛица.Представление), Строка(ДанныеФизЛица.Представление), "");
			УстановитьПараметрТабличногоДокумента(ОбластьМакета, "ОтветственныйПредставление", Представление);
		ИначеЕсли Шапка.Склад <> Справочники.Склады.ПустаяСсылка() Тогда 
			МОЛ = ОтветственныеЛицаБП.ОтветственноеЛицоНаСкладе(Шапка.Склад, Шапка.Дата);
			ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Шапка.Организация, МОЛ, Шапка.Дата);
			Представление = ?(ЗначениеЗаполнено(ДанныеФизЛица.Должность), Строка(ДанныеФизЛица.Должность) + " ", "");
			Представление = Представление + ?(ЗначениеЗаполнено(ДанныеФизЛица.Представление), Строка(ДанныеФизЛица.Представление), "");
			УстановитьПараметрТабличногоДокумента(ОбластьМакета, "ОтветственныйПредставление", Представление);
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
		Если УправлениеПечатьюБП.ЭтоПечатнаяФормаЗаПоставщика(Шапка.Ссылка) Тогда
			УправлениеПечатьюБП.ОчиститьФаксимильнуюПодписьИПечатьИзМакета(ТабличныйДокумент, ОбъектыПечати, Шапка.Ссылка);
		Иначе
			УправлениеПечатьюБП.ДоработатьФаксимильнуюПодписьИПечать(ТабличныйДокумент, ОбъектыПечати,
				Шапка.Организация, Шапка.Ссылка);
		КонецЕсли;
		
	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

// Функция выполняет расчет рублевых сумм для вывода таблиц документа на печать
//
Функция ПодготовитьТаблицуДокументаДляПечати(ВыборкаСтрок, ТаблицаПоТоварам, ПечататьТовары, ПечататьУслуги)

	ТаблицаПоТоварам.Очистить();

	ВалютаРегламентированногоУчета   = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	МассивРаспределения = Новый Массив;

	СуммаВзаиморасчетов 	= 0;
	СуммаВзаиморасчетовНДС 	= 0;
	ПерваяСтрокаДокумента 	= Истина;
	СуммаВключаетНДС 		= Неопределено;
	РасчетыВУсловныхЕдиницах= Неопределено;
	ДатаДокумента			= Неопределено;

	Пока ВыборкаСтрок.Следующий() Цикл
		
		СтрокаТаблицы = ТаблицаПоТоварам.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаСтрок);
		
		Если ПерваяСтрокаДокумента Тогда

			СуммаВключаетНДС 			= ВыборкаСтрок.СуммаВключаетНДС;
			РасчетыВУсловныхЕдиницах 	= ВыборкаСтрок.РасчетыВУсловныхЕдиницах;
			ДатаДокумента				= ВыборкаСтрок.ДатаДокумента;
			
			НуженПересчетВРубли = Ложь;
			
			Если ВыборкаСтрок.Проведен 
				И (ВыборкаСтрок.РасчетыВУсловныхЕдиницах
				ИЛИ (ВыборкаСтрок.ВалютаДокумента <> ВалютаРегламентированногоУчета И ВыборкаСтрок.ДатаДокумента >= '20090101000000')) Тогда
				НуженПересчетВРубли = Истина;
			КонецЕсли;
			
			ПерваяСтрокаДокумента = Ложь;
			
		КонецЕсли;
		
		Если НуженПересчетВРубли Тогда
			
			Если  СтрокаТаблицы.ВсегоРуб = 0 Тогда
				
				СтрокаТаблицы.Сумма = СтрокаТаблицы.Сумма;
				СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.СуммаНДС;
				
			Иначе
				
				СтрокаТаблицы.Сумма = СтрокаТаблицы.ВсегоРуб - ?(СуммаВключаетНДС, 0, СтрокаТаблицы.НДСРуб);
				СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.НДСРуб;
				
			КонецЕсли;	
			
			СтрокаТаблицы.СуммаБезНДС = СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС;
			
			СтрокаТаблицы.Цена = ?(СтрокаТаблицы.Количество = 0, 0, Окр(СтрокаТаблицы.Сумма/СтрокаТаблицы.Количество,2));
			
		КонецЕсли;	
				
		
	КонецЦикла;
	
	Если НЕ ПечататьТовары ИЛИ НЕ ПечататьУслуги Тогда
		// Исключим строки из табличный частей, которые не удовлетворяют условиям печати. При выборке запросом их необходимо получать, 
		// чтобы распределять сумма взаиморасчетов в рублях на все строки документа.
		Инд = ТаблицаПоТоварам.Количество() - 1;
		Пока Инд >= 0 Цикл
			СтрокаТовар = ТаблицаПоТоварам[Инд];
			
			Если СтрокаТовар.ID = 1 ИЛИ СтрокаТовар.ID = 2 Тогда // Товары или ВозвратнаяТара
				Если НЕ ПечататьТовары Тогда
					ТаблицаПоТоварам.Удалить(Инд);
				КонецЕсли;			
			ИначеЕсли СтрокаТовар.ID = 3 ИЛИ СтрокаТовар.ID = 4 Тогда // Услуги или АгентскиеУслуги
				Если НЕ ПечататьУслуги Тогда
					ТаблицаПоТоварам.Удалить(Инд);
				КонецЕсли;
			КонецЕсли;
			
			Инд = Инд - 1;
		КонецЦикла;
	КонецЕсли;

	Возврат ТаблицаПоТоварам;

КонецФункции

Функция ПолучитьТекстЗапросаДляФормированияТаблицыСведенийТОРГ12()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.Дата КАК ДатаДокумента
	|ПОМЕСТИТЬ ДокументыДляПечати
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	РеализацияТоваровУслуг.Дата КАК ДатаДляПолученияСведений,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	РеализацияТоваровУслуг.Дата КАК ДатаПодписанияДокумента,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Организация.ОбособленноеПодразделение
	|			ТОГДА РеализацияТоваровУслуг.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ РеализацияТоваровУслуг.Организация
	|	КОНЕЦ КАК Поставщик,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации КАК Подразделение,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.Контрагент КАК Покупатель,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	РеализацияТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетПродавца,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента КАК Договор,
	|	РеализацияТоваровУслуг.СчетНаОплатуПокупателю КАК ДокументОснование,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.Представление КАК Основание,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.Номер КАК ОснованиеНомер,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.Дата КАК ОснованиеДата,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.ГосударственныйКонтракт КАК ГосударственныйКонтрактСсылка,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.ГосударственныйКонтракт.Код КАК ГосударственныйКонтракт,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.УчетАгентскогоНДСПокупателем КАК НДСИсчисляетсяНалоговымАгентом,
	|	РеализацияТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваровУслуг.КурсВзаиморасчетов КАК Курс,
	|	РеализацияТоваровУслуг.КратностьВзаиморасчетов КАК Кратность,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.Руководитель КАК Руководитель,
	|	РеализацияТоваровУслуг.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	РеализацияТоваровУслуг.ОтпускПроизвел КАК Кладовщик,
	|	РеализацияТоваровУслуг.ЗаРуководителяНаОсновании КАК ЗаРуководителяНаОсновании,
	|	РеализацияТоваровУслуг.ЗаГлавногоБухгалтераНаОсновании КАК ЗаГлавногоБухгалтераНаОсновании,
	|	РеализацияТоваровУслуг.ДоверенностьНомер КАК ДоверенностьНомер,
	|	РеализацияТоваровУслуг.ДоверенностьДата КАК ДоверенностьДата,
	|	РеализацияТоваровУслуг.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	РеализацияТоваровУслуг.ДоверенностьЧерезКого КАК ДоверенностьЧерезКого
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО РеализацияТоваровУслуг.Организация = ДанныеПервичныхДокументов.Организация
	|			И РеализацияТоваровУслуг.Ссылка = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыДляПечати.Ссылка
	|			ИЗ
	|				ДокументыДляПечати КАК ДокументыДляПечати)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	ДокументыДляПечати.ДатаДокумента КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары) КАК ТабличнаяЧасть,
	|	1 КАК ПорядокТабличнойЧасти,
	|	РеализацияТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура КАК Товар,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ КАК ТоварКод,
	|	РеализацияТоваровУслуг.Количество КАК Количество,
	|	РеализацияТоваровУслуг.КоличествоМест КАК КоличествоМест,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ВидУпаковки,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЕдиницаИзмерения.Код, """") КАК ВидУпаковкиКод,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЕдиницаИзмерения.Наименование, """") КАК ВидУпаковкиНаименование,
	|	РеализацияТоваровУслуг.Коэффициент КАК КоличествоВОдномМесте,
	|	РеализацияТоваровУслуг.Цена КАК Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА РеализацияТоваровУслуг.Сумма - РеализацияТоваровУслуг.СуммаНДС
	|		ИНАЧЕ РеализацияТоваровУслуг.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	РеализацияТоваровУслуг.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоВРублях,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСВРублях,
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СуществуютЗаписиРублевыхСумм,
	|	РеализацияТоваровУслуг.НомерГТД.Код КАК НомерТД,
	|	РеализацияТоваровУслуг.СтранаПроисхождения.Код КАК КодСтраныПроисхождения
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ВозвратнаяТара),
	|	2,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	0,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	NULL,
	|	NULL,
	|	NULL,
	|	0,
	|	РеализацияТоваровУслуг.Цена,
	|	РеализацияТоваровУслуг.Сумма,
	|	0,
	|	NULL,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	NULL,
	|	NULL
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.ВозвратнаяТара КАК РеализацияТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ВозвратнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги),
	|	3,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	0,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	NULL,
	|	NULL,
	|	NULL,
	|	0,
	|	РеализацияТоваровУслуг.Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА РеализацияТоваровУслуг.Сумма - РеализацияТоваровУслуг.СуммаНДС
	|		ИНАЧЕ РеализацияТоваровУслуг.Сумма
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	NULL,
	|	NULL
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|ГДЕ
	|	&ВключатьУслуги = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги),
	|	4,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	0,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	NULL,
	|	NULL,
	|	NULL,
	|	0,
	|	РеализацияТоваровУслуг.Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА РеализацияТоваровУслуг.Сумма - РеализацияТоваровУслуг.СуммаНДС
	|		ИНАЧЕ РеализацияТоваровУслуг.Сумма
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	NULL,
	|	NULL
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|ГДЕ
	|	&ВключатьУслуги = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ,
	|	ПорядокТабличнойЧасти,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТаблицуСведенийТОРГ12(Знач МассивДокументов, Знач ВключатьУслуги) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаСведений = ПечатьТорговыхДокументов.ПолучитьОписаниеТОРГ12();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивДокументов", 	МассивДокументов);
	Запрос.УстановитьПараметр("ВключатьУслуги", 	ВключатьУслуги);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияТаблицыСведенийТОРГ12();
		
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	РеквизитыВалютыРегУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВалютаРегУчета, "Код, Наименование");
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	СтрокиДокументов = РезультатыЗапроса[3].Выгрузить();
	СтрокиДокументов.Индексы.Добавить("Документ");
	
	Пока Выборка.Следующий() Цикл
		
		СведенияОДокументе = ТаблицаСведений.Добавить();
		ЗаполнитьЗначенияСвойств(СведенияОДокументе, Выборка);
		
		СведенияОДокументе.Валюта             = ВалютаРегУчета;
		СведенияОДокументе.ВалютаКод          = РеквизитыВалютыРегУчета.Код;
		СведенияОДокументе.ВалютаНаименование = РеквизитыВалютыРегУчета.Наименование;
		
		ТаблицаДокумента = ПечатьТорговыхДокументов.ПолучитьОписаниеТаблицыТОРГ12();
		
		Отбор = Новый Структура("Документ", Выборка.Документ);
		СтрокиДокумента = СтрокиДокументов.НайтиСтроки(Отбор);
		
		НуженПересчетВРубли = НЕ (Выборка.ВалютаДокумента = ВалютаРегУчета);
		
		Для Каждого Строка Из СтрокиДокумента Цикл
			
			СтрокаТаблицыДокумента = ТаблицаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокумента, Строка);
			
			Если НуженПересчетВРубли Тогда
				Если Строка.СуществуютЗаписиРублевыхСумм Тогда
					СтрокаТаблицыДокумента.СуммаНДС 	= Строка.НДСВРублях;
					СтрокаТаблицыДокумента.СуммаБезНДС 	= Строка.ВсегоВРублях - Строка.НДСВРублях;
				Иначе
					Кратность = ?(Выборка.Кратность   	= 0, 1, Выборка.Кратность);
					СтрокаТаблицыДокумента.СуммаНДС 	= Строка.СуммаНДС * Выборка.Курс / Кратность;;
					СтрокаТаблицыДокумента.СуммаБезНДС  = (Строка.СуммаБезНДС + Строка.СуммаНДС) * Выборка.Курс / Кратность - СтрокаТаблицыДокумента.СуммаНДС;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаТаблицыДокумента.СуммаСНДС = СтрокаТаблицыДокумента.СуммаБезНДС + СтрокаТаблицыДокумента.СуммаНДС;
			
			Если НуженПересчетВРубли 
					ИЛИ (Выборка.СуммаВключаетНДС И СтрокаТаблицыДокумента.СуммаНДС <> 0) Тогда
				СтрокаТаблицыДокумента.Цена = ?(СтрокаТаблицыДокумента.Количество = 0,
					СтрокаТаблицыДокумента.СуммаБезНДС,
					СтрокаТаблицыДокумента.СуммаБезНДС / СтрокаТаблицыДокумента.Количество);
			КонецЕсли;
			
		КонецЦикла;
		
		СведенияОДокументе.ТаблицаДокумента = ТаблицаДокумента;
		
		ПорядокЗаполнения = Новый Структура("ЗаполнятьРуководителя, ЗаполнятьГлавногоБухгалтера, ЗаполнятьКладовщика");
		ПечатьТорговыхДокументов.ЗаполнитьДанныеОтветственныхЛиц(СведенияОДокументе, ПорядокЗаполнения);
		
		Если НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(СведенияОДокументе.Организация) Тогда
			СтатусФизическогоЛица = Справочники.Организации.СтатусФизическогоЛицаНаДату(
				СведенияОДокументе.Организация, СведенияОДокументе.ДатаДляПолученияСведений);
			Если СтатусФизическогоЛица = Перечисления.СтатусыФизическихЛиц.Самозанятый Тогда
				СведенияОДокументе.РуководительДолжностьНаименование = "";
			Иначе
				ИндивидуальныйПредприниматель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СведенияОДокументе.Организация, "ИндивидуальныйПредприниматель");
				Если ИндивидуальныйПредприниматель = СведенияОДокументе.Руководитель Тогда
					СведенияОДокументе.РуководительДолжностьНаименование = "Индивидуальный предприниматель";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ТаблицаСведений;
	
КонецФункции

Функция ПолучитьТекстЗапросаДляФормированияТаблицыСведенийАктаОбОказанииУслуг()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.Дата КАК ДатаДокумента
	|ПОМЕСТИТЬ ДокументыДляПечати
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	РеализацияТоваровУслуг.Дата КАК ДатаДляПолученияСведений,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	РеализацияТоваровУслуг.Контрагент КАК Получатель,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.ГосударственныйКонтракт КАК ГосударственныйКонтрактСсылка,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.ГосударственныйКонтракт.Код КАК ГосударственныйКонтракт,
	|	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.ДоговорКонтрагента) КАК ПредставлениеДоговора,
	|	РеализацияТоваровУслуг.Организация КАК Поставщик,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации КАК Подразделение,
	|	РеализацияТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетПродавца,
	|	РеализацияТоваровУслуг.ВалютаДокумента КАК Валюта,
	|	РеализацияТоваровУслуг.КурсВзаиморасчетов КАК Курс,
	|	РеализацияТоваровУслуг.КратностьВзаиморасчетов КАК Кратность,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ВалютаДокумента.Код, """") КАК ВалютаКод,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ВалютаДокумента.Наименование, """") КАК ВалютаНаименование,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	РеализацияТоваровУслуг.СчетНаОплатуПокупателю КАК ДокументОснование,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.Руководитель,
	|	РеализацияТоваровУслуг.ЗаРуководителяНаОсновании,
	|	РеализацияТоваровУслуг.ДоверенностьЧерезКого КАК ПредставительЗаказчика,
	|	РеализацияТоваровУслуг.ЗаЗаказчикаНаОсновании
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО РеализацияТоваровУслуг.Организация = ДанныеПервичныхДокументов.Организация
	|			И РеализацияТоваровУслуг.Ссылка = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыДляПечати.Ссылка
	|			ИЗ
	|				ДокументыДляПечати)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	ДокументыДляПечати.ДатаДокумента КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги) КАК ТабличнаяЧасть,
	|	1 КАК ПорядокТабличнойЧасти,
	|	РеализацияТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ КАК НоменклатураКод,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Цена КАК Цена,
	|	РеализацияТоваровУслуг.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА РеализацияТоваровУслуг.Сумма - РеализацияТоваровУслуг.СуммаНДС
	|		ИНАЧЕ РеализацияТоваровУслуг.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	РеализацияТоваровУслуг.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоВРублях,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСВРублях,
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СуществуютЗаписиРублевыхСумм
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги),
	|	2,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Цена,
	|	РеализацияТоваровУслуг.Сумма,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА РеализацияТоваровУслуг.Сумма - РеализацияТоваровУслуг.СуммаНДС
	|		ИНАЧЕ РеализацияТоваровУслуг.Сумма
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ,
	|	ПорядокТабличнойЧасти,
	|	НомерСтроки";
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТаблицуСведенийАктаОбОказанииУслуг(Знач МассивОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаСведений = ПечатьТорговыхДокументов.ПолучитьОписаниеАктаОбОказанииУслуг();
	
	Запрос = Новый Запрос();
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияТаблицыСведенийАктаОбОказанииУслуг();
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	СтрокиДокументов = РезультатыЗапроса[3].Выгрузить();
	СтрокиДокументов.Индексы.Добавить("Документ");
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	РеквизитыВалютыРегУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВалютаРегУчета, "Код, Наименование");
	
	Пока Выборка.Следующий() Цикл
		
		СведенияОДокументе = ТаблицаСведений.Добавить();
		ЗаполнитьЗначенияСвойств(СведенияОДокументе, Выборка);
		
		Если Выборка.РасчетыВУсловныхЕдиницах Тогда
			СведенияОДокументе.Валюта             = ВалютаРегУчета;
			СведенияОДокументе.ВалютаКод          = РеквизитыВалютыРегУчета.Код;
			СведенияОДокументе.ВалютаНаименование = РеквизитыВалютыРегУчета.Наименование;
		КонецЕсли;
		
		ТаблицаДокумента = ПечатьТорговыхДокументов.ПолучитьОписаниеТаблицыАктаОбОказанииУслуг();
		
		Отбор = Новый Структура("Документ", Выборка.Документ);
		СтрокиДокумента = СтрокиДокументов.НайтиСтроки(Отбор);
		
		НуженПересчетВРубли = Ложь;
		Если Выборка.Валюта <> ВалютаРегУчета
			И Выборка.РасчетыВУсловныхЕдиницах Тогда
			НуженПересчетВРубли = Истина;
		КонецЕсли;
		
		Для Каждого Строка Из СтрокиДокумента Цикл
			
			СтрокаТаблицыДокумента = ТаблицаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокумента, Строка);
			
			Если НуженПересчетВРубли Тогда
				Если Строка.СуществуютЗаписиРублевыхСумм Тогда
					СтрокаТаблицыДокумента.СуммаНДС = Строка.НДСВРублях;
					СтрокаТаблицыДокумента.СуммаБезНДС = Строка.ВсегоВРублях - Строка.НДСВРублях;
				Иначе
					Кратность = ?(Выборка.Кратность = 0, 1, Выборка.Кратность);
					СтрокаТаблицыДокумента.СуммаНДС = Строка.СуммаНДС * Выборка.Курс / Кратность;;
					СтрокаТаблицыДокумента.СуммаБезНДС = (Строка.СуммаБезНДС + Строка.СуммаНДС) * Выборка.Курс / Кратность - СтрокаТаблицыДокумента.СуммаНДС;
				КонецЕсли;
				СтрокаТаблицыДокумента.Сумма = СтрокаТаблицыДокумента.СуммаБезНДС + ?(Выборка.СуммаВключаетНДС, СтрокаТаблицыДокумента.СуммаНДС, 0);
				СтрокаТаблицыДокумента.Цена  = ?(СтрокаТаблицыДокумента.Количество=0, СтрокаТаблицыДокумента.Сумма, СтрокаТаблицыДокумента.Сумма / СтрокаТаблицыДокумента.Количество);
			КонецЕсли;
			СтрокаТаблицыДокумента.СуммаСНДС = СтрокаТаблицыДокумента.СуммаБезНДС + СтрокаТаблицыДокумента.СуммаНДС;
			
		КонецЦикла;
		
		СведенияОДокументе.ТаблицаДокумента = ТаблицаДокумента;
		
		ПечатьТорговыхДокументов.ЗаполнитьДанныеОтветственныхЛиц(СведенияОДокументе, Новый Структура("ЗаполнятьРуководителя"));
		
	КонецЦикла;
	
	Возврат ТаблицаСведений;
	
КонецФункции

Функция ПолучитьТекстЗапросаДляФормированияТаблицыСведенийАктаНаПередачуПрав()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.Дата КАК ДатаДокумента
	|ПОМЕСТИТЬ ДокументыДляПечати
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	РеализацияТоваровУслуг.Дата КАК ДатаДляПолученияСведений,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	РеализацияТоваровУслуг.Дата КАК ДатаПодписанияДокумента,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации КАК Подразделение,
	|	РеализацияТоваровУслуг.Организация КАК Лицензиар,
	|	РеализацияТоваровУслуг.Контрагент КАК Лицензиат,
	|	РеализацияТоваровУслуг.Контрагент КАК Плательщик,
	|	РеализацияТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетЛицензиара,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента КАК Договор,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.ГосударственныйКонтракт КАК ГосударственныйКонтрактСсылка,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.ГосударственныйКонтракт.Код КАК ГосударственныйКонтракт,
	|	РеализацияТоваровУслуг.СчетНаОплатуПокупателю КАК ДокументОснование,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.Представление КАК Основание,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.Номер КАК ОснованиеНомер,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.Дата КАК ОснованиеДата,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваровУслуг.КурсВзаиморасчетов КАК Курс,
	|	РеализацияТоваровУслуг.КратностьВзаиморасчетов КАК Кратность,
	|	РеализацияТоваровУслуг.Руководитель,
	|	РеализацияТоваровУслуг.ЗаРуководителяНаОсновании,
	|	РеализацияТоваровУслуг.ДоверенностьНомер КАК ДоверенностьНомер,
	|	РеализацияТоваровУслуг.ДоверенностьДата КАК ДоверенностьДата,
	|	РеализацияТоваровУслуг.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	РеализацияТоваровУслуг.ДоверенностьЧерезКого КАК ДоверенностьЧерезКого
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО РеализацияТоваровУслуг.Организация = ДанныеПервичныхДокументов.Организация
	|			И РеализацияТоваровУслуг.Ссылка = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыДляПечати.Ссылка
	|			ИЗ
	|				ДокументыДляПечати КАК ДокументыДляПечати)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	ДокументыДляПечати.ДатаДокумента КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары) КАК ТабличнаяЧасть,
	|	1 КАК ПорядокТабличнойЧасти,
	|	РеализацияТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура КАК Товар,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ КАК ТоварКод,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.КоличествоМест,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.НаименованиеПолное, """") КАК ЕдиницаИзмеренияНаименованиеПолное,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ВидУпаковки,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЕдиницаИзмерения.Код, """") КАК ВидУпаковкиКод,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЕдиницаИзмерения.Наименование, """") КАК ВидУпаковкиНаименование,
	|	РеализацияТоваровУслуг.Коэффициент КАК КоличествоВОдномМесте,
	|	РеализацияТоваровУслуг.Цена КАК Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА РеализацияТоваровУслуг.Сумма - РеализацияТоваровУслуг.СуммаНДС
	|		ИНАЧЕ РеализацияТоваровУслуг.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	РеализацияТоваровУслуг.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоВРублях,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСВРублях,
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СуществуютЗаписиРублевыхСумм
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги),
	|	3,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ РеализацияТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Содержание
	|		КОГДА НЕ РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	0,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.НаименованиеПолное, """"),
	|	NULL,
	|	NULL,
	|	NULL,
	|	0,
	|	РеализацияТоваровУслуг.Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА РеализацияТоваровУслуг.Сумма - РеализацияТоваровУслуг.СуммаНДС
	|		ИНАЧЕ РеализацияТоваровУслуг.Сумма
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ,
	|	ПорядокТабличнойЧасти,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТаблицуСведенийАктаНаПередачуПрав(Знач МассивДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаСведений = ПечатьТорговыхДокументов.ПолучитьОписаниеАктаНаПередачуПрав();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивДокументов", 	МассивДокументов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияТаблицыСведенийАктаНаПередачуПрав();
		
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	РеквизитыВалютыРегУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВалютаРегУчета, "Код, Наименование");
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	СтрокиДокументов = РезультатыЗапроса[3].Выгрузить();
	СтрокиДокументов.Индексы.Добавить("Документ");
	
	Пока Выборка.Следующий() Цикл
		
		СведенияОДокументе = ТаблицаСведений.Добавить();
		ЗаполнитьЗначенияСвойств(СведенияОДокументе, Выборка);
		
		СведенияОДокументе.Валюта             = ВалютаРегУчета;
		СведенияОДокументе.ВалютаКод          = РеквизитыВалютыРегУчета.Код;
		СведенияОДокументе.ВалютаНаименование = РеквизитыВалютыРегУчета.Наименование;
		
		ТаблицаДокумента = ПечатьТорговыхДокументов.ПолучитьОписаниеТаблицыАктаНаПередачуПрав();
		
		Отбор = Новый Структура("Документ", Выборка.Документ);
		СтрокиДокумента = СтрокиДокументов.НайтиСтроки(Отбор);
		
		НуженПересчетВРубли = Ложь;
		Если Выборка.ВалютаДокумента <> ВалютаРегУчета Тогда
			НуженПересчетВРубли	= Истина;
		КонецЕсли;
		
		Для Каждого Строка Из СтрокиДокумента Цикл
			
			СтрокаТаблицыДокумента = ТаблицаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокумента, Строка);
			
			Если НуженПересчетВРубли Тогда
				Если Строка.СуществуютЗаписиРублевыхСумм Тогда
					СтрокаТаблицыДокумента.СуммаНДС 	= Строка.НДСВРублях;
					СтрокаТаблицыДокумента.СуммаБезНДС 	= Строка.ВсегоВРублях - Строка.НДСВРублях;
				Иначе
					Кратность = ?(Выборка.Кратность   	= 0, 1, Выборка.Кратность);
					СтрокаТаблицыДокумента.СуммаНДС 	= Строка.СуммаНДС * Выборка.Курс / Кратность;;
					СтрокаТаблицыДокумента.СуммаБезНДС  = (Строка.СуммаБезНДС + Строка.СуммаНДС) * Выборка.Курс / Кратность - СтрокаТаблицыДокумента.СуммаНДС;
				КонецЕсли;
			КонецЕсли;
			СтрокаТаблицыДокумента.СуммаСНДС = СтрокаТаблицыДокумента.СуммаБезНДС + СтрокаТаблицыДокумента.СуммаНДС;
			СтрокаТаблицыДокумента.Цена 	 = ?(СтрокаТаблицыДокумента.Количество = 0, СтрокаТаблицыДокумента.СуммаБезНДС, 
														СтрокаТаблицыДокумента.СуммаБезНДС / СтрокаТаблицыДокумента.Количество);
		КонецЦикла;
		
		СведенияОДокументе.ТаблицаДокумента = ТаблицаДокумента;
		
		ПорядокЗаполнения = Новый Структура("ЗаполнятьРуководителя");
		ПечатьТорговыхДокументов.ЗаполнитьДанныеОтветственныхЛиц(СведенияОДокументе, ПорядокЗаполнения);
	
	КонецЦикла;
	
	Возврат ТаблицаСведений;
	
КонецФункции

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыМ15()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументРеализацияТоваровУслуг.Номер КАК Номер,
	|	ДокументРеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	ДокументРеализацияТоваровУслуг.Дата КАК ДатаСоставления,
	|	ДокументРеализацияТоваровУслуг.Дата КАК ДатаДокумента,
	|	ДокументРеализацияТоваровУслуг.Организация,
	|	ДокументРеализацияТоваровУслуг.Организация КАК ЮрФизЛицо,
	|	ДокументРеализацияТоваровУслуг.Организация КАК Руководители,
	|	ДокументРеализацияТоваровУслуг.Склад КАК Склад,
	|	ДокументРеализацияТоваровУслуг.Склад.Наименование КАК СкладНаименование,
	|	ДокументРеализацияТоваровУслуг.Контрагент.Код КАК КонтрагентКод,
	|	ДокументРеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов КАК Курс,
	|	ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК Кратность,
	|	ДокументРеализацияТоваровУслуг.ВалютаДокумента,
	|	ДокументРеализацияТоваровУслуг.ВидОперации,
	|	ДокументРеализацияТоваровУслуг.Проведен,
	|	ДокументРеализацияТоваровУслуг.СуммаВключаетНДС,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.Наименование КАК ДоговорНаименование,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора КАК ДоговорВид,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ДокументРеализацияТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументРеализацияТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчет,
	|	ДокументРеализацияТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатурныйНомер,
	|	РеализацияТоваровУслуг.СчетУчета.Код КАК СчетУчетаКод,
	|	РеализацияТоваровУслуг.ПереданныеСчетУчета.Код КАК ПереданныеСчетУчетаКод,
	|	РеализацияТоваровУслуг.СчетРасходов.Код КАК СчетРасходовКод,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Цена
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Цена * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Сумма
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Сумма * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.СуммаНДС
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.СуммаНДС * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаНДС,
	|	РеализацияТоваровУслуг.Сумма КАК СуммаВВалютеДокумента,
	|	РеализацияТоваровУслуг.СуммаНДС КАК СуммаНДСВВалютеДокумента,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	1 КАК ID,
	|	РеализацияТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслуг.СчетДоходов,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ) КАК ЭтоКомиссия,
	|	РеализацияТоваровУслуг.Ссылка.Ответственный,
	|	ДокументРеализацияТоваровУслуг.Руководитель,
	|	ДокументРеализацияТоваровУслуг.ЗаРуководителяНаОсновании,
	|	ДокументРеализацияТоваровУслуг.ГлавныйБухгалтер,
	|	ДокументРеализацияТоваровУслуг.ЗаГлавногоБухгалтераНаОсновании,
	|	ДокументРеализацияТоваровУслуг.ОтпускПроизвел,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0) КАК СуммаБезНДСРуб
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументРеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|		ПО (РеализацияТоваровУслуг.Ссылка = ДокументРеализацияТоваровУслуг.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|			И (ДокументРеализацияТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументРеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументРеализацияТоваровУслуг.Номер,
	|	ДокументРеализацияТоваровУслуг.Ссылка,
	|	ДокументРеализацияТоваровУслуг.Дата,
	|	ДокументРеализацияТоваровУслуг.Дата,
	|	ДокументРеализацияТоваровУслуг.Организация,
	|	ДокументРеализацияТоваровУслуг.Организация,
	|	ДокументРеализацияТоваровУслуг.Организация,
	|	ДокументРеализацияТоваровУслуг.Склад,
	|	ДокументРеализацияТоваровУслуг.Склад.Наименование,
	|	ДокументРеализацияТоваровУслуг.Контрагент.Код,
	|	ДокументРеализацияТоваровУслуг.Контрагент,
	|	ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументРеализацияТоваровУслуг.ВалютаДокумента,
	|	ДокументРеализацияТоваровУслуг.ВидОперации,
	|	ДокументРеализацияТоваровУслуг.Проведен,
	|	ДокументРеализацияТоваровУслуг.СуммаВключаетНДС,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.Наименование,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах,
	|	ДокументРеализацияТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументРеализацияТоваровУслуг.БанковскийСчетОрганизации,
	|	ДокументРеализацияТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	NULL,
	|	NULL,
	|	РеализацияТоваровУслуг.СчетРасходов.Код,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Цена
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Цена * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Сумма
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Сумма * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.СуммаНДС
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.СуммаНДС * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Сумма,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	3,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.СчетДоходов,
	|	ЛОЖЬ,
	|	РеализацияТоваровУслуг.Ссылка.Ответственный,
	|	ДокументРеализацияТоваровУслуг.Руководитель,
	|	ДокументРеализацияТоваровУслуг.ЗаРуководителяНаОсновании,
	|	ДокументРеализацияТоваровУслуг.ГлавныйБухгалтер,
	|	ДокументРеализацияТоваровУслуг.ЗаГлавногоБухгалтераНаОсновании,
	|	ДокументРеализацияТоваровУслуг.ОтпускПроизвел,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументРеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|		ПО (РеализацияТоваровУслуг.Ссылка = ДокументРеализацияТоваровУслуг.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|			И (ДокументРеализацияТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументРеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументРеализацияТоваровУслуг.Номер,
	|	ДокументРеализацияТоваровУслуг.Ссылка,
	|	ДокументРеализацияТоваровУслуг.Дата,
	|	ДокументРеализацияТоваровУслуг.Дата,
	|	ДокументРеализацияТоваровУслуг.Организация,
	|	ДокументРеализацияТоваровУслуг.Организация,
	|	ДокументРеализацияТоваровУслуг.Организация,
	|	ДокументРеализацияТоваровУслуг.Склад,
	|	ДокументРеализацияТоваровУслуг.Склад.Наименование,
	|	ДокументРеализацияТоваровУслуг.Контрагент.Код,
	|	ДокументРеализацияТоваровУслуг.Контрагент,
	|	ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументРеализацияТоваровУслуг.ВалютаДокумента,
	|	ДокументРеализацияТоваровУслуг.ВидОперации,
	|	ДокументРеализацияТоваровУслуг.Проведен,
	|	ДокументРеализацияТоваровУслуг.СуммаВключаетНДС,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.Наименование,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора,
	|	ДокументРеализацияТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах,
	|	ДокументРеализацияТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументРеализацияТоваровУслуг.БанковскийСчетОрганизации,
	|	ДокументРеализацияТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ РеализацияТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	NULL,
	|	NULL,
	|	NULL,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Цена
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Цена * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Сумма
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Сумма * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументРеализацияТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.СуммаНДС
	|		КОГДА ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.СуммаНДС * ДокументРеализацияТоваровУслуг.КурсВзаиморасчетов / ДокументРеализацияТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Сумма,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	4,
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	NULL,
	|	ИСТИНА,
	|	РеализацияТоваровУслуг.Ссылка.Ответственный,
	|	ДокументРеализацияТоваровУслуг.Руководитель,
	|	ДокументРеализацияТоваровУслуг.ЗаРуководителяНаОсновании,
	|	ДокументРеализацияТоваровУслуг.ГлавныйБухгалтер,
	|	ДокументРеализацияТоваровУслуг.ЗаГлавногоБухгалтераНаОсновании,
	|	ДокументРеализацияТоваровУслуг.ОтпускПроизвел,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументРеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслуг
	|		ПО (РеализацияТоваровУслуг.Ссылка = ДокументРеализацияТоваровУслуг.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|			И (ДокументРеализацияТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументРеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка,
	|	ID,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ унифицированной формы М-15
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме М-15.
//
Функция ПечатьМ15(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	Перем ПодразделениеОтветственныхЛиц;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 10;
	ТабличныйДокумент.ПолеСнизу				= 10;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_РеализацияТоваровУслуг_М15";
	
	СисИнфо = Новый СистемнаяИнформация;
	Если ПустаяСтрока(СисИнфо.ИнформацияПрограммыПросмотра) Тогда 
		ТабличныйДокумент.ПолеСлева			= 0;
	Иначе
		ТабличныйДокумент.ПолеСлева			= 10;
	КонецЕсли;

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_М15");

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыМ15();

	ВыборкаШапки = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	ОписаниеТиповЧисло15_2 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	ОписаниеТиповЧисло15_3 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3);

	ВыборкаСтрок = Новый ТаблицаЗначений;
			
	ВыборкаСтрок.Колонки.Добавить("Номенклатура");
	ВыборкаСтрок.Колонки.Добавить("ТоварНаименование");
	ВыборкаСтрок.Колонки.Добавить("НоменклатурныйНомер");
	ВыборкаСтрок.Колонки.Добавить("СчетУчетаКод");
	ВыборкаСтрок.Колонки.Добавить("ПереданныеСчетУчетаКод");
	ВыборкаСтрок.Колонки.Добавить("СчетРасходовКод");
	ВыборкаСтрок.Колонки.Добавить("Количество", ОписаниеТиповЧисло15_3);
	ВыборкаСтрок.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ВыборкаСтрок.Колонки.Добавить("ЕдиницаИзмеренияКод");
	ВыборкаСтрок.Колонки.Добавить("Цена",                       ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("СуммаВключаетНДС");
	ВыборкаСтрок.Колонки.Добавить("Сумма", 						ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("СуммаНДС", 					ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("СуммаВВалютеДокумента", 		ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("СуммаНДСВВалютеДокумента", 	ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("СтавкаНДС");
    ВыборкаСтрок.Колонки.Добавить("СуммаБезНДСВВалютеДокумента",ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("КоррСчет");
	ВыборкаСтрок.Колонки.Добавить("КоррКод");
	ВыборкаСтрок.Колонки.Добавить("СуммаСНДС", 					ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("ID");
	ВыборкаСтрок.Колонки.Добавить("СуммаБезНДС", 				ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("СуммаРублевая", 				ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("ЭтоКомиссия",   				Новый ОписаниеТипов("Булево"));
	ВыборкаСтрок.Колонки.Добавить("ВсегоРуб", 					ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("НДСРуб", 					ОписаниеТиповЧисло15_2);
	ВыборкаСтрок.Колонки.Добавить("СуммаБезНДСРуб", 			ОписаниеТиповЧисло15_2);

	Пока ВыборкаШапки.СледующийПоЗначениюПоля("Ссылка") Цикл

		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ВыборкаСтрок 	  = ПодготовитьТаблицуДокументаДляПечати(ВыборкаШапки, ВыборкаСтрок, Истина, Ложь);
		
		// Получаем области макета для вывода в табличный документ
		Шапка            = Макет.ПолучитьОбласть("Шапка");
		ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		СтрокаТаблицы    = Макет.ПолучитьОбласть("Строка");
		Подвал           = Макет.ПолучитьОбласть("Подвал");	
		
		// Выведем шапку документа
		Шапка.Параметры.Заполнить(ВыборкаШапки);
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаШапки.ЮрФизЛицо, ВыборкаШапки.ДатаСоставления, ВыборкаШапки.БанковскийСчет);
		СведенияОКонтрагенте  = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаШапки.Контрагент, ВыборкаШапки.ДатаСоставления);
		
		Шапка.Параметры.ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации);
		Шапка.Параметры.ОрганизацияПоОКПО        = СведенияОбОрганизации.КодПоОКПО;
		Шапка.Параметры.НомерДокумента           = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаШапки.Номер, Истина, Ложь);
		Шапка.Параметры.КонтрагентНаименование   = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОКонтрагенте, "НаименованиеДляПечатныхФорм,");
		Шапка.Параметры.Основание                = "Договор " + СокрЛП(ВыборкаШапки.ДоговорНаименование);
		
		ТабличныйДокумент.Вывести(Шапка);
		
		Для каждого СтрокаВыборки Из ВыборкаСтрок Цикл
			
			СтрокаВыборки.КоррСчет          = ?((ВыборкаШапки.ДоговорВид = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером ИЛИ ВыборкаШапки.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности), СтрокаВыборки.ПереданныеСчетУчетаКод, СтрокаВыборки.СчетРасходовКод);
			СтрокаВыборки.КоррКод           = СтрокаВыборки.НоменклатурныйНомер;
			СтрокаВыборки.ТоварНаименование = СокрЛП(СтрокаВыборки.ТоварНаименование);
			
			Сумма      = СтрокаВыборки.Сумма;
			СуммаНДС   = СтрокаВыборки.СуммаНДС;
			Количество = ?(ЗначениеЗаполнено(СтрокаВыборки.Количество), СтрокаВыборки.Количество, 1);
			
			Если ВыборкаШапки.СуммаВключаетНДС Тогда
				СтрокаВыборки.СуммаБезНДС = Сумма - СуммаНДС;
				СтрокаВыборки.СуммаСНДС   = Сумма;
				СтрокаВыборки.Цена        = Окр(СтрокаВыборки.СуммаБезНДС/Количество, 2);
			Иначе
				СтрокаВыборки.СуммаБезНДС = Сумма;
				СтрокаВыборки.СуммаСНДС   = Сумма + СуммаНДС;
			КонецЕсли;
		КонецЦикла;
		
		// Заполним подвал документа

		Подвал.Параметры.Заполнить(ВыборкаШапки);
		
		ПодразделениеОтветственныхЛиц = ВыборкаШапки.ПодразделениеОрганизации;
		
		ПредставлениеПоставщика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
		ОтветственныеЛицаОрганизации = ОтветственныеЛицаБП.ОтветственныеЛица(ВыборкаШапки.Организация, ВыборкаШапки.ДатаДокумента, ВыборкаШапки.ПодразделениеОрганизации);
		
		ДанныеЗаполнения = Новый Структура;
		
		Если ЗначениеЗаполнено(ВыборкаШапки.Руководитель) Тогда
			Если ВыборкаШапки.Руководитель = ОтветственныеЛицаОрганизации.Руководитель Тогда 
				ДанныеЗаполнения.Вставить("ФИОРуководителя", ОтветственныеЛицаОрганизации.РуководительПредставление);
				ДанныеЗаполнения.Вставить("ДолжностьРуководителя", "" + ОтветственныеЛицаОрганизации.РуководительДолжностьПредставление);
			Иначе
				ДанныеОтветственногоЛица = ОтветственныеЛицаБП.РеквизитыПодписиУполномоченногоЛица(ВыборкаШапки.Организация, ВыборкаШапки.Руководитель, ВыборкаШапки.ЗаРуководителяНаОсновании, ВыборкаШапки.ДатаДокумента);
                ЗаместительПоПриказу = "" + ДанныеОтветственногоЛица.ФИО.Представление;
				Если ЗначениеЗаполнено(ВыборкаШапки.ЗаРуководителяНаОсновании) Тогда 
					ЗаместительПоПриказу = ЗаместительПоПриказу + ", " + ДанныеОтветственногоЛица.ОснованиеПраваПодписиПредставление;
				КонецЕсли;
				ДанныеЗаполнения.Вставить("ФИОРуководителя", ЗаместительПоПриказу);
				Если ЗначениеЗаполнено(ДанныеОтветственногоЛица.Должность) Тогда
					ДанныеЗаполнения.Вставить("ДолжностьРуководителя", "" + ДанныеОтветственногоЛица.ДолжностьПредставление);
				КонецЕсли;
			КонецЕсли;
		Иначе
			ДанныеЗаполнения.Вставить("ФИОРуководителя", ОтветственныеЛицаОрганизации.РуководительПредставление);
			ДанныеЗаполнения.Вставить("ДолжностьРуководителя", "" + ОтветственныеЛицаОрганизации.РуководительДолжностьПредставление);
		КонецЕсли;
		
        Если ЗначениеЗаполнено(ВыборкаШапки.ГлавныйБухгалтер) И ВыборкаШапки.ГлавныйБухгалтер <> ОтветственныеЛицаОрганизации.ГлавныйБухгалтер Тогда
            ДанныеОтветственногоЛица = ОтветственныеЛицаБП.РеквизитыПодписиУполномоченногоЛица(ВыборкаШапки.Организация, ВыборкаШапки.ГлавныйБухгалтер, ВыборкаШапки.ЗаГлавногоБухгалтераНаОсновании, ВыборкаШапки.ДатаДокумента);
            ЗаместительПоПриказу = "" + ДанныеОтветственногоЛица.ФИО.Представление;
            Если ЗначениеЗаполнено(ВыборкаШапки.ЗаГлавногоБухгалтераНаОсновании) Тогда 
                ЗаместительПоПриказу = ЗаместительПоПриказу + ", " + ДанныеОтветственногоЛица.ОснованиеПраваПодписиПредставление;
            КонецЕсли;
            ДанныеЗаполнения.Вставить("ФИОГлавБухгалтера", ЗаместительПоПриказу);
            Если ЗначениеЗаполнено(ДанныеОтветственногоЛица.Должность) Тогда
                ДанныеЗаполнения.Вставить("ДолжностьГлавБух", "" + ДанныеОтветственногоЛица.ДолжностьПредставление);
            КонецЕсли;
        Иначе
            ДанныеЗаполнения.Вставить("ФИОГлавБухгалтера", ОтветственныеЛицаОрганизации.ГлавныйБухгалтерПредставление);
            ДанныеЗаполнения.Вставить("ДолжностьГлавБух", ОтветственныеЛицаОрганизации.ГлавныйБухгалтерДолжностьПредставление);
        КонецЕсли;
        
		Если ЗначениеЗаполнено(ВыборкаШапки.ОтпускПроизвел) Тогда 
			ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаШапки.Организация,ВыборкаШапки.ОтпускПроизвел,ВыборкаШапки.ДатаДокумента);
			ДанныеЗаполнения.Вставить("ДолжностьКладовщика", ДанныеФизЛица.Должность);
			ДанныеЗаполнения.Вставить("ФИОКладовщика", ДанныеФизЛица.Представление);
		КонецЕсли;
		
		ИтогНДС      = ВыборкаСтрок.Итог("СуммаНДС");
		
		ДанныеЗаполнения.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", ЧислоПрописью(ВыборкаСтрок.Количество(), ,",,,с,,,,,0"));
		ДанныеЗаполнения.Вставить("СуммаПрописью", ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(ВыборкаСтрок.Итог("СуммаСНДС"), ВалютаРегламентированногоУчета));
		ДанныеЗаполнения.Вставить("ИтогНДС", ?(ЗначениеЗаполнено(ИтогНДС), Формат(Цел(ИтогНДС), "ЧДЦ=0") + " руб. " + Формат((ИтогНДС - Цел(ИтогНДС)) * 100, "ЧЦ=2; ЧВН=") + " коп. ", "______ руб. ______ коп."));
		
		Подвал.Параметры.Заполнить(ДанныеЗаполнения);
		
		// Инициализируем счетчик страниц
		НомерСтраницы = 1;
		
		// Выведем заголовок табличной части
		ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
		
		// Выведем выборку строк
		Для каждого СтрокаВыборки Из ВыборкаСтрок Цикл

			СтрокаТаблицы.Параметры.Заполнить(СтрокаВыборки);
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(СтрокаТаблицы);
			//СтрокаСПодвалом.Добавить(ПодвалТаблицы);
			
			// Если обрабатываемая строка - последняя, будем проверять, помещается ли подвал документа
			Если ВыборкаСтрок.Индекс(СтрокаВыборки) = ВыборкаСтрок.Количество() - 1 Тогда 
				СтрокаСПодвалом.Добавить(Подвал);
			КонецЕсли;

			Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом) Тогда
				//ТабличныйДокумент.Вывести(ПодвалТаблицы);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				НомерСтраницы = НомерСтраницы + 1;
				ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(СтрокаТаблицы);

		КонецЦикла;
		
		// Выведем все подвалы
		
		ТабличныйДокумент.Вывести(Подвал);

		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаШапки.Ссылка);
			
		УправлениеПечатьюБП.ДоработатьФаксимильнуюПодписьИПечать(ТабличныйДокумент, ОбъектыПечати,
			ВыборкаШапки.Организация, ВыборкаШапки.Ссылка);
		
	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыТТН()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РеализацияТоваровУслугТовары.Номенклатура) КАК Количество
	|ПОМЕСТИТЬ КоличествоТоваров
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка В(&МассивДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Перевозчик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ РеализацияТоваровУслуг.Перевозчик
	|	КОНЕЦ КАК Перевозчик,
	|	РеализацияТоваровУслуг.Организация КАК Руководители,
	|	РеализацияТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	РеализацияТоваровУслуг.Контрагент КАК Покупатель,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Склад КАК Склад,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.Представление КАК Основание,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ПодразделениеОрганизации.ОбособленноеПодразделение, ЛОЖЬ) КАК ОбособленноеПодразделение,
	|	ВЫРАЗИТЬ(РеализацияТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное КАК СТРОКА(200)) КАК ПредставлениеПодразделения,
	|	РеализацияТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТоваровУслуг.КурсВзаиморасчетов КАК Курс,
	|	РеализацияТоваровУслуг.КратностьВзаиморасчетов КАК Кратность,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС КАК ЦенаВключаетНДС,
	|	ЕСТЬNULL(КоличествоТоваров.Количество, 0) КАК КоличествоНаименований,
	|	РеализацияТоваровУслуг.Руководитель КАК Руководитель,
	|	РеализацияТоваровУслуг.ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	РеализацияТоваровУслуг.ЗаРуководителяНаОсновании КАК ЗаРуководителяНаОсновании,
	|	РеализацияТоваровУслуг.ЗаГлавногоБухгалтераНаОсновании КАК ЗаГлавногоБухгалтераНаОсновании,
	|	РеализацияТоваровУслуг.ОтпускПроизвел КАК ОтпускПроизвел,
	|	РеализацияТоваровУслуг.ДоверенностьНомер КАК ДоверенностьНомер,
	|	РеализацияТоваровУслуг.ДоверенностьДата КАК ДоверенностьДата,
	|	РеализацияТоваровУслуг.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	РеализацияТоваровУслуг.ДоверенностьЧерезКого КАК ДоверенностьЧерезКого,
	|	РеализацияТоваровУслуг.КраткоеНаименованиеГруза КАК КраткоеНаименованиеГруза,
	|	РеализацияТоваровУслуг.СопроводительныеДокументы КАК СопроводительныеДокументы,
	|	РеализацияТоваровУслуг.Водитель КАК Водитель,
	|	РеализацияТоваровУслуг.ВодительскоеУдостоверение КАК ВодительскоеУдостоверение,
	|	РеализацияТоваровУслуг.МаркаПрицепа КАК Прицеп,
	|	РеализацияТоваровУслуг.РегистрационныйЗнакПрицепа КАК ГосНомерПрицепа,
	|	РеализацияТоваровУслуг.МаркаАвтомобиля КАК МаркаАвтомобиля,
	|	РеализацияТоваровУслуг.РегистрационныйЗнакАвтомобиля КАК РегистрационныйЗнакАвтомобиля,
	|	РеализацияТоваровУслуг.ВодительДолжность КАК ВодительДолжность
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоТоваров КАК КоличествоТоваров
	|		ПО РеализацияТоваровУслуг.Ссылка = КоличествоТоваров.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслуг.ТоварНаименование КАК ТоварНаименование,
	|	РеализацияТоваровУслуг.ТоварКод КАК ТоварКод,
	|	РеализацияТоваровУслуг.Артикул КАК Артикул,
	|	РеализацияТоваровУслуг.Количество КАК Количество,
	|	РеализацияТоваровУслуг.КоличествоМест КАК КоличествоМест,
	|	РеализацияТоваровУслуг.БазоваяЕдиницаНаименование КАК БазоваяЕдиницаНаименование,
	|	РеализацияТоваровУслуг.БазоваяЕдиницаКодПоОКЕИ КАК БазоваяЕдиницаКодПоОКЕИ,
	|	РеализацияТоваровУслуг.ВидУпаковки КАК ВидУпаковки,
	|	РеализацияТоваровУслуг.Коэффициент КАК Коэффициент,
	|	РеализацияТоваровУслуг.КоличествоВОдномМесте КАК КоличествоВОдномМесте,
	|	РеализацияТоваровУслуг.Цена КАК Цена,
	|	РеализацияТоваровУслуг.Сумма КАК Сумма,
	|	РеализацияТоваровУслуг.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслуг.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента,
	|	РеализацияТоваровУслуг.СуммаНДСВВалютеДокумента КАК СуммаНДСВВалютеДокумента,
	|	РеализацияТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|	РеализацияТоваровУслуг.ЭтоВозвратнаяТара КАК ЭтоВозвратнаяТара,
	|	РеализацияТоваровУслуг.КоэффициентПерерасчета КАК КоэффициентПерерасчета,
	|	РеализацияТоваровУслуг.Весовой КАК Весовой
	|ПОМЕСТИТЬ ВТ_ТаблицаПоТоварам
	|ИЗ
	|	&ТаблицаПоТоварам КАК РеализацияТоваровУслуг
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслуг.ТоварНаименование КАК ТоварНаименование,
	|	РеализацияТоваровУслуг.ТоварКод КАК ТоварКод,
	|	РеализацияТоваровУслуг.Артикул КАК Артикул,
	|	РеализацияТоваровУслуг.Количество КАК Количество,
	|	РеализацияТоваровУслуг.КоличествоМест КАК КоличествоМест,
	|	РеализацияТоваровУслуг.БазоваяЕдиницаНаименование КАК БазоваяЕдиницаНаименование,
	|	РеализацияТоваровУслуг.БазоваяЕдиницаКодПоОКЕИ КАК БазоваяЕдиницаКодПоОКЕИ,
	|	РеализацияТоваровУслуг.ВидУпаковки КАК ВидУпаковки,
	|	РеализацияТоваровУслуг.Коэффициент КАК Коэффициент,
	|	РеализацияТоваровУслуг.КоличествоВОдномМесте КАК КоличествоВОдномМесте,
	|	РеализацияТоваровУслуг.Цена КАК Цена,
	|	РеализацияТоваровУслуг.Сумма КАК Сумма,
	|	РеализацияТоваровУслуг.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслуг.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента,
	|	РеализацияТоваровУслуг.СуммаНДСВВалютеДокумента КАК СуммаНДСВВалютеДокумента,
	|	РеализацияТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|	РеализацияТоваровУслуг.ЭтоВозвратнаяТара КАК ЭтоВозвратнаяТара,
	|	РеализацияТоваровУслуг.КоэффициентПерерасчета КАК КоэффициентПерерасчета,
	|	РеализацияТоваровУслуг.Весовой КАК Весовой
	|ИЗ
	|	ВТ_ТаблицаПоТоварам КАК РеализацияТоваровУслуг
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
		
	Возврат ТекстЗапроса;

КонецФункции

// Получить данные объектов для печати ТТН
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//
// Возвращаемое значение:
//  Структура с данными объектов для печати ТТН.
//
Функция ПолучитьДанныеДляПечатнойФормыТТН(МассивОбъектов) Экспорт
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Ссылка.Дата КАК ДатаДокумента,
	|	РеализацияТоваровУслуг.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.Проведен, ЛОЖЬ) КАК Проведен,
	|	РеализацияТоваровУслуг.Ссылка.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.СуммаВключаетНДС, ЛОЖЬ) КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	РеализацияТоваровУслуг.Номенклатура.Код КАК ТоварКод,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Артикул,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.КоличествоМест,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения КАК БазоваяЕдиницаНаименование,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код КАК БазоваяЕдиницаКодПоОКЕИ,
	|	РеализацияТоваровУслуг.ЕдиницаИзмерения КАК ВидУпаковки,
	|	РеализацияТоваровУслуг.Коэффициент,
	|	РеализацияТоваровУслуг.Коэффициент КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Цена
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Цена * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Сумма
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Сумма * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.СуммаНДС
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.СуммаНДС * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаНДС,
	|	РеализацияТоваровУслуг.Сумма КАК СуммаВВалютеДокумента,
	|	РеализацияТоваровУслуг.СуммаНДС КАК СуммаНДСВВалютеДокумента,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара,
	|	1 КАК КоэффициентПерерасчета,
	|	ЛОЖЬ КАК Весовой,
	|	1 КАК ID,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ) КАК ЭтоКомиссия,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0) КАК СуммаБезНДСРуб
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|			И (РеализацияТоваровУслуг.Ссылка.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Ссылка.Дата,
	|	РеализацияТоваровУслуг.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.Проведен, ЛОЖЬ),
	|	РеализацияТоваровУслуг.Ссылка.СчетУчетаРасчетовСКонтрагентом,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.СуммаВключаетНДС, ЛОЖЬ),
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	РеализацияТоваровУслуг.Номенклатура.Код,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	NULL,
	|	1,
	|	1,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Цена
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Цена * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Сумма
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Сумма * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.СуммаНДС
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.СуммаНДС * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Сумма,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ЛОЖЬ,
	|	1,
	|	ЛОЖЬ,
	|	3,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|			И (РеализацияТоваровУслуг.Ссылка.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Ссылка.Дата,
	|	РеализацияТоваровУслуг.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.Проведен, ЛОЖЬ),
	|	РеализацияТоваровУслуг.Ссылка.СчетУчетаРасчетовСКонтрагентом,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка.СуммаВключаетНДС, ЛОЖЬ),
	|	РеализацияТоваровУслуг.НомерСтроки,
	|	РеализацияТоваровУслуг.Номенклатура,
	|	РеализацияТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	РеализацияТоваровУслуг.Номенклатура.Код,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА РеализацияТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Количество,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	РеализацияТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	NULL,
	|	1,
	|	1,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Цена
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Цена * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.Сумма
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.Сумма * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА РеализацияТоваровУслуг.СуммаНДС
	|		КОГДА РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(РеализацияТоваровУслуг.СуммаНДС * РеализацияТоваровУслуг.Ссылка.КурсВзаиморасчетов / РеализацияТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	РеализацияТоваровУслуг.Сумма,
	|	РеализацияТоваровУслуг.СуммаНДС,
	|	РеализацияТоваровУслуг.СтавкаНДС,
	|	ЛОЖЬ,
	|	1,
	|	ЛОЖЬ,
	|	4,
	|	ИСТИНА,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО РеализацияТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И РеализацияТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|			И (РеализацияТоваровУслуг.Ссылка.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка,
	|	ID,
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(РасчетыВУсловныхЕдиницах),
	|	МАКСИМУМ(Проведен),
	|	МАКСИМУМ(СчетУчетаРасчетовСКонтрагентом),
	|	МАКСИМУМ(СуммаВключаетНДС)
	|ПО
	|	Ссылка";

	РезультатЗапросаТЧ = Запрос.Выполнить();
	ВыборкаПоРегистраторам = РезультатЗапросаТЧ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОписаниеТиповЧисло15_2 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	
	ТаблицаПоТоварам = Новый ТаблицаЗначений;
	КолонкиЗапроса = РезультатЗапросаТЧ.Колонки;
	Для Каждого Колонка Из КолонкиЗапроса Цикл
		ТаблицаПоТоварам.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	ТаблицаПоТоварам.Колонки.Добавить("СуммаБезНДС", 				ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаРублевая", 				ОписаниеТиповЧисло15_2);
	
	ТаблицаПоТоварамДоп  = ТаблицаПоТоварам.СкопироватьКолонки();
	
	Пока ВыборкаПоРегистраторам.Следующий() Цикл 
		
		ВыборкаСтрок = ВыборкаПоРегистраторам.Выбрать();
		ТаблицаПоТоварамДоп.Очистить();
		
		ТаблицаПоТоварамДоп = ПодготовитьТаблицуДокументаДляПечати(ВыборкаСтрок, ТаблицаПоТоварамДоп, Истина, Ложь);
	
		// Копируем в общую таблицу
		Для Каждого СтрокаТовар Из ТаблицаПоТоварамДоп Цикл
			НоваяСтрока = ТаблицаПоТоварам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовар);
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаПоТоварам", ТаблицаПоТоварам);
	
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыТТН();
	
	МассивРезультатов 			= Запрос.ВыполнитьПакет();
	РезультатПоШапке			= МассивРезультатов[1];
	РезультатПоТабличнойЧасти 	= МассивРезультатов[3];
	
	СтруктураДанныхДляПечати 	= Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы транспортной накладной
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыТранспортнойНакладной()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Перевозчик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ РеализацияТоваровУслуг.Перевозчик
	|	КОНЕЦ КАК Перевозчик,
	|	РеализацияТоваровУслуг.Организация КАК Руководители,
	|	РеализацияТоваровУслуг.Контрагент КАК Покупатель,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Контрагент КАК ЗаказчикПеревозок,
	|	РеализацияТоваровУслуг.ДоговорКонтрагента.Представление КАК Основание,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	РеализацияТоваровУслуг.КраткоеНаименованиеГруза КАК КраткоеНаименованиеГруза,
	|	РеализацияТоваровУслуг.СопроводительныеДокументы КАК СопроводительныеДокументы,
	|	РеализацияТоваровУслуг.Водитель КАК Водитель,
	|	РеализацияТоваровУслуг.ВодительскоеУдостоверение КАК ВодительскоеУдостоверение,
	|	РеализацияТоваровУслуг.МаркаАвтомобиля КАК МаркаАвтомобиля,
	|	РеализацияТоваровУслуг.РегистрационныйЗнакАвтомобиля КАК РегистрационныйЗнакАвтомобиля,
	|	РеализацияТоваровУслуг.ОтпускПроизвел КАК ОтпускПроизвел
	|ПОМЕСТИТЬ ВТ_РеквизитыТранспортнойНакладной
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РеквизитыТранспортнойНакладной.Ссылка КАК Ссылка,
	|	ВТ_РеквизитыТранспортнойНакладной.Дата КАК Дата,
	|	ВТ_РеквизитыТранспортнойНакладной.Номер КАК Номер,
	|	ВТ_РеквизитыТранспортнойНакладной.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА КонтрагентГрузополучатель.ОбособленноеПодразделение
	|			ТОГДА КонтрагентГрузополучатель.ГоловнойКонтрагент
	|		ИНАЧЕ ВТ_РеквизитыТранспортнойНакладной.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВТ_РеквизитыТранспортнойНакладной.Грузоотправитель КАК Грузоотправитель,
	|	ВТ_РеквизитыТранспортнойНакладной.Перевозчик КАК Перевозчик,
	|	ВТ_РеквизитыТранспортнойНакладной.Руководители КАК Руководители,
	|	ВТ_РеквизитыТранспортнойНакладной.Покупатель КАК Покупатель,
	|	ВТ_РеквизитыТранспортнойНакладной.Контрагент КАК Контрагент,
	|	ВТ_РеквизитыТранспортнойНакладной.ЗаказчикПеревозок КАК ЗаказчикПеревозок,
	|	ВТ_РеквизитыТранспортнойНакладной.Основание КАК Основание,
	|	ВТ_РеквизитыТранспортнойНакладной.АдресДоставки КАК АдресДоставки,
	|	ВТ_РеквизитыТранспортнойНакладной.КраткоеНаименованиеГруза КАК КраткоеНаименованиеГруза,
	|	ВТ_РеквизитыТранспортнойНакладной.СопроводительныеДокументы КАК СопроводительныеДокументы,
	|	ВТ_РеквизитыТранспортнойНакладной.Водитель КАК Водитель,
	|	ВТ_РеквизитыТранспортнойНакладной.ВодительскоеУдостоверение КАК ВодительскоеУдостоверение,
	|	ВТ_РеквизитыТранспортнойНакладной.МаркаАвтомобиля КАК МаркаАвтомобиля,
	|	ВТ_РеквизитыТранспортнойНакладной.РегистрационныйЗнакАвтомобиля КАК РегистрационныйЗнакАвтомобиля,
	|	ВТ_РеквизитыТранспортнойНакладной.ОтпускПроизвел КАК ОтпускПроизвел
	|ИЗ
	|	ВТ_РеквизитыТранспортнойНакладной КАК ВТ_РеквизитыТранспортнойНакладной
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК КонтрагентГрузополучатель
	|		ПО ВТ_РеквизитыТранспортнойНакладной.Грузополучатель = КонтрагентГрузополучатель.Ссылка";
		
	Возврат ТекстЗапроса;
КонецФункции

// Получить данные объектов для печати транспортной накладной
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//
// Возвращаемое значение:
//  Структура с данными объектов для печати транспортной накладной.
//
Функция ПолучитьДанныеДляПечатнойФормыТранспортнаяНакладная(МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыТранспортнойНакладной();
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаРезультата;
	
КонецФункции

Функция ПодготовитьТекстЗапросаДляПечатиСправкиРасчетаРублевыеСуммыДокументовВВалюте(НомераТаблиц) Экспорт
	
	НомераТаблиц.Вставить("ВТ_ТаблицаПоШапкеДокумента",                                НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаРеквизитов",                                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РегистрСведенийРублевыеСуммыДокументовВВалюте",             НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ПоДокументамЗачетнныхАвансов",                           НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПредоплат",                                          НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_ВТ_ПоДокументамЗачетнныхАвансов",               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_ВТ_ТаблицаПоШапкеДокумента",                    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаСумм",                                               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_РегистрСведенийРублевыеСуммыДокументовВВалюте", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеОбрабатываемогоДокумента.Ссылка КАК Ссылка,
	|	ДанныеОбрабатываемогоДокумента.Дата КАК Дата,
	|	ДанныеОбрабатываемогоДокумента.Проведен КАК Проведен,
	|	ДанныеОбрабатываемогоДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ДанныеОбрабатываемогоДокумента.Организация КАК Организация,
	|	ДанныеОбрабатываемогоДокумента.Контрагент КАК Контрагент,
	|	ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ДанныеОбрабатываемогоДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ)
	|						ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиУЕ)
	|					КОГДА ЕСТЬNULL(ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.ОплатаВВалюте, ЛОЖЬ)
	|						ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиВал)
	|					ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВСчетОтгрузкиРуб)
	|				КОНЕЦ
	|		ИНАЧЕ ДанныеОбрабатываемогоДокумента.СчетУчетаРасчетовСКонтрагентом
	|	КОНЕЦ КАК СчетУчетаРасчетовСКонтрагентом,
	|	ДанныеОбрабатываемогоДокумента.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	ДанныеОбрабатываемогоДокумента.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ДанныеОбрабатываемогоДокумента.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	"""" КАК НомерВходящегоДокумента,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ЕСТЬNULL(ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.УчетАгентскогоНДС, ЛОЖЬ) КАК УчетАгентскогоНДС,
	|	ДанныеОбрабатываемогоДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ПОМЕСТИТЬ ВТ_ТаблицаПоШапкеДокумента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеОбрабатываемогоДокумента
	|ГДЕ
	|	ДанныеОбрабатываемогоДокумента.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаПоШапкеДокумента.Ссылка КАК Ссылка,
	|	ВТ_ТаблицаПоШапкеДокумента.Дата КАК Дата,
	|	ВТ_ТаблицаПоШапкеДокумента.Проведен КАК Проведен,
	|	ВТ_ТаблицаПоШапкеДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаПоШапкеДокумента.Контрагент КАК Контрагент,
	|	ВТ_ТаблицаПоШапкеДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ТаблицаПоШапкеДокумента.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ВТ_ТаблицаПоШапкеДокумента.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	ВТ_ТаблицаПоШапкеДокумента.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ВТ_ТаблицаПоШапкеДокумента.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	ВТ_ТаблицаПоШапкеДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	0 КАК ВсегоВал,
	|	0 КАК НДСВал
	|ИЗ
	|	ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РублевыеСуммыДокументовВВалюте.Всего КАК Всего,
	|	РублевыеСуммыДокументовВВалюте.НДС КАК НДС,
	|	РублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РублевыеСуммыДокументовВВалюте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|		ПО РублевыеСуммыДокументовВВалюте.Регистратор = ВТ_ТаблицаПоШапкеДокумента.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ТабличнаяЧастьДокумента,
	|	НомерСтрокиДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Регистратор КАК Ссылка,
	|	Хозрасчетный.Сумма КАК СуммаПредоплатыРуб,
	|	Хозрасчетный.ВалютнаяСуммаДт КАК СуммаПредоплатыВал,
	|	ХозрасчетныйСубконто.Значение КАК Документ,
	|	ПРЕДСТАВЛЕНИЕ(ХозрасчетныйСубконто.Значение) КАК ДокументПредоплатыПредставление,
	|	ВТ_ТаблицаПоШапкеДокумента.Организация КАК Организация
	|ПОМЕСТИТЬ ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|		ПО Хозрасчетный.Регистратор = ВТ_ТаблицаПоШапкеДокумента.Ссылка
	|			И Хозрасчетный.СчетДт = ВТ_ТаблицаПоШапкеДокумента.СчетУчетаРасчетовПоАвансам
	|			И Хозрасчетный.СчетКт = ВТ_ТаблицаПоШапкеДокумента.СчетУчетаРасчетовСКонтрагентом
	|			И (ВТ_ТаблицаПоШапкеДокумента.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор
	|			И Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|			И (ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Ссылка КАК Ссылка,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.СуммаПредоплатыРуб КАК СуммаПредоплатыРуб,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.СуммаПредоплатыВал КАК СуммаПредоплатыВал,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Документ КАК Документ,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.ДокументПредоплатыПредставление КАК ДокументПредоплатыПредставление,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВходящегоДокумента
	|ИЗ
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента КАК ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Организация = ДанныеПервичныхДокументов.Организация
	|			И ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Документ = ДанныеПервичныхДокументов.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Ссылка
	|ИТОГИ
	|	СУММА(СуммаПредоплатыРуб),
	|	СУММА(СуммаПредоплатыВал)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаПоШапкеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка КАК Ссылка,
	|	2 КАК ПорядокТабличныхЧастей,
	|	ОбрабатываемаяТаблица.НомерСтроки КАК НомерСтроки,
	|	ОбрабатываемаяТаблица.Номенклатура КАК Товар,
	|	ОбрабатываемаяТаблица.Номенклатура.Наименование КАК ТоварНаименование,
	|	ОбрабатываемаяТаблица.Сумма КАК ВсегоВал,
	|	ОбрабатываемаяТаблица.СуммаНДС КАК НДСВал,
	|	ОбрабатываемаяТаблица.СтавкаНДС КАК СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК ВсегоРуб,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДСРуб,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДСРуб
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка,
	|	1,
	|	ОбрабатываемаяТаблица.НомерСтроки,
	|	ОбрабатываемаяТаблица.Номенклатура,
	|	ОбрабатываемаяТаблица.Номенклатура.Наименование,
	|	ОбрабатываемаяТаблица.Сумма,
	|	ОбрабатываемаяТаблица.СуммаНДС,
	|	ОбрабатываемаяТаблица.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка,
	|	3,
	|	ОбрабатываемаяТаблица.НомерСтроки,
	|	ОбрабатываемаяТаблица.Номенклатура,
	|	ОбрабатываемаяТаблица.Номенклатура.Наименование,
	|	ОбрабатываемаяТаблица.Сумма,
	|	ОбрабатываемаяТаблица.СуммаНДС,
	|	ОбрабатываемаяТаблица.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ПорядокТабличныхЧастей,
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(ВсегоВал),
	|	СУММА(НДСВал),
	|	СУММА(ВсегоРуб),
	|	СУММА(НДСРуб),
	|	СУММА(НалоговаяБазаНДСРуб)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте";
	
	Возврат ТекстЗапроса;
	
КонецФункции	

#Область ПечатьАктаОПриемкеВыполненныхСтроительныхРабот

Функция ДанныеДляПечатиАктаОПриемкеВыполненныхСтроительныхРабот(МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	ЕСТЬNULL(ОбъектыВыполненияРабот.Ссылка, НЕОПРЕДЕЛЕНО) КАК ОбъектВыполненияРабот,
	|	ЕСТЬNULL(ОбъектыВыполненияРабот.Наименование, """") КАК ОбъектВыполненияРаботНаименование,
	|	ЕСТЬNULL(ОбъектыВыполненияРабот.НаименованиеПолное, """") КАК ОбъектВыполненияРаботНаименованиеПолное,
	|	РеализацияТоваровУслуг.ПериодНачалаРабот КАК ПериодНачалаРабот,
	|	РеализацияТоваровУслуг.ПериодОкончанияРабот КАК ПериодОкончанияРабот,
	|	РеализацияТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(Валюты.Код, """") КАК ВалютаКод,
	|	ЕСТЬNULL(Валюты.Наименование, """") КАК ВалютаНаименование,
	|	РеализацияТоваровУслуг.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	РеализацияТоваровУслуг.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.Номер, """") КАК НомерДоговора,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаДоговора,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.Наименование, """") КАК НаименованиеДоговора
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК Валюты
	|		ПО РеализацияТоваровУслуг.ВалютаДокумента = Валюты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РеализацияТоваровУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыВыполненияРабот КАК ОбъектыВыполненияРабот
	|		ПО РеализацияТоваровУслуг.ОбъектВыполненияРабот = ОбъектыВыполненияРабот.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.СтроительныеРаботы)
	|	И РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугУслуги.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугУслуги.Содержание КАК Содержание,
	|	ЕСТЬNULL(СправочникНоменклатура.Наименование, """") КАК НоменклатураНаименование,
	|	ЕСТЬNULL(СправочникНоменклатура.НаименованиеПолное, """") КАК НоменклатураНаименованиеПолное,
	|	РеализацияТоваровУслугУслуги.Сумма КАК Сумма,
	|	РеализацияТоваровУслугУслуги.СтавкаНДС КАК СтавкаНДС,
	|	РеализацияТоваровУслугУслуги.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслугУслуги.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО РеализацияТоваровУслугУслуги.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслугУслуги.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугСмета.НомерСтроки КАК НомерСтроки,
	|	РеализацияТоваровУслугСмета.ПозицияПоСмете КАК НомерПозицииПоСмете,
	|	РеализацияТоваровУслугСмета.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугСмета.Количество КАК Количество,
	|	РеализацияТоваровУслугСмета.Цена КАК Цена,
	|	РеализацияТоваровУслугСмета.Сумма КАК Сумма,
	|	ЕСТЬNULL(СправочникНоменклатура.Наименование, """") КАК НоменклатураНаименование,
	|	ЕСТЬNULL(СправочникНоменклатура.НаименованиеПолное, """") КАК НоменклатураНаименованиеПолное,
	|	РеализацияТоваровУслугСмета.Содержание КАК Содержание,
	|	ЕСТЬNULL(КлассификаторЕдиницИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
	|	ЕСТЬNULL(КлассификаторЕдиницИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	РеализацияТоваровУслугСмета.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Смета КАК РеализацияТоваровУслугСмета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторЕдиницИзмерения КАК КлассификаторЕдиницИзмерения
	|			ПО СправочникНоменклатура.ЕдиницаИзмерения = КлассификаторЕдиницИзмерения.Ссылка
	|		ПО РеализацияТоваровУслугСмета.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслугСмета.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаДанныхПечати = Новый ТаблицаЗначений;
	ТаблицаДанныхПечати.Колонки.Добавить("НомерАкта");
	ТаблицаДанныхПечати.Колонки.Добавить("ДатаАкта");
	ТаблицаДанныхПечати.Колонки.Добавить("Дата");
	ТаблицаДанныхПечати.Колонки.Добавить("Организация");
	ТаблицаДанныхПечати.Колонки.Добавить("Контрагент");
	ТаблицаДанныхПечати.Колонки.Добавить("ВалютаКод");
	ТаблицаДанныхПечати.Колонки.Добавить("ВалютаНаименование");
	ТаблицаДанныхПечати.Колонки.Добавить("КурсВзаиморасчетов");
	ТаблицаДанныхПечати.Колонки.Добавить("КратностьВзаиморасчетов");
	ТаблицаДанныхПечати.Колонки.Добавить("НомерДоговора");
	ТаблицаДанныхПечати.Колонки.Добавить("ДатаДоговора");
	ТаблицаДанныхПечати.Колонки.Добавить("НаименованиеДоговора");
	ТаблицаДанныхПечати.Колонки.Добавить("ПредставлениеЗаказчика");
	ТаблицаДанныхПечати.Колонки.Добавить("ПредставлениеПодрядчика");
	ТаблицаДанныхПечати.Колонки.Добавить("АдресОбъекта");
	ТаблицаДанныхПечати.Колонки.Добавить("АдресОбъектаКодСтраны");
	ТаблицаДанныхПечати.Колонки.Добавить("ОбъектВыполненияРабот");
	ТаблицаДанныхПечати.Колонки.Добавить("ОбъектВыполненияРаботНаименование");
	ТаблицаДанныхПечати.Колонки.Добавить("ПериодНачалаРабот");
	ТаблицаДанныхПечати.Колонки.Добавить("ПериодОкончанияРабот");
	ТаблицаДанныхПечати.Колонки.Добавить("ПериодНачалаРаботСтр");
	ТаблицаДанныхПечати.Колонки.Добавить("ПериодОкончанияРаботСтр");
	ТаблицаДанныхПечати.Колонки.Добавить("ДанныеРаздела");
	ТаблицаДанныхПечати.Колонки.Добавить("Ссылка");
	ТаблицаДанныхПечати.Колонки.Добавить("Всего");
	ТаблицаДанныхПечати.Колонки.Добавить("ВсегоНДС");
	ТаблицаДанныхПечати.Колонки.Добавить("ВсегоСУчетомНДС");
	ТаблицаДанныхПечати.Колонки.Добавить("СуммаВключаетНДС");
	
	ТаблицаШапки   = РезультатЗапроса[0].Выгрузить();
	ТаблицаРаздела = РезультатЗапроса[1].Выгрузить();
	ТаблицаСметы   = РезультатЗапроса[2].Выгрузить();
	Для Каждого СтрокаШапки из ТаблицаШапки Цикл
		СтрокаДанныхПечати = ТаблицаДанныхПечати.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаДанныхПечати, СтрокаШапки);
		
		СтрокаДанныхПечати.НомерАкта = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаШапки.Номер, Истина, Ложь);
		СтрокаДанныхПечати.ДатаАкта  = Формат(СтрокаШапки.Дата, "ДЛФ=DD");
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(СтрокаШапки.Организация, СтрокаШапки.Дата, , , , Истина);
		СтрокаДанныхПечати.ПредставлениеПодрядчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации,
			"НаименованиеДляПечатныхФорм,ИНН,ЮридическийАдрес");
		
		СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(СтрокаШапки.Контрагент, СтрокаШапки.Дата);
		СтрокаДанныхПечати.ПредставлениеЗаказчика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОКонтрагенте,
			"НаименованиеДляПечатныхФорм,ИНН,ЮридическийАдрес");
		
		СтрокаДанныхПечати.ОбъектВыполненияРаботНаименование = ?(ЗначениеЗаполнено(СтрокаШапки.ОбъектВыполненияРаботНаименованиеПолное),
			  СтрокаШапки.ОбъектВыполненияРаботНаименованиеПолное, СтрокаШапки.ОбъектВыполненияРаботНаименование);
		
		АдресОбъектаСтруктурой = УправлениеКонтактнойИнформациейБП.АдресСтруктурой(СтрокаШапки.ОбъектВыполненияРабот,
			Справочники.ВидыКонтактнойИнформации.АдресОбъектаВыполненияРабот);
		СтрокаДанныхПечати.АдресОбъекта          = АдресОбъектаСтруктурой.Представление;
		СтрокаДанныхПечати.АдресОбъектаКодСтраны = АдресОбъектаСтруктурой.КодСтраны;
		
		СтрокаДанныхПечати.ПериодНачалаРаботСтр    = Формат(СтрокаШапки.ПериодНачалаРабот, "ДЛФ=D");
		СтрокаДанныхПечати.ПериодОкончанияРаботСтр = Формат(СтрокаШапки.ПериодОкончанияРабот, "ДЛФ=D");
		
		СтрокаДанныхПечати.ДанныеРаздела = ДанныеРазделаРабот(ТаблицаРаздела, ТаблицаСметы, СтрокаШапки.Ссылка, СтрокаДанныхПечати);
	КонецЦикла;
	
	Возврат ТаблицаДанныхПечати;
	
КонецФункции

Функция ДанныеРазделаРабот(ТаблицаРаздела, ТаблицаСметы, СсылкаНаДокумент, СтрокаДанныхПечати)
	
	ДанныеРаздела = Новый ТаблицаЗначений;
	ДанныеРаздела.Колонки.Добавить("Работы");
	ДанныеРаздела.Колонки.Добавить("РаботыНаименование");
	ДанныеРаздела.Колонки.Добавить("Сумма");
	ДанныеРаздела.Колонки.Добавить("СуммаНДС");
	ДанныеРаздела.Колонки.Добавить("СуммаСНДС");
	ДанныеРаздела.Колонки.Добавить("СтавкаНДС");
	ДанныеРаздела.Колонки.Добавить("ДанныеПоСмете");
	
	НайденныеСтроки = ТаблицаРаздела.НайтиСтроки(Новый Структура("Ссылка", СсылкаНаДокумент));
	Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
		СтрокаДанныхРаздела = ДанныеРаздела.Добавить();
		СтрокаДанныхРаздела.Работы = СтрокаТаблицы.Номенклатура;
		СтрокаДанныхРаздела.РаботыНаименование = ?(ЗначениеЗаполнено(СтрокаТаблицы.Содержание), 
			СтрокаТаблицы.Содержание, СтрокаТаблицы.НоменклатураНаименование);
		СтрокаДанныхРаздела.Сумма = ?(СтрокаДанныхПечати.СуммаВключаетНДС,
			СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС, СтрокаТаблицы.Сумма);
		СтрокаДанныхРаздела.СуммаНДС  = СтрокаТаблицы.СуммаНДС;
		СтрокаДанныхРаздела.СуммаСНДС = СтрокаДанныхРаздела.Сумма + СтрокаДанныхРаздела.СуммаНДС;
		СтрокаДанныхРаздела.СтавкаНДС = СтрокаТаблицы.СтавкаНДС;
		
		СтрокаДанныхРаздела.ДанныеПоСмете = ДанныеПоТаблицеСметы(ТаблицаСметы, СсылкаНаДокумент);
		
		СтрокаДанныхПечати.Всего           = СтрокаДанныхРаздела.Сумма;
		СтрокаДанныхПечати.ВсегоНДС        = СтрокаДанныхРаздела.СуммаНДС;
		СтрокаДанныхПечати.ВсегоСУчетомНДС = СтрокаДанныхРаздела.СуммаСНДС;
	КонецЦикла;
	
	Возврат ДанныеРаздела;
	
КонецФункции

Функция ДанныеПоТаблицеСметы(ТаблицаСметы, СсылкаНаДокумент)
	
	ДанныеПоСмете = Новый ТаблицаЗначений;
	ДанныеПоСмете.Колонки.Добавить("НомерСтроки");
	ДанныеПоСмете.Колонки.Добавить("НомерПозицииПоСмете");
	ДанныеПоСмете.Колонки.Добавить("НоменклатураНаименование");
	ДанныеПоСмете.Колонки.Добавить("ЕдиницаИзмеренияКод");
	ДанныеПоСмете.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ДанныеПоСмете.Колонки.Добавить("Количество");
	ДанныеПоСмете.Колонки.Добавить("Цена");
	ДанныеПоСмете.Колонки.Добавить("Сумма");
	
	НайденныеСтроки = ТаблицаСметы.НайтиСтроки(Новый Структура("Ссылка", СсылкаНаДокумент));
	Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
		СтрокаДанныеПоСмете = ДанныеПоСмете.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныеПоСмете, СтрокаТаблицы);
		Если ЗначениеЗаполнено(СтрокаТаблицы.Содержание) Тогда
			СтрокаДанныеПоСмете.НоменклатураНаименование = СтрокаТаблицы.Содержание;
		Иначе
			СтрокаДанныеПоСмете.НоменклатураНаименование = ?(ЗначениеЗаполнено(СтрокаТаблицы.НоменклатураНаименованиеПолное), 
				СтрокаТаблицы.НоменклатураНаименованиеПолное, СтрокаТаблицы.НоменклатураНаименование);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеПоСмете;
	
КонецФункции

Функция ПечатьАктаОПриемкеВыполненныхСтроительныхРабот(ТаблицаДанныхДляПечати, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб        = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ПервыйДокумент = Истина;
	Макет = УправлениеПечатью.МакетПечатнойФормы(ПараметрыПечати.ИмяМакетаПечати);
	Для Каждого СтрокаДанныхПечати Из ТаблицаДанныхДляПечати Цикл
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Шапка
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(СтрокаДанныхПечати);
		ТабличныйДокумент.Вывести(Область);
		
		// Шапка таблицы
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы");
		Область.Параметры.Заполнить(СтрокаДанныхПечати);
		ТабличныйДокумент.Вывести(Область);
		
		// Данные по разделу работ
		ДанныеРаздела = СтрокаДанныхПечати.ДанныеРаздела;
		Для Каждого СтрокаДанныхРаздела Из ДанныеРаздела Цикл
			Область = Макет.ПолучитьОбласть("СтрокаРаздела");
			Область.Параметры.Заполнить(СтрокаДанныхРаздела);
			ТабличныйДокумент.Вывести(Область);
			
			// Данные по смете
			ДанныеПоСмете = СтрокаДанныхРаздела.ДанныеПоСмете;
			Для Каждого СтрокаДанныхПоСмете Из ДанныеПоСмете Цикл
				Область = Макет.ПолучитьОбласть("СтрокаДанныхПоСмете");
				Область.Параметры.Заполнить(СтрокаДанныхПоСмете);
				ТабличныйДокумент.Вывести(Область);
			КонецЦикла;
		КонецЦикла;
		
		// Подвал таблицы
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы");
		Область.Параметры.Заполнить(СтрокаДанныхПечати);
		ТабличныйДокумент.Вывести(Область);
		
		// Подвал
		Область = Макет.ПолучитьОбласть("Подвал");
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, СтрокаДанныхПечати.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

Функция ПрочитатьДанныеКарточкиЭД(ДвоичныеДанные, ВидДокумента = "")
	
	Результат = Неопределено;
	Попытка
		ДанныеКонтейнера = ОбменСКонтрагентами.ОписаниеКонтейнераЭлектронногоДокумента(ДвоичныеДанные);
		
		ПараметрыВизуализации = ОбменСКонтрагентами.НовыеПараметрыВизуализацииЭлектронногоДокумента();
		ПараметрыВизуализации.ВыводитьДопДанные = Ложь;
		ПараметрыВизуализации.ВыводитьБанковскиеРеквизиты = Истина;
		ПараметрыВизуализации.ВыводитьКопияВерна = Ложь;
		ПредставлениеДокумента = ОбменСКонтрагентами.ПредставлениеЭлектронногоДокументаПоФайлу(ДанныеКонтейнера.Содержание.ВидДокумента,
			ДанныеКонтейнера.ДанныеОсновногоФайла.ДвоичныеДанные,, ПараметрыВизуализации);
		
		Данные = ОбменСКонтрагентами.ДанныеЭлектронногоДокументаПоФайлу(ДанныеКонтейнера.ДанныеОсновногоФайла.ДвоичныеДанные);
		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("ДеревоРазбора", Данные.ДеревоРазбора);
		ДанныеДокумента.Вставить("СтрокаОбъекта", Данные.НовыйЭД);

	Исключение
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеБПВызовСервера.ОбработатьИсключениеПоЭДНаСервере(НСтр("ru='Получение данных электронного документа'"),
			ТекстОшибки);
		Возврат Результат;
	КонецПопытки;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ИНН"         , ДанныеКонтейнера.Содержание.Отправитель.ИНН);
	СтруктураДанных.Вставить("КПП"         , ДанныеКонтейнера.Содержание.Отправитель.КПП);
	СтруктураДанных.Вставить("Наименование", ДанныеКонтейнера.Содержание.Отправитель.Наименование);
	СтруктураДанных.Вставить("Показывать"  , Ложь);
	СтруктураДанных.Вставить("ВидЭД"       , ДанныеКонтейнера.Содержание.ТипДокумента);
	СтруктураДанных.Вставить("Документ"      , ""); //наименование первичного документа из xml
	СтруктураДанных.Вставить("НомерДокумента", ДанныеКонтейнера.Содержание.НомерДокумента);
	СтруктураДанных.Вставить("ДатаДокумента" , ДанныеКонтейнера.Содержание.ДатаДокумента);
	СтруктураДанных.Вставить("СуммаДокумента", ДанныеКонтейнера.Содержание.СуммаДокумента);
	СтруктураДанных.Вставить("ФайлДанных"    , ДанныеКонтейнера.ДанныеОсновногоФайла);
	
	СтруктураДанных.Вставить("ТабличныйДокумент", ПредставлениеДокумента);
	
	СтруктураДанных.Вставить("ДанныеДокумента", ДанныеДокумента);
	Если Не ЗначениеЗаполнено(СтруктураДанных.ИНН) Тогда
		
		ШаблонСообщения = НСтр("ru = 'У отправителя ""%1"" не указан ИНН'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СтруктураДанных.Наименование);				
		СтруктураДанных.Вставить("ТекстОшибки", ТекстОшибки);
		
	КонецЕсли;
	
	Если СтруктураДанных.ВидЭД = ОбменСКонтрагентами.ТипыДокументов().АктВыполненныхРабот Тогда
		Если ДанныеКонтейнера.Содержание.Формат = ОбменСКонтрагентами.ФорматыДокументов().ФНС.ПередачаРабот.ИнформацияПродавца Тогда
			Документ = НСтр("ru='Акт о выполнении работ (оказании услуг) '") + СтруктураДанных.НомерДокумента + НСтр("ru=' от '") + СтруктураДанных.ДатаДокумента;
		Иначе
			Документ = НСтр("ru = 'УПД №'") + " " + СтруктураДанных.НомерДокумента
				+ " " + НСтр("ru = 'от'") + " " + СтруктураДанных.ДатаДокумента;
		КонецЕсли;
		СтруктураДанных.Документ = Документ;
	ИначеЕсли СтруктураДанных.ВидЭД = ОбменСКонтрагентами.ТипыДокументов().ТоварнаяНакладная
		    Или СтруктураДанных.ВидЭД = ОбменСКонтрагентами.ТипыДокументов().УПД
			Или СтруктураДанных.ВидЭД = Перечисления.ТипыДокументовЭДО.Прочее Тогда
		Документ = НСтр("ru = 'УПД №'") + " " + СтруктураДанных.НомерДокумента
				+ " " + НСтр("ru = 'от'") + " " + Формат(СтруктураДанных.ДатаДокумента, "ДЛФ=DD");
			
		СтруктураДанных.Показывать = Истина;
		СтруктураДанных.Документ = Документ;
	КонецЕсли;
	
	Если СтруктураДанных.ВидЭД <> Перечисления.ТипыДокументовЭДО.Прочее Тогда
		
		ИННОрганизации = ДанныеКонтейнера.Содержание.Получатель.ИНН;
		Если Не ЗначениеЗаполнено(ИННОрганизации) Тогда
			
			ТекстОшибки = НСтр("ru = 'Не указан ИНН организации-получателя'");
			СтруктураДанных.Вставить("ТекстОшибки", ТекстОшибки);
			
		Иначе
			
			СсылкаНаОрганизацию = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Организации", ИННОрганизации);
			Если Не ЗначениеЗаполнено(СсылкаНаОрганизацию) Тогда
				
				ШаблонСообщения = НСтр("ru = 'Не удалось найти организацию-получателя с ИНН %1'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ИННОрганизации);
				СтруктураДанных.Вставить("ТекстОшибки", ТекстОшибки);
				
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция ИмяСохраняемогоФайла(ВладелецЭД)
	
	НаименованиеФайла = "";
	Если ЗначениеЗаполнено(ВладелецЭД) И НЕ ЗначениеЗаполнено(НаименованиеФайла) Тогда
		
		Если ТипЗнч(ВладелецЭД) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ШаблонФайла = НСтр("ru='Электронный %1 № %2 от %3'");
		Иначе
			ШаблонФайла = НСтр("ru='Электронная %1 № %2 от %3'");
		КонецЕсли;
		
		СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВладелецЭД, "Номер, Дата");
		НаименованиеФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонФайла,
			НРег(Строка(ТипЗнч(ВладелецЭД))),
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтруктураРеквизитов.Номер, Истина),
			Формат(СтруктураРеквизитов.Дата, "ДЛФ=DD"));
		
		НаименованиеФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(НаименованиеФайла);
		
	КонецЕсли;
	
	Возврат НаименованиеФайла;
	
КонецФункции

Функция ПодготовитьТаблицуНДСПоОтгрузкеСУчетомКурсаАвансов(ТаблицаНДСПоОтгрузке, ТаблицаВзаиморасчеты, ТаблицаРеквизиты) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаРеквизиты)
	 Или Не ЗначениеЗаполнено(ТаблицаНДСПоОтгрузке) Тогда
		Возврат ТаблицаНДСПоОтгрузке;
	КонецЕсли;
	
	СтруктураТаблиц = Новый Структура;
	СтруктураТаблиц.Вставить("ТоварыУслуги",  ТаблицаНДСПоОтгрузке);
	СтруктураТаблиц.Вставить("Взаиморасчеты", ТаблицаВзаиморасчеты);
	СтруктураТаблиц.Вставить("Реквизиты",     ТаблицаРеквизиты);
	
	Возврат УчетДоходовРасходов.ПолучитьТаблицуРеализацииПоКурсуАвансов(
		СтруктураТаблиц, Ложь);

КонецФункции

Функция КоличествоДокументовЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт
	
	ТаблицаДанных = ПерсонализированныеПредложенияСервисов.ТаблицаДанных();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РеализацияТоваровУслуг.Ссылка) КАК КоличествоДокументов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Организация = &Организация
	|	И РеализацияТоваровУслуг.Дата >= &НачалоПериода
	|	И РеализацияТоваровУслуг.Дата <= &КонецПериода
	|	И РеализацияТоваровУслуг.Проведен = ИСТИНА";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КоличествоДокументов = Выборка.КоличествоДокументов;
	Иначе
		КоличествоДокументов = 0;
	КонецЕсли;
	
	// Добавляем итог по разделу
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок            = 0;
	СтрокаДанных.ЗначениеПоказателя = КоличествоДокументов;

	Возврат ТаблицаДанных;
	
КонецФункции

#Область ОбработчикиОбновления

Процедура ЗаменитьВидыОперацийНаПростыеПриПереходеС20Отложенно(Параметры) Экспорт

	Запрос = Новый Запрос;
	
	КонецПериодаВыборки = '20191231235959';
	
	Если Параметры.Свойство("КонецПериодаВыборки")
		И ТипЗнч(Параметры.КонецПериодаВыборки) = Тип("Дата") Тогда
		КонецПериодаВыборки = Параметры.КонецПериодаВыборки;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("КонецПериодаВыборки", 	КонецПериодаВыборки);	
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Дата
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата <= &КонецПериодаВыборки
	|	И РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПродажаКомиссия)
	|	И РеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваровУслуг.Дата УБЫВ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РеализацияТоваровУслуг.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка,
	|	ТаблицаДокументов.Дата,
	|	СУММА(ТаблицаДокументов.ЕстьТовары) КАК ЕстьТовары,
	|	СУММА(ТаблицаДокументов.ЕстьВозвратнаяТара) КАК ЕстьВозвратнаяТара,
	|	СУММА(ТаблицаДокументов.ЕстьУслуги) КАК ЕстьУслуги,
	|	СУММА(ТаблицаДокументов.ЕстьАгентскиеУслуги) КАК ЕстьАгентскиеУслуги
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_Документы.Ссылка КАК Ссылка,
	|		ВТ_Документы.Дата КАК Дата,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0) КАК ЕстьТовары,
	|		0 КАК ЕстьВозвратнаяТара,
	|		0 КАК ЕстьУслуги,
	|		0 КАК ЕстьАгентскиеУслуги
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Документы.Ссылка,
	|		ВТ_Документы.Дата,
	|		0,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0),
	|		0,
	|		0
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.ВозвратнаяТара КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Документы.Ссылка,
	|		ВТ_Документы.Дата,
	|		0,
	|		0,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0),
	|		0
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Документы.Ссылка,
	|		ВТ_Документы.Дата,
	|		0,
	|		0,
	|		0,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0)
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)) КАК ТаблицаДокументов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументов.Ссылка,
	|	ТаблицаДокументов.Дата
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДокументов.ЕстьТовары) + СУММА(ТаблицаДокументов.ЕстьВозвратнаяТара) + СУММА(ТаблицаДокументов.ЕстьУслуги) + СУММА(ТаблицаДокументов.ЕстьАгентскиеУслуги) = 1 И
	|	(СУММА(ТаблицаДокументов.ЕстьТовары) = 1
	|		ИЛИ СУММА(ТаблицаДокументов.ЕстьУслуги) = 1)";

	УстановитьПривилегированныйРежим(Истина);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	ВыборкаПоДокументам = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Попытка
			
			ЗаменитьВДокументеВидОперацииНаПростойПриПереходеС20(ВыборкаПоДокументам);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			КонецПериодаВыборки = ВыборкаПоДокументам.Дата; // Запоминаем дату, с которой будем начинать в следующий раз.
			
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать реализацию товаров и услуг: %1 по причине:
					|%2'"),
					ВыборкаПоДокументам.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, ВыборкаПоДокументам.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	// Запоминаем дату, с которой будем начинать в следующий раз.
	Параметры.Вставить("КонецПериодаВыборки", КонецПериодаВыборки);
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ЗаменитьВидыОперацийНаПростыеПриПереходеС20
				|не удалось обработать некоторые реализации товаров и услуг (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.РеализацияТоваровУслуг,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаменитьВидыОперацийНаПростыеПриПереходеС20
					|обработала очередную порцию реализаций товаров и услуг: %1 документов'"), ОбъектовОбработано));
	КонецЕсли;


КонецПроцедуры

Процедура ЗаменитьВДокументеВидОперацииНаПростойПриПереходеС20(ВыборкаПоДокументам)
	
	НачатьТранзакцию();
	Попытка
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.РеализацияТоваровУслуг");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаПоДокументам.Ссылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = ВыборкаПоДокументам.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если ДокументОбъект = Неопределено Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		НовыйВидОперации = Неопределено;
		
		Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПродажаКомиссия Тогда
			Если ДокументОбъект.Товары.Количество() > 0
				И ДокументОбъект.ВозвратнаяТара.Количество() = 0
				И ДокументОбъект.Услуги.Количество() = 0
				И ДокументОбъект.АгентскиеУслуги.Количество() = 0 Тогда

				НовыйВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Товары;
			
			ИначеЕсли ДокументОбъект.Товары.Количество() = 0
				И ДокументОбъект.ВозвратнаяТара.Количество() = 0
				И ДокументОбъект.Услуги.Количество() > 0
				И ДокументОбъект.АгентскиеУслуги.Количество() = 0 Тогда

				НовыйВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Услуги;
				
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НовыйВидОперации) Тогда
			// Документ уже обработан либо его данные перестали удовлетворять условию замены вида операции
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		ДокументОбъект.ВидОперации = НовыйВидОперации;
		
		// Запись обработанного объекта (без перепроведения).
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаполнитьКодТНВЭДОтложено(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", '20160701'); // учет НДС по ФЗ-150
	Запрос.УстановитьПараметр("Россия", Справочники.СтраныМира.Россия);
	Запрос.УстановитьПараметр("СтраныЕАЭС", УправлениеКонтактнойИнформацией.СтраныУчастникиЕАЭС().ВыгрузитьКолонку("Ссылка"));
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО РеализацияТоваровУслугТовары.Ссылка.Контрагент = Контрагенты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураТоваров
	|		ПО РеализацияТоваровУслугТовары.Номенклатура = НоменклатураТоваров.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка.Дата >= &Дата
	|	И Контрагенты.СтранаРегистрации <> &Россия
	|	И Контрагенты.СтранаРегистрации В(&СтраныЕАЭС)
	|	И Контрагенты.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|	И РеализацияТоваровУслугТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|	И РеализацияТоваровУслугТовары.КодТНВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|	И НоменклатураТоваров.КодТНВЭД <> ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|	И РеализацияТоваровУслугТовары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваровУслугТовары.Ссылка.Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если РезультатЗапроса.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Для каждого СтрокаРезультата Из РезультатЗапроса Цикл
		
		НачатьТранзакцию();
		Попытка
			
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.РеализацияТоваровУслуг");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаРезультата.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = СтрокаРезультата.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
			КонецЕсли;
			
			Для Каждого СтрокаДокумента Из ДокументОбъект.Товары Цикл
				Если СтрокаДокумента.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда 
					СтрокаДокумента.КодТНВЭД = СтрокаДокумента.Номенклатура.КодТНВЭД;
				КонецЕсли;
			КонецЦикла;
			
			// Запись обработанного объекта.
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			
			ОтменитьТранзакцию();
			// Если не удалось обработать какой-либо элемент справочника, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"),
					СтрокаРезультата.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, СтрокаРезультата.Ссылка, ТекстСообщения);
				
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ЗаполнитьКодТНВЭДОтложено
				|не удалось обработать некоторые документы ""Реализация (акты, накладные)"" (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.РеализацияТоваровУслуг,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаполнитьКодТНВЭДОтложено
					|обработала очередную порцию документов ""Реализация (акты, накладные)"": %1'"), ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСпособЗачетаАвансаДляОтгрузкиБезПереходаПраваСобственности() Экспорт
	
	// Для документов "Реализация товаров и услуг" с видом операции "Отгрузка без перехода права собственности"
	// по договорам с расчетами в рублях и валюте (кроме у.е.) необходимо установить способ зачета аванса
	// в значение "Не зачитывать" с целью сохранения прежних движений у ранее введенных документов 
	// после реализации возможности зачета предварительных оплат.
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РеализацияТоваровУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности)
	|	И РеализацияТоваровУслуг.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.НеЗачитывать)
	|	И НЕ ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект = Неопределено Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект.СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.НеЗачитывать;
			// Запись обработанного объекта.
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"),
					Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, Выборка.Ссылка, ТекстСообщения);
				
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияПрослеживаемостиПриПереходеС20() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.УдалитьРНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();;
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// Если есть документы с РНПТ, то установим константу "ВестиУчетПрослеживаемыхТоваров".
	Если НЕ Константы.ВестиУчетПрослеживаемыхТоваров.Получить() Тогда
		Константы.ВестиУчетПрослеживаемыхТоваров.Установить(Истина);
	КонецЕсли;
	
	ВыборкаПоДокументам = РезультатЗапроса.Выбрать();
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		ДокументОбъект = ВыборкаПоДокументам.Ссылка.ПолучитьОбъект();
		
		Если ДокументОбъект = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаДокумента Из ДокументОбъект.Товары Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаДокумента.ИдентификаторСтроки) Тогда
				СтрокаДокумента.ИдентификаторСтроки = Новый УникальныйИдентификатор;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаДокумента.УдалитьРНПТ) Тогда
				СтрокаДокумента.ПрослеживаемыйТовар = Истина;
				СтрокаСведенийПрослеживаемости       = ДокументОбъект.СведенияПрослеживаемости.Добавить();
				СтрокаСведенийПрослеживаемости.ИдентификаторСтроки        = СтрокаДокумента.ИдентификаторСтроки;
				СтрокаСведенийПрослеживаемости.РНПТ                       = СтрокаДокумента.УдалитьРНПТ;
				СтрокаСведенийПрослеживаемости.Количество                 = СтрокаДокумента.Количество;
				СтрокаСведенийПрослеживаемости.КоличествоПрослеживаемости = СтрокаДокумента.Количество;
				СтрокаДокумента.УдалитьРНПТ = Неопределено;
			КонецЕсли;
		КонецЦикла;
				
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"),
					ВыборкаПоДокументам.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, ВыборкаПоДокументам.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаменитьВидОперацииПередачаКомиссионеру(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПродажаКомиссия)
	|	И РеализацияТоваровУслуг.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваровУслуг.Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.РеализацияТоваровУслуг");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если ДокументОбъект = Неопределено ИЛИ
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров Тогда
			ОтменитьТранзакцию();
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров;
								
		Попытка
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре Документы.РеализацияТоваровУслуг.ЗаменитьВидОперацииПередачаКомиссионеру() не удалось обработать документ ""Реализация товаров и услуг"" по причине:
					|%1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, Выборка.Ссылка, ТекстСообщения);
				
		КонецПопытки; 
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов > 0 Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В процедуре Документы.РеализацияТоваровУслуг.ЗаменитьВидОперацииПередачаКомиссионеру() не удалось обработать документ ""Реализация товаров и услуг"": в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.РеализацияТоваровУслуг,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Документы.РеализацияТоваровУслуг.ЗаменитьВидОперацииПередачаКомиссионеру() обработала очередную порцию документов ""Реализация товаров и услуг"": %1 документов'"), ОбъектовОбработано));
	КонецЕсли;
			
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

Функция ТекстЗапросаПараметрыПечатиЧека(ДокументСсылка, НомераТаблиц, СообщениеОбОшибке) Экспорт
	
	ТекстЗапроса = ТекстЗапросаРеквизитыПечатиЧека(НомераТаблиц) 
		+ ТекстЗапросаДанныеДляПечатиЧека(НомераТаблиц)
		+ ТекстЗапросаШтрихкодыУпаковок(НомераТаблиц);
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ВозвратнаяТараКРеализацииПоСчету(СчетНаОплату, РеализацииПоСчету) Экспорт
	
	ВозвратнаяТара = Новый ТаблицаЗначений;
	ВозвратнаяТара.Колонки.Добавить("СчетНаОплатуПокупателю", Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПокупателю"));
	ВозвратнаяТара.Колонки.Добавить("Номенклатура",           Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ВозвратнаяТара.Колонки.Добавить("Количество",             ОбщегоНазначения.ОписаниеТипаЧисло(10, 3));
	ВозвратнаяТара.Колонки.Добавить("Цена",                   ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ВозвратнаяТара.Колонки.Добавить("Сумма",                  ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетНаОплату", СчетНаОплату);
	Запрос.УстановитьПараметр("РеализацииПоСчету", РеализацииПоСчету);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализованнаяТара.Номенклатура КАК Номенклатура,
	|	СУММА(РеализованнаяТара.Количество) КАК Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВозвратнаяТара КАК РеализованнаяТара
	|ГДЕ
	|	РеализованнаяТара.Ссылка В(&РеализацииПоСчету)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализованнаяТара.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратнаяТара.Ссылка КАК СчетНаОплатуПокупателю,
	|	ВозвратнаяТара.Номенклатура,
	|	ВозвратнаяТара.Количество,
	|	ВозвратнаяТара.Цена,
	|	ВозвратнаяТара.Сумма
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю.ВозвратнаяТара КАК ВозвратнаяТара
	|ГДЕ
	|	ВозвратнаяТара.Ссылка = &СчетНаОплату";
	
	Результат = Запрос.ВыполнитьПакет();
	
	РеализованнаяНоменклатура = Результат[0].Выгрузить();
	
	ПоляОтбора = "Номенклатура";
	РеализованнаяНоменклатура.Индексы.Добавить(ПоляОтбора);
	Отбор = Новый Структура(ПоляОтбора);
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, Выборка);
		НайденныеСтроки = РеализованнаяНоменклатура.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			НоваяСтрока = ВозвратнаяТара.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		Иначе
			
			РеализованоРанее = НайденныеСтроки[0].Количество;
			НайденныеСтроки[0].Количество = Макс(РеализованоРанее - Выборка.Количество, 0);
			
			Если РеализованоРанее < Выборка.Количество Тогда
				НоваяСтрока = ВозвратнаяТара.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.Количество = Выборка.Количество - РеализованоРанее;
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(НоваяСтрока);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВозвратнаяТара;
	
КонецФункции

Функция ТекстЗапросаОтгрузкаПоСчету(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьСчетНаОплатуПокупателю Или Реквизиты.ЭтоДоговорСКомиссионером Тогда
		ПараметрыПроведения.Вставить("ТаблицаСчетовНаОплату",               Неопределено);
		ПараметрыПроведения.Вставить("ОтгрузкаТоваровОказаниеУслугПоСчету", Неопределено);
		ПараметрыПроведения.Вставить("ОтгрузкаВозвратнойТарыПоСчету",       Неопределено);
		Возврат "";
	КонецЕсли;
	
	ЧастиЗапроса = Новый Массив;
	
	ЧастиЗапроса.Добавить(
	"ВЫБРАТЬ
	|	ТаблицаРеквизиты.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|ИЗ
	|	Реквизиты КАК ТаблицаРеквизиты
	|ГДЕ
	|	ТаблицаРеквизиты.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)");
	Если Реквизиты.ЕстьТовары Тогда
		ЧастиЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТаблицаТовары.СчетНаОплатуПокупателю
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)");
	КонецЕсли; 
	
	Если Реквизиты.ЕстьТара Тогда
		ЧастиЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТаблицаВозвратнаяТара.СчетНаОплатуПокупателю
		|ИЗ
		|	ТаблицаВозвратнаяТара КАК ТаблицаВозвратнаяТара
		|ГДЕ
		|	ТаблицаВозвратнаяТара.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)");
	КонецЕсли; 
	
	Если Реквизиты.ЕстьУслуги Тогда
		ЧастиЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТаблицаУслуги.СчетНаОплатуПокупателю
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги
		|ГДЕ
		|	ТаблицаУслуги.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)");
	КонецЕсли; 
	
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ЧастиЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТаблицаАгентскиеУслуги.СчетНаОплатуПокупателю
		|ИЗ
		|	ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги
		|ГДЕ
		|	ТаблицаАгентскиеУслуги.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)");
	КонецЕсли; 
	
	ТекстЗапроса = СтрСоединить(ЧастиЗапроса, Символы.ПС+"ОБЪЕДИНИТЬ"+Символы.ПС) + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();;
	НомераТаблиц.Вставить("ТаблицаСчетовНаОплату", НомераТаблиц.Количество());
	
	Если Реквизиты.ЕстьТовары Или Реквизиты.ЕстьУслуги Или Реквизиты.ЕстьАгентскиеУслуги Тогда
			НомераТаблиц.Вставить("ОтгрузкаТоваровОказаниеУслугПоСчету", НомераТаблиц.Количество());
			Шаблон = 
				"ВЫБРАТЬ
				|	ВложенныйЗапрос.ЭтоУслуга КАК ЭтоУслуга,
				|	ВЫБОР
				|		КОГДА Реквизиты.СчетНаОплатуПокупателю = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
				|			ТОГДА ВложенныйЗапрос.СчетНаОплатуПокупателю
				|		ИНАЧЕ Реквизиты.СчетНаОплатуПокупателю
				|	КОНЕЦ КАК СчетНаОплатуПокупателю,
				|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
				|	ВложенныйЗапрос.Содержание КАК Содержание,
				|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
				|ИЗ
				|	ВТ_ВложенныйЗапрос КАК ВложенныйЗапрос
				|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
				|		ПО (ИСТИНА)
				|
				|СГРУППИРОВАТЬ ПО
				|	ВложенныйЗапрос.ЭтоУслуга,
				|	ВложенныйЗапрос.Номенклатура,
				|	ВЫБОР
				|		КОГДА Реквизиты.СчетНаОплатуПокупателю = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
				|			ТОГДА ВложенныйЗапрос.СчетНаОплатуПокупателю
				|		ИНАЧЕ Реквизиты.СчетНаОплатуПокупателю
				|	КОНЕЦ,
				|	ВложенныйЗапрос.Содержание";
			ЧастиЗапроса = Новый Массив;
			
			Если Реквизиты.ЕстьТовары Тогда
				ЧастиЗапроса.Добавить(
				"ВЫБРАТЬ
				|		ЛОЖЬ КАК ЭтоУслуга,
				|		ТаблицаТовары.Номенклатура КАК Номенклатура,
				|		ТаблицаТовары.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
				|		"""" КАК Содержание,
				|		ТаблицаТовары.Количество КАК Количество
				|	ИЗ
				|		ТаблицаТовары КАК ТаблицаТовары");
			КонецЕсли;
			Если Реквизиты.ЕстьУслуги Тогда
				ЧастиЗапроса.Добавить(
				"ВЫБРАТЬ
				|	ИСТИНА КАК ЭтоУслуга,
				|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
				|	ТаблицаУслуги.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
				|	ТаблицаУслуги.Содержание КАК Содержание,
				|	ВЫБОР
				|		КОГДА ТаблицаУслуги.Количество = 0
				|			ТОГДА 1
				|		ИНАЧЕ ТаблицаУслуги.Количество
				|	КОНЕЦ КАК Количество
				|ИЗ
				|	ТаблицаУслуги КАК ТаблицаУслуги");
			КонецЕсли;
			Если Реквизиты.ЕстьАгентскиеУслуги Тогда
				ЧастиЗапроса.Добавить(
				"ВЫБРАТЬ
				|	ИСТИНА КАК ЭтоУслуга,
				|	ТаблицаАгентскиеУслуги.Номенклатура КАК Номенклатура,
				|	ТаблицаАгентскиеУслуги.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
				|	ТаблицаАгентскиеУслуги.Содержание КАК Содержание,
				|	ВЫБОР
				|		КОГДА ТаблицаАгентскиеУслуги.Количество = 0
				|			ТОГДА 1
				|		ИНАЧЕ ТаблицаАгентскиеУслуги.Количество
				|	КОНЕЦ КАК Количество
				|ИЗ
				|	ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги");
			КонецЕсли;
			ТекстЗапроса = ТекстЗапроса + СтрЗаменить(Шаблон, "ВТ_ВложенныйЗапрос", "("+СтрСоединить(ЧастиЗапроса,ОбщегоНазначения.ТекстОбъединитьВсе())+")") + ОбщегоНазначения.РазделительПакетаЗапросов();
	Иначе
		ПараметрыПроведения.Вставить("ОтгрузкаТоваровОказаниеУслугПоСчету", Неопределено);
	КонецЕсли;
	
	Если Реквизиты.ЕстьТара Тогда
		НомераТаблиц.Вставить("ОтгрузкаВозвратнойТарыПоСчету", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ТаблицаВозвратнаяТара.Номенклатура КАК Номенклатура,
			|	СУММА(ТаблицаВозвратнаяТара.Количество) КАК Количество
			|ИЗ
			|	ТаблицаВозвратнаяТара КАК ТаблицаВозвратнаяТара
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаВозвратнаяТара.Номенклатура" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	Иначе
		ПараметрыПроведения.Вставить("ОтгрузкаВозвратнойТарыПоСчету", Неопределено);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТаблицуСведенийСчетаНаОплату(МассивОбъектов, СоответствиеДокументовИСчетов = Неопределено, ДокументыБезСчетовНаОплату = Неопределено) Экспорт
	
	МассивСчетов = Новый Массив;
	ДокументыБезСчетовНаОплату = Новый Массив;
	
	// Соответствие - ключ - документ реализации, значение - массив счетов к нему
	СоответствиеДокументовИСчетов = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_РеализацияТоваров
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю,
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ТабличнаяЧасть
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
	|	И РеализацияТоваровУслугТовары.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугУслуги.СчетНаОплатуПокупателю,
	|	РеализацияТоваровУслугУслуги.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|ГДЕ
	|	РеализацияТоваровУслугУслуги.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
	|	И РеализацияТоваровУслугУслуги.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугАгентскиеУслуги.СчетНаОплатуПокупателю,
	|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслугАгентскиеУслуги
	|ГДЕ
	|	РеализацияТоваровУслугАгентскиеУслуги.СчетНаОплатуПокупателю <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка)
	|	И РеализацияТоваровУслугАгентскиеУслуги.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_РеализацияТоваров.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ВТ_ТабличнаяЧасть.СчетНаОплатуПокупателю, ВТ_РеализацияТоваров.СчетНаОплатуПокупателю) КАК СчетНаОплатуПокупателю
	|ИЗ
	|	ВТ_РеализацияТоваров КАК ВТ_РеализацияТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТабличнаяЧасть КАК ВТ_ТабличнаяЧасть
	|		ПО ВТ_РеализацияТоваров.Ссылка = ВТ_ТабличнаяЧасть.Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Пока РезультатЗапроса.Следующий() Цикл
		Если ЗначениеЗаполнено(РезультатЗапроса.СчетНаОплатуПокупателю) Тогда
			МассивСчетов.Добавить(РезультатЗапроса.СчетНаОплатуПокупателю);
			Если СоответствиеДокументовИСчетов[РезультатЗапроса.Ссылка] = Неопределено Тогда
				СоответствиеДокументовИСчетов.Вставить(РезультатЗапроса.Ссылка, Новый Массив);
			КонецЕсли; 
			СоответствиеДокументовИСчетов[РезультатЗапроса.Ссылка].Добавить(РезультатЗапроса.СчетНаОплатуПокупателю);
		Иначе
			ДокументыБезСчетовНаОплату.Добавить(РезультатЗапроса.Ссылка);
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Документы.СчетНаОплатуПокупателю.ПолучитьТаблицуСведенийСчетаНаОплату(МассивСчетов);
КонецФункции

// Вид операции "Перенос задолженности".
Функция ТекстЗапросаРеквизитыПереносЗадолженностиДебиторскаяЗадолженность(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	Если НЕ Реквизиты.ОплатаПриПолучении 
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности Тогда
		
		ПараметрыПроведения.Вставить("РеквизитыПереносЗадолженностиДебиторскаяЗадолженность", Неопределено);
		
		Возврат "";
	КонецЕсли; 

	НомераТаблиц.Вставить("РеквизитыПереносЗадолженностиДебиторскаяЗадолженность", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Ссылка КАК ДокументРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкаДолга.ПереносЗадолженности) КАК ВидОперации,
	|	Реквизиты.СчетРасчетовСПеревозчиком КАК СчетРасчетов,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Перевозчик КАК Контрагент,
	|	Реквизиты.ПеревозчикДоговор КАК ДоговорКонтрагент,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	&ПрименяетсяУСН КАК УчитыватьЗадолженностьУСН,
	|	&УчитыватьЗадолженностьУСНПатент КАК УчитыватьЗадолженностьУСНПатент,
	|	""Поступление"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	ИСТИНА КАК РасчетыПоРеализации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ОплатаЧерезКомиссионера
	|			ТОГДА &ОплатаПринятаПлатежнымАгентом
	|		ИНАЧЕ &ОплатаПринятаКурьером
	|	КОНЕЦ КАК СодержаниеПроводки
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ПодготовитьТаблицуПереносаЗадолженности(ТаблицаВзаиморасчетов, ТаблицаРеквизитыПереносЗадолженности, Отказ) Экспорт
	Если ТаблицаРеквизитыПереносЗадолженности = Неопределено Тогда
	
		Возврат Неопределено;
	КонецЕсли; 
	
	РеквизитыПереносЗадолженности = ТаблицаРеквизитыПереносЗадолженности[0];

	ТаблицаПереносаЗадолженности = УчетВзаиморасчетов.ПустаяТаблицаВзаиморасчетовПереносЗадолженности(
		РеквизитыПереносЗадолженности.УчитыватьЗадолженностьУСН);
	
	ТаблицаВзаиморасчетовПереносЗадолженности = ТаблицаВзаиморасчетов.Скопировать(
		Новый Структура("ДокументРасчетов", РеквизитыПереносЗадолженности.Регистратор));
		
	Для каждого СтрокаВзаиморасчетов Из ТаблицаВзаиморасчетовПереносЗадолженности Цикл
		
		СтрокаТаблицы = ТаблицаПереносаЗадолженности.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаВзаиморасчетов);
		
		СтрокаТаблицы.ДокументРасчетов = РеквизитыПереносЗадолженности.ДокументРасчетов;
		
		СтрокаТаблицы.КорСчет = РеквизитыПереносЗадолженности.СчетРасчетов;
		СтрокаТаблицы.КорПодразделение = СтрокаТаблицы.Подразделение;
		
		СвойстваКорСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.КорСчет);
		Для Сч = 1 По 3 Цикл
			Если СвойстваКорСчета["ВидСубконто" + Сч] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты Тогда
				СтрокаТаблицы["КорСубконто" + Сч] = РеквизитыПереносЗадолженности.Контрагент;
			ИначеЕсли СвойстваКорСчета["ВидСубконто" + Сч] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры Тогда
				СтрокаТаблицы["КорСубконто" + Сч] = РеквизитыПереносЗадолженности.ДоговорКонтрагент;
			ИначеЕсли СвойстваКорСчета["ВидСубконто" + Сч] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами Тогда
				СтрокаТаблицы["КорСубконто" + Сч] = РеквизитыПереносЗадолженности.ДокументРасчетов;
			КонецЕсли; 
		КонецЦикла; 
		
		СтрокаТаблицы.КорВалютаВзаиморасчетов = СтрокаТаблицы.ВалютаВзаиморасчетов;
		СтрокаТаблицы.КорСуммаВзаиморасчетов  = СтрокаТаблицы.СуммаВзаиморасчетов;
		
		СтрокаТаблицы.СуммаБУ              = СтрокаВзаиморасчетов.СуммаРуб;
		СтрокаТаблицы.СуммаНУ              = СтрокаВзаиморасчетов.СуммаРуб;
		
		СтрокаТаблицы.СуммаРуб             = СтрокаВзаиморасчетов.СуммаРуб;
		
		// Пересчитаем суммы из валюты взаиморасчетов в рубли
		КурсВзаиморасчетов = СтрокаВзаиморасчетов.СуммаРуб / СтрокаВзаиморасчетов.СуммаВзаиморасчетов;
		
		СтрокаТаблицы.СуммаБУ_ЕНВД         = Окр(СтрокаВзаиморасчетов.СуммаВзаиморасчетовЕНВД * КурсВзаиморасчетов,         2);
		СтрокаТаблицы.СуммаБУ_Комитента    = Окр(СтрокаВзаиморасчетов.СуммаВзаиморасчетовКомитента * КурсВзаиморасчетов,    2);
		СтрокаТаблицы.СуммаБУ_Патент       = Окр(СтрокаВзаиморасчетов.СуммаВзаиморасчетовПатент * КурсВзаиморасчетов,       2);
		СтрокаТаблицы.СуммаБУ_ТорговыйСбор = Окр(СтрокаВзаиморасчетов.СуммаВзаиморасчетовТорговыйСбор * КурсВзаиморасчетов, 2);
		
		СтрокаТаблицы.Содержание = РеквизитыПереносЗадолженности.СодержаниеПроводки;
		
	КонецЦикла;
	
	Возврат ТаблицаПереносаЗадолженности;
КонецФункции

Функция ТекстЗапросаРеализацииПоСчетам() Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ 
	|	Шапка.СчетНаОплатуПокупателю КАК СчетНаОплату,
	|	Шапка.Ссылка КАК Реализация
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Шапка
	|ГДЕ
	|	Шапка.СчетНаОплатуПокупателю В (&СчетаНаОплату)
	|	И Шапка.Проведен
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтрокиТЧ.СчетНаОплатуПокупателю,
	|	СтрокиТЧ.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК СтрокиТЧ
	|ГДЕ
	|	СтрокиТЧ.СчетНаОплатуПокупателю В (&СчетаНаОплату)
	|	И СтрокиТЧ.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтрокиТЧ.СчетНаОплатуПокупателю,
	|	СтрокиТЧ.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК СтрокиТЧ
	|ГДЕ
	|	СтрокиТЧ.СчетНаОплатуПокупателю В (&СчетаНаОплату)
	|	И СтрокиТЧ.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтрокиТЧ.СчетНаОплатуПокупателю,
	|	СтрокиТЧ.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК СтрокиТЧ
	|ГДЕ
	|	СтрокиТЧ.СчетНаОплатуПокупателю В (&СчетаНаОплату)
	|	И СтрокиТЧ.Ссылка.Проведен";
	
	Возврат ТекстЗапроса;
КонецФункции 

Функция ТекстЗапросаРеализованнаяНоменклатураПоСчету() Экспорт
	
	ТекстЗапросаРеализованнаяНоменклатураПоСчету = 
	"ВЫБРАТЬ
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	РеализованныеТовары.Номенклатура КАК Номенклатура,
	|	"""" КАК Содержание,
	|	РеализованныеТовары.Цена КАК Цена,
	|	РеализованныеТовары.Количество КАК Количество
	|ИЗ
	|	ВТ_РеализацииПоСчету КАК ВТ_РеализацииПоСчету
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализованныеТовары
	|		ПО ВТ_РеализацииПоСчету.РеализацияТоваров = РеализованныеТовары.Ссылка
	|ГДЕ
	|	НЕ РеализованныеТовары.Ссылка ЕСТЬ NULL
	|	И (РеализованныеТовары.СчетНаОплатуПокупателю = &СчетНаОплату
	|			ИЛИ РеализованныеТовары.СчетНаОплатуПокупателю = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	РеализованныеУслуги.Номенклатура,
	|	РеализованныеУслуги.Содержание,
	|	РеализованныеУслуги.Цена,
	|	ВЫБОР
	|		КОГДА РеализованныеУслуги.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ РеализованныеУслуги.Количество
	|	КОНЕЦ
	|ИЗ
	|	ВТ_РеализацииПоСчету КАК ВТ_РеализацииПоСчету
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК РеализованныеУслуги
	|		ПО ВТ_РеализацииПоСчету.РеализацияТоваров = РеализованныеУслуги.Ссылка
	|ГДЕ
	|	НЕ РеализованныеУслуги.Ссылка ЕСТЬ NULL
	|	И (РеализованныеУслуги.СчетНаОплатуПокупателю = &СчетНаОплату
	|			ИЛИ РеализованныеУслуги.СчетНаОплатуПокупателю = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	РеализованныеАгентскиеУслуги.Номенклатура,
	|	РеализованныеАгентскиеУслуги.Содержание,
	|	РеализованныеАгентскиеУслуги.Цена,
	|	ВЫБОР
	|		КОГДА РеализованныеАгентскиеУслуги.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ РеализованныеАгентскиеУслуги.Количество
	|	КОНЕЦ
	|ИЗ
	|	ВТ_РеализацииПоСчету КАК ВТ_РеализацииПоСчету
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализованныеАгентскиеУслуги
	|		ПО ВТ_РеализацииПоСчету.РеализацияТоваров = РеализованныеАгентскиеУслуги.Ссылка
	|ГДЕ
	|	НЕ РеализованныеАгентскиеУслуги.Ссылка ЕСТЬ NULL
	|	И (РеализованныеАгентскиеУслуги.СчетНаОплатуПокупателю = &СчетНаОплату
	|			ИЛИ РеализованныеАгентскиеУслуги.СчетНаОплатуПокупателю = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПокупателю.ПустаяСсылка))";
	
	Возврат ТекстЗапросаРеализованнаяНоменклатураПоСчету;
КонецФункции 

Функция ТекстЗапросаСчетаПоРеализации() Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Шапка.СчетНаОплатуПокупателю КАК СчетНаОплату
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Шапка
	|ГДЕ
	|	Шапка.Проведен
	|	И Шапка.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтрокиТЧ.СчетНаОплатуПокупателю
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК СтрокиТЧ
	|ГДЕ
	|	СтрокиТЧ.Ссылка.Проведен
	|	И СтрокиТЧ.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтрокиТЧ.СчетНаОплатуПокупателю
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК СтрокиТЧ
	|ГДЕ
	|	СтрокиТЧ.Ссылка.Проведен
	|	И СтрокиТЧ.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СтрокиТЧ.СчетНаОплатуПокупателю
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК СтрокиТЧ
	|ГДЕ
	|	СтрокиТЧ.Ссылка.Проведен
	|	И СтрокиТЧ.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПечатьЧека

Функция ТекстЗапросаРеквизитыПечатиЧека(ИменаТаблиц)
	
	ИменаТаблиц.Добавить("ВТ_ДоговорПлатежногоАгента");
	ИменаТаблиц.Добавить("ВТ_ДоговорПлатежногоАгентаСводно");
	ИменаТаблиц.Добавить("ВТ_Реквизиты");
	ИменаТаблиц.Добавить("ВТ_РасшифровкаПлатежа");
	ИменаТаблиц.Добавить("РеквизитыПечатиЧека");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.ДоговорКонтрагента КАК ДоговорПлатежногоАгента,
	|	РеализацияТоваровУслуг.Контрагент КАК ПлатежныйАгент,
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ДоговораПлатежногоАгента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ДокументСсылка
	|	И РеализацияТоваровУслуг.ДоговорКонтрагента.ПлатежныйАгент
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслуг.ДоговорКонтрагента,
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДоговораПлатежногоАгента.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВТ_ДоговораПлатежногоАгента.ДоговорПлатежногоАгента) КАК ДоговорПлатежногоАгента,
	|	ВТ_ДоговораПлатежногоАгента.ПлатежныйАгент КАК ПлатежныйАгент,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ_ДоговораПлатежногоАгента.ДоговорПлатежногоАгента) КАК КоличествоДоговоров
	|ПОМЕСТИТЬ ВТ_ДоговораПлатежногоАгентаСводно
	|ИЗ
	|	ВТ_ДоговораПлатежногоАгента КАК ВТ_ДоговораПлатежногоАгента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДоговораПлатежногоАгента.Ссылка,
	|	ВТ_ДоговораПлатежногоАгента.ПлатежныйАгент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.СчетНаОплатуПокупателю КАК ДокументОснование,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.ВидОперации КАК ВидОперации,
	|	РеализацияТоваровУслуг.Патент КАК Патент,
	|	РеализацияТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	ЕСТЬNULL(ВТ_ДоговораПлатежногоАгента.ДоговорПлатежногоАгента, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК ДоговорПлатежногоАгента,
	|	ЕСТЬNULL(ВТ_ДоговораПлатежногоАгента.ПлатежныйАгент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК ПлатежныйАгент,
	|	ЕСТЬNULL(ВТ_ДоговораПлатежногоАгента.КоличествоДоговоров, 0) > 1 КАК НесколькоДоговоровПлатежногоАгента,
	|	РеализацияТоваровУслуг.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_Реквизиты
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДоговораПлатежногоАгентаСводно КАК ВТ_ДоговораПлатежногоАгента
	|		ПО РеализацияТоваровУслуг.Ссылка = ВТ_ДоговораПлатежногоАгента.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслугТовары.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	РеализацияТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(РеализацияТоваровУслугТовары.СуммаНДС) КАК СуммаНДС,
	|	РеализацияТоваровУслугТовары.Ссылка КАК Сделка,
	|	0 КАК СуммаПлатежа,
	|	0 КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ ВТ_РасшифровкаПлатежа
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Ссылка,
	|	РеализацияТоваровУслугТовары.Ссылка.ДоговорКонтрагента,
	|	РеализацияТоваровУслугТовары.СтавкаНДС,
	|	РеализацияТоваровУслугТовары.Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугУслуги.Ссылка,
	|	РеализацияТоваровУслугУслуги.Ссылка.ДоговорКонтрагента,
	|	РеализацияТоваровУслугУслуги.СтавкаНДС,
	|	РеализацияТоваровУслугУслуги.СуммаНДС,
	|	РеализацияТоваровУслугУслуги.Ссылка,
	|	0,
	|	0
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|ГДЕ
	|	РеализацияТоваровУслугУслуги.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка,
	|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка.ДоговорКонтрагента,
	|	РеализацияТоваровУслугАгентскиеУслуги.СтавкаНДС,
	|	РеализацияТоваровУслугАгентскиеУслуги.СуммаНДС,
	|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка,
	|	0,
	|	0
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслугАгентскиеУслуги
	|ГДЕ
	|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Организация КАК Организация,
	|	0 КАК СуммаДокумента,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Номер КАК Номер,
	|	НЕОПРЕДЕЛЕНО КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОплатыККТ.Наличные) КАК ТипОплаты,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств) КАК ТипРасчета,
	|	Реквизиты.Патент КАК Патент,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	Реквизиты.НесколькоДоговоровПлатежногоАгента КАК НесколькоДоговоровПлатежногоАгента,
	|	Реквизиты.ПлатежныйАгент КАК ПлатежныйАгент
	|ИЗ
	|	ВТ_Реквизиты КАК Реквизиты";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

// Для корректной работы в запросе уже должна быть таблица ВТ_РасшифровкаПлатежа которая содержит данные по всем оплачиваемым документам
// Исходя из данных этой таблицы подбираем документы и номенклатуру этих документов.
Функция ТекстЗапросаДанныеДляПечатиЧека(ИменаТаблиц) Экспорт
	
	// Таблица сделок из реквизитов платежа. В таблице могут быть как счета так и накладные (если печатаем из денежных документов)
	ИменаТаблиц.Добавить("ВТ_Сделки");
	// Сводная таблица товаров из ТЧ Товары, Услуги и АгентскиеУслуги. Используется для формирования таблицы оплачиваемой номенклатуры
	ИменаТаблиц.Добавить("ВТ_Товары");
	
	// Таблицы содержат информацию по номенклатуре из всех документов из таблицы ВТ_Сделки
	ИменаТаблиц.Добавить("ВТ_ОплачиваемаяНоменклатураРеализация");
	
	// Реквизиты документов по которым принимается оплата. Это может быть как текущий документ (если печать идет из накладной), так и все документы нужного вида из ВТ_РеквизитыПлатежа 
	// Используем как самостоятельно, так и для определения суммовых показателей таблицы ОплачиваемаяНоменклатура (валюта, флаг включения НДС в стоимость).
	ИменаТаблиц.Добавить("ВТ_ОплачиваемыеДокументыРеализация"); 
	
	ИменаТаблиц.Добавить("ВТ_ДокументыРеализации");
	ИменаТаблиц.Добавить("ВТ_Субконто");
	
	ИменаТаблиц.Добавить("ВТ_ЗачетАвансов");
	
	ИменаТаблиц.Добавить("ОплачиваемаяНоменклатура");
	ИменаТаблиц.Добавить("ОплачиваемыеДокументы");
	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_РасшифровкаПлатежа.Сделка КАК Сделка,
	|	ВТ_РасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ВТ_РасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ВТ_РасшифровкаПлатежа.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВТ_РасшифровкаПлатежа.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов
	|ПОМЕСТИТЬ ВТ_Сделки
	|ИЗ
	|	ВТ_РасшифровкаПлатежа КАК ВТ_РасшифровкаПлатежа
	|ГДЕ
	|	ВТ_РасшифровкаПлатежа.Сделка ССЫЛКА Документ.РеализацияТоваровУслуг
	|	И ВТ_РасшифровкаПлатежа.Сделка <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяССылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РасшифровкаПлатежа.Сделка,
	|	ВТ_РасшифровкаПлатежа.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сделка,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Ссылка КАК Документ,
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	"""" КАК Содержание,
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	РеализацияТоваровУслугТовары.Количество КАК Количество,
	|	РеализацияТоваровУслугТовары.Цена КАК Цена,
	|	РеализацияТоваровУслугТовары.Сумма КАК Сумма,
	|	РеализацияТоваровУслугТовары.СтавкаНДС КАК СтавкаНДС,
	|	РеализацияТоваровУслугТовары.СуммаНДС КАК СуммаНДС,
	|	РеализацияТоваровУслугТовары.НомерСтроки КАК НомерСтроки,
	|	0 КАК НомерТабличнойЧасти,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорПлатежногоАгента,
	|	НЕОПРЕДЕЛЕНО КАК ПлатежныйАгент,
	|	ЕСТЬNULL(РеализацияТоваровУслугТовары.СтранаПроисхождения.Код, НЕОПРЕДЕЛЕНО) КАК КодСтраныПроисхожденияТовара,
	|	ЕСТЬNULL(РеализацияТоваровУслугТовары.НомерГТД.РегистрационныйНомер, НЕОПРЕДЕЛЕНО) КАК НомерТаможеннойДекларации
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	ВТ_Сделки КАК ВТ_Сделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО ВТ_Сделки.Сделка = РеализацияТоваровУслугТовары.Ссылка
	|			И (ВТ_Сделки.СтавкаНДС = РеализацияТоваровУслугТовары.СтавкаНДС
	|				ИЛИ ВТ_Сделки.СтавкаНДС = НЕОПРЕДЕЛЕНО)
	|ГДЕ
	|	НЕ РеализацияТоваровУслугТовары.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугУслуги.Ссылка,
	|	РеализацияТоваровУслугУслуги.Номенклатура,
	|	РеализацияТоваровУслугУслуги.Содержание,
	|	ИСТИНА,
	|	РеализацияТоваровУслугУслуги.Количество,
	|	РеализацияТоваровУслугУслуги.Цена,
	|	РеализацияТоваровУслугУслуги.Сумма,
	|	РеализацияТоваровУслугУслуги.СтавкаНДС,
	|	РеализацияТоваровУслугУслуги.СуммаНДС,
	|	РеализацияТоваровУслугУслуги.НомерСтроки,
	|	1,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ВТ_Сделки КАК ВТ_Сделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|		ПО ВТ_Сделки.Сделка = РеализацияТоваровУслугУслуги.Ссылка
	|			И (ВТ_Сделки.СтавкаНДС = РеализацияТоваровУслугУслуги.СтавкаНДС
	|				ИЛИ ВТ_Сделки.СтавкаНДС = НЕОПРЕДЕЛЕНО)
	|ГДЕ
	|	НЕ РеализацияТоваровУслугУслуги.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугАгентскиеУслуги.Ссылка,
	|	РеализацияТоваровУслугАгентскиеУслуги.Номенклатура,
	|	РеализацияТоваровУслугАгентскиеУслуги.Содержание,
	|	ИСТИНА,
	|	РеализацияТоваровУслугАгентскиеУслуги.Количество,
	|	РеализацияТоваровУслугАгентскиеУслуги.Цена,
	|	РеализацияТоваровУслугАгентскиеУслуги.Сумма,
	|	РеализацияТоваровУслугАгентскиеУслуги.СтавкаНДС,
	|	РеализацияТоваровУслугАгентскиеУслуги.СуммаНДС,
	|	РеализацияТоваровУслугАгентскиеУслуги.НомерСтроки,
	|	2,
	|	РеализацияТоваровУслугАгентскиеУслуги.ДоговорКонтрагента,
	|	РеализацияТоваровУслугАгентскиеУслуги.Контрагент,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ВТ_Сделки КАК ВТ_Сделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализацияТоваровУслугАгентскиеУслуги
	|		ПО ВТ_Сделки.Сделка = РеализацияТоваровУслугАгентскиеУслуги.Ссылка
	|			И (ВТ_Сделки.СтавкаНДС = РеализацияТоваровУслугАгентскиеУслуги.СтавкаНДС
	|				ИЛИ ВТ_Сделки.СтавкаНДС = НЕОПРЕДЕЛЕНО)
	|ГДЕ
	|	НЕ РеализацияТоваровУслугАгентскиеУслуги.Ссылка ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Сделки.Сделка КАК Документ,
	|	ДокументРеализация.Дата КАК ДатаДокумента,
	|	ДокументРеализация.Организация КАК Организация,
	|	ДокументРеализация.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДокументРеализация.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ДокументРеализация.ВалютаДокумента КАК ВалютаДокумента,
	|	РеализацияТовары.НомерСтроки КАК НомерСтроки,
	|	РеализацияТовары.НомерТабличнойЧасти КАК НомерТабличнойЧасти,
	|	РеализацияТовары.Номенклатура КАК Номенклатура,
	|	&ЧастьЗапросаДляВыбораСодержанияУслуг КАК Наименование,
	|	РеализацияТовары.ЭтоУслуга КАК ЭтоУслуга,
	|	РеализацияТовары.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	РеализацияТовары.ПлатежныйАгент КАК ПлатежныйАгент,
	|	СУММА(ВЫБОР
	|			КОГДА РеализацияТовары.Количество = 0
	|					И ЕСТЬNULL(РеализацияТовары.ЭтоУслуга, ИСТИНА)
	|				ТОГДА 1
	|			ИНАЧЕ РеализацияТовары.Количество
	|		КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА РеализацияТовары.Количество = 0
	|					И ЕСТЬNULL(РеализацияТовары.ЭтоУслуга, ИСТИНА)
	|				ТОГДА 1
	|			ИНАЧЕ РеализацияТовары.Количество
	|		КОНЕЦ) КАК КоличествоОтгружено,
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА ДокументРеализация.СуммаВключаетНДС
	|					ТОГДА РеализацияТовары.Цена
	|				КОГДА РеализацияТовары.Количество = 0
	|					ТОГДА РеализацияТовары.Сумма + РеализацияТовары.СуммаНДС
	|				ИНАЧЕ (РеализацияТовары.Сумма + РеализацияТовары.СуммаНДС) / РеализацияТовары.Количество
	|			КОНЕЦ КАК ЧИСЛО(15, 2))) КАК Цена,
	|	РеализацияТовары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(РеализацияТовары.СуммаНДС) КАК СуммаНДС,
	|	0 КАК СуммаСкидок,
	|	СУММА(ВЫБОР
	|			КОГДА ДокументРеализация.СуммаВключаетНДС
	|				ТОГДА РеализацияТовары.Сумма
	|			ИНАЧЕ РеализацияТовары.Сумма + РеализацияТовары.СуммаНДС
	|		КОНЕЦ) КАК Сумма,
	|	РеализацияТовары.КодСтраныПроисхожденияТовара КАК КодСтраныПроисхожденияТовара,
	|	РеализацияТовары.НомерТаможеннойДекларации КАК НомерТаможеннойДекларации
	|ПОМЕСТИТЬ ВТ_ОплачиваемаяНоменклатураРеализация
	|ИЗ
	|	ВТ_Сделки КАК ВТ_Сделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Товары КАК РеализацияТовары
	|		ПО ВТ_Сделки.Сделка = РеализацияТовары.Документ
	|			И (ВТ_Сделки.СтавкаНДС = РеализацияТовары.СтавкаНДС
	|				ИЛИ ВТ_Сделки.СтавкаНДС = НЕОПРЕДЕЛЕНО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДокументРеализация
	|		ПО ВТ_Сделки.Сделка = ДокументРеализация.Ссылка
	|ГДЕ
	|	НЕ РеализацияТовары.Документ ЕСТЬ NULL
	|	И (РеализацияТовары.Количество <> 0
	|			ИЛИ РеализацияТовары.ЭтоУслуга)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Сделки.Сделка,
	|	ДокументРеализация.Дата,
	|	ДокументРеализация.Организация,
	|	ДокументРеализация.ДоговорКонтрагента,
	|	ДокументРеализация.СуммаВключаетНДС,
	|	ДокументРеализация.ВалютаДокумента,
	|	РеализацияТовары.НомерСтроки,
	|	РеализацияТовары.НомерТабличнойЧасти,
	|	РеализацияТовары.Номенклатура,
	|	РеализацияТовары.ЭтоУслуга,
	|	РеализацияТовары.ДоговорПлатежногоАгента,
	|	РеализацияТовары.СтавкаНДС,
	|	РеализацияТовары.ПлатежныйАгент,
	|	РеализацияТовары.КодСтраныПроисхожденияТовара,
	|	&ЧастьЗапросаДляВыбораСодержанияУслуг,
	|	РеализацияТовары.НомерТаможеннойДекларации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплачиваемаяНоменклатура.Документ КАК Документ,
	|	ОплачиваемаяНоменклатура.Организация КАК Организация,
	|	ОплачиваемаяНоменклатура.ДатаДокумента КАК Дата,
	|	ОплачиваемаяНоменклатура.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	0 КАК СуммаСкидкиПоДокументу,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.Ссылка, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ОплачиваемаяНоменклатура.ВалютаДокумента КАК ВалютаДокумента,
	|	ОплачиваемаяНоменклатура.ВалютаДокумента КАК ВалютаПлатежа,
	|	ВТ_Сделки.СуммаПлатежа КАК СуммаОплаты,
	|	ВТ_Сделки.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ОплачиваемаяНоменклатура.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ОплачиваемаяНоменклатура.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ОплачиваемаяНоменклатура.Сумма) КАК Сумма,
	|	ЛОЖЬ КАК ЭтоВозврат
	|ПОМЕСТИТЬ ВТ_ОплачиваемыеДокументыРеализация
	|ИЗ
	|	ВТ_Сделки КАК ВТ_Сделки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОплачиваемаяНоменклатураРеализация КАК ОплачиваемаяНоменклатура
	|		ПО ВТ_Сделки.Сделка = ОплачиваемаяНоменклатура.Документ
	|			И (ВТ_Сделки.СтавкаНДС = ОплачиваемаяНоменклатура.СтавкаНДС
	|				ИЛИ ВТ_Сделки.СтавкаНДС = НЕОПРЕДЕЛЕНО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО (ОплачиваемаяНоменклатура.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплачиваемаяНоменклатура.Документ,
	|	ОплачиваемаяНоменклатура.Организация,
	|	ОплачиваемаяНоменклатура.ДатаДокумента,
	|	ОплачиваемаяНоменклатура.СуммаВключаетНДС,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.Ссылка, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)),
	|	ВТ_Сделки.СуммаПлатежа,
	|	ВТ_Сделки.СуммаВзаиморасчетов,
	|	ОплачиваемаяНоменклатура.СтавкаНДС,
	|	ОплачиваемаяНоменклатура.ВалютаДокумента,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ОплачиваемаяНоменклатура.ВалютаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Сделки.Сделка КАК Сделка
	|ПОМЕСТИТЬ ВТ_ДокументыРеализации
	|ИЗ
	|	ВТ_Сделки КАК ВТ_Сделки
	|ГДЕ
	|	ВТ_Сделки.Сделка ССЫЛКА Документ.РеализацияТоваровУслуг
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Сделка,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	ВЫБОР 
	|		КОГДА РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности) 
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСРасчетыПоОтгрузкеРуб) 
	|		ИНАЧЕ РеализацияТоваровУслуг.СчетУчетаРасчетовСКонтрагентом 
	|	КОНЕЦ КАК СчетРасчетов,
	|	ХозрасчетныйСубконто.Регистратор КАК Регистратор,
	|	ХозрасчетныйСубконто.НомерСтроки КАК НомерСтроки,
	|	ХозрасчетныйСубконто.ВидДвижения КАК ВидДвижения,
	|	ХозрасчетныйСубконто.Период КАК Период
	|ПОМЕСТИТЬ ВТ_Субконто
	|ИЗ
	|	ВТ_ДокументыРеализации КАК ВТ_ДокументРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ_ДокументРеализации.Сделка = РеализацияТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО (РеализацияТоваровУслуг.Ссылка = ХозрасчетныйСубконто.Значение)
	|			И (ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами))
	|			И (ХозрасчетныйСубконто.Период >= РеализацияТоваровУслуг.Дата)
	|			И (ХозрасчетныйСубконто.Период <= &ДатаРасчета)
	|ГДЕ
	|	НЕ ХозрасчетныйСубконто.Регистратор ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Субконто.Сделка КАК ОплачиваемыйДокумент,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_Субконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет)
	|					И Хозрасчетный.СчетДт = ВТ_Субконто.СчетРасчетов
	|				ТОГДА ЕСТЬNULL(Хозрасчетный.Сумма, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаДокумента,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_Субконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит)
	|					И Хозрасчетный.СчетКт = ВТ_Субконто.СчетРасчетов
	|				ТОГДА ЕСТЬNULL(Хозрасчетный.Сумма, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаОплаты
	|ПОМЕСТИТЬ ВТ_ЗачетАвансов
	|ИЗ
	|	ВТ_Субконто КАК ВТ_Субконто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО ВТ_Субконто.Регистратор = Хозрасчетный.Регистратор
	|			И ВТ_Субконто.НомерСтроки = Хозрасчетный.НомерСтроки
	|			И ВТ_Субконто.Период = Хозрасчетный.Период
	|			И ВТ_Субконто.Организация = Хозрасчетный.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Субконто.Сделка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплачиваемаяНоменклатура.Документ КАК Документ,
	|	ОплачиваемаяНоменклатура.ДатаДокумента КАК ДатаДокумента,
	|	ОплачиваемаяНоменклатура.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СправочникНоменклатура.КодНоменклатурнойКлассификацииККТ.Код, НЕОПРЕДЕЛЕНО) КАК КодВидаНоменклатурнойКлассификации,
	|	ЕСТЬNULL(СправочникНоменклатура.КодВидаТРУ, НЕОПРЕДЕЛЕНО) КАК КодВидаТРУ,
	|	ЕСТЬNULL(СправочникНоменклатура.Артикул, НЕОПРЕДЕЛЕНО) КАК Артикул,
	|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Код, НЕОПРЕДЕЛЕНО) КАК КодЕдиницыИзмерения,
	|	НЕОПРЕДЕЛЕНО КАК Штрихкод,
	|	&УсловиеМаркируемаяПродукция КАК МаркируемаяПродукция, 
	|	ОплачиваемаяНоменклатура.Наименование КАК Наименование,
	|	ОплачиваемаяНоменклатура.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	ОплачиваемаяНоменклатура.ПлатежныйАгент КАК ПлатежныйАгент,
	|	ОплачиваемаяНоменклатура.ЭтоУслуга КАК ЭтоУслуга,
	|	СУММА(ОплачиваемаяНоменклатура.Количество) КАК Количество,
	|	СУММА(ОплачиваемаяНоменклатура.КоличествоОтгружено) КАК КоличествоОтгружено,
	|	ОплачиваемаяНоменклатура.Цена КАК Цена,
	|	ОплачиваемаяНоменклатура.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ОплачиваемаяНоменклатура.СуммаСкидок) КАК СуммаСкидок,
	|	СУММА(ОплачиваемаяНоменклатура.Сумма) КАК Сумма,
	|	МИНИМУМ(ОплачиваемаяНоменклатура.НомерСтроки) КАК НомерСтроки,
	|	МИНИМУМ(ОплачиваемаяНоменклатура.НомерТабличнойЧасти) КАК НомерТабличнойЧасти,
	|	СУММА(ОплачиваемаяНоменклатура.СуммаНДС) КАК СуммаНДС,
	|	ОплачиваемаяНоменклатура.КодСтраныПроисхожденияТовара КАК КодСтраныПроисхожденияТовара,
	|	ОплачиваемаяНоменклатура.НомерТаможеннойДекларации КАК НомерТаможеннойДекларации
	|ИЗ
	|	ВТ_ОплачиваемаяНоменклатураРеализация КАК ОплачиваемаяНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ПО СправочникНоменклатура.Ссылка = ОплачиваемаяНоменклатура.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплачиваемаяНоменклатура.Номенклатура,
	|	ЕСТЬNULL(СправочникНоменклатура.КодНоменклатурнойКлассификацииККТ.Код, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СправочникНоменклатура.КодВидаТРУ, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СправочникНоменклатура.Артикул, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Код, НЕОПРЕДЕЛЕНО),
	|	ОплачиваемаяНоменклатура.ЭтоУслуга,
	|	ОплачиваемаяНоменклатура.СтавкаНДС,
	|	ОплачиваемаяНоменклатура.Наименование,
	|	ОплачиваемаяНоменклатура.ДатаДокумента,
	|	ОплачиваемаяНоменклатура.ДоговорПлатежногоАгента,
	|	ОплачиваемаяНоменклатура.Документ,
	|	ОплачиваемаяНоменклатура.Цена,
	|	ОплачиваемаяНоменклатура.ПлатежныйАгент,
	|	ОплачиваемаяНоменклатура.КодСтраныПроисхожденияТовара,
	|	ОплачиваемаяНоменклатура.НомерТаможеннойДекларации,
	|	ОплачиваемаяНоменклатура.НомерТабличнойЧасти,
	|	&УсловиеМаркируемаяПродукция,
	|	ОплачиваемаяНоменклатура.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОплачиваемаяНоменклатура.НомерТабличнойЧасти,
	|	ОплачиваемаяНоменклатура.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОплачиваемыеДокументыРеализация.Документ КАК Документ,
	|	ВТ_ОплачиваемыеДокументыРеализация.Организация КАК Организация,
	|	ВТ_ОплачиваемыеДокументыРеализация.Дата КАК Дата,
	|	ВТ_ОплачиваемыеДокументыРеализация.СуммаСкидкиПоДокументу КАК СуммаСкидкиПоДокументу,
	|	ВТ_ОплачиваемыеДокументыРеализация.РасчетыВУсловныхЕдиницах
	|		И ВТ_ОплачиваемыеДокументыРеализация.ВалютаДокумента <> ВТ_ОплачиваемыеДокументыРеализация.ВалютаПлатежа КАК РасчетыВУсловныхЕдиницах,
	|	ВТ_ОплачиваемыеДокументыРеализация.СтавкаНДС КАК СтавкаНДС,
	|	ВТ_ОплачиваемыеДокументыРеализация.СуммаОплаты КАК СуммаОплаты,
	|	ВТ_ОплачиваемыеДокументыРеализация.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ВТ_ЗачетАвансов.СуммаДокумента, 0) = 0
	|				ТОГДА 0
	|			КОГДА ВТ_ЗачетАвансов.СуммаОплаты >= ВТ_ЗачетАвансов.СуммаДокумента
	|				ТОГДА 1
	|			ИНАЧЕ ВТ_ЗачетАвансов.СуммаОплаты / ВТ_ЗачетАвансов.СуммаДокумента
	|		КОНЕЦ * ВТ_ОплачиваемыеДокументыРеализация.Сумма КАК ЧИСЛО(15, 2)) КАК СуммаОплатыВсего
	|ИЗ
	|	ВТ_ОплачиваемыеДокументыРеализация КАК ВТ_ОплачиваемыеДокументыРеализация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗачетАвансов КАК ВТ_ЗачетАвансов
	|		ПО ВТ_ОплачиваемыеДокументыРеализация.Документ = ВТ_ЗачетАвансов.ОплачиваемыйДокумент";
	
	ЧастьЗапросаДляВыбораСодержанияУслуг = ОбщегоНазначенияБПВызовСервера.ПолучитьЧастьЗапросаДляВыбораСодержанияУслуг("РеализацияТовары");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЧастьЗапросаДляВыбораСодержанияУслуг", ЧастьЗапросаДляВыбораСодержанияУслуг);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеМаркируемаяПродукция",          ИнтеграцияИСМПБП.УсловиеПоВидуПродукции());
	
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

Функция ТекстЗапросаРеализацииПоСчету(ИменаТаблиц) Экспорт
	// Таблица содержит данные по реализации номенклатуры по счету, для случая если чек печатается по счету
	ИменаТаблиц.Добавить("ВТ_РеализованнаяНоменклатура");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.СчетНаОплату КАК СчетНаОплату,
	|	ВложенныйЗапрос.ЭтоУслуга КАК ЭтоУслуга,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Содержание КАК Содержание,
	|	ВложенныйЗапрос.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	ВложенныйЗапрос.ПлатежныйАгент КАК ПлатежныйАгент,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ВТ_РеализованнаяНоменклатура
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_РеализацииПоСчету.СчетНаОплату КАК СчетНаОплату,
	|		ЛОЖЬ КАК ЭтоУслуга,
	|		РеализованныеТовары.Номенклатура КАК Номенклатура,
	|		"""" КАК Содержание,
	|		НЕОПРЕДЕЛЕНО КАК ДоговорПлатежногоАгента,
	|		РеализованныеТовары.Количество КАК Количество,
	|		РеализованныеТовары.СтавкаНДС КАК СтавкаНДС,
	|		НЕОПРЕДЕЛЕНО КАК ПлатежныйАгент
	|	ИЗ
	|		ВТ_РеализацииПоСчету КАК ВТ_РеализацииПоСчету
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализованныеТовары
	|			ПО ВТ_РеализацииПоСчету.РеализацияТоваров = РеализованныеТовары.Ссылка
	|	ГДЕ
	|		НЕ РеализованныеТовары.Ссылка ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_РеализацииПоСчету.СчетНаОплату,
	|		ИСТИНА,
	|		РеализованныеУслуги.Номенклатура,
	|		РеализованныеУслуги.Содержание,
	|		НЕОПРЕДЕЛЕНО,
	|		ВЫБОР
	|			КОГДА РеализованныеУслуги.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ РеализованныеУслуги.Количество
	|		КОНЕЦ,
	|		РеализованныеУслуги.СтавкаНДС,
	|		НЕОПРЕДЕЛЕНО
	|	ИЗ
	|		ВТ_РеализацииПоСчету КАК ВТ_РеализацииПоСчету
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Услуги КАК РеализованныеУслуги
	|			ПО ВТ_РеализацииПоСчету.РеализацияТоваров = РеализованныеУслуги.Ссылка
	|	ГДЕ
	|		НЕ РеализованныеУслуги.Ссылка ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_РеализацииПоСчету.СчетНаОплату,
	|		ИСТИНА,
	|		РеализованныеАгентскиеУслуги.Номенклатура,
	|		РеализованныеАгентскиеУслуги.Содержание,
	|		РеализованныеАгентскиеУслуги.ДоговорКонтрагента,
	|		ВЫБОР
	|			КОГДА РеализованныеАгентскиеУслуги.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ РеализованныеАгентскиеУслуги.Количество
	|		КОНЕЦ,
	|		РеализованныеАгентскиеУслуги.СтавкаНДС,
	|		НЕОПРЕДЕЛЕНО
	|	ИЗ
	|		ВТ_РеализацииПоСчету КАК ВТ_РеализацииПоСчету
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.АгентскиеУслуги КАК РеализованныеАгентскиеУслуги
	|			ПО ВТ_РеализацииПоСчету.РеализацияТоваров = РеализованныеАгентскиеУслуги.Ссылка
	|	ГДЕ
	|		НЕ РеализованныеАгентскиеУслуги.Ссылка ЕСТЬ NULL) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.СчетНаОплату,
	|	ВложенныйЗапрос.ДоговорПлатежногоАгента,
	|	ВложенныйЗапрос.ЭтоУслуга,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Содержание,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.ПлатежныйАгент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетНаОплату,
	|	Номенклатура,
	|	Содержание";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
КонецФункции

Функция НаименованиеВСтрокеЧека(СтруктураШапки) Экспорт
	
	Возврат "";
	
КонецФункции

Функция ТекстЗапросаШтрихкодыУпаковок(ИменаТаблиц)

	ИменаТаблиц.Добавить("ШтрихкодыУпаковок");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ДокументыРеализации.Сделка КАК Документ,
	|	РеализацияТоваровУслугШтрихкодыУпаковок.ШтрихкодУпаковки КАК ШтрихкодУпаковки
	|ИЗ
	|	ВТ_ДокументыРеализации КАК ВТ_ДокументыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.ШтрихкодыУпаковок КАК РеализацияТоваровУслугШтрихкодыУпаковок
	|		ПО ВТ_ДокументыРеализации.Сделка = РеализацияТоваровУслугШтрихкодыУпаковок.Ссылка
	|ГДЕ
	|	НЕ РеализацияТоваровУслугШтрихкодыУпаковок.ШтрихкодУпаковки ЕСТЬ NULL";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции 

#КонецОбласти 

Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьТовары 
		ИЛИ Не Реквизиты.ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ВТ_Прослеживаемость",                  Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДС",                       Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации",               Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТовары",                 Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТоварыРеализованные",    Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТоварыТаблицаДокумента", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",                    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДС",                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации",                 НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТовары",                   НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТоварыРеализованные",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТоварыТаблицаДокумента",   НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	СУММА(РеализацияТоваровУслугСведенияПрослеживаемости.Количество) КАК Количество,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугТовары.ПрослеживаемыйКомплект
	|			ТОГДА РеализацияТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения
	|		ИНАЧЕ РеализацияТоваровУслугТовары.СтранаПроисхождения
	|	КОНЕЦ КАК СтранаПроисхождения,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Комитент КАК Комитент,
	|	СУММА(РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВЫБОР
	|					КОГДА РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПродаж)
	|				КОНЕЦ
	|		КОГДА &ДокументСНДС
	|				И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|	КОНЕЦ КАК ОтражениеВОтчетности,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ &ДокументСНДС
	|					И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		КОГДА РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|				И НЕ &РеализацияФизЛицу
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РеализацияТовараКомитента)
	|		КОГДА &ЭтоЭкспорт
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВывозТовараЗаПределыРФ)
	|		КОГДА &РеализацияФизЛицу
	|				И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПередачаСведенийОРозничнойРеализацииКомитенту)
	|		КОГДА &РеализацияФизЛицу
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РеализацияРозничная)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Реализация)
	|	КОНЕЦ КАК КодОперации,
	|	РеализацияТоваровУслугТовары.Ссылка КАК ДокументОперации,
	|	ВЫБОР
	|		КОГДА &РеализацияФизЛицу
	|				И НЕ РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА &РеализацияФизЛицу
	|				И НЕ РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА РеализацияТоваровУслугСведенияПрослеживаемости.Комитент
	|		ИНАЧЕ РеализацияТоваровУслугТовары.Ссылка.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА &ЭтоВыкупТоваровКомиссионером
	|			ТОГДА РеализацияТоваровУслугТовары.Ссылка.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ КАК Комиссионер,
	|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслугТовары.Ссылка.Дата, КВАРТАЛ) КАК ПериодОперации,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				И НЕ(&ДокументСНДС
	|						И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию))
	|			ТОГДА ВЫБОР
	|					КОГДА РеализацияТоваровУслугТовары.Ссылка.ЭтоУниверсальныйДокумент
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ТипДокументаВПрослеживаемости,
	|	РеализацияТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура КАК Комплектующие,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Сумма КАК СуммаБезНДС,
	|	РеализацияТоваровУслугТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	РеализацияТоваровУслугТовары.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_Прослеживаемость
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка
	|			И РеализацияТоваровУслугТовары.ИдентификаторСтроки = РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС)
	|			И (НЕ(&ДокументСНДС
	|					И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)))
	|			И РеализацияТоваровУслугТовары.Ссылка = ДанныеПервичныхДокументов.Документ
	|			И РеализацияТоваровУслугТовары.Ссылка.Организация = ДанныеПервичныхДокументов.Организация
	|ГДЕ
	|	РеализацияТоваровУслугТовары.ПрослеживаемыйТовар
	|	И РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Комитент,
	|	ВЫБОР
	|		КОГДА &ЭтоВыкупТоваровКомиссионером
	|			ТОГДА РеализацияТоваровУслугТовары.Ссылка.Контрагент
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ,
	|	РеализацияТоваровУслугТовары.Номенклатура,
	|	РеализацияТоваровУслугТовары.Ссылка,
	|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслугТовары.Ссылка.Дата, КВАРТАЛ),
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """"),
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)),
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВЫБОР
	|					КОГДА РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПродаж)
	|				КОНЕЦ
	|		КОГДА &ДокументСНДС
	|				И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				И НЕ(&ДокументСНДС
	|						И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию))
	|			ТОГДА ВЫБОР
	|					КОГДА РеализацияТоваровУслугТовары.Ссылка.ЭтоУниверсальныйДокумент
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	РеализацияТоваровУслугТовары.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугТовары.ПрослеживаемыйКомплект
	|			ТОГДА РеализацияТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения
	|		ИНАЧЕ РеализацияТоваровУслугТовары.СтранаПроисхождения
	|	КОНЕЦ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Сумма,
	|	РеализацияТоваровУслугТовары.ПрослеживаемыйКомплект,
	|	РеализацияТоваровУслугТовары.НомерСтроки,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ &ДокументСНДС
	|					И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		КОГДА РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|				И НЕ &РеализацияФизЛицу
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РеализацияТовараКомитента)
	|		КОГДА &ЭтоЭкспорт
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ВывозТовараЗаПределыРФ)
	|		КОГДА &РеализацияФизЛицу
	|				И РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПередачаСведенийОРозничнойРеализацииКомитенту)
	|		КОГДА &РеализацияФизЛицу
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РеализацияРозничная)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Реализация)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &РеализацияФизЛицу
	|				И НЕ РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА &РеализацияФизЛицу
	|				И НЕ РеализацияТоваровУслугТовары.СчетУчета В (&ТоварыПринятыеНаКомиссию)
	|			ТОГДА РеализацияТоваровУслугСведенияПрослеживаемости.Комитент
	|		ИНАЧЕ РеализацияТоваровУслугТовары.Ссылка.Контрагент
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.СуммаРуб - ТаблицаТовары.СуммаНДСРуб КАК СуммаБезНДС,
	|	РеализацияТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТ_СуммыБезНДС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТовары КАК ТаблицаТовары
	|		ПО РеализацияТоваровУслугТовары.ИдентификаторСтроки = ТаблицаТовары.ИдентификаторСтроки
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|	И РеализацияТоваровУслугТовары.ПрослеживаемыйТовар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.ПрослеживаемыйКомплект
	|			ТОГДА ВТ_Прослеживаемость.Комплектующие
	|		ИНАЧЕ ВТ_Прослеживаемость.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВТ_Прослеживаемость.Количество КАК Количество,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
	|	ВТ_Прослеживаемость.КодОперации КАК КодОперации,
	|	ВТ_Прослеживаемость.ДокументОперации КАК ДокументОперации,
	|	ВТ_Прослеживаемость.Контрагент КАК Контрагент,
	|	ВТ_Прослеживаемость.Комиссионер КАК Комиссионер,
	|	ВТ_Прослеживаемость.ПериодОперации КАК ПериодОперации,
	|	ВТ_Прослеживаемость.НомерДокумента КАК НомерДокумента,
	|	ВТ_Прослеживаемость.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
	|	ВТ_Прослеживаемость.Комитент КАК Комитент,
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.ПрослеживаемыйКомплект
	|			ТОГДА ВТ_Прослеживаемость.СуммаБезНДС
	|		ИНАЧЕ ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВТ_Прослеживаемость.Комплектующие КАК Комплектующие,
	|	ВТ_Прослеживаемость.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_Прослеживаемость.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВТ_Прослеживаемость.ИдентификаторСтроки = ВТ_СуммыБезНДС.ИдентификаторСтроки
	|ГДЕ
	|	НЕ &ЭтоДоговорСКомиссионером
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.Комитент КАК Комитент,
	|	ВТ_Прослеживаемость.Комиссионер КАК Комиссионер,
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.Комитент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЗапасовПрослеживаемыхТоваров.ТоварыОтКомитентаНаРеализацию)
	|	КОНЕЦ КАК ВидЗапасов,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДляВозврата,
	|	ВТ_Прослеживаемость.Комплектующие КАК Комплектующие
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.Номенклатура,
	|	ВТ_Прослеживаемость.СтранаПроисхождения,
	|	ВТ_Прослеживаемость.РНПТ,
	|	ВТ_Прослеживаемость.Комитент,
	|	ВТ_Прослеживаемость.Комиссионер,
	|	ВТ_Прослеживаемость.Комплектующие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Количество КАК Количество,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	РеализацияТоваровУслугТовары.Ссылка КАК СопроводительныйДокумент,
	|	РеализацияТоваровУслугТовары.Ссылка.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслугТовары.НомерСтроки КАК ПорядковыйНомер,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Комитент КАК Комитент
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|		ПО РеализацияТоваровУслугТовары.Ссылка = РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка
	|			И РеализацияТоваровУслугТовары.ИдентификаторСтроки = РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
	|ГДЕ
	|	РеализацияТоваровУслугТовары.ПрослеживаемыйТовар
	|	И РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	ВТ_СуммыБезНДС.СуммаБезНДС КАК СуммаБезНДС,
	|	ВТ_Прослеживаемость.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВТ_Прослеживаемость.ИдентификаторСтроки = ВТ_СуммыБезНДС.ИдентификаторСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.Номенклатура,
	|	ВТ_СуммыБезНДС.СуммаБезНДС,
	|	ВТ_Прослеживаемость.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПрослеживаемыеТоварыВРаботах(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьУслуги
		ИЛИ Не Реквизиты.ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ВТ_ПрослеживаемостьУслуги",       Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТоварыВРаботах",    Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперацииПоРаботам", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_ПрослеживаемостьУслуги",       НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТоварыВРаботах",    НомераТаблиц.Количество()); 
	НомераТаблиц.Вставить("ПрослеживаемыеОперацииПоРаботам", НомераТаблиц.Количество());

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СУММА(РеализацияТоваровУслугСведенияПрослеживаемости.Количество) КАК Количество,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(РеализацияТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Сумма КАК СуммаБезНДС,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПродаж)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|	КОНЕЦ КАК ОтражениеВОтчетности,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		КОГДА &РеализацияФизЛицу
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.РеализацияРозничная)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Реализация)
	|	КОНЕЦ КАК КодОперации,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка КАК ДокументОперации,
	|	ВЫБОР
	|		КОГДА &РеализацияФизЛицу
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Дата, КВАРТАЛ) КАК ПериодОперации,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВЫБОР
	|					КОГДА РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.ЭтоУниверсальныйДокумент
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ТипДокументаВПрослеживаемости,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТ_ПрослеживаемостьУслуги
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Услуги КАК РеализацияТоваровУслугУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.СведенияПрослеживаемости КАК РеализацияТоваровУслугСведенияПрослеживаемости
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|			ПО (НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС)
	|				И РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка = ДанныеПервичныхДокументов.Документ
	|				И РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Организация = ДанныеПервичныхДокументов.Организация
	|		ПО РеализацияТоваровУслугУслуги.ИдентификаторСтроки = РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
	|ГДЕ
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка = &Ссылка
	|	И РеализацияТоваровУслугУслуги.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """"),
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)),
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Номенклатура,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Сумма,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка,
	|	РеализацияТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА &РеализацияФизЛицу
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Контрагент
	|	КОНЕЦ,
	|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.Дата, КВАРТАЛ),
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ВЫБОР
	|					КОГДА РеализацияТоваровУслугСведенияПрослеживаемости.Ссылка.ЭтоУниверсальныйДокумент
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрослеживаемостьУслуги.Количество КАК Количество,
	|	ВТ_ПрослеживаемостьУслуги.РНПТ КАК РНПТ,
	|	ВТ_ПрослеживаемостьУслуги.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_ПрослеживаемостьУслуги.НомерДокумента КАК НомерДокумента,
	|	ВТ_ПрослеживаемостьУслуги.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_ПрослеживаемостьУслуги.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВТ_ПрослеживаемостьУслуги.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЗапасовПрослеживаемыхТоваров.ТоварыПередаваемыеВСоставеРабот) КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДляВозврата,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|	НЕОПРЕДЕЛЕНО КАК Комиссионер,
	|	НЕОПРЕДЕЛЕНО КАК Комплектующие
	|ИЗ
	|	ВТ_ПрослеживаемостьУслуги КАК ВТ_ПрослеживаемостьУслуги
	|ГДЕ
	|	НЕ &ЭтоДоговорСКомиссионером
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПрослеживаемостьУслуги.Количество КАК Количество,
	|	ВТ_ПрослеживаемостьУслуги.РНПТ КАК РНПТ,
	|	ВТ_ПрослеживаемостьУслуги.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_ПрослеживаемостьУслуги.НомерДокумента КАК НомерДокумента,
	|	ВТ_ПрослеживаемостьУслуги.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_ПрослеживаемостьУслуги.Номенклатура КАК Номенклатура,
	|	ВТ_ПрослеживаемостьУслуги.СуммаБезНДС КАК СуммаБезНДС,
	|	ВТ_ПрослеживаемостьУслуги.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВТ_ПрослеживаемостьУслуги.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
	|	ВТ_ПрослеживаемостьУслуги.КодОперации КАК КодОперации,
	|	ВТ_ПрослеживаемостьУслуги.ДокументОперации КАК ДокументОперации,
	|	ВТ_ПрослеживаемостьУслуги.Контрагент КАК Контрагент,
	|	ВТ_ПрослеживаемостьУслуги.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
	|	ВТ_ПрослеживаемостьУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ИСТИНА КАК ПрослеживаемыйКомплект,
	|	ВТ_ПрослеживаемостьУслуги.ПериодОперации КАК ПериодОперации
	|ИЗ
	|	ВТ_ПрослеживаемостьУслуги КАК ВТ_ПрослеживаемостьУслуги";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Процедура ЗаполнитьСтрокиТЧЗагрузкаИзФайла(ТаблицаТовары, ИмяТаблицы, ДанныеОбъекта)
	
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТовары, "Номенклатура", Истина), ДанныеОбъекта);
	
	Для Каждого СтрокаТовара Из ТаблицаТовары Цикл
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТовара.Номенклатура);
		Если СведенияОНоменклатуре <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(СтрокаТовара.СтавкаНДС) Тогда
				СтрокаТовара.СтавкаНДС = СведенияОНоменклатуре.СтавкаНДС;
			КонецЕсли;
			
			СтрокаТовара.ЕдиницаИзмерения    = СведенияОНоменклатуре.ЕдиницаИзмерения;
			СтрокаТовара.Коэффициент         = СведенияОНоменклатуре.Коэффициент;
			СтрокаТовара.НомерГТД            = СведенияОНоменклатуре.НомерГТД;
			СтрокаТовара.СтранаПроисхождения = СведенияОНоменклатуре.СтранаПроисхождения;
			Если ДанныеОбъекта.ВедетсяУчетПрослеживаемыхТоваров Тогда 
				СтрокаТовара.ПрослеживаемыйТовар = СведенияОНоменклатуре.ПрослеживаемыйТовар;
			Иначе
				СтрокаТовара.ПрослеживаемыйТовар = Ложь;
			КонецЕсли;
		Иначе
			СтрокаТовара.СтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(
				Перечисления.ВидыСтавокНДС.Общая, ДанныеОбъекта.Дата);
		КонецЕсли;
		
		СтрокаТовара.Цена = ?(СтрокаТовара.Количество = 0, СтрокаТовара.Сумма, Окр(СтрокаТовара.Сумма/СтрокаТовара.Количество, 2, 1));
		
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТовара, ДанныеОбъекта.СуммаВключаетНДС);
		
	КонецЦикла;
			
КонецПроцедуры

Функция ТекстЗапросаАкцизы(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	Если Не Реквизиты.ЕстьТовары Или Не Реквизиты.ЕстьУчетПодакцизныхТоваров Тогда
		ПараметрыПроведения.Вставить("ТаблицаАкцизы",          Неопределено);
		ПараметрыПроведения.Вставить("ТаблицаРеквизитыАкцизы", Неопределено);
		Возврат "";
	КонецЕсли;

	НомераТаблиц.Вставить("ТаблицаРеквизитыАкцизы", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаАкцизы",          НомераТаблиц.Количество());

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Регистратор,
	|	РеализацияТоваровУслуг.Дата КАК Период,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.ПодразделениеОрганизации КАК ПодразделениеОрганизации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Субконто КАК Субконто,
	|	СУММА(РеализацияТоваровУслугТовары.СуммаАкциза) КАК СуммаАкциза,
	|	РеализацияТоваровУслугТовары.СчетАкциза КАК СчетАкциза,
	|	РеализацияТоваровУслугТовары.СубконтоАкцизы КАК СубконтоАкцизы
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	|	И РеализацияТоваровУслугТовары.ПодакцизныйТовар = ИСТИНА
	|	И РеализацияТоваровУслугТовары.СуммаАкциза > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Субконто,
	|	РеализацияТоваровУслугТовары.СчетАкциза,
	|	РеализацияТоваровУслугТовары.СубконтоАкцизы";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Определяет является ли документ "Реализация (акты, накладные, УПД)" операцией реализации
// через электронную торговую площадку физлицам в страну ЕАЭС
//
// Параметры:
// ДокументСсылка - ДокументСсылкаРеализацияТоваровУслуг
//
// Возвращаемое значение:
// Булево - если операция реализации через электронную торговую площадку физлицам в страну ЕАЭС
//
Функция РеализацияЧерезЭлектроннуюТорговуюПлощадкуФизическомуЛицуВСтрануЕАЭС(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РеализацияТоваровУслуг.СтранаРеализации, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.СтранаРеализации.УчастникЕАЭС
	|					И РеализацияТоваровУслуг.СтранаРеализации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|		ИНАЧЕ ЕСТЬNULL(РеализацияТоваровУслуг.Контрагент.СтранаРегистрации, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|				И ЕСТЬNULL(РеализацияТоваровУслуг.Контрагент.СтранаРегистрации.УчастникЕАЭС, ЛОЖЬ) = ИСТИНА
	|	КОНЕЦ КАК РеализацияФизлицуВЕАЭС
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &ДокументСсылка
	|	И РеализацияТоваровУслуг.ЭлектроннаяТорговаяПлощадка
	|	И РеализацияТоваровУслуг.Контрагент.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И НЕ РеализацияТоваровУслуг.Контрагент.ИндивидуальныйПредприниматель";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.РеализацияФизлицуВЕАЭС;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ТекстЗапросаЗачетАвансовПлатежногоАгента(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Не Реквизиты.ОплатаЧерезКомиссионера Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовПлатежногоАгентаРеквизиты",        Неопределено);
		ПараметрыПроведения.Вставить("ЗачетАвансовПлатежногоАгентаТаблицаДокумента", Неопределено);
		ПараметрыПроведения.Вставить("ЗачетАвансовПлатежногоАгентаТаблицаАвансов",   Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ЗачетАвансовПлатежногоАгентаРеквизиты",        НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовПлатежногоАгентаТаблицаДокумента", НомераТаблиц.Количество());
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.ДеятельностьНаТорговомСборе,
	|	Реквизиты.ДеятельностьНаПатенте,
	|	&ПрименяетсяУСН КАК УчитыватьЗадолженностьУСН,
	|	&УчитыватьЗадолженностьУСНПатент КАК УчитыватьЗадолженностьУСНПатент,
	|	""Выбытие"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК ДокументРасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКомиссионером КАК СчетРасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКомиссионеромПоАвансам КАК СчетАвансов,
	|	Реквизиты.Комиссионер КАК Контрагент,
	|	Реквизиты.ДоговорКомиссионера КАК ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	Реквизиты.РасчетыВУсловныхЕдиницах,
	|	Реквизиты.УчетАгентскогоНДС,
	|	&РасчетыВВалюте КАК РасчетыВВалюте,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов,
	|	ТаблицаСуммВзаиморасчетов.СуммаРуб,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовКомитента,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовЕНВД,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовТорговыйСбор,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетовПатент
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСуммВзаиморасчетов КАК ТаблицаСуммВзаиморасчетов
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов ЕСТЬ НЕ NULL "
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Если Реквизиты.СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовПлатежногоАгентаТаблицаАвансов", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ЗачетАвансовПлатежногоАгентаТаблицаАвансов",   НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса
		+ "ВЫБРАТЬ
		|	РеализацияТоваровУслугЗачетАвансов.НомерСтроки КАК НомерСтроки,
		|	Реквизиты.СчетУчетаРасчетовСКомиссионеромПоАвансам КАК СчетАвансов,
		|	Реквизиты.Комиссионер,
		|	Реквизиты.ДоговорКомиссионера,
		|	РеализацияТоваровУслугЗачетАвансов.ДокументАванса,
		|	РеализацияТоваровУслугЗачетАвансов.СуммаЗачета
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.ЗачетАвансов КАК РеализацияТоваровУслугЗачетАвансов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	РеализацияТоваровУслугЗачетАвансов.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаменитьУстаревшийВидЭлектронногоДокумента(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидЭлектронногоДокумента", Перечисления.ТипыДокументовЭДО.УдалитьАктИсполнитель);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ВидЭлектронногоДокумента = &ВидЭлектронногоДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваровУслуг.Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.РеализацияТоваровУслуг");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
			Если ДокументОбъект = Неопределено ИЛИ
					ДокументОбъект.ВидЭлектронногоДокумента = Перечисления.ТипыДокументовЭДО.АктВыполненныхРабот Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект.ВидЭлектронногоДокумента = Перечисления.ТипыДокументовЭДО.АктВыполненныхРабот;
		
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре ОбменСКонтрагентамиБП.ЗаменитьУстаревшийВидЭлектронногоДокумента() не удалось обработать документ ""Реализация товаров и услуг"" по причине:
					|%1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, Выборка.Ссылка, ТекстСообщения);
				
		КонецПопытки; 
		
	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов > 0 Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В процедуре ОбменСКонтрагентамиБП.ЗаменитьУстаревшийВидЭлектронногоДокумента() не удалось обработать документ ""Реализация товаров и услуг"": в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.РеализацияТоваровУслуг,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ОбменСКонтрагентамиБП.ЗаменитьУстаревшийВидЭлектронногоДокумента() обработала очередную порцию документов ""Реализация товаров и услуг"": %1 документов'"), ОбъектовОбработано));
	КонецЕсли;
			
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьАдресДоставки(Параметры) Экспорт

	Параметры.ОбработкаЗавершена = Истина;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуТранспортнойКомпанией") Тогда
		Возврат;
	КонецЕсли; 
	
	СпособДоставкиСамовывоз = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.СпособыДоставки.Самовывоз");
	Если СпособДоставкиСамовывоз = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СпособДоставкиСамовывоз", СпособДоставкиСамовывоз);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.СпособДоставки = &СпособДоставкиСамовывоз
	|	И РеализацияТоваровУслуг.АдресДоставки <> """"";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НачатьТранзакцию();
		
		Попытка
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.РеализацияТоваровУслуг");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
			Если ДокументОбъект = Неопределено ИЛИ
					ДокументОбъект.АдресДоставки = "" Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект.АдресДоставки = "";
		
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре РеализацияТоваровУслуг.ОчиститьАдресДоставки() не удалось обработать документ ""Реализация товаров и услуг"" по причине:
					|%1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, Выборка.Ссылка, ТекстСообщения);
				
		КонецПопытки; 
	
	КонецЦикла; 
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов > 0 Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В процедуре РеализацияТоваровУслуг.ОчиститьАдресДоставки() не удалось обработать документ ""Реализация товаров и услуг"": в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.РеализацияТоваровУслуг,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура РеализацияТоваровУслуг.ОчиститьАдресДоставки() обработала очередную порцию документов ""Реализация товаров и услуг"": %1 документов'"), ОбъектовОбработано));
	КонецЕсли;
			
	Параметры.ОбработкаЗавершена = ОбъектовОбработано = 0 И ПроблемныхОбъектов = 0;
КонецПроцедуры
 
Процедура ИсправитьСтавкуНДСЯндексМаркет(Параметры) Экспорт

	Параметры.ОбработкаЗавершена = Истина;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяОтгрузкаБезПереходаПраваСобственности") Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслугТовары.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслугТовары.Ссылка.Дата КАК Дата
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|ГДЕ
	|	РеализацияТоваровУслугТовары.Ссылка.ДокументБезНДС
	|	И РеализацияТоваровУслугТовары.Ссылка.ОтчетМаркетплейса <> ЗНАЧЕНИЕ(Документ.ОтчетМаркетплейса.ПустаяСсылка)
	|	И РеализацияТоваровУслугТовары.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И РеализацияТоваровУслугТовары.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровУслугТовары.Ссылка,
	|	РеализацияТоваровУслугТовары.Ссылка.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НачатьТранзакцию();
		
		Попытка
			// Блокируем объект от изменения другими сеансами.
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.РеализацияТоваровУслуг");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
			Если ДокументОбъект = Неопределено 
				ИЛИ (НЕ ЗначениеЗаполнено(ДокументОбъект.ОтчетМаркетплейса)) 
				ИЛИ (НЕ ДокументОбъект.ДокументБезНДС) Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Для Каждого СтрокаТовары Из ДокументОбъект.Товары Цикл
				СтрокаТовары.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
			КонецЦикла;
					
			// Запись обработанного объекта (без перепроведения).
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре РеализацияТоваровУслуг.ИсправитьСтавкуНДСЯндексМаркет() не удалось обработать документ ""Реализация товаров и услуг"" по причине:
					|%1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, Выборка.Ссылка, ТекстСообщения);
				
		КонецПопытки; 
	
	КонецЦикла; 
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов > 0 Тогда 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В процедуре РеализацияТоваровУслуг.ИсправитьСтавкуНДСЯндексМаркет() не удалось обработать документ ""Реализация товаров и услуг"": в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ПроблемныхОбъектов + ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.РеализацияТоваровУслуг,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура РеализацияТоваровУслуг.ИсправитьСтавкуНДСЯндексМаркет() обработала очередную порцию документов ""Реализация товаров и услуг"": %1 документов'"), ОбъектовОбработано));
	КонецЕсли;
			
	Параметры.ОбработкаЗавершена = ОбъектовОбработано = 0 И ПроблемныхОбъектов = 0;
КонецПроцедуры

Процедура ЗаполнитьПризнакЭлектроннаяТорговаяПлощадка(Параметры) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетТаможенныхДекларацийЭкспорт") Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО РеализацияТоваровУслуг.Ссылка = РеализацияТоваровУслугТовары.Ссылка
	|ГДЕ
	|	НЕ РеализацияТоваровУслуг.ЭлектроннаяТорговаяПлощадка
	|	И РеализацияТоваровУслуг.Дата >= ДАТАВРЕМЯ(2024, 7, 1)
	|	И РеализацияТоваровУслуг.СтранаРеализации В(&СтраныЕАЭС)
	|	И РеализацияТоваровУслуг.Контрагент.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И НЕ РеализацияТоваровУслуг.Контрагент.ИндивидуальныйПредприниматель
	|	И РеализацияТоваровУслугТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И &КурсорСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеализацияТоваровУслуг.Ссылка";
	
	Запрос.Параметры.Вставить("СтраныЕАЭС", Справочники.СтраныМира.СтраныЕАЭС());
	
	Если Не Параметры.Свойство("КурсорСсылка") Тогда
		
		Запрос.УстановитьПараметр("КурсорСсылка", Истина);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&КурсорСсылка",
								"РеализацияТоваровУслуг.Ссылка > &КурсорСсылка");
		
		Запрос.УстановитьПараметр("КурсорСсылка", Параметры.КурсорСсылка);
		
	КонецЕсли;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		//Проверка на NULL и пустые поля
		
		Если НЕ ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Параметры.Вставить("КурсорСсылка", Выборка.Ссылка);
		
		Попытка
		
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ЭлектроннаяТорговаяПлощадка = Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			
			//Отменяем транзакции, в том числе, если вдруг вызывались транзакции в стандартных функциях, отменяем их
			
				Пока ТранзакцияАктивна() Цикл
				
					ОтменитьТранзакцию();
				
				КонецЦикла;

				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка при выполнении обработчика РеализацияТоваровУслуг.ЗаполнитьПризнакЭлектроннаяТорговаяПлощадка() для документа ""%1"" по причине:
						|%2'"),
					Выборка.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,
					Выборка.Ссылка, ТекстСообщения);

				ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
				
		КонецПопытки
			
	КонецЦикла;
	
	Если ОбъектовОбработано + ПроблемныхОбъектов = 0 Тогда
		
		Если Выборка.Количество() = 0 Тогда
			
			Параметры.ОбработкаЗавершена = Истина;
			
		Иначе
			
			Параметры.ОбработкаЗавершена = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Параметры.ОбработкаЗавершена = Ложь;
		
		Если ОбъектовОбработано = 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при выполнении обработчика РеализацияТоваровУслуг.ЗаполнитьПризнакЭлектроннаяТорговаяПлощадка()
					|не удалось записать изменения реквизита ""Интернет-заказ"" в документе ""Реализация (акты, накладные, УПД)"" для %1 элементов.'"), 
					ПроблемныхОбъектов);
			ВызватьИсключение ТекстСообщения;
			
		Иначе
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Процедура РеализацияТоваровУслуг.ЗаполнитьПризнакЭлектроннаяТорговаяПлощадка()
						|обработала очередную порцию документов ""Реализация (акты, накладные, УПД)"": %1 элементов.'"), 
						ОбъектовОбработано));
						
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
