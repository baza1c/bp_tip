#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполним реквизиты формы из параметров.
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры,
		"Организация, Контрагент, Грузоотправитель, Грузополучатель,
		|АдресДоставки, АдресДоставкиЗначение, СтранаРеализации, ЭлектроннаяТорговаяПлощадка,
		|ДатаДокумента, СведенияОТранспортировкеИГрузе,
		|ПеревозкаАвтотранспортом, СпособДоставки,
		|Перевозчик, МаркаАвтомобиля, РегистрационныйЗнакАвтомобиля,
		|МаркаПрицепа, РегистрационныйЗнакПрицепа,
		|Водитель, ВодительДолжность, ВодительскоеУдостоверение,
		|КраткоеНаименованиеГруза, СопроводительныеДокументы, ВидТранспортаНаГранице,
		|ДокументОснование");
	
	ПолноеНаименованиеКонтрагента = "";
	СсылкаДляПереходаНаКарту = УправлениеКонтактнойИнформациейБП.СтрокаСсылкиПоказатьНаКарте();
	
	// Отобразим грузоотправителя и грузополучателя
	Если НЕ ЗначениеЗаполнено(Грузоотправитель) Тогда
		ГрузоотправительОнЖе = 1;
	Иначе
		ГрузоотправительОнЖе = 0;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Грузополучатель) Тогда
		ГрузополучательОнЖе = 1;
	Иначе
		ГрузополучательОнЖе = 0;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Перевозчик) Тогда
		ПеревозчикОнЖе = 1;
	Иначе
		ПеревозчикОнЖе = 0;
	КонецЕсли;
	
	// Если самовывоз или доставка собственными силами - можно выбрать перевозчика, так как он не задан в виде отгрузки
	Элементы.ГруппаПеревозчик.Доступность  = НЕ ЗначениеЗаполнено(СпособДоставки) 
		ИЛИ ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СпособДоставки, "ИмяПредопределенныхДанных"));
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекстГрузоотправительОнЖе = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "Наименование");
	Иначе
		ТекстГрузоотправительОнЖе = НСтр("ru = 'Организация'");
	КонецЕсли;
	Элементы.ГрузоотправительОнЖе1.СписокВыбора[0].Представление = ТекстГрузоотправительОнЖе;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекстПеревозчикОнЖе = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "Наименование");
	Иначе
		ТекстПеревозчикОнЖе = НСтр("ru = 'Организация'");
	КонецЕсли;
	Элементы.ПеревозчикОнЖе1.СписокВыбора[0].Представление = ТекстПеревозчикОнЖе;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Контрагент, "Наименование, НаименованиеПолное");
	
		ТекстГрузополучательОнЖе = РеквизитыКонтрагента.Наименование;
		
		Если ЗначениеЗаполнено(РеквизитыКонтрагента.НаименованиеПолное) Тогда
			ПолноеНаименованиеКонтрагента = РеквизитыКонтрагента.НаименованиеПолное;
		Иначе
			НаименованиеКонтрагента = РеквизитыКонтрагента.Наименование;
		КонецЕсли;
	Иначе
		ТекстГрузополучательОнЖе = НСтр("ru = 'Контрагент'");
	КонецЕсли;
	Элементы.ГрузополучательОнЖе1.СписокВыбора[0].Представление = ТекстГрузополучательОнЖе;
	
	
	ИспользоватьАдресДоставки = НЕ ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуТранспортнойКомпанией") 
		ИЛИ НЕ ЗначениеЗаполнено(СпособДоставки)
		ИЛИ СпособДоставки = Справочники.СпособыДоставки.Самовывоз;
		
	Элементы.ГруппаАдресДоставки.Видимость = ИспользоватьАдресДоставки;
	Если ИспользоватьАдресДоставки Тогда
		// Заполним списки выбора адреса доставки и доверенности значениями по умолчанию
		РеализацияТоваровУслугФормы.ЗаполнитьСписокАдресовДоставки(ЭтаФорма, Контрагент, Грузополучатель, ДатаДокумента);
	КонецЕсли; 
	
	
	ИспользоватьДоставкуАвтотранспортом = ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуАвтотранспортом");
	Элементы.ПеревозкаАвтотранспортом.Видимость = ИспользоватьДоставкуАвтотранспортом;
	Элементы.ГруппаПеревозкаАвтотранспортом.Видимость = ИспользоватьДоставкуАвтотранспортом;
	
	ЕстьПравоНаПросмотрТранспортныхСредств = ПравоДоступа("Просмотр", Метаданные.Справочники.ТранспортныеСредства);
	Если Не ЕстьПравоНаПросмотрТранспортныхСредств Тогда
		Элементы.Марка.КнопкаВыбора = Ложь;
		Элементы.Марка.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	УстановитьЗаголовокГруппыПрицеп(ЭтотОбъект);
	
	УправлениеФормой(ЭтаФорма);
	
	УчетНДСКлиентСервер.ЗаполнитьСписокВыбораВидовТранспорта(Элементы.ВидТранспортаНаГранице.СписокВыбора);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Документ.РеализацияТоваровУслуг",
		"ФормаРеквизитыОрганизацииКонтрагентаТовары",
		НСтр("ru='Новости: Реализация (акт, накладная, УПД)'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаРеализацииПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	ИначеЕсли Модифицированность И НЕ ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;

	Если ПеренестиВДокумент И НЕ Отказ Тогда
		ОбработкаПроверкиЗаполненияНаКлиенте(Отказ);
	КонецЕсли;

	Если Отказ Тогда
		ПеренестиВДокумент = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ПеренестиВДокумент И Модифицированность Тогда
		СтруктураРезультат = Новый Структура("
			|Грузоотправитель,Грузополучатель, ПеревозкаАвтотранспортом,
			|АдресДоставки, АдресДоставкиЗначение, СтранаРеализации, ЭлектроннаяТорговаяПлощадка,
			|Перевозчик, МаркаАвтомобиля, РегистрационныйЗнакАвтомобиля,
			|МаркаПрицепа, РегистрационныйЗнакПрицепа,
			|СведенияОТранспортировкеИГрузе,
			|Водитель, ВодительскоеУдостоверение, ВодительДолжность,
			|КраткоеНаименованиеГруза, СопроводительныеДокументы, ВидТранспортаНаГранице");
		
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, ЭтаФорма);
		
		Если ГрузоотправительОнЖе = 1 Тогда
			СтруктураРезультат.Грузоотправитель = Неопределено;
		КонецЕсли;

		Если ГрузополучательОнЖе = 1 Тогда
			СтруктураРезультат.Грузополучатель = Неопределено;
		КонецЕсли;
		
		Если ПеревозчикОнЖе = 1 Тогда
			СтруктураРезультат.Перевозчик = Неопределено;
		КонецЕсли;
		
		ОповеститьОВыборе(СтруктураРезультат);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГрузоотправительОнЖеПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ГрузоотправительАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузоотправительОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ГрузоотправительСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузоотправительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательОнЖеПриИзменении(Элемент)
	
	ГрузополучательОнЖеПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательПриИзменении(Элемент)
	
	ОбновитьСписокАдресовДоставки();

КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузополучательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикОнЖеПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозчикОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВодительПриИзменении(Элемент)
	
	ЗаполнитьИнформациюПоВодителю();
	
КонецПроцедуры

&НаКлиенте
Процедура ВодительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаВыбора = ОткрытьФорму("Справочник.ФизическиеЛица.ФормаСписка",
			Новый Структура("РежимВыбора", Истина), Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВодительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		Водитель = ВыбранноеЗначение;
		ЗаполнитьИнформациюПоВодителю();
		Возврат;
	КонецЕсли;
	
	ВодительОбработкаВыбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ВодительАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбораВодителя = ВодительДанныеВыбора(ПараметрыПолученияДанных);
	Если ДанныеВыбораВодителя.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ДанныеВыбораВодителя;
	Иначе
		ОбщегоНазначенияБПКлиент.ФИОАвтоПодбор(Текст, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МаркаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЕстьПравоНаПросмотрТранспортныхСредств Тогда
		ФормаВыбора = ОткрытьФорму("Справочник.ТранспортныеСредства.ФормаВыбора", , Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МаркаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеАвтомобиля = ДанныеАвтомобиляНаСервере(ВыбранноеЗначение);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеАвтомобиля, 
		"МаркаАвтомобиля, РегистрационныйЗнакАвтомобиля, МаркаПрицепа, РегистрационныйЗнакПрицепа");
	
	УстановитьЗаголовокГруппыПрицеп(ЭтотОбъект);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура МаркаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Текст) Или Не ЕстьПравоНаПросмотрТранспортныхСредств Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбораАвтомобиля = МаркаАвтоДанныеВыбора(ПараметрыПолученияДанных);
	Если ДанныеВыбораАвтомобиля.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ДанныеВыбораАвтомобиля;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура МаркаПрицепаПриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрицеп(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрационныйЗнакПрицепаПриИзменении(Элемент)
	
	УстановитьЗаголовокГруппыПрицеп(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеревозкаАвтотранспортомПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПереходНаКартуНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, АдресДоставки);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	ДанныеФормы = Новый Структура("АдресДоставки, АдресДоставкиЗначение");
	ЗаполнитьЗначенияСвойств(ДанныеФормы, ЭтотОбъект);
	РеализацияТоваровУслугФормыКлиент.АдресДоставкиОбработкаВыбора(ДанныеФормы, ВыбранноеЗначение, СтандартнаяОбработка);
	Если ДанныеФормы.АдресДоставки <> АдресДоставки ИЛИ ДанныеФормы.АдресДоставкиЗначение <> АдресДоставкиЗначение Тогда
		Модифицированность = Истина;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеФормы);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.АдресДоставкиНачалоВыбора(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПриИзменении(Элемент)
	РеализацияТоваровУслугФормыВызовСервера.ПроверитьАдрес(АдресДоставки, АдресДоставкиЗначение);
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Если Модифицированность И ЗначениеЗаполнено(Водитель) Тогда
		СохранитьДанныеВодителя();
	КонецЕсли;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;

	Элементы.Грузоотправитель.Доступность                    = (Форма.ГрузоотправительОнЖе = 0);
	Элементы.Грузополучатель.Доступность                     = (Форма.ГрузополучательОнЖе = 0);
	Элементы.Перевозчик.Доступность                          = (Форма.ПеревозчикОнЖе = 0);
	Элементы.ГруппаПеревозкаАвтотранспортом.Доступность = Форма.ПеревозкаАвтотранспортом;
	Элементы.АдресДоставкиПереходНаКарту.Доступность         = ЗначениеЗаполнено(Форма.АдресДоставки);
	
	Если Форма.СтранаРеализации = ПредопределенноеЗначение("Справочник.СтраныМира.Россия")
		ИЛИ ТипЗнч(Форма.ДокументОснование) <> Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Форма.ВидТранспортаНаГранице = "";
		Элементы.ВидТранспортаНаГранице.Видимость = Ложь;
		Элементы.ЭлектроннаяТорговаяПлощадка.Видимость = Ложь;
		ЭлектроннаяТорговаяПлощадка = Ложь;
	Иначе
		Элементы.ВидТранспортаНаГранице.Видимость = Истина;
		Элементы.ЭлектроннаяТорговаяПлощадка.Видимость = ВидимостьЭлектроннойТорговойПлощадки(Форма.СтранаРеализации,
			Форма.Контрагент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидимостьЭлектроннойТорговойПлощадки( Знач СтранаРеализации, Знач Контрагент)
	Возврат Справочники.СтраныМира.ГосударствоЧленТаможенногоСоюза(СтранаРеализации)
			И Справочники.Контрагенты.ЭтоФизическоеЛицо(Контрагент);
КонецФункции

&НаСервере
Процедура ОбновитьСписокАдресовДоставки()

	Если ГрузополучательОнЖе = 1 Тогда
		ВыбранныйГрузополучатель = Контрагент;
	Иначе
		ВыбранныйГрузополучатель = Грузополучатель;
	КонецЕсли;

	РеализацияТоваровУслугФормы.ЗаполнитьСписокАдресовДоставки(ЭтаФорма, Контрагент, ВыбранныйГрузополучатель, ДатаДокумента);
	Если Элементы.АдресДоставки.СписокВыбора.Количество() > 0 Тогда
		ВыбранноеЗначение = Элементы.АдресДоставки.СписокВыбора[0].Значение;
		
		АдресДоставкиЗначение = ВыбранноеЗначение;
		АдресДоставки = РеализацияТоваровУслугФормыВызовСервера.ПредставлениеКонтактнойИнформации(ВыбранноеЗначение);
	Иначе
		АдресДоставкиЗначение = "";
		АдресДоставки = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПроверкиЗаполненияНаКлиенте(Отказ)
	
	Если ГрузоотправительОнЖе = 0 Тогда
		Если НЕ ЗначениеЗаполнено(Грузоотправитель) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", НСтр("ru = 'Грузоотправитель'"));
			Поле = "Грузоотправитель";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ГрузополучательОнЖе = 0 Тогда
		Если НЕ ЗначениеЗаполнено(Грузополучатель) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", НСтр("ru = 'Грузополучатель'"));
			Поле = "Грузополучатель";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ПеревозчикОнЖе = 0 Тогда
		Если НЕ ЗначениеЗаполнено(Перевозчик) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", НСтр("ru = 'Перевозчик'"));
			Поле = "Перевозчик";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ГрузополучательОнЖеПриИзмененииНаСервере()

	ОбновитьСписокАдресовДоставки();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВодителяНаСервере(Знач ФизЛицо, Знач Период)
	
	ДанныеВодителя = Новый Структура;
	ДанныеВодителя.Вставить("Водитель", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизЛицо, "Наименование"));
	
	ВодительскоеУдостоверение = Документы.ПутевойЛист.ВодительскоеУдостоверениеФизлица(ФизЛицо, Период);
	
	Если ВодительскоеУдостоверение = Неопределено Тогда
		ДанныеВодителя.Вставить("ВодительскоеУдостоверение", "");
	Иначе
		ДанныеВодителя.Вставить("ВодительскоеУдостоверение", 
			СтрШаблон("%1 %2", ВодительскоеУдостоверение.Серия, ВодительскоеУдостоверение.Номер));
		КонецЕсли;
		
	Возврат ДанныеВодителя;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеАвтомобиляНаСервере(Знач Автомобиль)
	
	ДанныеАвтомобиля = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Автомобиль, "Марка, РегистрационныйЗнак, МаркаПрицепа, РегистрационныйЗнакПрицепа");
		
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("МаркаАвтомобиля",               ДанныеАвтомобиля.Марка);
	СтруктураДанных.Вставить("РегистрационныйЗнакАвтомобиля", ДанныеАвтомобиля.РегистрационныйЗнак);
	СтруктураДанных.Вставить("МаркаПрицепа",                  ДанныеАвтомобиля.МаркаПрицепа);
	СтруктураДанных.Вставить("РегистрационныйЗнакПрицепа",    ДанныеАвтомобиля.РегистрационныйЗнакПрицепа);
	Возврат СтруктураДанных;
	
КонецФункции

&НаСервереБезКонтекста
Функция МаркаАвтоДанныеВыбора(Знач ПараметрыПолученияДанных)
	
	Возврат Справочники.ТранспортныеСредства.ПолучитьДанныеВыбора(ПараметрыПолученияДанных);
	
КонецФункции

&НаСервереБезКонтекста
Функция ВодительДанныеВыбора(Знач ПараметрыПолученияДанных)
	
	Возврат Справочники.ФизическиеЛица.ПолучитьДанныеВыбора(ПараметрыПолученияДанных);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокГруппыПрицеп(Форма)
	
	Элементы = Форма.Элементы;
	Если ЗначениеЗаполнено(Форма.МаркаПрицепа) И ЗначениеЗаполнено(Форма.РегистрационныйЗнакПрицепа) Тогда
		ЗаголовокГруппы = СтрШаблон(НСтр("ru='Прицеп: %1 (%2)'"), Форма.МаркаПрицепа, Форма.РегистрационныйЗнакПрицепа);
	ИначеЕсли ЗначениеЗаполнено(Форма.МаркаПрицепа) Или ЗначениеЗаполнено(Форма.РегистрационныйЗнакПрицепа) Тогда
		ЗаголовокГруппы = СтрШаблон(НСтр("ru='Прицеп: %1%2'"), Форма.МаркаПрицепа, Форма.РегистрационныйЗнакПрицепа);
	Иначе
		ЗаголовокГруппы = НСтр("ru='Прицеп: Без прицепа'");
	КонецЕсли;
	Элементы.ГруппаПрицеп.ЗаголовокСвернутогоОтображения = ЗаголовокГруппы;
	
КонецПроцедуры

&НаСервере
Процедура ВодительОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	ДанныеВодителя = ДанныеВодителяНаСервере(ВыбранноеЗначение, ДатаДокумента);
	Водитель = ДанныеВодителя.Водитель;
	
	ЗаполнитьИнформациюПоВодителю();
	
	Если ЗначениеЗаполнено(ДанныеВодителя.ВодительскоеУдостоверение) 
		И ДанныеВодителя.ВодительскоеУдостоверение <> ВодительскоеУдостоверение Тогда
		// Информацию о водительском удостоверении из карточки физлица считаем более приоритетной.
		ВодительскоеУдостоверение = ДанныеВодителя.ВодительскоеУдостоверение;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюПоВодителю()
	
	Если Не ЗначениеЗаполнено(Водитель) Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяКлючОбъекта(),
		ИмяКлючНастроек(),
		Новый Соответствие);
		
	ДанныеВодителя = Настройки[Водитель];
	Если ДанныеВодителя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеВодителя.Свойство("ВодительскоеУдостоверение") И ЗначениеЗаполнено(ДанныеВодителя.ВодительскоеУдостоверение) Тогда
		ВодительскоеУдостоверение = ДанныеВодителя.ВодительскоеУдостоверение;
	КонецЕсли;
	Если ДанныеВодителя.Свойство("ВодительДолжность") И ЗначениеЗаполнено(ДанныеВодителя.ВодительДолжность) Тогда
		ВодительДолжность = ДанныеВодителя.ВодительДолжность;
	КонецЕсли;
	Если ДанныеВодителя.Свойство("МаркаАвтомобиля") И ЗначениеЗаполнено(ДанныеВодителя.МаркаАвтомобиля) Тогда
		МаркаАвтомобиля = ДанныеВодителя.МаркаАвтомобиля;
	КонецЕсли;
	Если ДанныеВодителя.Свойство("РегистрационныйЗнакАвтомобиля") 
		И ЗначениеЗаполнено(ДанныеВодителя.РегистрационныйЗнакАвтомобиля) Тогда
		РегистрационныйЗнакАвтомобиля = ДанныеВодителя.РегистрационныйЗнакАвтомобиля;
	КонецЕсли;
	Если ДанныеВодителя.Свойство("МаркаПрицепа") И ЗначениеЗаполнено(ДанныеВодителя.МаркаПрицепа) Тогда
		МаркаПрицепа = ДанныеВодителя.МаркаПрицепа;
	КонецЕсли;
	Если ДанныеВодителя.Свойство("РегистрационныйЗнакПрицепа") 
		И ЗначениеЗаполнено(ДанныеВодителя.РегистрационныйЗнакПрицепа) Тогда
		РегистрационныйЗнакПрицепа = ДанныеВодителя.РегистрационныйЗнакПрицепа;
	КонецЕсли;
	УстановитьЗаголовокГруппыПрицеп(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеВодителя()
	
	Если Не ЗначениеЗаполнено(Водитель) Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяКлючОбъекта(),
		ИмяКлючНастроек(),
		Новый Соответствие);
		
	ДанныеВодителя = Новый Структура;
	ДанныеВодителя.Вставить("ВодительскоеУдостоверение",     ВодительскоеУдостоверение);
	ДанныеВодителя.Вставить("МаркаАвтомобиля",               МаркаАвтомобиля);
	ДанныеВодителя.Вставить("РегистрационныйЗнакАвтомобиля", РегистрационныйЗнакАвтомобиля);
	ДанныеВодителя.Вставить("МаркаПрицепа",                  МаркаПрицепа);
	ДанныеВодителя.Вставить("РегистрационныйЗнакПрицепа",    РегистрационныйЗнакПрицепа);
	ДанныеВодителя.Вставить("ВодительДолжность",             ВодительДолжность);
	
	
	Настройки.Вставить(Водитель, ДанныеВодителя);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяКлючОбъекта(),
		ИмяКлючНастроек(),
		Настройки);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяКлючОбъекта()

	Возврат "ТранспортнаяНакладная";

КонецФункции

&НаСервереБезКонтекста
Функция ИмяКлючНастроек()

	Возврат "ДанныеВодителя";

КонецФункции

#КонецОбласти
