
#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;
&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;
&НаКлиенте
Перем ЭтоНоваяСтрока;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РеализацияТоваровУслугФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// Активизировать первую непустую табличную часть
	АктивизироватьТабличнуюЧасть = ОбщегоНазначенияБПВызовСервера.ПолучитьПервуюНепустуюВидимуюТабличнуюЧасть(
		ЭтотОбъект, СписокВидимыхТабличныхЧастей());
	ОбщегоНазначенияБПВызовСервера.АктивизироватьЭлементФормы(ЭтотОбъект, АктивизироватьТабличнуюЧасть);
	
	ВидОперацииПриОткрытии = Объект.ВидОперации;
	
	УстановитьЭлементыФормыПоВидуОперации();
	
	УстановитьВидимостьСчетовУчета();
	
	УстановитьУсловноеОформление();
	
	СсылкаДляПереходаНаКарту = РеализацияТоваровУслугФормы.СтрокаСсылкиПоказатьНаКарте();
	
	УчетНДС.ПрименитьПраваДоступаСчетаФактуры(
		СчетФактура,
		Элементы.СчетФактураПросмотр,
		Элементы.СчетФактураРедактирование); 
		
	Элементы.ЗаявлениеОВвозеТоваровПолученноеПросмотр.Видимость = ПравоДоступа("Просмотр",
		Метаданные.Документы.ЗаявлениеОВвозеТоваровПолученное);
	Элементы.ЗаявлениеОВвозеТоваровПолученноеКнопка.Доступность = ПравоДоступа("Редактирование",
		Метаданные.Документы.ЗаявлениеОВвозеТоваровПолученное);
	
	Элементы.СтатформаПросмотр.Видимость = ПравоДоступа("Просмотр", Метаданные.Документы.РегламентированныйОтчет);
	
	ИспользоватьПодключаемоеОборудование = МенеджерОборудованияБП.ИспользуетсяОборудование("СканерШтрихкода");
	
	ПроверкаРНПТ.ПриСозданииНаСервереДокумент(ЭтотОбъект, Параметры);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	Если УправлениеПечатьюБП.ИспользуетсяИзмененныйМакет(Объект.Ссылка) Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяИзмененныйМакет");
	КонецЕсли;
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.РеализацияТоваровУслуг",
		"ФормаДокументаОбщая",
		НСтр("ru='Новости: Реализация (акт, накладная, УПД)'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	НадписьСчет = НСтр("ru = 'Указывается в табличной части'");
	
	ДополнительныеПараметры = Неопределено;
	СобытияФормИС.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка, ДополнительныеПараметры);
	ШтрихкодированиеИС.ИнициализироватьКэшМаркируемойПродукции(ЭтотОбъект);
	
	Если Параметры.Свойство("ДобавитьНоменклатуру") Тогда
		ТаблицаТовары = Новый ТаблицаЗначений;
		ТаблицаТовары.Колонки.Добавить("Номенклатура");
		
		НоваяСтрока = ТаблицаТовары.Добавить();
		НоваяСтрока.Номенклатура = Параметры.ДобавитьНоменклатуру;
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("АдресПодобраннойНоменклатурыВХранилище", ПоместитьВоВременноеХранилище(ТаблицаТовары));
		ОбработкаВыбораПодборВставкаИзБуфераНаСервере(СтруктураВозврата, "Услуги");
	КонецЕсли; 
	
	УправлениеПанельюПодсказки.ПриСозданииНаСервере(ЭтотОбъект);
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(
			ЭтотОбъект,
			Элементы.ГруппаГлобальныеКоманды);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	ЭлектронноеАктированиеЕИСГИСНР.СоздатьЭлементФормыСтатусаОшибкиГИСНР(ЭтотОбъект, Объект.Ссылка);
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	
	МобильныйКлиентАдаптацияФормы();
	МобильныйКлиентБП.ПроверитьИЗарегистроватьСобытиеДляОценки("просмотрдокументов", ПоказыватьФормуОценкиМК);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
		ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаВыбора" Тогда
		ОбработкаЗаполненияТабличнойЧастиНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
		ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ВыборПорядкаУчетаРасчетов" Тогда
		ОбработкаВыбораПорядокУчетаРасчетовНаСервере(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаПодписиТовары" Тогда
		РеализацияТоваровУслугФормыКлиент.ОбработкаВыбораРеквизитыОрганизацииКонтрагента(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДоставка" Тогда
		РеализацияТоваровУслугФормыКлиент.ОбработкаВыбораРеквизитыОрганизацииКонтрагента(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора);
		
		РеализацияТоваровУслугФормыКлиентСервер.ПроверитьВозможностьОформленияЭПД(ЭтотОбъект);
		
		РеализацияВЕАЭС = РеализацияТоваровУслугФормыВызовСервера.EстьРеализацияВЕАЭС(Объект.СтранаРеализации, Объект.Контрагент);
		
		УправлениеФормой(ЭтотОбъект);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.ОперацияСПатентом.Форма.ФормаДокумента" Тогда
		ЗаполнитьПатентОбработкаВыбора(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.УниверсальныйПередаточныйДокумент" Тогда
		РеализацияТоваровУслугФормыКлиент.ОбработкаВыбораУниверсальныйПередаточныйДокумент(ЭтотОбъект, ВыбранноеЗначение);
		УправлениеФормой(ЭтотОбъект);
	
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.НомераГТД.Форма.ФормаПодбораРНПТ" Тогда
		ОбработкаВыбораРНПТНаКлиенте(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.НомераГТД.Форма.ФормаВыбораПоОстаткам" Тогда
		ОбработкаВыбораГТДНаКлиенте(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаРедактированияНалоговойБазыНДС" Тогда
		ОбработкаВыбораАналитикиНДСНаКлиенте(ВыбранноеЗначение);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_СчетФактураВыданный"
		И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ДокументыОснования") И Параметр.ДокументыОснования.Найти(Объект.Ссылка) <> Неопределено Тогда
		РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьТекстПроСчетФактуру(ЭтотОбъект, Параметр.РеквизитыСФ);
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "Запись_ЗаявлениеОВвозеТоваровПолученное" Тогда
		
		Если Не ЗначениеЗаполнено(ЗаявлениеОВвозеТоваровПолученное)
			И ЗначениеЗаполнено(Параметр)
			И Параметр.Основания.Найти(Объект.Ссылка) <> Неопределено Тогда
			
			ЗаявлениеОВвозеТоваровПолученное = Параметр.ЗаявлениеОВвозеТоваровПолученное;
			
		КонецЕсли;
		
		УчетНДСКлиентСервер.ЗаполнитьРеквизитыФормыПроЗаявлениеОВвозеТоваровПолученное(ЭтотОбъект, ЗаявлениеОВвозеТоваровПолученное);
		УправлениеФормой(ЭтотОбъект);
		
	ИначеЕсли ИмяСобытия = "ОбработанаТабличнаяЧастьТовары" И ТипЗнч(Параметр) = Тип("Структура") 
		И Параметр.Свойство("ИдентификаторВызывающейФормы")
		И Параметр.ИдентификаторВызывающейФормы = УникальныйИдентификатор Тогда
		ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметр);
	ИначеЕсли ИмяСобытия = "ИзмененСтатусДокументов" Тогда
		// Статус документа изменен в форме списка
		Если Не Объект.Ссылка.Пустая() И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
			СтатусДокумента = ПолучитьСтатусДокумента(Объект.Организация, Объект.Ссылка);
			УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(
				ЭтотОбъект, СтатусДокумента);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" И Параметр.Владелец = Объект.Организация Тогда
		
		ОбработатьоповещениеОбИзмененииБанковскогоСчетаИлиОрганизации();
		
	ИначеЕсли ИмяСобытия = "Запись_Организации" И Источник = Объект.Организация Тогда
		
		ОбработатьоповещениеОбИзмененииБанковскогоСчетаИлиОрганизации();
		
	ИначеЕсли ИмяСобытия = "Запись_ДоговорыКонтрагентов" И Объект.ДоговорКонтрагента = Источник Тогда
		
		ТребуетсяПерерасчитатьСрокОплаты =
			ИспользуетсяПланированиеПлатежей И АвторасчетСрокаОплаты;
			
		Если ТребуетсяПерерасчитатьСрокОплаты Тогда
			ОбновитьСрокОплатыНаСервере();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_Контрагенты" И (Объект.Контрагент = Источник ИЛИ Объект.Грузополучатель = Источник) Тогда
		
		ОбновитьСписокАдресовДоставкиНаСервере();

	ИначеЕсли Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			СтруктураПараметров = Новый Структура;
			
			Если Параметр[1] = Неопределено Тогда
				Штрихкод = Параметр[0]
			Иначе
				Штрихкод = Параметр[1][1];
			КонецЕсли;
			ШтрихкодВBase64 = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(Штрихкод);
			ДобавитьПоШтрихкодуНаСервере(ШтрихкодВBase64);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ДанныеСкопированыВБуферОбмена" Тогда
		
		УстановитьДоступностьКомандыВставки(ЭтотОбъект, Истина);
	ИначеЕсли ИмяСобытия = "Запись_УведомлениеОПеремещенииПрослеживаемыхТоваров" 
		И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("СопроводительныеДокументы")
		И Параметр.СопроводительныеДокументы.Найти(Объект.Ссылка) <> Неопределено Тогда
		
		УстановитьПредставлениеНадписиУведомлениеОПеремещении();
		ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);
		
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	ИначеЕсли ИмяСобытия = ЭлектронноеАктированиеЕИСКлиентСервер.ИмяСобытияЗаписьГосконтрактаЕИС()
			И ТипЗнч(Параметр) = Тип("Структура") 
			И Параметр.Свойство("Ссылка")
			И Параметр.Ссылка = ЭтотОбъект.ГосконтрактЕИС Тогда
			// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
			ОбработатьИзменениеГосконтрактаЕИС();
			// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
			
	ИначеЕсли ИмяСобытия = "Запись_ИспользоватьНумерациюДоговоровСПокупателями" Тогда
		
		УстановитьПредлагатьНовыйДоговорСНумерацией();
		
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант" И Источник = "ЗаполнятьОтветственногоПоПродажамИзКонтрагента" Тогда
		ОбработатьОповещениеОбИзмененииНастроекОтветственного();
		
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если УчетОригиналовПервичныхДокументовБПКлиент.ЭтоСобытиеИзмененияСостоянияОриганалаПервичногоДокумента(
			Объект.Ссылка, ИмяСобытия, Источник) Тогда
		СтатусДокумента = ПолучитьСтатусДокумента(Объект.Организация, Объект.Ссылка);
		УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(ЭтотОбъект, СтатусДокумента);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
	
	ДополнительныеПараметры = Неопределено;
	СобытияФормИСКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник, ДополнительныеПараметры);
	
	УправлениеПанельюПодсказкиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	Если РеализацияТоваровУслугФормыКлиент.ОбрабатыватьОповещениеНаСервере(ИмяСобытия) Тогда
		ОбработкаОповещенияНаСервере();
	КонецЕсли;
	
	Если ИмяСобытия = ЭлектронноеАктированиеЕИСГИСНРКлиент.ИмяОповещенияОбОтсутствииОшибки() Тогда
		ЭлектронноеАктированиеЕИСГИСНРКлиент.УстановитьВидимостьЭлементаФормыСтатусаОшибкиГИСНР(ЭтотОбъект, Параметр);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ТолькоПросмотр И ИспользоватьПодключаемоеОборудование Тогда
		// Попробуем подключить сканер штрихкода
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	КонецЕсли;
	
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// Подсистема "ОбменСКонтрагентами"
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец Подсистема "ОбменСКонтрагентами"
	
	ПроверкаРНПТКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);
	
	ОбменСМаркетплейсКлиент.ПодключитьОбработчикСопоставленияНоменклатуры(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	РеализацияТоваровУслугФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	СобытияФормИС.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
		
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ПриЧтенииФормыДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
	ОбменСМаркетплейс.ПодготовитьСопоставлениеНоменклатурыПоДокументу(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	// Возможно было изменение вида операции в ПередЗаписьюНаСервере() на прошлом этапе записи текущего документа из формы. 
	// Поэтому чтобы в модуле объекта в ОбработкаПроверкиЗаполнения() проверка выдавала сообщения
	// относительно общей формы, установим вид операции, который был при открытии 
	// (гарантированно соответствующий общей форме)
	// Если проверки пройдут, то в ПередЗаписьюНаСервере() будет определен рекомендуемый вид операции снова.
	Объект.ВидОперации = ВидОперацииПриОткрытии;
	
	РеализацияТоваровУслугФормыКлиент.ПередЗаписью(
		ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	РеализацияТоваровУслугФормы.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	РеализацияТоваровУслугФормы.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
	УчетНДСКлиентСервер.ОтобразитьПолеУПДВыданный(ЭтотОбъект);
	
	УстановитьСостояниеДокумента();
	
	Если ВедетсяУчетНДСПоФЗ335
		И ПокупательНалоговыйАгентПоНДС = Истина Тогда 
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПроверкаРНПТ.ПослеЗаписиНаСервереДокумент(ЭтотОбъект);
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
	РаботаСДоговорамиКонтрагентовБП.СохранитьСпособВыставленияДокументов(
		ТекущийОбъект.ДоговорКонтрагента,
		ТекущийОбъект.ЭтоУниверсальныйДокумент);
	
	СобытияФормИС.ПослеЗаписиНаСервере(ЭтотОбъект);
	
	УстановитьВидимостьПроверкиРНПТ();
	
	ОбменСМаркетплейс.УдалитьПредупреждениеДокумента(ТекущийОбъект.Ссылка, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РекомендуемыйВидОперации = Документы.РеализацияТоваровУслуг.РекомендуемыйВидОперации(Объект);
	Если ЗначениеЗаполнено(РекомендуемыйВидОперации) Тогда
		ТекущийОбъект.ВидОперации = РекомендуемыйВидОперации;
	Иначе
		// Если рекомендации нет, то оставляем вид операции, который был при открытии.
		ТекущийОбъект.ВидОперации = ВидОперацииПриОткрытии;
	КонецЕсли;
	
	РеализацияТоваровУслугФормы.ПередЗаписьНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПроверкаРеквизитовОрганизации.ПередЗаписьюНаСервере(
		Объект.Организация,
		Объект.БанковскийСчетОрганизации,
		ИспользоватьНесколькоБанковскихСчетовОрганизации,
		Отказ,
		ПараметрыЗаписи,
		Ложь);
		
	ТекущийОбъект.ДополнительныеСвойства.Вставить("СтатусДокумента",      СтатусДокумента);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ТребуетсяСчетФактура", ТребуетсяСчетФактура И НЕ Объект.ДокументБезНДС);
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЕстьСчетФактура",      ЗначениеЗаполнено(СчетФактура));
	ТекущийОбъект.ДополнительныеСвойства.Вставить("НомерСчетаФактуры",    НомерСчетаФактуры);
	
	КоличествоРНПТДоАвтоподбора = Объект.СведенияПрослеживаемости.Итог("Количество");
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	РеализацияТоваровУслугФормыКлиент.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	СобытияФормИСКлиент.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
	Если ПараметрыЗаписи.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		РаботаСНоменклатуройКлиентБП.ПослеЗаписи_ФормаДокумента();
	КонецЕсли;
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтотОбъект, ПараметрыЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
	ЗапускатьПроверкуРНПТ = Объект.СведенияПрослеживаемости.Итог("Количество") <> КоличествоРНПТДоАвтоподбора;
	Если ЗапускатьПроверкуРНПТ Тогда
		ПроверкаРНПТКлиент.ЗапуститьПроверкуРНПТВДокументе(ЭтотОбъект);
	Иначе
		ПроверкаРНПТКлиент.СохранитьРезультатПроверкиРНПТПослеЗаписиДокумента(ЭтотОбъект);
	КонецЕсли;
	
	ОбменСМаркетплейсКлиент.ОповеститьОбИзмененииСтатусаПредупрежденияАвтозагрузкиМаркетплейса(
		Объект.Ссылка, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РеализацияТоваровУслугФормы.ПриЗаписиНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ИспользоватьПодключаемоеОборудование И ЗавершениеРаботы Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
	Если ПоказыватьФормуОценкиМК Тогда
		ОткрытьФорму("ОбщаяФорма.ОценитьПриложение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормИСКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
	НастройкиНалоговИОтчетовПредупреждениеФормыКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	РеализацияТоваровУслугФормы.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийШапка

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = РеализацияТоваровУслугФормыКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(ЭтотОбъект);
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами	
	
	ОбновитьСрокОтправкиУведомлений = Истина;
	ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		СкладПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерНаПечатьПриИзменении(Элемент)
	
	НомерНаПечатьПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении()

	КонтрагентПриИзмененииНаСервере();
		
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.Контрагент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	ЭлектронноеАктированиеБПКлиент.ДоговорКонтрагентаПриИзменении(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	
	ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораКонтрагенты");	

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ДоговорКонтрагентаПриИзмененииНаСервере();
	
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	ЭлектронноеАктированиеБПКлиент.ДоговорКонтрагентаПриИзменении(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(
		Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, ПредлагатьНовыйДоговор, СтандартнаяОбработка, ПредлагатьНовыйДоговорСНумерацией);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Истина;
	ОбщегоНазначенияБПКлиент.ОткрытьДоговорКонтрагента(ЭтотОбъект, Элемент, Объект.ДоговорКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(Объект.ДоговорКонтрагента),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПорядокУчетаРасчетовНажатие(Элемент, СтандартнаяОбработка)

	АдресХранилищаЗачетАвансов = ПоместитьЗачетАвансовВоВременноеХранилищеНаСервере();

	РеализацияТоваровУслугФормыКлиент.ПорядокУчетаРасчетовНажатие(
		ЭтотОбъект, Элемент, СтандартнаяОбработка, АдресХранилищаЗачетАвансов);

КонецПроцедуры

&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	РеализацияТоваровУслугФормыКлиент.ЦеныИВалютаНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетНаОплатуПокупателюПриИзменении(Элемент)

	Если НЕ ЗначениеЗаполнено(Объект.СчетНаОплатуПокупателю) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДокументПоСчету();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетНаОплатуПокупателюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОповещениеОВыборе = Новый ОписаниеОповещения("ВыборСчетаНаОплатуЗавершение", ЭтотОбъект, 
		Новый Структура("ИмяТабличнойЧасти", "Товары,Услуги,ВозвратнаяТара"));
		
	РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораСчетаНаОплату(ЭтотОбъект, Истина, ОповещениеОВыборе);
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОбновитьУсловноеОформление(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура НадписьСчетФактураНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	БухгалтерскийУчетКлиентПереопределяемый.ОткрытьСчетФактуру(ЭтотОбъект, СчетФактура, "СчетФактураВыданный");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветственныйПриИзменении(Элемент)
	
	ОтветственныйПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДОНажатие(Элемент, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПроверкаРеквизитовОрганизацииКлиент.РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Объект.Организация, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеятельностьНаПатентеПриИзменении(Элемент)
	
	ДеятельностьНаПатентеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеятельностьНаТорговомСбореПриИзменении(Элемент)
	
	Если Объект.ДеятельностьНаТорговомСборе И Объект.ДеятельностьНаПатенте Тогда
		Объект.ДеятельностьНаПатенте = Ложь;
		ДеятельностьНаТорговомСбореПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПатентПредставлениеПриИзменении(Элемент)
	
	УчетПСНКлиент.ПатентПриИзменении(ЭтотОбъект, Объект.Патент, ПатентПредставление);
	
	РеализацияТоваровУслугФормыКлиент.ПатентПриИзменении(ЭтотОбъект, Элемент);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПатентПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УчетПСНКлиент.ПатентОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УПДПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, ЭтотОбъект.Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУПДНажатие(Элемент, СтандартнаяОбработка)
	
	УчетНДСКлиент.ОткрытьФормуРеквизитовУПДВыданный(ЭтотОбъект, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СсылкаДляПереходаНаКартуНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, Объект.АдресДоставки);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиПриИзменении(Элемент)
	РеализацияТоваровУслугФормыВызовСервера.ПроверитьАдрес(Объект.АдресДоставки, Объект.АдресДоставкиЗначение);
	Элементы.АдресДоставкиПереходНаКарту.Доступность = ЗначениеЗаполнено(Объект.АдресДоставки);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.АдресДоставкиОбработкаВыбора(Объект, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.АдресДоставкиНачалоВыбора(Объект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СпособДоставкиПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.СпособДоставки) Тогда
		Реквизиты = РеквизитыУсловияДоставки(Объект.СпособДоставки);
		
		ЕстьДоставка             = (Реквизиты.ИмяПредопределенныхДанных <> "Самовывоз");
		Если ЕстьДоставка Тогда
			ЕстьНомерДляОтслеживания = Реквизиты.ИспользоватьНомерДляОтслеживания;
			Объект.Перевозчик        = Реквизиты.Контрагент;
		Иначе
			ЕстьНомерДляОтслеживания = Ложь;
			Объект.Перевозчик        = Неопределено;
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Реквизиты.УслугаДоставки) 
			И Объект.Услуги.НайтиСтроки(Новый Структура("Номенклатура", Реквизиты.УслугаДоставки)).Количество() = 0 Тогда
			
			ОповещениеОЗавершении = Новый ОписаниеОповещения("УсловияДоставкиЗавершение", ЭтотОбъект, Реквизиты);
			ТекстПредупреждения   = НСтр("ru = 'Добавить услугу по доставке?'");
		
			ПоказатьВопрос(ОповещениеОЗавершении, ТекстПредупреждения, РежимДиалогаВопрос.ДаНетОтмена);
		КонецЕсли; 
	Иначе
		ЕстьДоставка             = Ложь;
		ЕстьНомерДляОтслеживания = Ложь;
	КонецЕсли; 
	
	РеализацияТоваровУслугФормыКлиентСервер.ПроверитьВозможностьОформленияЭПД(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьБаннерУведомленийОПеремещении = Истина;
	ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьУведомлениеОПеремещенииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если Модифицированность И Не ЗначениеЗаполнено(УведомлениеОПеремещении) Тогда
		Записать();
	КонецЕсли;
	
	ПрослеживаемостьФормыКлиентБП.СоздатьПоказатьУведомления(ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭТрННажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	РеализацияТоваровУслугФормыКлиент.СостояниеЭТрННажатие(ЭтотОбъект);
КонецПроцедуры


#Область Панель_Подсказки

&НаКлиенте
Процедура СоветПоРаботе1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура НадписьЗаявленияОВвозеТоваровПолученногоНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	БухгалтерскийУчетКлиентПереопределяемый.ОткрытьЗаявлениеОВвозеТоваровПолученное(ЭтотОбъект, ЗаявлениеОВвозеТоваровПолученное);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийТаблицаТовары

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	ОбновитьИтоги(ЭтотОбъект);
	ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыСчетНаОплату(ЭтотОбъект);
	РеализацияТоваровУслугФормыКлиентСервер.УстановитьРеквизитыОтчетностиЭкспортеров(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущиеДанные.СтавкаНДС = СтавкаНДСПриРеализации;
	КонецЕсли;
	
	ЭтоНоваяСтрока = НоваяСтрока;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПроверкаРНПТКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	
	Если Копирование ИЛИ НоваяСтрока Тогда
		Элемент.ТекущиеДанные.СостояниеПроверкиРНПТ = 4;
		Если Копирование Тогда
			Элемент.ТекущиеДанные.Себестоимость = 0;
			Элемент.ТекущиеДанные.ДокументОприходования = Неопределено;
			Если ИспользоватьКонтрольныеЗнакиГИСМ Тогда
				Элемент.ТекущиеДанные.КиЗ_ГИСМ = Неопределено;
			КонецЕсли;
			Элемент.ТекущиеДанные.ИдентификаторСтроки = "";
			Если Элемент.ТекущиеДанные.ПрослеживаемыйТовар Тогда
				Элемент.ТекущиеДанные.РНПТ = "";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 
	
	Элемент.ТекущиеДанные.АналитикаНДС = НСтр("ru=''");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если ВестиУчетМаркируемойПродукцииИСМП Тогда
		ТоварыПриОкончанииРедактированияНаСервере();
	КонецЕсли;
	
	ПроверкаРНПТКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	
	ЭтоНоваяСтрока = Ложь;
	ЗаполнитьДобавленныеКолонкиТаблиц();
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПриОкончанииРедактированияНаСервере()

	ДополнительныеПараметры = Неопределено;
	СобытияФормИС.ПриИзмененииЭлемента(ЭтотОбъект, "Товары", ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТоварыНоменклатураПриИзмененииНаКлиенте();
	РеализацияТоваровУслугФормыКлиентСервер.УстановитьРеквизитыОтчетностиЭкспортеров(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораНоменклатура");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Товары, ТоварыНаКомиссии");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Товары, ТоварыНаКомиссии");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ЭлектронноеАктированиеБПКлиент.НоменклатураОбработкаВыбора(ТекущиеДанные, ВыбранноеЗначение, СтандартнаяОбработка);
	Если ЭлектронноеАктированиеБПКлиент.ЭтоПодборНоменклатурыЕИС(ВыбранноеЗначение) Тогда
		// При подборе номенклатуры из госконтракта ЕИС стандартная обработка отключается,
		// необходим программный вызов события ПриИзменении. 
		ТоварыНоменклатураПриИзмененииНаКлиенте();
		Элементы.Товары.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Товары");
	
	Если ЕстьУчетПодакцизныхТоваров Тогда
		УчетАкцизовКлиентСервер.ПриИзмененииНоменклатурыКоличества(ЭтотОбъект, "Товары", ЭкспортПриРеализации, Ложь, ЭтоКомиссия);
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено 
		И ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторСтроки) Тогда
		// Необходимо очистить дополнительные сведения
		НоваяСтрока = ТаблицаУдаленныхСтрок.Добавить();
		НоваяСтрока.ИдентификаторСтроки = ТекущиеДанные.ИдентификаторСтроки;
	КонецЕсли;
	
	ВызовУдалитьСвязанныеЗаписи = "Количество";
	
	ПодключитьОбработчикОжидания("Подключаемый_УдалитьСвязанныеЗаписи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Товары");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "Товары");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСтавкаНДС(ЭтотОбъект, "Товары");
	РеализацияТоваровУслугФормыКлиентСервер.УстановитьРеквизитыОтчетностиЭкспортеров(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаНДС(ЭтотОбъект, "Товары");

КонецПроцедуры

&НаКлиенте
Процедура ТоварыАналитикаНДСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ВыбраннаяСтрока = Элементы.Товары.ТекущаяСтрока;
	ОткрытьФормуРедактированияНДС(ВыбраннаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСчетУчетаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СвойстваСчетаУчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные.СчетУчета);
	ТекущиеДанные.СчетУчетаЗабалансовый = СвойстваСчетаУчета.Забалансовый;
	ТекущиеДанные.СчетУчетаПартионный = СвойстваСчетаУчета.ВидСубконто1 = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии")
		ИЛИ СвойстваСчетаУчета.ВидСубконто2 = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии")
		ИЛИ СвойстваСчетаУчета.ВидСубконто3 = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии");
	
	Если СпособОценкиМПЗПоСредней И НЕ (ТекущиеДанные.СчетУчетаЗабалансовый И ТекущиеДанные.СчетУчетаПартионный) Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.ДокументОприходования) Тогда
			ТекущиеДанные.Себестоимость = 0;
			ТекущиеДанные.ДокументОприходования = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьУчетПодакцизныхТоваров Тогда
		
		УчетАкцизовКлиентСервер.ПриИзмененииНоменклатурыКоличества(ЭтотОбъект, "Товары", ЭкспортПриРеализации, Ложь, ЭтоКомиссия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСчетДоходовПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ТекущиеДанные.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам	=
		БухгалтерскийУчетВызовСервераПовтИсп.НаСчетеВедетсяУчетПоНоменклатурнымГруппам(ТекущиеДанные.СчетДоходов);
		
	Если (НЕ ТекущиеДанные.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам)
		И БухгалтерскийУчетВызовСервераПовтИсп.НаСчетеВедетсяУчетПоПрочимДоходамИРасходам(ТекущиеДанные.СчетДоходов) Тогда
		
		ТекущиеДанные.Субконто 	= ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПрочиеДоходыИРасходы.РеализацияПрочегоИмущества");
		
	ИначеЕсли ИспользоватьОднуНоменклатурнуюГруппу И ТекущиеДанные.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам Тогда
		
		ТекущиеДанные.Субконто 	= ОсновнаяНоменклатурнаяГруппа;
		
	КонецЕсли;
	
	ОтобразитьСубконтоСчетаДоходовТовары = ОтобразитьСубконтоСчетаДоходовТовары ИЛИ НЕ ТекущиеДанные.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументОприходованияПриИзменении(Элемент)

	РеализацияТоваровУслугФормыКлиент.ТоварыДокументОприходованияПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументОприходованияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РеализацияТоваровУслугФормыКлиент.ТоварыДокументОприходованияНачалоВыбора(
		ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСчетНаОплатуПокупателюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюНачалоВыбора(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСчетНаОплатуПокупателюОткрытие(Элемент, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСчетНаОплатуПокупателюОчистка(Элемент, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюОчистка(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНомерГТДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РеализацияТоваровУслугФормыКлиент.ТоварыНомерГТДНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыРНПТНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	ПараметрыФормыПодбора = ПрослеживаемостьФормыКлиентБП.ПодготовитьСтруктуруПараметровДляФормыПодбораРНПТ();
	ПараметрыФормыПодбора.ЭтоПоступление         = Ложь;
	ПараметрыФормыПодбора.ПрослеживаемыйКомплект = ТекущиеДанные.ПрослеживаемыйКомплект;
	ПрослеживаемостьФормыКлиентБП.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Товары", ПараметрыФормыПодбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыРНПТОчистка(Элемент, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		
		Если СтрокаТабличнойЧасти.ПрослеживаемыйТовар Тогда
			СтрокиСРНПТ = Объект.СведенияПрослеживаемости.НайтиСтроки(Новый Структура("ИдентификаторСтроки", СтрокаТабличнойЧасти.ИдентификаторСтроки));
			
			Для Каждого СтрокаСРНПТ Из СтрокиСРНПТ Цикл
				Объект.СведенияПрослеживаемости.Удалить(СтрокаСРНПТ);
			КонецЦикла;
		Иначе
			СтрокаТабличнойЧасти.НомерГТД = Неопределено;
		КонецЕсли;
		
		ПрослеживаемостьФормыКлиентСерверБП.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокаТабличнойЧасти);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтранаПроисхожденияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторСтроки) Тогда
			// Необходимо очистить дополнительные сведения
			НоваяСтрока = ТаблицаУдаленныхСтрок.Добавить();
			НоваяСтрока.ИдентификаторСтроки = ТекущиеДанные.ИдентификаторСтроки;
		КонецЕсли;
		ПрослеживаемостьФормыКлиентСерверБП.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, ТекущиеДанные);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_УдалитьСвязанныеЗаписи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТоварыРНПТ" Тогда
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		Если ТекущиеДанные.ПрослеживаемыйТовар Тогда
			ПараметрыФормыПодбора = ПрослеживаемостьФормыКлиентБП.ПодготовитьСтруктуруПараметровДляФормыПодбораРНПТ();
			ПараметрыФормыПодбора.ЭтоПоступление         = Ложь;
			ПараметрыФормыПодбора.ПрослеживаемыйКомплект = ТекущиеДанные.ПрослеживаемыйКомплект;
			
			ПрослеживаемостьФормыКлиентБП.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Товары",ПараметрыФормыПодбора);
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Поле.Имя = "ТоварыПрослеживаемыйТовар" Тогда
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		Если ТекущиеДанные.ПрослеживаемыйТовар Тогда
			ПрослеживаемостьФормыКлиентБП.ПоказатьПредупреждениеПрослеживаемыйТовар();
		Конецесли;
	КонецЕсли;
	
	Если Поле.Имя = "ТоварыАналитикаНДС" Тогда
		ОткрытьФормуРедактированияНДС(ВыбраннаяСтрока);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	ВыделенныеСтроки = Элементы.Товары.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТовары Из ВыделенныеСтроки Цикл
		ТекДанные = Объект.Товары.НайтиПоИдентификатору(СтрокаТовары);
		Если ТекДанные <> Неопределено
			И ЗначениеЗаполнено(ТекДанные.ИдентификаторСтроки) Тогда
			// Необходимо очистить дополнительные сведения
			НоваяСтрока = ТаблицаУдаленныхСтрок.Добавить();
			НоваяСтрока.ИдентификаторСтроки = ТекДанные.ИдентификаторСтроки;
		КонецЕсли;
	КонецЦикла;
	
	ПодключитьОбработчикОжидания("Подключаемый_УдалитьСвязанныеЗаписи", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	Если Элементы.Товары.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоНоваяСтрока Тогда
		
		ТекущаяКолонкаТаблицы = Элементы.Товары.ТекущийЭлемент;
		
		Если ТекущаяКолонкаТаблицы.Имя = "ТоварыРНПТ" 
			И Не ЗначениеЗаполнено(Элементы.Товары.ТекущиеДанные.РНПТ) Тогда
			ПараметрыФормыПодбора = ПрослеживаемостьФормыКлиентБП.ПодготовитьСтруктуруПараметровДляФормыПодбораРНПТ();
			ПараметрыФормыПодбора.ЭтоПоступление         = Ложь;
			ПараметрыФормыПодбора.ПрослеживаемыйКомплект = Элементы.Товары.ТекущиеДанные.ПрослеживаемыйКомплект;
			ПрослеживаемостьФормыКлиентБП.ОткрытьФормуПодбораРНПТ(ЭтаФорма, "Товары", ПараметрыФормыПодбора);
		КонецЕсли;
		
		Если ТекущаяКолонкаТаблицы.Имя = "ТоварыАналитикаНДС" Тогда
			ВыбраннаяСтрока = Элементы.Товары.ТекущаяСтрока;
			ОткрытьФормуРедактированияНДС(ВыбраннаяСтрока);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицаВозвратнаяТара

&НаКлиенте
Процедура ВозвратнаяТараПриИзменении(Элемент)

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратнаяТара.ТекущиеДанные;
	
	ДанныеСтрокиТаблицы = Новый Структура("Номенклатура, Количество, Цена, Сумма");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ДанныеОбъекта = Новый Структура("Ссылка, Дата, ВидОперации, Организация, ДеятельностьНаПатенте,
		|Склад, ТипЦен, ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов,
		|СуммаВключаетНДС, ДоговорКонтрагента,
		|ЭтоКомиссия, Реализация, ДокументБезНДС");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	ДанныеОбъекта.СуммаВключаетНДС = Истина;
	ДанныеОбъекта.ЭтоКомиссия      = ЭтоКомиссия;
	ДанныеОбъекта.Реализация       = Истина;
	
	ПараметрыЗаполненияСчетовУчета = РеализацияТоваровУслугФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"ВозвратнаяТара.Номенклатура",
		Объект,
		ТекущиеДанные,
		ДанныеОбъекта,
		ДанныеСтрокиТаблицы);
		
	ВозвратнаяТараНоменклатураПриИзмененииНаСервере(ДанныеСтрокиТаблицы, ДанныеОбъекта, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "ВозвратнаяТара");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "ВозвратнаяТара");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараКоличествоПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "ВозвратнаяТара");

КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараЦенаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "ВозвратнаяТара");

КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараСуммаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "ВозвратнаяТара");

КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийТаблицаУслуги

&НаКлиенте
Процедура УслугиПослеУдаления(Элемент)
	РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыСчетНаОплату(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущиеДанные.СтавкаНДС = СтавкаНДСПриРеализации;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	УслугиНоменклатураПриИзмененииНаКлиенте(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораНоменклатура");

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	ЭлектронноеАктированиеБПКлиент.НоменклатураОбработкаВыбора(ТекущиеДанные, ВыбранноеЗначение, СтандартнаяОбработка);
	Если ЭлектронноеАктированиеБПКлиент.ЭтоПодборНоменклатурыЕИС(ВыбранноеЗначение) Тогда
		// При подборе номенклатуры из госконтракта ЕИС стандартная обработка отключается,
		// необходим программный вызов события ПриИзменении.
		УслугиНоменклатураПриИзмененииНаКлиенте(ТекущиеДанные);
		Элементы.Услуги.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСчетНаОплатуПокупателюПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюНачалоВыбора(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСодержаниеПриИзменении(Элемент)
	
	УслугиСодержаниеПриИзмененииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Услуги", 1);

КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Услуги", 1);

КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "Услуги", 1);

КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСтавкаНДС(ЭтотОбъект, "Услуги");

КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаНДСПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаНДС(ЭтотОбъект, "Услуги");

КонецПроцедуры

&НаКлиенте
Процедура УслугиСчетДоходовПриИзменении(Элемент)
	
	СтрокаТаблицы	= Элементы.Услуги.ТекущиеДанные;
	СтрокаТаблицы.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам	=
		БухгалтерскийУчетВызовСервераПовтИсп.НаСчетеВедетсяУчетПоНоменклатурнымГруппам(СтрокаТаблицы.СчетДоходов);
		
	Если ИспользоватьОднуНоменклатурнуюГруппу И СтрокаТаблицы.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам Тогда
		
		СтрокаТаблицы.Субконто 	= ОсновнаяНоменклатурнаяГруппа;
		
	КонецЕсли;
	
	ОтобразитьСубконтоСчетаДоходовУслуги = ОтобразитьСубконтоСчетаДоходовУслуги ИЛИ НЕ СтрокаТаблицы.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам;

КонецПроцедуры

&НаКлиенте
Процедура УслугиСчетНаОплатуПокупателюПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСчетНаОплатуПокупателюОчистка(Элемент, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюОчистка(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСодержаниеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	ЭлектронноеАктированиеБПКлиент.НоменклатураОбработкаВыбора(ТекущиеДанные, ВыбранноеЗначение, СтандартнаяОбработка);
	Если ЭлектронноеАктированиеБПКлиент.ЭтоПодборНоменклатурыЕИС(ВыбранноеЗначение) Тогда
		// При подборе содержания услуги из госконтракта ЕИС стандартная обработка отключается,
		// необходим программный вызов события ПриИзменении. 
		УслугиСодержаниеПриИзмененииНаКлиенте();
		Элементы.Услуги.ЗакончитьРедактированиеСтроки(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийТаблицаАгентскиеУслуги

&НаКлиенте
Процедура АгентскиеУслугиПослеУдаления(Элемент)
	РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыСчетНаОплату(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиПриИзменении(Элемент)

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущиеДанные.СтавкаНДС = СтавкаНДСПриРеализации;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.АгентскиеУслуги.ТекущиеДанные;
	
	ДанныеСтрокиТаблицы = Новый Структура("Номенклатура, Содержание, Количество,
		|Цена, Сумма, СтавкаНДС, СуммаНДС, СчетНаОплатуПокупателю, СчетНаОплатуПокупателюПредставление,
		|Всего");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ДанныеОбъекта = Новый Структура("Дата, ВидОперации, Организация, ДеятельностьНаПатенте,
		|Склад, ТипЦен, ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов,
		|СуммаВключаетНДС, ДоговорКонтрагента,
		|ЭтоКомиссия, Реализация, ДокументБезНДС");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	ДанныеОбъекта.ЭтоКомиссия = ЭтоКомиссия;
	ДанныеОбъекта.Реализация  = Истина;
	
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ДанныеОбъекта);
	
	АгентскиеУслугиНоменклатураПриИзмененииНаСервере(ДанныеСтрокиТаблицы, ДанныеОбъекта);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораНоменклатура");

КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиСодержаниеПриИзменении(Элемент)
	ТекущиеДанные = Элементы.АгентскиеУслуги.ТекущиеДанные;
	РаботаСНоменклатуройКлиентБП.СодержаниеПриИзменении(ТекущиеДанные, ТекущаяДатаДокумента);
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиКоличествоПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "АгентскиеУслуги", 1);

КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиЦенаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "АгентскиеУслуги", 1);

КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиСуммаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "АгентскиеУслуги", 1);

КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиСтавкаНДСПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСтавкаНДС(ЭтотОбъект, "АгентскиеУслуги");

КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиСуммаНДСПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаНДС(ЭтотОбъект, "АгентскиеУслуги");

КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиКонтрагентПриИзменении(Элемент)

	СтрокаТаблицы = Элементы.АгентскиеУслуги.ТекущиеДанные;

	// Подготовим данные для передачи на сервер
	ДанныеОбъекта = Новый Структура("Организация, Дата, ДокументБезНДС");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);

	ДанныеСтроки = Новый Структура("Контрагент, ДоговорКонтрагента");
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, СтрокаТаблицы);

	ПричиныИзменения = Новый Массив;
	ПричиныИзменения.Добавить("АгентскиеУслуги.Контрагент");
	ПричиныИзменения.Добавить("АгентскиеУслуги.ДоговорКонтрагента"); // Договор может измениться в ходе серверного вызова
	
	ПараметрыЗаполненияСчетовУчета = РеализацияТоваровУслугФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		ПричиныИзменения,
		Объект,
		СтрокаТаблицы,
		ДанныеОбъекта,
		ДанныеСтроки);
		
	// Заполним
	Изменения = АгентскиеУслугиКонтрагентПриИзмененииНаСервере(ДанныеОбъекта, ДанныеСтроки, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Изменения);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.АгентскиеУслуги);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиКонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиКонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиДоговорКонтрагентаПриИзменении(Элемент)

	ПараметрыЗаполненияСчетовУчета = РеализацияТоваровУслугФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"АгентскиеУслуги.ДоговорКонтрагента",
		Объект,
		Элементы.АгентскиеУслуги.ТекущиеДанные);
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);

КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Истина;
	ОбщегоНазначенияБПКлиент.ОткрытьДоговорКонтрагента(ЭтотОбъект, Элемент, Элементы.АгентскиеУслуги.ТекущиеДанные.ДоговорКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ТаблицаФормыВыбор(ЭтотОбъект, Элемент, Поле);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиСчетНаОплатуПокупателюПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюНачалоВыбора(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиСчетНаОплатуПокупателюПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиСчетНаОплатуПокупателюОчистка(Элемент, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюОчистка(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура АгентскиеУслугиКонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры


#КонецОбласти 

#Область ОбработчикиКомандФормы
	
&НаКлиенте
Процедура ДобавитьИзПоступления(Команда)

	РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораПоступленияТоваровУслуг(ЭтотОбъект, "Товары", "Добавить");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПоступлению(Команда)

	РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораПоступленияТоваровУслуг(ЭтотОбъект, "Товары", "Заполнить");

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзПоступленияТара(Команда)

	РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораПоступленияТоваровУслуг(ЭтотОбъект, "ВозвратнаяТара", "Добавить");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПоступлениюТара(Команда)

	РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораПоступленияТоваровУслуг(ЭтотОбъект, "ВозвратнаяТара", "Заполнить");

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзПоступленияУслуги(Команда)

	РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораПоступленияТоваровУслуг(ЭтотОбъект, "Услуги", "Добавить");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПоступлениюУслуги(Команда)

	РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораПоступленияТоваровУслуг(ЭтотОбъект, "Услуги", "Заполнить");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСчету(Команда)
	
	ЗаполнитьТабличнуюЧастьПоСчету("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСчетуТара(Команда)
	
	ЗаполнитьТабличнуюЧастьПоСчету("ВозвратнаяТара");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСчетуУслуги(Команда)
	
	ЗаполнитьТабличнуюЧастьПоСчету("Услуги");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборТовары(Команда)

	РеализацияТоваровУслугФормыКлиент.ПодборНоменклатуры(ЭтотОбъект, "Товары", Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборУслуги(Команда)

	РеализацияТоваровУслугФормыКлиент.ПодборНоменклатуры(ЭтотОбъект, "Услуги", Команда);

КонецПроцедуры

&НаКлиенте
Процедура ПодборВозвратнаяТара(Команда)

	РеализацияТоваровУслугФормыКлиент.ПодборНоменклатуры(ЭтотОбъект, "ВозвратнаяТара", Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВУслуги(Команда)
	
	ТекущиеДанные = Элементы.АгентскиеУслуги.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Не выбрана строка.'"));
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = Объект.Услуги.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
	Объект.АгентскиеУслуги.Удалить(ТекущиеДанные);
	
	// Заполняем счета учета
	ПараметрыЗаполненияСчетовУчета = РеализацияТоваровУслугФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"Услуги.Номенклатура",
		Объект,
		НоваяСтрока);
	
	ИзмененныеРеквизиты = СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);
	Если ИзмененныеРеквизиты.Свойство("СчетДоходов") И ЗначениеЗаполнено(НоваяСтрока.СчетДоходов) Тогда
		НоваяСтрока.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам =
			БухгалтерскийУчетВызовСервераПовтИсп.НаСчетеВедетсяУчетПоНоменклатурнымГруппам(НоваяСтрока.СчетДоходов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВАгентскиеУслуги(Команда)

	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Не выбрана строка.'"));
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = Объект.АгентскиеУслуги.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
	Объект.Услуги.Удалить(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТовары(Команда)

	АдресХранилищаТовары = ПоместитьТоварыВоВременноеХранилищеНаСервере();

	РеализацияТоваровУслугФормыКлиент.ИзменитьТовары(ЭтотОбъект, Команда, АдресХранилищаТовары);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыписатьСчетФактуру(Команда)

	РеализацияТоваровУслугФормыКлиент.ВыписатьСчетФактуру(ЭтотОбъект, Команда);
	Если СчетФактура.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.НадписьСчетФактура);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Оповестить("Запись_СчетФактураВыданный");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентов(Команда)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентовВДокументеПоКнопке(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВВЕТИС(Команда)
	
	ДополнительныеПараметры = Новый Структура("Основание", Объект.Ссылка);
	ОткрытьФорму("Документ.ИсходящаяТранспортнаяОперацияВЕТИС.ФормаОбъекта", ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзСчета(Команда)
	ОбработчикВыбора = Новый ОписаниеОповещения("ДобавитьИзСчетаЗавершение", ЭтотОбъект, Новый Структура);
	
	РеализацияТоваровУслугФормыКлиент.ДобавитьИзСчета(ЭтотОбъект, ОбработчикВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЭлектроннуюТранспортнуюНакладную(Команда)
	РеализацияТоваровУслугФормыКлиент.СоздатьЭтРННаОсновании(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьЗаявлениеОВвозеТоваровПолученное(Команда)
	
	ЗаявлениеОВвозеТоваровПолученное = УчетНДСКлиент.СоздатьЗаявлениеОВвозеТоваровПолученное(ЭтотОбъект);
	Если НЕ ЗначениеЗаполнено(ЗаявлениеОВвозеТоваровПолученное) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаявленияОВвозеТоваровПолученного = Новый Структура;
	ПараметрыЗаявленияОВвозеТоваровПолученного.Вставить("ЗаявлениеОВвозеТоваровПолученное", ЗаявлениеОВвозеТоваровПолученное);
	ПараметрыЗаявленияОВвозеТоваровПолученного.Вставить("Основания", Новый Массив);
	ПараметрыЗаявленияОВвозеТоваровПолученного.Основания.Добавить(Объект.Ссылка);
	
	Оповестить("Запись_ЗаявлениеОВвозеТоваровПолученное", ПараметрыЗаявленияОВвозеТоваровПолученного);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтатформаНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ОтчетностьВФТС) Тогда
		
		Если Объект.ПометкаУдаления Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Отчет ""Статистическая форма учета перемещения товаров"" нельзя создать на основании документа, помеченного на удаление.'"));
			Возврат;
		КонецЕсли;
		
		Если Модифицированность Тогда
			Записать();
		КонецЕсли;
		
		Реквизиты = Новый Структура;
		Реквизиты.Вставить("Ссылка", Объект.Ссылка);
		Реквизиты.Вставить("Дата", Объект.Дата);
		Реквизиты.Вставить("Организация", Объект.Организация);
		
		ОтчетностьВФТС = УчетНДСВызовСервера.ОпределитьСсылкуНаСуществующуюСтатформу(Реквизиты);
		НаправлениеПеремещения = "ЭК";
		УчетНДСКлиент.ОткрытьСтатформу(НаправлениеПеремещения, Реквизиты, ОтчетностьВФТС);
		
	Иначе
		ПоказатьЗначение(,ОтчетностьВФТС);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УслугиНоменклатураПриИзмененииНаКлиенте(СтрокаНоменклатура)

	// Получим общие параметры обработки для реквизитов документа
	ПараметрыОбработки = РеализацияТоваровУслугФормыКлиент.ПодготовитьПараметрыОбработкиУслугиНоменклатураПриИзменении(
		ЭтаФорма, СтрокаНоменклатура);

	// Дополнительные поля, требующиеся для заполнения добавленных колонок табличного поля текущей формы.
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Всего", СтрокаНоменклатура.Всего);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("СчетДоходовВедетсяУчетПоНоменклатурнымГруппам", СтрокаНоменклатура.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам);
	
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтаФорма, ПараметрыОбработки.ДанныеОбъекта);
	
	УслугиНоменклатураПриИзмененииНаСервере(
		ПараметрыОбработки.ДанныеСтрокиТаблицы,
		ПараметрыОбработки.ДанныеОбъекта,
		ПараметрыОбработки.СчетаУчетаКЗаполнению);
	
	ЗаполнитьЗначенияСвойств(СтрокаНоменклатура, ПараметрыОбработки.ДанныеСтрокиТаблицы);
	
	ОтобразитьСубконтоСчетаДоходовУслуги = ОтобразитьСубконтоСчетаДоходовУслуги ИЛИ НЕ СтрокаНоменклатура.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам;

КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	Если УправлениеПечатьюБПВызовСервера.ИспользуетсяИзмененныйМакет(Объект.Ссылка) Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_ИспользуетсяИзмененныйМакет");
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(ИмяТаблицы = "") Экспорт

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	Если ПустаяСтрока(ИмяТаблицы) ИЛИ ИмяТаблицы = "Товары" Тогда
		СписокСчетов            = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Товары, "СчетНаОплатуПокупателю");
		РеквизитыСчетовНаОплату = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокСчетов, "Номер, Дата");
		
		Для каждого СтрокаТаблицы Из Объект.Товары Цикл
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(ЭтотОбъект, СтрокаТаблицы, РеквизитыСчетовНаОплату, ПараметрыОбъекта);
		КонецЦикла;
		
		ЗаполнитьПризнакМаркируемойПродукцииТаблицыТовары(Объект.Товары, ИспользоватьКонтрольныеЗнакиГИСМ, ВестиУчетМаркируемойПродукцииИСМП);
		
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяТаблицы) ИЛИ ИмяТаблицы = "Услуги" Тогда
		СписокСчетов            = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Услуги, "СчетНаОплатуПокупателю");
		РеквизитыСчетовНаОплату = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокСчетов, "Номер, Дата");
		
		Для каждого СтрокаТаблицы Из Объект.Услуги Цикл
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, РеквизитыСчетовНаОплату, ПараметрыОбъекта);
		КонецЦикла;
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяТаблицы) ИЛИ ИмяТаблицы = "АгентскиеУслуги" Тогда
		СписокСчетов            = ОбщегоНазначения.ВыгрузитьКолонку(Объект.АгентскиеУслуги, "СчетНаОплатуПокупателю");
		РеквизитыСчетовНаОплату = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокСчетов, "Номер, Дата");
		
		Для каждого СтрокаТаблицы Из Объект.АгентскиеУслуги Цикл
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыАгентскиеУслуги(СтрокаТаблицы, РеквизитыСчетовНаОплату, ПараметрыОбъекта);
		КонецЦикла;
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяТаблицы) ИЛИ ИмяТаблицы = "ВозвратнаяТара" Тогда
		СписокСчетов            = ОбщегоНазначения.ВыгрузитьКолонку(Объект.ВозвратнаяТара, "СчетНаОплатуПокупателю");
		РеквизитыСчетовНаОплату = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокСчетов, "Номер, Дата");
		
		Для каждого СтрокаТаблицы Из Объект.ВозвратнаяТара Цикл
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыВозвратнаяТара(СтрокаТаблицы, РеквизитыСчетовНаОплату, ПараметрыОбъекта);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиНаСервере() Экспорт

	ЗаполнитьДобавленныеКолонкиТаблиц();
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере() Экспорт
	
	ТекущаяДатаДокумента          = Объект.Дата;
	ДатыИзмененияОтветственныхЛиц = ОтветственныеЛицаБППовтИсп.ДатыИзмененияОтветственныхЛицОрганизаций(Объект.Организация);
	
	РеквизитыОрганизацииСсылка = ПроверкаРеквизитовОрганизации.СтрокаСообщенияНеЗаполненБанковскийСчет();
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьСостояниеДокумента();
	
	УстановитьОтборПоОрганизации();

	СтатусДокумента = ПолучитьСтатусДокумента(Объект.Организация, Объект.Ссылка);
	
	УчетОригиналовПервичныхДокументовБП.ПриСозданииНаСервере_ФормаДокумента(ЭтотОбъект, СтатусДокумента);
	
	ВалютаРегламентированногоУчета 	= ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ЭтоОтгрузка 					= Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности;
	
	ОсновнойБанковскийСчетОрганизацииЗаполнен = ЗначениеЗаполнено(Объект.Организация.ОсновнойБанковскийСчет);
	
	Если ИспользоватьОднуНоменклатурнуюГруппу Тогда
		ОсновнаяНоменклатурнаяГруппа = БухгалтерскийУчетВызовСервераПовтИсп.ОсновнаяНоменклатурнаяГруппа();
	КонецЕсли;
	
	ЗаполнитьОтображениеСубконтоСчетаДоходов();

	ДоговорУказан = ЗначениеЗаполнено(Объект.ДоговорКонтрагента);
	
	Если ДоговорУказан Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.ДоговорКонтрагента, "ВидДоговора, ВалютаВзаиморасчетов, РасчетыВУсловныхЕдиницах, УчетАгентскогоНДСПокупателем");
	
		ВалютаВзаиморасчетов          = РеквизитыДоговора.ВалютаВзаиморасчетов;
		ЭтоКомиссия                   = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером;
		ПокупательНалоговыйАгентПоНДС = РеквизитыДоговора.УчетАгентскогоНДСПокупателем;
		
	Иначе

		ВалютаВзаиморасчетов          = ВалютаРегламентированногоУчета;
		ЭтоКомиссия                   = (Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров);
		ПокупательНалоговыйАгентПоНДС = Ложь;
		
		Если ЗначениеЗаполнено(Объект.Контрагент)
			и НЕ ЗначениеЗаполнено(Объект.Ссылка) 
			и НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			РеализацияТоваровУслугФормы.ПроверитьУстановитьДоговорКонтрагента(ЭтаФорма, Новый Массив);
		ИначеЕсли ВестиУчетПоДоговорам И РеализацияТоваровУслугФормы.ДоступноСозданиеОсновногоДоговораПоВидуОперации(Объект.ВидОперации) Тогда
			ПредлагатьНовыйДоговор = РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПредлагатьНовыйДоговорСНумерацией();
	
	РеализацияТоваровУслугФормы.УстановитьПараметрыВыбора(ЭтотОбъект);
	
	РеализацияТоваровУслугФормы.ЗаполнитьСписокАдресовДоставки(ЭтотОбъект, Объект.Контрагент, Объект.Грузополучатель, Объект.Дата);
	
	ПечатьФискальныхДокументовФормыКлиентСервер.ПодготовитьФормуНаСервере(ЭтотОбъект);
	
	Если ЕстьУчетПодакцизныхТоваров и Параметры.Ключ.Пустая() Тогда
		ПараметрыЗаполнения = Новый Структура;
		УчетАкцизов.ПараметрыДляЗаполненияДанныхПоАкцизам(ПараметрыЗаполнения);
		ПараметрыЗаполнения.ЭкспортПриРеализации = ЭкспортПриРеализации;
		ПараметрыЗаполнения.ЭтоПередачаТоваров   = ЭтоКомиссия;
		УчетАкцизов.ЗаполнитьДанныеПоАкцизам(Объект.Товары, Объект.Организация, Объект.Дата, ПараметрыЗаполнения);
	КонецЕсли;
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	Если ЭтоОтгрузка Тогда
		УстановитьНомерНаПечать(ЭтотОбъект);
	КонецЕсли;
		
	УстановитьЗначениеТребуетсяСчетФактура();
	
	ЕстьПравоСозданиеЭТрН = ПравоДоступа("Изменение", Метаданные.Документы.ЭлектроннаяТранспортнаяНакладная);
	Если НастроенОбменЭПД Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ОбменСКонтрагентамиБП.РеквизитыЭТрН(Объект.Ссылка));
	КонецЕсли; 
	
	УстановитьПредставлениеНадписиЗаявлениеОВвозеТоваровПолученное();
	
	УстановитьВидимостьПроверкиРНПТ();
	
	ЕстьПравоЧтениеПоступлениеТоваровУслуг = ПравоДоступа("Чтение", Метаданные.Документы.ПоступлениеТоваровУслуг);
	ЕстьПравоЧтениеСчетНаОплату            = ПравоДоступа("Чтение", Метаданные.Документы.СчетНаОплатуПокупателю);
	// Для заполненного документа сразу выводим напоминание об уведомлении в подвале документа если требуется.
	ТребуетсяНапоминание = ТребуетсяНапоминаниеОбУведомленииОПеремещении(ЭтотОбъект);
	УстановитьПредставлениеНадписиУведомлениеОПеремещении();

	Элементы.ТоварыЗаполнитьПоПоступлению.Видимость = ЕстьПравоЧтениеПоступлениеТоваровУслуг;
	Элементы.ТоварыДобавитьИзПоступления.Видимость  = ЕстьПравоЧтениеПоступлениеТоваровУслуг;
	Элементы.ТоварыЗаполнитьПоСчету.Видимость       = ЕстьПравоЧтениеСчетНаОплату;
	
	Элементы.ВозвратнаяТараДобавитьИзПоступленияТара.Видимость  = ЕстьПравоЧтениеПоступлениеТоваровУслуг;
	Элементы.ВозвратнаяТараЗаполнитьПоПоступлениюТара.Видимость = ЕстьПравоЧтениеПоступлениеТоваровУслуг;
	Элементы.ВозвратнаяТараЗаполнитьПоСчетуТара.Видимость       = ЕстьПравоЧтениеСчетНаОплату;
	
	Элементы.УслугиДобавитьИзПоступленияУслуги.Видимость  = ЕстьПравоЧтениеПоступлениеТоваровУслуг;
	Элементы.УслугиЗаполнитьПоПоступлениюУслуги.Видимость = ЕстьПравоЧтениеПоступлениеТоваровУслуг;
	Элементы.УслугиЗаполнитьПоСчетуУслуги.Видимость       = ЕстьПравоЧтениеСчетНаОплату;
	
	РеализацияТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
	РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьТекстПроСчетФактуру(ЭтотОбъект);
	
	РеализацияТоваровУслугФормы.ЗаполнитьРеквизитыУПД(ЭтотОбъект);
	
	УчетПСН.НастроитьВыборПатента(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
	РеализацияТоваровУслугФормы.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	// Проверка буфера обмена на наличие скопированных строк
	УстановитьДоступностьКомандыВставки(ЭтотОбъект, Не ОбщегоНазначения.ПустойБуферОбмена());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦеныИВалюты(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь) Экспорт

	Если ПерезаполнитьЦены ИЛИ ПересчитатьЦены ИЛИ ПересчитатьНДС Тогда
		ПриИзмененииЦеныИВалютыНаСервере(ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены, ПересчитатьЦены, ПересчитатьНДС);
	Иначе
		ОбновитьИтоги(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойНаСервере() Экспорт
	
	УстановитьЗначениеТребуетсяСчетФактура();
	УправлениеФормой(ЭтотОбъект);
	РеализацияТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы() Экспорт

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата)
		Или УчетнаяПолитика.ПлательщикНДСПрименяющийУСН(Объект.Организация, Объект.Дата);
	ПлательщикНДФЛ       = УчетнаяПолитика.ПлательщикНДФЛ(Объект.Организация, Объект.Дата);
	ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Объект.Организация, Объект.Дата);
	УчитыватьТаруЗаБалансом = УчетнаяПолитика.УчитыватьТаруЗаБалансом(Объект.Организация, Объект.Дата);
	
	// При реализации в страны ЕАЭС заполняем и отражаем колонку "Код ТН ВЭД" для целей счета-фактуры и книги продаж.
	РеализацияВЕАЭС = РеализацияТоваровУслугФормыВызовСервера.EстьРеализацияВЕАЭС(Объект.СтранаРеализации, Объект.Контрагент);
	ВедетсяУчетНДСПоФЗ150 = УчетНДС.ВедетсяУчетНДСПоФЗ150(Объект.Дата);
	ВедетсяУчетНДСПоФЗ335 = УчетНДС.ВедетсяУчетНДСПоФЗ335(Объект.Дата);
	
	Если ПрименяетсяУСНПатент Тогда
		ПрименяетсяНесколькоПатентов = УчетПСН.ИспользуетсяНесколькоПатентов(Объект.Организация, Объект.Дата);
	КонецЕсли;
	
	ПрименяетсяТолькоУСНПатент           = УчетнаяПолитика.ПрименяетсяТолькоУСНПатент(Объект.Организация, Объект.Дата);
	ИспользоватьТипыЦенНоменклатуры      = ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры") И ПравоДоступа("Чтение", Метаданные.Справочники.ТипыЦенНоменклатуры);
	ЕстьВалютныйУчет                     = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
	ИспользоватьОднуНоменклатурнуюГруппу = БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу();
	ИспользуетсяПланированиеПлатежей     = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеПлатежейОтПокупателей");
	ВестиУчетПоДоговорам                 = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ИспользоватьКонтрольныеЗнакиГИСМ     = ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольныеЗнакиГИСМ")
										 И ИнтеграцияГИСМКлиентСерверБП.МожноИспользоватьКонтрольныеЗнакиГИСМ(Объект.Дата);
	ВестиУчетМаркируемойПродукцииИСМП    = ПолучитьФункциональнуюОпцию("ВестиУчетМаркируемойПродукцииИСМП");
	ВедетсяУчетПрослеживаемыхТоваров = 
		ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Объект.Дата);
	ИспользоватьНачислениеНДССМежценовойРазницы = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеНДССМежценовойРазницы");
	УведомлениеОПеремещенииПодаетсяВТомЧислеДоПересеченияГраницы = 
		ПрослеживаемостьБРУ.УведомлениеПодаетсяДоМоментаПересеченияГраницы(Объект.Дата);

	ИспользоватьДоставкуТранспортнойКомпанией = ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуТранспортнойКомпанией");
	
	РаздельныйУчетТорговыйСборПриУСН = УчетнаяПолитика.РаздельныйУчетТорговыйСборПриУСН(Объект.Организация, Объект.Дата);
	
	СпособОценкиМПЗ          = УчетнаяПолитика.СпособОценкиМПЗ(Объект.Организация, Объект.Дата);
	СпособОценкиМПЗПоСредней = (СпособОценкиМПЗ = Перечисления.СпособыОценки.ПоСредней);

	// Используется в условном оформлении
	ТребуетсяСчетРасходовПоОказаниюУслуг =
	РегистрыНакопления.РеализацияУслуг.ТребуетсяСчетРасходовПоОказаниюУслуг(Объект.Дата, Объект.Организация);
	
	ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров = ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров");
	ОсуществляетсяРеализацияТоваровУслугКомитентов = ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугКомитентов");
	
	ИспользоватьНесколькоБанковскихСчетовОрганизации =
		Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Объект.Организация);
		
	ИспользоватьОбменЭД = ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") 
		И ОбменСКонтрагентами.ОрганизацияПодключена(Объект.Организация);
		
	УстановитьПараметрыДоставки();
	
	РеализацияТоваровУслугФормыКлиентСервер.ПроверитьВозможностьОформленияЭПД(ЭтотОбъект);
	
	ПроизводствоПодакцизныхТоваров = ПолучитьФункциональнуюОпцию("ПроизводствоПодакцизныхТоваров");
	Если ПроизводствоПодакцизныхТоваров Тогда
		УчетАкцизов.СписокПроизводимыхПодакцизныхТоваров(Объект.Организация, ВидыПроизведенныхПодакцизныхТоваров);
		ЕстьУчетПодакцизныхТоваров = ВидыПроизведенныхПодакцизныхТоваров.Количество() > 0
			И Не Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Оборудование;
		ЭкспортПриРеализации       = Справочники.Контрагенты.ЭтоИностраннаяОрганизация(Объект.Контрагент);
	КонецЕсли;
	
	ЗаполнятьОтветственногоПоПродажамИзКонтрагента = Константы.ЗаполнятьОтветственногоПоПродажамИзКонтрагента.Получить();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьВыпискуСчетаФактуры(Результат, ДополнительныеПараметры) Экспорт
	
	РеализацияТоваровУслугФормыКлиент.ОбработатьВыпискуСчетаФактуры(ЭтотОбъект, Результат, ДополнительныеПараметры);
	Если СчетФактура.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("Запись_СчетФактураВыданный");
	
	УправлениеФормой(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.НадписьСчетФактура);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	Если ТипЗнч(НастройкиУсловногоОформления) <> Тип("Структура") Тогда
		НастройкиУсловногоОформления = Новый Структура();
		УсловноеОформление.Элементы.Очистить();
	КонецЕсли;

	// Условное оформление, связанное с видимостью, устанавливаем сразу для всех колонок.
	УстановитьУсловноеОформлениеШапкаИВидимость();

	// Условное оформление для полей, расположенных на страницах

	ОбновитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьУсловноеОформление(Форма)

	Элементы = Форма.Элементы;

	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТовары
		И НЕ Форма.НастройкиУсловногоОформления.Свойство("ТоварыПроинициализировано") Тогда

		Форма.УстановитьУсловноеОформлениеТовары();

	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаУслуги
		И НЕ Форма.НастройкиУсловногоОформления.Свойство("УслугиПроинициализировано") Тогда

		Форма.УстановитьУсловноеОформлениеУслуги();

	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаАгентскиеУслуги
		И НЕ Форма.НастройкиУсловногоОформления.Свойство("АгентскиеУслугиПроинициализировано") Тогда

		Форма.УстановитьУсловноеОформлениеАгентскиеУслуги();

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеШапкаИВидимость()

	// Склад

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Склад");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтметкаНезаполненногоСклад", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// СчетУчетаРасчетовПоТаре

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетУчетаРасчетовПоТаре");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЕстьСтрокиВозвратнойТары", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// ТоварыСчетДоходов, ТоварыСубконто, ТоварыСчетРасходов, ТоварыСчетУчетаНДСПоРеализации

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетДоходов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСубконто");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетРасходов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетУчетаНДСПоРеализации");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЭтоКомиссия", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ТоварыПереданныеСчетУчета

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПереданныеСчетУчета");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЭтоОтгрузка", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЭтоКомиссия", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ПорядокУчетаРасчетов

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПорядокУчетаРасчетов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СчетаРасчетовЗаполнены", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);


	// Реквизиты не используются, если документ составлен неплательщиком НДС, 
	// или покупатель является налоговым агентом по НДС.

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыВсего");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетУчетаНДСПоРеализации");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиВсего");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСчетУчетаНДСПоРеализации");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АгентскиеУслугиСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АгентскиеУслугиСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АгентскиеУслугиВсего");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.ДокументБезНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"ПокупательНалоговыйАгентПоНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"ВедетсяУчетНДСПоФЗ335",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	// ТоварыСубконто

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСубконто");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтобразитьСубконтоСчетаДоходовТовары", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// УслугиСубконто

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСубконто");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтобразитьСубконтоСчетаДоходовУслуги", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ТоварыДокументОприходования
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыДокументОприходования");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОсуществляетсяРеализацияТоваровУслугКомитентов", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СпособОценкиМПЗПоСредней", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеТовары() Экспорт

	НастройкиУсловногоОформления.Вставить("ТоварыПроинициализировано", Истина);

	//НомерЗаказа
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНомерЗаказа");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ОтчетМаркетплейса", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	// ТоварыДокументОприходования

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыДокументОприходования");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ДокументОприходования", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Авто>'"));
	
	// ТоварыДокументОприходования
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыДокументОприходования");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
	
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
				"Объект.Товары.СчетУчетаПартионный", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Товары.СчетУчетаЗабалансовый", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"СпособОценкиМПЗПоСредней", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ТоварыДокументОприходования
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыДокументОприходования");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОсуществляетсяРеализацияТоваровУслугКомитентов", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СпособОценкиМПЗПоСредней", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	

	// ТоварыСебестоимость

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСебестоимость");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.Себестоимость", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Авто>'"));
	
	// ТоварыСебестоимость
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСебестоимость");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ДокументОприходования", ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.СчетУчетаПартионный", ВидСравненияКомпоновкиДанных.Равно, Истина);
			
		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Товары.СчетУчетаЗабалансовый", ВидСравненияКомпоновкиДанных.Равно, Истина);
				
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"СпособОценкиМПЗПоСредней", ВидСравненияКомпоновкиДанных.Равно, Ложь);
				
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// ТоварыСубконто

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСубконто");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ПлательщикНДФЛ", ВидСравненияКомпоновкиДанных.Равно, Ложь);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// ТоварыСубконто

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСубконто");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.Субконто", ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ИспользоватьОднуНоменклатурнуюГруппу", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтобразитьСубконтоСчетаДоходовТовары", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// ТоварыСчетДоходов, ТоварыСчетРасходов, ТоварыСчетУчетаНДСПоРеализации

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетДоходов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетРасходов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетУчетаНДСПоРеализации");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.СчетУчетаЗабалансовый", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// ТоварыСчетУчетаНДСПоРеализации

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетУчетаНДСПоРеализации");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// ТоварыСуммаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаНДС");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НДССМежценовойРазницы", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	// ТоварыАналитикаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыАналитикаНДС");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.НДССМежценовойРазницы", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.ДокументБезНДС", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
			"ПокупательНалоговыйАгентПоНДС", ВидСравненияКомпоновкиДанных.Равно, Истина);
			
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
			"ВедетсяУчетНДСПоФЗ335", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ТоварыКиЗ_ГИСМ

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКиЗ_ГИСМ");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПродукцияМаркируемаяДляГИСМ", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// КиЗ_ГИСМ только для товаров
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКиЗ_ГИСМ");
	ЗапрещенныеОперации = Новый СписокЗначений;
	ЗапрещенныеОперации.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.ПродажаКомиссия);
	ЗапрещенныеОперации.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.ПередачаТоваров);
	ЗапрещенныеОперации.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.Товары);
	ЗапрещенныеОперации.Добавить(Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ВидОперации", ВидСравненияКомпоновкиДанных.НеВСписке, ЗапрещенныеОперации);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ТоварыКодТНВЭД
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыКодТНВЭД");
	
	ГруппаОтбораИЛИ = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИЛИ,
		"РеализацияВЕАЭС", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИЛИ,
		"ВедетсяУчетНДСПоФЗ150", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	// Таможенная декларация или РНПТ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПодсказкаРНПТ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВедетсяУчетПрослеживаемыхТоваров", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// РНПТ заполняется авто
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыРНПТ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.РНПТ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПрослеживаемыйТовар", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПрослеживаемыйКомплект", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<Авто>'"));
	
	// Заполнение РНПТ не требуется
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыРНПТ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.СтранаПроисхождения", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.НомерГТД", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПрослеживаемыйТовар", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<Не требуется>'"));
	
	// РНПТ для комплектов заполняется вручную
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыРНПТ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.РНПТ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПрослеживаемыйТовар", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПрослеживаемыйКомплект", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<...>'"));
	
	// Страна происхождения
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСтранаПроисхождения");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПрослеживаемыйТовар", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.СтранаПроисхождения", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// Признак прослеживаемости
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПрослеживаемыйТовар");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВедетсяУчетПрослеживаемыхТоваров", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Номер ТД
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНомерГТД");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПрослеживаемыйТовар", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Номер РНПТ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыРНПТ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСостояниеПроверкиРНПТ");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПрослеживаемыйТовар", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Акцизы
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаАкциза");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетАкциза");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСубконтоАкцизы");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЕстьУчетПодакцизныхТоваров", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетАкциза");
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.ПодакцизныйТовар", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.СуммаАкциза", ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.СчетАкциза", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеУслуги() Экспорт

	НастройкиУсловногоОформления.Вставить("УслугиПроинициализировано", Истина);


	// УслугиСубконто

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСубконто");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ПлательщикНДФЛ", ВидСравненияКомпоновкиДанных.Равно, Ложь);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// УслугиСубконто

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСубконто");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.Субконто", ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ИспользоватьОднуНоменклатурнуюГруппу", ВидСравненияКомпоновкиДанных.Равно, Истина);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтобразитьСубконтоСчетаДоходовУслуги", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// УслугиСчетУчетаНДСПоРеализации

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСчетУчетаНДСПоРеализации");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// УслугиСуммаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСуммаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// УслугиСчетРасходов

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСчетРасходов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТребуетсяСчетРасходовПоОказаниюУслуг", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеАгентскиеУслуги() Экспорт

	НастройкиУсловногоОформления.Вставить("АгентскиеУслугиПроинициализировано", Истина);


	// АгентскиеУслугиСуммаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АгентскиеУслугиСуммаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.АгентскиеУслуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.АгентскиеУслуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.ПредупреждениеОЗаполненииРеквизитовСчета.Видимость = ПроверкаРеквизитовОрганизацииКлиентСервер.ПоказатьПредупреждениеОРеквизитахСчета(Форма, Объект.Организация);
	
	// Заголовок поля с РНПТ
	ТекстЗаголовкаРНПТ = НСтр("ru = 'Таможенная декларация или РНПТ'");
	Если НЕ Форма.ВедетсяУчетПрослеживаемыхТоваров Тогда
		ТекстЗаголовкаРНПТ = НСтр("ru = 'Таможенная декларация'");
	КонецЕсли;
	Элементы.ТоварыГруппаРНПТ.Заголовок = ТекстЗаголовкаРНПТ;
	
	ТекстЗаголовкаСуммаАкциза = НСтр("ru = 'В т.ч. акциз'");
	Если Объект.ВалютаДокумента <> Форма.ВалютаРегламентированногоУчета Тогда
		ТекстЗаголовкаСуммаАкциза = НСтр("ru = 'В т.ч. акциз в руб.'");
	КонецЕсли;
	Элементы.ТоварыСуммаАкциза.Заголовок = ТекстЗаголовкаСуммаАкциза;

	ЭтоОборудование    = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Оборудование");
	ЭтоПродажаКомиссия = (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ПродажаКомиссия")
							ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Товары")
							ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Услуги"));
							
	Если ЭтоОборудование Тогда
		Элементы.ГруппаТовары.Заголовок = "Оборудование";
	Иначе
		Элементы.ГруппаТовары.Заголовок = "Товары";
	КонецЕсли;
	
	// Может оказаться так, что отображается только одна страница. 
	// В таком случае убираем отображение страниц
	Если НужноУбратьЗакладкиСтраниц(Объект) Тогда
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	КонецЕсли;
	
	Элементы.ГруппаАдресДоставки.Видимость  = Форма.ЕстьДоставка И Форма.ИспользоватьДоставкуТранспортнойКомпанией;
	Элементы.НомерДляОтслеживания.Видимость = Форма.ЕстьНомерДляОтслеживания И Форма.ИспользоватьДоставкуТранспортнойКомпанией;
	Элементы.АдресДоставкиПереходНаКарту.Доступность = ЗначениеЗаполнено(Объект.АдресДоставки);	
	
	// Управление видимостью патента.
	Элементы.Патент.Доступность     = Объект.ДеятельностьНаПатенте ИЛИ Форма.ПрименяетсяТолькоУСНПатент;
	Элементы.Патент.Видимость       = НЕ ЭтоОборудование И (Форма.ПрименяетсяНесколькоПатентов ИЛИ НЕ Форма.ПрименяетсяТолькоУСНПатент);
	
	// Если только патент, то флаг избыточен - патент должен быть доступен для выбора всегда.
	Элементы.ДеятельностьНаПатенте.Видимость = НЕ ЭтоОборудование И НЕ Форма.ПрименяетсяТолькоУСНПатент;
	Элементы.ОтступШапка.Видимость  = НЕ ЭтоОборудование И Форма.ПрименяетсяУСНПатент И НЕ Форма.ПрименяетсяТолькоУСНПатент;
	Элементы.ОтступПодвал.Видимость = НЕ ЭтоОборудование И Форма.ПрименяетсяУСНПатент И НЕ Форма.ПрименяетсяТолькоУСНПатент;
	
	Элементы.ДеятельностьНаТорговомСборе.Видимость = НЕ ЭтоОборудование И Форма.РаздельныйУчетТорговыйСборПриУСН;
	
	ВидимостьАгентскихУслуг = НЕ (Форма.ЭтоКомиссия ИЛИ Форма.ЭтоОтгрузка 
		ИЛИ (Форма.ПокупательНалоговыйАгентПоНДС = Истина И Форма.ВедетсяУчетНДСПоФЗ335));
	Элементы.ГруппаАгентскиеУслуги.Видимость           = ВидимостьАгентскихУслуг;
	Элементы.УслугиПеренестиВАгентскиеУслуги.Видимость = ВидимостьАгентскихУслуг;
		
	Элементы.ГруппаУслуги.Видимость          = НЕ (Форма.ЭтоКомиссия
		ИЛИ (Форма.ПокупательНалоговыйАгентПоНДС = Истина И Форма.ВедетсяУчетНДСПоФЗ335));
		
	Элементы.ГруппаДеятельностьНаПатенте.Видимость = НЕ Форма.ЭтоКомиссия;
		
	Элементы.ДекорацияОтгрузкаУслуг.Видимость = Форма.ЭтоОтгрузка;
	Элементы.ДекорацияОтгрузкаУслугСчетФактура.Видимость = Форма.ЭтоОтгрузка;
	
	Элементы.НомерНаПечать.Видимость = Форма.ЭтоОтгрузка
		И Не Форма.ПлательщикНДС
		И ЗначениеЗаполнено(Объект.НомерВходящегоДокумента);
	
	// Доступность взаимосвязанных полей
	Элементы.ДоговорКонтрагента.Доступность       = ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Контрагент);
	Элементы.ПодразделениеОрганизации.Доступность = ЗначениеЗаполнено(Объект.Организация);

	ДоступностьПорядокУчетаРасчетов = (НЕ Форма.ВестиУчетПоДоговорам И ЗначениеЗаполнено(Объект.Контрагент)) ИЛИ ЗначениеЗаполнено(Объект.ДоговорКонтрагента);
		
	Элементы.ПорядокУчетаРасчетов.Доступность = ДоступностьПорядокУчетаРасчетов;
	Элементы.ПорядокУчетаРасчетов.Гиперссылка = ДоступностьПорядокУчетаРасчетов;
	
	Элементы.СчетУчетаРасчетовПоТаре.Видимость = НЕ Форма.УчитыватьТаруЗаБалансом;
	
	// Счет-фактура
	Если Объект.ЭтоУниверсальныйДокумент Тогда
		НеОтображатьРеквизитыСчетаФактуры = Истина;
	Иначе
		НеОтображатьРеквизитыСчетаФактуры = Объект.ДокументБезНДС;
	КонецЕсли;
	
	УчетНДСКлиентСервер.НастроитьПоляСчетаФактуры(
		Форма.Элементы.СчетФактураКнопка,
		Форма.Элементы.СчетФактураСсылка,
		Форма.Элементы.НадписьСчетФактура,
		НеОтображатьРеквизитыСчетаФактуры,
		Форма.ТребуетсяСчетФактура,
		Форма.СчетФактура);
		
	УчетНДСКлиентСервер.ОтобразитьПолеУПДВыданный(Форма);
	
	РеализацияТоваровУслугФормыКлиентСервер.УстановитьРеквизитыОтчетностиЭкспортеров(Форма);
	
	НалоговыйАгентПоФЗ335 = Форма.ПокупательНалоговыйАгентПоНДС = Истина И Форма.ВедетсяУчетНДСПоФЗ335;
	
	Элементы.ИтогиВсегоНДС.Видимость = Не (Объект.ДокументБезНДС ИЛИ НалоговыйАгентПоФЗ335);
	
	Форма.ПатентПредставление = УчетПСНКлиентСервер.ВыбранныйПатентПредставление(
		Объект.Патент, УчетПСНКлиентСервер.СписокПатентов(Форма));
	
	РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыСчетНаОплату(Форма);
	
	ОбновитьИтоги(Форма);
	РеализацияТоваровУслугФормыКлиентСервер.СформироватьНадписьЦеныИВалюта(Форма);
	Элементы.ЦеныИВалюта.Видимость = НЕ НалоговыйАгентПоФЗ335;
	
	Элементы.СостояниеЭТрН.Видимость = Форма.НастроенОбменЭПД;
	Элементы.СостояниеЭТрН.Гиперссылка = ЗначениеЗаполнено(Форма.ЭлектроннаяТранспортнаяНакладная) ИЛИ Форма.ЕстьПравоСозданиеЭТрН;
	
	УстановитьВидимость(Форма);
	
	ПользователиБПКлиентСервер.УстановитьПодсказкуОтветственного(Форма);
	
	УчетОригиналовПервичныхДокументовБПКлиентСервер.УстановитьДоступностьДекорацииСостояния(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЭлементыФормыПоВидуОперации()
	
	// При создании формы сразу устанавливаем свойства элементов,
	// которые зависят от вида операции и которые в дальнейшем менять не требуется.

	ЭтоОборудование    = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Оборудование");
	
	Если ЭтоОборудование Тогда
		Элементы.ГруппаТовары.Заголовок = "Оборудование";
	Иначе
		Элементы.ГруппаТовары.Заголовок = "Товары";
	КонецЕсли;
	
КонецПроцедуры

// Серверная обработка изменения реквизитов:

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	РеализацияТоваровУслугФормы.ДатаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	РеализацияТоваровУслугФормы.ОрганизацияПриИзменении(ЭтотОбъект);
	УстановитьОтборПоОрганизации();
	Объект.ДоговорКомиссионера = Неопределено;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура НомерНаПечатьПриИзмененииНаСервере()
	
	Объект.НомерВходящегоДокумента = НомерНаПечать;
	ЭтаФорма.Модифицированность = Истина;
	УстановитьНомерНаПечать(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		РеализацияТоваровУслугФормы.КонтрагентПриИзменении(ЭтотОбъект);
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	ЭлектронноеАктированиеБП.ДоговорКонтрагентаПриИзмененииНаСервере(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
		
КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		Если ВедетсяУчетПрослеживаемыхТоваров 
			И Объект.СведенияПрослеживаемости.Итог("Сумма") <> 0 Тогда
			ПрослеживаемостьФормыБП.ПересчитатьСуммыВВалютеПоКомплектам(ЭтотОбъект);
		КонецЕсли;
		РеализацияТоваровУслугФормы.ДоговорКонтрагентаПриИзменении(ЭтотОбъект);
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	ЭлектронноеАктированиеБП.ДоговорКонтрагентаПриИзмененииНаСервере(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
		
КонецПроцедуры

&НаСервере
Процедура СкладПриИзмененииНаСервере()
	
	РеализацияТоваровУслугФормы.СкладПриИзменении(ЭтотОбъект);	
	
КонецПроцедуры

&НаСервере
Процедура ОтветственныйПриИзмененииНаСервере()
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ДеятельностьНаПатентеПриИзмененииНаСервере()
	
	РеализацияТоваровУслугФормы.ОбработатьИзменениеДеятельностьНаПатенте(ЭтотОбъект);
	
	РеализацияТоваровУслугФормы.УстановитьПараметрыВыбора(ЭтотОбъект);
	
	РеализацияТоваровУслугФормы.УстановитьДеятельностьНаТорговомСборе(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДеятельностьНаТорговомСбореПриИзмененииНаСервере()
	
	РеализацияТоваровУслугФормы.ОбработатьИзменениеДеятельностьНаПатенте(ЭтотОбъект);
	
	РеализацияТоваровУслугФормы.УстановитьПараметрыВыбора(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПоШтрихкодуНаСервере(ШтрихкодВBase64)
	
	РеализацияТоваровУслугФормы.ДобавитьПоШтрихкодуНаСервере(ЭтотОбъект, ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.Base64ВШтрихкод(ШтрихкодВBase64));
	
	ЗаполнитьДобавленныеКолонкиТаблиц("Товары");
	
	ЗаполнитьОтображениеСубконтоСчетаДоходов("Товары");
	
	ОбновитьИтоги(ЭтотОбъект);	

КонецПроцедуры 

&НаСервере
Процедура ТоварыНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)

	РеализацияТоваровУслугФормы.ТоварыНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, ДанныеОбъекта, СчетаУчетаКЗаполнению);
	
	РеквизитыСчетовНаОплату = Новый Соответствие;
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СчетНаОплатуПокупателю) Тогда
		
		РеквизитыСчетовНаОплату.Вставить(
			СтрокаТабличнойЧасти.СчетНаОплатуПокупателю, 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТабличнойЧасти.СчетНаОплатуПокупателю, "Номер, Дата"));
	
	КонецЕсли; 
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(ЭтотОбъект, СтрокаТабличнойЧасти, РеквизитыСчетовНаОплату, ДанныеОбъекта);
	
	СобытияФормИС.ПриИзмененииЭлемента(ЭтотОбъект, "Номенклатура", СтрокаТабличнойЧасти);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВозвратнаяТараНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта, Знач РеквизитыКЗаполнению)
	
	ЗаполненныеСчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.РеализацияТоваровУслуг,
		РеквизитыКЗаполнению,
		ДанныеОбъекта,
		"ВозвратнаяТара",
		СтрокаТабличнойЧасти);
		
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ЗаполненныеСчета);
	
	Если Не ЗначениеЗаполнено(ДанныеОбъекта.ТипЦен) Тогда
		ДанныеОбъекта.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам);
	КонецЕсли;

	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		СтрокаТабличнойЧасти.Номенклатура, 
		ДанныеОбъекта,
		Ложь);
		
	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
		СведенияОНоменклатуре.Цена, ДанныеОбъекта.СуммаВключаетНДС, Истина,
		УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СведенияОНоменклатуре.СтавкаНДС));
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УслугиНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)

	РеализацияТоваровУслугФормы.УслугиНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, ДанныеОбъекта, СчетаУчетаКЗаполнению);
	
	РеквизитыСчетовНаОплату = Новый Соответствие;
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СчетНаОплатуПокупателю) Тогда
		
		РеквизитыСчетовНаОплату.Вставить(
			СтрокаТабличнойЧасти.СчетНаОплатуПокупателю, 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТабличнойЧасти.СчетНаОплатуПокупателю, "Номер, Дата"));
	
	КонецЕсли; 
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТабличнойЧасти, РеквизитыСчетовНаОплату, ДанныеОбъекта);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура АгентскиеУслугиНоменклатураПриИзмененииНаСервере(СтрокаТаблицы, Знач ДанныеОбъекта)
	
	Если Не ЗначениеЗаполнено(ДанныеОбъекта.ТипЦен) Тогда
		ДанныеОбъекта.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам);
	КонецЕсли;

	СведенияОНоменклатуре	= БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		СтрокаТаблицы.Номенклатура, ДанныеОбъекта, Ложь);
	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы.Содержание	= СведенияОНоменклатуре.НаименованиеПолное;
	СтрокаТаблицы.Цена			= СведенияОНоменклатуре.Цена;
	СтрокаТаблицы.СтавкаНДС		= СведенияОНоменклатуре.СтавкаНДС;
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТаблицы, 1);
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, ДанныеОбъекта.СуммаВключаетНДС);
	
	РеквизитыСчетовНаОплату = Новый Соответствие;
	Если ЗначениеЗаполнено(СтрокаТаблицы.СчетНаОплатуПокупателю) Тогда
		
		РеквизитыСчетовНаОплату.Вставить(
			СтрокаТаблицы.СчетНаОплатуПокупателю, 
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицы.СчетНаОплатуПокупателю, "Номер, Дата"));
	
	КонецЕсли; 
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыАгентскиеУслуги(СтрокаТаблицы, РеквизитыСчетовНаОплату, ДанныеОбъекта);

КонецПроцедуры

&НаСервереБезКонтекста
Функция АгентскиеУслугиКонтрагентПриИзмененииНаСервере(Знач ДанныеОбъекта, Знач ДанныеСтроки, Знач СчетаУчетаКЗаполнению)

	Изменения = Новый Структура;
	
	ВидыДоговоров = Новый Массив;
	ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);

	ДоговорКонтрагента = ДанныеСтроки.ДоговорКонтрагента;
	Если РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(
		ДоговорКонтрагента, 
		ДанныеСтроки.Контрагент, 
		ДанныеОбъекта.Организация, 
		ВидыДоговоров) Тогда
		
		Изменения.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
		
	Иначе
		
		СчетаУчетаКЗаполнению.Удалить("ДоговорКонтрагента");
		
	КонецЕсли;

	ИзмененияСчетовУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.РеализацияТоваровУслуг,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"АгентскиеУслуги",
		ДанныеСтроки);
		
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Изменения, ИзмененияСчетовУчета, Истина);
	
	Возврат Изменения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	
	Объект = Форма.Объект;
	
	Форма.ИтогиВсего    = Объект.Товары.Итог("Всего");
	Форма.ИтогиВсегоНДС = Объект.Товары.Итог("СуммаНДС");
	
	Если НЕ Форма.ЭтоОтгрузка Тогда
		Форма.ИтогиВсего = Форма.ИтогиВсего
			+ Объект.Услуги.Итог("Всего")
			+ Объект.АгентскиеУслуги.Итог("Всего");
		Форма.ИтогиВсегоНДС = Форма.ИтогиВсегоНДС
			+ Объект.Услуги.Итог("СуммаНДС")
			+ Объект.АгентскиеУслуги.Итог("СуммаНДС");
	Иначе
		Форма.ИтогиВсего = Форма.ИтогиВсего
			+ Объект.Услуги.Итог("Всего");
		Форма.ИтогиВсегоНДС = Форма.ИтогиВсегоНДС
			+ Объект.Услуги.Итог("СуммаНДС");
	КонецЕсли;

	Форма.ЕстьСтрокиВозвратнойТары   = Объект.ВозвратнаяТара.Количество() > 0;
	Форма.ОтметкаНезаполненногоСклад = Форма.ЕстьСтрокиВозвратнойТары ИЛИ Объект.Товары.Количество() > 0;
	
КонецПроцедуры

// Прочий функционал:

&НаСервере
Процедура ЗаполнитьТЧПоСчетуНаОплату()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьПоСчету("Товары, Услуги, ВозвратнаяТара", 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.СчетНаОплатуПокупателю)); // Счета учета заполняются здесь
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	Если ЕстьУчетПодакцизныхТоваров Тогда
		ПараметрыЗаполнения = Новый Структура;
		УчетАкцизов.ПараметрыДляЗаполненияДанныхПоАкцизам(ПараметрыЗаполнения);
		ПараметрыЗаполнения.ЭкспортПриРеализации = ЭкспортПриРеализации;
		ПараметрыЗаполнения.ЭтоПередачаТоваров   = ЭтоКомиссия;
		УчетАкцизов.ЗаполнитьДанныеПоАкцизам(Объект.Товары, Объект.Организация, Объект.Дата, ПараметрыЗаполнения);
	КонецЕсли;
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ЗаполнитьОтображениеСубконтоСчетаДоходов();
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция СписокВидимыхТабличныхЧастей()

	СписокТЧ = Новый СписокЗначений;
	
	СписокТЧ.Добавить("Товары", "Товары");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
		СписокТЧ.Добавить("ВозвратнаяТара", "ВозвратнаяТара");
	КонецЕсли;
	Если НЕ (ЭтоКомиссия ИЛИ ПокупательНалоговыйАгентПоНДС = Истина) Тогда
		СписокТЧ.Добавить("Услуги", "Услуги");
		СписокТЧ.Добавить("АгентскиеУслуги", "АгентскиеУслуги");
	КонецЕсли;
	
	Возврат СписокТЧ;

КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(Форма, СтрокаТаблицы, Знач РеквизитыСчетовНаОплату, Знач ПараметрыОбъекта)

	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
	Если ЗначениеЗаполнено(СтрокаТаблицы.СчетНаОплатуПокупателю) Тогда
		РеквизитыСчетаНаОплату = РеквизитыСчетовНаОплату[СтрокаТаблицы.СчетНаОплатуПокупателю];
		
		СтрокаТаблицы.СчетНаОплатуПокупателюПредставление = РеализацияТоваровУслугФормы.ПредставлениеСчетаНаОплату(РеквизитыСчетаНаОплату);
	Иначе
		СтрокаТаблицы.СчетНаОплатуПокупателюПредставление =  "";
	КонецЕсли; 
	
	Если ПравоДоступа("Чтение", Метаданные.ПланыСчетов.Хозрасчетный) Тогда
	
		Если ТипЗнч(СтрокаТаблицы) <> Тип("Структура") 
			Или СтрокаТаблицы.Свойство("СчетДоходов") Тогда
			// Могут отсутствовать свойства, связанные со счетами
			СтрокаТаблицы.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам	=
				БухгалтерскийУчетВызовСервераПовтИсп.НаСчетеВедетсяУчетПоНоменклатурнымГруппам(СтрокаТаблицы.СчетДоходов);
		КонецЕсли;
			
		Если ТипЗнч(СтрокаТаблицы) <> Тип("Структура") 
			Или СтрокаТаблицы.Свойство("СчетУчета") Тогда
			// Могут отсутствовать свойства, связанные со счетами
			СвойстваСчетаУчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетУчета);
			СтрокаТаблицы.СчетУчетаЗабалансовый = СвойстваСчетаУчета.Забалансовый;
			
			СтрокаТаблицы.СчетУчетаПартионный = СвойстваСчетаУчета.ВидСубконто1 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии
				ИЛИ СвойстваСчетаУчета.ВидСубконто2 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии
				ИЛИ СвойстваСчетаУчета.ВидСубконто3 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии;
				
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьАналитикуНДС(СтрокаТаблицы, ПараметрыОбъекта);
	ПрослеживаемостьФормыКлиентСерверБП.ЗаполнитьПредставлениеРНПТ(Форма, СтрокаТаблицы);
	ПроверкаРНПТ.ЗаполнитьСостояниеПроверкиРНПТПоУмолчанию(Форма, СтрокаТаблицы);
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПризнакМаркируемойПродукцииТаблицыТовары(Товары, ИспользоватьКонтрольныеЗнакиГИСМ, ВестиУчетМаркируемойПродукцииИСМП)
	
	Если (Не ИспользоватьКонтрольныеЗнакиГИСМ) Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеМаркируемаяПродукция = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ОбщегоНазначения.ВыгрузитьКолонку(Товары, "Номенклатура", Истина), "ВидМаркировки");
	Для Каждого СтрокаТаблицы Из Товары Цикл
		СтрокаНоменклатуры = СоответствиеМаркируемаяПродукция.Получить(СтрокаТаблицы.Номенклатура);
		Если СтрокаНоменклатуры = Неопределено Тогда
			ПараметрыМаркируемойПродукции = Новый Структура("ПродукцияМаркируемаяДляГИСМ" , Ложь);
		Иначе
			ПараметрыМаркируемойПродукции = Новый Структура("ПродукцияМаркируемаяДляГИСМ" , 
				СтрокаНоменклатуры.ВидМаркировки = Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ПараметрыМаркируемойПродукции);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, Знач РеквизитыСчетовНаОплату, Знач ПараметрыОбъекта)

	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
	Если ЗначениеЗаполнено(СтрокаТаблицы.СчетНаОплатуПокупателю) Тогда
		РеквизитыСчетаНаОплату = РеквизитыСчетовНаОплату[СтрокаТаблицы.СчетНаОплатуПокупателю];
		
		СтрокаТаблицы.СчетНаОплатуПокупателюПредставление = РеализацияТоваровУслугФормы.ПредставлениеСчетаНаОплату(РеквизитыСчетаНаОплату);
	Иначе
		СтрокаТаблицы.СчетНаОплатуПокупателюПредставление =  "";
	КонецЕсли; 
	
	Если СтрокаТаблицы.Свойство("СчетДоходов") И ПравоДоступа("Чтение", Метаданные.ПланыСчетов.Хозрасчетный) Тогда
		СтрокаТаблицы.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам	=
			БухгалтерскийУчетВызовСервераПовтИсп.НаСчетеВедетсяУчетПоНоменклатурнымГруппам(СтрокаТаблицы.СчетДоходов);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыАгентскиеУслуги(СтрокаТаблицы, Знач РеквизитыСчетовНаОплату, Знач ПараметрыОбъекта)

	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
	Если ЗначениеЗаполнено(СтрокаТаблицы.СчетНаОплатуПокупателю) Тогда
		РеквизитыСчетаНаОплату = РеквизитыСчетовНаОплату[СтрокаТаблицы.СчетНаОплатуПокупателю];
		
		СтрокаТаблицы.СчетНаОплатуПокупателюПредставление = РеализацияТоваровУслугФормы.ПредставлениеСчетаНаОплату(РеквизитыСчетаНаОплату);
	Иначе
		СтрокаТаблицы.СчетНаОплатуПокупателюПредставление =  "";
	КонецЕсли; 

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыВозвратнаяТара(СтрокаТаблицы, Знач РеквизитыСчетовНаОплату, Знач ПараметрыОбъекта)

	Если ЗначениеЗаполнено(СтрокаТаблицы.СчетНаОплатуПокупателю) Тогда
		РеквизитыСчетаНаОплату = РеквизитыСчетовНаОплату[СтрокаТаблицы.СчетНаОплатуПокупателю];
		
		СтрокаТаблицы.СчетНаОплатуПокупателюПредставление = РеализацияТоваровУслугФормы.ПредставлениеСчетаНаОплату(РеквизитыСчетаНаОплату);
	Иначе
		СтрокаТаблицы.СчетНаОплатуПокупателюПредставление =  "";
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтображениеСубконтоСчетаДоходов(ИмяТаблицы = "");

	Если ИспользоватьОднуНоменклатурнуюГруппу Тогда
		Если ПустаяСтрока(ИмяТаблицы) ИЛИ ИмяТаблицы = "Товары" Тогда
			ОтобразитьСубконтоСчетаДоходовТовары = СчетаУчетаВДокументах.ОтобразитьСубконтоСчетаДоходов(Объект.Товары);
		КонецЕсли;
		Если ПустаяСтрока(ИмяТаблицы) ИЛИ ИмяТаблицы = "Услуги" Тогда
			ОтобразитьСубконтоСчетаДоходовУслуги = СчетаУчетаВДокументах.ОтобразитьСубконтоСчетаДоходов(Объект.Услуги);
		КонецЕсли;
	Иначе
		ОтобразитьСубконтоСчетаДоходовТовары = Истина;
		ОтобразитьСубконтоСчетаДоходовУслуги = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(Знач Форма, ПараметрыОбъекта)

	Объект = Форма.Объект;
	
	Если ТипЗнч(ПараметрыОбъекта) <> Тип("Структура") Тогда
		ПараметрыОбъекта = Новый Структура;
	КонецЕсли;
	
	ПараметрыОбъекта.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
	ПараметрыОбъекта.Вставить("ВалютаДокумента",  Объект.ВалютаДокумента);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииЦеныИВалютыНаСервере(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь)

	РеализацияТоваровУслугФормы.ЗаполнитьРассчитатьСуммы(
		ЭтотОбъект,
		ВалютаДоИзменения,
		КурсДоИзменения,
		КратностьДоИзменения,
		ПерезаполнитьЦены,
		ПересчитатьЦены,
		ПересчитатьНДС);
		
	ЗаполнитьДобавленныеКолонкиТаблиц();
	ОбновитьИтоги(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПорядокУчетаРасчетовНаСервере(ВыбранноеЗначение)

	РеализацияТоваровУслугФормы.ОбработкаВыбораПорядокУчетаРасчетов(ЭтотОбъект, ВыбранноеЗначение);
	
	ЗаполнитьДобавленныеКолонкиТаблиц();

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОповещениеОбИзмененииБанковскогоСчетаИлиОрганизации()
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчетОрганизации) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;
	
	УстановитьФункциональныеОпцииФормы();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличнуюЧастьПоСчету(ТабличнаяЧасть)
	
	Если Объект[ТабличнаяЧасть].Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Перед заполнением табличная часть будет очищена. Заполнить?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗаполнениемТабличнойЧастиЗавершение", ЭтотОбъект, ТабличнаяЧасть);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыборСчетаНаОплатуЗавершение", ЭтотОбъект, Новый Структура("ИмяТабличнойЧасти", ТабличнаяЧасть));
		РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораСчетаНаОплату(ЭтотОбъект, Ложь, ОповещениеОЗавершении); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокументПоСчету()
	
	Если Объект.Товары.Количество() = 0 
		И Объект.Услуги.Количество() = 0 
		И Объект.ВозвратнаяТара.Количество() = 0 Тогда
		
		ОбработатьВыборСчетаНаОплату();
	
	Иначе
	
		ТекстВопроса = "Заполнить документ по счету?";
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьДокументПоСчетуЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборСчетаНаОплатуНаСервере(ЗаполнитьТабличнуюЧасть)
	
	РеквизитыСчетаНаОплату = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СчетНаОплатуПокупателю, "Контрагент, ДоговорКонтрагента");
	
	Если ЗначениеЗаполнено(РеквизитыСчетаНаОплату.Контрагент) 
		И Объект.Контрагент <> РеквизитыСчетаНаОплату.Контрагент Тогда
		Объект.Контрагент = РеквизитыСчетаНаОплату.Контрагент;
		Если ЗначениеЗаполнено(РеквизитыСчетаНаОплату.ДоговорКонтрагента) Тогда
			Объект.ДоговорКонтрагента = РеквизитыСчетаНаОплату.ДоговорКонтрагента;
		КонецЕсли;
		КонтрагентПриИзмененииНаСервере();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыСчетаНаОплату.ДоговорКонтрагента) 
		И Объект.ДоговорКонтрагента <> РеквизитыСчетаНаОплату.ДоговорКонтрагента Тогда
		Объект.ДоговорКонтрагента 		= РеквизитыСчетаНаОплату.ДоговорКонтрагента;
		ДоговорКонтрагентаПриИзмененииНаСервере();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		МассивВидовДоговоров   = РеализацияТоваровУслугФормы.ВидыДоговоровДокумента(Объект.ВидОперации);
		ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорКонтрагента, "ВидДоговора");
		
		Если МассивВидовДоговоров.Найти(ВидДоговораКонтрагента) = Неопределено Тогда
			Объект.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
		
	Если ЗаполнитьТабличнуюЧасть Тогда
		ЗаполнитьТЧПоСчетуНаОплату();
	КонецЕсли; 

КонецПроцедуры 

&НаКлиенте
Процедура ОбработатьВыборСчетаНаОплату(ЗаполнитьТабличнуюЧасть = Истина)

	ОбработатьВыборСчетаНаОплатуНаСервере(ЗаполнитьТабличнуюЧасть);
	
	РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыСчетНаОплату(ЭтотОбъект);
	ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);

	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.Контрагент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗаполнениемТабличнойЧастиЗавершение(Результат, ТабличнаяЧасть) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект[ТабличнаяЧасть].Очистить();
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыборСчетаНаОплатуЗавершение", ЭтотОбъект, Новый Структура("ИмяТабличнойЧасти", ТабличнаяЧасть));
		РеализацияТоваровУслугФормыКлиент.ОткрытьФормуВыбораСчетаНаОплату(ЭтотОбъект, Ложь, ОповещениеОЗавершении); 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаЗаполненияТабличнойЧастиНаСервере(ВыбранноеЗначение, ТабличнаяЧасть)

	Если Объект.ВидОперации = Перечисления.ВидыОперацийРеализацияТоваров.Оборудование
		И ТабличнаяЧасть = "Товары" Тогда
		ТабличнаяЧастьИсточника = "Оборудование";
	Иначе
		ТабличнаяЧастьИсточника = ТабличнаяЧасть;
	КонецЕсли;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьПоПоступлению(ТабличнаяЧасть, "Добавить", ВыбранноеЗначение, ТабличнаяЧастьИсточника);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ОбработкаЗаполненияПоСпискуСчетовНаСервере(МассивСчетов, ТабличнаяЧасть, ОчищатьСтроки)
	
	РеализацияТоваровУслугФормы.ПриДобавленииСчетов(ЭтотОбъект, МассивСчетов);

	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	ДокументОбъект.ЗаполнитьПоСчету(ТабличнаяЧасть, МассивСчетов, ОчищатьСтроки);
	
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Функция ПоместитьТоварыВоВременноеХранилищеНаСервере()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметры)
	
	РеализацияТоваровУслугФормы.ОбработкаОповещенияОбработкиТабличнойЧастиТовары(ЭтотОбъект, Параметры);
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьЗачетАвансовВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(Объект.ЗачетАвансов.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаКлиенте
Процедура ВопросЗаполнитьДокументПоСчетуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработатьВыборСчетаНаОплату(Результат = КодВозвратаДиалога.Да);
	ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПатентОбработкаВыбора(ВыбранныйПатент)
	
	Если ТипЗнч(ВыбранныйПатент) <> Тип("ДокументСсылка.ОперацияСПатентом") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПатентОбработкаВыбораНаСервере(ВыбранныйПатент);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПатентОбработкаВыбораНаСервере(ВыбранныйПатент)
	
	ВидДеятельностиПатента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранныйПатент, "Патент");
	
	Если Не УчетПСН.ПатентДоступенДляВыбора(ВидДеятельностиПатента, Объект.Организация, Объект.Дата) Тогда
		ВидДеятельностиПатента = Справочники.Патенты.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидДеятельностиПатента) Тогда
		Объект.ДеятельностьНаПатенте       = Истина;
		Объект.Патент                      = ВидДеятельностиПатента;
		
		ДеятельностьНаПатентеПриИзмененииНаСервере();
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	ДополнительныеПараметры = Неопределено;
	СобытияФормИСКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзСчетаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	СписокТабличныхЧастей = "Товары,Услуги,ВозвратнаяТара";
	
	Если Результат.УдаленныеСчета.Количество() > 0 Тогда
		Если Результат.РезультатВыбора = КодВозвратаДиалога.Да Тогда
			РеализацияТоваровУслугФормыКлиент.УдалитьСчетаИзТабличныхЧастей(ЭтотОбъект, Результат.УдаленныеСчета, СписокТабличныхЧастей);
		ИначеЕсли Результат.РезультатВыбора = КодВозвратаДиалога.Нет Тогда
			РеализацияТоваровУслугФормыКлиент.ОчиститьСсылкиНаСчетаВТабличныхЧастях(ЭтотОбъект, Результат.УдаленныеСчета, СписокТабличныхЧастей);
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Результат.УдаленныеСчета.Количество() > 0 
		ИЛИ Результат.ДобавленныеСчета.Количество() > 0;
	
	Если Результат.ДобавленныеСчета.Количество() > 0 Тогда
		ДобавитьИзСчетаНаСервере(СписокТабличныхЧастей, Результат.ДобавленныеСчета);
	КонецЕсли; 
	
	РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыСчетНаОплату(ЭтотОбъект);
	
	ОбновитьИтоги(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ДобавитьИзСчетаНаСервере(СписокТабличныхЧастей, МассивСчетов)
	
	// Если есть счет по документу, и добавляем еще один, то перенесем счет в строки
	Если ЗначениеЗаполнено(Объект.СчетНаОплатуПокупателю) Тогда
		РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьСчетНаОплатуПоСтрокам(ЭтотОбъект, Объект.СчетНаОплатуПокупателю);
		Объект.СчетНаОплатуПокупателю = Неопределено;
	КонецЕсли;
	
	РеализацияТоваровУслугФормы.ПриДобавленииСчетов(ЭтотОбъект, МассивСчетов);
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьПоСчету(СписокТабличныхЧастей, МассивСчетов, Ложь);// Счета учета заполняются здесь
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
	Если ЕстьУчетПодакцизныхТоваров Тогда
		ПараметрыЗаполнения = Новый Структура;
		УчетАкцизов.ПараметрыДляЗаполненияДанныхПоАкцизам(ПараметрыЗаполнения);
		ПараметрыЗаполнения.ЭкспортПриРеализации = ЭкспортПриРеализации;
		ПараметрыЗаполнения.ЭтоПередачаТоваров   = ЭтоКомиссия;
		УчетАкцизов.ЗаполнитьДанныеПоАкцизам(Объект.Товары, Объект.Организация, Объект.Дата, ПараметрыЗаполнения);
	КонецЕсли;
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
КонецПроцедуры

&НаКлиенте
Процедура СчетНаОплатуГиперссылкаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОбработчикВыбора = Новый ОписаниеОповещения("ДобавитьИзСчетаЗавершение", ЭтотОбъект, Новый Структура);
	
	РеализацияТоваровУслугФормыКлиент.ИзменитьСписокДокументовОснований(ЭтотОбъект, ОбработчикВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ВыборСчетаНаОплатуЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") 
		И ВыбранноеЗначение.Количество() > 1 Тогда
		
		ОбработкаЗаполненияПоСпискуСчетовНаСервере(ВыбранноеЗначение, ДополнительныеПараметры.ИмяТабличнойЧасти, Истина);
		
		РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыСчетНаОплату(ЭтотОбъект);
		
		ПодключитьОбработчикОжидания("Подключаемый_НапоминатьОбУведомленииОПеремещении", 0.1, Истина);

	Иначе
		
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
			Объект.СчетНаОплатуПокупателю = ВыбранноеЗначение[0];
		Иначе
			Объект.СчетНаОплатуПокупателю = ВыбранноеЗначение;
		КонецЕсли; 
		
		ЗаполнитьДокументПоСчету();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараСчетНаОплатуПокупателюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюНачалоВыбора(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараСчетНаОплатуПокупателюОчистка(Элемент, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюОчистка(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараСчетНаОплатуПокупателюОткрытие(Элемент, СтандартнаяОбработка)
	РеализацияТоваровУслугФормыКлиент.ТабличнаяЧастьСчетНаОплатуПокупателюОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокАдресовДоставкиНаСервере()
	РеализацияТоваровУслугФормы.ЗаполнитьСписокАдресовДоставки(ЭтотОбъект, Объект.Контрагент, Объект.Грузополучатель, Объект.Дата);
КонецПроцедуры 

&НаСервере
Процедура УстановитьПараметрыДоставки()
	ЕстьДоставка = Ложь;
	ЕстьНомерДляОтслеживания = Ложь;
	
	Если Объект.СпособДоставки.Пустая() Тогда
		Возврат;
	КонецЕсли; 
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СпособДоставки, "ИмяПредопределенныхДанных, ИспользоватьНомерДляОтслеживания");
	
	ЕстьДоставка = (Реквизиты.ИмяПредопределенныхДанных <> "Самовывоз");
	ЕстьНомерДляОтслеживания = Реквизиты.ИспользоватьНомерДляОтслеживания;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОрганизации()

	СписокВыбора = Новый Массив;
	СписокВыбора.Добавить(Справочники.Организации.ПустаяСсылка());
	СписокВыбора.Добавить(Объект.Организация);
	
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Организация", Новый ФиксированныйМассив(СписокВыбора)));
	
	Элементы.СпособДоставки.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);

КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыУсловияДоставки(УсловияДоставки)
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УсловияДоставки, "ИмяПредопределенныхДанных, ИспользоватьНомерДляОтслеживания, УслугаДоставки, Контрагент");
КонецФункции

&НаКлиенте
Процедура УсловияДоставкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
	
		НоваяСтрокаУслуги = Объект.Услуги.Добавить();
		НоваяСтрокаУслуги.Номенклатура = ДополнительныеПараметры.УслугаДоставки;
		
		УслугиНоменклатураПриИзмененииНаКлиенте(НоваяСтрокаУслуги);
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияНаСервере()
	
	РеализацияТоваровУслугФормы.ОбработкаОповещенияНаСервере(ЭтотОбъект);
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьАналитикуНДС(СтрокаТабличнойЧасти, ПараметрыОбъекта)
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СуммаНДС) Тогда
		
		ТекстСообщения = НСтр("ru='%1 %2 (%3)'");
		СтрокаТабличнойЧасти.АналитикаНДС = СтрШаблон(ТекстСообщения,
			СтрокаТабличнойЧасти.СуммаНДС,
			ПараметрыОбъекта.ВалютаДокумента,
			СтрокаТабличнойЧасти.СтавкаНДС);
		
	ИначеЕсли СтрокаТабличнойЧасти.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0") Тогда
		СтрокаТабличнойЧасти.АналитикаНДС = НСтр("ru='НДС 0%'");
	ИначеЕсли СтрокаТабличнойЧасти.Сумма = 0 Тогда
		СтрокаТабличнойЧасти.АналитикаНДС = НСтр("ru=''");
	Иначе
		СтрокаТабличнойЧасти.АналитикаНДС = НСтр("ru='Без НДС'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораАналитикиНДСНаКлиенте(ЗначенияВыбранныеВФормеРедактированияНалоговойБазыНДС)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ЗначенияВыбранныеВФормеРедактированияНалоговойБазыНДС);
	
	Модифицированность = Истина;
	
	Если СтрокаТабличнойЧасти.Свойство("АналитикаНДС") Тогда
		ПараметрыОбъекта = Неопределено;
		ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);
		ЗаполнитьАналитикуНДС(СтрокаТабличнойЧасти, ПараметрыОбъекта);
	КонецЕсли;
	
	Если СтрокаТабличнойЧасти.Свойство("Количество") Тогда
		Если СтрокаТабличнойЧасти.Количество = 0 Тогда
			СтрокаТабличнойЧасти.Цена = 0;
		Иначе
			СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияНДС(ВыбранноеЗначение)

	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Не ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;

	ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ВыбранноеЗначение);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",         ТолькоПросмотр);
	ПараметрыФормы.Вставить("Номенклатура",           ДанныеСтроки.Номенклатура);
	ПараметрыФормы.Вставить("Организация",            Объект.Организация);
	ПараметрыФормы.Вставить("Дата",                   Объект.Дата);
	ПараметрыФормы.Вставить("ВалютаДокумента",        Объект.ВалютаДокумента);
	ПараметрыФормы.Вставить("ВалютаРеглУчета",        ЭтотОбъект.ВалютаРегламентированногоУчета);
	ПараметрыФормы.Вставить("Сумма",                  ДанныеСтроки.Сумма);
	ПараметрыФормы.Вставить("НалоговаяБазаНДС",       ДанныеСтроки.НалоговаяБазаНДС);
	ПараметрыФормы.Вставить("СтавкаНДС",              ДанныеСтроки.СтавкаНДС);
	ПараметрыФормы.Вставить("СуммаВключаетНДС",       Объект.СуммаВключаетНДС);
	ПараметрыФормы.Вставить("СуммаНДС",               ДанныеСтроки.СуммаНДС);
	ПараметрыФормы.Вставить("Всего",                  ДанныеСтроки.Всего);
	
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.Форма.ФормаРедактированияНалоговойБазыНДС", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

#Область ПроверкаКонтрагентов

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания) Экспорт	
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтотОбъект, ПараметрыФоновогоЗадания);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область СчетаУчета

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив();
	ЭлементыСчетов.Добавить("ТоварыСчетУчета");
	ЭлементыСчетов.Добавить("ТоварыПереданныеСчетУчета");
	ЭлементыСчетов.Добавить("ТоварыСчетДоходов");
	ЭлементыСчетов.Добавить("ТоварыСубконто");
	ЭлементыСчетов.Добавить("ТоварыСчетУчетаНДСПоРеализации");
	ЭлементыСчетов.Добавить("ТоварыСчетРасходов");
	ЭлементыСчетов.Добавить("ТоварыСчетАкциза");
	ЭлементыСчетов.Добавить("ТоварыСубконтоАкцизы");
	ЭлементыСчетов.Добавить("ВозвратнаяТараСчетУчета");
	Если НЕ УчитыватьТаруЗаБалансом Тогда
		ЭлементыСчетов.Добавить("СчетУчетаРасчетовПоТаре");
	КонецЕсли;
	ЭлементыСчетов.Добавить("УслугиСчетДоходов");
	ЭлементыСчетов.Добавить("УслугиСубконто");
	ЭлементыСчетов.Добавить("УслугиСчетУчетаНДСПоРеализации");
	ЭлементыСчетов.Добавить("УслугиСчетРасходов");
	ЭлементыСчетов.Добавить("АгентскиеУслугиСчетРасчетов");
	
	// Поле формы ПорядокУчетаРасчетов содержит счета и другие реквизиты.
	// Поэтому видимостью этого поля управляем в УчетВзаиморасчетовФормы.УстановитьПорядокУчетаРасчетов()
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
КонецПроцедуры

#КонецОбласти

#Область СтатусыДокументов

&НаСервереБезКонтекста
Функция ПолучитьСтатусДокумента(Знач Организация, Знач Ссылка)
	
	Возврат (РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокумента(Ссылка, Организация).Статус =
		Перечисления.СтатусыДокументовРеализации.Подписан);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаписатьСтатусДокументаНаСервере(ДокументСсылка, СтатусДокумента)
	
	СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
	СтатусыДокумента.Статус = Документы.РеализацияТоваровУслуг.СтатусДокументаПеречислением(СтатусДокумента);
	РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(ДокументСсылка, СтатусыДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияИС()
	
	СоответствиеТребованиямГИСМТКлиент.ПроверитьВыполнениеЗадания(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ПодразделениеОрганизацииПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПодразделениеОрганизацииПриИзмененииНаСервере()
	
	ОтветственныеЛицаБП.УстановитьОтветственныхЛиц(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодписиНажатие(Элемент)
	
	Если НЕ ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	СчетаНаОплату = РеализацияТоваровУслугФормыКлиентСервер.СписокСчетовНаОплатуПокупателю(Объект);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр", 					ТолькоПросмотр);
	ПараметрыФормы.Вставить("Организация", 						Объект.Организация);
	ПараметрыФормы.Вставить("Контрагент",						Объект.Контрагент);
	ПараметрыФормы.Вставить("ДатаДокумента",					Объект.Дата);
	ПараметрыФормы.Вставить("Руководитель",						Объект.Руководитель);
	ПараметрыФормы.Вставить("ЗаРуководителяНаОсновании", 		Объект.ЗаРуководителяНаОсновании);
	ПараметрыФормы.Вставить("ГлавныйБухгалтер",					Объект.ГлавныйБухгалтер);
	ПараметрыФормы.Вставить("ЗаГлавногоБухгалтераНаОсновании",	Объект.ЗаГлавногоБухгалтераНаОсновании);
	ПараметрыФормы.Вставить("ОтпускПроизвел",					Объект.ОтпускПроизвел);
	ПараметрыФормы.Вставить("ДоверенностьНомер",				Объект.ДоверенностьНомер);
	ПараметрыФормы.Вставить("ДоверенностьДата",					Объект.ДоверенностьДата);
	ПараметрыФормы.Вставить("ДоверенностьВыдана",				Объект.ДоверенностьВыдана);
	ПараметрыФормы.Вставить("ДоверенностьЧерезКого",			Объект.ДоверенностьЧерезКого);
	ПараметрыФормы.Вставить("ЗаЗаказчикаНаОсновании",			Объект.ЗаЗаказчикаНаОсновании);
	ПараметрыФормы.Вставить("ОтветственныйЗаОформление",		Объект.ОтветственныйЗаОформление);
	ПараметрыФормы.Вставить("ДоступноОформлениеУслуг",			Истина);
	ПараметрыФормы.Вставить("СчетаНаОплату",					СчетаНаОплату);
	ПараметрыФормы.Вставить("РежимЗаполненияОснованияПередачи",	Объект.РежимЗаполненияОснованияПередачи);
	ПараметрыФормы.Вставить("ОснованиеПередачи",				Объект.ОснованиеПередачи);
	ПараметрыФормы.Вставить("ДоговорКонтрагента",				Объект.ДоговорКонтрагента);
	
	ОткрытьФорму("Документ.РеализацияТоваровУслуг.Форма.ФормаПодписиТовары", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДоставкаНажатие(Элемент)
	
	РеализацияТоваровУслугФормыКлиент.ДоставкаНажатие(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусДокументаПриИзменении(Элемент)
	
	УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(
		ЭтотОбъект, СтатусДокумента);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСрокОплатыНаСервере()
	
	РеализацияТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КоличествоСтрок = Элементы[ИмяТаблицы].ВыделенныеСтроки.Количество();
	Если КоличествоСтрок = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	СкопироватьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОКопированииСтрокВБуферОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	КоличествоСтрок = ВставитьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОВставкеСтрокИзБуфераОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИмяТаблицы)

	ДобавленныеСтроки = РеализацияТоваровУслугФормы.ОбработкаВыбораПодборВставкаИзБуфера(ЭтотОбъект, ВыбранноеЗначение, ИмяТаблицы);
	
	ЗаполнитьДобавленныеКолонкиТаблиц(ИмяТаблицы);
	
	ЗаполнитьОтображениеСубконтоСчетаДоходов(ИмяТаблицы);
	
	ОбновитьИтоги(ЭтотОбъект);
	
	РеализацияТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыСчетНаОплату(ЭтотОбъект);
	
	МассивОснований = Новый Массив;
	Если ДобавленныеСтроки[ИмяТаблицы].Количество() > 0 
		И ЗначениеЗаполнено(ДобавленныеСтроки[ИмяТаблицы][0].СчетНаОплатуПокупателю) Тогда
	
		МассивОснований.Добавить(ДобавленныеСтроки[ИмяТаблицы][0].СчетНаОплатуПокупателю);
	КонецЕсли; 
	
	РеализацияТоваровУслугФормы.ПриДобавленииСчетов(ЭтотОбъект, МассивОснований);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеОригиналаБПНажатие(Элемент)
	
	Если СтатусДокумента <> ПолучитьСтатусДокумента(Объект.Организация, Объект.Ссылка) Тогда
		// Пользователь изменил статус документа.
		// Перед открытием оригиналов первичных документов БСП нужно сначала сохранить статус,
		// чтобы обновились статусы первичных документов БСП.
		ЗаписатьСтатусДокументаНаСервере(Объект.Ссылка, СтатусДокумента);
	КонецЕсли;
	
	УчетОригиналовПервичныхДокументовКлиент.ОткрытьФормуИзмененияСостоянийПечатныхФорм(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область КопированиеВставкаСтрокЧерезБуферОбмена

&НаСервере
Процедура СкопироватьСтрокиНаСервере(ИмяТаблицы)
	
	ОбщегоНазначения.СкопироватьСтрокиВБуферОбмена(Объект[ИмяТаблицы], 
		Элементы[ИмяТаблицы].ВыделенныеСтроки, Объект.Ссылка.Метаданные().Имя);

КонецПроцедуры

&НаСервере
Функция ВставитьСтрокиНаСервере(ИмяТаблицы)
	
	ПараметрыВставки = ОбработкаТабличныхЧастей.ПолучитьПараметрыВставкиДанныхИзБуфераОбмена(Объект.Ссылка, ИмяТаблицы);
	ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки);
	ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ПараметрыВставки, ПараметрыВставки.ИмяТаблицы);
	
	Возврат ПараметрыВставки.КоличествоДобавленныхСтрок;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИмяТекущейТабличнойЧасти()
	
	ИмяТекущейСтраницы = Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя;
	ИмяТаблицы = "";
	Если ИмяТекущейСтраницы = "ГруппаТовары" Тогда
		
		ИмяТаблицы = "Товары";
		
	ИначеЕсли ИмяТекущейСтраницы = "ГруппаУслуги" Тогда
		
		ИмяТаблицы = "Услуги";
		
	КонецЕсли;
	
	Возврат ИмяТаблицы;
	
КонецФункции

&НаСервере
Процедура ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки)
	
	СписокСвойств = Новый Массив;
	
	Если ПараметрыВставки.ИмяТаблицы = "Товары" Тогда
		
		СписокСвойств.Добавить("Номенклатура");
		СписокСвойств.Добавить("КоличествоМест");
		СписокСвойств.Добавить("ЕдиницаИзмерения");
		СписокСвойств.Добавить("Коэффициент");
		СписокСвойств.Добавить("Количество");
		СписокСвойств.Добавить("Цена");
		СписокСвойств.Добавить("СтавкаНДС");
		СписокСвойств.Добавить("НомерГТД");
		СписокСвойств.Добавить("СтранаПроисхождения");
		СписокСвойств.Добавить("СчетНаОплатуПокупателю");
		
		Если ПараметрыВставки.ПоказыватьСчетаУчетаВДокументах Тогда
			
			СписокСвойств.Добавить("СчетУчета");
			Если ПараметрыВставки.ИсточникИДокументОдногоВида Тогда
				
				СписокСвойств.Добавить("СчетДоходов");
				СписокСвойств.Добавить("Субконто");
				СписокСвойств.Добавить("СчетУчетаНДСПоРеализации");
				СписокСвойств.Добавить("СчетРасходов");
				
			КонецЕсли;
			
		КонецЕсли;
	
	ИначеЕсли ПараметрыВставки.ИмяТаблицы = "Услуги" Тогда
	
		СписокСвойств.Добавить("Номенклатура");
		СписокСвойств.Добавить("Содержание");
		СписокСвойств.Добавить("Количество");
		СписокСвойств.Добавить("Цена");
		СписокСвойств.Добавить("СтавкаНДС");
		СписокСвойств.Добавить("СчетНаОплатуПокупателю");
		
		Если ПараметрыВставки.ПоказыватьСчетаУчетаВДокументах И ПараметрыВставки.ИсточникИДокументОдногоВида Тогда
			
			СписокСвойств.Добавить("СчетДоходов");
			СписокСвойств.Добавить("Субконто");
			СписокСвойств.Добавить("СчетУчетаНДСПоРеализации");
			СписокСвойств.Добавить("СчетРасходов");
		
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыВставки.СписокСвойств = ОбработкаТабличныхЧастей.ПолучитьСписокСвойствИмеющихсяВТаблицеДанных(
		ПараметрыВставки.Данные, СписокСвойств);
	
КонецПроцедуры
	
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандыВставки(Форма, Доступность)

	Доступность = Не Форма.ТолькоПросмотр И Доступность;
	Элементы = Форма.Элементы;
	Элементы.ТоварыВставитьСтроки.Доступность						 = Доступность;
	Элементы.ТоварыКонтекстноеМенюВставитьСтроки.Доступность		 = Доступность;
	Элементы.УслугиВставитьСтроки.Доступность						 = Доступность;
	Элементы.УслугиКонтекстноеМенюВставитьСтроки.Доступность		 = Доступность;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяНапоминаниеОбУведомленииОПеремещении(Форма)
	
	Объект = Форма.Объект;
	
	ПрослеживаемыеТовары = Объект.Товары.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	
	ЕстьПрослеживаемыеТовары = ПрослеживаемыеТовары.Количество() > 0;
	
	Возврат ЕстьПрослеживаемыеТовары И Форма.РеализацияВЕАЭС;
	
КонецФункции

&НаСервере
Процедура НапоминатьОбУведомленияхОПеремещении()
	
	УстановитьВидимость(ЭтотОбъект);
	Если ПоказыватьБаннер Тогда
		Если УведомлениеОПеремещенииПодаетсяВТомЧислеДоПересеченияГраницы Тогда
			Элементы.ДекорацияУведомление.Заголовок
				= НСтр("ru = 'В документе присутствуют прослеживаемые товары. Необходимо создать уведомление о перемещении в ЕАЭС и отправить в ФНС.'");
			Элементы.ДекорацияУведомление.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
		Иначе
			// Дата отсчета срока - 5 дней со дня продажи товара.
			СрокУведомления = ПрослеживаемостьБП.СрокПодачиУведомления(Объект.Дата);
			
			ШаблонТекста = НСтр("ru = 'В документе присутствуют прослеживаемые товары. До %1 необходимо создать уведомление о перемещении в ЕАЭС и отправить в ФНС.'");
			Элементы.ДекорацияУведомление.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонТекста, Формат(СрокУведомления, "ДФ='дд ММММ'"));
			Элементы.ДекорацияУведомление.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		КонецЕсли;
		
		Элементы.ГруппаБаннерУведомлениеОПеремещении.Видимость = Истина;
	Иначе
		Элементы.ГруппаБаннерУведомлениеОПеремещении.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбработкаВыбораГТДНаКлиенте(ВыбранноеЗначение)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		
		// Если в результате выбора пришло только значение,
		// то переложим его в структуру.
		Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.НомераГТД") Тогда
			ВыбранноеЗначение = Новый Структура("НомерГТД", ВыбранноеЗначение);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыбранноеЗначение);
	КонецЕсли;
	
	ПрослеживаемостьФормыКлиентСерверБП.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокаТабличнойЧасти);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораРНПТНаСервере(ИдентификаторСтроки, ВыбранноеЗначение)
	
	ПрослеживаемостьФормыБП.ОбработкаВыбораРНПТН(ЭтотОбъект, ИдентификаторСтроки, ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НапоминатьОбУведомленииОПеремещении()
	
	ТребуетсяНапоминаниеДо = ТребуетсяНапоминание;
	ПоказыватьБаннерДо     = ПоказыватьБаннер;
	
	ТребуетсяНапоминание = ТребуетсяНапоминаниеОбУведомленииОПеремещении(ЭтотОбъект);
	ПоказыватьБаннер     = ТребуетсяНапоминание 
							И НЕ ЗначениеЗаполнено(УведомлениеОПеремещении) 
							И НЕ ОтключитьБаннерУведомленийОПеремещении
							И НЕ Объект.ПометкаУдаления;
	
	Если ТребуетсяНапоминаниеДо <> ТребуетсяНапоминание
		ИЛИ ПоказыватьБаннерДо <> ПоказыватьБаннер 
		ИЛИ ОбновитьСрокОтправкиУведомлений Тогда
		
		НапоминатьОбУведомленияхОПеремещении();
		ОбновитьСрокОтправкиУведомлений = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдалитьСвязанныеЗаписи()

	УдалитьСвязанныеЗаписиНаСервере();
	ПроверкаРНПТКлиент.ЗапуститьПроверкуРНПТВДокументе(ЭтотОбъект);
	
	Если Объект.Товары.Количество()>0 Тогда
		
		// Переприсваиваем текущие элементы, чтобы положение курсора было в конце поля
		
		Если ВызовУдалитьСвязанныеЗаписи = "Номенклатура"
			И Элементы.Товары.ТекущийЭлемент.Имя = "ТоварыКоличество" Тогда
			
			Элементы.Товары.ТекущийЭлемент = Элементы.Товары.ПодчиненныеЭлементы["ТоварыЦена"];
			Элементы.Товары.ТекущийЭлемент = Элементы.Товары.ПодчиненныеЭлементы["ТоварыГруппаКоличество"].ПодчиненныеЭлементы["ТоварыКоличество"];
			Элементы.Товары.ИзменитьСтроку();
			ВызовУдалитьСвязанныеЗаписи = "";
			
		ИначеЕсли ВызовУдалитьСвязанныеЗаписи = "Количество"
			И Элементы.Товары.ТекущийЭлемент.Имя ="ТоварыЦена" Тогда
			
			Элементы.Товары.ТекущийЭлемент = Элементы.Товары.ПодчиненныеЭлементы["ТоварыСумма"]; 
			Элементы.Товары.ТекущийЭлемент = Элементы.Товары.ПодчиненныеЭлементы["ТоварыЦена"];
			Элементы.Товары.ИзменитьСтроку();
			ВызовУдалитьСвязанныеЗаписи = "";
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура УдалитьСвязанныеЗаписиНаСервере()
	
	Для каждого СтрокаКлюча Из ТаблицаУдаленныхСтрок Цикл
		СтруктураОтбора = Новый Структура("ИдентификаторСтроки", СтрокаКлюча.ИдентификаторСтроки);
		МассивСтрокСведенияПрослеживаемости = Объект.СведенияПрослеживаемости.НайтиСтроки(СтруктураОтбора);
		Для каждого СтрокаТЧ Из МассивСтрокСведенияПрослеживаемости Цикл
			Объект.СведенияПрослеживаемости.Удалить(СтрокаТЧ);
		КонецЦикла;
		СтрокиТоваров = Объект.Товары.НайтиСтроки(СтруктураОтбора);
		Если СтрокиТоваров.Количество() > 0 Тогда
			ПрослеживаемостьФормыКлиентСерверБП.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокиТоваров[0]);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаУдаленныхСтрок.Очистить();
	
	УстановитьВидимостьПроверкиРНПТ();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНомерНаПечать(Форма)
	
	Объект = Форма.Объект;
	
	Форма.НомерНаПечать = ?(ЗначениеЗаполнено(Объект.НомерВходящегоДокумента), Объект.НомерВходящегоДокумента, Объект.Номер);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимость(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ВидимостьРеквизитовГИСМ = Форма.ИспользоватьКонтрольныеЗнакиГИСМ И 
		(Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ПродажаКомиссия")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ПередачаТоваров")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Товары")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности"));
		
	Элементы.ТоварыКиЗ_ГИСМ.Видимость      = ВидимостьРеквизитовГИСМ;
	
	Элементы.ГруппаУведомлениеОПеремещении.Видимость = Форма.ТребуетсяНапоминание 
		И ПрослеживаемостьВызовСервера.МожноДобавлятьУведомленияОПеремещении();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеНадписиУведомлениеОПеремещении()
	
	ПрослеживаемостьФормыБП.УстановитьПредставлениеНадписиУведомлениеОПеремещении(ЭтотОбъект);
	
КонецПроцедуры

//ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС

&НаКлиенте
Процедура ЗаполнениеГосконтрактаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЭтотОбъект.ГосконтрактЕИСНеЗаполнен Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоГосконтракту();
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоГосконтракту() Экспорт
	
	ЭлектронноеАктированиеБП.ЗаполнитьПоГосконтракту(Объект, ЭтотОбъект.ГосконтрактЕИС, "Товары, Услуги");
	ЗаполнитьДобавленныеКолонкиТаблиц();
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеГосконтрактаЕИС()
	
	ЭлектронноеАктированиеБП.ДоговорКонтрагентаПриИзмененииНаСервере(ЭтотОбъект);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЕИСГосзакупкиОткрытьСписокФайлов(Команда)
	
	ЭлектронноеАктированиеЕИСКлиент.ОткрытьСписокПриложенныхФайловДокумента(Объект.Ссылка, ЭтотОбъект);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС

&НаСервере
Процедура ИзменитьВидимостьПодсказокНаСервере()
	
	УправлениеПанельюПодсказки.ИзменитьВидимостьПодсказок(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьЭлементПодсказкиНаФорме(ИмяЭлементаПодсказки) Экспорт
	
	СнятьВыделениеЭлементаПодсказкиНаФорме();
	
	Если ИмяЭлементаПодсказки = "ПодборТовары" Тогда
		
		ВыделенныйЭлементПодсказки = "ТоварыПодборТовары";
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = УправлениеПанельюПодсказкиКлиентСервер.ЦветФонаВыделенияЭлементаПодсказки();
		ПодключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме", 1.3, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделениеЭлементаПодсказкиНаФорме() Экспорт
	
	ОтключитьОбработчикОжидания("СнятьВыделениеЭлементаПодсказкиНаФорме");
	
	Если ВыделенныйЭлементПодсказки = "ТоварыПодборТовары" Тогда
		Элемент = Элементы[ВыделенныйЭлементПодсказки];
		Элемент.ЦветФона = Новый Цвет();
	КонецЕсли;
	
	ВыделенныйЭлементПодсказки = "";
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НужноУбратьЗакладкиСтраниц(Объект)

	Возврат (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.ПередачаТоваров")
		И НЕ ОбщегоНазначенияБПВызовСервера.ПолучитьФункциональнуюОпциюОбъекта("ИспользоватьВозвратнуюТару", Объект));

КонецФункции

&НаКлиенте 
Процедура ОбработкаВыбораРНПТНаКлиенте(ВыбранноеЗначение)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти <> Неопределено Тогда
		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ИдентификаторСтроки) Тогда
			СтрокаТабличнойЧасти.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		КонецЕсли;
		ОбработкаВыбораРНПТНаСервере(СтрокаТабличнойЧасти.ИдентификаторСтроки, ВыбранноеЗначение);
		ПрослеживаемостьФормыКлиентСерверБП.ЗаполнитьПредставлениеРНПТ(ЭтаФорма, СтрокаТабличнойЧасти);
		УстановитьВидимостьПроверкиРНПТ();
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ПроверкаРНПТКлиент.ЗапуститьПроверкуРНПТВДокументе(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСодержаниеПриИзмененииНаКлиенте()
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	РаботаСНоменклатуройКлиентБП.СодержаниеПриИзменении(ТекущиеДанные, ТекущаяДатаДокумента);
	
	ЭлектронноеАктированиеБПКлиент.УслугиСодержаниеПриИзменении(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

// Пересчеты реквизитов в строках табличных частей

&НаКлиенте
Процедура ТоварыНоменклатураПриИзмененииНаКлиенте()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	// Получим общие параметры обработки для реквизитов документа
	ПараметрыОбработки = РеализацияТоваровУслугФормыКлиент.ПодготовитьПараметрыОбработкиТоварыНоменклатураПриИзменении(
		ЭтотОбъект, ТекущиеДанные);

	// Дополнительные поля, требующиеся для заполнения добавленных колонок табличного поля текущей формы.
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Всего", ТекущиеДанные.Всего);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("АналитикаНДС", ТекущиеДанные.АналитикаНДС);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("СчетДоходовВедетсяУчетПоНоменклатурнымГруппам", ТекущиеДанные.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("СчетУчетаЗабалансовый", ТекущиеДанные.СчетУчетаЗабалансовый);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("СчетУчетаПартионный", ТекущиеДанные.СчетУчетаПартионный);

	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("ИдентификаторСтроки", ТекущиеДанные.ИдентификаторСтроки);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("РНПТ", ТекущиеДанные.РНПТ);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("ПодсказкаРНПТ", ТекущиеДанные.ПодсказкаРНПТ);
	
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбработки.ДанныеОбъекта);
	
	ТоварыНоменклатураПриИзмененииНаСервере(
		ПараметрыОбработки.ДанныеСтрокиТаблицы,
		ПараметрыОбработки.ДанныеОбъекта,
		ПараметрыОбработки.СчетаУчетаКЗаполнению);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыОбработки.ДанныеСтрокиТаблицы);
	
	ОтобразитьСубконтоСчетаДоходовТовары = ОтобразитьСубконтоСчетаДоходовТовары ИЛИ НЕ ТекущиеДанные.СчетДоходовВедетсяУчетПоНоменклатурнымГруппам;

	Если ТекущиеДанные <> Неопределено 
		И ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторСтроки) Тогда
		// Необходимо очистить дополнительные сведения
		НоваяСтрока = ТаблицаУдаленныхСтрок.Добавить();
		НоваяСтрока.ИдентификаторСтроки = ТекущиеДанные.ИдентификаторСтроки;
	КонецЕсли;
	
	ВызовУдалитьСвязанныеЗаписи = "Номенклатура";
	
	ПодключитьОбработчикОжидания("Подключаемый_УдалитьСвязанныеЗаписи", 0.1, Истина);

КонецПроцедуры

&НаСервере
Функция УстановитьВидимостьПроверкиРНПТ()
	
	ОтображатьПроверкуРНПТ = ПроверкаРНПТ.ИспользованиеПроверкиВозможно()
		И ВедетсяУчетПрослеживаемыхТоваров
		И Объект.СведенияПрослеживаемости.Количество() > 0;
		
	Элементы.ГруппаПроверкаРНПТ.Видимость = ОтображатьПроверкуРНПТ;
	Элементы.ТоварыСостояниеПроверкиРНПТ.Видимость = ОтображатьПроверкуРНПТ;
	
КонецФункции

&НаСервере
Процедура ОтобразитьРезультатПроверкиРНПТ() Экспорт
	ПроверкаРНПТ.ОтобразитьРезультатПроверкиРНПТВДокументе(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗапуститьПроверкуРНПТВДокументе()
	ПроверкаРНПТКлиент.ЗапуститьПроверкуРНПТВДокументе(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиРНПТ()
	ПроверкаРНПТКлиент.ОбработатьРезультатПроверкиРНПТВДокументе(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПроверитьРНПТФоновоеЗадание() Экспорт
	ПроверкаРНПТ.ПроверитьРНПТВДокументеФоновоеЗадание(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура СохранитьРезультатПроверкиРНПТПослеЗаписиДокумента() Экспорт
	ПроверкаРНПТ.СохранитьРезультатПроверкиРНПТПослеЗаписиДокумента(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Функция ПараметрыСозданияНовогоДоговора()
	
	Возврат РаботаСДоговорамиКонтрагентовБП.ПараметрыСозданияНовогоДоговора(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура УстановитьПредлагатьНовыйДоговорСНумерацией()
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьПредлагатьНовыйДоговорСНумерацией(ЭтотОбъект);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаСервере
Процедура ОбработатьОповещениеОбИзмененииНастроекОтветственного()
	
	ПользователиБП.ОбработатьОповещениеОбИзмененииНастроекОтветственного(ЭтотОбъект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаСервере
Процедура УстановитьПредставлениеНадписиЗаявлениеОВвозеТоваровПолученное()
	
	УчетНДСВызовСервера.УстановитьПредставлениеНадписиЗаявлениеОВвозеТоваровПолученное(ЭтотОбъект);
	
КонецПроцедуры

// Прочий функционал:

&НаСервере
Процедура УстановитьПараметрыВыбораСтавкиНДС() Экспорт
	
	УчетНДС.УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "ТоварыСтавкаНДС");
	УчетНДС.УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "УслугиСтавкаНДС");
	УчетНДС.УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "АгентскиеУслугиСтавкаНДС");
	
КонецПроцедуры


// Формирование надписи "Счет-фактура"
&НаСервере
Процедура УстановитьЗначениеТребуетсяСчетФактура()
	
	Если ЭтоОтгрузка Тогда
		ТребуетсяСчетФактура = УчетнаяПолитика.НачислятьНДСПоОтгрузке(Объект.Организация, Объект.Дата);
	Иначе
		ТребуетсяСчетФактура = (ЗначениеЗаполнено(Объект.ДоговорКонтрагента) ИЛИ НЕ ВестиУчетПоДоговорам) И НЕ ЭтоКомиссия;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	АдаптироватьМКСписокФормы();
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаКоманднаяПанель.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, Элементы.ПодменюПечать.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандуВПодменюМКПоделиться(Элементы, Элементы.ОтправитьПоЭлектроннойПочте);
	
	КомандыЭДО = Элементы.Найти("КомандыЭДО");
	Если КомандыЭДО <> Неопределено Тогда
		МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, КомандыЭДО);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьНомерДатаФормаДокумента(Элементы, Элементы.ГруппаНомерДата);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаНомерДата);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Организация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаКонтрагент);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ДоговорКонтрагента);
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаСчетНаОплату);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Склад);
	
	Элементы.ЦеныИВалюта.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ЦеныИВалюта.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ЦеныИВалюта);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаЛевая);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаОсновныеРеквизиты);
	
	Элементы.ДеятельностьНаПатенте.ПоложениеЗаголовка=ПоложениеЗаголовкаЭлементаФормы.Авто;
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаДеятельностьНаПатенте);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаРасчетыЦеныИВалюта);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаИтогиРеквизитыОрганизацииИКонтрагента);
		
	Элементы.УПД.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.УПД);
	
	Элементы.СчетФактураПросмотр.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.СчетФактураПросмотр);
		
	Элементы.СтатусДокумента.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаПодвал);
		
	Элементы.ГруппаАдресДоставки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаКомментарийОтветственный.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.ВалютаДокументаИтогиВсего.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ВалютаДокументаИтогиВсего.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВалютаДокументаИтогиВсего.РастягиватьПоГоризонтали = Истина;
	Элементы.ВалютаДокументаИтогиВсего.Ширина = 0;
	
	Элементы.ГруппаИтоги.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
	Элементы.ГруппаИтоги.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаПодписанИтоги.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Авто;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаПодписанИтоги);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "Товары");
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "Услуги");
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "ВозвратнаяТара");
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "АгентскиеУслуги");
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()

	АдаптироватьМКСписокФормыТовары();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
		АдаптироватьМКСписокФормыВозвратнаяТара();
	КонецЕсли;
	
	АдаптироватьМКСписокФормыУслуги();
	
	Если ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугКомитентов") Тогда
		АдаптироватьМКСписокФормыАгентскиеУслуги();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормыТовары()
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыПодбор ="ТоварыПодборТовары";
	КомандыПоля.ИмяКомандыДобавитьСтроку = "ТоварыДобавить";
	КомандыПоля.КомандыТаблицыПрочие.Добавить("ТоварыДобавитьПоШтрихкоду");
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаТовары;
	ОписаниеГруппыПоля.ПоказатьГруппуНового = Истина;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.КолонкаНомерСтроки = "ТоварыНомерСтроки";
	ОписаниеКолонокПоля.ВажныеКолонки = "ТоварыГруппаНоменклатура,ТоварыНоменклатура";
	ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = 
		"ТоварыНоменклатура,ТоварыКиЗ_ГИСМ,ТоварыСчетНаОплатуПокупателюСсылка,ТоварыСтранаПроисхождения,ТоварыНомерГТД";
	ОписаниеКолонокПоля.СкрытьКолонки = "ТоварыОтступ,ТоварыПрослеживаемыйТовар,ТоварыПодсказкаРНПТ";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
		Элементы.Товары, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
	
	Элементы.Переместить(Элементы.ТоварыНоменклатура, Элементы.Товары, Элементы.ТоварыГруппаНоменклатура);
	
	Элементы.ТоварыНоменклатураКод.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.Переместить(Элементы.ТоварыНоменклатураКод, Элементы.Товары, Элементы.ТоварыСчетНаОплатуПокупателю);

	Элементы.ТоварыНоменклатураАртикул.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	Элементы.Переместить(Элементы.ТоварыНоменклатураАртикул, Элементы.Товары, Элементы.ТоварыСчетНаОплатуПокупателю);
	
	Элементы.ТоварыКоличество.Формат = "ЧФ='Кол-во: Ч'";
	Элементы.ТоварыЦена.Формат = "ЧФ='Цена: Ч'";
	Элементы.ТоварыСумма.Формат = "ЧФ='Сумма: Ч'";
	Элементы.ТоварыСуммаНДС.Формат = "ЧФ='НДС: Ч'";
	Элементы.ТоварыВсего.Формат = "ЧФ='Всего: Ч'";
	
	Элементы.ТоварыРНПТ.ПодсказкаВвода = НСтр("ru = 'Нажмите для выбора'");
	
	Элементы.ТоварыПрослеживаемыйТовар.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	
	Элементы.Переместить(Элементы.ТоварыКиЗ_ГИСМ, Элементы.Товары, Элементы.ТоварыСтранаПроисхождения);
	
	УстановитьУсловноеОформлениеТовары();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормыВозвратнаяТара()
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыПодбор ="ВозвратнаяТараПодборВозвратнаяТара";
	КомандыПоля.ИмяКомандыДобавитьСтроку = "ВозвратнаяТараДобавить";
	КомандыПоля.КомандыТаблицыПрочие.Добавить("ГруппаВозвратнаяТараЗаполнить");
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаВозвратнаяТара;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.КолонкаНомерСтроки = "ВозвратнаяТараНомерСтроки";
	ОписаниеКолонокПоля.ВажныеКолонки = "ВозвратнаяТараНоменклатура";
	ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = "ВозвратнаяТараНоменклатура";
	ОписаниеКолонокПоля.СкрытьКолонки = "ВозвратнаяТараОтступ";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
	Элементы.ВозвратнаяТара, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
	
	Элементы.Переместить(Элементы.ВозвратнаяТараНоменклатураКод, Элементы.ВозвратнаяТара, Элементы.ВозвратнаяТараГруппаКоличество);
	Элементы.Переместить(Элементы.ВозвратнаяТараНоменклатураАртикул, Элементы.ВозвратнаяТара, Элементы.ВозвратнаяТараГруппаКоличество);
	
	Элементы.ВозвратнаяТараКоличество.Формат = "ЧФ='Количество: Ч'";
	Элементы.ВозвратнаяТараЦена.Формат = "ЧФ='Цена: Ч'";
	Элементы.ВозвратнаяТараСумма.Формат = "ЧФ='Сумма: Ч'";
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормыУслуги()
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыПодбор ="УслугиПодборУслуги";
	КомандыПоля.ИмяКомандыДобавитьСтроку = "УслугиДобавить";
	КомандыПоля.КомандыТаблицыПрочие.Добавить("ГруппаУслугиЗаполнить");
	КомандыПоля.КомандыТаблицыПрочие.Добавить("УслугиПеренестиВАгентскиеУслуги");
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаУслуги;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.КолонкаНомерСтроки = "УслугиНомерСтроки";
	ОписаниеКолонокПоля.ВажныеКолонки = "УслугиГруппаНоменклатура,УслугиНоменклатура";
	ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = 
		"УслугиНоменклатура,УслугиСодержание,УслугиСубконто,УслугиСчетНаОплатуПокупателю,УслугиСчетНаОплатуПокупателюСсылка";
	ОписаниеКолонокПоля.СкрытьКолонки = "УслугиОтступ";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
		Элементы.Услуги, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
	
	Элементы.УслугиГруппаНоменклатура.Ширина = 0;
	
	Элементы.УслугиКоличество.Формат = "ЧФ='Кол-во: Ч'";
	Элементы.УслугиЦена.Формат = "ЧФ='Цена: Ч'";
	Элементы.УслугиСумма.Формат = "ЧФ='Сумма: Ч'";
	Элементы.УслугиСуммаНДС.Формат = "ЧФ='НДС: Ч'";
	Элементы.УслугиВсего.Формат = "ЧФ='Всего: Ч'";
	
	УстановитьУсловноеОформлениеУслуги();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормыАгентскиеУслуги()
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыДобавитьСтроку = "АгентскиеУслугиДобавить";
	КомандыПоля.КомандыТаблицыПрочие.Добавить("АгентскиеУслугиПеренестиВУслуги");
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаАгентскиеУслуги;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.КолонкаНомерСтроки = "АгентскиеУслугиНомерСтроки";
	ОписаниеКолонокПоля.ВажныеКолонки = "АгентскиеУслугиГруппаНоменклатура,АгентскиеУслугиНоменклатура";
	ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = 
	"АгентскиеУслугиНоменклатура,АгентскиеУслугиСодержание,АгентскиеУслугиСчетНаОплатуПокупателю,"
	+ "АгентскиеУслугиДоговорКонтрагента,АгентскиеУслугиДоговорКонтрагента,АгентскиеУслугиСчетНаОплатуПокупателюСсылка";
	ОписаниеКолонокПоля.СкрытьКолонки = "АгентскиеУслугиЭтоНекорректныйКонтрагент";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
	Элементы.АгентскиеУслуги, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
	
	Элементы.АгентскиеУслугиГруппаНоменклатура.Ширина = 0;
	
	Элементы.АгентскиеУслугиКоличество.Формат = "ЧФ='Кол-во: Ч'";
	Элементы.АгентскиеУслугиЦена.Формат = "ЧФ='Цена: Ч'";
	Элементы.АгентскиеУслугиСумма.Формат = "ЧФ='Сумма: Ч'";
	Элементы.АгентскиеУслугиСуммаНДС.Формат = "ЧФ='НДС: Ч'";
	Элементы.АгентскиеУслугиВсего.Формат = "ЧФ='Всего: Ч'";
	
	УстановитьУсловноеОформлениеАгентскиеУслуги();
	
КонецПроцедуры

#КонецОбласти

#Область ОтложенноеСопоставлениеНоменклатуры

&НаКлиенте
Процедура Подключаемый_ПоказатьСопоставлениеНоменклатуры()
	
	ОбменСМаркетплейсКлиент.ПоказатьСопоставлениеНоменклатуры(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ЭтоНоваяСтрока = Ложь;

#КонецОбласти

