#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"Номенклатура,Организация,СуммаВключаетНДС,Сумма,СтавкаНДС,СуммаНДС,
		|НалоговаяБазаНДС,Всего,Дата");
	
	ОпределитьСпособНачисленияНДС();
	
	УправлениеФормой(ЭтотОбъект);
	УстановитьЗаголовокФормы();
	
	УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "СтавкаНДСРасчетная", "Авансы");
	УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "СтавкаНДС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ФормаРедактированияНалоговойБазыНДС_Закрыть" И Источник = ВладелецФормы Тогда
		// Сообщение от основной формы документа при нажатии там Esc.
		// Сбрасываем флаг модифицированности и закрываем форму редактирования строки без вопросов.
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент Тогда
		СтруктураРезультат = Новый Структура(
			"НалоговаяБазаНДС,Сумма,СуммаНДС,СтавкаНДС,Всего");
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, ЭтотОбъект);
		ОповеститьОВыборе(СтруктураРезультат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И (Модифицированность Или ПеренестиВДокумент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И Не ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;

	Если ПеренестиВДокумент И Не Отказ Тогда
		Отказ = Не ПроверитьЗаполнениеНаКлиенте();
	КонецЕсли;

	Если Отказ Тогда
		ПеренестиВДокумент = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособНачисленияНДСПриИзменении(Элемент)
	
	УстановитьСтавкуНДС(СпособНачисленияНДС);
	
	Если СпособНачисленияНДС = 0 Тогда
		ЭтотОбъект.НалоговаяБазаНДС = 0;
		РассчитатьСуммуНДС(ЭтотОбъект.Сумма, ЭтотОбъект.СуммаВключаетНДС);
	ИначеЕсли СпособНачисленияНДС = 1 Тогда
		РассчитатьНалоговуюБазуНДС();
		РассчитатьСуммуНДС(ЭтотОбъект.НалоговаяБазаНДС);
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьПродажиПриИзменении(Элемент)
	
	РассчитатьСуммуНДС(ЭтотОбъект.Сумма, ЭтотОбъект.СуммаВключаетНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	РассчитатьСуммуНДС(ЭтотОбъект.Сумма, ЭтотОбъект.СуммаВключаетНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаНДСПриИзменении(Элемент)
	
	РассчитатьВсего(ЭтотОбъект.СуммаВключаетНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриобретенияПриИзменении(Элемент)
	
	РассчитатьНалоговуюБазуНДС();
	РассчитатьСуммуНДС(ЭтотОбъект.НалоговаяБазаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаРеализацииПриИзменении(Элемент)
	
	РассчитатьНалоговуюБазуНДС();
	РассчитатьСуммуНДС(ЭтотОбъект.НалоговаяБазаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаБазаНДСПриИзменении(Элемент)
	
	РассчитатьСуммуНДС(ЭтотОбъект.НалоговаяБазаНДС);
	ЭтотОбъект.ЦенаПриобретения = ЭтотОбъект.Сумма - ЭтотОбъект.НалоговаяБазаНДС;
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСРасчетнаяПриИзменении(Элемент)
	
	РассчитатьСуммуНДС(ЭтотОбъект.НалоговаяБазаНДС);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыВыбораСтавкиНДС(Форма, ИмяЭлементаСтавкаНДС, ДополнительныйПараметр = "") Экспорт
	
	ПараметрНДСПриУСН = Новый ПараметрВыбора("СтавкаНДСПриУСН",
		РегистрыСведений.НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН(Организация, Дата));
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(ПараметрНДСПриУСН);
	Если ЗначениеЗаполнено(ДополнительныйПараметр) Тогда
		ДополнительныйПараметрВыбора = Новый ПараметрВыбора(ДополнительныйПараметр, Истина);
		МассивПараметровВыбора.Добавить(ДополнительныйПараметрВыбора);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугКомитентов") Тогда
		ОсуществляетсяРеализацияТоваровУслугКомитентов = Новый ПараметрВыбора("ОсуществляетсяРеализацияТоваровУслугКомитентов", Истина);
		МассивПараметровВыбора.Добавить(ОсуществляетсяРеализацияТоваровУслугКомитентов);
	КонецЕсли;
	
	Форма.Элементы[ИмяЭлементаСтавкаНДС].ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаНачислениеНаСтоимостьПродажи.Видимость = Форма.СпособНачисленияНДС = 0;
	Элементы.ГруппаНачислениеНаРазницуСтоимости.Видимость = Форма.СпособНачисленияНДС = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуНДС(БазаРасчета, СуммаВключаетНДС = Истина)
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Сумма", БазаРасчета);
	СтруктураПараметров.Вставить("СтавкаНДС", ЭтотОбъект.СтавкаНДС);
	СтруктураПараметров.Вставить("СуммаНДС", 0);
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(
		СтруктураПараметров, Истина);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПараметров, "СуммаНДС");
	
	РассчитатьВсего(СуммаВключаетНДС);

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьНалоговуюБазуНДС()
	
	ЭтотОбъект.НалоговаяБазаНДС = ЭтотОбъект.Сумма - ЭтотОбъект.ЦенаПриобретения;

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьВсего(СуммаВключаетНДС = Истина)
	
	ЭтотОбъект.Всего = ЭтотОбъект.Сумма + ?(СуммаВключаетНДС, 0, ЭтотОбъект.СуммаНДС);

КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	НаименованиеНоменклатуры = "";
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ДанныеОбъекта = Новый Структура("Дата, Организация", ТекущаяДатаСеанса(), Организация);
		СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
			Номенклатура, ДанныеОбъекта, Ложь);
			НаименованиеНоменклатуры = СведенияОНоменклатуре.Наименование;
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'НДС: %1'"),
		НаименованиеНоменклатуры);

КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНаКлиенте()

	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", НСтр("ru = '% НДС'"));
		Поле = ?(СпособНачисленияНДС = 0, "СтавкаНДС", "СтавкаНДСРасчетная");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
	КонецЕсли;
	
	Если СпособНачисленияНДС = 1 Тогда
		Если Не ЗначениеЗаполнено(ЦенаПриобретения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Заполнение", НСтр("ru = 'Цена приобретения'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЦенаПриобретения", "", Отказ);
		КонецЕсли;
		Если ЦенаПриобретения >= Сумма Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Поле", "Корректность", НСтр("ru = 'Цена приобретения'"),,,
				НСтр("ru='Цена приобретения должна быть меньше цены реализации'") );
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЦенаПриобретения", "", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Не Отказ;

КонецФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьСпособНачисленияНДС()
	
	Если ЗначениеЗаполнено(НалоговаяБазаНДС) Тогда
		СпособНачисленияНДС = 1;
		ЦенаПриобретения = Сумма - НалоговаяБазаНДС;
	Иначе
		СпособНачисленияНДС = 0;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьСтавкуНДС(СпособНачисленияНДС)
	
	Если СпособНачисленияНДС = 0 Тогда
		СтавкаНДС = Перечисления.СтавкиНДС.ОбычнаяСтавкаНДСПоРасчетной(СтавкаНДС);
	ИначеЕсли СпособНачисленияНДС = 1 Тогда
		СтавкаНДС = Перечисления.СтавкиНДС.РасчетнаяСтавкаНДСПоОбычной(СтавкаНДС);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти