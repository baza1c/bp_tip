
#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОбщегоНазначенияБП.УстановитьВидимостьКолонокДополнительнойИнформации(ЭтотОбъект);
	
	ОтправкаПочтовыхСообщений.ПриСозданииНаСервере(ЭтотОбъект);
	
	СписокСвойств = "ИмяТаблицы";
	Если Параметры.Свойство(СписокСвойств) Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, СписокСвойств);
	КонецЕсли;
	
	Элементы.ГруппаПодсказкаМножественныйВыбор.Видимость 
		= (ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "МножественныйВыбор", Ложь) = ИСТИНА);
	
	Если Параметры.Свойство("Организация") Тогда
		
		ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы, 
		"ГруппаИ", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ГруппаИ, "Организация", Параметры.Организация, ВидСравненияКомпоновкиДанных.Равно);
		
	
		Если Параметры.Свойство("Контрагент") Тогда
		
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ГруппаИ, "Контрагент", Параметры.Контрагент, ВидСравненияКомпоновкиДанных.Равно);
			
		КонецЕсли;
		
		Если Параметры.Свойство("Статформа") Тогда
			
			СтраныЕАЭС = Справочники.СтраныМира.СтраныЕАЭС(Истина);
			
			Для каждого Страна Из СтраныЕАЭС Цикл
				Элементы.ОтборСтранаРеализации.СписокВыбора.Добавить(Страна.Значение);
			КонецЦикла;
			
			Элементы.ГруппаОтборыДляСтатформы.Видимость = Истина;
			
			ОтборПериод = Параметры.НачалоПериода;
			ОтборПериодИспользование = ЗначениеЗаполнено(ОтборПериод);
			ОтборПериодПредставление = Формат (ОтборПериод,"ДФ='ММММ гггг'") + НСтр("ru = ' г.'");
			
			ОтборСтранаРеализации = Справочники.СтраныМира.НайтиПоРеквизиту("КодАльфа2", Параметры.ТоргующаяСтранаКод);
			ОтборСтранаРеализацииИспользование = ЗначениеЗаполнено(ОтборСтранаРеализации);
			
			УчетНДСКлиентСервер.ЗаполнитьСписокВыбораВидовТранспорта(Элементы.ОтборВидТранспортаНаГранице.СписокВыбора);
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"СтранаЕАЭС",
				Истина,
				ВидСравненияКомпоновкиДанных.Равно);
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"ПометкаУдаления",
				Ложь,
				ВидСравненияКомпоновкиДанных.Равно);
			
			УстановитьВосстановленныеОтборы();
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПериодИспользованиеПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период")
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПредставлениеПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(ОтборПериодПредставление) Тогда
		ОтборПериодИспользование = Ложь;
		ОтборПериод = '00010101';
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ПараметрыПериода = Новый Структура("НачалоПериода, КонецПериода, ВидПериода, ИмяФормы");
	ПараметрыПериода.НачалоПериода = ОтборПериод;
	ПараметрыПериода.КонецПериода = КонецМесяца(ОтборПериод);
	ПараметрыПериода.ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц");
	ПараметрыПериода.ИмяФормы = "ВыборСтандартногоПериодаМесяц";
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму(
			СтрШаблон("ОбщаяФорма.%1","ВыборСтандартногоПериодаМесяц"),
			ПараметрыПериода,
			ЭтотОбъект, , , ,
			Новый ОписаниеОповещения("ОтборПериодПредставлениеЗавершениеВыбора", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	ОтборПериодИспользование = Ложь;
	ОтборПериод = '00010101';
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период");
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтранаРеализацииИспользованиеПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СтранаРеализации");
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтранаРеализацииПриИзменении(Элемент)
	ОтборСтранаРеализацииИспользование = ЗначениеЗаполнено(ОтборСтранаРеализации);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СтранаРеализации");
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидТранспортаНаГраницеИспользованиеПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ВидТранспортаНаГранице");
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидТранспортаНаГраницеПриИзменении(Элемент)
	ОтборВидТранспортаНаГраницеИспользование = ЗначениеЗаполнено(ОтборВидТранспортаНаГранице);
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ВидТранспортаНаГранице");
КонецПроцедуры

&НаКлиенте
Процедура ОтборЭлектроннаяТорговаяПлощадкаИспользованиеПриИзменении(Элемент)
	ОтборЭлектроннаяТорговаяПлощадка = ОтборЭлектроннаяТорговаяПлощадкаИспользование;
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ЭлектроннаяТорговаяПлощадка");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	Если ТипЗнч(Элемент.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("Запись_РеализацияТоваровУслуг", , Элемент.ТекущаяСтрока);
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "СтранаРеализации");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ВидТранспортаНаГранице");
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "ЭлектроннаяТорговаяПлощадка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодПредставлениеЗавершениеВыбора(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		
		ОтборПериодПредставление = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			РезультатВыбора.НачалоПериода, РезультатВыбора.КонецПериода, Истина);
		ОтборПериод = РезультатВыбора.НачалоПериода;
		
		ОтборПериодИспользование = ЗначениеЗаполнено(ОтборПериод);
		ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Период");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти