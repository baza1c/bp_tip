#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ВнешниеДанные Экспорт;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения, , Истина);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Увольнение") Тогда
		СтрокаДанныеУвольнения = "Организация, ФизическоеЛицо, ДатаУвольнения";
		ДанныеУвольнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, СтрокаДанныеУвольнения);
		Организация = ДанныеУвольнения["Организация"];
		Сотрудник = ДанныеУвольнения["ФизическоеЛицо"];
		НалоговыйПериод = Год(ДанныеУвольнения["ДатаУвольнения"]);
		СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.Сводно;
		Дата = ?(ЗначениеЗаполнено(ДанныеУвольнения["ДатаУвольнения"]), КонецДня(ДанныеУвольнения["ДатаУвольнения"]) + 1, ТекущаяДатаСеанса());
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Действие") И ДанныеЗаполнения.Действие = "ОбработкаЗапросаКабинетСотрудника" Тогда
			Сотрудник 			= ДанныеЗаполнения.Данные.ФизическоеЛицо;
			Организация 		= ДанныеЗаполнения.Данные.Организация;
			НалоговыйПериод 	= ДанныеЗаполнения.Данные.НалоговыйПериод;
			СпособФормирования 	= ДанныеЗаполнения.Данные.СпособФормирования;
		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Действие") И ДанныеЗаполнения.Действие = "Заполнить" Тогда 
			ЗаполнитьПоДаннымЗаполнения(ДанныеЗаполнения);	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.Сводно Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "РегистрацияВНалоговомОргане");
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "КодИФНС");
	КонецЕсли;
	
	Если ВнешниеДанные = Истина Тогда
		Если Не ЗначениеЗаполнено(СпособФормирования) Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "СведенияОДоходах.МесяцНалоговогоПериода");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "НалоговыйПериод");
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Организация");
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "РегистрацияВНалоговомОргане");
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "КодИФНС");
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "СпособФормирования");
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Дата");
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Номер");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьДанныеДокумента(Отказ) Экспорт
	
	СоответствиеСтавокДоходов = УчетНДФЛ.СоответствиеДоходовСтавкам();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураДанныхНА = УчетНДФЛ.СправкиНДФЛДанныеНалоговогоАгента(Организация, НалоговыйПериод, РегистрацияВНалоговомОргане, Дата, Телефон, ДолжностьПодписавшегоЛица, СправкуПодписал);
		СтруктураДанныхНА.КодНалоговогоОргана = КодИФНС;
		УчетНДФЛ.СправкиНДФЛПроверитьДанныеНалоговогоАгента(ЭтотОбъект, СтруктураДанныхНА, СпособФормирования, Отказ);
	КонецЕсли;
	
	ПараметрыПроверкиДанныхФизическихЛиц = Документы.СправкаНДФЛ.ПараметрыПроверкиДанныхФизическихЛиц();
	ПараметрыПроверкиДанныхФизическихЛиц.Ссылка						= Ссылка;
	ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные 			= ПолучитьДанныеСотрудникДляПроверки();
	ПараметрыПроверкиДанныхФизическихЛиц.ДатаДокумента				= Дата;
	ПараметрыПроверкиДанныхФизическихЛиц.ПутьКДаннымФизическогоЛица = "";
	ПараметрыПроверкиДанныхФизическихЛиц.ПроверятьАдрес				= Не УчетНДФЛ.ВыводитьФорму2НДФЛ2018Года(НалоговыйПериод, Дата);
	
	Документы.СправкаНДФЛ.ПроверитьДанныеФизическогоЛица(ПараметрыПроверкиДанныхФизическихЛиц, Отказ);
	
	Доходы = СведенияОДоходах.Выгрузить();
	Вычеты = СведенияОВычетах.Выгрузить();
	Уведомления = УведомленияНОоПравеНаВычеты.Выгрузить();
	
	Сводно = (СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.Сводно);
	
	НачалоСообщенияОбОшибке = СтрШаблон(
		НСтр("ru = 'Справка 2-НДФЛ по %1:'"), 
		ПараметрыПроверкиДанныхФизическихЛиц.ПроверяемыеДанные);
	
	УчетНДФЛ.СправкиНДФЛПроверитьДанныеОДоходахНалогахВычетах(
		Ссылка,
		Дата,
		ЭтотОбъект,
		"",
		Доходы,
		Вычеты,
		СоответствиеСтавокДоходов,
		НачалоСообщенияОбОшибке,
		Отказ,
		Сводно,,,
		Уведомления);
	
КонецПроцедуры

Функция ПолучитьДанныеСотрудникДляПроверки()
	ДанныеСотрудника = Новый Структура("Сотрудник, ИНН, Фамилия, Имя, Отчество, Адрес, ВидДокумента, СерияДокумента, НомерДокумента,
									   |Гражданство, ДатаРождения, СтатусНалогоплательщика, АдресЗарубежом, СотрудникНаименование");
									   
	ЗаполнитьЗначенияСвойств(ДанныеСотрудника, ЭтотОбъект);
	
	Если ЗначениеЗаполнено(ДанныеСотрудника.Сотрудник) Тогда
		ДанныеСотрудника.СотрудникНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСотрудника.Сотрудник, "Наименование");
	Иначе
		ДанныеСотрудника.СотрудникНаименование = "";
	КонецЕсли;	
	
	Возврат ДанныеСотрудника;
КонецФункции	

#Область ЗаполнениеИРасчет

Функция ПараметрыРасчета() Экспорт

	ПараметрыСправокНДФЛ = СправкиНДФЛ.ПараметрыСправокНДФЛ();

	ПараметрыСправокНДФЛ.СведенияОДоходах 					= ЭтотОбъект.СведенияОДоходах;
	ПараметрыСправокНДФЛ.СведенияОВычетах 					= ЭтотОбъект.СведенияОВычетах;
	ПараметрыСправокНДФЛ.НалоговыйПериод					= ЭтотОбъект.НалоговыйПериод;
	ПараметрыСправокНДФЛ.ДатаДокумента 						= КонецДня(ЭтотОбъект.Дата);
	ПараметрыСправокНДФЛ.Организация 						= ЭтотОбъект.Организация;
	ПараметрыСправокНДФЛ.РегистрацияВНалоговомОргане		= РегистрацияВНалоговомОргане(); 
	ПараметрыСправокНДФЛ.ИспользоватьНомераСправок 			= Ложь;
	ПараметрыСправокНДФЛ.ЗаполнятьБезДоходовПоЦеннымБумагам = Ложь;
	ПараметрыСправокНДФЛ.УведомленияНОоПравеНаВычеты 		= ЭтотОбъект.УведомленияНОоПравеНаВычеты;

	Возврат ПараметрыСправокНДФЛ;
	
КонецФункции    

Процедура Рассчитать(ПараметрыРасчета = Неопределено) Экспорт
	
	Если ПараметрыРасчета = Неопределено Тогда
		ПараметрыРасчета = ПараметрыРасчета();		
	КонецЕсли;
	
	ОбновитьНалоги(ЭтотОбъект, ПараметрыРасчета);
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымЗаполнения(ДанныеЗаполнения)
	
	ЗаполнитьИсходныеДанныеДокумента(ДанныеЗаполнения);
	
	//Если Не ЗначениеЗаполнено(ПланируемаяДатаВыплаты) Тогда
	//	ЗаполнитьПланируемуюДатуВыплаты(); 
	//КонецЕсли;	
	//
	//УстановитьРассчетУдержаний(); 
	//ПерезаполнитьДанныеОбъекта(ДанныеЗаполнения.Начисления.ВыгрузитьКолонку("Сотрудник"));

	ЗапрашиваемыеЗначения = Новый Структура;
	ФиксированныеЗначения = Новый Массив;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ЗапрашиваемыеЗначения.Вставить("Организация", "Организация");
		ФиксированныеЗначения.Добавить("Организация");
	КонецЕсли;
	
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтотОбъект, ЗапрашиваемыеЗначения, ФиксированныеЗначения);
	
КонецПроцедуры   

Процедура ЗаполнитьИсходныеДанныеДокумента(ДанныеЗаполнения)
	
	Если Организация.Пустая() Тогда    
		ДанныеЗаполнения.Свойство("Организация", ЭтотОбъект.Организация);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Дата) Тогда
		ДанныеЗаполнения.Свойство("Дата", ЭтотОбъект.Дата);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.НалоговыйПериод) Тогда
		ДанныеЗаполнения.Свойство("НалоговыйПериод", ЭтотОбъект.НалоговыйПериод);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ЭтотОбъект.РегистрацияВНалоговомОргане) Тогда
		ДанныеЗаполнения.Свойство("РегистрацияВНалоговомОргане", ЭтотОбъект.РегистрацияВНалоговомОргане);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.СпособФормирования) Тогда
		ДанныеЗаполнения.Свойство("СпособФормирования", ЭтотОбъект.СпособФормирования);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Сотрудник) Тогда
		ДанныеЗаполнения.Свойство("Сотрудник", ЭтотОбъект.Сотрудник);
	КонецЕсли;
	
	ДанныеЗаполнения.Свойство("Ответственный", ЭтотОбъект.Ответственный);
	
КонецПроцедуры

Функция РегистрацияВНалоговомОргане()
	
	Если ЭтотОбъект.СпособФормирования = Перечисления.ПорядокФормированияСправкиОДоходахФизическогоЛица.Сводно Тогда
		Возврат Неопределено;
	Иначе
		Возврат ЭтотОбъект.РегистрацияВНалоговомОргане;
	КонецЕсли;
	
КонецФункции   

Процедура ОбновитьНалоги(СправкаПоСотруднику, ПараметрыСправокНДФЛ) Экспорт
								
	СправкаПоСотруднику.ФиксНалоги = Ложь;
		
	ДанныеСправок = Новый Массив;
	ДанныеСправок.Добавить(СправкаПоСотруднику);
	
	ПрочитатьДанные(ДанныеСправок, ПараметрыСправокНДФЛ);
			
	СправкаПоСотруднику.ФиксУведомление = Ложь;
	
	ДанныеСправок = Новый Массив;
	ДанныеСправок.Добавить(СправкаПоСотруднику);
	
	СправкиНДФЛ.ПрочитатьДанныеУведомлений(ДанныеСправок, ПараметрыСправокНДФЛ.Организация, ПараметрыСправокНДФЛ.НалоговыйПериод);	
	
	Для каждого ИмяПоля Из УчетНДФЛ.ПоляЗачтеноАвансовыхПлатежей() Цикл
		Если ЗначениеЗаполнено(СправкаПоСотруднику[ИмяПоля]) Тогда
			СправкиНДФЛ.УточнитьСтатусРаботающегоПоПатенту(ДанныеСправок, ПараметрыСправокНДФЛ.НалоговыйПериод);
			Прервать;	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры	

Процедура ПрочитатьДанные(ДанныеСправок, ПараметрыСправокНДФЛ, ОбновлятьНеФиксированныеДанные = Истина, 
	НомерКорректировки = 0) Экспорт
	
	ПрочитатьДанныеСотрудников(ДанныеСправок, ПараметрыСправокНДФЛ.НалоговыйПериод, 
		ПараметрыСправокНДФЛ.ДатаДокумента, ОбновлятьНеФиксированныеДанные);
	
	Если НомерКорректировки = 99 Тогда
		Возврат
	КонецЕсли;
	
	СправкиСНефиксированнымиСведениямиОНалогах = Новый Массив;
	Для Каждого ДанныеСправкиПоСотруднику Из ДанныеСправок Цикл
		Если Не ДанныеСправкиПоСотруднику.ФиксНалоги Тогда
			СправкиСНефиксированнымиСведениямиОНалогах.Добавить(ДанныеСправкиПоСотруднику);
		КонецЕсли;	
	КонецЦикла;	
	
	ПрочитатьДанныеОДоходахИНалогах(СправкиСНефиксированнымиСведениямиОНалогах, ПараметрыСправокНДФЛ);
											
	СправкиСНефиксированнымиСведениямиУведомлений = Новый Массив;
	Для Каждого ДанныеСправкиПоСотруднику Из ДанныеСправок Цикл
		Если Не ДанныеСправкиПоСотруднику.ФиксУведомление Тогда
			Для каждого ИмяПоля Из УчетНДФЛ.ПоляЗачтеноАвансовыхПлатежей() Цикл
				Если ЗначениеЗаполнено(ДанныеСправкиПоСотруднику[ИмяПоля]) Тогда
					СправкиСНефиксированнымиСведениямиУведомлений.Добавить(ДанныеСправкиПоСотруднику);
					Прервать;	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;	
	КонецЦикла;	
	
	//ПрочитатьДанныеУведомлений(СправкиСНефиксированнымиСведениямиУведомлений, ПараметрыСправокНДФЛ.Организация, 
	//	ПараметрыСправокНДФЛ.НалоговыйПериод);
	//
	//УточнитьСтатусРаботающегоПоПатенту(СправкиСНефиксированнымиСведениямиУведомлений, ПараметрыСправокНДФЛ.НалоговыйПериод);
	
КонецПроцедуры 

Процедура ПрочитатьДанныеОДоходахИНалогах(ДанныеСправок, ПараметрыСправокНДФЛ) Экспорт
	
	Сотрудники = Новый Массив;
	СправкиПоСотрудникам = Новый Соответствие;
	ВыводитьФормуСправки2022 = УчетНДФЛ.ВыводитьФорму2НДФЛ2022Года(ПараметрыСправокНДФЛ.НалоговыйПериод, ПараметрыСправокНДФЛ.ДатаДокумента);

	Для Каждого ДанныеСправкиПоСотруднику Из ДанныеСправок Цикл
		Сотрудники.Добавить(ДанныеСправкиПоСотруднику.Сотрудник);
		
		СправкиПоСотрудникам.Вставить(ДанныеСправкиПоСотруднику.Сотрудник, ДанныеСправкиПоСотруднику);
		
		Для Каждого СтавкаНДФЛ Из Перечисления.НДФЛСтавки Цикл 
			УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "ОбщаяСуммаДохода", 0);
			УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "ОблагаемаяСуммаДохода", 0);
			УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "Исчислено", 0);
			УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "Удержано", 0);
			УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "Перечислено", 0);
			УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "Задолженность", 0);
			УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "ИзлишнеУдержано", 0);
			Если ВыводитьФормуСправки2022 Тогда
				УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "НалогНаПрибыльДляДивидендов", 0);
				УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "СуммаДоходаНеудержанного", 0);
				УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправкиПоСотруднику, СтавкаНДФЛ, "НалогСДивидендовУплаченныйЗаРубежом", 0);
			КонецЕсли;
		КонецЦикла;	

	КонецЦикла;	

	ДанныеДоходахНалогахВычетах = УчетНДФЛ.ДанныеОДоходахНалогахВычетах(Сотрудники, 
		ПараметрыСправокНДФЛ.НалоговыйПериод, 
		ПараметрыСправокНДФЛ.Организация, 
		ПараметрыСправокНДФЛ.РегистрацияВНалоговомОргане, 
		ПараметрыСправокНДФЛ.ДатаДокумента, 
		ПараметрыСправокНДФЛ.КППОтправки);
	
	Если ПараметрыСправокНДФЛ.НалоговыйПериод >= Год(УчетНДФЛ.ДатаПереходаНаКодыОКТМО()) 
		И ПараметрыСправокНДФЛ.ЗаполнятьБезДоходовПоЦеннымБумагам Тогда
		СтруктураПоиска = Новый Структура("ВключатьВДекларациюПоНалогуНаПрибыль", Ложь);
		ДанныеДоходахНалогахВычетах.Доходы = ДанныеДоходахНалогахВычетах.Доходы.Скопировать(
		ДанныеДоходахНалогахВычетах.Доходы.НайтиСтроки(СтруктураПоиска));
		ДанныеДоходахНалогахВычетах.Вычеты = ДанныеДоходахНалогахВычетах.Вычеты.Скопировать(
		ДанныеДоходахНалогахВычетах.Вычеты.НайтиСтроки(СтруктураПоиска));
		ДанныеДоходахНалогахВычетах.Налоги = ДанныеДоходахНалогахВычетах.Налоги.Скопировать(
		ДанныеДоходахНалогахВычетах.Налоги.НайтиСтроки(СтруктураПоиска));
	КонецЕсли;
	ДанныеДоходахНалогахВычетах.Доходы.Свернуть(
	"МесяцНалоговогоПериода,Ставка,КодДохода,КодДоходаКодДляОтчетности,КодВычета,Сотрудник","СуммаДохода, СуммаВычета"); 
	ПримененныеВычетыПоГруппам = ДанныеДоходахНалогахВычетах.Вычеты.Скопировать(,"ГруппаВычета,Сотрудник");
	ДанныеДоходахНалогахВычетах.Вычеты.Свернуть("КодВычета,Ставка,Сотрудник","СуммаВычета"); 
	Если ПараметрыСправокНДФЛ.НалоговыйПериод < Год(УчетНДФЛ.ДатаИзмененияПорядкаИсчисленияНалогаДляИностранцев()) Тогда
		ДанныеДоходахНалогахВычетах.Налоги.Свернуть(
		"Ставка,Сотрудник","ОбщаяСуммаДохода, ОблагаемаяСуммаДохода, Исчислено, Удержано, Перечислено, Задолженность, 
		|ИзлишнеУдержано") 
	ИначеЕсли ВыводитьФормуСправки2022 Тогда
		ДанныеДоходахНалогахВычетах.Налоги.Свернуть(
		"Ставка,Сотрудник","ОбщаяСуммаДохода, ОблагаемаяСуммаДохода, Исчислено, ЗачтеноАвансовыхПлатежей, Удержано, 
		|Перечислено, Задолженность, ИзлишнеУдержано, НалогНаПрибыльДляДивидендов, НалогСДивидендовУплаченныйЗаРубежом, СуммаДоходаНеудержанного") 
	Иначе 
		ДанныеДоходахНалогахВычетах.Налоги.Свернуть(
		"Ставка,Сотрудник","ОбщаяСуммаДохода, ОблагаемаяСуммаДохода, Исчислено, ЗачтеноАвансовыхПлатежей, Удержано, 
		|Перечислено, Задолженность, ИзлишнеУдержано");
	КонецЕсли;
	
	Если ПараметрыСправокНДФЛ.ИспользоватьНомераСправок Тогда
		Для Каждого СтрокаДанныхОДоходах Из ДанныеДоходахНалогахВычетах.Доходы Цикл
			Если СтрокаДанныхОДоходах.СуммаДохода = 0 И СтрокаДанныхОДоходах.СуммаВычета = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаДоходовСправки = ПараметрыСправокНДФЛ.СведенияОДоходах.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДоходовСправки, СтрокаДанныхОДоходах);
			СтрокаДоходовСправки.НомерСправки = СправкиПоСотрудникам[СтрокаДанныхОДоходах.Сотрудник].НомерСправки;
			СтрокаДоходовСправки.МесяцНалоговогоПериодаСтрокой = УчетНДФЛКлиентСервер.МесяцНалоговогоПериодаСтрокой(
			СтрокаДоходовСправки.МесяцНалоговогоПериода);
		КонецЦикла;
		Для Каждого СтрокаДанныхОВычетах Из ДанныеДоходахНалогахВычетах.Вычеты Цикл
			Если СтрокаДанныхОВычетах.СуммаВычета = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаВычетовСправки = ПараметрыСправокНДФЛ.СведенияОВычетах.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаВычетовСправки, СтрокаДанныхОВычетах);
			СтрокаВычетовСправки.НомерСправки = СправкиПоСотрудникам[СтрокаВычетовСправки.Сотрудник].НомерСправки;
		КонецЦикла;	
	Иначе
		Для Каждого СтрокаДанныхОДоходах Из ДанныеДоходахНалогахВычетах.Доходы Цикл
			Если СтрокаДанныхОДоходах.СуммаДохода = 0 И СтрокаДанныхОДоходах.СуммаВычета = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаДоходовСправки = ПараметрыСправокНДФЛ.СведенияОДоходах.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДоходовСправки, СтрокаДанныхОДоходах);
		КонецЦикла;
		ПараметрыСправокНДФЛ.СведенияОВычетах.Загрузить(ДанныеДоходахНалогахВычетах.Вычеты);
	КонецЕсли;	
	
	Для Каждого СтрокаИтогов Из ДанныеДоходахНалогахВычетах.Налоги Цикл
		ДанныеСправки = СправкиПоСотрудникам[СтрокаИтогов.Сотрудник];
		ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "ОбщаяСуммаДохода");
		ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "ОблагаемаяСуммаДохода");
		ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "Исчислено");
		Если ПараметрыСправокНДФЛ.НалоговыйПериод >= Год(УчетНДФЛ.ДатаИзмененияПорядкаИсчисленияНалогаДляИностранцев()) Тогда
			ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "ЗачтеноАвансовыхПлатежей");
		КонецЕсли;
		ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "Удержано");
		ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "Перечислено");
		ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "Задолженность");
		ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "ИзлишнеУдержано");
		Если ВыводитьФормуСправки2022 Тогда
			ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "НалогНаПрибыльДляДивидендов");
			ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "НалогСДивидендовУплаченныйЗаРубежом");
			ЗаполнитьПолеИтогов(ДанныеСправки, СтрокаИтогов, "СуммаДоходаНеудержанного");
		КонецЕсли;
		
	КонецЦикла;	
	
	СотрудникиСвычетамиПоУведомлениям = Новый Соответствие;
	Для Каждого СтрокаДанныхОВычетах Из ПримененныеВычетыПоГруппам Цикл
		Если СтрокаДанныхОВычетах.ГруппаВычета = Перечисления.ГруппыВычетовПоНДФЛ.Имущественные 
			Или СтрокаДанныхОВычетах.ГруппаВычета = Перечисления.ГруппыВычетовПоНДФЛ.СоциальныеПоУведомлениюНО Тогда
			СотрудникиСвычетамиПоУведомлениям.Вставить(СтрокаДанныхОВычетах.Сотрудник, Истина);
		КонецЕсли;	
	КонецЦикла;	
	Если ПараметрыСправокНДФЛ.УведомленияНОоПравеНаВычеты <> Неопределено 
		И СотрудникиСвычетамиПоУведомлениям.Количество() > 0 Тогда
		Сотрудники = Новый Массив;	
		Для каждого Элемент Из СотрудникиСвычетамиПоУведомлениям Цикл
			Сотрудники.Добавить(Элемент.Ключ)
		КонецЦикла; 
		ДанныеУведомлений = УчетНДФЛ.ДанныеУведомленийНаВычеты(Сотрудники, ПараметрыСправокНДФЛ.НалоговыйПериод, 
		ПараметрыСправокНДФЛ.Организация);
		Для каждого СтрокаУведомлений Из ДанныеУведомлений Цикл
			ЗаполнитьЗначенияСвойств(ПараметрыСправокНДФЛ.УведомленияНОоПравеНаВычеты.Добавить(), СтрокаУведомлений);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПолеИтогов(ДанныеСправки, ДанныеИтоговПоСтавке, ИмяПоля) Экспорт
	УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправки, ДанныеИтоговПоСтавке.Ставка, ИмяПоля, 
	ДанныеИтоговПоСтавке[ИмяПоля])
КонецПроцедуры

Процедура УстановитьЗначениеПоляИтоговПоСтавке(ДанныеСправки, Ставка, ИмяПоля, Значение) Экспорт
	ИмяПоляСправки = УчетНДФЛКлиентСервер.СправкиНДФЛИмяПоляИтоговПоСтавке(ИмяПоля, Ставка);
	Если ИмяПоляСправки <> Неопределено И ДанныеСправки <> Неопределено 
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеСправки, ИмяПоляСправки) <> Неопределено Тогда 
		ДанныеСправки[ИмяПоляСправки] = Значение;
	КонецЕсли;
КонецПроцедуры

Процедура ПрочитатьДанныеСотрудников(ДанныеСправок, НалоговыйПериод, ДатаДокумента, 
	ОбновлятьНеФиксированныеДанные = Истина) Экспорт
	
	ЗаполняемыеПоля = СправкиНДФЛФормыКлиентСервер.ИменаФиксируемыхПолей();	
	
	СотрудникиДляЗаполнения = Новый Массив;
	ДанныеСправокПоСотрудникам = Новый Соответствие;
	
	Для Каждого ДанныеСправкиПоСотруднику Из ДанныеСправок Цикл
		
		Если СотрудникиДляЗаполнения.Найти(ДанныеСправкиПоСотруднику.Сотрудник) = Неопределено Тогда
		
			ПолучатьКадровыеДанныеСотрудника = Ложь;
			Если ОбновлятьНеФиксированныеДанные Тогда
				Для Каждого ИмяПоля Из ЗаполняемыеПоля Цикл
					Если Не ДанныеСправкиПоСотруднику["Фикс" + ИмяПоля] Тогда 
						ПолучатьКадровыеДанныеСотрудника = Истина;	
					КонецЕсли;			
				КонецЦикла;	
			КонецЕсли;
			
			Если ПолучатьКадровыеДанныеСотрудника Тогда
				СотрудникиДляЗаполнения.Добавить(ДанныеСправкиПоСотруднику.Сотрудник);			
				ДанныеСправокПоСотрудникам.Вставить(ДанныеСправкиПоСотруднику.Сотрудник, 
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеСправкиПоСотруднику));
			КонецЕсли;
		
		Иначе
			ДанныеСправокПоСотрудникам[ДанныеСправкиПоСотруднику.Сотрудник].Добавить(ДанныеСправкиПоСотруднику)
		КонецЕсли;
		
	КонецЦикла;	
	
	Если СотрудникиДляЗаполнения.Количество() > 0 Тогда
				
		НеобходимыеКадровыеДанные = Новый Массив();
		
		НеобходимыеКадровыеДанные.Добавить("ФизическоеЛицо");
		НеобходимыеКадровыеДанные.Добавить("Фамилия");
		НеобходимыеКадровыеДанные.Добавить("Наименование");
		НеобходимыеКадровыеДанные.Добавить("Имя");
		НеобходимыеКадровыеДанные.Добавить("Отчество");
		НеобходимыеКадровыеДанные.Добавить("ДокументВид");
		НеобходимыеКадровыеДанные.Добавить("ДокументСерия");
		НеобходимыеКадровыеДанные.Добавить("ДокументНомер");
		НеобходимыеКадровыеДанные.Добавить("ДокументСтранаВыдачи");
		НеобходимыеКадровыеДанные.Добавить("ДатаРождения");	
		НеобходимыеКадровыеДанные.Добавить("Страна");
		НеобходимыеКадровыеДанные.Добавить("ИНН");
		НеобходимыеКадровыеДанные.Добавить("АдресЗаПределамиРФ");
		НеобходимыеКадровыеДанные.Добавить("АдресЗаПределамиРФПредставление");
		НеобходимыеКадровыеДанные.Добавить("АдресПоПрописке");
		НеобходимыеКадровыеДанные.Добавить("АдресПоПропискеПредставление");
		
		МенеджерВТ = Новый МенеджерВременныхТаблиц;
		
		КадровыйУчет.СоздатьНаДатуВТКадровыеДанныеФизическихЛиц(МенеджерВТ, Истина, СотрудникиДляЗаполнения, 
		НеобходимыеКадровыеДанные, ДатаДокумента);
		ПрочитатьИННвСтранеГражданства(МенеджерВТ, СотрудникиДляЗаполнения, ДатаДокумента);
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадровыеДанныеФизЛиц.АдресПоПропискеПредставление КАК АдресПредставление,
		|	КадровыеДанныеФизЛиц.АдресЗаПределамиРФПредставление КАК АдресЗарубежомПредставление,
		|	КадровыеДанныеФизЛиц.АдресПоПрописке КАК Адрес,
		|	КадровыеДанныеФизЛиц.АдресЗаПределамиРФ КАК АдресЗарубежом,
		|	КадровыеДанныеФизЛиц.Фамилия КАК Фамилия,
		|	КадровыеДанныеФизЛиц.Имя КАК Имя,
		|	КадровыеДанныеФизЛиц.Отчество КАК Отчество,
		|	КадровыеДанныеФизЛиц.ДатаРождения КАК ДатаРождения,
		|	ЕСТЬNULL(КадровыеДанныеФизЛиц.Страна, ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)) КАК Гражданство,
		|	КадровыеДанныеФизЛиц.ДокументВид КАК ВидДокумента,
		|	КадровыеДанныеФизЛиц.ДокументСерия КАК СерияДокумента,
		|	КадровыеДанныеФизЛиц.ДокументНомер КАК НомерДокумента,
		|	КадровыеДанныеФизЛиц.ДокументСтранаВыдачи КАК СтранаВыдачиДокумента,
		|	КадровыеДанныеФизЛиц.ИНН КАК ИНН,
		|	КадровыеДанныеФизЛиц.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТТекущиеДанныеФизЛиц
		|ИЗ
		|	ВТКадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизЛиц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТКадровыеДанныеФизическихЛиц
		|";	
		
		Запрос.Выполнить();
		
		КадровыйУчет.СоздатьНаДатуВТКадровыеДанныеФизическихЛиц(МенеджерВТ, Истина, СотрудникиДляЗаполнения, 
		"СтатусНалогоплательщика", КонецГода(Дата(НалоговыйПериод, 1, 1)));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТекущиеДанныеФизЛиц.Адрес КАК Адрес,
		|	ТекущиеДанныеФизЛиц.АдресЗарубежом КАК АдресЗарубежом,
		|	ТекущиеДанныеФизЛиц.Фамилия КАК Фамилия,
		|	ТекущиеДанныеФизЛиц.Имя КАК Имя,
		|	ТекущиеДанныеФизЛиц.Отчество КАК Отчество,
		|	ТекущиеДанныеФизЛиц.ДатаРождения КАК ДатаРождения,
		|	ТекущиеДанныеФизЛиц.Гражданство КАК Гражданство,
		|	ЕСТЬNULL(ИННывСтранеГражданства.ИННвСтранеГражданства, """") КАК ИННвСтранеГражданства,
		|	ТекущиеДанныеФизЛиц.ВидДокумента КАК ВидДокумента,
		|	ТекущиеДанныеФизЛиц.СерияДокумента КАК СерияДокумента,
		|	ТекущиеДанныеФизЛиц.НомерДокумента КАК НомерДокумента,
		|	ТекущиеДанныеФизЛиц.СтранаВыдачиДокумента КАК СтранаВыдачиДокумента,
		|	ТекущиеДанныеФизЛиц.ИНН КАК ИНН,
		|	СтатусыФизЛицНаКонецГода.СтатусНалогоплательщика КАК СтатусНалогоплательщика,
		|	ТекущиеДанныеФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	ВТТекущиеДанныеФизЛиц КАК ТекущиеДанныеФизЛиц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеФизическихЛиц КАК СтатусыФизЛицНаКонецГода
		|		ПО ТекущиеДанныеФизЛиц.ФизическоеЛицо = СтатусыФизЛицНаКонецГода.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИННвСтранеГражданства КАК ИННывСтранеГражданства
		|		ПО ТекущиеДанныеФизЛиц.ФизическоеЛицо = ИННывСтранеГражданства.ФизическоеЛицо";

		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ДанныеСправокПоСотруднику = ДанныеСправокПоСотрудникам[Выборка.ФизическоеЛицо];
			Для каждого ДанныеСправкиПоСотруднику Из ДанныеСправокПоСотруднику Цикл
				
				ЕстьКолонкаИННвСтранеГражданства = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеСправкиПоСотруднику, "ИННвСтранеГражданства");
				
				Для Каждого ИмяПоля Из ЗаполняемыеПоля Цикл
					Если НЕ ДанныеСправкиПоСотруднику["Фикс" + ИмяПоля] Тогда 
						ДанныеСправкиПоСотруднику[ИмяПоля] = Выборка[ИмяПоля];
						Если ИмяПоля = "Гражданство" И ЕстьКолонкаИННвСтранеГражданства Тогда
							ДанныеСправкиПоСотруднику.ИННвСтранеГражданства = Выборка.ИННвСтранеГражданства;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;	
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры	

Процедура ПрочитатьИННвСтранеГражданства(МенеджерВременныхТаблиц, Сотрудники, Дата)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГражданствоФизическихЛицСрезПоследних.ИНН КАК ИННвСтранеГражданства,
	|	ГражданствоФизическихЛицСрезПоследних.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТИННвСтранеГражданства
	|ИЗ
	|	РегистрСведений.ГражданствоФизическихЛиц.СрезПоследних(&Дата, ) КАК ГражданствоФизическихЛицСрезПоследних
	|ГДЕ
	|	ГражданствоФизическихЛицСрезПоследних.ФизическоеЛицо В(&Сотрудники)";
	Запрос.Выполнить();
	
КонецПроцедуры
 
#КонецОбласти 

Процедура СформироватьФайлСправки(ПараметрыФормированияСправки) Экспорт
	
	ИспользуетсяЭДОБЗК = ПараметрыФормированияСправки.ИспользуетсяЭДОБЗК;
	СправкаНДФЛ = ПараметрыФормированияСправки.СправкаНДФЛ;
	ИмяФайла = ПараметрыФормированияСправки.ИмяФайла;
	ТребуетсяПодписьФайла = ПараметрыФормированияСправки.ТребуетсяПодписьФайла; 
	ФизическоеЛицо = ПараметрыФормированияСправки.ФизическоеЛицо; 
	Организация = ПараметрыФормированияСправки.Организация; 
	
	ТабличныйДокумент = Документы.СправкаНДФЛ.СформироватьПечатнуюФорму2НДФЛ(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СправкаНДФЛ), Новый СписокЗначений);
	
	Если ИспользуетсяЭДОБЗК Тогда
		
		ДанныеПечатнойФормы = ЭДОБЗКВызовСервера.ДобавитьПечатнуюФорму(
			ТабличныйДокумент, СправкаНДФЛ, "Форма2НДФЛ", ИмяФайла, Организация, ФизическоеЛицо);
			
		Если Не ТребуетсяПодписьФайла Тогда
			РегистрыСведений.ЗапланированныеДействияСФайламиДокументовЭДОБЗК.ЗарегистрироватьОбработкуФайлов(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПечатнойФормы.ФайлОбъекта),
				Перечисления.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников);
		КонецЕсли;
		
	Иначе
		
		Поток = Новый ПотокВПамяти();
		ТабличныйДокумент.Записать(Поток, ЭДОБЗКВызовСервера.ТипФайлаЭлектронногоДокумента());
		АдресХранилища = ПоместитьВоВременноеХранилище(Поток.ЗакрытьИПолучитьДвоичныеДанные());
		
		ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
		ПараметрыФайла.ВладелецФайлов = СправкаНДФЛ;
		ПараметрыФайла.ИмяБезРасширения = ИмяФайла;
		ПараметрыФайла.РасширениеБезТочки = "pdf";
		
		ПрисоединенныйФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, АдресХранилища);
		
		ВидДокумента = Справочники.ВидыДокументовЭДОБЗК.ВидПоИдентификаторуПечатнойФормы("Форма2НДФЛ");
		ЗаявкиСотрудников.СоздатьДокументЭДОБЗКСправкаСотруднику(Организация, 
			ФизическоеЛицо, СправкаНДФЛ, ПрисоединенныйФайл, Не ТребуетсяПодписьФайла, ВидДокумента);
		
	КонецЕсли;   
	
КонецПроцедуры  

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли