#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Заполнение списков выбора, которые не зависят от данных (могут использоваться при обновлении подсказок).
	ОбменЛисткамиНетрудоспособностиФСС.КодыПричинИсправления(Элементы.КодПричиныИсправленияЭЛН.СписокВыбора);
	
	ЭтоВебКлиент = ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	Если ЭтоВебКлиент Тогда
		Элементы.ПредставлениеПричиныНетрудоспособности.КнопкаВыбора = Ложь;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗапланироватьПолноеОбновлениеСведенийДляФСС();
		
		Если ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
			Объект.Сотрудник = Параметры.Сотрудник;
		КонецЕсли;
		Если Параметры.Свойство("Дата") И ЗначениеЗаполнено(Параметры.Дата) Тогда 
			Объект.Дата = Параметры.Дата;
		Иначе
			// Заполнение "пустого" документа.
			ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный, Месяц",
				"Объект.Организация",
				"Объект.Ответственный",
				"Объект.ПериодРегистрации");
			ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ПричинаНетрудоспособности) Тогда
			Объект.ПричинаНетрудоспособности    = Перечисления.ПричиныНетрудоспособности.ОбщееЗаболевание;
			Объект.КодПричиныНетрудоспособности = "01";
		КонецЕсли;
		
		Если НачалоМесяца(Объект.Дата) <> НачалоМесяца(Объект.ПериодРегистрации) Тогда
			Объект.ПериодРегистрации = НачалоМесяца(Объект.Дата);
		КонецЕсли;
		
		ОбновитьГоловнуюОрганизацию();
		
		ЗаполнитьПоКадровымДаннымСотрудника();
		
		ЗаполнитьУсловияИсчисления();
		
		Если ЗначениеЗаполнено(Объект.ПериодРегистрации) Тогда
			Объект.ПланируемаяДатаВыплаты = ПланируемаяДатаВыплаты();
			ПересчитатьНДФЛ();
		КонецЕсли;
		
		ПриПолученииДанныхНаСервере();
		
	КонецЕсли;
	
	Элементы.МесяцНачисленияСтрокой.Видимость = НачалоМесяца(Объект.Дата) <> НачалоМесяца(Объект.ПериодРегистрации);
	
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// Конец БлокировкаИзмененияОбъектов
	
	// ЭДОБЗК
	ЭДОБЗК.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка, Объект);
	// Конец ЭДОБЗК
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// ИнтеграцияС1САрхивом 
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивом = ОбщегоНазначения.ОбщийМодуль("ОбменСАрхивом");
		МодульОбменСАрхивом.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1САрхивом

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗапланироватьЧтениеСведенийДляФСС();
	
	// Вместо обработчика обновления.
	ТекущийОбъект.ВключитьАвтозаполнениеПериодовОплаты();
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	Если Объект.ДоляНеполногоВремени = 0 Тогда
		Объект.ДоляНеполногоВремени = 1;
	КонецЕсли;
	
	ПриПолученииДанныхНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект,
		ПравоДоступа("Изменение", Метаданные.Документы.БольничныйЛист));
	// Конец БлокировкаИзмененияОбъектов
	
	// ЭДОБЗК
	ЭДОБЗК.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, Объект);
	// Конец ЭДОБЗК
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыРезультатыРасчетаНДФЛ" И ЭтоТекущаяФорма(Источник.ВладелецФормы) Тогда
		ОбновитьДанныеНДФЛНаСервере(Параметр);
	КонецЕсли;
	
	Если ИмяСобытия = "ВведеныДанныеЛисткаНетрудоспособности" И ЭтоТекущаяФорма(Источник) Тогда
		ОбновитьДанныеЛисткаНетрудоспособностиПослеРедактированияНаСервере(Параметр);
	ИначеЕсли ИмяСобытия = "Запись_ЗаявлениеСотрудникаНаВыплатуПособия" Тогда
		ОбновитьЭлементыПрямыхВыплатФСС();
	ИначеЕсли ИмяСобытия = "Запись_РеестрДанныхЭЛНЗаполняемыхРаботодателем" Тогда
		ТребуетсяПрочитатьСведенияОбЭЛН = Истина;
		ОбновитьСведенияДляФСС();
	ИначеЕсли ИмяСобытия = "Запись_ИсходящееСообщениеОСтраховомСлучаеФСС"
		Или ИмяСобытия = "Запись_ВходящийЗапросФССДляРасчетаПособия"
		Или ИмяСобытия = "Запись_ДанныеСФРДляРасчетаПособия"
		Или ИмяСобытия = "Запись_ОтветНаЗапросФССДляРасчетаПособия" Тогда
		ОбновитьЭлементыСЭДО();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// ЭДОБЗК
	ЭДОБЗККлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭДОБЗК
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ПроведениеСервер.УстановитьПризнакПроверкиРеквизитов(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	ПередЗаписьюНаСервереЭЛН(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Оповестить("Запись_БольничныйЛист", ПараметрыЗаписи, Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ИмяПроцедурыПослеЗаписи") Тогда
		Обработчик = Новый ОписаниеОповещения(ПараметрыЗаписи.ИмяПроцедурыПослеЗаписи, ЭтотОбъект, ПараметрыЗаписи);
		ПараметрыЗаписи.Удалить("ИмяПроцедурыПослеЗаписи");
		ВыполнитьОбработкуОповещения(Обработчик, Истина);
	КонецЕсли;
	
	Оповестить("ЗаявкиСотрудниковЗаписанДокумент", Объект.Ссылка, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка)
		И ЗначениеЗаполнено(Объект.НомерЛисткаНетрудоспособности)
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		ПодключитьОбработчикОжидания("ПолучитьЭЛНИзФССПослеОткрытияФормы", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ОбработкаПроверкиЗаполненияНаСервереЭЛН(Отказ, ПроверяемыеРеквизиты);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗапланироватьЧтениеСведенийДляФСС();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец БлокировкаИзмененияОбъектов
	
	// ЭДОБЗК
	ЭДОБЗК.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Объект);
	// Конец ЭДОБЗК
	
	ОбновитьСведенияДляФСС();
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерЛисткаНетрудоспособностиПриИзменении(Элемент)
	НомерЛисткаНетрудоспособностиПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	СотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтотОбъект, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой", Модифицированность);
	
	ПериодРегистрацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбораПериода = Новый Структура("НачалоПериода, КонецПериода", НачалоМесяца(Объект.ПериодРегистрации), КонецМесяца(Объект.ПериодРегистрации));
	ОписаниеОповещения     = Новый ОписаниеОповещения("МесяцНачисленияСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбораПериода, Элементы.МесяцНачисленияСтрокой, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойНачалоВыбораЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПериодРегистрации = РезультатВыбора.НачалоПериода;
	
	ПериодРегистрацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтотОбъект, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой", Направление, Модифицированность);
	ПериодРегистрацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПервичныйЛистокНетрудоспособностиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		ПараметрыОтбора.Вставить("ДатаОкончания", НачалоДня(НачалоДня(Объект.ДатаНачала)-1));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ПараметрыОтбора.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		ПараметрыОтбора.Вставить("Сотрудник", Объект.Сотрудник);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПричинаНетрудоспособности) Тогда
		ПараметрыОтбора.Вставить("ПричинаНетрудоспособности", Объект.ПричинаНетрудоспособности);
	КонецЕсли;
	
	ПараметрыОткрытияФормыВыбора = Новый Структура;
	ПараметрыОткрытияФормыВыбора.Вставить("РежимВыбораПервичногоБольничногоЛиста", Истина);
	
	Если ПараметрыОтбора.Количество() > 0 Тогда
		ПараметрыОткрытияФормыВыбора.Вставить("Отбор", ПараметрыОтбора);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ПервичныйБольничныйЛист) Тогда
		ПараметрыОткрытияФормыВыбора.Вставить("ПервичныйБольничныйЛист", Объект.ПервичныйБольничныйЛист);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыОткрытияФормыВыбора.Вставить("ТекущийДокумент", Объект.Ссылка);
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПервичныйБольничныйЛистЗавершениеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("Документ.БольничныйЛист.ФормаВыбора", ПараметрыОткрытияФормыВыбора, , , , , Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПричиныНетрудоспособностиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ОбщееЗаболевание     = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание");
	ПоБеременностиИРодам = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам");
	ПоУходуЗаРебенком    = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ПоУходуЗаРебенком");
	До7ЛетАмбулаторно    = ПредопределенноеЗначение("Перечисление.СлучаиУходаЗаБольнымиДетьми.ПоУходуДо7миЛетАмбулаторно");
	
	ДанныеВыбора = Новый СписокЗначений;
	
	СтруктураПоиска = Новый Структура("ПричинаНетрудоспособности, КодПричиныНетрудоспособности",
										ОбщееЗаболевание, "01");
	МассивСтрок = ТаблицаЗаболеваний.НайтиСтроки(СтруктураПоиска);
	ДанныеВыбора.Добавить(МассивСтрок[0].Наименование);
	
	СтруктураПоиска = Новый Структура("ПричинаНетрудоспособности, КодПричиныНетрудоспособности",
										ПоБеременностиИРодам, "05");
	МассивСтрок = ТаблицаЗаболеваний.НайтиСтроки(СтруктураПоиска);
	ДанныеВыбора.Добавить(МассивСтрок[0].Наименование);
	
	СтруктураПоиска = Новый Структура("ПричинаНетрудоспособности, СлучайУходаЗаБольнымРебенком, КодПричиныНетрудоспособности",
										ПоУходуЗаРебенком, До7ЛетАмбулаторно, "09");
	МассивСтрок = ТаблицаЗаболеваний.НайтиСтроки(СтруктураПоиска);
	ДанныеВыбора.Добавить(МассивСтрок[0].Наименование);
	
	Если ЗначениеЗаполнено(Объект.ПричинаНетрудоспособности) Тогда
		Если (Объект.ПричинаНетрудоспособности = ОбщееЗаболевание
				И ЗначениеЗаполнено(Объект.КодПричиныНетрудоспособности)
				И Объект.КодПричиныНетрудоспособности <> "01")
			И Объект.ПричинаНетрудоспособности <> ПоБеременностиИРодам
			И НЕ (Объект.ПричинаНетрудоспособности = ПоУходуЗаРебенком И Объект.СлучайУходаЗаБольнымРебенком = До7ЛетАмбулаторно) Тогда
			
			Если Объект.ПричинаНетрудоспособности = ПоУходуЗаРебенком Тогда
				СтруктураПоиска = Новый Структура("ПричинаНетрудоспособности, СлучайУходаЗаБольнымРебенком",
													Объект.ПричинаНетрудоспособности, Объект.СлучайУходаЗаБольнымРебенком);
			Иначе
				СтруктураПоиска = Новый Структура("ПричинаНетрудоспособности", 
													Объект.ПричинаНетрудоспособности);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Объект.КодПричиныНетрудоспособности) Тогда
				СтруктураПоиска.Вставить("КодПричиныНетрудоспособности", Объект.КодПричиныНетрудоспособности);
			КонецЕсли;
			
			МассивСтрок = ТаблицаЗаболеваний.НайтиСтроки(СтруктураПоиска);
			Если МассивСтрок.Количество() <> 0 Тогда
				Если Не МассивСтрок[0].Недоступен Тогда
					ДанныеВыбора.Добавить(МассивСтрок[0].Наименование);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоВебКлиент Тогда
		ДанныеВыбора.Добавить(НСтр("ru = 'Показать все...'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПричиныНетрудоспособностиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьОповещениеОбИзмененииПричиныЗаболевания", ЭтотОбъект);
	
	ОткрытьФорму("Документ.БольничныйЛист.Форма.ФормаВыбораПричиныЗаболевания",
		Новый Структура("ДатаНачалаСобытия", Объект.ДатаНачалаСобытия),
		ЭтотОбъект,
		,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПричиныНетрудоспособностиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтруктураПоиска = Новый Структура("Наименование", ВыбранноеЗначение);
	МассивСтрок = ТаблицаЗаболеваний.НайтиСтроки(СтруктураПоиска);
	Если МассивСтрок.Количество() = 0 Тогда
		СтандартнаяОбработка = Ложь;
		Оповещение = Новый ОписаниеОповещения("ОбработатьОповещениеОбИзмененииПричиныЗаболевания", ЭтотОбъект);
		ОткрытьФорму("Документ.БольничныйЛист.Форма.ФормаВыбораПричиныЗаболевания",
			Новый Структура("ДатаНачалаСобытия", Объект.ДатаНачалаСобытия),
			ЭтотОбъект,
			,
			,
			,
			Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		Объект.ПричинаНетрудоспособности = МассивСтрок[0].ПричинаНетрудоспособности;
		Объект.СлучайУходаЗаБольнымРебенком = МассивСтрок[0].СлучайУходаЗаБольнымРебенком;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПричиныНетрудоспособностиПриИзменении(Элемент)
	
	ПричинаНетрудоспособностиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КарантинПоКоронавирусуПриИзменении(Элемент)
	КарантинПоКоронавирусуПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	ДатаНачалаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	ДатаОкончанияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНарушенияРежимаПриИзменении(Элемент)
	
	РассчитатьНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентОплатыПриИзменении(Элемент)
	
	РассчитатьНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РайонныйКоэффициентРФНаНачалоСобытияПриИзменении(Элемент)
	
	РассчитатьНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоляНеполногоВремениПриИзменении(Элемент)
	
	Если Объект.ДоляНеполногоВремени = 0 Тогда
		Объект.ДоляНеполногоВремени = 1;
	КонецЕсли;
	РассчитатьСреднийЗаработок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентОплатыБезЛьготПриИзменении(Элемент)
	
	РассчитатьНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПоловиннойОплатыПриИзменении(Элемент)
	
	РассчитатьНачисленияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланируемаяДатаВыплатыПриИзменении(Элемент)
	
	ПересчитатьНДФЛ();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключаемыеПериодыНадписьНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИзменитьПериодыНаКлиенте("ИсключаемыеПериоды");
КонецПроцедуры

&НаКлиенте
Процедура ПериодыПростояНадписьНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИзменитьПериодыНаКлиенте("ПериодыПростоя");
КонецПроцедуры

#Область ПрямыеВыплатыФСС

&НаКлиенте
Процедура ПособиеВыплачиваетсяФССПриИзменении(Элемент)
	ОбновитьСведенияДляФСС();
КонецПроцедуры

&НаКлиенте
Процедура СсылкиПрямыхВыплатФССОбработкаНавигационнойСсылки(Элемент, Адрес, СтандартнаяОбработка)
	Если Адрес = "СоздатьСведения" Тогда
		СтандартнаяОбработка = Ложь;
		СоздатьСведенияДляПрямыхВыплатФСС();
	ИначеЕсли Адрес = "ПоказатьВсе" Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьВсеСведенияДляПрямыхВыплатФСС();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ЭЛН

&НаКлиенте
Процедура ОтправитьЭЛНПриИзменении(Элемент)
	ОтправитьЭЛНПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИсправлениеЭЛНПриИзменении(Элемент)
	ИсправлениеЭЛНПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИсправлениеЭЛНЧисломПриИзменении(Элемент)
	ИсправлениеЭЛНЧисломПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КодПричиныИсправленияЭЛНПриИзменении(Элемент)
	Если Не ЗначениеЗаполнено(ОписаниеПричиныИсправленияЭЛН)
		Или ОписаниеПричиныИсправленияЭЛН = Элементы.КодПричиныИсправленияЭЛН.Подсказка Тогда
		ОписаниеПричиныИсправленияЭЛН = НаименованиеПричиныИсправленияЭЛН(ЭтотОбъект);
	КонецЕсли;
	ОбновитьЭлементыЭЛН();
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПричиныИсправленияЭЛННачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.КомментарийНачалоВыбора(
		ЭтотОбъект,
		Элемент,
		ДанныеВыбора,
		СтандартнаяОбработка,
		"ОписаниеПричиныИсправленияЭЛН",
		Элемент.ПодсказкаВвода);
КонецПроцедуры

&НаКлиенте
Процедура СсылкиРеестровЭЛНОбработкаНавигационнойСсылки(Элемент, Адрес, СтандартнаяОбработка)
	
	Если Адрес = "ПринятыйРеестрЭЛН" Тогда
		
		СтандартнаяОбработка = Ложь;
		УчетПособийСоциальногоСтрахованияКлиент.ОткрытьРеестрЭЛН(
			СведенияОбЭЛН.ПринятыйРеестр,
			Объект.НомерЛисткаНетрудоспособности);
		
	ИначеЕсли Адрес = "ПодготовленныйРеестрЭЛН" Тогда
		
		СтандартнаяОбработка = Ложь;
		УчетПособийСоциальногоСтрахованияКлиент.ОткрытьРеестрЭЛН(
			СведенияОбЭЛН.ПодготовленныйРеестр,
			Объект.НомерЛисткаНетрудоспособности);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Подвал

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.КомментарийНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка)
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПервичныйПродолжениеПриИзменении(Элемент)
	
	Объект.ЯвляетсяПродолжениемБолезни = ПервичныйПродолжение;
	Если НЕ Объект.ЯвляетсяПродолжениемБолезни Тогда
		Объект.ДатаНачалаСобытия = Объект.ДатаНачала;
	КонецЕсли;
	ПодготовитьТаблицуЗаболеваний(Объект.ДатаНачалаСобытия);
	ЯвляетсяПродолжениемБолезниПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КодНарушенияРежимаПриИзменении(Элемент)
	
	ИзменениеКодаНарушенияРежима();
	
КонецПроцедуры

&НаКлиенте
Процедура КодНарушенияРежимаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущийКод = Элемент.СписокВыбора.НайтиПоЗначению(Объект.КодНарушенияРежима);
	ОповещениеВыбора = Новый ОписаниеОповещения("ВыборИзСпискаЗавершение",ЭтотОбъект);
	ПоказатьВыборИзСписка(ОповещениеВыбора, Элемент.СписокВыбора, Элемент, ТекущийКод);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		Если НачалоМесяца(Объект.ПериодРегистрации) <> НачалоМесяца(Объект.Дата) Тогда
			Объект.ПериодРегистрации = НачалоМесяца(Объект.Дата);
			ПериодРегистрацииПриИзмененииНаСервере();
		Иначе
			ОбработатьИзменениеДатыНаСервере();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛИтогНалогНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ КлючевыеРеквизитыЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	УчетНДФЛКлиент.ОткрытьФормуПодробнееОРасчетеНДФЛ(Объект.Организация, ЭтотОбъект, Объект.ПериодРегистрации, Объект.Сотрудник,
		Объект.ПланируемаяДатаВыплаты);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленоИтогНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ КлючевыеРеквизитыЗаполнены() Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("РедактированиеРасчетаЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресПараметровВХранилище",   АдресПараметровВХранилище());
	ПараметрыОткрытия.Вставить("ЯвляетсяПродолжениемБолезни", Объект.ЯвляетсяПродолжениемБолезни или ТолькоПросмотр);
	ПараметрыОткрытия.Вставить("ТолькоПросмотр",              ТолькоПросмотр);
	
	ОткрытьФорму("Документ.БольничныйЛист.Форма.ФормаПодробнееОРасчете", 
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СтажЛетПриИзменении(Элемент)
	
	ИзменитьСтажИПересчитать();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ЗакрытьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура ДнейПриостановленияТДНадписьНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПроверкиБЗККлиентСервер.ПериодСоответствуетТребованиям(
			ЭтотОбъект,
			"Объект",
			"ДатаНачала",
			"ДатаОкончания",
			НСтр("ru = 'нетрудоспособности'")) Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ДатаНачалаСобытия) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не заполнена дата начала нетрудоспособности.'"), "Объект.ДатаНачалаСобытия");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ГоловнаяОрганизация) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не выбрана организация.'"), "Объект.Организация");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не выбран сотрудник.'"), "Объект.Сотрудник");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не определен первый год расчета среднего заработка.'"), "Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не определен второй год расчета среднего заработка.'"), "Объект.ПериодРасчетаСреднегоЗаработкаВторойГод");
		Возврат;
	КонецЕсли;
	Если Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод > Объект.ПериодРасчетаСреднегоЗаработкаВторойГод Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Первый год расчета среднего заработка больше второго.'"), "Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод");
		Возврат;
	ИначеЕсли Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Совпадают годы расчета среднего заработка.'"), "Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод");
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("АдресСтруктурыБольничного", АдресСтруктурыБольничногоДляФормыПриостановленияТрудовыхДоговоров());
	Обработчик     = Новый ОписаниеОповещения("ПослеВводаПриостановленийТрудовыхДоговоров", ЭтотОбъект, ПараметрыФормы);
	ОткрытьФорму("Документ.БольничныйЛист.Форма.ПриостановленияТрудовыхДоговоров", ПараметрыФормы, ЭтотОбъект, , , , Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыСКонтрагентамиПриИзменении(Элемент)
	
	Если РасчетыСКонтрагентами Тогда
		Объект.СпособРасчетовСФизическимиЛицами = ПредопределенноеЗначение("Перечисление.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами");
	Иначе
		Объект.СпособРасчетовСФизическимиЛицами = ПредопределенноеЗначение("Перечисление.СпособыРасчетовСФизическимиЛицами.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПредставлениеЛисткаНетрудоспособностиНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИменаРеквизитов = ИменаРеквизитовЛисткаНетрудоспособности();
	ПараметрыФормы = Новый Структура(ИменаРеквизитов);
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Объект);
	ПараметрыФормы.Вставить("СтрокаВозвращаемыхРеквизитов", ИменаРеквизитов);
	ПараметрыФормы.Вставить("НомерЛисткаНетрудоспособности", Объект.НомерЛисткаНетрудоспособности);
	ПараметрыФормы.Вставить("ДатаНачалаСобытия",             Объект.ДатаНачалаСобытия);
	ПараметрыФормы.Вставить("ДатаНачалаСобытия", Объект.ДатаНачалаСобытия);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ГоловнаяОрганизация", Объект.ГоловнаяОрганизация);
	ПараметрыФормы.Вставить("ФизическоеЛицо", Объект.ФизическоеЛицо);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("Номер", Объект.Номер);
	ПараметрыФормы.Вставить("Дата", Объект.Дата);
	ПараметрыФормы.Вставить("ПериодыУходаЗаРодственниками", ПериодыУхода());
	ОбработчикВыбора = Новый ОписаниеОповещения("ПослеВводаДанныхЛисткаНетрудоспособности", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВводДанныхЛисткаНетрудоспособности", ПараметрыФормы, ЭтотОбъект, , , , ОбработчикВыбора);
КонецПроцедуры

#Область ЭЛН

&НаКлиенте
Процедура ПолучитьИзФСС(Команда)
	ОчиститьСообщения();
	Если Не РазрешеноПолучениеИзФСС() Тогда
		Возврат;
	КонецЕсли;
	Результат = ПолучитьИзФССНаСервере();
	Если Результат.БольничныйЗаполнен Тогда
		ПослеУспешнойЗагрузкиЭЛН();
	Иначе
		НачатьПолучениеДанныхИзФСС(Результат.ЗапросДляПолученияЭЛН);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИзФССБезКэша(Команда)
	НачатьПолучениеДанныхИзФСС(ВыгрузитьЗапросДляПолученияЭЛН());
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	Обработчик = Новый ОписаниеОповещения("ПослеВыбораФайлаЭЛН", ЭтотОбъект);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗагрузки.Диалог.Фильтр = НСтр("ru = 'Файлы XML (*.xml)|*.xml|Все файлы (*.*)|*'");
	ПараметрыЗагрузки.Диалог.ИндексФильтра = 0;
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите файл ЭЛН, полученный из ФСС'");
	
	ФайловаяСистемаКлиент.ЗагрузитьФайл(Обработчик, ПараметрыЗагрузки);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайл(Команда)
	ОписаниеФайла = ОписаниеФайлаЭЛН(Объект.НомерЛисткаНетрудоспособности, Объект.ГоловнаяОрганизация, УникальныйИдентификатор);
	УчетПособийСоциальногоСтрахованияКлиент.СохранитьФайлXML(ОписаниеФайла);
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьБольничный(Команда)
	ЗаполнитьБольничныйПоДаннымЛисткаНетрудоспособности();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеРеестрыЭЛН(Команда)
	ОтборыСписка = Новый Структура;
	ОтборыСписка.Вставить("ГоловнаяОрганизация", Объект.ГоловнаяОрганизация);
	ОтборыСписка.Вставить("Организация",         Объект.Организация);
	
	ПараметрыСписка = Новый Структура;
	ПараметрыСписка.Вставить("Отбор", ОтборыСписка);
	ПараметрыСписка.Вставить("НомерЛисткаНетрудоспособности", Объект.НомерЛисткаНетрудоспособности);
	
	УникальностьСписка = Объект.НомерЛисткаНетрудоспособности;
	
	ОткрытьФорму("Документ.РеестрДанныхЭЛНЗаполняемыхРаботодателем.ФормаСписка", ПараметрыСписка, , УникальностьСписка);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПериодыПростояИсключения

&НаКлиенте
Процедура ИзменитьПериодыНаКлиенте(ИмяТаблицы)
	Если Не ПроверкиБЗККлиентСервер.ПериодСоответствуетТребованиям(
			ЭтотОбъект,
			"Объект",
			"ДатаНачала",
			"ДатаОкончания",
			НСтр("ru = 'нетрудоспособности'")) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("АдресСтруктурыБольничного", АдресСтруктурыБольничногоДляФормПериодов());
	ПараметрыФормы.Вставить("ИмяТаблицы", ИмяТаблицы);
	Обработчик     = Новый ОписаниеОповещения("ПослеВводаПериодов", ЭтотОбъект, ПараметрыФормы);
	ОткрытьФорму("Документ.БольничныйЛист.Форма." + ИмяТаблицы, ПараметрыФормы, ЭтотОбъект, , , , Обработчик);
КонецПроцедуры

&НаКлиенте
Функция ПериодыУхода()
	ПериодыУхода = Новый Массив;
	ИменаПолей = "КодПричины, ДатаНачала, ДатаОкончания, КодСвязи,
		|Фамилия, Имя, Отчество, СНИЛС, ДатаРождения,
		|РежимЛечения, ВозрастЛет, ВозрастМесяцев, СлучайУхода";
	Для Каждого СтрокаТаблицы Из Объект.ПериодыУходаЗаРодственниками Цикл
		Структура = Новый Структура(ИменаПолей);
		ЗаполнитьЗначенияСвойств(Структура, СтрокаТаблицы, ИменаПолей);
		ПериодыУхода.Добавить(Структура);
	КонецЦикла;
	Возврат ПериодыУхода;
КонецФункции

&НаКлиенте
Процедура ПослеВводаПериодов(РезультатВыбора, ПараметрыФормы) Экспорт
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	Модифицированность = Истина;
	ПослеВводаПериодовНаСервере(РезультатВыбора, ПараметрыФормы);
КонецПроцедуры

&НаСервере
Процедура ПослеВводаПериодовНаСервере(Знач РезультатВыбора, Знач ПараметрыФормы)
	Объект.АвтозаполнениеПериодовОплаты = РезультатВыбора.АвтозаполнениеПериодовОплаты И Объект.АвтозаполнениеПериодовОплаты;
	Объект.СдвигатьПериодОплаты = РезультатВыбора.СдвигатьПериодОплаты;
	Объект[ПараметрыФормы.ИмяТаблицы].Очистить();
	Для Каждого Структура Из РезультатВыбора[ПараметрыФормы.ИмяТаблицы] Цикл
		ЗаполнитьЗначенияСвойств(Объект[ПараметрыФормы.ИмяТаблицы].Добавить(), Структура);
	КонецЦикла;
	ЗаполнитьПараметрыОплаты();
	РассчитатьНачисленияНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ПриостановленияТрудовыхДоговоров

&НаСервере
Функция АдресСтруктурыБольничногоДляФормыПриостановленияТрудовыхДоговоров()
	ИменаСвойств = 
	"Ссылка, ДатаНачала, ДатаОкончания, ДатаНачалаСобытия,
	|Организация, ГоловнаяОрганизация, Сотрудник, ФизическоеЛицо,
	|ПричинаНетрудоспособности, НачалоВПериодПриостановленияТД,
	|ДнейПриостановленияТДЗаГод1, ДнейПриостановленияТДЗаГод2,
	|ПериодРасчетаСреднегоЗаработкаПервыйГод, ПериодРасчетаСреднегоЗаработкаВторойГод";
	
	СтруктураБольничного = Новый Структура(ИменаСвойств);
	ЗаполнитьЗначенияСвойств(СтруктураБольничного, Объект, ИменаСвойств);
	
	СтруктураБольничного.Вставить("ПриостановленияТрудовыхДоговоров", Объект.ПриостановленияТрудовыхДоговоров.Выгрузить());
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураБольничного, УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура ПослеВводаПриостановленийТрудовыхДоговоров(РезультатВыбора, ПараметрыФормы) Экспорт
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	Модифицированность = Истина;
	ПослеВводаПриостановленийТрудовыхДоговоровНаСервере(РезультатВыбора, ПараметрыФормы);
КонецПроцедуры

&НаСервере
Процедура ПослеВводаПриостановленийТрудовыхДоговоровНаСервере(Знач РезультатВыбора, Знач ПараметрыФормы)
	УдалитьИзВременногоХранилища(ПараметрыФормы.АдресСтруктурыБольничного);
	Объект.НачалоВПериодПриостановленияТД = РезультатВыбора.НачалоВПериодПриостановленияТД;
	Объект.ПриостановленияТрудовыхДоговоров.Очистить();
	Для Каждого Структура Из РезультатВыбора.ПриостановленияТрудовыхДоговоров Цикл
		Период = Объект.ПриостановленияТрудовыхДоговоров.Добавить();
		ЗаполнитьЗначенияСвойств(Период, Структура);
	КонецЦикла;
	ПроверитьПриостановленияТрудовыхДоговоров(Истина);
	ОбновитьЭлементыПараметровОплаты();
	РассчитатьСреднийЗаработок();
	РассчитатьНачисленияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПриостановленияТрудовыхДоговоров()
	ДанныеУчета = УчетПособийСоциальногоСтрахования.ДанныеУчетаПриостановленийТД(Объект);
	Объект.ПриостановленияТрудовыхДоговоров.Загрузить(ДанныеУчета.ПриостановленияТД);
	Объект.НачалоВПериодПриостановленияТД = ДанныеУчета.НачалоВПериодПриостановленияТД;
	ПроверитьПриостановленияТрудовыхДоговоров(Истина);
КонецПроцедуры

&НаСервере
Процедура ПроверитьПриостановленияТрудовыхДоговоров(ИзмененияРазрешены)
	ПриостановленияТД = Объект.ПриостановленияТрудовыхДоговоров.Выгрузить();
	ПриостановленияТД.Колонки.Добавить("Год",         Новый ОписаниеТипов("Число"));
	ПриостановленияТД.Колонки.Добавить("Ошибка",      Новый ОписаниеТипов("Булево"));
	ПриостановленияТД.Колонки.Добавить("ТекстОшибки", Новый ОписаниеТипов("Строка"));
	
	УчетПособийСоциальногоСтрахования.ПроверитьПриостановленияТрудовыхДоговоров(
		Объект,
		ПриостановленияТД,
		ИзмененияРазрешены);
	
	Объект.ПриостановленияТрудовыхДоговоров.Загрузить(ПриостановленияТД);
	
	Если ПриостановленияТД.Найти(Истина, "Ошибка") = Неопределено Тогда
		Элементы.ДнейПриостановленияТДНадпись.ЦветТекста = Новый Цвет;
	Иначе
		Элементы.ДнейПриостановленияТДНадпись.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
	КонецЕсли;
	
	ДнейПриостановленияТД = Объект.ДнейПриостановленияТДЗаГод1 + Объект.ДнейПриостановленияТДЗаГод2;
	ОбновитьГруппуПриостановкаТД();
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаИзмененияОбъектов

// БлокировкаИзмененияОбъектов
&НаКлиенте
Процедура Подключаемый_РазблокироватьФормуОбъекта(Команда)
	
	БлокировкаИзмененияОбъектовКлиент.РазблокироватьФормуОбъекта(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры
// Конец БлокировкаИзмененияОбъектов

#КонецОбласти

#Область ИнтеграцияС1СДокументооборотом

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
			Команда,
			ЭтотОбъект,
			Объект);
	КонецЕсли;
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ОбновитьПодключаемыеКоманды(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Выплатить(Команда)
	
	Если НЕ Объект.ПометкаУдаления И НЕ Объект.Проведен ИЛИ Модифицированность Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Провести");
		Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередВыплатойСледуетПровестиЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед выплатой документ следует провести'"), Кнопки,, КодВозвратаДиалога.Да);
	Иначе
		СформироватьДокументыВыплаты();
	КонецЕсли;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

#Область ИнтеграцияС1САрхивом

// ИнтеграцияС1САрхивом

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграцииС1САрхивом(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивомКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСАрхивомКлиент");
		МодульОбменСАрхивомКлиент.ВыполнитьПодключаемуюКоманду(Команда, ЭтотОбъект, Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуИнтеграцииС1САрхивомНаСервере(Знач Параметры) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивом = ОбщегоНазначения.ОбщийМодуль("ОбменСАрхивом");
		МодульОбменСАрхивом.ВыполнитьПодключаемуюКоманду(ЭтотОбъект, Параметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИнтеграцияС1САрхивом_Нажатие(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1САрхивом") Тогда
		МодульОбменСАрхивомКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбменСАрхивомКлиент");
		МодульОбменСАрхивомКлиент.Нажатие(Элемент, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// Конец ИнтеграцияС1САрхивом

#КонецОбласти

#Область УсловияИсчисления

&НаСервере
Процедура ЗаполнитьУсловияИсчисления()
	
	Если ЗначениеЗаполнено(ДатаУвольненияСотрудника)
		И Объект.ДатаНачалаСобытия > ДатаУвольненияСотрудника
		И НЕ ЕстьДоговорыГПХ Тогда
		УчетПособийСоциальногоСтрахования.УстановитьУсловиеИсчисления(Объект, "47");
	Иначе
		УчетПособийСоциальногоСтрахования.УдалитьУсловиеИсчисления(Объект, "47");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Конец ЭДОБЗК

#КонецОбласти

#Область СведенияДляФСС

&НаСервере
Процедура ЗапланироватьПолноеОбновлениеСведенийДляФСС()
	
	ЗапланироватьЧтениеСведенийДляФСС();
	ЗапланироватьЗаполнениеСведенийДляФССЗависящихОтНачислений();
	
КонецПроцедуры

&НаСервере
Процедура ЗапланироватьЧтениеСведенийДляФСС()
	
	ТребуетсяПрочитатьФлажокИспользоватьПрямыеВыплатыФСС = Истина;
	ТребуетсяПрочитатьСведенияОбЭЛН = Истина;
	ТребуетсяПрочитатьРеквизитыЭЛН = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗапланироватьЗаполнениеСведенийДляФССЗависящихОтНачислений()
	
	ЕстьПособияЗаСчетФСС = Неопределено;
	
	ТребуетсяЗаполнитьПараметрыПрямыхВыплатФСС = Истина;
	ТребуетсяЗаполнитьПараметрыОтправкиЭЛН = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияДляФСС()
	
	// Чтение данных из базы данных.
	Если ТребуетсяПрочитатьФлажокИспользоватьПрямыеВыплатыФСС Тогда
		ПрочитатьФлажокИспользоватьПрямыеВыплатыФСС();
	КонецЕсли;
	ПрочитатьСведенияОбЭЛНЕслиТребуется();
	Если ТребуетсяПрочитатьРеквизитыЭЛН Тогда
		ПрочитатьРеквизитыЭЛН();
	КонецЕсли;
	
	// Заполнение вторичных данных.
	Если ТребуетсяЗаполнитьПараметрыПрямыхВыплатФСС Тогда
		ЗаполнитьПараметрыПрямыхВыплатФСС();
	КонецЕсли;
	Если ТребуетсяЗаполнитьПараметрыОтправкиЭЛН Тогда
		ЗаполнитьПараметрыОтправкиЭЛН();
	КонецЕсли;
	
	// Обновление элементов формы.
	ОбновитьЭлементыСЭДО();
	ОбновитьЭлементыЭЛН();
	ОбновитьЭлементыПрямыхВыплатФСС();
	
	// Обновление страницы.
	ВидимостьСтраницы = Элементы.ГруппаПрямыеВыплатыФСС.Видимость Или Элементы.ГруппаЭЛН.Видимость;
	Элементы.СтраницаОтправкаСведенийВФСС.Видимость = ВидимостьСтраницы;
	
	Если ВидимостьСтраницы Тогда
		
		Элементы.НеЗакрытГруппа.Видимость = СведенияОбЭЛН.ЭтоЭЛН И Не СведенияОбЭЛН.Закрыт;
		
		Если СведенияОбЭЛН.ЭтоПоследнийБольничный Или Не ЗначениеЗаполнено(Объект.НомерЛисткаНетрудоспособности) Тогда
			Элементы.ГруппаСсылкиБольничныхЭтогоЭЛН.Видимость = Ложь;
		Иначе
			Если Не Объект.Проведен И Не Объект.ПометкаУдаления Тогда
				Если СведенияОбЭЛН.БольничныйПроведен Тогда
					ЗаголовокНадписи = НСтр("ru = 'Последний проведенный больничный: <a href=""%1"">%2</a>.'");
				Иначе
					ЗаголовокНадписи = НСтр("ru = 'Последний больничный: <a href=""%1"">%2</a>.'");
				КонецЕсли;
			Иначе
				ЗаголовокНадписи = НСтр("ru = 'Данный больничный не является последним для ЭЛН %3.
					|Сведения для ФСС можно изменить в <a href=""%1"">%2</a>.'");
			КонецЕсли;
			ЗаголовокНадписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокНадписи,
				ПолучитьНавигационнуюСсылку(СведенияОбЭЛН.Больничный),
				СведенияОбЭЛН.БольничныйПредставление,
				Объект.НомерЛисткаНетрудоспособности);
			Элементы.ГруппаСсылкиБольничныхЭтогоЭЛН.Видимость = Истина;
			Элементы.СсылкиБольничныхЭтогоЭЛН.Заголовок       = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокНадписи);
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьСреднийЗаработок(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ЕстьПособияЗаСчетФСС()
	Если ЕстьПособияЗаСчетФСС = Неопределено Тогда
		ЕстьПособияЗаСчетФСС = ПрямыеВыплатыПособийСоциальногоСтрахования.КоллекцияСодержитПособияЗаСчетФСС(
			Объект.Начисления.Выгрузить(, "Начисление").ВыгрузитьКолонку("Начисление"));
	КонецЕсли;
	Возврат ЕстьПособияЗаСчетФСС;
КонецФункции

&НаСервере
Функция ЭтоНСПЗ()
	Возврат Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ТравмаНаПроизводстве
		Или Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.Профзаболевание;
КонецФункции

#КонецОбласти

#Область СЭДО

&НаСервере
Процедура ОбновитьЭлементыСЭДО()
	// СЭДО по выплате пособий - Только для вступивших в прямые выплаты.
	Если Не ИспользоватьПрямыеВыплатыФСС Тогда
		Элементы.СЭДОГруппа.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	// СЭДО - только для ЭЛН.
	Если Не СведенияОбЭЛН.ЭтоЭЛН Тогда
		Элементы.СЭДОГруппа.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	// СЭДО с 2022 года.
	Если ЗначениеЗаполнено(Объект.ДатаНачалаСобытия)
		И Объект.ДатаНачалаСобытия < '20220101' Тогда
		Элементы.СЭДОГруппа.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.СЭДОГруппа.Видимость = Истина;
	
	ЕстьВходящийЗапрос            = ЗначениеЗаполнено(ВходящийЗапрос);
	ЕстьОтветНаЗапрос             = ЗначениеЗаполнено(ОтветНаЗапрос);
	ЕстьСообщениеОСтраховомСлучае = ЗначениеЗаполнено(СообщениеОСтраховомСлучае);
	ЕстьДанныеСФРДляРасчетаПособия = ЗначениеЗаполнено(ДанныеСФРДляРасчетаПособия);
	Выплачивается                 = Объект.ПособиеВыплачиваетсяФСС И Не СведенияОбЭЛН.Аннулирован;
	
	Элементы.ВходящийЗапросОтсутствуетГруппа.Видимость = Выплачивается И Не ЕстьВходящийЗапрос И Не ЕстьОтветНаЗапрос;
	Элементы.СообщитьОСтраховомСлучаеГруппа.Видимость  = Не ЕстьСообщениеОСтраховомСлучае;
	Элементы.СообщениеОСтраховомСлучае.Видимость       = ЕстьСообщениеОСтраховомСлучае;
	Элементы.ДанныеСФРДляРасчетаПособия.Видимость      = ЕстьДанныеСФРДляРасчетаПособия;
	Элементы.ВходящийЗапрос.Видимость                  = ЕстьВходящийЗапрос;
	Элементы.ОтветНаЗапросОтсутствуетГруппа.Видимость  = Не ЕстьОтветНаЗапрос И ЕстьВходящийЗапрос;
	Элементы.ОтветНаЗапросГруппа.Видимость             = ЕстьОтветНаЗапрос;
	
	Если СведенияОбЭЛН.Аннулирован Тогда
		Элементы.АннулированГруппа.Видимость = Истина;
		Если ЗначениеЗаполнено(СведенияОбЭЛН.ДубликатБольничный) Тогда
			Элементы.ДубликатНадпись.Видимость = Истина;
			Элементы.ДубликатНадпись.Заголовок = СтрШаблон(
				НСтр("ru = 'Взамен выдан <a href=""%1"">%2</a>'"),
				ПолучитьНавигационнуюСсылку(СведенияОбЭЛН.ДубликатБольничный),
				СведенияОбЭЛН.ДубликатБольничный);
		ИначеЕсли ЗначениеЗаполнено(СведенияОбЭЛН.ДубликатНомерЛН) Тогда
			Элементы.ДубликатНадпись.Видимость = Истина;
			Элементы.ДубликатНадпись.Заголовок = СтрШаблон(НСтр("ru = 'Взамен выдан ЭЛН № %1'"), СведенияОбЭЛН.ДубликатНомерЛН);
		Иначе
			Элементы.ДубликатНадпись.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Элементы.АннулированГруппа.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрямыеВыплатыФСС

&НаСервере
Процедура ПрочитатьФлажокИспользоватьПрямыеВыплатыФСС()
	
	ТребуетсяПрочитатьФлажокИспользоватьПрямыеВыплатыФСС = Ложь;
	
	ИспользоватьПрямыеВыплатыФСС = ПрямыеВыплатыПособийСоциальногоСтрахования.ПособиеПлатитУчастникПилотногоПроекта(
		Объект.Организация,
		Объект.ПериодРегистрации,
		Объект.ЭЛНКарантинПоКоронавирусу);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыПрямыхВыплатФСС()
	
	ТребуетсяЗаполнитьПараметрыПрямыхВыплатФСС = Ложь;
	
	Объект.ПособиеВыплачиваетсяФСС = ИспользоватьПрямыеВыплатыФСС
		И ЗначениеЗаполнено(Объект.НомерЛисткаНетрудоспособности)
		И ЕстьПособияЗаСчетФСС();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыПрямыхВыплатФСС()
	
	Элементы.ГруппаПрямыеВыплатыФСС.Видимость = ИспользоватьПрямыеВыплатыФСС;
	
	Если ИспользоватьПрямыеВыплатыФСС Тогда
		
		РазрешенВводНовыхСведений = Объект.ПособиеВыплачиваетсяФСС И Не СведенияОбЭЛН.Аннулирован;
		
		УстановитьПривилегированныйРежим(Истина);
		Заявление = Документы.ЗаявлениеСотрудникаНаВыплатуПособия.НайтиЗаявлениеПоОснованию(
			Объект.Ссылка,
			,
			"Ссылка, Представление, Дата, Номер");
		Если Заявление = Неопределено Тогда
			Если ТекущаяДатаСеанса() >= ПрямыеВыплатыПособийСоциальногоСтрахования.ДатаВступленияВСилуФорм2021Года() Тогда
				ЗаголовокНадписи = НСтр("ru = '<a href=""СоздатьСведения"">Ввести сведения для реестра прямых выплат ФСС</a>'");
			Иначе
				ЗаголовокНадписи = НСтр("ru = '<a href=""СоздатьСведения"">Ввести заявление сотрудника на выплату пособия</a>'");
			КонецЕсли;
		Иначе
			Если Заявление.Дата < ПрямыеВыплатыПособийСоциальногоСтрахования.ДатаВступленияВСилуФорм2021Года() Тогда
				ПредставлениеДокумента = СтрШаблон(
					НСтр("ru = 'Заявление сотрудника на выплату пособия %1 от %2'"),
					Заявление.Номер,
					Формат(Заявление.Дата, "ДЛФ=D"));
			Иначе
				ПредставлениеДокумента = Заявление.Представление;
			КонецЕсли;
			ЗаголовокНадписи = СтрШаблон(
				НСтр("ru = '<a href=""%1"">%2</a>'"),
				ПолучитьНавигационнуюСсылку(Заявление.Ссылка),
				ПредставлениеДокумента);
		КонецЕсли;
		ЗаголовокНадписи = ЗаголовокНадписи + "   " + НСтр("ru = '<a href=""ПоказатьВсе"">Показать все</a>'");
		Элементы.СсылкиПрямыхВыплатФСС.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокНадписи);
		
		Если Не РазрешенВводНовыхСведений
			Или Не ПравоДоступа("Просмотр", Метаданные.Документы.ЗаявлениеСотрудникаНаВыплатуПособия) Тогда
			Элементы.СсылкиПрямыхВыплатФСС.Доступность = Ложь;
		Иначе
			Элементы.СсылкиПрямыхВыплатФСС.Доступность = Объект.ПособиеВыплачиваетсяФСС;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСведенияДляПрямыхВыплатФСС()
	Если Модифицированность Или Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПараметрыЗаписи = Новый Структура("РежимЗаписи, ИмяПроцедурыПослеЗаписи");
		ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Запись;
		ПараметрыЗаписи.ИмяПроцедурыПослеЗаписи = "ОткрытьЗаявлениеСотрудникаНаВыплатуПособия";
		Записать(ПараметрыЗаписи);
	Иначе
		ОткрытьЗаявлениеСотрудникаНаВыплатуПособия(Неопределено, Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗаявлениеСотрудникаНаВыплатуПособия(ОбъектЗаписан, ПараметрыЗаписи) Экспорт
	ПрямыеВыплатыПособийСоциальногоСтрахованияКлиент.ОткрытьЗаявлениеСотрудникаПоОснованию(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеСведенияДляПрямыхВыплатФСС()
	ОтборыСписка = Новый Структура;
	ОтборыСписка.Вставить("ГоловнаяОрганизация",           Объект.ГоловнаяОрганизация);
	ОтборыСписка.Вставить("Организация",                   Объект.Организация);
	ОтборыСписка.Вставить("Сотрудник",                     Объект.Сотрудник);
	ОтборыСписка.Вставить("НомерЛисткаНетрудоспособности", Объект.НомерЛисткаНетрудоспособности);
	
	ПараметрыСписка = Новый Структура;
	ПараметрыСписка.Вставить("Отбор", ОтборыСписка);
	
	УникальностьСписка = Объект.НомерЛисткаНетрудоспособности;
	
	ОткрытьФорму("Документ.ЗаявлениеСотрудникаНаВыплатуПособия.ФормаСписка", ПараметрыСписка, , УникальностьСписка);
КонецПроцедуры

#КонецОбласти

#Область ЭЛН

&НаКлиенте
Процедура ПолучитьЭЛНИзФССПослеОткрытияФормы()
	Если Не РазрешеноПолучениеИзФСС() Тогда
		Возврат;
	КонецЕсли;
	Результат = ПолучитьИзФССНаСервере();
	Если Результат.БольничныйЗаполнен Тогда
		ПослеУспешнойЗагрузкиЭЛН();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеДанныхИзФСС(ЗапросДляПолученияЭЛН)
	Если ЗапросДляПолученияЭЛН = Неопределено Тогда
		Возврат;
	КонецЕсли;
	// Подписание и отправка запроса, указание на обработчик расшифровки ответа.
	Оповещение = Новый ОписаниеОповещения("ПолучитьДанныеИзФССЗавершение", ЭтотОбъект);
	ЭлектронныйДокументооборотСФССКлиент.ПолучитьДанныеЭЛНИзФСС(Оповещение, ЗапросДляПолученияЭЛН);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанныеИзФССЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбменВыполненУспешно = ЗагрузитьОтветСервисаФСС(Результат.АдресРасшифрованногоОтветаSOAP, Ложь);
	Если ОбменВыполненУспешно Тогда
		ПодписьПрошлаПроверку = (Результат.ПодписьВалидна <> Ложь);
		Если ПодписьПрошлаПроверку Тогда
			ПараметрыЗаписи = Новый Структура;
			ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
			ПараметрыЗаписи.Вставить("ОчиститьСообщения", Ложь);
			Записать(ПараметрыЗаписи);
		КонецЕсли;
		ПослеУспешнойЗагрузкиЭЛН();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеУспешнойЗагрузкиЭЛН()
	ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Загружены данные ЛН №%1.'"),
		Объект.НомерЛисткаНетрудоспособности);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Данные ЭЛН загружены из ФСС'"), , ТекстОповещения, БиблиотекаКартинок.Успешно32);
КонецПроцедуры

&НаКлиенте
Функция РазрешеноПолучениеИзФСС()
	Если Не ЗначениеЗаполнено(Объект.ГоловнаяОрганизация) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не выбрана организация.'"), "Объект.Организация");
		Возврат Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.НомерЛисткаНетрудоспособности) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не заполнен номер листка нетрудоспособности.'"), "Объект.НомерЛисткаНетрудоспособности");
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

&НаСервере
Функция ПолучитьИзФССНаСервере()
	Результат = Новый Структура("БольничныйЗаполнен, ЗапросДляПолученияЭЛН");
	Результат.БольничныйЗаполнен = Ложь;
	
	ПроверятьВозможностьЗагрузки = Истина;
	ТекстXML = РегистрыСведений.СведенияОбЭЛН.ИсходныйXML(
		Объект.НомерЛисткаНетрудоспособности,
		Объект.ГоловнаяОрганизация,
		ПроверятьВозможностьЗагрузки);
	Если ЗначениеЗаполнено(ТекстXML) Тогда
		Адрес = ПоместитьВоВременноеХранилище(ТекстXML);
		Результат.БольничныйЗаполнен = ЗагрузитьОтветСервисаФСС(Адрес, Ложь);
	КонецЕсли;
	
	Если Не Результат.БольничныйЗаполнен Тогда
		// Формирование параметров запроса к сервису получения ЭЛН.
		Результат.ЗапросДляПолученияЭЛН = ВыгрузитьЗапросДляПолученияЭЛН();
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция ВыгрузитьЗапросДляПолученияЭЛН()
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не выбрана организация.'"), "Организация", "Объект");
		Возврат Неопределено;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		СообщенияБЗККлиентСервер.СообщитьВФорме(НСтр("ru = 'Не выбран сотрудник.'"), "Сотрудник", "Объект");
		Возврат Неопределено;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.НомерЛисткаНетрудоспособности) Тогда
		Текст = НСтр("ru = 'Не заполнен номер листка нетрудоспособности.'");
		СообщенияБЗККлиентСервер.СообщитьВФорме(Текст, "НомерЛисткаНетрудоспособности", "Объект");
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбменЛисткамиНетрудоспособностиФСС.ВыгрузитьЗапросДляПолученияЭЛН(Объект);
КонецФункции

&НаСервере
Функция ЗагрузитьОтветСервисаФСС(Знач АдресРасшифрованногоОтветаSOAP, Знач ЗагрузкаИзФайла)
	Документ = РеквизитФормыВЗначение("Объект");
	
	РезультатОбмена = ОбменЛисткамиНетрудоспособностиФСС.ЗагрузитьОтветСервисаФСС(
		Документ,
		"getPrivateLNData",
		АдресРасшифрованногоОтветаSOAP,
		ЗагрузкаИзФайла);
	ОбменВыполненУспешно = Не РезультатОбмена.Отказ;
	
	Если ОбменВыполненУспешно Тогда
		ЗначениеВРеквизитФормы(Документ, "Объект");
		ОбновитьФормуПослеЗагрузкиЭЛН();
		Модифицированность = Истина;
	КонецЕсли;
	
	Возврат ОбменВыполненУспешно;
КонецФункции

&НаСервере
Процедура ОбновитьФормуПослеЗагрузкиЭЛН()
	Документы.БольничныйЛист.ОчиститьСлучайУходаПриНеобходимости(Объект);
	ЗаполнитьПериодРасчетаСреднегоЗаработка();
	ЗапланироватьПолноеОбновлениеСведенийДляФСС();
	ЗаполнитьПоПервичномуБольничномуЛисту();
	ПричинаНетрудоспособностиПриИзмененииНаСервере();
	ЯвляетсяПродолжениемБолезниПриИзмененииНаСервере();
	ДатаНачалаПриИзмененииНаСервере();
	ДатаОкончанияПриИзмененииНаСервере();
	ОбновитьГиперссылкуВводаДанныхЛисткаНетрудоспособности();
	ОбновитьСведенияДляФСС();
	ПроверитьПриостановленияТрудовыхДоговоров(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаЭЛН(ОписаниеФайла, ПустойПараметр) Экспорт
	Если ОписаниеФайла = Неопределено Или Не ЗначениеЗаполнено(ОписаниеФайла.Хранение) Тогда
		Возврат;
	КонецЕсли;
	ОчиститьСообщения();
	
	Попытка
		ФайлЗагружен = ЗагрузитьОтветСервисаФСС(ОписаниеФайла.Хранение, Истина);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Кратко = СтрШаблон(
			НСтр("ru = 'При загрузке ЭЛН из файла возникла ошибка: ""%1"".'"),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		Подробно = СтрШаблон(
			НСтр("ru = 'Ошибка загрузки файла ""%1"":
				|%2
				|
				|Вероятно, файл не соответствует спецификации ответа веб-сервиса ФСС ЭЛН.'"), 
			ОписаниеФайла.Имя,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		ИнформированиеПользователяКлиент.Предупредить(Кратко, Подробно, НСтр("ru = 'Ошибка загрузки ЭЛН из файла'"));
		ФайлЗагружен = Ложь;
	КонецПопытки;
	Если ФайлЗагружен Тогда
		Записать(Новый Структура("ОчиститьСообщения", Ложь));
		ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Загружены данные ЛН №%1.'"),
			Объект.НомерЛисткаНетрудоспособности);
		ПоказатьОповещениеПользователя(НСтр("ru = 'Данные ЭЛН загружены из ФСС'"), , ТекстОповещения, БиблиотекаКартинок.Успешно32);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеФайлаЭЛН(Знач НомерЛН, Знач ГоловнаяОрганизация, Знач ИдентификаторФормы)
	Возврат ОбменЛисткамиНетрудоспособностиФСС.ОписаниеФайлаЭЛН(НомерЛН, ГоловнаяОрганизация, ИдентификаторФормы);
КонецФункции

&НаСервере
Процедура ПередЗаписьюНаСервереЭЛН(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПрочитатьСведенияОбЭЛНЕслиТребуется();
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЭтоПоследнийБольничныйЭЛН", СведенияОбЭЛН.ЭтоПоследнийБольничныйЭЛН);
	
	Если СведенияОбЭЛН.ЭтоПоследнийБольничныйЭЛН Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ОтправитьЭЛН", ОтправитьЭЛН);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ИсправлениеЭЛН", ИсправлениеЭЛН);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("КодПричиныИсправленияЭЛН", КодПричиныИсправленияЭЛН);
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ОписаниеПричиныИсправленияЭЛН", ОписаниеПричиныИсправленияЭЛН);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервереЭЛН(Отказ, ПроверяемыеРеквизиты)
	
	Если ОтправитьЭЛН И ИсправлениеЭЛН И СведенияОбЭЛН.ЭтоПоследнийБольничныйЭЛН Тогда
		Если Не ЗначениеЗаполнено(КодПричиныИсправленияЭЛН) Тогда
			Текст = НСтр("ru = 'Не заполнен код причины исправления ЭЛН'");
			СообщенияБЗККлиентСервер.СообщитьОбОшибкеВФорме(Отказ, Текст, "КодПричиныИсправленияЭЛН");
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОписаниеПричиныИсправленияЭЛН) Тогда
			Текст = НСтр("ru = 'Не заполнено описание причины исправления ЭЛН'");
			СообщенияБЗККлиентСервер.СообщитьОбОшибкеВФорме(Отказ, Текст, "ОписаниеПричиныИсправленияЭЛН");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСведенияОбЭЛНЕслиТребуется()
	Если Не ТребуетсяПрочитатьСведенияОбЭЛН Тогда
		Возврат;
	КонецЕсли;
	
	ТребуетсяПрочитатьСведенияОбЭЛН = Ложь;
	СведенияОбЭЛН = СведенияОбЭЛН(Объект.НомерЛисткаНетрудоспособности, Объект.ГоловнаяОрганизация);
	
	Элементы.КодПричиныИсправленияЭЛН.Подсказка = НаименованиеПричиныИсправленияЭЛН(ЭтотОбъект);
	
	ЭтоПоследний = Не ЗначениеЗаполнено(СведенияОбЭЛН.Больничный) Или СведенияОбЭЛН.Больничный = Объект.Ссылка;
	Закрыт = СтрНачинаетсяС(Объект.НомерЛисткаНетрудоспособности, "9990")
		Или (СведенияОбЭЛН.СостояниеФСС <> Перечисления.СостоянияЭЛНВФСС.Открыт
			И СведенияОбЭЛН.СостояниеФСС <> Перечисления.СостоянияЭЛНВФСС.Продлен
			И СведенияОбЭЛН.СостояниеФСС <> Перечисления.СостоянияЭЛНВФСС.НаправленНаМСЭ
			И СведенияОбЭЛН.СостояниеФСС <> Перечисления.СостоянияЭЛНВФСС.ДополненДаннымиМСЭ);
	
	СведенияОбЭЛН.Вставить("ЭтоПоследнийБольничный", ЭтоПоследний);
	СведенияОбЭЛН.Вставить("ЭтоПоследнийБольничныйЭЛН", СведенияОбЭЛН.ЭтоЭЛН И ЭтоПоследний);
	СведенияОбЭЛН.Вставить("Закрыт", Закрыт);
	СведенияОбЭЛН.Вставить("ЭтоЗакрытыйЭЛН", СведенияОбЭЛН.ЭтоЭЛН И Закрыт);
	
	ВходящийЗапрос            = СведенияОбЭЛН.ВходящийЗапрос;
	ОтветНаЗапрос             = СведенияОбЭЛН.ОтветНаЗапрос;
	СообщениеОСтраховомСлучае = СведенияОбЭЛН.СообщениеОСтраховомСлучае;
	ДанныеСФРДляРасчетаПособия = СведенияОбЭЛН.ДанныеСФРДляРасчетаПособия;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СведенияОбЭЛН(НомерЛН, ГоловнаяОрганизация) Экспорт
	Результат = Новый Структура(
	"ДоступенИсходныйXML,
	|СостояниеФСС,
	|Аннулирован,
	|ДубликатНомерЛН,
	|ДубликатБольничный,
	|ДатаНачала,
	|ДатаОкончания,
	|ЭтоЭЛН,
	|ИзмененияПринятыФСС,
	|ТребуетсяРеестрЭЛН,
	|Исправление,
	|КодПричиныИсправления,
	|ОписаниеПричиныИсправления,
	|Организация,
	|Больничный,
	|БольничныйПроведен,
	|БольничныйПредставление,
	|ВходящийЗапрос,
	|ОтветНаЗапрос,
	|СообщениеОСтраховомСлучае,
	|ДанныеСФРДляРасчетаПособия,
	|ПодготовленныйРеестр,
	|ПодготовленныйРеестрНомер,
	|ПодготовленныйРеестрДата,
	|ПринятыйРеестр,
	|ПринятыйРеестрНомер,
	|ПринятыйРеестрДата");
	
	УстановитьПривилегированныйРежим(Истина);
	Если ЗначениеЗаполнено(НомерЛН)
		И ЗначениеЗаполнено(ГоловнаяОрганизация)
		И ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОбЭЛН.ДоступенИсходныйXML КАК ДоступенИсходныйXML,
		|	СведенияОбЭЛН.ДоступенИсходныйXML
		|		ИЛИ СведенияОбЭЛН.Хеш <> """" КАК ЭтоЭЛН,
		|	СведенияОбЭЛН.СостояниеФСС КАК СостояниеФСС,
		|	СведенияОбЭЛН.ПериодНетрудоспособностиНачало КАК ДатаНачала,
		|	СведенияОбЭЛН.ПериодНетрудоспособностиОкончание КАК ДатаОкончания,
		|	СведенияОбЭЛН.ИзмененияПринятыФСС КАК ИзмененияПринятыФСС,
		|	СведенияОбЭЛН.ТребуетсяРеестрЭЛН КАК ТребуетсяРеестрЭЛН,
		|	СведенияОбЭЛН.Исправление КАК Исправление,
		|	СведенияОбЭЛН.КодПричиныИсправления КАК КодПричиныИсправления,
		|	СведенияОбЭЛН.ОписаниеПричиныИсправления КАК ОписаниеПричиныИсправления,
		|	СведенияОбЭЛН.Организация КАК Организация,
		|	СведенияОбЭЛН.Больничный КАК Больничный,
		|	БольничныйЛист.Проведен КАК БольничныйПроведен,
		|	БольничныйЛист.Представление КАК БольничныйПредставление,
		|	СведенияОбЭЛН.ВходящийЗапрос КАК ВходящийЗапрос,
		|	СведенияОбЭЛН.ОтветНаЗапрос КАК ОтветНаЗапрос,
		|	СведенияОбЭЛН.СообщениеОСтраховомСлучае КАК СообщениеОСтраховомСлучае,
		|	СведенияОбЭЛН.ДанныеСФРДляРасчетаПособия КАК ДанныеСФРДляРасчетаПособия,
		|	СведенияОбЭЛН.ПодготовленныйКОтправкеРеестр КАК ПодготовленныйРеестр,
		|	Подготовленный.Номер КАК ПодготовленныйРеестрНомер,
		|	Подготовленный.Дата КАК ПодготовленныйРеестрДата,
		|	СведенияОбЭЛН.ПринятыйРеестр КАК ПринятыйРеестр,
		|	Принятый.Номер КАК ПринятыйРеестрНомер,
		|	Принятый.Дата КАК ПринятыйРеестрДата,
		|	ДубликатЭЛН.НомерЛисткаНетрудоспособности КАК ДубликатНомерЛН,
		|	ДубликатЭЛН.Больничный КАК ДубликатБольничный
		|ИЗ
		|	РегистрСведений.СведенияОбЭЛН КАК СведенияОбЭЛН
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.БольничныйЛист КАК БольничныйЛист
		|		ПО СведенияОбЭЛН.Больничный = БольничныйЛист.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеестрДанныхЭЛНЗаполняемыхРаботодателем КАК Подготовленный
		|		ПО СведенияОбЭЛН.ПодготовленныйКОтправкеРеестр = Подготовленный.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеестрДанныхЭЛНЗаполняемыхРаботодателем КАК Принятый
		|		ПО СведенияОбЭЛН.ПринятыйРеестр = Принятый.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбЭЛН КАК ДубликатЭЛН
		|		ПО СведенияОбЭЛН.НомерЛисткаНетрудоспособности = ДубликатЭЛН.НомерЗаменяемогоЛН
		|ГДЕ
		|	СведенияОбЭЛН.НомерЛисткаНетрудоспособности = &НомерЛН
		|	И СведенияОбЭЛН.ГоловнаяОрганизация = &ГоловнаяОрганизация";
		Запрос.УстановитьПараметр("НомерЛН", НомерЛН);
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
		Таблица = Запрос.Выполнить().Выгрузить();
		Если Таблица.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(Результат, Таблица[0]);
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.ЭтоЭЛН = Неопределено Тогда
		Результат.ЭтоЭЛН              = Ложь;
		Результат.ДоступенИсходныйXML = Ложь;
		Результат.ИзмененияПринятыФСС = Ложь;
		Результат.ТребуетсяРеестрЭЛН  = Ложь;
		Результат.Исправление         = Ложь;
		Результат.КодПричиныИсправления      = "";
		Результат.ОписаниеПричиныИсправления = "";
	КонецЕсли;
	
	Если Результат.Больничный = Неопределено Тогда
		Поля = "Ссылка, Проведен, Представление";
		Последний = Документы.БольничныйЛист.ПоследнийДокументВЦепочкеИсправлений(НомерЛН, ГоловнаяОрганизация, , Поля);
		Если Последний <> Неопределено Тогда
			Результат.Больничный              = Последний.Ссылка;
			Результат.БольничныйПроведен      = Последний.Проведен;
			Результат.БольничныйПредставление = Последний.Представление;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат.Больничный) Тогда
		Результат.Больничный              = Документы.БольничныйЛист.ПустаяСсылка();
		Результат.БольничныйПроведен      = Ложь;
		Результат.БольничныйПредставление = "";
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Результат.ПодготовленныйРеестр) Тогда
		Результат.ПодготовленныйРеестр      = Документы.РеестрДанныхЭЛНЗаполняемыхРаботодателем.ПустаяСсылка();
		Результат.ПодготовленныйРеестрНомер = "";
		Результат.ПодготовленныйРеестрДата  = '00010101';
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Результат.ПринятыйРеестр) Тогда
		Результат.ПринятыйРеестр      = Документы.РеестрДанныхЭЛНЗаполняемыхРаботодателем.ПустаяСсылка();
		Результат.ПринятыйРеестрНомер = "";
		Результат.ПринятыйРеестрДата  = '00010101';
	КонецЕсли;
	
	Результат.Аннулирован = Результат.ЭтоЭЛН И (
		Результат.СостояниеФСС = Перечисления.СостоянияЭЛНВФСС.Аннулирован
		Или Результат.СостояниеФСС = Перечисления.СостоянияЭЛНВФСС.ВозвращенМО);
	
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ПрочитатьРеквизитыЭЛН()
	ТребуетсяПрочитатьРеквизитыЭЛН = Ложь;
	ОтправитьЭЛН                   = СведенияОбЭЛН.ТребуетсяРеестрЭЛН И Не ЗначениеЗаполнено(ВходящийЗапрос);
	ИсправлениеЭЛН                 = СведенияОбЭЛН.Исправление;
	КодПричиныИсправленияЭЛН       = СведенияОбЭЛН.КодПричиныИсправления;
	ОписаниеПричиныИсправленияЭЛН  = СведенияОбЭЛН.ОписаниеПричиныИсправления;
	ИсправлениеЭЛНЧислом           = Число(ИсправлениеЭЛН);
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеПринятогоРеестраЭЛН()
	Если СведенияОбЭЛН.Свойство("ДанныеПринятогоРеестра") Тогда
		Возврат; // Данные уже подготовлены и закэшированы.
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СведенияОбЭЛН.ПринятыйРеестр) Тогда
		ДанныеПринятогоРеестра = ОбменЛисткамиНетрудоспособностиФСС.ДанныеРеестраВЧастиБольничногоОтправляемыеВФСС(
			СведенияОбЭЛН.ПринятыйРеестр,
			Объект.НомерЛисткаНетрудоспособности);
	Иначе
		ДанныеПринятогоРеестра = ОбменЛисткамиНетрудоспособностиФСС.СтруктураДанныхРеестровВЧастиБольничного();
	КонецЕсли;
	
	СведенияОбЭЛН.Вставить("ДанныеПринятогоРеестра", ДанныеПринятогоРеестра);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыОтправкиЭЛН()
	ТребуетсяЗаполнитьПараметрыОтправкиЭЛН = Ложь;
	
	Если Не СведенияОбЭЛН.ЭтоЭЛН Тогда
		// По бумажному ЛН часть работодателя заполняется на бумаге.
		ОтправитьЭЛН   = Ложь;
		ИсправлениеЭЛН = Ложь;
		
	ИначеЕсли СтрНачинаетсяС(Объект.НомерЛисткаНетрудоспособности, "9990") Тогда
		// Для 65+ не требуется отправка реестров ЭЛН.
		ОтправитьЭЛН   = Ложь;
		ИсправлениеЭЛН = Ложь;
		
	ИначеЕсли Не ЕстьПособияЗаСчетФСС() Тогда
		// Если пособие не назначается то сведения для ЭЛН направлять не нужно.
		// Фонд не требует представления документов, если не было исчислено пособие.
		ОтправитьЭЛН   = Ложь;
		ИсправлениеЭЛН = Ложь;
		
	ИначеЕсли Не ЭтоНСПЗ() Тогда
		// Для обычных пособий сдаваемых в электронной форме не требуется отправлять сведения об ЭЛН,
		// т.к. данные для ЭЛН заполняются из других источников.
		ОтправитьЭЛН   = Ложь;
		ИсправлениеЭЛН = Ложь;
		
	ИначеЕсли ЗначениеЗаполнено(ВходящийЗапрос) Тогда
		// Для пособий отправляемых через СЭДО не требуется отправлять сведения об ЭЛН,
		// т.к. данные для ЭЛН заполняются из данных ответа на запрос СЭДО.
		ОтправитьЭЛН   = Ложь;
		ИсправлениеЭЛН = Ложь;
		
	ИначеЕсли Не ЗначениеЗаполнено(СведенияОбЭЛН.ПринятыйРеестр) Тогда
		// Требуется первичная отправка ЭЛН.
		ОтправитьЭЛН   = Истина;
		ИсправлениеЭЛН = Ложь;
		
	Иначе
		
		ПодготовитьДанныеПринятогоРеестраЭЛН();
		Если СведенияОбЭЛН.ДанныеПринятогоРеестра.ОсновноеМестоРаботы = Неопределено Тогда
			ИзменилисьДанные = Истина; // Нештатная ситуация - строка ЭЛН исчезла из принятого реестра.
		Иначе
			ДанныеБольничного = ОбменЛисткамиНетрудоспособностиФСС.ДанныеБольничногоОтправляемыеВФСС(Объект);
			ИзменилисьДанные = Не ОбщегоНазначенияБЗК.ЗначенияСовпадают(СведенияОбЭЛН.ДанныеПринятогоРеестра, ДанныеБольничного);
		КонецЕсли;
		
		ОтправитьЭЛН   = ИзменилисьДанные;
		ИсправлениеЭЛН = ИзменилисьДанные;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыЭЛН()
	
	Элементы.ПодменюЭЛНВыгрузитьФайл.Видимость           = СведенияОбЭЛН.ДоступенИсходныйXML;
	Элементы.ПодменюЭЛНПерезаполнитьБольничный.Видимость = СведенияОбЭЛН.ДоступенИсходныйXML;
	
	ЕстьПраво    = ПравоДоступа("Просмотр", Метаданные.Документы.РеестрДанныхЭЛНЗаполняемыхРаботодателем);
	ЭтоПоследний = СведенияОбЭЛН.ЭтоПоследнийБольничныйЭЛН И Не СведенияОбЭЛН.Аннулирован;
	
	Элементы.ГруппаЭЛН.Видимость = ЭтоПоследний Или (ЕстьПраво И СведенияОбЭЛН.ЭтоЭЛН);
	Если Не Элементы.ГруппаЭЛН.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.КодПричиныИсправленияЭЛН.Подсказка = НаименованиеПричиныИсправленияЭЛН(ЭтотОбъект);
	
	ЕстьПодготовленный = ЗначениеЗаполнено(СведенияОбЭЛН.ПодготовленныйРеестр) И СведенияОбЭЛН.ПринятыйРеестр <> СведенияОбЭЛН.ПодготовленныйРеестр;
	ЕстьПринятый       = ЗначениеЗаполнено(СведенияОбЭЛН.ПринятыйРеестр);
	ЕстьРеестр         = (ЕстьПодготовленный Или ЕстьПринятый);
	
	Если ЕстьРеестр Или (ЕстьПраво И Не СведенияОбЭЛН.Аннулирован) Тогда
		Если ЕстьПодготовленный Или ЕстьПринятый Тогда
			Если ЕстьПринятый Тогда
				ЗаголовокНадписи = НСтр("ru = 'Последний принятый реестр ЭЛН: <a href=""ПринятыйРеестрЭЛН"">№ %1 от %2</a>'");
				Если ЕстьПодготовленный Тогда
					ЗаголовокНадписи = ЗаголовокНадписи + "   " + НСтр("ru = 'Подготовленный: <a href=""ПодготовленныйРеестрЭЛН"">№ %3 от %4</a>'");
				КонецЕсли;
			Иначе
				ЗаголовокНадписи = НСтр("ru = 'Последний подготовленный реестр ЭЛН: <a href=""ПодготовленныйРеестрЭЛН"">№ %3 от %4</a>'");
			КонецЕсли;
			ЗаголовокНадписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокНадписи,
				СведенияОбЭЛН.ПринятыйРеестрНомер,
				Формат(СведенияОбЭЛН.ПринятыйРеестрДата, "ДЛФ=D"),
				СведенияОбЭЛН.ПодготовленныйРеестрНомер,
				Формат(СведенияОбЭЛН.ПодготовленныйРеестрДата, "ДЛФ=D"));
		Иначе
			ЗаголовокНадписи = НСтр("ru = 'Для этого ЭЛН еще не создан реестр ЭЛН'");
		КонецЕсли;
		Элементы.СсылкиРеестровЭЛН.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокНадписи);
		Элементы.ГруппаСсылкиРеестровЭЛН.Видимость = Истина;
	Иначе
		Элементы.ГруппаСсылкиРеестровЭЛН.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаОтправитьПервичныйЭЛН.Видимость   = ЭтоПоследний И Не ЕстьПринятый;
	Элементы.ГруппаОтправитьИсправлениеЭЛН.Видимость = ЭтоПоследний И ЕстьПринятый;
	Элементы.ГруппаПричинаИсправленияЭЛН.Видимость   = ЭтоПоследний И ОтправитьЭЛН И ИсправлениеЭЛН;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НаименованиеПричиныИсправленияЭЛН(Форма)
	ЭлементСписка = Форма.Элементы.КодПричиныИсправленияЭЛН.СписокВыбора.НайтиПоЗначению(Форма.КодПричиныИсправленияЭЛН);
	Если ЭлементСписка <> Неопределено Тогда
		Если КодСимвола(ЭлементСписка.Представление, 3) = КодСимвола(".") Тогда
			Возврат СокрЛП(Сред(ЭлементСписка.Представление, 4));
		Иначе
			Возврат СокрЛП(ЭлементСписка.Представление)
		КонецЕсли;
	КонецЕсли;
	Возврат "";
КонецФункции

&НаСервере
Процедура ОтправитьЭЛНПриИзмененииНаСервере()
	ОбновитьЭлементыЭЛН();
КонецПроцедуры

&НаСервере
Процедура ИсправлениеЭЛНПриИзмененииНаСервере()
	ОтправитьЭЛН = ИсправлениеЭЛН;
	ИсправлениеЭЛНЧислом = Число(ИсправлениеЭЛН);
	ОбновитьЭлементыЭЛН();
КонецПроцедуры

&НаСервере
Процедура ИсправлениеЭЛНЧисломПриИзмененииНаСервере()
	ОтправитьЭЛН = Истина;
	ИсправлениеЭЛН = Булево(ИсправлениеЭЛНЧислом);
	ОбновитьЭлементыЭЛН();
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЯвляетсяПродолжениемБолезниПриИзмененииНаСервере()
	
	Если НЕ Объект.ЯвляетсяПродолжениемБолезни Тогда
		
		Объект.ПервичныйБольничныйЛист = Документы.БольничныйЛист.ПустаяСсылка();
		Объект.НомерПервичногоЛисткаНетрудоспособности = "";
		УстановитьДоступностьЭлементовПоПервичномуБольничномуЛисту(ЭтотОбъект);
		
	КонецЕсли;
	УстановитьСвойстваПоляПервичногоБольничногоЛиста(ЭтотОбъект);
	ОбновитьЭлементыСпособРасчетовЗаСчетРаботодателя();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементовПоПервичномуБольничномуЛисту(Форма)
	
	ИменаЭлементов = Новый Массив;
	ИменаЭлементов.Добавить("ПроцентОплатыБезЛьгот");
	ИменаЭлементов.Добавить("ОграничениеПособияБезЛьгот");
	ИменаЭлементов.Добавить("ПрименятьЛьготыПриНачисленииПособия");
	ИменаЭлементов.Добавить("ФинансированиеФедеральнымБюджетом");
	ИменаЭлементов.Добавить("ДатаНачала");
	ИменаЭлементов.Добавить("РайонныйКоэффициентРФНаНачалоСобытия");
	ИменаЭлементов.Добавить("ОграничениеПособия");
	ИменаЭлементов.Добавить("ОграничениеПособияБезЛьгот");
	ИменаЭлементов.Добавить("ОграничениеПособияЗаСчетФБ");
	ИменаЭлементов.Добавить("СтажЛет");
	ИменаЭлементов.Добавить("ПроцентОплаты");
	ИменаЭлементов.Добавить("ДекорацияПроцент");
	ИменаЭлементов.Добавить("СтажЛетЛьготыРадиация");
	ИменаЭлементов.Добавить("ПроцентОплатыБезЛьготРадиация");
	ИменаЭлементов.Добавить("ДекорацияПроцентЛьготыРадиация1");
	ИменаЭлементов.Добавить("СтажЛетЛьготыСлужба");
	ИменаЭлементов.Добавить("ПроцентОплатыБезЛьготСлужба");
	ИменаЭлементов.Добавить("ДекорацияПроцентЛьготыСлужба1");
	ИменаЭлементов.Добавить("ПроцентОплатыЛьготыСлужба");
	ИменаЭлементов.Добавить("ДекорацияПроцентЛьготыСлужба2");
	ИменаЭлементов.Добавить("ПроцентОплатыТравма");
	ИменаЭлементов.Добавить("ДекорацияПроцентТравма");
	
	ДоступностьЭлементов = Не ЗначениеЗаполнено(Форма.Объект.ПервичныйБольничныйЛист);
	
	Для Каждого ИмяЭлемента Из ИменаЭлементов Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяЭлемента, "Доступность", ДоступностьЭлементов);
	КонецЦикла;
	
	Если ДоступностьЭлементов Тогда
		ЗаголовокКомандыОткрытьСреднийЗаработок = НСтр("ru='Изменить'");
	Иначе
		ЗаголовокКомандыОткрытьСреднийЗаработок = НСтр("ru='Подробнее'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ОткрытьСреднийЗаработок",
		"Заголовок",
		ЗаголовокКомандыОткрытьСреднийЗаработок);
	
	МассивПараметровВыбора = Новый Массив;
	
	Если Не ДоступностьЭлементов И ЗначениеЗаполнено(Форма.Объект.ПричинаНетрудоспособности) Тогда
		
		ПричиныНетрудоспособности = Новый Массив;
		ПричиныНетрудоспособности.Добавить(Форма.Объект.ПричинаНетрудоспособности);
		ПособиеПриДолечивании = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ПособиеПриДолечивании");
		
		Если Форма.Объект.ПричинаНетрудоспособности <> ПособиеПриДолечивании Тогда
			ПричиныНетрудоспособности.Добавить(ПособиеПриДолечивании);
		КонецЕсли; 
		
		МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(ПричиныНетрудоспособности)));
		
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ПричинаНетрудоспособности",
		"ПараметрыВыбора",
		Новый ФиксированныйМассив(МассивПараметровВыбора));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваПоляПервичногоБольничногоЛиста(Форма)

	Форма.Элементы.НадписьПервичныйЛистокНетрудоспособности.Доступность = Форма.Объект.ЯвляетсяПродолжениемБолезни;
	Если ЗначениеЗаполнено(Форма.Объект.ПервичныйБольничныйЛист) Тогда
		Форма.НадписьПервичныйЛистокНетрудоспособности = Строка(Форма.Объект.ПервичныйБольничныйЛист);
		Форма.Элементы.НадписьПервичныйЛистокНетрудоспособности.ЦветТекста = Форма.ЦветГиперссылка;
	Иначе
		Форма.НадписьПервичныйЛистокНетрудоспособности = Нстр("ru = 'Больничный лист'");
		Форма.Элементы.НадписьПервичныйЛистокНетрудоспособности.ЦветТекста = Форма.ЦветПустойГиперссылки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНадписьДнейОсвобожденияОтРаботы()
	
	НадписьДнейОсвобожденияОтРаботы = ОбщегоНазначенияБЗК.ПредставлениеКоличестваДней(
		Объект.ДатаНачала,
		Объект.ДатаОкончания);
		
	Если ПоказыватьГруппСпособРасчетовЗаСчетРаботодателя() Тогда
		НадписьДнейОсвобожденияОтРаботы = СтрШаблон(НСтр("ru = '%1 (период действия договора подряда)'"),
			НадписьДнейОсвобожденияОтРаботы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПервичномуБольничномуЛисту()
	
	Если Не ЗначениеЗаполнено(Объект.ПервичныйБольничныйЛист) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьПоПервичномуБольничномуЛисту(Объект.ПервичныйБольничныйЛист);
	ЗначениеВРеквизитФормы(ДокументОбъект,"Объект");
	
	ГодНачалаСобытия = Неопределено;
	СтажЛет = 0;
	УстановитьСвойстваПолейСтажЛьготы();
	ОтобразитьРазмерОплаты();
	
	РассчитатьСреднийЗаработок();
	
	УстановитьДоступностьЭлементовПоПервичномуБольничномуЛисту(ЭтотОбъект);
	УстановитьСвойстваПолейОтметкиОНарушенииРежима(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыОплаты(Знач ФлажокНазначитьПособие = Неопределено, Знач Кэш = Неопределено)
	Если Кэш = Неопределено Тогда
		Кэш = Новый Соответствие;
	КонецЕсли;
	
	Документы.БольничныйЛист.ЗаполнитьПараметрыОплаты(Объект, Кэш);
	
	Если ТипЗнч(ФлажокНазначитьПособие) = Тип("Булево")
		И Объект.НазначитьПособие <> ФлажокНазначитьПособие Тогда
		Объект.НазначитьПособие = ФлажокНазначитьПособие;
		Объект.АвтозаполнениеПериодовОплаты = Ложь;
	КонецЕсли;
	
	ОбновитьЭлементыПараметровОплаты(Кэш);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПроцентОплатыИОграничениеПособия()
	
	ИзменитьСтаж();
	Структура = УчетПособийСоциальногоСтрахования.ПроцентОплатыИОграничениеПособия(Объект);
	ЗаполнитьЗначенияСвойств(Объект, Структура);
	
КонецПроцедуры

&НаСервере
Процедура НомерЛисткаНетрудоспособностиПриИзмененииНаСервере()
	
	ОбновитьГиперссылкуВводаДанныхЛисткаНетрудоспособности();
	
	ЗапланироватьПолноеОбновлениеСведенийДляФСС();
	ОбновитьСведенияДляФСС();
	
КонецПроцедуры

&НаКлиенте
Процедура ПервичныйБольничныйЛистЗавершениеВыбора(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(ВыбранноеЗначение) И ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.БольничныйЛист") Тогда
		Объект.ПервичныйБольничныйЛист = ВыбранноеЗначение;
		УстановитьСвойстваПоляПервичногоБольничногоЛиста(ЭтотОбъект);
		ЗаполнитьПоПервичномуБольничномуЛисту();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНДФЛ()
	
	ФиксРасчет = Объект.НДФЛ.НайтиСтроки(Новый Структура("ФиксРасчет", Истина));
	Если ФиксРасчет.Количество() = 0 Тогда
		ОбъектФормы = РеквизитФормыВЗначение("Объект");
		Документы.БольничныйЛист.ПересчитатьНДФЛ(Объект, ОбъектФормы);
	КонецЕсли;
	УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИтоговыеСуммыНачислений()
	
	ИменаКолонок = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("Налог");
	НДФЛ = 0;
	Для каждого ИмяКолонки Из ИменаКолонок Цикл
		НДФЛ = НДФЛ + Объект.НДФЛ.Итог(ИмяКолонки);
	КонецЦикла;
	КВыплате = Объект.Начисления.Итог("Результат") - НДФЛ;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДатуНачалаПоловиннойОплаты()
	
	Объект.ДатаНачалаПоловиннойОплаты = '00010101';
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачалаСобытия)
		Или Не ЗначениеЗаполнено(Объект.ДатаНачала)
		Или Не ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		Возврат;
	КонецЕсли;
	
	ДнейПолнойОплаты = УчетПособийСоциальногоСтрахования.ДнейПолнойОплатыПередПоловинной(
		Объект.СлучайУходаЗаБольнымРебенком,
		Объект.ДатаНачалаСобытия);
	Если ДнейПолнойОплаты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачалаПоловиннойОплаты = Объект.ДатаНачалаСобытия + ДнейПолнойОплаты * 86400;
	Если Объект.ДатаОкончания < НачалоДня(ДатаНачалаПоловиннойОплаты) - 1 Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДатаНачалаПоловиннойОплаты = ДатаНачалаПоловиннойОплаты;
	
КонецПроцедуры

&НаСервере
Процедура КарантинПоКоронавирусуПриИзмененииНаСервере()
	ЗапланироватьПолноеОбновлениеСведенийДляФСС();
	ПричинаНетрудоспособностиПриИзмененииНаСервере();
	ОбновитьСведенияДляФСС();
КонецПроцедуры

&НаСервере
Процедура ДатаНачалаПриИзмененииНаСервере()
	
	Объект.АвтозаполнениеПериодовОплаты = Истина;
	
	Если ЗначениеЗаполнено(Объект.ДатаОкончания) И Объект.ДатаОкончания < Объект.ДатаНачала Тогда
		Объект.ДатаОкончания = Объект.ДатаНачала;
	КонецЕсли;
	
	Если НЕ Объект.ЯвляетсяПродолжениемБолезни Тогда
		Объект.ДатаНачалаСобытия = Объект.ДатаНачала;
	КонецЕсли;
	
	ПодготовитьТаблицуЗаболеваний(Объект.ДатаНачалаСобытия);
	
	ЗаполнитьПериодРасчетаСреднегоЗаработка();
	
	ДатаНачалаСобытияПриИзмененииНаСервере();
	
	ОбновитьНадписьДнейОсвобожденияОтРаботы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПериодРасчетаСреднегоЗаработка()
	
	ПериодРасчетаСреднего = УчетПособийСоциальногоСтрахованияКлиентСервер.ПериодРасчетаСреднегоЗаработкаФСС(Объект.ДатаНачалаСобытия);
	
	Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод = Год(ПериодРасчетаСреднего.ДатаНачала);
	Объект.ПериодРасчетаСреднегоЗаработкаВторойГод = Год(ПериодРасчетаСреднего.ДатаОкончания);
	
КонецПроцедуры

&НаСервере
Процедура ДатаОкончанияПриИзмененииНаСервере()
	
	ЗаполнитьДатуНачалаПоловиннойОплаты();
	
	Объект.АвтозаполнениеПериодовОплаты = Истина;
	ЗаполнитьПараметрыОплаты(Неопределено);
	
	ОбновитьНадписьДнейОсвобожденияОтРаботы();
	
	ЗаполнитьПроцентОплатыИОграничениеПособия();
	
	РассчитатьСреднийЗаработок();
	
	ОбновитьЭлементыСпособРасчетовЗаСчетРаботодателя();
	
КонецПроцедуры

&НаСервере
Процедура ПериодРегистрацииПриИзмененииНаСервере()
	
	ЗапланироватьПолноеОбновлениеСведенийДляФСС();
	
	ЗаполнитьУсловияИсчисления();
	
	Если ЗначениеЗаполнено(Объект.ПериодРегистрации) Тогда
		Объект.ПланируемаяДатаВыплаты = ПланируемаяДатаВыплаты();
		ПересчитатьНДФЛ();
	КонецЕсли;
	
	ОбновитьСведенияДляФСС();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
	// Вставить содержимое обработчика.
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"РайонныйКоэффициентРФНаНачалоСобытия",
		"Видимость",
		ПолучитьФункциональнуюОпциюФормы("ПрименятьРайонныйКоэффициент"));
		
КонецПроцедуры

&НаСервере
Функция АдресПараметровВХранилище()
	
	Возврат Документы.БольничныйЛист.АдресПараметровВХранилище(РеквизитФормыВЗначение("Объект"), ДополнительныеПараметрыРасчета());
	
КонецФункции

&НаСервере
Процедура ОбновитьЭлементыПараметровОплаты(Кэш = Неопределено)
	
	Если Кэш = Неопределено Тогда
		Кэш = Новый Соответствие;
	КонецЕсли;
	
	ОплачиваемыеДатыНачислений = Документы.БольничныйЛист.ОплачиваемыеДатыНачислений(Объект, Кэш);
	ОплачиваемыеПериоды = ОбщегоНазначенияБЗК.ТаблицаПериодовИзТаблицыДат(ОплачиваемыеДатыНачислений, "");
	
	Объект.ДнейОплаты = ОплачиваемыеДатыНачислений.Количество();
	ОбновитьГруппуИсключаемыеПериоды(Кэш, ОплачиваемыеПериоды);
	
	ОбновитьГруппуПриостановкаТД();
	
	ОбновитьГруппуПериодыПростоя(Кэш);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаНачалаПоловиннойОплаты",
		"Видимость",
		(ЗначениеЗаполнено(Объект.ДатаНачалаПоловиннойОплаты)
		Или Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьГруппуИсключаемыеПериоды(Кэш, ОплачиваемыеПериоды)
	
	Если Объект.ИсключаемыеПериоды.Количество() = 0 Тогда
		ИсключаемыеПериодыНадпись = НСтр("ru = 'Отсутствуют'");
	Иначе
		// Формирование заголовка и текста гиперссылки исключаемых периодов.
		ПредставленияИсключаемыхПериодов = Новый Массив;
		Если Объект.НазначитьПособие Тогда
			Для Каждого ИсключаемыйПериод Из Объект.ИсключаемыеПериоды Цикл
				ПредставленияИсключаемыхПериодов.Добавить(
					ОбщегоНазначенияБЗК.НаименованиеПериода(ИсключаемыйПериод.ДатаНачала, ИсключаемыйПериод.ДатаОкончания));
			КонецЦикла;
			НеоплачиваемыхДней = Объект.ИсключаемыеПериоды.Итог("КалендарныхДней");
		Иначе
			ПредставленияИсключаемыхПериодов.Добавить(
				ОбщегоНазначенияБЗК.НаименованиеПериода(Объект.ДатаНачала, Объект.ДатаОкончания));
			НеоплачиваемыхДней = ОбщегоНазначенияБЗК.КоличествоДнейВПериоде(Объект.ДатаНачала, Объект.ДатаОкончания);
		КонецЕсли;
		ЗаголовокНадписи = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 день;;%1 дня;%1 дней;'"),
			НеоплачиваемыхДней);
		ЗаголовокНадписи = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокНадписи);
		ТекстНадписи = СтрСоединить(ПредставленияИсключаемыхПериодов, ", ");
		
		ИсключаемыеПериодыНадпись = СтрШаблон(НСтр("ru = '%1 (%2)'"), ЗаголовокНадписи, ТекстНадписи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьГруппуПериодыПростоя(Кэш)
	
	Если Объект.ПериодыПростоя.Количество() = 0 Тогда
		ПериодыПростояНадпись = НСтр("ru = 'Отсутствуют'");
	Иначе
		// Формирование заголовка и текста гиперссылки периодов простоя.
		ПредставленияПериодовПростоя = Новый Массив;
		ДнейПростоя = 0;
		Для Каждого ПериодПростоя Из Объект.ПериодыПростоя Цикл
			ПредставленияПериодовПростоя.Добавить(
				ОбщегоНазначенияБЗК.НаименованиеПериода(ПериодПростоя.ДатаНачала, ПериодПростоя.ДатаОкончания));
			ДнейПростоя = ДнейПростоя + ОбщегоНазначенияБЗК.КоличествоДнейВПериоде(
				ПериодПростоя.ДатаНачала, ПериодПростоя.ДатаОкончания);
		КонецЦикла;
		ЗаголовокНадписи = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 день;;%1 дня;%1 дней;'"),
			ДнейПростоя);
		ЗаголовокНадписи = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокНадписи);
		ТекстНадписи = СтрСоединить(ПредставленияПериодовПростоя, ", ");
		
		ПериодыПростояНадпись = СтрШаблон(НСтр("ru = '%1 (%2)'"), ЗаголовокНадписи, ТекстНадписи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДатаНачалаСобытияПриИзмененииНаСервере()
	
	ЗаполнитьПоКадровымДаннымСотрудника();
	
	ЗаполнитьДатуНачалаПоловиннойОплаты();
	
	ЗаполнитьПараметрыОплаты(Неопределено);
	
	ЗаполнитьУсловияИсчисления();
	
	ЗаполнитьПроцентОплатыИОграничениеПособия();
	
	РассчитатьСреднийЗаработок();
	
	НастроитьЭлементыДополненияПричиныНетрудоспособности();
	
	ОбновитьЭлементыСпособРасчетовЗаСчетРаботодателя();
	
КонецПроцедуры

&НаСервере
Функция АдресСтруктурыБольничногоДляФормПериодов()
	ИменаСвойств = 
	"Ссылка,Организация,ПериодРегистрации,Сотрудник,ДатаНачала,ДатаОкончания,НазначитьПособие,
	|ДатаНачалаСобытия,ДатаНачалаОплаты,ДатаОкончанияОплаты,ДнейОплаты,АвтозаполнениеПериодовОплаты,
	|ПричинаНетрудоспособности,КодПричиныНетрудоспособности,СлучайУходаЗаБольнымРебенком,
	|ДатаНачалаПоловиннойОплаты,ДатаНарушенияРежима,ПрерывистыйМетод,СдвигатьПериодОплаты,НачалоВПериодПриостановленияТД,
	|ЯвляетсяПродолжениемБолезни,ПервичныйБольничныйЛист,
	|ОсвобождениеДатаНачала1,ОсвобождениеДатаНачала2,ОсвобождениеДатаНачала3,
	|ОсвобождениеДатаОкончания1,ОсвобождениеДатаОкончания2,ОсвобождениеДатаОкончания3";
	
	СтруктураБольничного = Новый Структура(ИменаСвойств);
	ЗаполнитьЗначенияСвойств(СтруктураБольничного, Объект, ИменаСвойств);
	СтруктураБольничного.Вставить("ЭтоЭЛН", СведенияОбЭЛН.ЭтоЭЛН);
	
	ПериодЛН = УчетПособийСоциальногоСтрахованияКлиентСервер.ПериодЛН(Объект);
	СтруктураБольничного.Вставить("ДатаНачалаЛН",    ПериодЛН.ДатаНачала);
	СтруктураБольничного.Вставить("ДатаОкончанияЛН", ПериодЛН.ДатаОкончания);	
	
	СтруктураБольничного.Вставить("ИсключаемыеПериоды",           Объект.ИсключаемыеПериоды.Выгрузить());
	СтруктураБольничного.Вставить("ПериодыПростоя",               Объект.ПериодыПростоя.Выгрузить());
	СтруктураБольничного.Вставить("ПериодыУходаЗаРодственниками", Объект.ПериодыУходаЗаРодственниками.Выгрузить());
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураБольничного, УникальныйИдентификатор);
КонецФункции

// ЭДОБЗК

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодключаемыеКоманды(УправляемаяФорма)
	ЭДОБЗККлиентСервер.ОбновитьКоманды(УправляемаяФорма, УправляемаяФорма.Объект, Истина);
КонецПроцедуры

#Область СреднийЗаработок

&НаСервере
Процедура РассчитатьСреднийЗаработок()
	Если Не ЗначениеЗаполнено(Объект.ДатаНачалаСобытия)
		Или Не ЗначениеЗаполнено(Объект.Сотрудник)
		Или Не ЗначениеЗаполнено(Объект.ПричинаНетрудоспособности)  Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьДанныеДляРасчетаСреднего();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыРасчета = ПараметрыРасчетаСреднегоДневногоЗаработкаФСС();
	СреднийЗаработок = УчетПособийСоциальногоСтрахования.СреднийЗаработокФСС(ПараметрыРасчета);
	Объект.СреднийДневнойЗаработок = СреднийЗаработок.Общий;
	Объект.СреднийДневнойЗаработокРК = СреднийЗаработок.РК;
	Объект.СреднийДневнойЗаработокСН = СреднийЗаработок.СН;
	Объект.МинимальныйСреднедневнойЗаработок = УчетПособийСоциальногоСтрахования.МинимальныйСреднедневнойЗаработокФСС(ПараметрыРасчета);
	
	РассчитатьНачисленияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДляРасчетаСреднего()
	Если ГодНачалаСобытия = Год(Объект.ДатаНачалаСобытия) Тогда
		Возврат;
	КонецЕсли;
	ГодНачалаСобытия = Год(Объект.ДатаНачалаСобытия);
	
	Если Не Объект.ЯвляетсяПродолжениемБолезни Тогда
		Объект.СреднийЗаработокФСС.Очистить();
		Объект.ОтработанноеВремяДляСреднегоФСС.Очистить();
		
		РасчетныеГоды = Новый Массив;
		РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод);
		РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод);
		
		СведенияОЗаработке = Новый Соответствие;
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
			МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
			СведенияОЗаработке = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеОЗаработкеДляРасчетаСреднегоФСС(
				Объект.Сотрудник, Объект.Организация, Объект.ДатаНачалаСобытия, РасчетныеГоды);
		КонецЕсли;
		
		Если СведенияОЗаработке.Количество() = 0 Тогда
			
			Документы.БольничныйЛист.ЗаполнитьДанныеСреднегоЗаработкаПоПредыдущемуДокументу(
				Объект, РасчетныеГоды);
			
		ИначеЕсли СведенияОЗаработке.Количество() = 1 Тогда
			
			Если СведенияОЗаработке.Получить(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод) = Неопределено Тогда
				
				Документы.БольничныйЛист.ЗаполнитьДанныеСреднегоЗаработкаПоПредыдущемуДокументу(
					Объект, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод));
				
			Иначе
				
				Документы.БольничныйЛист.ЗаполнитьДанныеСреднегоЗаработкаПоПредыдущемуДокументу(
					Объект, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод));
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДнейБолезниУходаЗаДетьми = 0;
		
		ПараметрыРасчета = ПараметрыРасчетаСреднегоДневногоЗаработкаФСС();
		
		Для каждого СведенияГода Из СведенияОЗаработке Цикл
			
			Для каждого СведенияМесяца Из СведенияГода.Значение Цикл
				
				Для Каждого СтрокаЗаработка Из СведенияМесяца.Значение.Заработок Цикл
					ЗаполнитьЗначенияСвойств(Объект.СреднийЗаработокФСС.Добавить(), СтрокаЗаработка);
				КонецЦикла;
				
				Если СведенияМесяца.Значение.ДнейБолезниУходаЗаДетьми <> 0 Тогда
					
					СтрокаСведений = Объект.ОтработанноеВремяДляСреднегоФСС.Добавить();
					СтрокаСведений.ФизическоеЛицо = Объект.ФизическоеЛицо;
					СтрокаСведений.Период = Дата(СведенияГода.Ключ, СведенияМесяца.Ключ, 1);
					СтрокаСведений.ДнейБолезниУходаЗаДетьми = СведенияМесяца.Значение.ДнейБолезниУходаЗаДетьми;
					
					Если УчетПособийСоциальногоСтрахованияКлиентСервер.УчитыватьСтрокуИсточникаСреднегоЗаработка(СтрокаСведений.Период, ПараметрыРасчета) Тогда
						ДнейБолезниУходаЗаДетьми = ДнейБолезниУходаЗаДетьми + СтрокаСведений.ДнейБолезниУходаЗаДетьми;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРасчетаСреднегоДневногоЗаработкаФСС()
	ПараметрыРасчета = УчетПособийСоциальногоСтрахования.ПараметрыРасчетаСреднегоДневногоЗаработкаФССПоДокументу(Объект);
	
	ПараметрыРасчета.ИспользоватьДниБолезниУходаЗаДетьми = Документы.БольничныйЛист.ИспользоватьДниБолезниУходаЗаДетьми(Объект);
	ПараметрыРасчета.УчитыватьДниПриостановленияТД       = Документы.БольничныйЛист.УчитыватьДниПриостановленияТД(Объект);
	ПараметрыРасчета.РайонныйКоэффициентРФ               = Объект.РайонныйКоэффициентРФНаНачалоСобытия;
	ПараметрыРасчета.ПорядокРасчета                      = УчетПособийСоциальногоСтрахованияКлиентСервер.ПорядокРасчетаСреднегоЗаработкаФСС(Объект.ДатаНачалаСобытия);
	ПараметрыРасчета.ПрименятьПредельнуюВеличину         = Не ЭтоНСПЗ();
	
	ПараметрыРасчета.ДанныеНачислений                 = ДанныеОЗаработкеДляРасчетаСреднегоЗаработка();
	ПараметрыРасчета.ДанныеВремени                    = ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка();
	ПараметрыРасчета.ДанныеСтрахователей              = УчетПособийСоциальногоСтрахования.ПустаяТаблицаДанныеСтрахователейСреднийЗаработокФСС();
	ПараметрыРасчета.ПриостановленияТрудовыхДоговоров = Объект.ПриостановленияТрудовыхДоговоров;
	
	Возврат ПараметрыРасчета;
КонецФункции

&НаСервере
Функция ДанныеОЗаработкеДляРасчетаСреднегоЗаработка()
	
	ДанныеОЗаработкеДляРасчетаСреднегоЗаработка = УчетПособийСоциальногоСтрахования.ПустаяТаблицаНачисленийСреднийЗаработокФСС();
	
	Для каждого СтрокаДанных Из Объект.СреднийЗаработокФСС Цикл
		СтрокаДанныхОЗаработке = ДанныеОЗаработкеДляРасчетаСреднегоЗаработка.Добавить();
		СтрокаДанныхОЗаработке.ФизическоеЛицо = СтрокаДанных.ФизическоеЛицо;
		СтрокаДанныхОЗаработке.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2011;
		СтрокаДанныхОЗаработке.Период = СтрокаДанных.Период;
		СтрокаДанныхОЗаработке.Сумма = СтрокаДанных.Сумма;
		СтрокаДанныхОЗаработке.Начисление = СтрокаДанных.Начисление;
	КонецЦикла;
	
	Возврат ДанныеОЗаработкеДляРасчетаСреднегоЗаработка;
	
КонецФункции

&НаСервере
Функция ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка()
	
	ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка = УчетПособийСоциальногоСтрахования.ПустаяТаблицаОтработанноеВремяСреднийЗаработокФСС();
	
	Для каждого СтрокаДанных Из Объект.ОтработанноеВремяДляСреднегоФСС Цикл
		СтрокаДанныхОбОтработанномВремени = ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка.Добавить();
		СтрокаДанныхОбОтработанномВремени.ФизическоеЛицо = СтрокаДанных.ФизическоеЛицо;
		СтрокаДанныхОбОтработанномВремени.Период = СтрокаДанных.Период;
		СтрокаДанныхОбОтработанномВремени.ДнейБолезниУходаЗаДетьми = СтрокаДанных.ДнейБолезниУходаЗаДетьми;
	КонецЦикла;
	
	Возврат ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка;
	
КонецФункции

&НаСервере
Процедура ОбновитьЭлементыСпособРасчетовЗаСчетРаботодателя()
	
	ПоказыватьГруппу = ПоказыватьГруппСпособРасчетовЗаСчетРаботодателя();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
	"ГруппаСпособРасчетовЗаСчетРаботодателя", "Видимость", ПоказыватьГруппу);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСреднийЗаработок(УправляемаяФорма)
	
	Подсказки = Новый Массив;
	УправляемаяФорма.СреднийДневнойЗаработок = 0;
	Если УправляемаяФорма.Объект.СреднийДневнойЗаработок > 0 Тогда
		УправляемаяФорма.СреднийДневнойЗаработок = УправляемаяФорма.Объект.СреднийДневнойЗаработок;
		Подсказки.Добавить(СтрШаблон(НСтр("ru = 'Средний заработок: %1'"), Формат(УправляемаяФорма.Объект.СреднийДневнойЗаработок, "ЧДЦ=2")));
	КонецЕсли;
	Если УправляемаяФорма.Объект.СреднийДневнойЗаработокРК > 0 Или УправляемаяФорма.Объект.СреднийДневнойЗаработокСН > 0 Тогда
		Подсказки.Добавить(НСтр("ru = 'В том числе:'"));
	КонецЕсли;
	Если УправляемаяФорма.Объект.СреднийДневнойЗаработокРК > 0 Тогда
		Подсказки.Добавить(СтрШаблон(НСтр("ru = '- Районный коэффициент: %1'"), Формат(УправляемаяФорма.Объект.СреднийДневнойЗаработокРК, "ЧДЦ=2")));
	КонецЕсли;
	Если УправляемаяФорма.Объект.СреднийДневнойЗаработокСН > 0 Тогда
		Подсказки.Добавить(СтрШаблон(НСтр("ru = '- Северная надбавка: %1'"), Формат(УправляемаяФорма.Объект.СреднийДневнойЗаработокСН, "ЧДЦ=2")));
	КонецЕсли;
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		УправляемаяФорма, "СреднийДневнойЗаработок", СтрСоединить(Подсказки, "," + Символы.ПС));
	КонецПроцедуры
	
&НаСервере
Функция ПоказыватьГруппСпособРасчетовЗаСчетРаботодателя()
	
	ПоказыватьГруппу = Не Объект.ЯвляетсяПродолжениемБолезни И Объект.СотрудникДоговорник
		И Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ОбщееЗаболевание
		И ЗначениеЗаполнено(Объект.ДатаНачала) И ЗначениеЗаполнено(Объект.ДатаОкончания);
		
	Если ПоказыватьГруппу Тогда
		ДатаНачалаОплатыЗаСчетФСС = УчетПособийСоциальногоСтрахования.ДатаНачалаОплатыЗаСчетФСС(Объект.ДатаНачалаСобытия, Объект.ПричинаНетрудоспособности);
		ПоказыватьГруппу = Объект.ДатаНачалаОплаты < ДатаНачалаОплатыЗаСчетФСС
	КонецЕсли;
	
	Возврат ПоказыватьГруппу;
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.КонтрольВеденияУчета
&НаКлиенте
Процедура Подключаемый_ОткрытьОтчетПоПроблемам(ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка)
	
	КонтрольВеденияУчетаКлиентБЗК.ОткрытьОтчетПоПроблемамОбъекта(ЭтотОбъект, Объект.Ссылка, СтандартнаяОбработка);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	УстановитьФункциональныеОпцииФормы();

	ЦветГиперссылка       = ЦветаСтиля.ГиперссылкаЦвет;
	ЦветПустойГиперссылки = ЦветаСтиля.ЦветПустойГиперссылки;
	ЦветОшибки            = ЦветаСтиля.ПоясняющийОшибкуТекст;
	
	Кэш = Новый Соответствие;
	ВидОплатыЗаСчетРаботодателя = Документы.БольничныйЛист.ВидОплатыЗаСчетРаботодателя(Объект, Кэш);
	ВидОплатыПособия = Документы.БольничныйЛист.ВидОплатыПособия(Объект, Кэш);
	ВидНеоплачиваемогоВремени = Документы.БольничныйЛист.ВидНеоплачиваемогоВремени(Объект, Кэш);

	ПервичныйПродолжение = Объект.ЯвляетсяПродолжениемБолезни;
	КодНарушенияРежима   = Объект.КодНарушенияРежима;
	
	РасчетыСКонтрагентами = Объект.СотрудникДоговорник
		И Объект.СпособРасчетовСФизическимиЛицами = Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами;
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой");
	
	НастроитьЭлементыДополненияПричиныНетрудоспособности();
	УстановитьДоступностьЭлементовПоПервичномуБольничномуЛисту(ЭтотОбъект);

	УстановитьСвойстваПоляПервичногоБольничногоЛиста(ЭтотОбъект);
	ОбновитьНадписьДнейОсвобожденияОтРаботы();
	
	ЗаполнитьСписокВыбораКодНарушенияРежима();
	УстановитьСвойстваПолейСтажЛьготы();
	УстановитьСвойстваПолейОтметкиОНарушенииРежима(ЭтотОбъект);
	ОтобразитьРазмерОплаты();
	
	ЗаполнитьРеквизитыФормыПоКадровымДаннымСотрудника(КадровыеДанныеСотрудника(Ложь));
	ЗаполнитьИтоговыеСуммыНачислений();
	ОтобразитьЗначокВнимание();
	
	ОбновитьЭлементыПараметровОплаты();
	ОбновитьЭлементыСпособРасчетовЗаСчетРаботодателя();
	
	ГодНачалаСобытия = Год(Объект.ДатаНачалаСобытия);
	
	ПодготовитьТаблицуЗаболеваний(Объект.ДатаНачалаСобытия);
	
	ОтобразитьпризнакКорректировкиНДФЛ();
	
	ОбновитьГиперссылкуВводаДанныхЛисткаНетрудоспособности();
	ОбновитьСведенияДляФСС();
	
	УстановитьВидимостьКарантинаПоКоронавирусу();
	ПроверитьПриостановленияТрудовыхДоговоров(Ложь);
	УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов();
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДнейОплаты()
	
	Если Не Объект.НазначитьПособие
		Или Не ЗначениеЗаполнено(Объект.ДатаНачала)
		Или Не ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда
		
		Объект.ДнейОплаты = 0;
		
	Иначе
		Если Объект.ДатаНачалаСобытия <> Объект.ДатаНачала Тогда
			ОплаченоРанее = ЗарплатаКадрыКлиентСервер.ДнейВПериоде(Объект.ДатаНачалаСобытия, Объект.ДатаНачала - 1, Истина);
		Иначе
			ОплаченоРанее = 0;
		КонецЕсли;
		
		Объект.ДатаОкончанияОплаты = ЗарплатаКадрыКлиентСервер.ДобавитьДней(Объект.ДатаНачалаОплаты, Объект.ДнейОплаты);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	ЗапланироватьПолноеОбновлениеСведенийДляФСС();
	
	Объект.ПрименятьЛьготыПриНачисленииПособия = Ложь;
	Объект.ФинансированиеФедеральнымБюджетом   = Неопределено;
	
	ОчиститьТаблицыНДФЛ();
	ЗаполнитьПоКадровымДаннымСотрудника();
	ЗаполнитьУсловияИсчисления();
	
	Объект.ПланируемаяДатаВыплаты = ПланируемаяДатаВыплаты();
	
	УстановитьСвойстваПолейСтажЛьготы();
	ЗаполнитьПроцентОплатыИОграничениеПособия();
	
	ГодНачалаСобытия = Неопределено;
	
	РассчитатьСреднийЗаработок();
	
	ОбновитьСведенияДляФСС();
	
	ОбновитьЭлементыСпособРасчетовЗаСчетРаботодателя();
	
	ОбновитьНадписьДнейОсвобожденияОтРаботы();
	
КонецПроцедуры

&НаСервере
Функция КадровыеДанныеСотрудника(РазрешенаЗаменаСсылкиСотрудникаГПХНаСотрудникаПоТД)
	ИменаКадровыхДанных =
	"ФизическоеЛицо,
	|Подразделение,
	|ДатаУвольнения,
	|ВидЗанятости,
	|ДоляНеполногоРабочегоВремени,
	|РайонныйКоэффициентРФ";
	ДанныеСотрудника = Новый Структура(ИменаКадровыхДанных);
	ДанныеСотрудника.Вставить("ЕстьДоговорыГПХ", Ложь);
	
	Если Не ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		Возврат ДанныеСотрудника;
	КонецЕсли;
	ДатаНачалаСобытия = ?(ЗначениеЗаполнено(Объект.ДатаНачалаСобытия), Объект.ДатаНачалаСобытия, ТекущаяДатаСеанса());
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(
		Истина,
		Объект.Сотрудник,
		ИменаКадровыхДанных,
		ДатаНачалаСобытия);
	Если КадровыеДанные.Количество() = 0 Тогда
		Возврат ДанныеСотрудника;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ДанныеСотрудника, КадровыеДанные[0]);
	
	// Получение кадровых данных основного сотрудника и подмена ссылки при необходимости.
	Если РазрешенаЗаменаСсылкиСотрудникаГПХНаСотрудникаПоТД
		И ДанныеСотрудника.ВидЗанятости <> Перечисления.ВидыЗанятости.ОсновноеМестоРаботы
		И ДанныеСотрудника.ВидЗанятости <> Перечисления.ВидыЗанятости.Совместительство Тогда
		ДанныеОсновногоСотрудника = КадровыйУчет.КадровыеДанныеОсновногоСотрудникаФизическогоЛица(
			Объект.Организация,
			ДанныеСотрудника.ФизическоеЛицо,
			ИменаКадровыхДанных,
			ДатаНачалаСобытия,
			Истина);
		Если ДанныеОсновногоСотрудника <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(ДанныеОсновногоСотрудника.ДатаУвольнения)
				Или ДанныеОсновногоСотрудника.ДатаУвольнения >= ДатаНачалаСобытия Тогда
				Объект.Сотрудник = ДанныеОсновногоСотрудника.Сотрудник;
				ЗаполнитьЗначенияСвойств(ДанныеСотрудника, ДанныеОсновногоСотрудника);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеСотрудника.ЕстьДоговорыГПХ = КадровыйУчет.ЕстьДоговорыГПХ(КадровыеДанные[0].ФизическоеЛицо, Объект.Организация, ДатаНачалаСобытия);
	Возврат ДанныеСотрудника;
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормыПоКадровымДаннымСотрудника(КадровыеДанныеСотрудника)
	ДатаУвольненияСотрудника = КадровыеДанныеСотрудника.ДатаУвольнения;
	ПодразделениеСотрудника  = КадровыеДанныеСотрудника.Подразделение;
	ЕстьДоговорыГПХ = КадровыеДанныеСотрудника.ЕстьДоговорыГПХ;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоКадровымДаннымСотрудника()
	
	ДанныеСотрудника = КадровыеДанныеСотрудника(Истина);
	
	ЗаполнитьРеквизитыФормыПоКадровымДаннымСотрудника(ДанныеСотрудника);
	
	Объект.ФизическоеЛицо       = ДанныеСотрудника.ФизическоеЛицо;
	Объект.ОсновноеМестоРаботы  = ДанныеСотрудника.ВидЗанятости = Перечисления.ВидыЗанятости.ОсновноеМестоРаботы;
	Объект.ДоляНеполногоВремени = УчетПособийСоциальногоСтрахования.ДоляНеполногоРабочегоВремени(
		ДанныеСотрудника.ДоляНеполногоРабочегоВремени);
	Объект.РайонныйКоэффициентРФНаНачалоСобытия = ДанныеСотрудника.РайонныйКоэффициентРФ;
	
	Объект.СотрудникДоговорник = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСотрудника.ДатаУвольнения)
		Или ДанныеСотрудника.ДатаУвольнения < Объект.ДатаНачалаСобытия Тогда
		УстановитьПривилегированныйРежим(Истина);
		ТекущийДоговор = СЭДОФСС.ТекущийДоговорФизлица(Объект.ГоловнаяОрганизация, Объект.ФизическоеЛицо, Объект.ДатаНачалаСобытия);
		УстановитьПривилегированныйРежим(Ложь);
		Если ТекущийДоговор <> Неопределено Тогда
			Объект.СотрудникДоговорник = Не ТекущийДоговор.Трудовой И ТекущийДоговор.ГПХ;
		КонецЕсли;
	КонецЕсли;
	Если Объект.СотрудникДоговорник Тогда
		Объект.СпособРасчетовСФизическимиЛицами = Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами;
		РасчетыСКонтрагентами = Истина;
	Иначе
		Объект.СпособРасчетовСФизическимиЛицами = Перечисления.СпособыРасчетовСФизическимиЛицами.ПустаяСсылка();
		РасчетыСКонтрагентами = Ложь;
	КонецЕсли;
	
	// Приостановления трудовых договоров.
	ЗаполнитьПриостановленияТрудовыхДоговоров();

КонецПроцедуры

&НаСервере
Процедура НачисленияПриОкончанииРедактированияНаСервере()
	
	ЗапланироватьПолноеОбновлениеСведенийДляФСС();
	ПересчитатьНДФЛ();
	ЗаполнитьИтоговыеСуммыНачислений();
	ОтобразитьЗначокВнимание();
	ОбновитьСведенияДляФСС();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыДополненияПричиныНетрудоспособности()
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Очистить();
	
	ТаблицаСлучаевУхода = Перечисления.СлучаиУходаЗаБольнымиДетьми.ТаблицаВыбора(Объект.ДатаНачалаСобытия);
	
	Если ЗначениеЗаполнено(Объект.КодПричиныНетрудоспособности) Тогда
		КодыПричиныНетрудоспособности = СтрРазделить(Объект.КодПричиныНетрудоспособности, Символы.ПС);
	Иначе
		КодыПричиныНетрудоспособности = Перечисления.ПричиныНетрудоспособности.КодыПричины(Объект.ДатаНачалаСобытия, Объект.ПричинаНетрудоспособности);
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаСлучаевУхода Цикл
		Если КодыПричиныНетрудоспособности.Найти(СтрокаТаблицы.КодПричины) <> Неопределено Тогда
			СписокВыбора.Добавить(СтрокаТаблицы.СлучайУхода, СтрокаТаблицы.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Случай = Объект.СлучайУходаЗаБольнымРебенком;
	Если ЗначениеЗаполнено(Случай) И СписокВыбора.НайтиПоЗначению(Случай) = Неопределено Тогда
		СтрокаТаблицы = ТаблицаСлучаевУхода.Найти(Случай, "СлучайУхода");
		Представление = ?(СтрокаТаблицы = Неопределено, Строка(Случай), СтрокаТаблицы.Представление);
		СписокВыбора.Добавить(Случай, Новый ФорматированнаяСтрока(Представление, , ЦветОшибки));
		Элементы.ПредставлениеПричиныНетрудоспособности.ЦветТекста          = ЦветОшибки;
		Элементы.ПредставлениеПричиныНетрудоспособности.ЦветТекстаЗаголовка = ЦветОшибки;
	Иначе
		Элементы.ПредставлениеПричиныНетрудоспособности.ЦветТекста          = Новый Цвет;
		Элементы.ПредставлениеПричиныНетрудоспособности.ЦветТекстаЗаголовка = Новый Цвет;
	КонецЕсли;
	
	УстановитьВидимостьКарантинаПоКоронавирусу();
	
КонецПроцедуры

&НаСервере
Процедура ПричинаНетрудоспособностиПриИзмененииНаСервере()
	
	Документы.БольничныйЛист.ОчиститьСлучайУходаПриНеобходимости(Объект);
	
	СтруктураПоиска = Новый Структура("Наименование", ПредставлениеПричиныНетрудоспособности);
	МассивСтрок = ТаблицаЗаболеваний.НайтиСтроки(СтруктураПоиска);
	Если МассивСтрок.Количество() <> 0 Тогда
		Объект.КодПричиныНетрудоспособности = МассивСтрок[0].КодПричиныНетрудоспособности;
	КонецЕсли;
	
	Если Не ОбменЛисткамиНетрудоспособностиФСС.ЭтоКарантинныйЭЛН(
			Объект.ПричинаНетрудоспособности,
			Объект.НомерЛисткаНетрудоспособности) Тогда
		Объект.ЭЛНКарантинПоКоронавирусу = Ложь;
	КонецЕсли;
	
	Кэш = Новый Соответствие;
	ВидОплатыПособия = Документы.БольничныйЛист.ВидОплатыПособия(Объект, Кэш);
	
	Объект.АвтозаполнениеПериодовОплаты = Истина;
	
	УстановитьСвойстваПолейСтажЛьготы();
	
	ОбновитьЭлементыПараметровОплаты(Кэш);
	
	УстановитьДнейОплаты();
	
	ОбновитьНадписьДнейОсвобожденияОтРаботы();
	
	ОбновитьЭлементыСпособРасчетовЗаСчетРаботодателя();
	
	НастроитьЭлементыДополненияПричиныНетрудоспособности();
	
	ЗаполнитьПроцентОплатыИОграничениеПособия();
	
	Документы.БольничныйЛист.ЗаполнитьПараметрыОплаты(Объект, Кэш);
	ЗаполнитьДатуНачалаПоловиннойОплаты();
	
	УстановитьВидимостьКарантинаПоКоронавирусу();
	
	ГодНачалаСобытия = Неопределено;
	РассчитатьСреднийЗаработок();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКарантинаПоКоронавирусу()
	
	ЭтоУходЗаРебенком = (Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком);
	
	Если ЭтоУходЗаРебенком Тогда
		КарантинПоКоронавирусуВидимость = Ложь;
	Иначе
		КарантинПоКоронавирусуВидимость = ОбменЛисткамиНетрудоспособностиФСС.ЭтоКарантинныйЭЛН(
			Объект.ПричинаНетрудоспособности,
			Объект.НомерЛисткаНетрудоспособности);
	КонецЕсли;
	
	Элементы.ГруппаКарантинПоКоронавирусу.Видимость = КарантинПоКоронавирусуВидимость;
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ОбновитьГоловнуюОрганизацию();
	УстановитьФункциональныеОпцииФормы();
	СотрудникПриИзмененииНаСервере();
	
	Объект.ПланируемаяДатаВыплаты = ПланируемаяДатаВыплаты();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьГоловнуюОрганизацию()
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Объект.Организация);
	Иначе
		Объект.ГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СведенияОбНДФЛ(ВходящиеДанные = Неопределено, ФизическоеЛицо = Неопределено) Экспорт
	
	Если ВходящиеДанные = Неопределено Тогда
		Форма = ЭтотОбъект;
		ОбъектФормы    = Объект;
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Иначе
		Форма          = ВходящиеДанные;
		ОбъектФормы    = ВходящиеДанные.Объект;
		
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ВходящийОбъект", Новый ОписаниеТипов("ДокументОбъект.БольничныйЛист")));
		МассивИменРеквизитовФормы = Новый Массив;
		ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, МассивИменРеквизитовФормы);
		ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы);
		КопироватьДанныеФормы(ОбъектФормы, ЭтотОбъект["ВходящийОбъект"]);
		
		ДокументОбъект = РеквизитФормыВЗначение("ВходящийОбъект");
	КонецЕсли;
	
	ДополнительныеСведения = УчетНДФЛФормы.ДополнительныеДанныеДляПолученияСведенийОДоходахНДФЛДокумента();
	ДополнительныеСведения.МесяцНачисления = ОбъектФормы.ПериодРегистрации;
	ДополнительныеСведения.ПланируемаяДатаВыплаты = ОбъектФормы.ПланируемаяДатаВыплаты;
	
	СведенияОДоходахНДФЛ = УчетНДФЛФормы.СведенияОДоходахНДФЛДокумента(ОбъектФормы, "Начисления", ДополнительныеСведения);
	АдресСведенийОбНДФЛ = УчетНДФЛФормы.СведенияОбНДФЛ(Форма);
	
	ДанныеОбНДФЛ = ПолучитьИзВременногоХранилища(АдресСведенийОбНДФЛ);
	ДанныеОбНДФЛ.Вставить("СведенияОДоходах",      СведенияОДоходахНДФЛ.СведенияОДоходах);
	ДанныеОбНДФЛ.Вставить("ВычетыКДоходам",        СведенияОДоходахНДФЛ.ВычетыКДоходам);
	ДанныеОбНДФЛ.Вставить("Сотрудник",             ОбъектФормы.Сотрудник);
	ДанныеОбНДФЛ.Вставить("СериализованныйОбъект", ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(ДокументОбъект));
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеОбНДФЛ, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция НДФЛПодробнееНаСервере(ФизическиеЛица) Экспорт
	
	Если ТипЗнч(ФизическиеЛица) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическиеЛица);
	Иначе
		СписокФизическихЛиц = ФизическиеЛица;
	КонецЕсли;
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	НДФЛПодробнее = Новый Массив;
	НДФЛПодробнее.Добавить(ДокументОбъект.Ссылка);
	НДФЛПодробнее.Добавить(УчетНДФЛФормы.РегистрНалоговогоУчетаПоНДФЛ(ДокументОбъект, Модифицированность, СписокФизическихЛиц, Объект.ПериодРегистрации));
	
	Возврат НДФЛПодробнее;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеНДФЛНаСервере(АдресВременногоХранилища)
	
	Параметр = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Объект.НДФЛ.Загрузить(Параметр.НДФЛ);
	Объект.ПримененныеВычетыНаДетейИИмущественные.Загрузить(Параметр.ПримененныеВычетыНаДетейИИмущественные);
	Объект.КорректировкиВыплаты.Загрузить(Параметр.КорректировкиВыплаты);
	ОтобразитьпризнакКорректировкиНДФЛ();
	ЗаполнитьИтоговыеСуммыНачислений();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицыНДФЛ()
	
	Объект.НДФЛ.Очистить();
	Объект.ПримененныеВычетыНаДетейИИмущественные.Очистить();
	Объект.КорректировкиВыплаты.Очистить();
	ОтобразитьПризнакКорректировкиНДФЛ();
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьпризнакКорректировкиНДФЛ()
	
	ФиксРасчетНДФЛ = Объект.НДФЛ.НайтиСтроки(Новый Структура("ФиксРасчет", Истина));
	ФиксРасчетКорректировки = Объект.КорректировкиВыплаты.НайтиСтроки(Новый Структура("ФиксРасчет", Истина));
	Элементы.ДекорацияНДФЛ.Видимость = 
		ФиксРасчетНДФЛ.Количество() <> 0
		ИЛИ ФиксРасчетКорректировки.Количество() <> 0;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьЗначокВнимание()
	
	Если СтажЛет = 0 Тогда
		Элементы.ГруппаРезультатРасчетаПодвал.Видимость = Ложь;
	Иначе
		ПодсчитатьИтог();
		Если ЗначениеЗаполнено(Объект.Сотрудник)
			И ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
			Элементы.ГруппаРезультатРасчетаПодвал.Видимость = ИтогГод1 = 0 ИЛИ ИтогГод2 = 0;
		Иначе
			Элементы.ГруппаРезультатРасчетаПодвал.Видимость = Ложь;
		КонецЕсли;
		
		Если НЕ БаннерПогашен Тогда
			ОтключенныеПредупреждения = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"НастройкиОтображенияБольничныхЛистов",
				"БольничныеЛистыНулевыеПериоды",
				Новый Массив);
				
				Элементы.Баннер.Видимость = ОтключенныеПредупреждения.Найти(Объект.Ссылка) = Неопределено;
				БаннерПогашен = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодсчитатьИтог()
	
	ИтогГод1 = 0;
	ИтогГод2 = 0;
	
	Для каждого СтрокаСреднегоЗаработкаФСС  Из Объект.СреднийЗаработокФСС Цикл
		
		Год = Год(СтрокаСреднегоЗаработкаФСС.Период);
		Если Год = Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод Тогда
			НомерГода = 1;
		ИначеЕсли Год = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод Тогда
			НомерГода = 2;
		Иначе
			НомерГода = Неопределено;
		КонецЕсли;
			
		Если НомерГода <> Неопределено Тогда
			
			ЭтотОбъект["ИтогГод" + НомерГода] = СтрокаСреднегоЗаработкаФСС.Сумма;
			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПланируемаяДатаВыплаты()
	
	КорректироватьДатуВыплаты = Ложь;
	Если НачалоМесяца(Объект.Дата) <> НачалоМесяца(Объект.ПериодРегистрации)
		ИЛИ НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		
		ДатаВыплаты = КонецМесяца(Объект.ПериодРегистрации) + 5 * 86400;
		КорректироватьДатуВыплаты = Истина;
		
	Иначе
		
		// выплата зарплаты в текущем месяце
		ДатаВыплаты = УчетЗарплаты.ПланируемаяДатаВыплатыЗарплаты(Объект.Организация, ДобавитьМесяц(Объект.ПериодРегистрации, -1));
		Если ДатаВыплаты <= Объект.Дата Тогда
			// выплата аванса за текущий месяц
			ДатаВыплаты = УчетЗарплаты.ПланируемаяДатаВыплатыАванса(Объект.Организация, Объект.ПериодРегистрации);
		КонецЕсли;
		Если ДатаВыплаты <= Объект.Дата Тогда
			// выплата зарплаты за текущий месяц
			ДатаВыплаты = УчетЗарплаты.ПланируемаяДатаВыплатыЗарплаты(Объект.Организация, Объект.ПериодРегистрации);
		КонецЕсли;
		Если ДатаВыплаты <= Объект.Дата Тогда
			// выплата зарплаты следующего месяца
			ДатаВыплаты = УчетЗарплаты.ПланируемаяДатаВыплатыЗарплаты(Объект.Организация, ДобавитьМесяц(Объект.ПериодРегистрации, 1));
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаУвольненияСотрудника)
		И НачалоМесяца(ДатаУвольненияСотрудника) = Объект.ПериодРегистрации Тогда
		ДатаВыплаты = Мин(ДатаУвольненияСотрудника, ДатаВыплаты);
		КорректироватьДатуВыплаты = Истина;
	КонецЕсли;
	
	Если КорректироватьДатуВыплаты Тогда
		ДатаВыплаты = УчетЗарплаты.БлижайшийРабочийДеньДляВыплаты(Объект.Организация, ДатаВыплаты);
	КонецЕсли;
	
	Возврат ДатаВыплаты;
	
КонецФункции

&НаСервере
Процедура ОбработатьИзменениеДатыНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		Объект.ПланируемаяДатаВыплаты = ПланируемаяДатаВыплаты();
		ПересчитатьНДФЛ();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИзСпискаЗавершение(ВыбранныйКод, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйКод <> Неопределено Тогда
		Модифицированность = Истина;
		КодНарушенияРежима = ВыбранныйКод.Значение;
		ИзменениеКодаНарушенияРежима();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеКодаНарушенияРежима()
	
	Если КодНарушенияРежима = "Отсутствует" Тогда
		Объект.КодНарушенияРежима = "";
		Объект.ДатаНарушенияРежима = "";
		РассчитатьНачисленияНаСервере();
	Иначе
		Объект.КодНарушенияРежима = КодНарушенияРежима;
	КонецЕсли;
	УстановитьСвойстваПолейОтметкиОНарушенииРежима(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьНачисленияНаСервере()
	
	// От флажка ИспользоватьПрямыеВыплатыФСС зависят начисления,
	// а от начислений - флажки Объект.ПособиеВыплачиваетсяФСС и ОтправитьЭЛН.
	ЕстьПособияЗаСчетФСС                       = Неопределено;
	ТребуетсяЗаполнитьПараметрыПрямыхВыплатФСС = Истина;
	ТребуетсяЗаполнитьПараметрыОтправкиЭЛН     = Истина;
	ПрочитатьФлажокИспользоватьПрямыеВыплатыФСС();
	
	Документы.БольничныйЛист.РассчитатьНачисления(Объект, ДополнительныеПараметрыРасчета());
	ПересчитатьНДФЛ();
	ЗаполнитьИтоговыеСуммыНачислений();
	ОтобразитьЗначокВнимание();
	ОбновитьСведенияДляФСС();
	
КонецПроцедуры

&НаСервере
Функция ДополнительныеПараметрыРасчета()
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодразделениеСотрудника",      ПодразделениеСотрудника);
	ДополнительныеПараметры.Вставить("ВидОплатыЗаСчетРаботодателя",  ВидОплатыЗаСчетРаботодателя);
	ДополнительныеПараметры.Вставить("ВидОплатыПособия",             ВидОплатыПособия);
	ДополнительныеПараметры.Вставить("ВидНеоплачиваемогоВремени",    ВидНеоплачиваемогоВремени);
	ДополнительныеПараметры.Вставить("ИспользоватьПрямыеВыплатыФСС", ИспользоватьПрямыеВыплатыФСС);
	ДополнительныеПараметры.Вставить("УникальныйИдентификатор",      УникальныйИдентификатор);
	ДополнительныеПараметры.Вставить("ДнейПриостановленияТД",        ДнейПриостановленияТД);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = 0;
	ЗарплатаКадрыПереопределяемый.СостояниеДокумента(Объект, СостояниеДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеРасчетаЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если НЕ ПустаяСтрока(РезультатРедактирования) Тогда
		ЗаполнитьНачисленияПересчитатьНалог(РезультатРедактирования);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисленияПересчитатьНалог(АдресВХранилище)
	
	ПараметрыОповещения = ПолучитьИзВременногоХранилища(АдресВХранилище);
	
	ДесериализованныйОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(ПараметрыОповещения.СериализованныйОбъект);
	
	ЗаполнитьЗначенияСвойств(Объект, ДесериализованныйОбъект,
		"ПериодРасчетаСреднегоЗаработкаПервыйГод, ПериодРасчетаСреднегоЗаработкаВторойГод, СреднийДневнойЗаработок, СреднийДневнойЗаработокРК, СреднийДневнойЗаработокСН, МинимальныйСреднедневнойЗаработок");
	
	Объект.ОтработанноеВремяДляСреднегоФСС.Загрузить(ДесериализованныйОбъект.ОтработанноеВремяДляСреднегоФСС.Выгрузить());
	Объект.СреднийЗаработокФСС.Загрузить(ДесериализованныйОбъект.СреднийЗаработокФСС.Выгрузить());
	Объект.Начисления.Загрузить(ДесериализованныйОбъект.Начисления.Выгрузить());
	
	НачисленияПриОкончанииРедактированияНаСервере();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораКодНарушенияРежима()
	
	Элементы.КодНарушенияРежима.СписокВыбора.Добавить("Отсутствует", Нстр("ru = 'Отсутствует'"));
	ПрямыеВыплатыПособийСоциальногоСтрахованияФормы.ЗаполнитьСписокВыбораКодНарушенияРежима(Элементы.КодНарушенияРежима);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваПолейСтажЛьготы()
	
	ТравмаНаПроизводстве = 
		Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ТравмаНаПроизводстве;
	ПрименятьЛьготы = 
		Объект.ПрименятьЛьготыПриНачисленииПособия;
	ЗачетНестраховыхПериодов = 
		Объект.ФинансированиеФедеральнымБюджетом =
			Перечисления.ОснованияФинансированияПособияФедеральнымБюджетом.ЗачетНестраховыхПериодов;
	
	Элементы.ГруппаСтажПроцентОплатыСтандартная.Видимость =
		НЕ ТравмаНаПроизводстве И НЕ ПрименятьЛьготы;
	Элементы.ГруппаСтажПроцентОплатыЛьготыРадиация.Видимость =
		НЕ ТравмаНаПроизводстве И ПрименятьЛьготы И НЕ ЗачетНестраховыхПериодов;
	Элементы.ГруппаСтажПроцентОплатыЛьготыСлужба.Видимость =
		НЕ ТравмаНаПроизводстве И ПрименятьЛьготы И ЗачетНестраховыхПериодов;
	Элементы.ГруппаСтажПроцентОплатыТравма.Видимость =
		ТравмаНаПроизводстве;
	
	Если СтажЛет = 0 Тогда
		Если Объект.СтажЛет = 0 Тогда
			Если Объект.СтажМесяцев < 6 Тогда
				СтажЛет = 0;
			Иначе
				СтажЛет = 1;
			КонецЕсли;
		ИначеЕсли Объект.СтажЛет < 5 Тогда
			СтажЛет = 1;
		ИначеЕсли Объект.СтажЛет < 8 Тогда
			СтажЛет = 5;
		Иначе
			СтажЛет = 8;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьТаблицуЗаболеваний(ДатаНачалаСобытия)
	
	ТаблицаЗаболеваний.Загрузить(Документы.БольничныйЛист.ПредставлениеПричиныЗаболевания(ДатаНачалаСобытия, Объект.СлучайУходаЗаБольнымРебенком));
	
	Если Объект.ПричинаНетрудоспособности <> Перечисления.ПричиныНетрудоспособности.ПоУходуЗаРебенком Тогда
		СлучайУходаЗаБольнымРебенком = Перечисления.СлучаиУходаЗаБольнымиДетьми.ПустаяСсылка();
	Иначе
		СлучайУходаЗаБольнымРебенком = Объект.СлучайУходаЗаБольнымРебенком;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура(
		"ПричинаНетрудоспособности, СлучайУходаЗаБольнымРебенком",
		Объект.ПричинаНетрудоспособности, СлучайУходаЗаБольнымРебенком);
	Если ЗначениеЗаполнено(Объект.КодПричиныНетрудоспособности) Тогда
		СтруктураПоиска.Вставить("КодПричиныНетрудоспособности", Объект.КодПричиныНетрудоспособности);
	КонецЕсли;
	МассивСтрок = ТаблицаЗаболеваний.НайтиСтроки(СтруктураПоиска);
	Если МассивСтрок.Количество() <> 0 Тогда
		ПредставлениеПричиныНетрудоспособности = МассивСтрок[0].Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОповещениеОбИзмененииПричиныЗаболевания(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатРедактирования) = Тип("Структура") Тогда
		
		Модифицированность = Истина;
		
		Объект.ПричинаНетрудоспособности    = РезультатРедактирования.ПричинаНетрудоспособности;
		Объект.СлучайУходаЗаБольнымРебенком = РезультатРедактирования.СлучайУходаЗаБольнымРебенком;
		Объект.КодПричиныНетрудоспособности = РезультатРедактирования.КодПричиныНетрудоспособности;
		
		ПредставлениеПричиныНетрудоспособности = РезультатРедактирования.Наименование;
		
		ПричинаНетрудоспособностиПриИзмененииНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваПолейОтметкиОНарушенииРежима(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.ДатаНарушенияРежима.Доступность = ЗначениеЗаполнено(Объект.КодНарушенияРежима);
	Если НЕ ЗначениеЗаполнено(Объект.КодНарушенияРежима) Тогда
		Форма.КодНарушенияРежима = Элементы.КодНарушенияРежима.СписокВыбора.Получить(0);
	Иначе
		Форма.КодНарушенияРежима = Объект.КодНарушенияРежима;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСтажИПересчитать()
	
	ЗаполнитьПроцентОплатыИОграничениеПособия();
	РассчитатьСреднийЗаработок();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСтаж()
	
	УстановитьЗначениеСтажа();
	Если Не Объект.ПричинаНетрудоспособности = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ТравмаНаПроизводстве")
		И Не Объект.ПрименятьЛьготыПриНачисленииПособия Тогда 
		Если СтажЛет =0 Тогда
			Объект.ОграничениеПособияБезЛьгот = ПредопределенноеЗначение("Перечисление.ВидыОграниченияПособия.ОграничениеВРазмереММОТ");
			Объект.ОграничениеПособия         = ПредопределенноеЗначение("Перечисление.ВидыОграниченияПособия.ОграничениеВРазмереММОТ");
		Иначе
			Объект.ОграничениеПособияБезЛьгот = ПредопределенноеЗначение("Перечисление.ВидыОграниченияПособия.ОбщееОграничение");
			Объект.ОграничениеПособия         = ПредопределенноеЗначение("Перечисление.ВидыОграниченияПособия.ОбщееОграничение");
		КонецЕсли;
	КонецЕсли;
	
	ОтобразитьРазмерОплаты();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначениеСтажа()
	
	Если СтажЛет =0 Тогда
		Объект.СтажЛет            = 0;
		Объект.СтажРасширенныйЛет = 0;
		Объект.СтажМесяцев            = ?(Объект.СтажМесяцев >= 6, 0, Объект.СтажМесяцев);
		Объект.СтажРасширенныйМесяцев = ?(Объект.СтажРасширенныйМесяцев >= 6, 0, Объект.СтажРасширенныйМесяцев);
	ИначеЕсли СтажЛет < 5 Тогда
		Объект.СтажЛет = ?((Объект.СтажЛет > 0 И Объект.СтажЛет < 5) ИЛИ (Объект.СтажЛет = 0 И Объект.СтажМесяцев >= 6), Объект.СтажЛет, 1);
		Объект.СтажРасширенныйЛет = ?((Объект.СтажРасширенныйЛет > 0 И Объект.СтажРасширенныйЛет < 5) ИЛИ (Объект.СтажРасширенныйЛет = 0 И Объект.СтажРасширенныйМесяцев >= 6), Объект.СтажРасширенныйЛет, 1);
	ИначеЕсли СтажЛет < 8 Тогда
		Объект.СтажЛет = ?(Объект.СтажЛет >= 5 И Объект.СтажЛет < 8, Объект.СтажЛет, 5);
		Объект.СтажРасширенныйЛет = ?(Объект.СтажРасширенныйЛет >= 5 И Объект.СтажРасширенныйЛет < 8, Объект.СтажРасширенныйЛет, 5);
	Иначе
		Объект.СтажЛет = ?(Объект.СтажЛет >= 8 , Объект.СтажЛет, 8);
		Объект.СтажРасширенныйЛет = ?(Объект.СтажРасширенныйЛет >= 8 , Объект.СтажРасширенныйЛет, 8);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРазмерОплаты()
	
	ВидимостьНадписиМРОТ = СтажЛет = 0;
	Если УчетПособийСоциальногоСтрахования.ДействуетПолнаяОплатаПоУходуЗаРебенкомДо8Лет(Объект.ДатаНачалаСобытия)
		И УчетПособийСоциальногоСтрахования.ЭтоПолностьюОплачиваемыйУходЗаРебенкомДо8Лет(Объект, Объект.СлучайУходаЗаБольнымРебенком) Тогда
		ВидимостьНадписиМРОТ = Ложь;
	КонецЕсли;
	
	Элементы.ДекорацияМРОТ.Видимость               = ВидимостьНадписиМРОТ;
	Элементы.ДекорацияМРОТЛьготыРадиация.Видимость = ВидимостьНадписиМРОТ;
	Элементы.ДекорацияМРОТЛьготыСлужба.Видимость   = ВидимостьНадписиМРОТ;
	
КонецПроцедуры

&НаСервере
Процедура ЗакрытьБаннер()
	
	// Закроем баннер на форме.
	Элементы.Баннер.Видимость = Ложь;
	
	ОтключенныеПредупреждения = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"НастройкиОтображенияБольничныхЛистов",
		"БольничныеЛистыНулевыеПериоды",
		Новый Массив);
		
	Если ОтключенныеПредупреждения.Найти(Объект.Ссылка) = Неопределено Тогда
		
		ОтключенныеПредупреждения.Добавить(Объект.Ссылка);
	
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
				"НастройкиОтображенияБольничныхЛистов",
				"БольничныеЛистыНулевыеПериоды",
				ОтключенныеПредупреждения);
	
		БаннерПогашен = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция КлючевыеРеквизитыЗаполнены()
	
	Если Не ЗначениеЗаполнено(Объект.ФизическоеЛицо)
		Или Не ЗначениеЗаполнено(Объект.ПричинаНетрудоспособности)
		Или Не ЗначениеЗаполнено(Объект.ДатаНачалаСобытия) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Необходимо заполнить обязательные для заполнения поля'"));
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СписокТабличныхЧастейДокумента()
	
	СписокТабличныхЧастей = Новый Массив;
	
	СписокТабличныхЧастей.Добавить("Начисления");
	СписокТабличныхЧастей.Добавить("НДФЛ");
	СписокТабличныхЧастей.Добавить("ПримененныеВычетыНаДетейИИмущественные");
	
	Возврат СписокТабличныхЧастей;
	
КонецФункции

&НаСервере
Функция ЕстьЗаполненныеТабличныеЧасти()
	
	ДанныеВТЧЕсть = Ложь;
	
	СписокТабличныхЧастей = СписокТабличныхЧастейДокумента();
	
	Для каждого ИмяТабличнойЧасти Из СписокТабличныхЧастей Цикл
		Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
			ДанныеВТЧЕсть = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат ДанныеВТЧЕсть;
	
КонецФункции

&НаСервере
Процедура УстановитьОтображениеПредупрежденийПриИзмененииКлючевыхРеквизитов()
	
	Если ЕстьЗаполненныеТабличныеЧасти() Тогда
		ОтображениеПредупреждения = ОтображениеПредупрежденияПриРедактировании.Отображать;
	Иначе
		ОтображениеПредупреждения = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	КонецЕсли;
	
	Элементы.ПланируемаяДатаВыплаты.ОтображениеПредупрежденияПриРедактировании = ОтображениеПредупреждения;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьГруппуПриостановкаТД()
	
	Если ДнейПриостановленияТД = 0 Тогда
		ДнейПриостановленияТДНадпись = НСтр("ru = 'Не зарегистрировано'");
	Иначе
		ДнейПриостановленияТДНадпись = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 день;;%1 дня;%1 дней;'"),
			ДнейПриостановленияТД);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоТекущаяФорма(Форма)
	Возврат Форма = ЭтотОбъект
		Или Форма.УникальныйИдентификатор = УникальныйИдентификатор;
КонецФункции

#Область ПилотныйПроектФСС

&НаКлиенте
Процедура ПослеВводаДанныхЛисткаНетрудоспособности(РезультатВыбора, ПустойПараметр) Экспорт
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		ОбновитьДанныеЛисткаНетрудоспособностиПослеРедактированияНаСервере(РезультатВыбора);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеЛисткаНетрудоспособностиПослеРедактированияНаСервере(РезультатВыбора)
	ЗаполнитьЗначенияСвойств(Объект, РезультатВыбора, ИменаРеквизитовЛисткаНетрудоспособности());
	
	Если РезультатВыбора.ЗаполнитьБольничный Тогда
		ЗаполнитьБольничныйПоДаннымЛисткаНетрудоспособности();
	КонецЕсли;
	
	НастроитьЭлементыДополненияПричиныНетрудоспособности();
	ОбновитьГиперссылкуВводаДанныхЛисткаНетрудоспособности();
	УстановитьСвойстваПоляПервичногоБольничногоЛиста(ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьБольничныйПоДаннымЛисткаНетрудоспособности()
	Объект.ПериодыУходаЗаРодственниками.Очистить();
	
	ОбменЛисткамиНетрудоспособностиФСС.ЗаполнитьПериодНетрудоспособности(Объект);
	ОбменЛисткамиНетрудоспособностиФСС.ЗаполнитьПричинуНетрудоспособностиИСлучайУходаЗаРебенком(Объект);
	
	Если СведенияОбЭЛН.ЭтоЭЛН И СведенияОбЭЛН.ДоступенИсходныйXML Тогда
		ДанныеЭЛН = ДанныеЭЛНДляЗаполнения();
		ОбменЛисткамиНетрудоспособностиФСС.ЗаполнитьРеквизитыШапкиПоУходу(Объект, ДанныеЭЛН);
		Документы.БольничныйЛист.ЗаполнитьТаблицуУходаЗаРодственниками(Объект, ДанныеЭЛН);
	КонецЕсли;
	
	ОбновитьФормуПослеЗагрузкиЭЛН();
КонецПроцедуры

&НаСервере
Функция ДанныеЭЛНДляЗаполнения()
	// Заполнение из ЭЛН.
	Если СведенияОбЭЛН.ЭтоЭЛН И СведенияОбЭЛН.ДоступенИсходныйXML Тогда
		ТекстXML = РегистрыСведений.СведенияОбЭЛН.ИсходныйXML(
			Объект.НомерЛисткаНетрудоспособности,
			Объект.ГоловнаяОрганизация);
		Если ЗначениеЗаполнено(ТекстXML) Тогда
			ДанныеЭЛН = ЭЛНФСС.ДанныеЭЛНИзXML(ТекстXML);
		КонецЕсли;
	Иначе
		ДанныеЭЛН = Неопределено;
	КонецЕсли;
	
	// Заполнение из бумажного ЛН.
	Если ДанныеЭЛН = Неопределено Тогда
		ДанныеЭЛН = Новый Структура;
		ДанныеЭЛН.Вставить("АктуальныйФормат", Ложь); // Отключается проверка заполнения даты рождения родственника.
		ПериодыЛечения = ОбменЛисткамиНетрудоспособностиФСС.СоздатьТаблицуПериодовЛеченияРодственников(ДанныеЭЛН);
		ЗаполнитьПериодыЛеченияПоДаннымБумажногоЛН(ПериодыЛечения);
	КонецЕсли;
	
	Возврат ДанныеЭЛН;
КонецФункции

&НаСервере
Процедура ЗаполнитьПериодыЛеченияПоДаннымБумажногоЛН(ПериодыЛечения)
	
	Если Не Перечисления.ПричиныНетрудоспособности.ЭтоУходЗаРодственником(Объект.ПричинаНетрудоспособности) Тогда
		Возврат;
	КонецЕсли;
	
	ПериодыИРежимыЛечения = ПериодыЛечения.СкопироватьКолонки("ДатаНачала, ДатаОкончания, РежимЛечения");
	
	РежимЛечения = Перечисления.РежимыЛечения.НайтиПоСлучаюУхода(Объект.СлучайУходаЗаБольнымРебенком);
	
	Если Не ЗначениеЗаполнено(Объект.КодПричиныНетрудоспособности) Тогда
		Объект.КодПричиныНетрудоспособности = Перечисления.ПричиныНетрудоспособности.КодПричины(
			Объект.ДатаНачалаСобытия,
			Объект.ПричинаНетрудоспособности,
			Объект.СлучайУходаЗаБольнымРебенком);
	КонецЕсли;
	
	Если РежимЛечения <> Перечисления.РежимыЛечения.Стационар
		И Объект.ДатаНачала <= Объект.ПериодНахожденияВСтационареСРебенкомС
		И Объект.ПериодНахожденияВСтационареСРебенкомС <= Объект.ДатаОкончания Тогда
		
		Если Объект.ДатаНачала < Объект.ПериодНахожденияВСтационареСРебенкомС Тогда
			ПериодИРежимЛечения = ПериодыИРежимыЛечения.Добавить();
			ПериодИРежимЛечения.ДатаНачала    = Объект.ДатаНачала;
			ПериодИРежимЛечения.ДатаОкончания = Объект.ПериодНахожденияВСтационареСРебенкомС - 86400;
			ПериодИРежимЛечения.РежимЛечения  = РежимЛечения;
		КонецЕсли;
		
		ДатаОкончанияСтационара = Мин(Объект.ПериодНахожденияВСтационареСРебенкомПо, Объект.ДатаОкончания);
		
		ПериодИРежимЛечения = ПериодыИРежимыЛечения.Добавить();
		ПериодИРежимЛечения.ДатаНачала    = Объект.ПериодНахожденияВСтационареСРебенкомС;
		ПериодИРежимЛечения.ДатаОкончания = ДатаОкончанияСтационара;
		ПериодИРежимЛечения.РежимЛечения  = Перечисления.РежимыЛечения.Стационар;
		
		Если ДатаОкончанияСтационара < Объект.ДатаОкончания Тогда
			ПериодИРежимЛечения = ПериодыИРежимыЛечения.Добавить();
			ПериодИРежимЛечения.ДатаНачала    = ДатаОкончанияСтационара + 86400;
			ПериодИРежимЛечения.ДатаОкончания = Объект.ДатаОкончания;
			ПериодИРежимЛечения.РежимЛечения  = РежимЛечения;
		КонецЕсли;
		
	Иначе
		
		ПериодИРежимЛечения = ПериодыИРежимыЛечения.Добавить();
		ПериодИРежимЛечения.ДатаНачала    = Объект.ДатаНачала;
		ПериодИРежимЛечения.ДатаОкончания = Объект.ДатаОкончания;
		ПериодИРежимЛечения.РежимЛечения  = РежимЛечения;
		
	КонецЕсли;
	
	Для НомерРодственника = 1 По 2 Цикл
		Номер = Формат(НомерРодственника, "ЧГ=");
		ФИО = Объект["ПоУходуФИО" + Номер];
		Если Не ЗначениеЗаполнено(ФИО) Тогда
			Продолжить;
		КонецЕсли;
		СтруктураФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИО);
		Для Каждого ПериодИРежимЛечения Из ПериодыИРежимыЛечения Цикл
			СтрокаТаблицы = ПериодыЛечения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ПериодИРежимЛечения, "ДатаНачала, ДатаОкончания, РежимЛечения");
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураФИО, "Фамилия, Имя, Отчество");
			СтрокаТаблицы.КодПричины        = Объект.КодПричиныНетрудоспособности;
			СтрокаТаблицы.СлучайУхода       = Объект.СлучайУходаЗаБольнымРебенком;
			СтрокаТаблицы.ФИО               = ФИО;
			СтрокаТаблицы.ВозрастЛет        = Объект["ПоУходуВозрастЛет"       + Номер];
			СтрокаТаблицы.ВозрастМесяцев    = Объект["ПоУходуВозрастМесяцев"   + Номер];
			СтрокаТаблицы.КодСвязи          = Объект["ПоУходуРодственнаяСвязь" + Номер];
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИменаРеквизитовЛисткаНетрудоспособности()
	Возврат
	"ПредоставленДубликатЛисткаНетрудоспособности,
	|МедицинскаяОрганизация,
	|НаименованиеМедицинскойОрганизации,
	|АдресМедицинскойОрганизации,
	|ОГРНМедицинскойОрганизации,
	|ДатаВыдачиЛисткаНетрудоспособности,
	|КодПричиныНетрудоспособности,
	|ДополнительныйКодПричиныНетрудоспособности,
	|ВторойКодПричиныНетрудоспособности,
	|НаименованиеОрганизацииВЛисткеНетрудоспособности,
	|ОсновноеМестоРаботы,
	|НомерЛисткаПоОсновномуМестуРаботы,
	|ДатаИзмененияКодаПричиныНетрудоспособности,
	|ДатаОкончанияПутевки,
	|НомерПутевки,
	|ОГРН_Санатория,
	|ПоУходуВозрастЛет1,
	|ПоУходуВозрастМесяцев1,
	|ПоУходуРодственнаяСвязь1,
	|ПоУходуФИО1,
	|ПоУходуИспользованоДней1,
	|ПоУходуВозрастЛет2,
	|ПоУходуВозрастМесяцев2,
	|ПоУходуРодственнаяСвязь2,
	|ПоУходуФИО2,
	|ПоУходуИспользованоДней2,
	|ПоставленаНаУчетВРанниеСрокиБеременности,
	|КодНарушенияРежима,
	|ДатаНарушенияРежима,
	|ПериодНахожденияВСтационареСРебенкомС,
	|ПериодНахожденияВСтационареСРебенкомПо,
	|ДатаНаправленияВБюроМСЭ,
	|ДатаРегистрацииДокументовМСЭ,
	|ДатаОсвидетельствованияМСЭ,
	|ГруппаИнвалидности,
	|УтратаТрудоспобности,
	|ПрерывистыйМетод,
	|ОсвобождениеДатаНачала1,
	|ОсвобождениеДатаОкончания1,
	|ОсвобождениеДолжностьВрача1,
	|ОсвобождениеФИОВрача1,
	|ОсвобождениеИдентификационныйНомерВрача1,
	|ОсвобождениеФИОВрачаПредседателяВК1,
	|ОсвобождениеДолжностьВрачаПредседателяВК1,
	|ОсвобождениеИдентификационныйНомерВрачаПредседателяВК1,
	|ОсвобождениеДатаНачала2,
	|ОсвобождениеДатаОкончания2,
	|ОсвобождениеДолжностьВрача2,
	|ОсвобождениеФИОВрача2,
	|ОсвобождениеИдентификационныйНомерВрача2,
	|ОсвобождениеФИОВрачаПредседателяВК2,
	|ОсвобождениеДолжностьВрачаПредседателяВК2,
	|ОсвобождениеИдентификационныйНомерВрачаПредседателяВК2,
	|ОсвобождениеДатаНачала3,
	|ОсвобождениеДатаОкончания3,
	|ОсвобождениеДолжностьВрача3,
	|ОсвобождениеФИОВрача3,
	|ОсвобождениеИдентификационныйНомерВрача3,
	|ОсвобождениеФИОВрачаПредседателяВК3,
	|ОсвобождениеДолжностьВрачаПредседателяВК3,
	|ОсвобождениеИдентификационныйНомерВрачаПредседателяВК3,
	|ПриступитьКРаботеС,
	|ДатаНовыйСтатусНетрудоспособного,
	|НовыйСтатусНетрудоспособного,
	|НомерЛисткаПродолжения,
	|УсловияИсчисленияКод1,
	|УсловияИсчисленияКод2,
	|УсловияИсчисленияКод3,
	|ДатаАктаН1,
	|ДатаНачалаРаботы";
КонецФункции

&НаСервере
Процедура ОбновитьГиперссылкуВводаДанныхЛисткаНетрудоспособности()
	
	Если ЗначениеЗаполнено(Объект.НомерЛисткаНетрудоспособности)
		И ЗначениеЗаполнено(Объект.ДатаВыдачиЛисткаНетрудоспособности) Тогда
		ПредставлениеЛисткаНетрудоспособности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Заполнены данные листка нетрудоспособности %1 от %2'"),
			Объект.НомерЛисткаНетрудоспособности,
			Формат(Объект.ДатаВыдачиЛисткаНетрудоспособности, "ДЛФ=D"));
	Иначе
		ПредставлениеЛисткаНетрудоспособности = НСтр("ru = 'Заполнить данные листка нетрудоспособности'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВыплатаДокумента

&НаКлиенте
Процедура ВопросПередВыплатойСледуетПровестиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		Записать();
	КонецЕсли;
	
	ДокументПроведен = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	Если ДокументПроведен Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		Оповестить("ВыполненаЗаписьДокумента", Новый Структура("ДокументСсылка", Объект.Ссылка));
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось провести документ'"));
		Возврат;
	КонецЕсли;
	
	СформироватьДокументыВыплаты();

КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокументыВыплаты()
	
	Если МожноСоздатьВедомостиПоРасчетномуДокументу(Объект.Ссылка) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",             Объект.Организация);
		ПараметрыФормы.Вставить("РасчетныйДокумент",       Объект.Ссылка);
		ПараметрыФормы.Вставить("РежимВыплатыПоДокументу", Истина);
		ПараметрыФормы.Вставить("ПериодСобытия",           Объект.ПериодРегистрации);
		ПараметрыФормы.Вставить("ФизическоеЛицо",          Объект.ФизическоеЛицо);
		ПараметрыФормы.Вставить("ДатаВыплаты",             Объект.ПланируемаяДатаВыплаты);
		
		ОткрытьФорму("Обработка.ПомощникУчетаЗарплаты.Форма.Форма", ПараметрыФормы, Объект.Ссылка); 
	КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент)
	
	МожноСоздатьВедомости = Истина;
	ВзаиморасчетыССотрудникамиПереопределяемый.МожноСоздатьВедомостиПоРасчетномуДокументу(РасчетныйДокумент, МожноСоздатьВедомости);
	Возврат МожноСоздатьВедомости;
	
КонецФункции

#КонецОбласти
