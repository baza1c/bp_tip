
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Параметры.АдресПараметровВХранилище) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	// "Распаковываем" параметры
	ПараметрыРасчета = ПолучитьИзВременногоХранилища(Параметры.АдресПараметровВХранилище);
	
	ДесериализованныйОбъект = ЗарплатаКадры.ДесериализоватьОбъектИзДвоичныхДанных(ПараметрыРасчета.СериализованныйОбъект);
	ЗначениеВДанныеФормы(ДесериализованныйОбъект, Объект);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРасчета);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Объект, , "СреднийЗаработокФСС");
	
	ТаблицаНачислений = Объект.СреднийЗаработокФСС.Выгрузить();
	
	ПрименятьРайонныйКоэффициент = ПолучитьФункциональнуюОпцию("ПрименятьРайонныйКоэффициент",
		Новый Структура("Организация", Организация));
	ПрименятьСевернуюНадбавку = ПолучитьФункциональнуюОпцию("ПрименятьСевернуюНадбавку",
		Новый Структура("Организация", Организация));
		
	Элементы.Год1Месяц1.Видимость            = Не (ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку);
	Элементы.Год1Месяц1Гиперссылка.Видимость = ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку;
	Элементы.Год2Месяц1.Видимость            = Не (ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку);
	Элементы.Год2Месяц1Гиперссылка.Видимость = ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку;
	
	// Настройка параметров редактирования таблицы
	Если ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку Тогда
		Элементы.ТаблицаЗаработкаГод1Месяц.ТолькоПросмотр    = Истина;
		Элементы.ТаблицаЗаработкаГод1Месяц.ГиперссылкаЯчейки = Истина;
		Элементы.ТаблицаЗаработкаГод1Месяц.ЦветТекста        = ЦветаСтиля.ЦветГиперссылки;
		Элементы.ТаблицаЗаработкаГод2Месяц.ТолькоПросмотр    = Истина;
		Элементы.ТаблицаЗаработкаГод2Месяц.ГиперссылкаЯчейки = Истина;
		Элементы.ТаблицаЗаработкаГод2Месяц.ЦветТекста        = ЦветаСтиля.ЦветГиперссылки;
	Иначе
		ТаблицаНачислений.ЗаполнитьЗначения(ПланыВидовРасчета.Начисления.ПустаяСсылка(), "Начисление");
		ТаблицаНачислений.Свернуть("ФизическоеЛицо,Период,Начисление", "Сумма");
	КонецЕсли;
	РежимПодробногоВводаСреднегоЗаработка =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиПользователя",
															"РежимПодробногоВводаСреднегоЗаработкаФСС");
	
	РежимПодробногоВводаСреднегоЗаработкаЧисло = РежимПодробногоВводаСреднегоЗаработка;
	
	Если Параметры.ЯвляетсяПродолжениемБолезни И НЕ ТолькоПросмотр Тогда
		// В режиме продолжения редактировать параметры расчета нельзя
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПериодВариантВвода",
			"ТолькоПросмотр",
			Истина);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДанныеОЗаработкеГруппаГод",
			"ТолькоПросмотр",
			Истина);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДанныеОЗаработкеГруппаТаблица",
			"ТолькоПросмотр",
			Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СреднийЗаработокГруппа",
			"ТолькоПросмотр",
			Истина);
		
	КонецЕсли;
	
	Если ПричинаНетрудоспособности <> Перечисления.ПричиныНетрудоспособности.ПоБеременностиИРодам Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Год1ДниУхода1",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Год2ДниУхода1",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДекорацияДни1",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ДекорацияДни2",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ТаблицаЗаработкаГруппа1",
			"ОтображатьВШапке",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ТаблицаЗаработкаГруппа2",
			"ОтображатьВШапке",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ТаблицаЗаработкаГод1ДниУхода",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ТаблицаЗаработкаГод2ДниУхода",
			"Видимость",
			Ложь);
			
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПеречитатьДанныеУчета",
		"Видимость",
		Не ТолькоПросмотр);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НачисленияРезультатВТомЧислеЗаСчетФБ",
		"Видимость",
		Объект.ПрименятьЛьготыПриНачисленииПособия);
		
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СреднийЗаработокМРОТДекорация",
		"Видимость",
		Объект.ОграничениеПособия = Перечисления.ВидыОграниченияПособия.ОграничениеВРазмереММОТ
		ИЛИ Объект.ОграничениеПособияБезЛьгот = Перечисления.ВидыОграниченияПособия.ОграничениеВРазмереММОТ
		ИЛИ ЗначениеЗаполнено(Объект.ДатаНарушенияРежима)
		ИЛИ Объект.СреднийЗаработокФСС = 0);
		

	ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо");
	
	ПодготовитьТаблицу();
	НастроитьМаксимальноеКоличествоДнейВФеврале(ЭтаФорма);
	
	ОбновитьЗаголовкиТаблицы(ЭтотОбъект);
	
	СреднийЗаработокФСС.Загрузить(ТаблицаНачислений);
	ЗагрузитьСреднийЗаработокФСС();
	
	ЗафиксироватьПоказателиСреднегоЗаработка();
	
	УстановитьПодсказкуКРасчетуСреднегоЗаработка();
	
	УстановитьЗаголовокФормы();
	
	Элементы.ГруппаКнопокПросмотр.Видимость       = ТолькоПросмотр;
	Элементы.ГруппаКнопокРедактирование.Видимость = НЕ ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ФормаОК.КнопкаПоУмолчанию      = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИдентификаторВладельца = ВладелецФормы.УникальныйИдентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		ВопросСохранитьИзменения();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Год1ПриИзменении(Элемент)
	
	ПриИзмененииРасчетногоГода(0);
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Регулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод = Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод + Направление;
	СтандартнаяОбработка = Ложь;
	Модифицированность = Истина;
	
	ПриИзмененииРасчетногоГода(0);
	
КонецПроцедуры

&НаКлиенте
Процедура Год2ПриИзменении(Элемент)
	
	ПриИзмененииРасчетногоГода(1);
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Регулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Объект.ПериодРасчетаСреднегоЗаработкаВторойГод = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод + Направление;
	СтандартнаяОбработка = Ложь;
	Модифицированность = Истина;
	
	ПриИзмененииРасчетногоГода(1);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимПодробногоВводаСреднегоЗаработкаЧислоПриИзменении(Элемент)
	
	РежимПодробногоВводаСреднегоЗаработка = РежимПодробногоВводаСреднегоЗаработкаЧисло;
	РежимПодробногоВводаСреднегоЗаработкаЧислоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ГодМесяцПриИзменении(Элемент)
	
	Если Элемент.Имя = "Год1Месяц1" Тогда
		Год   = Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод;
		Сумма = ТаблицаЗаработка[0].Год1Месяц;
	Иначе
		Год   = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод;
		Сумма = ТаблицаЗаработка[0].Год2Месяц;
	КонецЕсли;
	
	ТаблицаЗаработкаГодМесяцПриИзменении(Год, 1, Сумма);
	
	Пересчитать();
	
КонецПроцедуры

&НаКлиенте
Процедура Год1Месяц1ГиперссылкаНажатие(Элемент, СтандартнаяОбработка)
	
	Если Не РежимПодробногоВводаСреднегоЗаработка Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьФормуРедактированияСреднегоПоГоду(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Год2Месяц1ГиперссылкаНажатие(Элемент, СтандартнаяОбработка)
	
	Если Не РежимПодробногоВводаСреднегоЗаработка Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьФормуРедактированияСреднегоПоГоду(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТаблицаЗаработка

&НаКлиенте
Процедура ТаблицаЗаработкаГод1МесяцПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ТаблицаЗаработка.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗаработкаГодМесяцПриИзменении(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод,
		СтрокаТаблицы.НомерМесяца, СтрокаТаблицы.Год1Месяц);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаработкаГод2МесяцПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ТаблицаЗаработка.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗаработкаГодМесяцПриИзменении(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод,
		СтрокаТаблицы.НомерМесяца, СтрокаТаблицы.Год2Месяц);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаработкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элементы.ТаблицаЗаработка.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ТаблицаЗаработкаГод1Месяц" Или Поле.Имя = "ТаблицаЗаработкаГод2Месяц" Тогда
		Если ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку Тогда
			Если Поле.Имя = "ТаблицаЗаработкаГод1Месяц" Тогда
				Год   = Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод;
			Иначе
				Год   = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод;
			КонецЕсли;
			Год = ?(Год = 0, 1, Год);
			Период = Дата(Год, СтрокаТаблицы.НомерМесяца, 1);
			ОтредактироватьДанныеДляСреднегоЗаработка(Период);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаработкаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Пересчитать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура OK(Команда)
	
	Если НЕ Модифицированность ИЛИ ТолькоПросмотр Тогда
		Закрыть();
	Иначе
		ПодготовитьФормуКПринятиюИзменений();
		Закрыть(АдресВХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьДанныеУчета(Команда)
	
	ДанныеЗаполнены = Ложь;
	Для НомерГода = 1 По 2 Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			ИндексМесяца = НомерМесяца - 1;
			
			Заработок = ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "Месяц"];
				
			ДнейБолезниУходаЗаДетьми = ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "ДниУхода"];
				
			Если Заработок <> 0 ИЛИ ДнейБолезниУходаЗаДетьми <> 0 Тогда
				ДанныеЗаполнены = Истина;
				Прервать;
			КонецЕсли; 
				
		КонецЦикла;
		
		Если ДанныеЗаполнены Тогда
			Прервать;
		КонецЕсли; 
				
	КонецЦикла;
	
	Если ДанныеЗаполнены Тогда
		
		ТекстВопроса = НСтр("ru='Перед заполнением введенные данные будут очищены.
			|Продолжить?'");
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеречитатьДанныеУчетаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		ПеречитатьДанныеУчетаЗавершение(КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РедактированиеЗаработка

&НаКлиенте
Процедура ТаблицаЗаработкаГодМесяцПриИзменении(Год, Месяц, Сумма)
	
	Год = ?(Год = 0, 1, Год);
	
	ДанныеНачислений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Новый Структура("Сумма", Сумма));
	ПерезаполнитьИПересчитать(Дата(Год, Месяц, 1), ДанныеНачислений);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФормуРедактированияСреднегоПоГоду(ГодРедактирования)
	
	СтрокаТаблицы = ТаблицаЗаработка[0];
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Год = ?(ГодРедактирования = 0, 1, ГодРедактирования);
	Период = Дата(Год, СтрокаТаблицы.НомерМесяца, 1);
	ОтредактироватьДанныеДляСреднегоЗаработка(Период);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтредактироватьДанныеДляСреднегоЗаработка(Период)
	
	ПараметрыОткрытия = Новый Структура("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыОткрытия.Вставить("ДанныеНачислений", ДанныеНачислений(Период));
	
	ДополнительныеПараметры = Новый Структура("Период", Период);
	Оповещение = Новый ОписаниеОповещения("ПриИзмененииСреднегоЗаработка", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.ВводДанныхДляРасчетаСреднегоПодробно", ПараметрыОткрытия, ЭтотОбъект,
		Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ДанныеНачислений(Период)
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(
			СреднийЗаработокФСС.Выгрузить(Новый Структура("Период", Период)));
КонецФункции

&НаКлиенте
Процедура ПриИзмененииСреднегоЗаработка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Модифицированность = Истина;
		ПерезаполнитьИПересчитать(ДополнительныеПараметры.Период, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьИПересчитать(Месяц, ДанныеНачислений)
	
	ЗаменитьДанныеНачислений(Месяц, ДанныеНачислений);
	ЗагрузитьСреднийЗаработокФСС();
	Пересчитать();
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьДанныеНачислений(Период, ДанныеНачислений);
	НайденныеСтроки = СреднийЗаработокФСС.НайтиСтроки(Новый Структура("Период", Период));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		СреднийЗаработокФСС.Удалить(НайденнаяСтрока);
	КонецЦикла;
	Для Каждого СтрокаНачислений Из ДанныеНачислений Цикл
		НоваяСтрока = СреднийЗаработокФСС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачислений);
		НоваяСтрока.Период = Период;
		НоваяСтрока.ФизическоеЛицо = ФизическоеЛицо;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура Пересчитать()
	
	ЗафиксироватьПоказателиСреднегоЗаработка();
	УстановитьПодсказкуКРасчетуСреднегоЗаработка();
	ПодсчитатьИтог(ТаблицаЗаработка, ИтогГод1, ИтогГод2);
	Документы.БольничныйЛист.РассчитатьНачисления(Объект, ДополнительныеПараметрыРасчета());
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьДанныеУчетаЗавершение(Знач Результат, Знач Параметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоДаннымУчета();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуКПринятиюИзменений()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"НастройкиПользователя", "РежимПодробногоВводаСреднегоЗаработкаФСС", РежимПодробногоВводаСреднегоЗаработка);
	
	СохранитьДанныеРасчетаСреднего();
	
	АдресВХранилище = Документы.БольничныйЛист.АдресПараметровВХранилище(РеквизитФормыВЗначение("Объект"), ДополнительныеПараметрыРасчета());
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ДополнительныеПараметрыРасчета()
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодразделениеСотрудника",      ПодразделениеСотрудника);
	ДополнительныеПараметры.Вставить("ВидОплатыЗаСчетРаботодателя",  ВидОплатыЗаСчетРаботодателя);
	ДополнительныеПараметры.Вставить("ВидОплатыПособия",             ВидОплатыПособия);
	ДополнительныеПараметры.Вставить("ВидНеоплачиваемогоВремени",    ВидНеоплачиваемогоВремени);
	ДополнительныеПараметры.Вставить("ИспользоватьПрямыеВыплатыФСС", ИспользоватьПрямыеВыплатыФСС);
	ДополнительныеПараметры.Вставить("УникальныйИдентификатор",      ИдентификаторВладельца);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

&НаСервере
Процедура СохранитьДанныеРасчетаСреднего()
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод);
	РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод);
	
	НомерГода = 1;
	Объект.СреднийЗаработокФСС.Очистить();
	Объект.ОтработанноеВремяДляСреднегоФСС.Очистить();
	Для каждого Год Из РасчетныеГоды Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			ИндексМесяца = НомерМесяца - 1;
			
			Заработок = ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "Месяц"];
			ДнейБолезниУходаЗаДетьми = ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "ДниУхода"];
				
			Если ДнейБолезниУходаЗаДетьми <> 0 Тогда
				ЗаписьОВремени = Объект.ОтработанноеВремяДляСреднегоФСС.Добавить();
				ЗаписьОВремени.ФизическоеЛицо           = ФизическоеЛицо;
				ЗаписьОВремени.Период                   = Дата(Год, НомерМесяца, 1);
				ЗаписьОВремени.ДнейБолезниУходаЗаДетьми = ДнейБолезниУходаЗаДетьми;
			КонецЕсли;
			
		КонецЦикла;
		
		НомерГода = НомерГода + 1;
		
	КонецЦикла;
	
	Для Каждого СтрокаСреднего Из СреднийЗаработокФСС Цикл
		СтрокаСреднего.ФизическоеЛицо = ФизическоеЛицо;
	КонецЦикла;
	Объект.СреднийЗаработокФСС.Загрузить(СреднийЗаработокФСС.Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДаннымУчета()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		Для НомерГода = 1 По 2 Цикл
			
			Для НомерМесяца = 1 По 12 Цикл
				
				ИндексМесяца = НомерМесяца - 1;
				
				ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "Месяц"]    = 0;
				ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "ДниУхода"] = 0;
				
			КонецЦикла;
			
		КонецЦикла;
		
		РасчетныеГоды = Новый Массив;
		РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод);
		РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод);
		
		ДанныеОЗаработке = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеОЗаработкеДляРасчетаСреднегоФСС(
			Сотрудник, Организация, ДатаНачалаСобытия, РасчетныеГоды);
			
		СреднийЗаработокФСС.Очистить();
		Для каждого СведенияОГоде Из ДанныеОЗаработке Цикл
				
			ИндексГода = РасчетныеГоды.Найти(СведенияОГоде.Ключ);
			Если ИндексГода <> Неопределено Тогда
				
				НомерГода = ИндексГода + 1;
				Для Каждого СведенияМесяца Из СведенияОГоде.Значение Цикл
					
					НомерМесяца  = СведенияМесяца.Ключ;
					ИндексМесяца = НомерМесяца - 1;
					
					СуммаЗаработка = 0;
					Для Каждого СтрокаЗаработка Из СведенияМесяца.Значение.Заработок Цикл
						ЗаполнитьЗначенияСвойств(СреднийЗаработокФСС.Добавить(), СтрокаЗаработка);
						Если СтрокаЗаработка.Сумма <> 0 Тогда
							СуммаЗаработка = СуммаЗаработка + СтрокаЗаработка.Сумма;
						КонецЕсли;
					КонецЦикла;
					Если СуммаЗаработка <> 0 Тогда
						ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "Месяц"] = СуммаЗаработка;
					КонецЕсли;
						
					Если СведенияМесяца.Значение.ДнейБолезниУходаЗаДетьми <> 0 Тогда
						ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "ДниУхода"] = 
							СведенияМесяца.Значение.ДнейБолезниУходаЗаДетьми;
					КонецЕсли; 
						
				КонецЦикла;
				
			КонецЕсли; 
			
		КонецЦикла;
		
		ЗафиксироватьПоказателиСреднегоЗаработка();
		
		УстановитьПодсказкуКРасчетуСреднегоЗаработка();
		
		ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
			ЭтаФорма,
			"ГруппаПериод",
			"");
			
		СвернутьДанныеДляКраткойФормы();
		
		Пересчитать();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРасчетаСреднегоДневногоЗаработкаФСС()
	
	ПараметрыРасчета = УчетПособийСоциальногоСтрахования.ПараметрыРасчетаСреднегоДневногоЗаработкаФССПоДокументу(Объект);
	
	ПараметрыРасчета.ИспользоватьДниБолезниУходаЗаДетьми = Документы.БольничныйЛист.ИспользоватьДниБолезниУходаЗаДетьми(Объект);
	ПараметрыРасчета.УчитыватьДниПриостановленияТД       = Документы.БольничныйЛист.УчитыватьДниПриостановленияТД(Объект);
	ПараметрыРасчета.РайонныйКоэффициентРФ               = Объект.РайонныйКоэффициентРФНаНачалоСобытия;
	ПараметрыРасчета.ПорядокРасчета                      = УчетПособийСоциальногоСтрахованияКлиентСервер.ПорядокРасчетаСреднегоЗаработкаФСС(Объект.ДатаНачалаСобытия);
	ПараметрыРасчета.ПрименятьПредельнуюВеличину         = Не ЭтоНСПЗ();
	
	ПараметрыРасчета.ДанныеНачислений                 = ДанныеОЗаработкеДляРасчетаСреднегоЗаработка();
	ПараметрыРасчета.ДанныеВремени                    = ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка();
	ПараметрыРасчета.ДанныеСтрахователей              = УчетПособийСоциальногоСтрахования.ПустаяТаблицаДанныеСтрахователейСреднийЗаработокФСС();
	ПараметрыРасчета.ПриостановленияТрудовыхДоговоров = Объект.ПриостановленияТрудовыхДоговоров;
	
	Возврат ПараметрыРасчета;
	
КонецФункции

&НаСервере
Функция ЭтоНСПЗ()
	Возврат Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.ТравмаНаПроизводстве
		Или Объект.ПричинаНетрудоспособности = Перечисления.ПричиныНетрудоспособности.Профзаболевание;
КонецФункции

&НаСервере
Функция ДанныеОЗаработкеДляРасчетаСреднегоЗаработка()
	
	ДанныеОЗаработкеДляРасчетаСреднегоЗаработка =
		УчетПособийСоциальногоСтрахования.ПустаяТаблицаНачисленийСреднийЗаработокФСС();
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод);
	РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод);
	
	ДнейБолезниУходаЗаДетьми = 0;
	
	НомерГода = 1;
	Для каждого Год Из РасчетныеГоды Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			Период = Дата(Год, НомерМесяца, 1);
			
			СтрокиЗаработка = СреднийЗаработокФСС.НайтиСтроки(Новый Структура("Период", Период));
			
			Для Каждого СтрокаЗаработка Из СтрокиЗаработка Цикл
				
				СтрокаДанныхОЗаработке = ДанныеОЗаработкеДляРасчетаСреднегоЗаработка.Добавить();
				СтрокаДанныхОЗаработке.ФизическоеЛицо = ФизическоеЛицо;
				СтрокаДанныхОЗаработке.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2011;
				СтрокаДанныхОЗаработке.Период = Период;
				СтрокаДанныхОЗаработке.Сумма = СтрокаЗаработка.Сумма;
				СтрокаДанныхОЗаработке.Начисление = СтрокаЗаработка.Начисление;
				
			КонецЦикла;
			
		КонецЦикла;
		
		НомерГода = НомерГода + 1;
		
	КонецЦикла;
	
	Возврат ДанныеОЗаработкеДляРасчетаСреднегоЗаработка;
	
КонецФункции

&НаСервере
Функция ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка()
	
	ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка =
		УчетПособийСоциальногоСтрахования.ПустаяТаблицаОтработанноеВремяСреднийЗаработокФСС();
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод);
	РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод);
	
	НомерГода = 1;
	Для каждого Год Из РасчетныеГоды Цикл
		
		Для НомерМесяца = 1 По 12 Цикл
			
			ИндексМесяца = НомерМесяца - 1;
			Период = Дата(Год, НомерМесяца, 1);
			
			СтрокаДанныхОбОтработанномВремени = ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка.Добавить();
			СтрокаДанныхОбОтработанномВремени.ФизическоеЛицо = ФизическоеЛицо;
			СтрокаДанныхОбОтработанномВремени.Период = Период;
			
			ДнейБолезниУходаЗаДетьми = ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "ДниУхода"];
			СтрокаДанныхОбОтработанномВремени.ДнейБолезниУходаЗаДетьми = ДнейБолезниУходаЗаДетьми;
			
		КонецЦикла;
		
		НомерГода = НомерГода + 1;
		
	КонецЦикла;
	
	Возврат ДанныеОбОтработанномВремениДляРасчетаСреднегоЗаработка;
	
КонецФункции

&НаСервере
Процедура УстановитьПодсказкуКРасчетуСреднегоЗаработка()
	
	РасчетныеГоды = Новый Массив;
	РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод);
	РасчетныеГоды.Добавить(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод);
	
	Итого     = ТаблицаЗаработка.Итог("Год1Месяц") + ТаблицаЗаработка.Итог("Год2Месяц");
	ИтогоДней = ТаблицаЗаработка.Итог("Год1ДниУхода") + ТаблицаЗаработка.Итог("Год2ДниУхода");
	
	ПараметрыРасчета = ПараметрыРасчетаСреднегоДневногоЗаработкаФСС();
	ДанныеРасчетаСреднего =
		УчетПособийСоциальногоСтрахованияКлиентСервер.ДанныеРасчетаСреднегоЗаработкаФСС(ПараметрыРасчета);
	КалендарныхДней =
		УчетПособийСоциальногоСтрахованияКлиентСервер.УчитываемыхДнейВКалендарныхГодахФСС(ПараметрыРасчета,
			ДанныеРасчетаСреднего);
	
	СреднедневнойЗаработок = ?(КалендарныхДней = 0, 0, Итого/ КалендарныхДней);
	
	ТекстПодсказки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Средний заработок: %1 / %2 = %3'"),
		Итого,
		КалендарныхДней,
		Формат(СреднедневнойЗаработок, "ЧДЦ=2; ЧН=0,00"));
	
	МасивСтрок = Новый Массив;
	МасивСтрок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Средний заработок: %1 / %2 = '"),
		Итого,
		КалендарныхДней));
	МасивСтрок.Добавить(
		Новый ФорматированнаяСтрока(Формат(СреднедневнойЗаработок, "ЧДЦ=2; ЧН=0,00"),
		Новый Шрифт(,, Истина)));
		
	ТекстРасшифровкиСреднедневной = Новый ФорматированнаяСтрока(МасивСтрок);
	
	МРОТ = ЗарплатаКадры.МинимальныйРазмерОплатыТрудаРФ(ДатаНачалаСобытия);
	
	ТекстРасшифровки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='МРОТ на %1: %2'"),
			Формат(ДатаНачалаСобытия, "ДЛФ=D"),
			Формат(МРОТ, "ЧДЦ=2"));
	
	Если РайонныйКоэффициентРФНаНачалоСобытия > 1 Тогда
		
		ТекстРасшифровки = ТекстРасшифровки +
			Символы.ПС +
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Районный коэффициент: %1'"),
						Формат(РайонныйКоэффициентРФНаНачалоСобытия, "ЧДЦ=2"));
		
		МРОТ = МРОТ * РайонныйКоэффициентРФНаНачалоСобытия;
		
	КонецЕсли;
	
	Если КалендарныхДней = 0 Тогда
		СреднийМРОТ = 0;
	Иначе
		СреднийМРОТ = МРОТ * 24 / КалендарныхДней;
	КонецЕсли;
	
	ТекстРасшифровки = ТекстРасшифровки +
		Символы.ПС +
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Минимальный средний заработок исходя из МРОТ: %1'"),
					Формат(СреднийМРОТ, "ЧДЦ=2"));
		
	Элементы.СреднийЗаработокМРОТДекорация.Заголовок = ТекстРасшифровки;
	Элементы.СреднийЗаработокРасшифровкиСреднедневной.Заголовок = ТекстРасшифровкиСреднедневной;
	
	// Заголовок группы
	Если СреднедневнойЗаработок = 0 И СреднийМРОТ = 0 Тогда
		ЗаголовокСвернутогоОтображения = НСтр("ru='<нет данных для расчета>'");
	Иначе
		ЗаголовокСвернутогоОтображения = СтрШаблон(НСтр("ru='Средний заработок: %1; минимальный (из МРОТ): %2'"),
			Формат(СреднедневнойЗаработок, "ЧДЦ=2; ЧН=0,00"), Формат(СреднийМРОТ, "ЧДЦ=2; ЧН=0,00"));
	КонецЕсли;
	Элементы.ГруппаСреднийЗаработок.ЗаголовокСвернутогоОтображения = ЗаголовокСвернутогоОтображения;
		
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСреднийЗаработокФСС()
	
	ДопустимРежимКраткогоВвода = Истина;
	ТаблицаДанных = СреднийЗаработокФСС.Выгрузить();
	ТаблицаДанных.Свернуть("Период", "Сумма");
	
	Для Каждого СтрокаСреднегоЗаработкаФСС  Из ТаблицаДанных Цикл
		
		Год = Год(СтрокаСреднегоЗаработкаФСС.Период);
		Если Год = Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод Тогда
			НомерГода = 1;
		ИначеЕсли Год = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод Тогда
			НомерГода = 2;
		Иначе
			НомерГода = Неопределено;
		КонецЕсли;
			
		Если НомерГода <> Неопределено Тогда
			
			НомерМесяца  = Месяц(СтрокаСреднегоЗаработкаФСС.Период);
			ИндексМесяца = НомерМесяца - 1;
			
			Если НомерМесяца <> 1 И СтрокаСреднегоЗаработкаФСС.Сумма <> 0 Тогда
				ДопустимРежимКраткогоВвода = Ложь;
			КонецЕсли;
			
			ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "Месяц"] = СтрокаСреднегоЗаработкаФСС.Сумма;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаОтработанногоВремени Из Объект.ОтработанноеВремяДляСреднегоФСС Цикл
		
		Год = Год(СтрокаОтработанногоВремени.Период);
		Если Год = Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод Тогда
			НомерГода = 1;
		ИначеЕсли Год = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод Тогда
			НомерГода = 2;
		Иначе
			НомерГода = Неопределено;
		КонецЕсли;
			
		Если НомерГода <> Неопределено Тогда
			
			НомерМесяца = Месяц(СтрокаОтработанногоВремени.Период);
			ИндексМесяца = НомерМесяца - 1;
			
			Если НомерМесяца <> 1 И СтрокаОтработанногоВремени.ДнейБолезниУходаЗаДетьми <> 0 Тогда
				ДопустимРежимКраткогоВвода = Ложь;
			КонецЕсли;
			
			ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "ДниУхода"] =
				СтрокаОтработанногоВремени.ДнейБолезниУходаЗаДетьми;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не РежимПодробногоВводаСреднегоЗаработка Тогда
		// Вариант, когда нет заработка по году
		Если ТаблицаДанных.Найти(Дата(Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод, 1, 1), "Период") = Неопределено Тогда
			ТаблицаЗаработка[0]["Год1Месяц"]    = 0;
			ТаблицаЗаработка[0]["Год1ДниУхода"] = 0;
		КонецЕсли;
		Если ТаблицаДанных.Найти(Дата(Объект.ПериодРасчетаСреднегоЗаработкаВторойГод, 1, 1), "Период") = Неопределено Тогда
			ТаблицаЗаработка[0]["Год2Месяц"]    = 0;
			ТаблицаЗаработка[0]["Год2ДниУхода"] = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ДопустимРежимКраткогоВвода Тогда
		РежимПодробногоВводаСреднегоЗаработка = Истина;
		РежимПодробногоВводаСреднегоЗаработкаЧисло = 1;
	КонецЕсли;
	
	ПодсчитатьИтог(ТаблицаЗаработка, ИтогГод1, ИтогГод2);
	УстановитьОтображениеКраткойПодробнойФормыВвода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРасчетногоГода(ИндексГода)
	
	МаксимальноВозможныйГод = Год(ДатаНачалаСобытия) - 1;
	Если ИндексГода = 0 Тогда
		
		Если Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод >= Объект.ПериодРасчетаСреднегоЗаработкаВторойГод Тогда
			
			Объект.ПериодРасчетаСреднегоЗаработкаВторойГод =
				Мин(МаксимальноВозможныйГод, Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод + 1);
			
			Если Объект.ПериодРасчетаСреднегоЗаработкаВторойГод = МаксимальноВозможныйГод Тогда
				Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод - 1;
			КонецЕсли;
			
		КонецЕсли; 
		
	Иначе
		
		Если Объект.ПериодРасчетаСреднегоЗаработкаВторойГод > МаксимальноВозможныйГод Тогда
			Объект.ПериодРасчетаСреднегоЗаработкаВторойГод = МаксимальноВозможныйГод;
		КонецЕсли;
		
		Если Объект.ПериодРасчетаСреднегоЗаработкаВторойГод <= Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод Тогда
			
			Если Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод = 1 Тогда
				Объект.ПериодРасчетаСреднегоЗаработкаВторойГод = 2;
			Иначе
				Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод - 1;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НастроитьМаксимальноеКоличествоДнейВФеврале(ЭтотОбъект);
	
	ОбновитьЗаголовкиТаблицы(ЭтотОбъект);
	
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		ЭтаФорма,
		"ГруппаПериод",
		НСтр("ru='После изменения расчетного периода, необходимо перезаполнить данные'"));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗаголовкиТаблицы(Форма)
	
	Форма.Элементы.ТаблицаЗаработкаГруппа1.Заголовок = СтрШаблон(НСтр("ru='%1 год'"),
		Строка(Формат(Форма.Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод, "ЧГ=")));
	Форма.Элементы.ТаблицаЗаработкаГруппа1.Заголовок = СтрШаблон(НСтр("ru='%1 год'"),
		Строка(Формат(Форма.Объект.ПериодРасчетаСреднегоЗаработкаВторойГод, "ЧГ=")));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ТаблицаЗаработкаГруппа1",
		"Заголовок",
		СтрШаблон(НСтр("ru='%1 год'"), Строка(Формат(Форма.Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод, "ЧГ="))));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ТаблицаЗаработкаГруппа2",
		"Заголовок",
		СтрШаблон(НСтр("ru='%1 год'"), Строка(Формат(Форма.Объект.ПериодРасчетаСреднегоЗаработкаВторойГод, "ЧГ="))));
		
	Если Форма.ПричинаНетрудоспособности <>
			ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ПоБеременностиИРодам") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"ТаблицаЗаработкаГод1Месяц",
			"Заголовок",
			СтрШаблон(НСтр("ru='Заработок (%1)'"),
				Строка(Формат(Форма.Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод, "ЧГ="))));
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"ТаблицаЗаработкаГод2Месяц",
			"Заголовок",
			СтрШаблон(НСтр("ru='Заработок (%1)'"),
				Строка(Формат(Форма.Объект.ПериодРасчетаСреднегоЗаработкаВторойГод, "ЧГ="))));
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Заголовок = СтрШаблон(НСтр("ru='Расчет больничного (%1)%2'"),
							Сотрудник,
							?(ТолькоПросмотр, НСтр("ru=' (только просмотр)'"), ""));
	
КонецПроцедуры

&НаСервере
Процедура ЗафиксироватьПоказателиСреднегоЗаработка()
	
	ПараметрыРасчета = ПараметрыРасчетаСреднегоДневногоЗаработкаФСС();
	СреднийЗаработок = УчетПособийСоциальногоСтрахования.СреднийЗаработокФСС(ПараметрыРасчета);
	Объект.СреднийДневнойЗаработок = СреднийЗаработок.Общий;
	Объект.СреднийДневнойЗаработокРК = СреднийЗаработок.РК;
	Объект.СреднийДневнойЗаработокСН = СреднийЗаработок.СН;
	Объект.МинимальныйСреднедневнойЗаработок = УчетПособийСоциальногоСтрахования.МинимальныйСреднедневнойЗаработокФСС(ПараметрыРасчета);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьМаксимальноеКоличествоДнейВФеврале(Форма)
	
	КоличествоДней =
		ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Дата(Форма.Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод,2,1));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Год1ДниУхода2",
		"МаксимальноеЗначение",
		КоличествоДней);
	
	ТекущееКоличество = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "Год1ДниУхода2");
	Если ТекущееКоличество > КоличествоДней Тогда
		Форма.ТаблицаЗаработка[1].Год1ДниУхода = КоличествоДней;
	КонецЕсли;
	
	КоличествоДней =
		ЗарплатаКадрыКлиентСервер.КоличествоДнейМесяца(Дата(Форма.Объект.ПериодРасчетаСреднегоЗаработкаВторойГод,2,1));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Год2ДниУхода2",
		"МаксимальноеЗначение",
		КоличествоДней);
	
	ТекущееКоличество = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "Год2ДниУхода2");
	Если ТекущееКоличество > КоличествоДней Тогда
		Форма.ТаблицаЗаработка[1].Год2ДниУхода = КоличествоДней;
	КонецЕсли;
	
	УстановитьКоличествоДнейПервыхМесяцев(Форма);
	
КонецПроцедуры

&НаСервере
Процедура РежимПодробногоВводаСреднегоЗаработкаЧислоПриИзмененииНаСервере()
	
	Если Не РежимПодробногоВводаСреднегоЗаработка Тогда
		СвернутьДанныеДляКраткойФормы();
	КонецЕсли;
	
	УстановитьОтображениеКраткойПодробнойФормыВвода();
	
КонецПроцедуры

&НаСервере
Процедура СвернутьДанныеДляКраткойФормы()
	
	Если РежимПодробногоВводаСреднегоЗаработка Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСреднийЗаработокФСС = СреднийЗаработокФСС.Выгрузить();
	Для Каждого СтрокаТаблицы Из ТаблицаСреднийЗаработокФСС Цикл
		СтрокаТаблицы.Период = НачалоГода(СтрокаТаблицы.Период);
	КонецЦикла;
	СтрокаСвертки = "Период,ФизическоеЛицо";
	Если ПрименятьРайонныйКоэффициент Или ПрименятьСевернуюНадбавку Тогда
		СтрокаСвертки = СтрокаСвертки + ",Начисление";
	КонецЕсли;
	ТаблицаСреднийЗаработокФСС.Свернуть(СтрокаСвертки, "Сумма");
	
	Для НомерГода = 1 По 2 Цикл
			
		Если НомерГода =1 Тогда
			Год = Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод;
		Иначе
			Год = Объект.ПериодРасчетаСреднегоЗаработкаВторойГод;
		КонецЕсли;
		Год = ?(Год = 0, 1, Год);
		
		СуммаПоГоду = 0;
		СуммаДнейПоГоду = 0;
		Для НомерМесяца = 1 По 12 Цикл
			
			ИндексМесяца = НомерМесяца - 1;
			
			СуммаПоГоду = СуммаПоГоду + ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "Месяц"];
			ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "Месяц"] = 0;
			
			СуммаДнейПоГоду = СуммаДнейПоГоду + ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "ДниУхода"];
			ТаблицаЗаработка[ИндексМесяца]["Год" + НомерГода + "ДниУхода"] = 0;
			
		КонецЦикла;
		
		ТаблицаЗаработка[0]["Год" + НомерГода + "Месяц"]    = СуммаПоГоду;
		ТаблицаЗаработка[0]["Год" + НомерГода + "ДниУхода"] = СуммаДнейПоГоду;
		
	КонецЦикла;
	
	СреднийЗаработокФСС.Очистить();
	Для Каждого СтрокаТаблицы Из ТаблицаСреднийЗаработокФСС Цикл
		НоваяСтрока = СреднийЗаработокФСС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеКраткойПодробнойФормыВвода()
	
	Элементы.ДанныеОЗаработкеГруппаГод.Видимость     = НЕ РежимПодробногоВводаСреднегоЗаработка;
	Элементы.ДанныеОЗаработкеГруппаТаблица.Видимость = РежимПодробногоВводаСреднегоЗаработка;
	
	УстановитьКоличествоДнейПервыхМесяцев(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКоличествоДнейПервыхМесяцев(Форма)
	
	Если Форма.РежимПодробногоВводаСреднегоЗаработка Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"Год1ДниУхода1",
			"МаксимальноеЗначение",
			31);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"Год2ДниУхода1",
			"МаксимальноеЗначение",
			31);
		
	Иначе
		
		КоличествоДней = ДеньГода(Дата(Форма.Объект.ПериодРасчетаСреднегоЗаработкаПервыйГод,12,31));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"Год1ДниУхода1",
			"МаксимальноеЗначение",
			КоличествоДней);
		
		КоличествоДней = ДеньГода(Дата(Форма.Объект.ПериодРасчетаСреднегоЗаработкаВторойГод,12,31));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Форма.Элементы,
			"Год2ДниУхода1",
			"МаксимальноеЗначение",
			КоличествоДней);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьТаблицу()
	
	Месяц = НачалоГода(ТекущаяДата());
	
	Для СчетчикМесяцев = 0 По 11 Цикл
		
		НоваяСтрока = ТаблицаЗаработка.Добавить();
		НоваяСтрока.Месяц       = Формат(Месяц, "ДФ='ММММ'");
		НоваяСтрока.НомерМесяца = Месяц(Месяц);
		
		Месяц = ДобавитьМесяц(НачалоМесяца(Месяц), 1);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПодсчитатьИтог(Знач ТаблицаЗаработка, ИтогГод1, ИтогГод2)
	
	ИтогГод1 = ТаблицаЗаработка.Итог("Год1Месяц");
	ИтогГод2 = ТаблицаЗаработка.Итог("Год2Месяц");
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьИзменения()
	
	ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
	Оповещение = Новый ОписаниеОповещения("ВопросСохранитьИзмененияЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСохранитьИзмененияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПодготовитьФормуКПринятиюИзменений();
		Закрыть(АдресВХранилище);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти