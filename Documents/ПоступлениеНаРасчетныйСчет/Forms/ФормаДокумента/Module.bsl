
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = Новый Массив;
	ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии");
	Если ПрименениеУСН Тогда
		ИдентификаторыСобытийПриОткрытии.Добавить("ПриОткрытии_УСН");
	КонецЕсли;
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.ПоступлениеНаРасчетныйСчет",
		"ФормаДокумента",
		НСтр("ru = 'Новости: Поступление на расчетный счет'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПоступлениеНаРасчетныйСчетФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект, "ГруппаКомментарий", "Ответственный");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
	Если ПлательщикНПД И ЧекиНПДКлиент.ЕстьПравоОбновленияЧековНПД()
		И ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧекеНПД) Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСтатусОфлайнЧековНПД", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПоступлениеНаРасчетныйСчетФормы.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПоступлениеНаРасчетныйСчетФормы.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
	Оповестить("Запись_ПоступлениеНаРасчетныйСчет", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененБанковскийСчет" И Параметр.Владелец = Объект.Организация Тогда
		ОбработкаОповещенияИзмененБанковскийСчет(ИмяСобытия, Параметр, Источник);
	ИначеЕсли ИмяСобытия = "Запись_Организации" Тогда
		Если Источник = Объект.Организация Тогда
			
			ПоступлениеНаРасчетныйСчетФормыВызовСервера.ОбработатьИзмененияВОрганизации(
				Объект.Организация,
				ИспользоватьНесколькоБанковскихСчетовОрганизации,
				ОсновнойБанковскийСчетОрганизацииЗаполнен,
				Объект.СчетОрганизации);
			
			Элементы.ПредупреждениеОЗаполненииРеквизитовСчета.Видимость =
				ПроверкаРеквизитовОрганизацииКлиентСервер.ПоказатьПредупреждениеОРеквизитахСчета(ЭтотОбъект, Объект.Организация);
			Модифицированность = Истина;
			
		КонецЕсли;
	ИначеЕсли ИмяСобытия = ЧекиНПДКлиентСервер.ИмяСобытияДляОповещенияОбИзмененииЧека()
		И Параметр.ДокументОснование = Объект.Ссылка Тогда
		
		ЧекиНПДКлиент.ДобавитьПричинуАннулированияЧекаВКомментарий(Параметр, ЭтотОбъект);
		УстановитьСведенияОЧекеНПД(Параметр);
		
	ИначеЕсли ИмяСобытия = ЧекиНПДКлиент.ИмяСобытияИзменениеСтатусаОфлайнЧека()
		И Параметр = Объект.Организация
		И ЧекиНПДКлиентСервер.ЧекОжидаетОтправкиВФНС(СведенияОЧекеНПД) Тогда
		
		ОбновитьСтатусОфлайнЧекаНПД();
		
	ИначеЕсли ИмяСобытия = "Запись_ИспользоватьНумерациюДоговоровСПокупателями" Тогда
		
		УстановитьПредлагатьНовыйДоговорСНумерацией();
		
	ИначеЕсли ИмяСобытия = "Запись_ВидОплаты" Тогда
		
		ОбновитьФорму = Ложь;
		ПоступлениеНаРасчетныйСчетФормыКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник, ОбновитьФорму);
		Если ОбновитьФорму Тогда
			ОбновитьФорму();
		ИначеЕсли СвойстваПлатежа.Свойство("ВидОплатыРеквизиты") И СвойстваПлатежа.ВидОплатыРеквизиты <> Неопределено Тогда
			СвойстваПлатежа.ВидОплатыРеквизиты.Вставить("УчитыватьНДСПоКомиссии", Источник.Объект.УчитыватьНДСПоКомиссии);
			ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ВыводПредупрежденияОНастройкеПорядкаОтраженияКомиссииВРасходах(
				ЭтотОбъект, Истина);
		КонецЕсли;
		
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
	Если ПоступлениеНаРасчетныйСчетФормыКлиент.ОбрабатыватьОповещениеНаСервере(ИмяСобытия) Тогда
		ОбработкаОповещенияНаСервере();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПоступлениеНаРасчетныйСчетФормы.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		Возврат;
	КонецЕсли;
	
	ВидОперацииПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ВидОперацииАвтоПодбор(Объект.ВидОперации, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыПриОплатеПриИзменении(Элемент)
	
	Объект.БезЗакрывающихДокументов = РасчетыПриОплате = "БезДокументов";
	ВидОперацииПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;
	
	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата,
		ТекущаяДатаДокумента, Объект.ВалютаДокумента, ВалютаРегламентированногоУчета);
	
	// При поступлении от продажи валюты также меняется курс ЦБ на дату реализации.
	Если НЕ ТребуетсяВызовСервера Тогда
		ТребуетсяВызовСервера = Объект.ВидОперации
			= ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажиИностраннойВалюты");
	КонецЕсли;
	
	// Если есть договоры в у.е., то необходимо получение курсов валют.
	Если НЕ ТребуетсяВызовСервера Тогда
		ТребуетсяВызовСервера = ЕстьРасчетыВУсловныхЕдиницах;
	КонецЕсли;
	
	// Проверка на изменение сроков действия патентов.
	Если НЕ ТребуетсяВызовСервера Тогда
		ТребуетсяВызовСервера = ПрименяетсяУСНПатент;
	КонецЕсли;
	
	// Если определили, что изменение даты может повлиять на какие-либо параметры,
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииСервер();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииСервер();
	Иначе
		Элементы.ПредупреждениеОЗаполненииРеквизитовСчета.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СчетОрганизацииПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.СчетОрганизации) Тогда
		Возврат;
	КонецЕсли;
	
	СчетОрганизацииПриИзмененииСервер(Новый Массив);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ПодразделениеОрганизацииИзменение();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетБанкПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СчетБанк) Тогда
		СчетБанкПриИзмененииСервер(Новый Массив);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		КонтрагентПриИзмененииСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКонтрагентаПриИзменении(Элемент)
	
	Если Не ПоступлениеНаРасчетныйСчетФормыКлиент.ТребуетсяВызовСервераПриИзмененииСчетаКонтрагента(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СчетКонтрагентаПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	СуммаДокументаПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаПрочиеПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СчетУчетаПрочиеПриИзмененииКлиент(ЭтотОбъект, Элемент);
	
	Если ПрименениеУСН Тогда
		ЗаполнитьОтражениеВУСННаСервере(Истина, Истина);
		ПоступлениеНаРасчетныйСчетФормыКлиентСервер.СформироватьПрочееТекстДоходыУСН(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтПрочие1ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтПрочиеПриИзменении(ЭтотОбъект, 1);
	Если ПрименениеУСН Тогда
		ЗаполнитьОтражениеВУСННаСервере(Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтПрочие2ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтПрочиеПриИзменении(ЭтотОбъект, 2);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтПрочие3ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтПрочиеПриИзменении(ЭтотОбъект, 3);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтПрочие1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтПрочиеНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтПрочие2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтПрочиеНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтПрочие3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтПрочиеНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетУчетаУслугиБанкаПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СчетУчетаУслугиБанкаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтУслугиБанка1ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтУслугиБанкаПриИзменении(ЭтотОбъект, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтУслугиБанка2ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтУслугиБанкаПриИзменении(ЭтотОбъект, 2);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтУслугиБанка3ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтУслугиБанкаПриИзменении(ЭтотОбъект, 3);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтУслугиБанка1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтУслугиБанкаНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтУслугиБанка2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтУслугиБанкаНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКтУслугиБанка3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СубконтоКтУслугиБанкаНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтражатьРазницуВКурсеВСоставеОперационныхРасходовПокупкаВалютыПриИзменении(Элемент)
	
	ОтражатьРазницуВКурсеВСоставеОперационныхРасходовПокупкаВалютыПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОтражатьРазницуВКурсеВСоставеОперационныхРасходовПокупкаВалютыПриИзмененииНаСервере()
	
	ПоступлениеНаРасчетныйСчетФормы.ОтражатьРазницуВКурсеВСоставеОперационныхРасходовПокупкаВалютыПриИзмененииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееДоходыУСНПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиентСервер.СформироватьПрочееТекстДоходыУСН(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяДвиженияДенежныхСредствПрочиеПриИзменении(Элемент)
	
	Если ПрименениеУСН Тогда
		ЗаполнитьОтражениеВУСННаСервере(Ложь, Истина);
	КонецЕсли;
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяДвиженияДенежныхСредствПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПроверкаРеквизитовОрганизацииКлиент.РеквизитыОрганизацииСсылкаОбработкаНавигационнойСсылки(Объект.Организация, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	УдалитьПредупреждениеПриЗагрузкеВыпискиИСпрятатьБаннер();
	ПоступлениеНаРасчетныйСчетФормыКлиент.ОповеститьОбИзмененииПредупрежденияПриЗагрузкеВыписки(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЧекНПДНажатие(Элемент)
	
	ЧекиНПДКлиент.ОткрытьФормуЧека(ЭтотОбъект, Объект.Ссылка, Объект.Организация, Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентЮрФизЛицоПриИзменении(Элемент)
	
	КонтрагентЮрФизЛицоПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеВУСНПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ОтражениеВУСНПриИзменении(ЭтотОбъект, Элемент);
	ОтражениеВУСНПриИзмененииНаСервере();
	ПоступлениеНаРасчетныйСчетФормыКлиентСервер.СформироватьПрочееТекстДоходыУСН(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалогПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Налог) Тогда
		НалогПриИзмененииНаСервере(Новый Массив);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникРазметкиАУСНПриИзменении(Элемент)
	
	ОтражениеВУСНПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееИсточникРазметкиАУСНПриИзменении(Элемент)
	
	ОтражениеВУСНПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВторойСчетОрганизацииПриИзменении(Элемент)
	
	ВторойСчетОрганизацииПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КодВалютнойОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьКодИзПеречняКВВО();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыРасшифровкаПлатежа

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасшифровкаПлатежа

&НаКлиенте
Процедура РасшифровкаПлатежаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ИнициализироватьСвойстваПлатежа(ЭтотОбъект); // Только создаем структуру, если она еще не создана.
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПриНачалеРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПередУдалением(Элемент, Отказ)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПередУдалением(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаУслугаНПДПриИзменении(Элемент)
	
	СтрокаПлатеж = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаПлатеж.СуммаПлатежа)
		Или Не ЗначениеЗаполнено(СтрокаПлатеж.УслугаНПД) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПлатеж.СуммаПлатежа = ЦенаУслугиНПД(СтрокаПлатеж.УслугаНПД, Объект.Организация, Объект.Дата);
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаСуммаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаУслугаНПДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаУслугаНПДАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаУслугаНПДОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаПриИзменении(Элемент)
	
	СтрокаПлатеж = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ИнициализироватьСвойстваПлатежа(ЭтотОбъект); // Только создаем структуру, если она еще не создана.
	
	Если СтрокаПлатеж.ДоговорКонтрагента = СвойстваПлатежа.ДоговорКонтрагента
		И Объект.РасшифровкаПлатежа.Индекс(СтрокаПлатеж) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииНаСервере(СтрокаПлатеж.ПолучитьИдентификатор());
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаСоздание(
		ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаАвтоПодбор(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДоговорКонтрагентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаДоговорКонтрагентаОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСтатьяДвиженияДенежныхСредствПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСпособПогашенияЗадолженностиПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СпособПогашенияЗадолженностиПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСделкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СделкаНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСуммаПлатежаПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаСуммаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаКурсВзаиморасчетовПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаКурсВзаиморасчетовПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСуммаВзаиморасчетовПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаСуммаВзаиморасчетовПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСтавкаНДСПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СтавкаНДСПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаСчетУчетаРасчетовСКонтрагентомПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаСчетУчетаРасчетовСКонтрагентомПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтражениеАвансаПредставлениеПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаОтражениеАвансаПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	Если ПрименяетсяУСНПатент Тогда
		ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
		Если ТекущиеДанные.ПорядокОтраженияАванса = ПредопределенноеЗначение("Перечисление.ПорядокОтраженияАвансов.ДоходПатент")
			Или ТипЗнч(ТекущиеДанные.ПорядокОтраженияАванса) = Тип("СправочникСсылка.Патенты") Тогда
			ТекущиеДанные.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
		Иначе
			Если ПлательщикНДСПриУСН Тогда
				ТекущиеДанные.СтавкаНДС = СтавкаНДСПриУСН;
			Иначе
				ТекущиеДанные.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
			КонецЕсли;
		КонецЕсли;
		ПоступлениеНаРасчетныйСчетФормыКлиент.СтавкаНДСПриИзменении(ЭтотОбъект, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтражениеАвансаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаОтражениеАвансаПредставлениеОбработкаВыбора(
		ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтражениеДоходаПредставлениеПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаОтражениеДоходаПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаОтражениеДоходаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаОтражениеДоходаПредставлениеОбработкаВыбора(
		ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыОтражениеАвансаПредставлениеПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаОтражениеАвансаПредставлениеПриИзменении(ЭтотОбъект, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыОтражениеАвансаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаОтражениеАвансаПредставлениеОбработкаВыбора(
		ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИнкассация

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияСчетУчетаРасчетовСКонтрагентомПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияСчетУчетаРасчетовСКонтрагентомПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияСубконтоКт1ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияСубконтоКтПриИзменении(ЭтотОбъект, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияСубконтоКт2ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияСубконтоКтПриИзменении(ЭтотОбъект, 2);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияСубконтоКт3ПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияСубконтоКтПриИзменении(ЭтотОбъект, 3);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияСубконтоКт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияСубконтоКтНачалоВыбора(
		ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияСубконтоКт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияСубконтоКтНачалоВыбора(
		ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияСубконтоКт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияСубконтоКтНачалоВыбора(
		ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияПередУдалением(Элемент, Отказ)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаИнкассацияПередУдалением(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		СтрокаПлатеж = Элемент.ТекущиеДанные;
		Если НЕ Копирование Тогда
			ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Объект, "СчетУчетаРасчетовСКонтрагентом");
			
			ЗаполнитьДобавленныеКолонкиТаблиц();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаИнкассацияСтатьяДвиженияДенежныхСредствПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ИнкассацияСтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПокупкаВалюты

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПокупкаВалютыПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПокупкаВалютыПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыПередУдалением(Элемент, Отказ)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПокупкаВалютыПередУдалением(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ИнициализироватьСвойстваПлатежа(ЭтотОбъект); // Только создаем структуру, если она еще не создана.
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПокупкаВалютыПриНачалеРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыСуммаПлатежаПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПокупкаВалютыСуммаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыСуммаВзаиморасчетовПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПокупкаВалютыСуммаВзаиморасчетовПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыКурсВзаиморасчетовПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПокупкаВалютыКурсВзаиморасчетовПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыКурсВзаиморасчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПокупкаВалютыКурсВзаиморасчетовНачалоВыбора(
		ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПокупкаВалютыДоговорКонтрагентаОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыДоговорКонтрагентаПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПокупкаВалютыДоговорКонтрагентаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыСтатьяДвиженияДенежныхСредствПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПокупкаВалютыСтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПродажаВалюты

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПродажаВалютыПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПродажаВалютыПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыПередУдалением(Элемент, Отказ)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПродажаВалютыПередУдалением(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ИнициализироватьСвойстваПлатежа(ЭтотОбъект); // Только создаем структуру, если она еще не создана.
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПродажаВалютыПриНачалеРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыСуммаВзаиморасчетовПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПродажаВалютыСуммаВзаиморасчетовПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыСуммаПлатежаПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПродажаВалютыСуммаПлатежаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыКурсВзаиморасчетовПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПродажаВалютыКурсВзаиморасчетовПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыКурсВзаиморасчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПродажаВалютыКурсВзаиморасчетовНачалоВыбора(
		ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПродажаВалютыДоговорКонтрагентаОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыДоговорКонтрагентаПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПродажаВалютыДоговорКонтрагентаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыСтатьяДвиженияДенежныхСредствПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПродажаВалютыСтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПлатежныеКарты

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ИнициализироватьСвойстваПлатежа(ЭтотОбъект); // Только создаем структуру, если она еще не создана.
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПлатежныеКартыПриНачалеРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПлатежныеКартыПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыПередУдалением(Элемент, Отказ)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПлатежныеКартыПередУдалением(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПлатежныеКартыПриОкончанииРедактирования(ЭтотОбъект, Элемент, НоваяСтрока, ОтменаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыСуммаПлатежаПриИзменении(Элемент)
	
	СтрокаПлатеж = Элемент.Родитель.ТекущиеДанные;
	РасшифровкаПлатежаПлатежныеКартыСуммаПлатежаПриИзмененииНаСервере(СтрокаПлатеж.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыСуммаУслугПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПлатежныеКартыСуммаУслугПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыСуммаВозвратаПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПлатежныеКартыСуммаВозвратаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПлатежныеКартыДоговорКонтрагентаОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыДоговорКонтрагентаПриИзменении(Элемент)
	
	СтрокаПлатеж = Элемент.Родитель.ТекущиеДанные;
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ИнициализироватьСвойстваПлатежа(ЭтотОбъект); // Только создаем структуру, если она еще не создана.
	
	Если СтрокаПлатеж.ДоговорКонтрагента = СвойстваПлатежа.ДоговорКонтрагента
		И Объект.РасшифровкаПлатежа.Индекс(СтрокаПлатеж) = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РасшифровкаПлатежаПлатежныеКартыДоговорКонтрагентаПриИзмененииСервер(СтрокаПлатеж.ПолучитьИдентификатор());
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПлатежныеКартыДоговорКонтрагентаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыСтатьяДвиженияДенежныхСредствПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.ПлатежныеКартыСтатьяДвиженияДенежныхСредствПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыОтражениеДоходаПредставлениеПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаОтражениеДоходаПредставлениеПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыОтражениеДоходаПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаОтражениеДоходаПредставлениеОбработкаВыбора(
		ЭтотОбъект, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыСпособПогашенияЗадолженностиПриИзменении(Элемент)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СпособПогашенияЗадолженностиПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПлатежныеКартыСделкаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.СделкаНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьКурсВалютыЦБРФ(Команда)
	
	ОбновитьКурсВалютыЦБРФНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьЧекНПД(Команда)
	
	Если Не ПоступлениеНаРасчетныйСчетФормыКлиент.ПроверитьЗаполнениеДляФормированияЧекаНПД(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Объект.Проведен Или Модифицированность Тогда
		ПараметрыЗаписи = Новый Структура();
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
	ЧекиНПДКлиент.ОткрытьФормуЧека(ЭтотОбъект, Объект.Ссылка, Объект.Организация, Объект.Контрагент);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработкаОповещенияИзмененБанковскийСчет(ИмяСобытия, Параметр, Источник)
	
	ПоступлениеНаРасчетныйСчетФормы.ОбработкаОповещенияИзмененБанковскийСчет(ЭтотОбъект, Параметр.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()
	
	ПоступлениеНаРасчетныйСчетФормы.ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВидОперацииПриИзмененииСервер()
	
	ПоступлениеНаРасчетныйСчетФормы.ВидОперацииПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеОрганизацииИзменение()
	
	ПоступлениеНаРасчетныйСчетФормы.ПодразделениеОрганизацииИзменение(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииСервер()
	
	ПоступлениеНаРасчетныйСчетФормы.ДатаПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	ПоступлениеНаРасчетныйСчетФормы.ОрганизацияПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СчетОрганизацииПриИзмененииСервер(ПричиныИзменения)
	
	ПоступлениеНаРасчетныйСчетФормы.СчетОрганизацииПриИзмененииСервер(ЭтотОбъект, ПричиныИзменения);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииСервер()
	
	ПоступлениеНаРасчетныйСчетФормы.КонтрагентПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СчетКонтрагентаПриИзмененииСервер()
	
	ПоступлениеНаРасчетныйСчетФормы.СчетКонтрагентаПриИзмененииСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура СчетБанкПриИзмененииСервер(ПричиныИзменения)
	
	ПоступлениеНаРасчетныйСчетФормы.СчетБанкПриИзмененииСервер(ЭтотОбъект, ПричиныИзменения);
	
КонецПроцедуры

&НаСервере
Процедура СуммаДокументаПриИзмененииСервер()
	
	ПоступлениеНаРасчетныйСчетФормы.СуммаДокументаПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура НалогПриИзмененииНаСервере(ПричиныИзменения)
	
	ПоступлениеНаРасчетныйСчетФормы.НалогПриИзмененииНаСервере(ЭтотОбъект, ПричиныИзменения);
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаПлатежныеКартыСуммаПлатежаПриИзмененииНаСервере(ИдСтроки);
	
	СтрокаПлатеж = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдСтроки);
	ПоступлениеНаРасчетныйСчетФормы.ПлатежныеКартыСуммаПлатежаПриИзмененииНаСервере(СтрокаПлатеж, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииНаСервере(ИдСтроки)
	
	СтрокаПлатеж = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдСтроки);
	ПоступлениеНаРасчетныйСчетФормы.ДоговорКонтрагентаПриИзмененииНаСервере(СтрокаПлатеж, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаПлатежныеКартыДоговорКонтрагентаПриИзмененииСервер(ИдСтроки)
	
	СтрокаПлатеж = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдСтроки);
	ПоступлениеНаРасчетныйСчетФормы.ПлатежныеКартыДоговорКонтрагентаПриИзмененииНаСервере(СтрокаПлатеж, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКурсВалютыЦБРФНаСервере()
	
	ПоступлениеНаРасчетныйСчетФормы.ОбновитьКурсВалютыЦБРФ(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтражениеВУСННаСервере(ЗаполнитьСуммы = Истина, ЗаполнитьСодержание = Ложь)
	
	ПоступлениеНаРасчетныйСчетФормы.ЗаполнитьОтражениеВУСННаСервере(ЭтотОбъект, ЗаполнитьСуммы, ЗаполнитьСодержание);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОповещенияНаСервере()
	
	ПоступлениеНаРасчетныйСчетФормы.ОбработкаОповещенияНаСервере(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПОДКЛЮЧАЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЗАВЕРШЕНИЕ НЕМОДАЛЬНЫХ ВЫЗОВОВ

&НаКлиенте
Процедура РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаКурсВзаиморасчетовНачалоВыбораЗавершение(
		ЭтотОбъект, РезультатЗакрытия, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПокупкаВалютыКурсВзаиморасчетовНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПокупкаВалютыКурсВзаиморасчетовНачалоВыбораЗавершение(
		ЭтотОбъект, РезультатЗакрытия, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПродажаВалютыКурсВзаиморасчетовНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	ПоступлениеНаРасчетныйСчетФормыКлиент.РасшифровкаПлатежаПродажаВалютыКурсВзаиморасчетовНачалоВыбораЗавершение(
		ЭтотОбъект, РезультатЗакрытия, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентЮрФизЛицоПриИзмененииНаСервере()
	
	ПоступлениеНаРасчетныйСчетФормы.УстановитьОграничениеТипаКонтрагента(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыСозданияНовогоДоговора() Экспорт
	
	Возврат РаботаСДоговорамиКонтрагентовБП.ПараметрыСозданияНовогоДоговора(ЭтотОбъект);
	
КонецФункции

&НаСервере
Процедура УстановитьПредлагатьНовыйДоговорСНумерацией()
	
	РаботаСДоговорамиКонтрагентовБП.УстановитьПредлагатьНовыйДоговорСНумерацией(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОтражениеВУСНПриИзмененииНаСервере()
	
	ПоступлениеНаРасчетныйСчетФормы.УправлениеЭлементамиОтраженияВУСННаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВторойСчетОрганизацииПриИзмененииСервер()
	
	ПоступлениеНаРасчетныйСчетФормы.ВторойСчетОрганизацииПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКодИзПеречняКВВО()

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта",             "Документ");
	ПараметрыФормы.Вставить("НазваниеОбъекта",        "ПоступлениеНаРасчетныйСчет");
	ПараметрыФормы.Вставить("НазваниеМакета",         "КлассификаторКВВО");
	ПараметрыФормы.Вставить("ТекущийПериод",          ТекущаяДата());
	ПараметрыФормы.Вставить("ТекущийКод",             Объект.КодВалютнойОперации);
		
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьКодИзКлассификатораЗавершение", 
		ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,, ОповещениеОЗакрытии);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКодИзКлассификатораЗавершение(РезультатЗакрытия, ПараметрыЗавершенияВыбора) Экспорт
	
	ВыбранныйКод = РезультатЗакрытия;
	
	Если ВыбранныйКод <> Неопределено Тогда
		
		Модифицированность = Истина;
		Объект.КодВалютнойОперации = ВыбранныйКод;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораСтавкиНДС() Экспорт
	
	УчетНДС.УстановитьПараметрыВыбораСтавкиНДС(ЭтотОбъект, "РасшифровкаПлатежаСтавкаНДС");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФорму()
	
	ПоступлениеНаРасчетныйСчетФормы.УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ЧекиНПД

&НаСервереБезКонтекста
Функция ЦенаУслугиНПД(УслугаНПД, Организация, Дата)
	
	Возврат ЧекиНПД.ЦенаУслугиНПД(УслугаНПД, Организация, Дата);
	
КонецФункции

&НаСервере
Процедура УстановитьСведенияОЧекеНПД(НовыеСведенияОЧекеНПД)
	
	СведенияОЧекеНПД = НовыеСведенияОЧекеНПД;
	ПоступлениеНаРасчетныйСчетФормы.УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСтатусОфлайнЧековНПД()
	
	ЧекиНПДКлиент.ОбновитьСтатусыОфлайнЧековНПД();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусОфлайнЧекаНПД()
	
	ПоступлениеНаРасчетныйСчетФормы.ОбновитьСтатусОфлайнЧекаНПД(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ПредупрежденияПриЗагрузкеВыписки

&НаСервере
Процедура УдалитьПредупреждениеПриЗагрузкеВыпискиИСпрятатьБаннер()
	
	БанкИКассаФормы.УдалитьПредупреждениеПриЗагрузкеВыпискиИСпрятатьБаннер(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область РедактированиеКомментарияВЗакрытомПериоде

&НаКлиенте
Процедура Подключаемый_РедактироватьКомментарий(Команда) 
	
	КомментарииДокументовЗакрытогоПериодаКлиент.ИзменитьКомментарий(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
