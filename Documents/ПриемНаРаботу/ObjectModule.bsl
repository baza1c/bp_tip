#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	НомерПриказа = "";
	ИдентификаторИсточника = "";
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект, , ,
		ДополнительныеСвойства.Свойство("ОтключитьПроверкуДатыЗапретаИзменения"));
	
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	КадровыйУчет.СформироватьКадровыеДвижения(ЭтотОбъект, Движения, ДанныеДляПроведения.КадровыеДвижения);
	КадровыйУчет.СформироватьДвиженияВидовЗанятостиСотрудников(Движения, ДанныеДляПроведения.ДвиженияВидовЗанятости);
	
	Если ЗагруженИзВнешнегоИсточника Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПлановыхНачислений = Новый Структура;
	СтруктураПлановыхНачислений.Вставить("ДанныеОПлановыхНачислениях", ДанныеДляПроведения.ПлановыеНачисления);
	
	РасчетЗарплаты.СформироватьДвиженияПлановыхНачислений(ЭтотОбъект, Движения, СтруктураПлановыхНачислений);
	РасчетЗарплаты.СформироватьДвиженияПлановыхВыплат(Движения, ДанныеДляПроведения.КадровыеДвижения);
	
	УчетСтажаПФР.ЗарегистрироватьПериодыВУчетеСтажаПФР(Движения, ДанныеДляРегистрацииВУчетаСтажаПФР());
	
	ЭлектронныеТрудовыеКнижки.СформироватьДвиженияМероприятийТрудовойДеятельности(
		Движения.МероприятияТрудовойДеятельности, ДанныеДляПроведения.МероприятияТрудовойДеятельности);
	
	ЭлектронныеТрудовыеКнижки.СформироватьДвиженияЗаявленийОВеденииТрудовыхКнижек(
		Движения.ЗаявленияОВеденииТрудовыхКнижек, ДанныеДляПроведения.ЗаявленияОВеденииТрудовыхКнижек);
	
	ЭлектронныеТрудовыеКнижки.СформироватьДвиженияЗаявленийОВеденииТрудовыхКнижекПереданных(
		Движения.ЗаявленияОВеденииТрудовыхКнижекПереданные, ДанныеДляПроведения.ЗаявленияОВеденииТрудовыхКнижекПереданные);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ДатаПриема = ТекущаяДатаСеанса();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		Сотрудник = ДанныеЗаполнения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, Сотрудник, Истина, Ложь)
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаПриема, "Объект.ДатаПриема", Отказ, НСтр("ru='Дата приема'"));
	
	КадровыйУчет.ПроверитьВозможностьПроведенияПоКадровомуУчету(
		ДатаПриема,
		Сотрудник,
		Ссылка,
		Отказ,
		Перечисления.ВидыКадровыхСобытий.Прием);
		
	КадровыйУчет.ПроверитьСоответствиеСотрудниковОрганизации(Организация, Сотрудник, Отказ);
	
	СообщениеПроверкиВидЗанятости = СотрудникиФормы.СообщениеОКонфликтеВидаЗанятостиНовогоСотрудникаССуществующими(
		Сотрудник,
		ФизическоеЛицо,
		Организация,
		ВидЗанятости,
		ДатаПриема);
		
	Если Не ПустаяСтрока(СообщениеПроверкиВидЗанятости) Тогда
		ОбщегоНазначения.СообщитьПользователю(СообщениеПроверкиВидЗанятости, , "ВидЗанятости", "Объект", Отказ);
	КонецЕсли;
	
	Если ОтразитьВТрудовойКнижке Тогда
		
		ТекстСообщения = "";
		ПутьКДанным = "";
		
		Если Не ЗначениеЗаполнено(ВидЗаявленияОВыбореСпособаВеденияТрудовойКнижки)
			И ЗначениеЗаполнено(ДатаЗаявленияОВыбореСпособаВеденияТрудовойКнижки) Тогда
			
			ТекстСообщения = НСтр("ru = 'Не указан способ ведения трудовой книжки'");
			ПутьКДанным = ПутьКДанным + "ВидЗаявленияОВыбореСпособаВеденияТрудовойКнижки";
			
		ИначеЕсли ДатаЗаявленияОВыбореСпособаВеденияТрудовойКнижки > ДатаПриема Тогда
			
			ТекстСообщения = НСтр("ru = 'Дата заявления о выборе способа ведения трудовой книжке должна быть меньше даты приема'");
			ПутьКДанным = ПутьКДанным + "ДатаЗаявленияОВыбореСпособаВеденияТрудовойКнижки";
			
		ИначеЕсли ЗначениеЗаполнено(ДатаЗаявленияОВыбореСпособаВеденияТрудовойКнижки)
			И ДатаЗаявленияОВыбореСпособаВеденияТрудовойКнижки < '20200101' Тогда
			
			ТекстСообщения = НСтр("ru = 'Дата заявления о выборе способа ведения трудовой книжки меньше даты начала учета - 01 января 2020 года'");
			ПутьКДанным = ПутьКДанным + "ДатаЗаявленияОВыбореСпособаВеденияТрудовойКнижки";
			
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, ПутьКДанным, "Объект", Отказ);
		КонецЕсли;
		
		ЭлектронныеТрудовыеКнижки.ПроверкаЗаполненияВторогоДокументаОснования(ЭтотОбъект, Ссылка, Отказ);
		
	КонецЕсли;
	
	Если ЗагруженИзВнешнегоИсточника Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "КоличествоСтавок");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СпособРасчетаАванса");
	Иначе
		ЭлектронныеТрудовыеКнижки.ПроверитьЗаполнениеКодаОКЗТрудовойФункции(ЭтотОбъект, "ДатаПриема", Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Документы.ПриемНаРаботу.ЗаполнитьДатуЗапретаРедактирования(ЭтотОбъект);
	
	Если НЕ Отказ И ЭтоНовый() Тогда
		УстановитьНовыйНомер();
		Если ПустаяСтрока(НомерПриказа) Тогда
			НомерПриказа = ЭлектронныеТрудовыеКнижкиВызовСервера.НомерНаПечать(Номер);
		КонецЕсли;
	КонецЕсли;
	Если ЗагруженИзВнешнегоИсточника Тогда
		Возврат;
	КонецЕсли;
	УчетПособийСоциальногоСтрахования.ПередЗаписьюДокументаПриемНаРаботу(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	Если ЗагруженИзВнешнегоИсточника Тогда
		Возврат;
	КонецЕсли;
	УчетПособийСоциальногоСтрахования.ПриЗаписиДокументаПриемНаРаботу(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает данные для формирования движений.
// Возвращает Структуру с полями.
//		КадровыеДвижения - данные, необходимые для формирования 
//				- кадровой истории (см. КадровыйУчетРасширенный.СформироватьКадровыеДвижения)
//				- авансов (см. РасчетЗарплатыРасширенный.СформироватьДвиженияПлановыхВыплат)
//				- истории применяемых графиков работы (см. КадровыйУчетРасширенный.СформироватьИсториюИзмененияГрафиков).
//		ПлановыеНачисления - данные, необходимые для формирования истории плановых начислений.
//		(см. РасчетЗарплатыРасширенный.СформироватьДвиженияПлановыхНачислений)
//		ЗначенияПоказателей (см. там же).
//
Функция ПолучитьДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриемНаРаботу.ДатаПриема КАК ДатаСобытия,
	|	ПриемНаРаботу.Сотрудник КАК Сотрудник,
	|	ПриемНаРаботу.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПриемНаРаботу.Организация КАК Организация,
	|	ПриемНаРаботу.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием) КАК ВидСобытия,
	|	ПриемНаРаботу.Должность КАК Должность,
	|	ПриемНаРаботу.ВидЗанятости КАК ВидЗанятости,
	|	ПриемНаРаботу.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПриемНаРаботу.Аванс КАК Аванс,
	|	ПриемНаРаботу.СпособРасчетаАванса КАК СпособРасчетаАванса,
	|	ПриемНаРаботу.КоличествоСтавок КАК КоличествоСтавок
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|ГДЕ
	|	ПриемНаРаботу.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриемНаРаботуНачисления.Ссылка.ДатаПриема КАК ДатаСобытия,
	|	ПриемНаРаботуНачисления.Ссылка.Сотрудник КАК Сотрудник,
	|	ПриемНаРаботуНачисления.Начисление КАК Начисление,
	|	ПриемНаРаботуНачисления.Размер КАК Размер,
	|	ПриемНаРаботуНачисления.Ссылка.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПриемНаРаботуНачисления.Ссылка.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	Документ.ПриемНаРаботу.Начисления КАК ПриемНаРаботуНачисления
	|ГДЕ
	|	ПриемНаРаботуНачисления.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриемНаРаботу.Ссылка КАК Ссылка,
	|	ПриемНаРаботу.Ссылка.Организация КАК Организация,
	|	ПриемНаРаботу.ДатаПриема КАК ДатаСобытия,
	|	ПриемНаРаботу.ДатаПриема КАК Период,
	|	ПриемНаРаботу.Сотрудник КАК Сотрудник,
	|	ПриемНаРаботу.Сотрудник.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПриемНаРаботу.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПриемНаРаботу.ВидЗанятости КАК ВидЗанятости
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|ГДЕ
	|	ПриемНаРаботу.Ссылка = &Ссылка";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПроведения = Новый Структура; 
	
	// Первый набор данных для проведения - таблица для формирования кадровых движений, истории графиков, авансов.
	ДанныеДляПроведения.Вставить("КадровыеДвижения", РезультатыЗапроса[0].Выгрузить());
	
	// Второй набор данных для проведения - таблица для формирования плановых начислений.
	ДанныеДляПроведения.Вставить("ПлановыеНачисления", РезультатыЗапроса[1].Выгрузить());
	
	ДанныеДляПроведения.Вставить("ДвиженияВидовЗанятости", РезультатыЗапроса[2].Выгрузить());
	ДанныеДляПроведения.Вставить("МероприятияТрудовойДеятельности",
		Документы.ПриемНаРаботу.ДанныеДляПроведенияМероприятияТрудовойДеятельности(Ссылка).Получить(Ссылка));
	
	ДополнитьДаннымиДляПроведенияЗаявленийОВеденииТрудовыхКнижек(ДанныеДляПроведения, Ссылка);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Функция ДанныеДляРегистрацииВУчетаСтажаПФР()
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Ссылка);
	
	ДанныеДляРегистрацииВУчете = Документы.ПриемНаРаботу.ДанныеДляРегистрацииВУчетаСтажаПФР(МассивСсылок);
		
	Возврат ДанныеДляРегистрацииВУчете[Ссылка];
														
КонецФункции

Процедура ДополнитьДаннымиДляПроведенияЗаявленийОВеденииТрудовыхКнижек(ДанныеДляПроведения, ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ПриемНаРаботу.НачалоТрудовойДеятельности
		|			ТОГДА ПриемНаРаботу.ДатаПриема
		|		КОГДА ПриемНаРаботу.ДатаЗаявленияОВыбореСпособаВеденияТрудовойКнижки = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДАТАВРЕМЯ(2020, 1, 1)
		|		ИНАЧЕ ПриемНаРаботу.ДатаЗаявленияОВыбореСпособаВеденияТрудовойКнижки
		|	КОНЕЦ КАК Период,
		|	ПриемНаРаботу.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПриемНаРаботу.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ПриемНаРаботу.НачалоТрудовойДеятельности
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.СведенияОТрудовойДеятельностиВЭлектроннойФорме)
		|		ИНАЧЕ ПриемНаРаботу.ВидЗаявленияОВыбореСпособаВеденияТрудовойКнижки
		|	КОНЕЦ КАК ВидЗаявления,
		|	ПриемНаРаботу.Ссылка КАК Заявление,
		|	ПриемНаРаботу.Дата КАК Дата
		|ПОМЕСТИТЬ ВТДанныеЗаявлений
		|ИЗ
		|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
		|ГДЕ
		|	ПриемНаРаботу.Ссылка = &Ссылка
		|	И ПриемНаРаботу.ОтразитьВТрудовойКнижке
		|	И (ПриемНаРаботу.НачалоТрудовойДеятельности
		|			ИЛИ ПриемНаРаботу.ВидЗаявленияОВыбореСпособаВеденияТрудовойКнижки <> ЗНАЧЕНИЕ(Перечисление.ВидыЗаявленийОПредоставленииСведенийОТрудовойДеятельности.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеЗаявлений.Период КАК Период,
		|	ДанныеЗаявлений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеЗаявлений.Организация КАК Организация,
		|	ДанныеЗаявлений.ВидЗаявления КАК ВидЗаявления,
		|	ДанныеЗаявлений.Заявление КАК Заявление
		|ИЗ
		|	ВТДанныеЗаявлений КАК ДанныеЗаявлений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеЗаявлений.Дата КАК ДатаПередачи,
		|	ДанныеЗаявлений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеЗаявлений.Организация КАК Организация,
		|	ДанныеЗаявлений.Заявление КАК Заявление,
		|	ЛОЖЬ КАК Отозвано
		|ИЗ
		|	ВТДанныеЗаявлений КАК ДанныеЗаявлений";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПроведения.Вставить("ЗаявленияОВеденииТрудовыхКнижек",
		РезультатыЗапроса[РезультатыЗапроса.Количество() - 2].Выгрузить());
	
	ДанныеДляПроведения.Вставить("ЗаявленияОВеденииТрудовыхКнижекПереданные",
		РезультатыЗапроса[РезультатыЗапроса.Количество() - 1].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли