
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ЗначенияДляЗаполнения = 
			Новый Структура(
				"ПредыдущийМесяц, 
				|Ответственный", 
				"Объект.ПериодРегистрации",
				"Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
		
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой");
		
		КонтролироватьОплатуВедомости = Не УчетЗарплаты.ЗарплатаВыплачиваетсяБезВедомости();
		УстановитьДоступностьЭлементов();
		
	КонецЕсли;
	
	Элементы.ЗарплатаКомпенсацияЗаЗадержкуЗарплаты.Видимость = Объект.Зарплата.Итог("КомпенсацияЗаЗадержкуЗарплаты") <> 0;
	Элементы.ЗарплатаВыплаченностьЗарплаты.Видимость =
		Объект.Зарплата.НайтиСтроки(Новый Структура("ВыплаченностьЗарплаты", Перечисления.ВыплаченностьЗарплаты.НеВыплачено)).Количество() > 0
		ИЛИ Объект.Зарплата.НайтиСтроки(Новый Структура("ВыплаченностьЗарплаты", Перечисления.ВыплаченностьЗарплаты.Задепонировано)).Количество() > 0;
	
	СвязьПараметровВыбораБанковскийСчет = Новый СвязьПараметраВыбора("Отбор.Владелец", "Элементы.Зарплата.ТекущиеДанные.ФизическоеЛицо", РежимИзмененияСвязанногоЗначения.Очищать);
	МассивСвязей = Новый Массив();
	МассивСвязей.Добавить(СвязьПараметровВыбораБанковскийСчет);
	ФиксированныйМассивСвязей = Новый ФиксированныйМассив(МассивСвязей);
	Элементы.ЗарплатаБанковскийСчет.СвязиПараметровВыбора = ФиксированныйМассивСвязей;
	
	ВидыДоходовИсполнительногоПроизводстваКлиентСервер.НастроитьПолеВидДоходаДляВыплатыЗарплаты(ЭтотОбъект,Объект.Дата,, Истина);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Документы.ВедомостьНаВыплатуЗарплаты);
	
	НастроитьПоляИсполнительногоПроизводства();
	
	УстановитьУсловноеОформление();
	
	Если КонтролироватьОплатуВедомости Тогда
		// БлокировкаИзмененияОбъектов
		БлокировкаИзмененияОбъектов.ПриСозданииНаСервереФормыОбъекта(ЭтотОбъект, Отказ, СтандартнаяОбработка);
		// Конец БлокировкаИзмененияОбъектов
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтотОбъект, "Объект.ПериодРегистрации", "МесяцНачисленияСтрокой");
	
	КонтролироватьОплатуВедомости = Не УчетЗарплаты.ЗарплатаВыплачиваетсяБезВедомости();
	
	ЗаполнитьТаблицуОплат();
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ВедомостьНаВыплатуЗарплатыФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	КонтролироватьОплатуВедомости = Не УчетЗарплаты.ЗарплатаВыплачиваетсяБезВедомости();
	Если КонтролироватьОплатуВедомости Тогда
		// БлокировкаИзмененияОбъектов
		БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, Ложь);
		// Конец БлокировкаИзмененияОбъектов
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ВзаиморасчетыССотрудникамиКлиент.ИмяСобытияИзмененияОплатыВедомости() Тогда
		УстановитьДоступностьЭлементов();
		УстановитьПредставлениеОплаты();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "ИзменениеОплатыВедомости" И Источник = Объект.Ссылка И КонтролироватьОплатуВедомости Тогда
		ОбновитьОбработатьСписокПлатежныхДокументов();
		РазблокироватьДокументПоВозможности();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Оповестить("Запись_ВедомостьНаВыплатуЗарплаты", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПересчитатьИтогНДФЛ();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	Если КонтролироватьОплатуВедомости Тогда
		// БлокировкаИзмененияОбъектов
		БлокировкаИзмененияОбъектов.ПослеЗаписиНаСервереФормыОбъекта(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
		// Конец БлокировкаИзмененияОбъектов
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "РасшифровкаСпискаДокументов" Тогда
		ВедомостьНаВыплатуЗарплатыКлиент.ОплатаПоказать(ЭтотОбъект, ЭтотОбъект.ТекущийЭлемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	КонецЕсли;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьПлатежныеДокументы" Тогда
		СтандартнаяОбработка = Ложь;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПлатежныеДокументы;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ДатаПриИзмененииНаСервере();
	ВидыДоходовИсполнительногоПроизводстваКлиентСервер.НастроитьПолеВидДоходаДляВыплатыЗарплаты(ЭтотОбъект,Объект.Дата,, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(
		ЭтотОбъект, 
		"Объект.ПериодРегистрации", 
		"МесяцНачисленияСтрокой", 
		Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(
		ЭтотОбъект, 
		"Объект.ПериодРегистрации", 
		"МесяцНачисленияСтрокой", 
		Направление, 
		Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодРегистрацииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидМестаВыплатыПриИзменении(Элемент)
	НастроитьПоляИсполнительногоПроизводства();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыНДФЛ

&НаКлиенте
Процедура НДФЛПриИзменении(Элемент)
	
	ПересчитатьИтогНДФЛ();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОплатыТаблица

&НаКлиенте
Процедура ОплатыТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение( , ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатыТаблицаПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыОткрыть.Заголовок =
		СтрШаблон(НСтр("ru = 'Открыть: %1'"), ТекущиеДанные.Представление);
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыПровести.Заголовок =
		СтрШаблон(НСтр("ru = 'Провести: %1'"), ТекущиеДанные.Представление);
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыОтменитьПроведение.Заголовок =
		СтрШаблон(НСтр("ru = 'Отменить проведение: %1'"), ТекущиеДанные.Представление);
	
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыОтменитьПроведение.Доступность =
		ТекущиеДанные.СостояниеДокумента = 1 И МожноРедактировать;
	Элементы.ОплатыТаблицаКонтекстноеМенюДокументОплатыПровести.Доступность
		= ТекущиеДанные.СостояниеДокумента <> 1 И МожноРедактировать;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура Оплатить(Команда)
	
	Если Объект.ПометкаУдаления Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Нельзя оформить выплату на основании документа, помеченного на удаление.'"));
		Возврат;
	Иначе
		Если Модифицированность Тогда
			Кнопки = Новый СписокЗначений;
			Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Записать");
			Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
			
			Оповещение = Новый ОписаниеОповещения("ВопросПередВыплатойСледуетЗаписатьЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед выплатой документ следует записать'"), Кнопки,, КодВозвратаДиалога.Да);
		Иначе
			ОплатитьНаСервере();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОплатыОткрыть(Команда)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПоказатьЗначение( , ТекущиеДанные.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОплатыПровести(Команда)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Модифицированность Тогда
		Кнопки = Новый СписокЗначений;
		Кнопки.Вставить(0, КодВозвратаДиалога.Да,     "Записать");
		Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередПроведениемДокументаОплатыСледуетПровестиЗавершение", ЭтотОбъект, ТекущиеДанные.Документ);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Данные ведомости были изменены. Перед выплатой изменения следует записать.'"), Кнопки,, КодВозвратаДиалога.Да);
	Иначе
		ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ТекущиеДанные.Документ, "Проведение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОплатыОтменитьПроведение(Команда)
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Не ВопросЗадавался Тогда
		УчетЗарплатыКлиент.ПоказатьВопросПриОтменеПроведенияПлатежногоДокумента(ЭтотОбъект);
	Иначе
		ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ТекущиеДанные.Документ, "ОтменаПроведения");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОплатыПеречитать(Команда)
	
	ОбновитьОбработатьСписокПлатежныхДокументов();
	РазблокироватьДокументПоВозможности();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#Область ОбратныеВызовы

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект) Экспорт
	
	УстановитьДоступностьЭлементов();
	УстановитьПредставлениеОплаты();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов() Экспорт
	
	ЕстьОплатаПоВедомости = ВзаиморасчетыССотрудниками.ЕстьОплатаПоВедомости(Объект.Ссылка);
		
	ТолькоПросмотр = 
		ДатыЗапретаИзменения.ИзменениеЗапрещено(Объект.Ссылка.Метаданные().ПолноеИмя(), Объект.Ссылка) 
		ИЛИ (КонтролироватьОплатуВедомости И ЕстьОплатаПоВедомости);
	
	ЕстьОплатаПоВедомости = ЕстьОплатаПоВедомости Или ОплатыТаблица.Количество() <> 0;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаОплатить",
		"Доступность",
		Не ЕстьОплатаПоВедомости И Не ТолькоПросмотр);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаОплатить",
		"Видимость",
		КонтролироватьОплатуВедомости);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеОплаты() Экспорт
	
	ВедомостьНаВыплатуЗарплатыФормы.УстановитьПредставлениеОплаты(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Колонка "Банковский счет" показывается только для перечисления на счет в банке

	ЭлементОформления = УсловноеОформление.Элементы.Добавить();

	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ВидМестаВыплаты");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбора.ПравоеЗначение = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет;

	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ЗарплатаБанковскийСчет");

КонецПроцедуры

&НаСервере
Процедура НастроитьПоляИсполнительногоПроизводства()
	
	Показывать = Перечисления.ВидыМестВыплатыЗарплаты.Безналичные().Найти(Объект.ВидМестаВыплаты) <> Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"ВидДоходаИсполнительногоПроизводства", 
		"Видимость", 
		Показывать);
	ОбщегоНазначенияБЗККлиентСервер.УстановитьОбязательностьПоляВводаФормы(
		ЭтотОбъект, 
		"ВидДоходаИсполнительногоПроизводства", 
		Показывать И Документы.ВедомостьНаВыплатуЗарплаты.ВидДоходаИсполнительногоПроизводстваОбязателен(Объект));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"ЗарплатаВзысканнаяСумма", 
		"Видимость", 
		Показывать);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, 
		"ИтогВзысканнаяСумма", 
		"Видимость", 
		Показывать);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередВыплатойСледуетЗаписатьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДокументЗаписан = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
	Если НЕ ДокументЗаписан Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось записать документ'"));
		Возврат;
	КонецЕсли;
	
	ОплатитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОплатитьНаСервере()
	
	ВедомостьНаВыплатуЗарплатыФормыПереопределяемый.СформироватьДокументыОплаты(ЭтотОбъект);
	УстановитьПредставлениеОплаты();
	ОбновитьОбработатьСписокПлатежныхДокументов();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	НастроитьПоляИсполнительногоПроизводства();
	
	УстановитьВидимостьНДФЛ();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьНДФЛ()
	
	ИспользуютсяСуммыНДФЛ = Объект.Дата >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	ИспользуетсяМногоступенчатаяШкалаНДФЛ = Объект.Дата >= УчетЗарплаты.ДатаНачалаУчетаПрогрессивногоНДФЛПоМногоступенчатойШкале();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"СтраницаНДФЛ",
		"Видимость",
		ИспользуютсяСуммыНДФЛ);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ИтогНДФЛ",
		"Видимость",
		ИспользуютсяСуммыНДФЛ);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"СтраницаНДФЛ",
		"Видимость",
		ИспользуютсяСуммыНДФЛ);
	
	Если ИспользуютсяСуммыНДФЛ Или КонтролироватьОплатуВедомости Тогда
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Авто;
	Иначе
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"НДФЛСуммаСПревышенияПоСтавке18",
		"Видимость",
		ИспользуетсяМногоступенчатаяШкалаНДФЛ);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"НДФЛСуммаСПревышенияПоСтавке20",
		"Видимость",
		ИспользуетсяМногоступенчатаяШкалаНДФЛ);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"НДФЛСуммаСПревышенияПоСтавке22",
		"Видимость",
		ИспользуетсяМногоступенчатаяШкалаНДФЛ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьИтогНДФЛ()
	
	ИтогНДФЛ = УчетНДФЛКлиентСервер.ИтоговаяСуммаНДФЛпоДокументу(Объект, "Сумма");
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.СтраницаПлатежныеДокументы.Видимость = КонтролироватьОплатуВедомости;
	
	Если ОплатыТаблица.Количество() = 0 Тогда
		ТекущаяСтраницаГруппыПлатежныеДокументы = Элементы.СтраницаПлатежныеДокументыПустая;
	Иначе
		ТекущаяСтраницаГруппыПлатежныеДокументы = Элементы.СтраницаПлатежныеДокументыСписок;
	КонецЕсли;
	Элементы.СтраницыПлатежныеДокументы.ТекущаяСтраница = ТекущаяСтраницаГруппыПлатежныеДокументы;
	
	УстановитьВидимостьНДФЛ();
	
	УстановитьДоступностьЭлементов();
	
	ОформитьСведенияОСуммеВыплат();
	
КонецПроцедуры

&НаСервере
Процедура БлокировкаИзмененияОбъектовПриЧтенииНаСервереФормыОбъекта()
	
	Если Не КонтролироватьОплатуВедомости Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект = ДанныеФормыВЗначение(Объект, Тип("ДокументОбъект.ВедомостьНаВыплатуЗарплаты"));
	
	// БлокировкаИзмененияОбъектов
	БлокировкаИзмененияОбъектов.ПриЧтенииНаСервереФормыОбъекта(ЭтотОбъект, ДокументОбъект, Ложь);
	// Конец БлокировкаИзмененияОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ОформитьСведенияОСуммеВыплат()
	
	ИтогКВыплате  = Объект.Зарплата.Итог("Сумма") + Объект.Зарплата.Итог("КомпенсацияЗаЗадержкуЗарплаты");
	ИтогВыплачено = УчетЗарплаты.ОплаченнаяСуммаПоВедомости(Объект.Организация, Объект.Ссылка);
	
	Элементы.ДекорацияВыплачено.Заголовок = УчетЗарплаты.ОписаниеСостоянияОплатыВедомости(ИтогКВыплате, ИтогВыплачено);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОплат()
	
	ТаблицаОплат = ОплатыТаблица.Выгрузить();
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	УчетЗарплаты.ЗаполнитьТаблицуОплат(ДокументОбъект, ТаблицаОплат);
	ОплатыТаблица.Загрузить(ТаблицаОплат);
	КоличествоДокументовОплаты = ОплатыТаблица.Количество();
	
	ОформитьТаблицуОплат();
	
КонецПроцедуры

&НаСервере
Процедура ОформитьТаблицуОплат()
	
	ТаблицаОплат = ОплатыТаблица.Выгрузить();
	УчетЗарплаты.ОформитьТаблицуОплат(ТаблицаОплат);
	ОплатыТаблица.Загрузить(ТаблицаОплат);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОбработатьСписокПлатежныхДокументов()
	
	ЗаполнитьТаблицуОплат();
	БлокировкаИзмененияОбъектовПриЧтенииНаСервереФормыОбъекта();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура РазблокироватьДокументПоВозможности()
	
	РазблокироватьДокумент = Истина;
	
	Для Каждого Строка Из ОплатыТаблица Цикл
		Если Строка.Проведен Тогда
			РазблокироватьДокумент = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если РазблокироватьДокумент Тогда
		БлокировкаИзмененияОбъектовКлиент.РазблокироватьФормуОбъекта(ЭтотОбъект, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередПроведениемДокументаОплатыСледуетПровестиЗавершение(Ответ, ПлатежныйДокумент) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДокументЗаписан = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
	Если Не ДокументЗаписан Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось записать изменения'"));
		Возврат;
	КонецЕсли;
	
	ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ПлатежныйДокумент, "Проведение");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ДокументСсылка, ТипОбработки)
	
	ОбработатьИзменениеСостоянияПлатежногоДокумента(ДокументСсылка, ТипОбработки);
	РазблокироватьДокументПоВозможности();
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередОтменойПроведенияЗавершение(Ответ, ДопПараметры = Неопределено) Экспорт
	
	ВопросЗадавался = Истина;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ОплатыТаблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьКомандуИзмененияСостоянияПлатежногоДокумента(ТекущиеДанные.Документ, "ОтменаПроведения");
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеСостоянияПлатежногоДокумента(ДокументСсылка, ТипОбработки)
	
	СтрокиТаблицы = ОплатыТаблица.НайтиСтроки(Новый Структура("Документ", ДокументСсылка));
	Если СтрокиТаблицы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	УчетЗарплаты.ОбработатьИзменениеСостоянияПлатежногоДокумента(Объект.Ссылка,
		ДокументСсылка,
		ТипОбработки,
		ОплатыТаблица.Индекс(СтрокиТаблицы[0]) + 1,
		Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементСписка = ОплатыСписок.НайтиПоЗначению(ДокументСсылка);
	Если ЭлементСписка = Неопределено И ТипОбработки = "Проведение" Тогда
		ОплатыСписок.Добавить(ДокументСсылка);
		УстановитьПредставлениеОплаты();
	ИначеЕсли ЭлементСписка <> Неопределено И ТипОбработки = "ОтменаПроведения" Тогда
		ОплатыСписок.Удалить(ЭлементСписка);
	КонецЕсли;
	
	БлокировкаИзмененияОбъектовПриЧтенииНаСервереФормыОбъекта();
	ОформитьТаблицуОплат();
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

