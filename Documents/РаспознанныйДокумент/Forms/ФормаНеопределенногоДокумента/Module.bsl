#Область ОписаниеПеременных

&НаКлиенте
Перем СвойстваЯчеекТаблицы Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Объект.Наименование;
	ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML();
	
	Если Объект.ТребуетсяОплата Тогда
		Элементы.ИмяФайла.Видимость = Ложь;
		Элементы.ТипДокумента.Видимость = Ложь;
		Элементы.ГруппаПодсказки.Видимость = Ложь;
		КоличествоФайлов = РаспознаваниеДокументовСлужебныйВызовСервера.КоличествоФайловРаспознанногоДокумента(Объект.Ссылка);
		Если КоличествоФайлов > 1 Тогда
			Элементы.СохранитьФайл.Заголовок = НСтр("ru = 'Сохранить файлы'");
		КонецЕсли;
	Иначе
		Элементы.КоличествоФайлов.Видимость = Ложь;
		Элементы.ОтправитьПовторно.Видимость = Ложь;
	КонецЕсли;
	
	Если Не РаспознаваниеДокументовСлужебныйВызовСервера.ДоступенНеоплаченныйРаспознанныйДокумент(Объект.Ссылка)
		Или Объект.ТребуетсяОплата Тогда
		
		Элементы.ГруппаПраво.Видимость = Ложь;
	Иначе
		АдресКартинки = ПоместитьВоВременноеХранилище(
			РаспознаваниеДокументовСлужебный.ИсходноеИзображение(Объект.Ссылка),
			УникальныйИдентификатор
		);
	КонецЕсли;
	
	Если Объект.ТребуетсяОплата Тогда
		Попытка
			ТекущийБаланс = РаспознаваниеДокументовSDK.ТекущийБаланс();
			Лимит = ТекущийБаланс.Лимит;
		Исключение
			Лимит = 0;
		КонецПопытки;
		
		Если Лимит < КоличествоФайлов
			Или Не РаспознаваниеДокументовСлужебныйВызовСервера.ДоступенНеоплаченныйРаспознанныйДокумент(Объект.Ссылка) Тогда
			
			Элементы.ОтправитьПовторно.Доступность = Ложь;
			Элементы.ОписаниеОшибки.Высота = 4;
			
			Объект.ОписаниеОшибки =
			НСтр("ru = 'Не хватило средств на счете для распознавания документов.
			           |Пожалуйста, пополните баланс.'");
			
		Иначе
			Элементы.СохранитьФайл.КнопкаПоУмолчанию = Ложь;
			Элементы.ОтправитьПовторно.КнопкаПоУмолчанию = Истина;
			Элементы.Переместить(Элементы.ОтправитьПовторно, Элементы.ГруппаКомандыИНастройки, Элементы.СохранитьФайл);
			
			Объект.ОписаниеОшибки =
			НСтр("ru = 'Не хватило средств на счете для распознавания документов. Пожалуйста, пополните баланс.
			           |Нажмите ""Отправить повторно"", чтобы отправить файлы на распознавание еще раз.'");
			
			Модифицированность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.ОписаниеОшибки) Тогда
		Элементы.ГруппаДополнительно.Видимость = Ложь;
		Элементы.ГруппаРеквизиты.Видимость = Истина;
	КонецЕсли;
	
	ЗаполнитьТаблицуДокумента();
	ЗаполнитьЭлементыИтогов();
	
	ИзменитьОтображениеПоТипуДокумента();
	
	Если ТаблицаДокумента.Количество() = 0 Тогда
		Элементы.ТаблицаДокумента.Видимость = Ложь;
	КонецЕсли;
	
	УправлениеФормой();
	
	ВсеСвойстваЯчеекТаблицы = РаспознаваниеДокументовСлужебный.СвойстваЯчеекТаблицы(Объект);
	АдресСвойствЯчеекТаблицы = ПоместитьВоВременноеХранилище(ВсеСвойстваЯчеекТаблицы, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИмяФормыОбработчика = РаспознаваниеДокументовСлужебныйКлиентПовтИсп.ПолучитьИмяОткрываемойФормыПоТипу(Объект.ТипДокумента, Объект.ВариантОбработки);
	Если ИмяФормыОбработчика <> "Документ.РаспознанныйДокумент.Форма.ФормаНеопределенногоДокумента" Тогда
		ПараметрыФормы = Новый Структура("Ключ", Объект.Ссылка);
		ОткрытьФорму(ИмяФормыОбработчика, ПараметрыФормы);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ЗагрузитьСвойстваЯчеекТаблицы", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	РаспознаваниеДокументовСлужебныйКлиент.ОбработкаВыбораОбратнойСвязи(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РаспознаваниеДокументовСлужебный.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КартинкаЗакрытьПодсказкуНажатие(Элемент)
	
	Элементы.ГруппаПодсказки.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипДокументаПриИзменении(Элемент)
	
	Обработчик = Новый ОписаниеОповещения("ПослеПроверкиИзменения", ЭтотОбъект);
	ПредлагатьЗаписать = Ложь;
	РаспознаваниеДокументовСлужебныйКлиент.ИзменитьТипДокумента(ЭтотОбъект, Обработчик, ПредлагатьЗаписать);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОбработкиПриИзменении(Элемент)
	
	Обработчик = Новый ОписаниеОповещения("ПослеПроверкиИзменения", ЭтотОбъект);
	ПредлагатьЗаписать = Ложь;
	РаспознаваниеДокументовСлужебныйКлиент.ИзменитьВариантОбработки(ЭтотОбъект, Обработчик, ПредлагатьЗаписать);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипДокументаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВариантОбработкиОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПолеПросмотраДокументСформирован(Элемент)
	
	Если Элемент.Документ.getElementById("image_setter") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаспознаваниеДокументовСлужебныйКлиент.ЗагрузитьКартинкуПоАдресу(Элементы.ПолеПросмотра, АдресКартинки);
	HTMLДокументСформирован = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаДокумента

&НаКлиенте
Процедура ТаблицаДокументаПриАктивизацииЯчейки(Элемент)
	
	ПриАктивизацииЯчейкиТаблицы(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьФайл(Команда)
	
	РаспознаваниеДокументовСлужебныйКлиент.СохранитьФайлыДокумента(Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПожаловатьсяНаКачество(Команда)
	
	РаспознаваниеДокументовСлужебныйКлиент.ОткрытьФормуОбратнойСвязи(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторно(Команда)
	
	ИнформацияОФайлах = РаспознаваниеДокументовСлужебныйВызовСервера.ФайлыРаспознанногоДокумента(Объект.Ссылка, УникальныйИдентификатор);
	РаспознаваниеДокументовКлиент.ПоказатьДобавлениеФайлов(УникальныйИдентификатор, , ИнформацияОФайлах);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрикрепитьСкан(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораОбъектаПрикрепления", ЭтотОбъект);
	ОткрытьФорму(
		"РегистрСведений.СвязанныеОбъектыРаспознаниеДокументов.Форма.ФормаВыбораСвязанного", , , , , ,
		ОписаниеОповещения
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеПроверкиИзменения(ТребуетсяИзменениеФормы, Контекст) Экспорт
	
	Если Не ТребуетсяИзменениеФормы Тогда
		УправлениеФормой();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	ТребуетсяУказатьВариантОбработки =
		(Объект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.НеопределенныйДокумент);
	Элементы.ВариантОбработки.Видимость = ТребуетсяУказатьВариантОбработки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОбъектаПрикрепления(Результат, Контекст) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьИсходноеИзображениеНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьИсходноеИзображениеНаСервере(КудаДобавляемСсылка)
	
	РаспознаваниеДокументовСлужебный.ПрикрепитьИсходныеФайлы(Объект.Ссылка, КудаДобавляемСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриблизитьПоКоординатам()
	ПриблизитьПоКоординатам(ДанныеПриближения.Координаты, ДанныеПриближения.СтрокВИзображении);
КонецПроцедуры

&НаКлиенте
Процедура ПриблизитьПоКоординатам(Координаты, СтрокВИзображении)
	
	Если HTMLДокументСформирован Тогда
		Если РаспознаваниеДокументовКлиентСервер.ВсеКоординатыНулевые(Координаты) Тогда
			Элементы.ПолеПросмотра.Документ.defaultView.clean_bbox();
		Иначе
			Элементы.ПолеПросмотра.Документ.defaultView.zoom_to_bbox(Координаты[0], Координаты[1], Координаты[2], Координаты[3], СтрокВИзображении);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДокумента()
	
	ТаблицаДокумента.Загрузить(РаспознаваниеДокументовСлужебный.ЗаполненнаяТаблицаДокумента(Объект));
	
	Если ТаблицаДокумента.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РеквизитыТабличныхЧастей.Сортировать("НомерСтрокиТЧ");
	ИзменяемаяСтрока = ТаблицаДокумента[0];
	
	Для Каждого Запись Из Объект.РеквизитыТабличныхЧастей Цикл
		Если Запись.СтрокаУдалена Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИзменяемаяСтрока.НомерСтроки <> Запись.НомерСтрокиТЧ Тогда
			Отбор = Новый Структура("НомерСтроки", Запись.НомерСтрокиТЧ);
			ИзменяемыеСтроки = ТаблицаДокумента.НайтиСтроки(Отбор);
			ИзменяемаяСтрока = ИзменяемыеСтроки[0];
		КонецЕсли;
		
		Если Запись.ИмяРеквизита = "Номенклатура" Тогда
			НомерСтрокиТаблицы = Запись.НомерСтрокиТЧ;
			ВыбранноеЗначение = ИзменяемаяСтрока[Запись.ИмяРеквизита];
			
			Элемент = УсловноеОформление.Элементы.Добавить();
			ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
			ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Номенклатура");
			
			ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДокумента.НомерСтроки");
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = ИзменяемаяСтрока.НомерСтроки;
			ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДокумента.Номенклатура");
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(255, 0, 0));
			Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СтрШаблон(НСтр("ru = 'Не сопоставлен: %1'"), Запись.РаспознанныйТекст));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриАктивизацииЯчейкиТаблицы(Элемент)
	
	Если ТипЗнч(ТекущийЭлемент) <> Тип("ТаблицаФормы") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийВыбранныйЭлемент = Элемент.ТекущийЭлемент;
	Если ТекущийВыбранныйЭлемент = Неопределено Или Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = ТекущийВыбранныйЭлемент.Имя;
	НомерСтрокиТЧ = Элемент.ТекущиеДанные.НомерСтроки;
	
	Ключ = РаспознаваниеДокументовСлужебныйКлиентСервер.КлючСвойстваЯчеекТаблицы(ИмяРеквизита, НомерСтрокиТЧ);
	
	Свойства = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СвойстваЯчеекТаблицы, Ключ);
	Если Свойства = Неопределено Тогда
		
		ДанныеПриближения = Новый Структура("Координаты, СтрокВИзображении", Неопределено, 0);
		ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.2, Истина);
		
		Возврат;
	КонецЕсли;
	
	ДанныеПриближения = Новый Структура("Координаты, СтрокВИзображении", Свойства.Координаты, Свойства.СтрокВИзображении);
	ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСвойстваЯчеекТаблицы() Экспорт
	
	СвойстваЯчеекТаблицы = ПолучитьИзВременногоХранилища(АдресСвойствЯчеекТаблицы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементыИтогов()
	
	ИтогоСумма = Объект.РеквизитыДокумента[10].Значение;
	ИтогоСуммаНДС = Объект.РеквизитыДокумента[11].Значение;
	ИтогоВсего = Объект.РеквизитыДокумента[12].Значение;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьОтображениеПоТипуДокумента()
	
	Элементы.Продавец.Видимость = (Объект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий);
	Элементы.Покупатель.Видимость = (Объект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий);
	
	Элементы.ПродавецОрганизация.Видимость = (Объект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий);
	Элементы.ПокупательОрганизация.Видимость = (Объект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий);
	
КонецПроцедуры

#КонецОбласти
