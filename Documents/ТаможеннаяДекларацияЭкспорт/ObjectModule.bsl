#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	Если УчетНДСВызовСервера.КлассифицироватьНоменклатуруПоОперациям0(Дата,Организация) Тогда
		КодОперации = "";
	Иначе
		КодОперации = УчетНДСВызовСервера.КодОперацииПоСтавке0ПоУмолчанию(Дата, ОбъектКопирования.Организация);
		
		МассивКодовОпераций = Новый Массив;
		Макет = Отчеты.РегламентированныйОтчетРеестрНДСПриложение5.ПолучитьМакет("СпискиВыбора2015Кв4");
	
		ОбластьМакета = Макет.ПолучитьОбласть("КодыОпераций");
	
		Для Ном = 1 По ОбластьМакета.ВысотаТаблицы Цикл
			МассивКодовОпераций.Добавить(СокрЛП(ОбластьМакета.Область(Ном,1).Текст));
		КонецЦикла;
		
		// Проверям, что полученный по умолчанию код операции 
		// соответсвутет разрешенным кодам операций в таможенной декларации
		
		Если МассивКодовОпераций.Найти(КодОперации) = Неопределено Тогда
			КодОперации = "";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДокументыОснования.Количество() > 0 Тогда
		ДокументОснование = ДокументыОснования[0].ДокументОснование;
	Иначе
		ДокументОснование = Неопределено;
	КонецЕсли;
	
	ИнформацияОДублях = ИнформацияОДубляхТаможенныхДеклараций();
	
	Если ИнформацияОДублях.ЕстьДубли Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ИнформацияОДублях.СообщениеПользователю, ЭтотОбъект, , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.ТаможеннаяДекларацияЭкспорт.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	УчетНДС.СформироватьДвиженияТаможеннаяДекларацияЭкспорт(
		ПараметрыПроведения.СведенияТаможенныхДекларацийЭкспорт,
		ПараметрыПроведения.Реквизиты,
		Движения, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЭтоУчастникЕАЭС = Ложь;
	РФ = Справочники.СтраныМира.Россия;
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		
		Если ДанныеЗаполнения.ПометкаУдаления Тогда
			ТекстСообщения = НСтр("ru = 'Таможенную декларацию на экспорт нельзя выписать на основании документа, помеченного на удаление.'");
			ВызватьИсключение ТекстСообщения
		КонецЕсли;
		
		Если ТипДанныхЗаполнения = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			И ЗначениеЗаполнено(ДанныеЗаполнения.СтранаРеализации) Тогда
			
			ЭтоРФ = ДанныеЗаполнения.СтранаРеализации = РФ;
			
			Если НЕ ЭтоРФ Тогда
				ЭтоУчастникЕАЭС = УчетНДСВызовСервера.ЭтоУчастникЕАЭС(ДанныеЗаполнения.СтранаРеализации);
			КонецЕсли;
			
		Иначе
			
			СтранаРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Контрагент, "СтранаРегистрации");
			ЭтоРФ = СтранаРегистрации = РФ;
			
			Если НЕ ЭтоРФ Тогда
				ЭтоУчастникЕАЭС = УчетНДСВызовСервера.ЭтоУчастникЕАЭС(ДанныеЗаполнения.Контрагент);
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЭтоРФ Тогда
			ТекстСообщения = НСтр("ru = 'Невозможно создать таможенную декларацию на экспорт.
				|В документе основании отражена реализация товаров внутри РФ.'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
	
		Если ЭтоУчастникЕАЭС Тогда
			ТекстСообщения = НСтр("ru = 'Невозможно создать таможенную декларацию на экспорт.
				|В документе основании отражена реализация товаров в страну ЕАЭС.
				|Создайте заявление о ввозе товаров (полученное).'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;

		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
		
	КонецЕсли;
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда

		Если ДанныеЗаполнения.Свойство("Товары") Тогда
			Если ДанныеЗаполнения.Товары.Количество() = 0 Тогда
				ТекстСообщения = НСтр("ru = 'Невозможно создать таможенную декларацию на экспорт.
					|В документе основании отсутствует информация о товарах.'");
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("СтранаРеализации") Тогда
			Если ЗначениеЗаполнено(ДанныеЗаполнения.СтранаРеализации) Тогда
				ЭтоУчастникЕАЭС = УчетНДСВызовСервера.ЭтоУчастникЕАЭС(ДанныеЗаполнения.СтранаРеализации);
			ИначеЕсли ДанныеЗаполнения.Свойство("Контрагент") Тогда
				ЭтоУчастникЕАЭС = УчетНДСВызовСервера.ЭтоУчастникЕАЭС(ДанныеЗаполнения.Контрагент);
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоУчастникЕАЭС Тогда
			ТекстСообщения = НСтр("ru = 'Невозможно создать таможенную декларацию на экспорт.
				|В документе основании отражена реализация товаров в страну ЕАЭС.
				|Создайте заявление о ввозе товаров (полученное).'");
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Ссылка") Тогда
			
			ПометкаУдаленияДокументаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Ссылка,
				"ПометкаУдаления");
			
			Если ПометкаУдаленияДокументаОснования = Истина Тогда
				ТекстСообщения = НСтр("ru = 'Таможенную декларацию на экспорт нельзя выписать на основании документа, помеченного на удаление.'");
				ВызватьИсключение ТекстСообщения;
			Иначе
				ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения.Ссылка);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("ДокументОснование");
	МассивНепроверяемыхРеквизитов.Добавить("СопроводительныеДокументы.ВидДокумента");
	МассивНепроверяемыхРеквизитов.Добавить("СопроводительныеДокументы.НомерТСД");
	МассивНепроверяемыхРеквизитов.Добавить("СопроводительныеДокументы.ДатаТСД");
	
	ПроверитьТаблицуОснований(Отказ);
	ПровестиФЛКНомераДекларации(Отказ);
	ПроверитьСопроводительныеДокументы(Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если НЕ ЗначениеЗаполнено(КодОперации)
		И НЕ УчетНДСВызовСервера.КлассифицироватьНоменклатуруПоОперациям0(Дата, Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Введите код операции. При текущих настройках он обязателен для заполнения.'"), ЭтотОбъект, , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения)
	
	ИнформацияОДублях = ИнформацияОДубляхТаможенныхДеклараций(ДанныеЗаполнения);
	
	Если ИнформацияОДублях.ЕстьДубли Тогда
		ВызватьИсключение ИнформацияОДублях.СообщениеПользователю;
	КонецЕсли;
	
	ИсключаемыеПоля = "Номер, Дата, Проведен, ПометкаУдаления";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, , ИсключаемыеПоля);
	
	Основание = ДокументыОснования.Добавить();
	Основание.ДокументОснование = ДанныеЗаполнения.Ссылка;
	
	ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Если УчетНДСВызовСервера.КлассифицироватьНоменклатуруПоОперациям0(ТекущаяДата,ДанныеЗаполнения.Организация) Тогда
		
		КодОперации = "";
		
	Иначе
		
		КодОперации = УчетНДСВызовСервера.КодОперацииПоСтавке0ПоУмолчанию(ТекущаяДата,ДанныеЗаполнения.Организация);
		
		МассивКодовОпераций = Новый Массив;
		
		Макет = Отчеты.РегламентированныйОтчетРеестрНДСПриложение5.ПолучитьМакет("СпискиВыбора2015Кв4");
	
		ОбластьМакета = Макет.ПолучитьОбласть("КодыОпераций");
	
		Для Ном = 1 По ОбластьМакета.ВысотаТаблицы Цикл
			МассивКодовОпераций.Добавить(СокрЛП(ОбластьМакета.Область(Ном,1).Текст));
		КонецЦикла;
		
		// Проверям, что полученный по умолчанию код операции 
		// соответсвутет разрешенным кодам операций в таможенной декларации
		
		Если МассивКодовОпераций.Найти(КодОперации) = Неопределено Тогда
			КодОперации = "";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИнформацияОДубляхТаможенныхДеклараций(ДокументОтгрузки = Неопределено)
	
	Результат = Новый Структура("ЕстьДубли, СообщениеПользователю");
	
	ЕстьДубли = Ложь;
	СообщенияПользователю = Новый Массив;
	
	СуществующиеДекларации = НайтиТаможенныеДекларацииПоДокументуОтгрузки(ДокументОтгрузки);
	Для Каждого СтрокаТЗ Из СуществующиеДекларации Цикл
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='На основании документа %1 уже введена %2'"),
			СтрокаТЗ.ДокументОтгрузки,
			СтрокаТЗ.Декларация);
		СообщенияПользователю.Добавить(ТекстСообщения);
		ЕстьДубли = Истина;
	КонецЦикла;
	
	СуществующиеДекларации = НайтиТаможенныеДекларацииПоНомеру();
	Если ЗначениеЗаполнено(СуществующиеДекларации) И СуществующиеДекларации.КоличествоДеклараций > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Таможенная декларация номер %1 уже зарегистрирована'"),
			НомерВходящегоДокумента);
		СообщенияПользователю.Добавить(ТекстСообщения);
		ЕстьДубли = Истина;
	КонецЕсли;
	
	СообщениеПользователю = СтрСоединить(СообщенияПользователю, ". ");
	
	Результат.ЕстьДубли             = ЕстьДубли;
	Результат.СообщениеПользователю = СообщениеПользователю;
	
	Возврат Результат;
	
КонецФункции

Функция НайтиТаможенныеДекларацииПоДокументуОтгрузки(ДокументОтгрузки)
	
	Если ЗначениеЗаполнено(ДокументОтгрузки) Тогда
		ДокументыОтгрузки = Новый Массив;
		ДокументыОтгрузки.Добавить(ДокументОтгрузки);
		
	Иначе
		ДокументыОтгрузки = ДокументыОснования.ВыгрузитьКолонку("ДокументОснование");
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументыОснования", ДокументыОтгрузки);
	Запрос.УстановитьПараметр("ЭтаДекларация"     , Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка КАК Декларация,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование КАК ДокументОтгрузки
	|ИЗ
	|	Документ.ТаможеннаяДекларацияЭкспорт.ДокументыОснования КАК ТаможеннаяДекларацияЭкспортДокументыОснования
	|ГДЕ
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование В(&ДокументыОснования)
	|	И НЕ ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка = &ЭтаДекларация
	|	И ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка.Проведен";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция НайтиТаможенныеДекларацииПоНомеру()
	
	Если Не ЗначениеЗаполнено(НомерВходящегоДокумента) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаможеннаяДекларацияЭкспорт.Ссылка) КАК КоличествоДеклараций
	|ИЗ
	|	Документ.ТаможеннаяДекларацияЭкспорт КАК ТаможеннаяДекларацияЭкспорт
	|ГДЕ
	|	ТаможеннаяДекларацияЭкспорт.НомерВходящегоДокумента = &НомерВходящегоДокумента
	|	И НЕ ТаможеннаяДекларацияЭкспорт.ПометкаУдаления
	|	И НЕ ТаможеннаяДекларацияЭкспорт.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("НомерВходящегоДокумента", НомерВходящегоДокумента);
	Запрос.УстановитьПараметр("Ссылка"                 , Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить().Выбрать();
	Результат.Следующий();
	
	Возврат Результат;
	
КонецФункции

#Область ОбработчикиПроверкиЗаполнения

Процедура ПровестиФЛКНомераДекларации(Отказ)
	
	ДлинаНомера = СтрДлина(СокрЛП(НомерВходящегоДокумента));
	Если ДлинаНомера = 23 ИЛИ (ДлинаНомера >= 25 И ДлинаНомера <= 29) Тогда
		Возврат;
		
	Иначе
		ТекстСообщения =
			НСтр("ru='Регистрационный номер таможенной декларации должен состоять либо из 23 символов, либо количество символов должно быть от 25 до 29.'");
			
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле",
			"Корректность",
			"Номер", , ,
			ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.НомерВходящегоДокумента", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьТаблицуОснований(Отказ)
	
	Если ДокументыОснования.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не введено ни одной строки в список ""Основания""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "НадписьВыбор", , Отказ);
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из ДокументыОснования Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.ДокументОснование)
			И НЕ СтрокаТЧ.ДокументОснование.Проведен Тогда
			
			ТекстСообщения = НСтр("ru = 'Таможенную декларацию можно провести только на основании проведенного документа.'");
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Корректность",
				НСтр("ru='Основания'"),
				СтрокаТЧ.НомерСтроки,
				НСтр("ru='Документы-основания таможенной декларации'"),
				ТекстСообщения);
				
			Если ДокументыОснования.Количество() > 1 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "НадписьДокументыОснования", , Отказ);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ДокументОснование", , Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ДокументыОснованияСвернуто = ДокументыОснования.Выгрузить(, "ДокументОснование");
	ДокументыОснованияСвернуто.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ДокументыОснованияСвернуто.ЗаполнитьЗначения(1, "Количество");
	ДокументыОснованияСвернуто.Свернуть("ДокументОснование", "Количество");
	Для Каждого СтрокаДокументОснование Из ДокументыОснованияСвернуто Цикл
		
		Если ЗначениеЗаполнено(СтрокаДокументОснование.ДокументОснование)
			И СтрокаДокументОснование.Количество > 1 Тогда
			
			Отбор = Новый Структура("ДокументОснование", СтрокаДокументОснование.ДокументОснование);
			
			СтрокиТЧ = ДокументыОснования.НайтиСтроки(Отбор);
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документ %1 уже выбран в строке %2. Повторный выбор не допускается.'"),
				СтрокаДокументОснование.ДокументОснование,
				СтрокиТЧ[0].НомерСтроки);
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Корректность",
				НСтр("ru='Основания'"),
				СтрокиТЧ[1].НомерСтроки,
				НСтр("ru='Документы-основания таможенной декларации'"),
				ТекстСообщения);
				
			Если ДокументыОснования.Количество() > 1 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "НадписьДокументыОснования", , Отказ);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ДокументОснование", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьСопроводительныеДокументы(Отказ)
	
	Для Каждого СопроводительныйДокумент Из СопроводительныеДокументы Цикл
		Если СопроводительныйДокумент.КодТС = "71" ИЛИ СопроводительныйДокумент.КодТС = "72" Тогда
			Продолжить;
		КонецЕсли;
		
		Префикс = "Объект.СопроводительныеДокументы[%1].";
		Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Префикс, Формат(СопроводительныйДокумент.НомерСтроки - 1, "ЧН=0; ЧГ="));
		
		Если НЕ ЗначениеЗаполнено(СопроводительныйДокумент.ВидДокумента) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Заполнение",
				НСтр("ru='Сопроводительный документ'"),
				СопроводительныйДокумент.НомерСтроки,
				НСтр("ru='Товаросопроводительные документы'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Префикс + "ВидДокумента", , Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СопроводительныйДокумент.НомерТСД) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Заполнение",
				НСтр("ru='Номер'"),
				СопроводительныйДокумент.НомерСтроки,
				НСтр("ru='Товаросопроводительные документы'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Префикс + "НомерТСД", , Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СопроводительныйДокумент.ДатаТСД) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Заполнение",
				НСтр("ru='Дата'"),
				СопроводительныйДокумент.НомерСтроки,
				НСтр("ru='Товаросопроводительные документы'"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Префикс + "ДатаТСД", , Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли