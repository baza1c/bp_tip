#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВремяДокументаПоУмолчанию() Экспорт
	
	Возврат Новый Структура("Часы, Минуты", 7, 0);
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область СчетаУчета

Процедура УстановитьПравилаЗаполненияСчетовУчета(Правила) Экспорт
	
	// Шапка
	
	// расчеты с поставщиком
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКонтрагентом", "РасчетыСПоставщиком");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТребуетсяУчетРасчетов");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаРасчетовПоАвансам", "АвансыПоставщику");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТребуетсяУчетАвансов");
	
	// расчеты с комиссионером
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКомиссионером", "РасчетыСПокупателем");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТребуетсяУчетРасчетов");
	
	// расчеты по таре
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовПоТаре", "ВозвратнаяТараПолученная");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УчетТарыНаБалансе");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "МатериалыПереработчика");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПростаяФорма"); // Локальное условие
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ОбъектыСтроительства"); // Локальное условие
	
	// расчеты по аренде с признанием прав пользования предметами аренды и по лизингу
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКонтрагентом", "АрендныеПлатежи");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УслугиАрендыЛизинга");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаРасчетовПоАвансам", "АвансыПоставщику");
	
	// расчеты по факторингу
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКонтрагентом", "РасчетыПоФакторингу");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УслугиФакторинга");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаРасчетовПоАвансам", "АвансыПоставщику");
	
	// Способ отражения расходов по амортизации
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СпособОтраженияРасходовПоАмортизации", "СпособОтраженияРасходовПоАмортизации");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "МестонахождениеОС", "МестонахождениеОС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОсновныеСредства");
	
	// Табличная часть Товары
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",      "ЗапасыКромеПринятыхНаКомиссиюИлиВПереработку");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Комитент",     "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "МатериалыПереработчика");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПродажаТоваровКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПриобретениеТоваровДляКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ТоварыВЦенахПродажи");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетЗатрат", "РасходыНаМалоценку");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "МатериалыПереработчика");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПродажаТоваровКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПриобретениеТоваровДляКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ТоварыВЦенахПродажи");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто1",                "СубконтоЗатрат1");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто2",                "СубконтоЗатрат2");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто3",                "СубконтоЗатрат3");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "ПодразделениеЗатрат",      "ПодразделениеЗатрат");
		
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",      "МатериалыПринятыеВПереработку");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "МатериалыПереработчика");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",      "ТоварыПринятыеНаКомиссию");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПродажаТоваровКомитента");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчета",      "ТоварыПриобретенныеДляКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Комитент",     "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПриобретениеТоваровДляКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ТоварыВЦенахПродажи");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчетаНДС",   "ПредъявленныйНДС_Запасы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Комитент",     "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "МатериалыПереработчика");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПродажаТоваровКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПриобретениеТоваровДляКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ТоварыВЦенахПродажи");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СпособУчетаНДС", "СпособУчетаНДСПредъявленного");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Комитент",     "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "МатериалыПереработчика");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПродажаТоваровКомитента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ПриобретениеТоваровДляКомитента");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетРасчетов",      "РасчетыСКомитентомЗаПриобретенныеДляНегоТоварыУслуги");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Комитент",         "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Контрагент",         "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента", "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ПриобретениеТоваровДляКомитента");
	
	// Табличная часть ВозвратнаяТара
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ВозвратнаяТара", "СчетУчета", "ВозвратнаяТараНаСкладе");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УчетТарыНаБалансе");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ВозвратнаяТара", "СчетУчета", "ВозвратнаяТараЗаБалансом");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "УчетТарыНаБалансе");
		
	// Табличная часть Услуги
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СпособУчетаНДС", "СпособУчетаНДСПредъявленного");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СчетЗатрат",     "Затраты");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "УслугиАрендыЛизинга");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "УслугиФакторинга");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто1",                "СубконтоЗатрат1");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто2",                "СубконтоЗатрат2");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто3",                "СубконтоЗатрат3");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "ПодразделениеЗатрат",      "ПодразделениеЗатрат");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетУчетаНДС",             "ПредъявленныйНДС_Расходы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС", "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СчетЗатратНУ",     "Затраты");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "УслугиАрендыЛизинга");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "УслугиФакторинга");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Основание",    "СчетЗатрат");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеПроверкиЗаполнения(Правила, "ПлательщикНалогаНаПрибыль");
	
	СчетаУчетаВДокументах.ДобавитьУровеньПодчиненности(Правила);
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СубконтоНУ1", "СубконтоЗатрат1");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Основание",    "Субконто1");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СубконтоНУ2", "СубконтоЗатрат2");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Основание",    "Субконто2");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СубконтоНУ3", "СубконтоЗатрат3");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Основание",    "Субконто3");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СчетЗатрат",     "АрендныеОбязательства");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УслугиАрендыЛизинга");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СчетУчетаНДС",   "ПредъявленныйНДС_Расходы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УслугиАрендыЛизинга");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СчетЗатрат",     "РасходыУслугиФакторинга");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УслугиФакторинга");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто1",           "СубконтоЗатрат1");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто2",           "СубконтоЗатрат2");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто3",           "СубконтоЗатрат3");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "ПодразделениеЗатрат", "ПодразделениеЗатрат");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СчетЗатратНУ",     "РасходыУслугиФакторинга");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УслугиФакторинга");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СубконтоНУ1",           "СубконтоЗатрат1");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СубконтоНУ2",           "СубконтоЗатрат2");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "СубконтоНУ3",           "СубконтоЗатрат3");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеПроверкиЗаполнения(Правила, "ПлательщикНалогаНаПрибыль");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СчетУчетаНДС",   "ПредъявленныйНДС_Расходы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "УслугиФакторинга");
	
	// Табличная часть Оборудование
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Оборудование", "СчетУчета",      "ОборудованиеНаСкладе");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Оборудование", "СчетУчетаНДС",   "ПредъявленныйНДС_ОсновныеСредства");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Оборудование", "СпособУчетаНДС", "СпособУчетаНДСПредъявленного");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	
	// Табличная часть ОбъектыСтроительства
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОбъектыСтроительства", "СчетУчета",      "ОбъектыСтроительства");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОбъектСтроительства", "ОбъектСтроительства");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОбъектыСтроительства", "СчетУчетаНДС",   "ПредъявленныйНДС_ОбъектыСтроительства");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОбъектСтроительства", "ОбъектСтроительства");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",            "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОбъектыСтроительства", "СпособУчетаНДС", "СпособУчетаНДСПредъявленного");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОбъектСтроительства", "ОбъектСтроительства");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",            "СуммаНДС");
	
	// Табличная часть ОсновныеСредства
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОсновныеСредства", "СчетУчета", "ОсновныеСредства");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОсновноеСредство", "ОсновноеСредство");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОсновныеСредства", "СчетНачисленияАмортизации", "ОсновныеСредстваАмортизация");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОсновноеСредство", "ОсновноеСредство");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ЕдиницаУчета", "ЕдиницаУчета");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОсновныеСредства");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОсновныеСредства", "СчетУчетаНДС", "ПредъявленныйНДС_ОсновныеСредства");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОсновноеСредство", "ОсновноеСредство");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС", "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОсновныеСредства");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОсновныеСредства", "СпособУчетаНДС", "СпособУчетаНДСпоОСиНМА");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОсновноеСредство", "ОсновноеСредство");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС", "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОсновныеСредства");
	
	// Табличная часть АгентскиеУслуги
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "АгентскиеУслуги", "СчетРасчетов",  "РасчетыСКомитентомЗаПриобретенныеДляНегоТоварыУслуги");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Контрагент",         "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ДоговорКонтрагента", "ДоговорКонтрагента");
	
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Дата");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Организация");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Склад");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Подразделение",            "ПодразделениеОрганизации");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "СпособЗачетаАвансов");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "НДСВключенВСтоимость");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "ОбъектыПредназначеныДляСдачиВАренду");
	
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ПростаяФорма",           "ВидОперации");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ОбъектыСтроительства",   "ВидОперации");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "МатериалыПереработчика", "ВидОперации");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "УслугиАрендыЛизинга",    "ВидОперации, ВедетсяУчетПредметовАренды");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "УслугиФакторинга",       "ВидОперации, ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ТребуетсяУчетРасчетов",  "ВидОперации, ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ТребуетсяУчетАвансов",   "ВидОперации, ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ОсновныеСредства",       "ВидОперации");
	
	// Табличная часть Топливо
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Топливо", "СчетУчетаНДС", "ПредъявленныйНДС_Запасы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Топливо", "СпособУчетаНДС", "СпособУчетаНДСПредъявленного");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	
	// Табличная часть Права
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Права", "СпособУчетаНДС",   "СпособУчетаНДСПредъявленного");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "ОбъектПрава");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Права", "Счет",             "Затраты");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "ОбъектПрава");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто1",             "СубконтоЗатрат1");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто2",             "СубконтоЗатрат2");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Субконто3",             "СубконтоЗатрат3");
	СчетаУчетаВДокументах.ДобавитьПодчиненноеПравилоЗаполнения(Правила, "Подразделение",         "ПодразделениеЗатрат");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	
	// Роль правила может быть ограничена функциональной опцией "ВестиУчетНМА". В этом случае срабатывает правило, следующее ниже.
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Права", "СчетУчетаНДС", "ПредъявленныйНДС_НематериальныеАктивы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "ОбъектПрава");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "Права");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Права", "СчетУчетаНДС",   "ПредъявленныйНДС_Расходы");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "ОбъектПрава");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СуммаНДС",     "СуммаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "Права");
	
КонецПроцедуры

Процедура ДополнитьДанныеЗаполненияСчетовУчета(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("ПростаяФорма") Тогда
		
		ДанныеЗаполнения.ПростаяФорма = ИспользоватьПростуюФорму(ДанныеЗаполнения.ВидОперации);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ОбъектыСтроительства") Тогда
		
		ДанныеЗаполнения.ОбъектыСтроительства = (ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("МатериалыПереработчика") Тогда
		
		ДанныеЗаполнения.МатериалыПереработчика = (ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ТребуетсяУчетРасчетов") Или ДанныеЗаполнения.Свойство("ТребуетсяУчетАвансов") Тогда
		
		ОсобенностиДокумента = ОсобенностиУчетаРасчетов(ДанныеЗаполнения.ВидОперации, ДанныеЗаполнения.ДоговорКонтрагента);
			
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ОсобенностиДокумента);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("УслугиАрендыЛизинга") Тогда
		
		ДанныеЗаполнения.УслугиАрендыЛизинга = ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
			И ДанныеЗаполнения.ВедетсяУчетПредметовАренды
			Или ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга;
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("УслугиФакторинга") Тогда
		
		Если (ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиФакторинга) Тогда
			ДанныеЗаполнения.УслугиФакторинга = Истина;
		ИначеЕсли ЗначениеЗаполнено(ДанныеЗаполнения.ДоговорКонтрагента) Тогда
			ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.ДоговорКонтрагента, "ВидДоговора");
			ДанныеЗаполнения.УслугиФакторинга = (ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией);
		Иначе
			ДанныеЗаполнения.УслугиФакторинга = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ОсновныеСредства") Тогда
		
		ДанныеЗаполнения.ОсновныеСредства = (ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства);
		
	КонецЕсли;
	
КонецПроцедуры

#Область УстаревшиеПроцедурыИФункции

// Устарела: Следует использовать ЗаполнитьПередЗаписью(), 
// ЗаполнитьПередОтображениемПользователю(), Заполнить(), 
// ЗаполнитьОбъектПриИзменении(), ЗаполнитьРеквизитыПриИзменении(),
// либо перед записью документа устанавливать дополнительное свойство 
// ЗаполнитьСчетаУчетаПередЗаписью
//
// Параметры:
//  Объект		- ДокументОбъект
Процедура ЗаполнитьСчетаУчетаРасчетов(Объект) Экспорт
	
	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаРасчетов(Объект);
	
КонецПроцедуры

// Устарела: Следует использовать ЗаполнитьПередЗаписью(), 
// ЗаполнитьПередОтображениемПользователю(), Заполнить(), 
// ЗаполнитьОбъектПриИзменении(), ЗаполнитьСтроки(),
// либо перед записью документа устанавливать дополнительное свойство 
// ЗаполнитьСчетаУчетаПередЗаписью
//
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, ИмяТабличнойЧасти) Экспорт

	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаВТабличнойЧасти(
		Объект,
		ИмяТабличнойЧасти);

КонецПроцедуры

// Устарела: Следует использовать ЗаполнитьПередЗаписью(), 
// ЗаполнитьПередОтображениемПользователю(), Заполнить(), 
// ЗаполнитьОбъектПриИзменении(), ЗаполнитьСтроки(),
// либо перед записью документа устанавливать дополнительное свойство 
// ЗаполнитьСчетаУчетаПередЗаписью
//
// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  Объект                - ДокументОбъект или соответствующие данные формы
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - оставлен для совместимости; не используется
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(Объект, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СведенияОНоменклатуре = Неопределено) Экспорт

	СчетаУчетаВДокументах.ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(
		Объект,
		СтрокаТабличнойЧасти,
		ИмяТабличнойЧасти);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

Функция ЕстьКомитентыНаЗакупку(Объект) Экспорт

	Результат = Ложь;
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		Если ЗначениеЗаполнено(СтрокаТовары.Контрагент) Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ИспользоватьПростуюФорму(ВидОперации) Экспорт
	
	Возврат
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги)
		Или
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары)
		Или
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды)
		Или
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга)
		Или
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства)
		Или
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков);
	
КонецФункции

Функция РеквизитыЗаСсылками(ВидОперации) Экспорт
	
	РеквизитыЗаСсылками = Новый Соответствие;
	
	// Прячутся за "ПорядокУчетаРасчетов"
	Для Каждого ОписаниеРеквизита Из УчетВзаиморасчетовФормы.РеквизитыДокументаПорядокУчетаРасчетов() Цикл
		РеквизитыЗаСсылками.Вставить(ОписаниеРеквизита.Ключ, "ПорядокУчетаРасчетов");
	КонецЦикла;
	
	// Прячутся за "АналитикаУчета"
	АналитикаУчета = Новый Массив;
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги Тогда
		
		АналитикаУчета.Добавить("Услуги.СчетЗатрат");
		АналитикаУчета.Добавить("Услуги.ПодразделениеЗатрат");
		АналитикаУчета.Добавить("Услуги.Субконто1");
		АналитикаУчета.Добавить("Услуги.Субконто2");
		АналитикаУчета.Добавить("Услуги.Субконто3");
		АналитикаУчета.Добавить("Услуги.СчетЗатратНУ");
		АналитикаУчета.Добавить("Услуги.СубконтоНУ1");
		АналитикаУчета.Добавить("Услуги.СубконтоНУ2");
		АналитикаУчета.Добавить("Услуги.СубконтоНУ3");
		АналитикаУчета.Добавить("Услуги.СчетУчетаНДС");
		АналитикаУчета.Добавить("Услуги.СпособУчетаНДС");
		АналитикаУчета.Добавить("Услуги.ОтражениеВУСН");
	
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары Тогда
		
		АналитикаУчета.Добавить("Товары.СчетЗатрат");
		АналитикаУчета.Добавить("Товары.ПодразделениеЗатрат");
		АналитикаУчета.Добавить("Товары.Субконто1");
		АналитикаУчета.Добавить("Товары.Субконто2");
		АналитикаУчета.Добавить("Товары.Субконто3");
	
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права Тогда
		
		АналитикаУчета.Добавить("Права.Счет");
		АналитикаУчета.Добавить("Права.Подразделение");
		АналитикаУчета.Добавить("Права.Субконто1");
		АналитикаУчета.Добавить("Права.Субконто2");
		АналитикаУчета.Добавить("Права.Субконто3");	
		
	КонецЕсли;
	
	Для Каждого ИмяРеквизита Из АналитикаУчета Цикл
		РеквизитыЗаСсылками.Вставить(ИмяРеквизита, "АналитикаУчета");
	КонецЦикла;
	
	Возврат РеквизитыЗаСсылками;
	
КонецФункции

Функция ОсобенностиУчетаРасчетов(ВидОперации, ДоговорКонтрагента) Экспорт
	
	ОсобенностиДокумента = УчетВзаиморасчетовФормы.НовыйОсобенностиУчетаРасчетовДокумента();
	
	ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеПлатежейПоставщикам");
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		ОсобенностиДокумента.ТребуетсяУчетРасчетов = Ложь;
		ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = Ложь;
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиФакторинга Тогда
		ОсобенностиДокумента.ТребуетсяУчетРасчетов = Ложь;
		ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = Ложь;
	ИначеЕсли Не ИспользоватьПростуюФорму(ВидОперации) И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
		Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
			ОсобенностиДокумента.ТребуетсяУчетАвансов = Ложь;
		ИначеЕсли ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией Тогда
			ОсобенностиДокумента.ТребуетсяУчетРасчетов = Ложь;
			ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = Ложь;
		КонецЕсли;
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги 
		И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
		Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
			ОсобенностиДокумента.ТребуетсяУчетРасчетовСКомиссионером = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ОсобенностиДокумента.ТребуетсяУчетРасчетов Тогда
		ОсобенностиДокумента.ТребуетсяУчетАвансов = Ложь;
		ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = Ложь;
	КонецЕсли;
	
	Возврат ОсобенностиДокумента;
	
КонецФункции

Функция ТекстЗапросаДанныеДляОбновленияЦенДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугВозвратнаяТара.Номенклатура,
	|	ПоступлениеТоваровУслугВозвратнаяТара.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	ИСТИНА КАК ЦенаВключаетНДС
	|ПОМЕСТИТЬ ВозвратнаяТара
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ВозвратнаяТара КАК ПоступлениеТоваровУслугВозвратнаяТара
	|ГДЕ
	|	ПоступлениеТоваровУслугВозвратнаяТара.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслугТовары.Цена КАК Цена,
	|	&Валюта КАК Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС КАК ЦенаВключаетНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
	|	И НЕ ПоступлениеТоваровУслугТовары.Номенклатура В
	|				(ВЫБРАТЬ
	|					ВозвратнаяТара.Номенклатура
	|				ИЗ
	|					ВозвратнаяТара КАК ВозвратнаяТара)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугУслуги.Номенклатура,
	|	ПоступлениеТоваровУслугУслуги.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
	|ГДЕ
	|	ПоступлениеТоваровУслугУслуги.Ссылка = &Ссылка
	|	И НЕ ПоступлениеТоваровУслугУслуги.Номенклатура В
	|				(ВЫБРАТЬ
	|					ВозвратнаяТара.Номенклатура
	|				ИЗ
	|					ВозвратнаяТара КАК ВозвратнаяТара)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВозвратнаяТара.Номенклатура,
	|	ВозвратнаяТара.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	ВозвратнаяТара.ЦенаВключаетНДС
	|ИЗ
	|	ВозвратнаяТара КАК ВозвратнаяТара
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Номенклатура,
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслугАгентскиеУслуги
	|ГДЕ
	|	ПоступлениеТоваровУслугАгентскиеУслуги.Ссылка = &Ссылка
	|	И НЕ ПоступлениеТоваровУслугАгентскиеУслуги.Номенклатура В
	|				(ВЫБРАТЬ
	|					ВозвратнаяТара.Номенклатура
	|				ИЗ
	|					ВозвратнаяТара КАК ВозвратнаяТара)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугОборудование.Номенклатура,
	|	ПоступлениеТоваровУслугОборудование.Цена,
	|	&Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
	|ГДЕ
	|	ПоступлениеТоваровУслугОборудование.Ссылка = &Ссылка
	|	И НЕ ПоступлениеТоваровУслугОборудование.Номенклатура В
	|				(ВЫБРАТЬ
	|					ВозвратнаяТара.Номенклатура
	|				ИЗ
	|					ВозвратнаяТара КАК ВозвратнаяТара)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Цена,
	|	Валюта";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает признак, что документ не может быть документом расхода по федеральному инвестиционному вычету.
// см. ИнвестиционныйНалоговыйВычет.МожноПрименятьНалоговыйВычет.
// Вызов функции универсален для менеджеров документов, описанных в параметре МенеджерДокумента
// функции ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета.
// 
// Параметры:
//  КритерииПримененияВычета - см. ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета
// 
// Возвращаемое значение:
//  Булево
//
Функция ЗапрещеноПрименятьНалоговыйВычет(КритерииПримененияВычета) Экспорт
	
	СписокДопустимыхОпераций = СписокОперацийПрименяетсяИнвестиционныйВычет();
	Если СписокДопустимыхОпераций.НайтиПоЗначению(КритерииПримененияВычета.ВидОперации) = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ВидОперации)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ТекстЗапросаДанныеДляПечатиСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("ВременнаяТаблицаНаличиеТоваров",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_Реквизиты",                      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты",                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_РублевыеСуммыДокументовВВалюте", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаРасхожденияПриПриемке",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента",         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СведенияОПрослеживаемости",         НомераТаблиц.Количество());
	
	// В случае включения НДС в стоимость проводки по НДС не будет в Хозрасчетном,
	// поэтому для таблицы СуммыПроводок используем НДСПредъявленный.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицыТоваров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ НаличиеТоваров
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ТаблицаТовары.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ТаблицаОборудование.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Оборудование КАК ТаблицаОборудование
	|	ГДЕ
	|		ТаблицаОборудование.Ссылка = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ТаблицаОбъектыСтроительства.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ТаблицаОбъектыСтроительства
	|	ГДЕ
	|		ТаблицаОбъектыСтроительства.Ссылка = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ТаблицаОсновныеСредства.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ТаблицаОсновныеСредства
	|	ГДЕ
	|		ТаблицаОсновныеСредства.Ссылка = &ДокументОснование
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ТаблицаТопливо.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Топливо КАК ТаблицаТопливо
	|	ГДЕ
	|		ТаблицаТопливо.Ссылка = &ДокументОснование) КАК ТаблицыТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА &ИспользуетсяПостановлениеНДС1137
	|				И Реквизиты.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку)
	|			ТОГДА ВЫРАЗИТЬ(&СчетФактура КАК Документ.СчетФактураПолученный).Продавец
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	Реквизиты.Грузополучатель КАК Грузополучатель,
	|	Реквизиты.Грузоотправитель КАК Грузоотправитель,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ЕстьРасхождения КАК ЕстьРасхождения
	|ПОМЕСТИТЬ ВТ_Реквизиты
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	"""" КАК ЦифровойИндексОбособленногоПодразделения,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА &ПредставлениеПоставщика
	|		КОГДА Реквизиты.Контрагент.ОбособленноеПодразделение
	|				И Реквизиты.Контрагент.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК Поставщик,
	|	Реквизиты.Контрагент.ИНН КАК ИННпоставщика,
	|	ВЫБОР
	|		КОГДА Реквизиты.Грузоотправитель.ОбособленноеПодразделение
	|				И Реквизиты.Грузоотправитель.ГоловнойКонтрагент = Реквизиты.Контрагент
	|				И Реквизиты.Грузоотправитель.ГоловнойКонтрагент.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Реквизиты.Грузоотправитель
	|		ИНАЧЕ Реквизиты.Контрагент
	|	КОНЕЦ КАК ОбособленноеПодразделениеПоставщика,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА &Грузоотправитель
	|		КОГДА Реквизиты.Грузоотправитель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.Грузоотправитель
	|		КОГДА Реквизиты.Контрагент.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Контрагент
	|		ИНАЧЕ ""он же""
	|	КОНЕЦ КАК Грузоотправитель,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Покупатель,
	|	Реквизиты.Организация.ИНН КАК ИННпокупателя,
	|	ВЫБОР
	|		КОГДА Реквизиты.ПодразделениеОрганизации.ОбособленноеПодразделение
	|			ТОГДА Реквизиты.ПодразделениеОрганизации
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК ОбособленноеПодразделениеПокупателя,
	|	ВЫБОР
	|		КОГДА Реквизиты.Грузополучатель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА Реквизиты.Грузополучатель
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Грузополучатель,
	|	Реквизиты.ВалютаДокумента КАК Валюта,
	|	Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ДоговорКонтрагента.Представление КАК Основание,
	|	Реквизиты.ДоговорКонтрагента.Дата КАК ОснованиеДата,
	|	Реквизиты.ДоговорКонтрагента.Номер КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА НаличиеТоваров.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьТовары,
	|	"""" КАК АдресДоставки,
	|	"""" КАК СведенияОТранспортировкеИГрузе,
	|	НЕОПРЕДЕЛЕНО КАК ОтветственныйЗаОформление,
	|	"""" КАК СопроводительныеДокументы,
	|	НЕОПРЕДЕЛЕНО КАК Перевозчик,
	|	ЛОЖЬ КАК ПеревозкаАвтотранспортом,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА &АдресПоставщика
	|		ИНАЧЕ """"""""
	|	КОНЕЦ КАК АдресПоставщика,
	|	ВЫБОР
	|		КОГДА &СводныйСФКомиссияПоЗакупке
	|			ТОГДА &ИННКПППоставщика
	|		ИНАЧЕ """"""""
	|	КОНЕЦ КАК ИННКПППоставщика,
	|	&СводныйСФКомиссияПоЗакупке КАК СводныйСФКомиссияПоЗакупке,
	|	"""" КАК Исполнитель,
	|	"""" КАК ИсполнительНаОсновании,
	|	"""" КАК ОтпускПроизвел,
	|	"""" КАК ДоверенностьНомер,
	|	"""" КАК ДоверенностьДата,
	|	"""" КАК ДоверенностьВыдана,
	|	"""" КАК ДоверенностьЧерезКого,
	|	"""" КАК ЗаЗаказчикаНаОсновании,
	|	Реквизиты.ЕстьРасхождения КАК ЕстьРасхождения
	|ИЗ
	|	ВТ_Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаличиеТоваров КАК НаличиеТоваров
	|		ПО Реквизиты.Ссылка = НаличиеТоваров.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктОРасхождениях.КоличествоПоДокументу КАК КоличествоПоДокументу,
	|	АктОРасхождениях.ЦенаПоДокументу КАК ЦенаПоДокументу,
	|	АктОРасхождениях.СуммаПоДокументу КАК СуммаПоДокументу,
	|	АктОРасхождениях.СуммаНДСПоДокументу КАК СуммаНДСПоДокументу,
	|	АктОРасхождениях.НомерГТДПоДокументу КАК НомерГТДПоДокументу,
	|	АктОРасхождениях.СтранаПроисхожденияПоДокументу КАК СтранаПроисхожденияПоДокументу,
	|	АктОРасхождениях.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ АктыОРасхождениях
	|ИЗ
	|	Документ.АктОРасхождениях.Товары КАК АктОРасхождениях
	|ГДЕ
	|	НЕ АктОРасхождениях.Ссылка.ПометкаУдаления
	|	И АктОРасхождениях.Ссылка.ДокументПоступления = &ДокументОснование
	|	И АктОРасхождениях.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АктОРасхождениях.КоличествоПоДокументу,
	|	АктОРасхождениях.ЦенаПоДокументу,
	|	АктОРасхождениях.СуммаПоДокументу,
	|	АктОРасхождениях.СуммаНДСПоДокументу,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	АктОРасхождениях.ИдентификаторСтроки
	|ИЗ
	|	Документ.АктОРасхождениях.Услуги КАК АктОРасхождениях
	|ГДЕ
	|	НЕ АктОРасхождениях.Ссылка.ПометкаУдаления
	|	И АктОРасхождениях.Ссылка.ДокументПоступления = &ДокументОснование
	|	И АктОРасхождениях.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АктОРасхождениях.КоличествоПоДокументу,
	|	АктОРасхождениях.ЦенаПоДокументу,
	|	АктОРасхождениях.СуммаПоДокументу,
	|	АктОРасхождениях.СуммаНДСПоДокументу,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	АктОРасхождениях.ИдентификаторСтроки
	|ИЗ
	|	Документ.АктОРасхождениях.АгентскиеУслуги КАК АктОРасхождениях
	|ГДЕ
	|	НЕ АктОРасхождениях.Ссылка.ПометкаУдаления
	|	И АктОРасхождениях.Ссылка.ДокументПоступления = &ДокументОснование
	|	И АктОРасхождениях.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерТабЧасти,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Товар,
	|	ТаблицаТовары.Номенклатура.Код КАК ТоварКод,
	|	ТаблицаТовары.Номенклатура.Артикул КАК ТоварАртикул,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.СтранаПроисхожденияПоДокументу
	|		ИНАЧЕ ТаблицаТовары.СтранаПроисхождения
	|	КОНЕЦ КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.СтранаПроисхожденияПоДокументу.НаименованиеПолное
	|		ИНАЧЕ ТаблицаТовары.СтранаПроисхождения.НаименованиеПолное
	|	КОНЕЦ КАК ПредставлениеСтраны,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.НомерГТДПоДокументу
	|		ИНАЧЕ ТаблицаТовары.НомерГТД
	|	КОНЕЦ КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.НомерГТДПоДокументу.Представление
	|		ИНАЧЕ ТаблицаТовары.НомерГТД.Представление
	|	КОНЕЦ КАК ПредставлениеГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.НомерГТДПоДокументу.РегистрационныйНомер
	|		ИНАЧЕ ТаблицаТовары.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ КАК РегистрационныйНомерТД,
	|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.КоличествоПоДокументу
	|		ИНАЧЕ ТаблицаТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.ЦенаПоДокументу
	|		ИНАЧЕ ТаблицаТовары.Цена
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.СуммаПоДокументу
	|		ИНАЧЕ ТаблицаТовары.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.СуммаНДСПоДокументу
	|		ИНАЧЕ ТаблицаТовары.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	ТаблицаТовары.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ТаблицаТовары.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ТаблицаТовары.СчетУчета.Забалансовый, ЛОЖЬ) КАК ЭтоКомиссия,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0) КАК СуммаБезНДСРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСРуб,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК КонтрагентСводныйСФ,
	|	НЕОПРЕДЕЛЕНО КАК ПериодичностьУслуги,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка) КАК ТоварКодТНВЭД,
	|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаТовары.Номенклатура.КодТНВЭД КАК НоменклатураКодТНВЭД,
	|	0 КАК НалоговаяБазаНДС,
	|	0 КАК СуммаАкциза
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ТаблицаТовары.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|			И (ТаблицаТовары.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктыОРасхождениях КАК АктОРасхождении
	|		ПО (АктОРасхождении.ИдентификаторСтроки = ТаблицаТовары.ИдентификаторСтроки)
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаОборудование.НомерСтроки,
	|	ТаблицаОборудование.Номенклатура,
	|	ТаблицаОборудование.Номенклатура.Код,
	|	ТаблицаОборудование.Номенклатура.КодОКВЭД2,
	|	ТаблицаОборудование.Номенклатура.НаименованиеПолное,
	|	ТаблицаОборудование.СтранаПроисхождения,
	|	ТаблицаОборудование.СтранаПроисхождения.НаименованиеПолное,
	|	ТаблицаОборудование.НомерГТД,
	|	ТаблицаОборудование.НомерГТД.Представление,
	|	ТаблицаОборудование.НомерГТД.РегистрационныйНомер,
	|	ТаблицаОборудование.Номенклатура.ЕдиницаИзмерения,
	|	ТаблицаОборудование.Количество,
	|	ТаблицаОборудование.Цена,
	|	ТаблицаОборудование.Сумма,
	|	ТаблицаОборудование.СуммаНДС,
	|	ТаблицаОборудование.СтавкаНДС,
	|	ЛОЖЬ,
	|	ТаблицаОборудование.Ссылка.СуммаВключаетНДС,
	|	ТаблицаОборудование.Ссылка,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
	|	ТаблицаОборудование.ИдентификаторСтроки,
	|	ТаблицаОборудование.Номенклатура.КодТНВЭД,
	|	0,
	|	0
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ТаблицаОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ТаблицаОборудование.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Оборудование))
	|			И (ТаблицаОборудование.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
	|ГДЕ
	|	ТаблицаОборудование.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ТаблицаУслуги.НомерСтроки,
	|	ТаблицаУслуги.Номенклатура,
	|	ТаблицаУслуги.Номенклатура.Код,
	|	ТаблицаУслуги.Номенклатура.Артикул,
	|	ТаблицаУслуги.Содержание,
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	ТаблицаУслуги.Номенклатура.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.КоличествоПоДокументу
	|		ИНАЧЕ ТаблицаУслуги.Количество
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.ЦенаПоДокументу
	|		ИНАЧЕ ТаблицаУслуги.Цена
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.СуммаПоДокументу
	|		ИНАЧЕ ТаблицаУслуги.Сумма
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУслуги.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.СуммаНДСПоДокументу
	|		ИНАЧЕ ТаблицаУслуги.СуммаНДС
	|	КОНЕЦ,
	|	ТаблицаУслуги.СтавкаНДС,
	|	ИСТИНА,
	|	ТаблицаУслуги.Ссылка.СуммаВключаетНДС,
	|	ТаблицаУслуги.Ссылка,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ТаблицаУслуги.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|			И (ТаблицаУслуги.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктыОРасхождениях КАК АктОРасхождении
	|		ПО (АктОРасхождении.ИдентификаторСтроки = ТаблицаУслуги.ИдентификаторСтроки)
	|ГДЕ
	|	ТаблицаУслуги.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	ТаблицаАгентскиеУслуги.НомерСтроки,
	|	ТаблицаАгентскиеУслуги.Номенклатура,
	|	ТаблицаАгентскиеУслуги.Номенклатура.Код,
	|	ТаблицаАгентскиеУслуги.Номенклатура.Артикул,
	|	ТаблицаАгентскиеУслуги.Содержание,
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	ТаблицаАгентскиеУслуги.Номенклатура.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаАгентскиеУслуги.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.КоличествоПоДокументу
	|		ИНАЧЕ ТаблицаАгентскиеУслуги.Количество
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаАгентскиеУслуги.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.ЦенаПоДокументу
	|		ИНАЧЕ ТаблицаАгентскиеУслуги.Цена
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаАгентскиеУслуги.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.СуммаПоДокументу
	|		ИНАЧЕ ТаблицаАгентскиеУслуги.Сумма
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаАгентскиеУслуги.Ссылка.ЕстьРасхождения
	|			ТОГДА АктОРасхождении.СуммаНДСПоДокументу
	|		ИНАЧЕ ТаблицаАгентскиеУслуги.СуммаНДС
	|	КОНЕЦ,
	|	ТаблицаАгентскиеУслуги.СтавкаНДС,
	|	ИСТИНА,
	|	ТаблицаАгентскиеУслуги.Ссылка.СуммаВключаетНДС,
	|	ТаблицаАгентскиеУслуги.Ссылка,
	|	ИСТИНА,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ТаблицаАгентскиеУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ТаблицаАгентскиеУслуги.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|			И (ТаблицаАгентскиеУслуги.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктыОРасхождениях КАК АктОРасхождении
	|		ПО (АктОРасхождении.ИдентификаторСтроки = ТаблицаАгентскиеУслуги.ИдентификаторСтроки)
	|ГДЕ
	|	ТаблицаАгентскиеУслуги.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ТаблицаОбъектыСтроительства.НомерСтроки,
	|	ТаблицаОбъектыСтроительства.ОбъектСтроительства,
	|	ТаблицаОбъектыСтроительства.ОбъектСтроительства.Код,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаОбъектыСтроительства.ОбъектСтроительства.Наименование,
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	0,
	|	ТаблицаОбъектыСтроительства.Сумма,
	|	ТаблицаОбъектыСтроительства.Сумма,
	|	ТаблицаОбъектыСтроительства.СуммаНДС,
	|	ТаблицаОбъектыСтроительства.СтавкаНДС,
	|	ЛОЖЬ,
	|	ТаблицаОбъектыСтроительства.Ссылка.СуммаВключаетНДС,
	|	ТаблицаОбъектыСтроительства.Ссылка,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ТаблицаОбъектыСтроительства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ТаблицаОбъектыСтроительства.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ОбъектыСтроительства))
	|			И (ТаблицаОбъектыСтроительства.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
	|ГДЕ
	|	ТаблицаОбъектыСтроительства.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	ТаблицаОсновныеСредства.НомерСтроки,
	|	ТаблицаОсновныеСредства.ОсновноеСредство,
	|	ТаблицаОсновныеСредства.ОсновноеСредство.Код,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаОсновныеСредства.ОсновноеСредство.Наименование,
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	0,
	|	ТаблицаОсновныеСредства.Сумма,
	|	ТаблицаОсновныеСредства.Сумма,
	|	ТаблицаОсновныеСредства.СуммаНДС,
	|	ТаблицаОсновныеСредства.СтавкаНДС,
	|	ЛОЖЬ,
	|	ТаблицаОсновныеСредства.Ссылка.СуммаВключаетНДС,
	|	ТаблицаОсновныеСредства.Ссылка,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ТаблицаОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ТаблицаОсновныеСредства.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ОС))
	|			И (ТаблицаОсновныеСредства.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
	|ГДЕ
	|	ТаблицаОсновныеСредства.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	ТаблицаТопливо.НомерСтроки,
	|	ТаблицаТопливо.Номенклатура,
	|	ТаблицаТопливо.Номенклатура.Код,
	|	ТаблицаТопливо.Номенклатура.Артикул,
	|	ТаблицаТопливо.Номенклатура.НаименованиеПолное,
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	ТаблицаТопливо.Номенклатура.ЕдиницаИзмерения,
	|	ТаблицаТопливо.Количество,
	|	ТаблицаТопливо.Цена,
	|	ТаблицаТопливо.Сумма,
	|	ТаблицаТопливо.СуммаНДС,
	|	ТаблицаТопливо.СтавкаНДС,
	|	ЛОЖЬ,
	|	ТаблицаТопливо.Ссылка.СуммаВключаетНДС,
	|	ТаблицаТопливо.Ссылка,
	|	ЛОЖЬ,
	|	ТаблицаТопливо.Сумма,
	|	ТаблицаТопливо.Сумма - ТаблицаТопливо.СуммаНДС,
	|	ТаблицаТопливо.СуммаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Топливо КАК ТаблицаТопливо
	|ГДЕ
	|	ТаблицаТопливо.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	ТаблицаПрава.НомерСтроки,
	|	ТаблицаПрава.ОбъектПрава,
	|	ТаблицаПрава.ОбъектПрава.Код,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаПрава.Содержание,
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	ТаблицаПрава.Количество,
	|	ТаблицаПрава.Цена,
	|	ТаблицаПрава.Сумма,
	|	ТаблицаПрава.СуммаНДС,
	|	ТаблицаПрава.СтавкаНДС,
	|	ИСТИНА,
	|	ТаблицаПрава.Ссылка.СуммаВключаетНДС,
	|	ТаблицаПрава.Ссылка,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Права КАК ТаблицаПрава
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ТаблицаПрава.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Права))
	|			И (ТаблицаПрава.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета)
	|ГДЕ
	|	ТаблицаПрава.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ.Код КАК НомерТовара,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Количество КАК КоличествоУчетное,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК Количество,
	|	ЕСТЬNULL(КлассификаторТНВЭД.ЕдиницаИзмерения.Наименование, ""шт"") КАК ЕдиницаИзмеренияПрослеживаемостиНаименование,
	|	ЕСТЬNULL(КлассификаторТНВЭД.ЕдиницаИзмерения.Код, ""796"") КАК ЕдиницаИзмеренияПрослеживаемостиКод,
	|	ТаблицаДокумента.Товар КАК Товар,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТоварКодТНВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|			ТОГДА ""--""
	|		ИНАЧЕ ТаблицаДокумента.ТоварКодТНВЭД
	|	КОНЕЦ КАК ТоварКодТНВЭД,
	|	ТаблицаДокумента.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТД,
	|	ТаблицаДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДокумента.Количество = 0
	|						ТОГДА 0
	|					КОГДА ТаблицаДокумента.СуммаНДС = 0
	|						ТОГДА ТаблицаДокумента.Цена
	|					ИНАЧЕ ВЫРАЗИТЬ((ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаНДС) / ТаблицаДокумента.Количество КАК ЧИСЛО(15, 2))
	|				КОНЕЦ
	|		ИНАЧЕ ТаблицаДокумента.Цена
	|	КОНЕЦ КАК Цена,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.КонтрагентСводныйСФ КАК КонтрагентСводныйСФ,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.НомерТабЧасти КАК НомерТабЧасти,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслугСведенияПрослеживаемости.Сумма = 0
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДокумента.СуммаВключаетНДС
	|						ТОГДА ВЫРАЗИТЬ(ПоступлениеТоваровУслугСведенияПрослеживаемости.Количество * (ТаблицаДокумента.Сумма - ТаблицаДокумента.СуммаНДС) / ТаблицаДокумента.Количество КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ ПоступлениеТоваровУслугСведенияПрослеживаемости.Количество * ТаблицаДокумента.Цена
	|				КОНЕЦ
	|		ИНАЧЕ ПоступлениеТоваровУслугСведенияПрослеживаемости.Сумма
	|	КОНЕЦ КАК СтоимостьБезНалога
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокумента КАК ТаблицаДокумента
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|			ПО ТаблицаДокумента.НоменклатураКодТНВЭД = КлассификаторТНВЭД.Ссылка
	|		ПО (ТаблицаДокумента.ИдентификаторСтроки = ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки)
	|			И ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка = ТаблицаДокумента.Ссылка";
	
	ТекстЗапроса = ТекстЗапроса
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

	Возврат ТекстЗапроса;

КонецФункции

Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	МассивРеквизитов = Новый Массив();
	МассивРеквизитов.Добавить("УдалитьУчитыватьНДС");
	МассивРеквизитов.Добавить("УдалитьПредъявленСчетФактура");
	МассивРеквизитов.Добавить("УдалитьНомерВходящегоСчетаФактуры");
	МассивРеквизитов.Добавить("УдалитьДатаВходящегоСчетаФактуры");
	МассивРеквизитов.Добавить("УдалитьНДСПредъявленКВычету");
	МассивРеквизитов.Добавить("УдалитьКодВидаОперации");
	МассивРеквизитов.Добавить("УдалитьКодСпособаПолучения");
	
	Возврат МассивРеквизитов;
	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыТОРГ2(МАссивОбъектов, СоответствиеОбъектовПечати) Экспорт
	СоответствиеОбъектовПечати = Новый Соответствие;
	МассивАктов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктОРасхождениях.ДокументПоступления КАК ДокументПоступления,
		|	АктОРасхождениях.Ссылка КАК АктОРасхождениях
		|ИЗ
		|	Документ.АктОРасхождениях КАК АктОРасхождениях
		|ГДЕ
		|	АктОРасхождениях.ДокументПоступления В(&МассивОбъектов)";
	Запрос.УстановитьПараметр("МассивОбъектов", МАссивОбъектов);
	
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		СоответствиеОбъектовПечати.Вставить(ВыборкаЗапроса.ДокументПоступления, ВыборкаЗапроса.АктОРасхождениях);
		МассивАктов.Добавить(ВыборкаЗапроса.АктОРасхождениях);
	КонецЦикла; 

	Возврат Документы.АктОРасхождениях.ПолучитьДанныеДляПечатнойФормыТОРГ2(МассивАктов);
КонецФункции

Функция АктОРасхожденияхПоДокументу(Ссылка, ПометкаУдаления = ЛОЖЬ) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АктОРасхождениях.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктОРасхождениях КАК АктОРасхождениях
	|		ПО ПоступлениеТоваровУслуг.Ссылка = АктОРасхождениях.ДокументПоступления
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &Ссылка
	|	И АктОРасхождениях.ПометкаУдаления = &ПометкаУдаления
	|	И АктОРасхождениях.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли; 
КонецФункции 

Функция ОпределитьВидОперацииПоДокументуОснованию(Основание) Экспорт

	Результат = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия;

	Если ЗначениеЗаполнено(Основание)
		И ТипЗнч(Основание) = Тип("ДокументСсылка.Доверенность") Тогда
		
		Если НЕ ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов") Тогда
		    // При наличии деятельности по договорам комиссии на закупку возможно 
		    // выдача доверенности для получения товаров для комитента

			ДоговорКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Основание, "ДоговорКонтрагента");
			
			ЭтоКомиссияНаПродажу = Ложь;
				
			Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
				ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
				ЭтоКомиссияНаПродажу = (ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
			КонецЕсли;
			
			Если НЕ ЭтоКомиссияНаПродажу Тогда
				Результат = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары;
			КонецЕсли;
		
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПриемкаТоваровИСМП") Тогда
		
		Результат = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
		
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "ДоговорКонтрагента.ВидДоговора");
			
		Если ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом
			Или НЕ ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов") Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", Основание);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СУММА(Таблицы.ЕстьТовары) КАК ЕстьТовары,
			|	СУММА(Таблицы.ЕстьУслуги) КАК ЕстьУслуги,
			|	СУММА(Таблицы.ЕстьВозвратнаяТара) КАК ЕстьВозвратнаяТара
			|ИЗ
			|	(ВЫБРАТЬ ПЕРВЫЕ 1
			|		1 КАК ЕстьТовары,
			|		0 КАК ЕстьУслуги,
			|		0 КАК ЕстьВозвратнаяТара
			|	ИЗ
			|		Документ.СчетНаОплатуПоставщика.Товары КАК ДокОснование
			|	ГДЕ
			|		ДокОснование.Ссылка = &Ссылка
			|		И НЕ ЕСТЬNULL(ДокОснование.Номенклатура.Услуга, ИСТИНА)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ ПЕРВЫЕ 1
			|		0,
			|		1,
			|		0
			|	ИЗ
			|		Документ.СчетНаОплатуПоставщика.Товары КАК ДокОснование
			|	ГДЕ
			|		ДокОснование.Ссылка = &Ссылка
			|		И ЕСТЬNULL(ДокОснование.Номенклатура.Услуга, ИСТИНА)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ ПЕРВЫЕ 1
			|		0,
			|		0,
			|		1
			|	ИЗ
			|		Документ.СчетНаОплатуПоставщика.ВозвратнаяТара КАК ДокОснование
			|	ГДЕ
			|		ДокОснование.Ссылка = &Ссылка) КАК Таблицы";
			Выборка = Запрос.Выполнить().Выбрать();
			ЕстьТовары 	= Ложь;
			ЕстьУслуги	= Ложь;
			ЕстьВозвратнаяТара	= Ложь;
			Если Выборка.Следующий() Тогда
				ЕстьТовары 	= ЗначениеЗаполнено(Выборка.ЕстьТовары) И Выборка.ЕстьТовары > 0;
				ЕстьУслуги	= ЗначениеЗаполнено(Выборка.ЕстьУслуги) И Выборка.ЕстьУслуги > 0;
				ЕстьВозвратнаяТара	= ЗначениеЗаполнено(Выборка.ЕстьВозвратнаяТара) И Выборка.ЕстьВозвратнаяТара > 0;
			КонецЕсли;
			
			Если ЕстьВозвратнаяТара Тогда
				// Использование возвратной тары выполняется через основную форму
			ИначеЕсли ЕстьТовары И ЕстьУслуги Тогда
				// Одновременное поступление товаров и услуг выполняется через основную форму
			ИначеЕсли ЕстьТовары Тогда
				Результат = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары;
			ИначеЕсли ЕстьУслуги Тогда
				Результат = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ПодготовитьТаблицуПриобретениеПрав(ТаблицаПрава, ТаблицаРеквизиты) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаПрава) Или Не ЗначениеЗаполнено(ТаблицаРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	ЭтоПлательщикНДФЛ = УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период);
	
	Если ЭтоПлательщикНДФЛ Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПлательщикНалогаНаПрибыль = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
		
	Для Каждого СтрокаПрав Из ТаблицаПрава Цикл
	
		Если ЗначениеЗаполнено(СтрокаПрав.НазначениеИспользования) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаПрав.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс
			Или ЭтоПлательщикНалогаНаПрибыль И СтрокаПрав.ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования Тогда
		
			СтрокаПрав.НазначениеИспользования = Справочники.НазначенияИспользованияНМА.НазначениеИспользования(СтрокаПрав);
			УчетНМА.УстановитьСубконтоНазначениеИспользования(СтрокаПрав);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция СтатусДокументаПеречислением(СтатусДокумента) Экспорт
	
	Возврат СтатусыДокументов.СтатусВходящегоДокументаПеречислением(СтатусДокумента);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ЗначенияЗаполнения");
	
	ВидОперации = Неопределено; 

	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "ВидОперации");
	КонецЕсли;

	// Если документ копируется, то вид формы получаем из копируемого документа.
	Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
		Если Параметры.Свойство("ЗначениеКопирования")
			И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ВидОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Параметры.ЗначениеКопирования, "ВидОперации");
		КонецЕсли;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
		ВидОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначенияЗаполнения, "ВидОперации");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидОперации) Тогда
		Если Параметры.Свойство("Основание")
			И ЗначениеЗаполнено(Параметры.Основание) Тогда
			ВидОперации = ОпределитьВидОперацииПоДокументуОснованию(Параметры.Основание);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидОперации) Тогда
		ДоговорКонтрагента = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЗначенияЗаполнения, "ДоговорКонтрагента");
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			ВидОперации = РаботаСДоговорамиКонтрагентовБПВызовСервера.ВидОперацииПоДоговору(
				ДоговорКонтрагента, Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
		КонецЕсли;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	ФормыПоступлениеТоваровУслуг = ПолучитьСоответствиеВидовОперацийФормам();
	ВыбраннаяФорма = ФормыПоступлениеТоваровУслуг[ВидОперации];
	Если ВыбраннаяФорма = Неопределено Тогда
		ВыбраннаяФорма = "ФормаДокумента";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц, ЭтоОтложенноеПроведение)

	НомераТаблиц.Вставить("ВременнаяТаблицаСоставДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());

	Если ЭтоОтложенноеПроведение Тогда
		// При отложенном проведении нет необходимости обращаться к табличным частям документа, 
		// которые могут содержать много строк. 
		// Суммы взаиморасчетов могут быть получены из ранее сформированных проводок документа.
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЛОЖЬ КАК ЕстьТовары,
		|	ЛОЖЬ КАК ЕстьУслуги,
		|	ЛОЖЬ КАК ЕстьАгентскиеУслуги,
		|	ЛОЖЬ КАК ЕстьОборудование,
		|	ЛОЖЬ КАК ЕстьОбъектыСтроительства,
		|	ЛОЖЬ КАК ЕстьТара,
		|	ЛОЖЬ КАК ЕстьОсновныеСредства,
		|	ЛОЖЬ КАК ЕстьТопливо,
		|	ЛОЖЬ КАК ЕстьПрава,
		|	ЛОЖЬ КАК ЕстьНДС
		|ПОМЕСТИТЬ СоставДокумента
		|ГДЕ
		|	ЛОЖЬ";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МАКСИМУМ(СоставДокумента.ЕстьТовары) КАК ЕстьТовары,
		|	МАКСИМУМ(СоставДокумента.ЕстьУслуги) КАК ЕстьУслуги,
		|	МАКСИМУМ(СоставДокумента.ЕстьАгентскиеУслуги) КАК ЕстьАгентскиеУслуги,
		|	МАКСИМУМ(СоставДокумента.ЕстьОборудование) КАК ЕстьОборудование,
		|	МАКСИМУМ(СоставДокумента.ЕстьОбъектыСтроительства) КАК ЕстьОбъектыСтроительства,
		|	МАКСИМУМ(СоставДокумента.ЕстьТара) КАК ЕстьТара,
		|	МАКСИМУМ(СоставДокумента.ЕстьОсновныеСредства) КАК ЕстьОсновныеСредства,
		|	МАКСИМУМ(СоставДокумента.ЕстьТопливо) КАК ЕстьТопливо,
		|	МАКСИМУМ(СоставДокумента.ЕстьПрава) КАК ЕстьПрава,
		|	МАКСИМУМ(СоставДокумента.ЕстьНДС) КАК ЕстьНДС
		|ПОМЕСТИТЬ СоставДокумента
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 1
		|		ИСТИНА КАК ЕстьТовары,
		|		ЛОЖЬ КАК ЕстьУслуги,
		|		ЛОЖЬ КАК ЕстьАгентскиеУслуги,
		|		ЛОЖЬ КАК ЕстьОборудование,
		|		ЛОЖЬ КАК ЕстьОбъектыСтроительства,
		|		ЛОЖЬ КАК ЕстьТара,
		|		ЛОЖЬ КАК ЕстьОсновныеСредства,
		|		ЛОЖЬ КАК ЕстьТопливо,
		|		ЛОЖЬ КАК ЕстьПрава,
		|		ЛОЖЬ КАК ЕстьНДС
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Услуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Оборудование КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.ВозвратнаяТара КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Топливо КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА,
		|		ЛОЖЬ
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Права КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Услуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Оборудование КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ЛОЖЬ,
		|		ИСТИНА
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Права КАК ТаблицаДокумента
		|	ГДЕ
		|		ТаблицаДокумента.Ссылка = &Ссылка
		|		И ТаблицаДокумента.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)) КАК СоставДокумента";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов() + 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВидДоговора, НЕОПРЕДЕЛЕНО) КАК ВидДоговора,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов, НЕОПРЕДЕЛЕНО) КАК ВалютаВзаиморасчетов,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.УчетАгентскогоНДС, ЛОЖЬ) КАК УчетАгентскогоНДС,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВидАгентскогоДоговора, ЛОЖЬ) КАК ВидАгентскогоДоговора,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ЭлектронныеУслуги, ЛОЖЬ) КАК ЭлектронныеУслуги,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.Номер, """") КАК НомерДоговора,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДоговора,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.Наименование, """") КАК НаименованиеДоговора,
	|	Реквизиты.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.СкладВСписке КАК СкладВСписке,
	|	ЕСТЬNULL(Реквизиты.Склад.ТипСклада, НЕОПРЕДЕЛЕНО) КАК ТипСклада,
	|	ЕСТЬNULL(Реквизиты.Склад.ТипЦенРозничнойТорговли, НЕОПРЕДЕЛЕНО) КАК ТипЦенРозничнойТорговли,
	|	Реквизиты.СчетУчетаРасчетовСКомиссионером КАК СчетУчетаРасчетовСКомиссионером,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.СчетУчетаРасчетовПоТаре КАК СчетУчетаРасчетовПоТаре,
	|	Реквизиты.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	Реквизиты.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	Реквизиты.МОЛ КАК МОЛ,
	|	Реквизиты.МестонахождениеОС КАК МестонахождениеОС,
	|	Реквизиты.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
	|	Реквизиты.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВидДоговора, ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)) = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом) КАК ЭтоКомиссия,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВидДоговора, ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)) = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку) КАК ЭтоКомиссияНаЗакупку,
	|	"""" КАК ИмяСписка,
	|	Реквизиты.СуммаДокумента КАК СуммаДокумента,
	|	Реквизиты.ЕстьРасхождения КАК ЕстьРасхождения,
	|	Контрагенты.СтранаРегистрации КАК СтранаРегистрации
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО Реквизиты.Контрагент = Контрагенты.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	Реквизиты.ВидАгентскогоДоговора КАК ВидАгентскогоДоговора,
	|	Реквизиты.ЭлектронныеУслуги КАК ЭлектронныеУслуги,
	|	Реквизиты.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	Реквизиты.Склад КАК Склад,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	Реквизиты.СкладВСписке КАК СкладВСписке,
	|	Реквизиты.ТипЦенРозничнойТорговли КАК ТипЦенРозничнойТорговли,
	|	Реквизиты.СчетУчетаРасчетовСКомиссионером КАК СчетУчетаРасчетовСКомиссионером,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.СчетУчетаРасчетовПоТаре КАК СчетУчетаРасчетовПоТаре,
	|	Реквизиты.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	Реквизиты.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	Реквизиты.МОЛ КАК МОЛ,
	|	Реквизиты.МестонахождениеОС КАК МестонахождениеОС,
	|	Реквизиты.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
	|	Реквизиты.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика,
	|	Реквизиты.ЭтоКомиссия КАК ЭтоКомиссия,
	|	Реквизиты.ЭтоКомиссияНаЗакупку КАК ЭтоКомиссияНаЗакупку,
	|	ЕСТЬNULL(СоставДокумента.ЕстьТовары, ЛОЖЬ) КАК ЕстьТовары,
	|	ЕСТЬNULL(СоставДокумента.ЕстьУслуги, ЛОЖЬ) КАК ЕстьУслуги,
	|	ЕСТЬNULL(СоставДокумента.ЕстьАгентскиеУслуги, ЛОЖЬ) КАК ЕстьАгентскиеУслуги,
	|	ЕСТЬNULL(СоставДокумента.ЕстьОборудование, ЛОЖЬ) КАК ЕстьОборудование,
	|	ЕСТЬNULL(СоставДокумента.ЕстьОбъектыСтроительства, ЛОЖЬ) КАК ЕстьОбъектыСтроительства,
	|	ЕСТЬNULL(СоставДокумента.ЕстьТара, ЛОЖЬ) КАК ЕстьТара,
	|	ЕСТЬNULL(СоставДокумента.ЕстьОсновныеСредства, ЛОЖЬ) КАК ЕстьОсновныеСредства,
	|	ЕСТЬNULL(СоставДокумента.ЕстьТопливо, ЛОЖЬ) КАК ЕстьТопливо,
	|	ЕСТЬNULL(СоставДокумента.ЕстьПрава, ЛОЖЬ) КАК ЕстьПрава,
	|	ЕСТЬNULL(СоставДокумента.ЕстьНДС, ЛОЖЬ) КАК ЕстьНДС,
	|	Реквизиты.ИмяСписка КАК ИмяСписка,
	|	Реквизиты.НомерДоговора КАК НомерДоговора,
	|	Реквизиты.ДатаДоговора КАК ДатаДоговора,
	|	Реквизиты.НаименованиеДоговора КАК НаименованиеДоговора,
	|	Реквизиты.СуммаДокумента КАК СуммаДокумента,
	|	Реквизиты.ЕстьРасхождения КАК ЕстьРасхождения,
	|	Реквизиты.СтранаРегистрации КАК СтранаРегистрации
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоставДокумента КАК СоставДокумента
	|		ПО (ИСТИНА)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты)
	
	ТекстЗапроса = "";
	// Временная таблица для документа с расхождениями, отбирает минимум между фактическими суммами 
	// и суммами, которые подтверждены документально.
	НомераТаблиц.Вставить("ВТ_РасхожденияПриПриемке", НомераТаблиц.Количество());
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВТ_РасхожденияПриПриемке.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_РасхожденияПриПриемке.СуммаПодтвержденнаяРуб КАК СуммаПодтвержденнаяРуб,
	|	ВТ_РасхожденияПриПриемке.СуммаНДСПодтвержденнаяРуб КАК СуммаНДСПодтвержденнаяРуб
	|ПОМЕСТИТЬ ВТ_РасхожденияПриПриемке
	|ИЗ
	|	(ВЫБРАТЬ
	|		АктОРасхожденияхТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		ВЫБОР
	|			КОГДА АктОРасхожденияхТовары.СуммаПоДокументу < АктОРасхожденияхТовары.Сумма
	|				ТОГДА ВЫБОР
	|						КОГДА АктОРасхожденияхТовары.Ссылка.СуммаВключаетНДС
	|							ТОГДА АктОРасхожденияхТовары.СуммаПоДокументу
	|						ИНАЧЕ АктОРасхожденияхТовары.СуммаПоДокументу + АктОРасхожденияхТовары.СуммаНДСПоДокументу
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА АктОРасхожденияхТовары.Ссылка.СуммаВключаетНДС
	|						ТОГДА АктОРасхожденияхТовары.Сумма
	|					ИНАЧЕ АктОРасхожденияхТовары.Сумма + АктОРасхожденияхТовары.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ КАК СуммаПодтвержденнаяРуб,
	|		ВЫБОР
	|			КОГДА АктОРасхожденияхТовары.СуммаНДСПоДокументу < АктОРасхожденияхТовары.СуммаНДС
	|				ТОГДА АктОРасхожденияхТовары.СуммаНДСПоДокументу
	|			ИНАЧЕ АктОРасхожденияхТовары.СуммаНДС
	|		КОНЕЦ КАК СуммаНДСПодтвержденнаяРуб
	|	ИЗ
	|		Документ.АктОРасхождениях.Товары КАК АктОРасхожденияхТовары
	|	ГДЕ
	|		АктОРасхожденияхТовары.Ссылка.ДокументПоступления = &Ссылка
	|		И &ЕстьРасхождения
	|		И НЕ АктОРасхожденияхТовары.Ссылка.ПометкаУдаления
	|		И АктОРасхожденияхТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АктОРасхожденияхУслуги.ИдентификаторСтроки,
	|		ВЫБОР
	|			КОГДА АктОРасхожденияхУслуги.СуммаПоДокументу < АктОРасхожденияхУслуги.Сумма
	|				ТОГДА ВЫБОР
	|						КОГДА АктОРасхожденияхУслуги.Ссылка.СуммаВключаетНДС
	|							ТОГДА АктОРасхожденияхУслуги.СуммаПоДокументу
	|						ИНАЧЕ АктОРасхожденияхУслуги.СуммаПоДокументу + АктОРасхожденияхУслуги.СуммаНДСПоДокументу
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА АктОРасхожденияхУслуги.Ссылка.СуммаВключаетНДС
	|						ТОГДА АктОРасхожденияхУслуги.Сумма
	|					ИНАЧЕ АктОРасхожденияхУслуги.Сумма + АктОРасхожденияхУслуги.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АктОРасхожденияхУслуги.СуммаНДСПоДокументу < АктОРасхожденияхУслуги.СуммаНДС
	|				ТОГДА АктОРасхожденияхУслуги.СуммаНДСПоДокументу
	|			ИНАЧЕ АктОРасхожденияхУслуги.СуммаНДС
	|		КОНЕЦ
	|	ИЗ
	|		Документ.АктОРасхождениях.Услуги КАК АктОРасхожденияхУслуги
	|	ГДЕ
	|		АктОРасхожденияхУслуги.Ссылка.ДокументПоступления = &Ссылка
	|		И &ЕстьРасхождения
	|		И НЕ АктОРасхожденияхУслуги.Ссылка.ПометкаУдаления
	|		И АктОРасхожденияхУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АктОРасхожденияхАгентскиеУслуги.ИдентификаторСтроки,
	|		ВЫБОР
	|			КОГДА АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу < АктОРасхожденияхАгентскиеУслуги.Сумма
	|				ТОГДА ВЫБОР
	|						КОГДА АктОРасхожденияхАгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|							ТОГДА АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу
	|						ИНАЧЕ АктОРасхожденияхАгентскиеУслуги.СуммаПоДокументу + АктОРасхожденияхАгентскиеУслуги.СуммаНДСПоДокументу
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА АктОРасхожденияхАгентскиеУслуги.Ссылка.СуммаВключаетНДС
	|						ТОГДА АктОРасхожденияхАгентскиеУслуги.Сумма
	|					ИНАЧЕ АктОРасхожденияхАгентскиеУслуги.Сумма + АктОРасхожденияхАгентскиеУслуги.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА АктОРасхожденияхАгентскиеУслуги.СуммаНДСПоДокументу < АктОРасхожденияхАгентскиеУслуги.СуммаНДС
	|				ТОГДА АктОРасхожденияхАгентскиеУслуги.СуммаНДСПоДокументу
	|			ИНАЧЕ АктОРасхожденияхАгентскиеУслуги.СуммаНДС
	|		КОНЕЦ
	|	ИЗ
	|		Документ.АктОРасхождениях.АгентскиеУслуги КАК АктОРасхожденияхАгентскиеУслуги
	|	ГДЕ
	|		АктОРасхожденияхАгентскиеУслуги.Ссылка.ДокументПоступления = &Ссылка
	|		И &ЕстьРасхождения
	|		И НЕ АктОРасхожденияхАгентскиеУслуги.Ссылка.ПометкаУдаления
	|		И АктОРасхожденияхАгентскиеУслуги.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке)) КАК ВТ_РасхожденияПриПриемке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторСтроки"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Если Реквизиты.ЕстьТовары Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаТовары", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаТовары.Сумма
		|		ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаТовары.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаТовары.Сумма
		|		ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
		|	КОНЕЦ КАК СуммаРуб,
		|	ТаблицаТовары.СуммаНДС КАК СуммаНДСРуб,
		|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТовары.ЦенаВРознице КАК ЦенаВРознице,
		|	ТаблицаТовары.СуммаВРознице КАК СуммаВРознице,
		|	ТаблицаТовары.СтавкаНДСВРознице КАК СтавкаНДСВРознице,
		|	ТаблицаТовары.НомерГТД КАК НомерГТД,
		|	ТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаТовары.СчетУчета КАК СчетУчета,
		|	ТаблицаТовары.СчетУчета.Забалансовый КАК СчетУчетаЗабалансовый,
		|	ТаблицаТовары.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ТаблицаТовары.ОтражениеВУСН КАК ОтражениеВУСН,
		|	ТаблицаТовары.Контрагент КАК Контрагент,
		|	ТаблицаТовары.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаТовары.СчетРасчетов КАК СчетРасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчета.Забалансовый
		|				И ТаблицаТовары.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК КомиссияПоЗакупке,
		|	ЕСТЬNULL(ВТ_РасхожденияПриПриемке.СуммаПодтвержденнаяРуб, 0) КАК СуммаПодтвержденнаяРуб,
		|	ЕСТЬNULL(ВТ_РасхожденияПриПриемке.СуммаНДСПодтвержденнаяРуб, 0) КАК СуммаНДСПодтвержденнаяРуб,
		|	ТаблицаТовары.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаТовары.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
		|	ТаблицаТовары.Субконто1 КАК Субконто1,
		|	ТаблицаТовары.Субконто2 КАК Субконто2,
		|	ТаблицаТовары.Субконто3 КАК Субконто3,
		|	ВЫБОР
		|		КОГДА &ПрименяетсяФСБУ5_2019
		|				И ТаблицаТовары.СчетУчета = &СчетУчетаМалоценки
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Малоценка,
		|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
		|	ТаблицаТовары.Склад КАК Склад
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасхожденияПриПриемке КАК ВТ_РасхожденияПриПриемке
		|		ПО ТаблицаТовары.ИдентификаторСтроки = ВТ_РасхожденияПриПриемке.ИдентификаторСтроки
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьУслуги Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаУслуги.Ссылка КАК Ссылка,
		|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
		|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
		|	ТаблицаУслуги.Содержание КАК Содержание,
		|	ТаблицаУслуги.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаУслуги.Сумма
		|		ИНАЧЕ ТаблицаУслуги.Сумма + ТаблицаУслуги.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаУслуги.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаУслуги.Сумма
		|		ИНАЧЕ ТаблицаУслуги.Сумма + ТаблицаУслуги.СуммаНДС
		|	КОНЕЦ КАК СуммаРуб,
		|	ТаблицаУслуги.СуммаНДС КАК СуммаНДСРуб,
		|	ТаблицаУслуги.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаУслуги.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаУслуги.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ТаблицаУслуги.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
		|	ТаблицаУслуги.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаУслуги.Субконто1 КАК Субконто1,
		|	ТаблицаУслуги.Субконто2 КАК Субконто2,
		|	ТаблицаУслуги.Субконто3 КАК Субконто3,
		|	ТаблицаУслуги.СчетЗатратНУ КАК СчетЗатратНУ,
		|	ТаблицаУслуги.СубконтоНУ1 КАК СубконтоНУ1,
		|	ТаблицаУслуги.СубконтоНУ2 КАК СубконтоНУ2,
		|	ТаблицаУслуги.СубконтоНУ3 КАК СубконтоНУ3,
		|	ТаблицаУслуги.ОтражениеВУСН КАК ОтражениеВУСН,
		|	ЕСТЬNULL(ВТ_РасхожденияПриПриемке.СуммаПодтвержденнаяРуб, 0) КАК СуммаПодтвержденнаяРуб,
		|	ЕСТЬNULL(ВТ_РасхожденияПриПриемке.СуммаНДСПодтвержденнаяРуб, 0) КАК СуммаНДСПодтвержденнаяРуб,
		|	ТаблицаУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ТаблицаУслуги
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ТаблицаУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасхожденияПриПриемке КАК ВТ_РасхожденияПриПриемке
		|		ПО ТаблицаУслуги.ИдентификаторСтроки = ВТ_РасхожденияПриПриемке.ИдентификаторСтроки
		|ГДЕ
		|	ТаблицаУслуги.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаАгентскиеУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаАгентскиеУслуги.Ссылка КАК Ссылка,
		|	ТаблицаАгентскиеУслуги.НомерСтроки КАК НомерСтроки,
		|	ТаблицаАгентскиеУслуги.Номенклатура КАК Номенклатура,
		|	ТаблицаАгентскиеУслуги.Содержание КАК Содержание,
		|	ТаблицаАгентскиеУслуги.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаАгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаАгентскиеУслуги.Сумма
		|		ИНАЧЕ ТаблицаАгентскиеУслуги.Сумма + ТаблицаАгентскиеУслуги.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаАгентскиеУслуги.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаАгентскиеУслуги.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаАгентскиеУслуги.Сумма
		|		ИНАЧЕ ТаблицаАгентскиеУслуги.Сумма + ТаблицаАгентскиеУслуги.СуммаНДС
		|	КОНЕЦ КАК СуммаРуб,
		|	ТаблицаАгентскиеУслуги.СуммаНДС КАК СуммаНДСРуб,
		|	ТаблицаАгентскиеУслуги.Контрагент КАК Контрагент,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаАгентскиеУслуги.СчетРасчетов КАК СчетРасчетов,
		|	ТаблицаАгентскиеУслуги.СтавкаНДС КАК СтавкаНДС,
		|	ЕСТЬNULL(ВТ_РасхожденияПриПриемке.СуммаПодтвержденнаяРуб, 0) КАК СуммаПодтвержденнаяРуб,
		|	ЕСТЬNULL(ВТ_РасхожденияПриПриемке.СуммаНДСПодтвержденнаяРуб, 0) КАК СуммаНДСПодтвержденнаяРуб,
		|	ТаблицаАгентскиеУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ТаблицаАгентскиеУслуги
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ТаблицаАгентскиеУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасхожденияПриПриемке КАК ВТ_РасхожденияПриПриемке
		|		ПО ТаблицаАгентскиеУслуги.ИдентификаторСтроки = ВТ_РасхожденияПриПриемке.ИдентификаторСтроки
		|ГДЕ
		|	ТаблицаАгентскиеУслуги.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьОборудование Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаОборудование", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаОборудование.Ссылка КАК Ссылка,
		|	ТаблицаОборудование.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОборудование.Номенклатура КАК Номенклатура,
		|	ТаблицаОборудование.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаОборудование.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаОборудование.Сумма
		|		ИНАЧЕ ТаблицаОборудование.Сумма + ТаблицаОборудование.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаОборудование.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаОборудование.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаОборудование.Сумма
		|		ИНАЧЕ ТаблицаОборудование.Сумма + ТаблицаОборудование.СуммаНДС
		|	КОНЕЦ КАК СуммаРуб,
		|	ТаблицаОборудование.СуммаНДС КАК СуммаНДСРуб,
		|	ТаблицаОборудование.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаОборудование.НомерГТД КАК НомерГТД,
		|	ТаблицаОборудование.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаОборудование.СчетУчета КАК СчетУчета,
		|	ТаблицаОборудование.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаОборудование.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ТаблицаОборудование.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаОборудование.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
		|	ТаблицаОборудование.Ссылка.Склад КАК Склад
		|ПОМЕСТИТЬ ТаблицаОборудование
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ТаблицаОборудование
		|ГДЕ
		|	ТаблицаОборудование.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьОбъектыСтроительства Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаОбъектыСтроительства", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаОбъектыСтроительства.Ссылка,
		|	ТаблицаОбъектыСтроительства.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОбъектыСтроительства.ОбъектСтроительства,
		|	ТаблицаОбъектыСтроительства.СтатьяЗатрат,
		|	ВЫБОР
		|		КОГДА ТаблицаОбъектыСтроительства.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаОбъектыСтроительства.Сумма
		|		ИНАЧЕ ТаблицаОбъектыСтроительства.Сумма + ТаблицаОбъектыСтроительства.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаОбъектыСтроительства.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаОбъектыСтроительства.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаОбъектыСтроительства.Сумма
		|		ИНАЧЕ ТаблицаОбъектыСтроительства.Сумма + ТаблицаОбъектыСтроительства.СуммаНДС
		|	КОНЕЦ КАК СуммаРуб,
		|	ТаблицаОбъектыСтроительства.СуммаНДС КАК СуммаНДСРуб,
		|	ТаблицаОбъектыСтроительства.СтавкаНДС,
		|	ТаблицаОбъектыСтроительства.СчетУчета,
		|	ТаблицаОбъектыСтроительства.СчетУчетаНДС,
		|	ТаблицаОбъектыСтроительства.СпособУчетаНДС
		|ПОМЕСТИТЬ ТаблицаОбъектыСтроительства
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ТаблицаОбъектыСтроительства
		|ГДЕ
		|	ТаблицаОбъектыСтроительства.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьТара Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаТара", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаТара.Ссылка,
		|	ТаблицаТара.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТара.Номенклатура,
		|	ТаблицаТара.Количество,
		|	ТаблицаТара.Сумма КАК СуммаВзаиморасчетов,
		|	ТаблицаТара.Сумма КАК СуммаРуб,
		|	ТаблицаТара.СчетУчета,
		|   ВЫБОР 
		|		КОГДА ТаблицаТара.Ссылка.СкладВСписке
		|   		ТОГДА ТаблицаТара.Склад
		|		ИНАЧЕ ТаблицаТара.Ссылка.Склад
		|	КОНЕЦ КАК Склад
		|ПОМЕСТИТЬ ТаблицаТара
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.ВозвратнаяТара КАК ТаблицаТара
		|ГДЕ
		|	ТаблицаТара.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьОсновныеСредства Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаОсновныеСредства", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаОсновныеСредства.Ссылка КАК Ссылка,
		|	ТаблицаОсновныеСредства.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаОсновныеСредства.ОсновноеСредство.ЕдиницаУчета КАК ЕдиницаУчета,
		|	ТаблицаОсновныеСредства.Количество КАК Количество,
		|	ВЫБОР
		|		КОГДА ТаблицаОсновныеСредства.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаОсновныеСредства.Сумма
		|		ИНАЧЕ ТаблицаОсновныеСредства.Сумма + ТаблицаОсновныеСредства.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаОсновныеСредства.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаОсновныеСредства.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаОсновныеСредства.Сумма
		|		ИНАЧЕ ТаблицаОсновныеСредства.Сумма + ТаблицаОсновныеСредства.СуммаНДС
		|	КОНЕЦ КАК СуммаРуб,
		|	ТаблицаОсновныеСредства.СуммаНДС КАК СуммаНДСРуб,
		|	ТаблицаОсновныеСредства.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаОсновныеСредства.СчетУчета КАК СчетУчета,
		|	ТаблицаОсновныеСредства.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
		|	ТаблицаОсновныеСредства.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаОсновныеСредства.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ТаблицаОсновныеСредства.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
		|	ТаблицаОсновныеСредства.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ТаблицаОсновныеСредства.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаОсновныеСредства.ПрименяетсяИнвестиционныйВычет
		|			ТОГДА &ПроцентИнвестиционногоВычета
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПроцентИнвестиционногоВычета
		|ПОМЕСТИТЬ ТаблицаОсновныеСредства
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ТаблицаОсновныеСредства
		|ГДЕ
		|	ТаблицаОсновныеСредства.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Реквизиты.ЕстьТопливо Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаТопливо", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаТопливо.Ссылка КАК Ссылка,
		|	ТаблицаТопливо.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТопливо.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ТаблицаТопливо.Номенклатура КАК Номенклатура,
		|	ТаблицаТопливо.Количество КАК Количество,
		|	ТаблицаТопливо.Цена КАК Цена,
		|	ТаблицаТопливо.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТопливо.СуммаНДС КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаТопливо.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаТопливо.Сумма
		|		ИНАЧЕ ТаблицаТопливо.Сумма + ТаблицаТопливо.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаТопливо.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаТопливо.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаТопливо.Сумма
		|		ИНАЧЕ ТаблицаТопливо.Сумма + ТаблицаТопливо.СуммаНДС
		|	КОНЕЦ КАК Сумма,
		|	&СчетУчетаТопливоВБаке КАК СчетУчета,
		|	ЛОЖЬ КАК СчетУчетаЗабалансовый,
		|	ТаблицаТопливо.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаТопливо.ОтражениеВУСН КАК ОтражениеВУСН,
		|	ТаблицаТопливо.СпособУчетаНДС КАК СпособУчетаНДС
		|ПОМЕСТИТЬ ТаблицаТопливо
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Топливо КАК ТаблицаТопливо
		|ГДЕ
		|	ТаблицаТопливо.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Реквизиты.ЕстьПрава Тогда
		НомераТаблиц.Вставить("ВременнаяТаблицаПрава", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаПрава.Ссылка КАК Ссылка,
		|	ТаблицаПрава.НомерСтроки КАК НомерСтроки,
		|	ТаблицаПрава.Содержание КАК Содержание,
		|	ТаблицаПрава.ОбъектПрава КАК ОбъектПрава,
		|	ТаблицаПрава.Количество КАК Количество,
		|	ТаблицаПрава.Цена КАК Цена,
		|	ТаблицаПрава.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаПрава.Сумма
		|		ИНАЧЕ ТаблицаПрава.Сумма + ТаблицаПрава.СуммаНДС
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаПрава.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Ссылка.СуммаВключаетНДС
		|			ТОГДА ТаблицаПрава.Сумма
		|		ИНАЧЕ ТаблицаПрава.Сумма + ТаблицаПрава.СуммаНДС
		|	КОНЕЦ КАК СуммаРуб,
		|	ТаблицаПрава.СуммаНДС КАК СуммаНДСРуб,
		|	ТаблицаПрава.Счет КАК СчетЗатрат,
		|	ТаблицаПрава.Подразделение КАК ПодразделениеЗатрат,
		|	ТаблицаПрава.Субконто1 КАК Субконто1,
		|	ТаблицаПрава.Субконто2 КАК Субконто2,
		|	ТаблицаПрава.Субконто3 КАК Субконто3,
		|	ТаблицаПрава.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаПрава.ПорядокВключенияСтоимостиВСоставРасходовУСН КАК ПорядокВключенияСтоимостиВСоставРасходовУСН,
		|	ТаблицаПрава.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.СрокИспользования <= 12
		|					И НематериальныеАктивы.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив)
		|				ИЛИ НематериальныеАктивы.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.ПустаяСсылка)
		|				ИЛИ &ЭтоПлательщикНДФЛ
		|					И НематериальныеАктивы.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Расходы)
		|		КОГДА &ЭтоПлательщикНДФЛ
		|				И НематериальныеАктивы.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив)
		|		ИНАЧЕ ЕСТЬNULL(НематериальныеАктивы.ОтражениеБУ, ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Расходы))
		|	КОНЕЦ КАК ОтражениеБУ,
		|	ВЫБОР
		|		КОГДА НематериальныеАктивы.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|		КОГДА ТаблицаПрава.СрокИспользования <= 12
		|				ИЛИ НематериальныеАктивы.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования)
		|		ИНАЧЕ ЕСТЬNULL(НематериальныеАктивы.ОтражениеНУ, ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования))
		|	КОНЕЦ КАК ОтражениеНУ,
		|	ТаблицаПрава.СрокИспользования КАК СрокИспользования,
		|	НАЧАЛОПЕРИОДА(ВЫБОР
		|			КОГДА Реквизиты.Дата > НематериальныеАктивы.НачалоДействияПрава
		|				ТОГДА Реквизиты.Дата
		|			ИНАЧЕ НематериальныеАктивы.НачалоДействияПрава
		|		КОНЕЦ, ДЕНЬ) КАК ДатаНачалаСписания,
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВЫБОР
		|						КОГДА Реквизиты.Дата > НематериальныеАктивы.НачалоДействияПрава
		|							ТОГДА Реквизиты.Дата
		|						ИНАЧЕ НематериальныеАктивы.НачалоДействияПрава
		|					КОНЕЦ, ДЕНЬ), МЕСЯЦ, ТаблицаПрава.СрокИспользования), ДЕНЬ, -1), ДЕНЬ) КАК ДатаОкончанияСписания,
		|	&УказанЛимитСтоимости
		|		И (ВЫРАЗИТЬ(ВЫБОР
		|				КОГДА &РаздельныйУчетНаСчете19
		|						И (ТаблицаПрава.СпособУчетаНДС = ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.УчитываетсяВCтоимости)
		|							ИЛИ ТаблицаПрава.СпособУчетаНДС = ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.Распределяется))
		|					ТОГДА ВЫБОР
		|							КОГДА ТаблицаПрава.Ссылка.СуммаВключаетНДС
		|								ТОГДА ТаблицаПрава.Сумма
		|							ИНАЧЕ ТаблицаПрава.Сумма + ТаблицаПрава.СуммаНДС
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ТаблицаПрава.Ссылка.СуммаВключаетНДС
		|							ТОГДА ТаблицаПрава.Сумма - ТаблицаПрава.СуммаНДС
		|						ИНАЧЕ ТаблицаПрава.Сумма
		|					КОНЕЦ
		|			КОНЕЦ * ВЫБОР
		|				КОГДА ТаблицаПрава.Ссылка.ВалютаДокумента <> &ВалютаРеглУчета
		|					ТОГДА ВЫБОР
		|							КОГДА ТаблицаПрава.Ссылка.КратностьВзаиморасчетов > 0
		|								ТОГДА ТаблицаПрава.Ссылка.КурсВзаиморасчетов / ТаблицаПрава.Ссылка.КратностьВзаиморасчетов
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ 1
		|			КОНЕЦ КАК ЧИСЛО(31, 2))) < &ЛимитСтоимостиСущественныхНМА
		|		И НЕ &ЭтоПлательщикНДФЛ КАК ЭтоМалоценныйНМА,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(ВЫБОР
		|					КОГДА ВидСубконтоСтатьяЗатрат.НомерСтроки = 1
		|						ТОГДА ТаблицаПрава.Субконто1
		|					КОГДА ВидСубконтоСтатьяЗатрат.НомерСтроки = 2
		|						ТОГДА ТаблицаПрава.Субконто2
		|					КОГДА ВидСубконтоСтатьяЗатрат.НомерСтроки = 3
		|						ТОГДА ТаблицаПрава.Субконто3
		|				КОНЕЦ КАК Справочник.СтатьиЗатрат).ВидРасходовНУ, ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПустаяСсылка)) = &ВидРасходовПовышающийКоэффициентНУ
		|		И НематериальныеАктивы.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования)
		|		И НЕ НематериальныеАктивы.ЭтоИсключительноеПраво
		|		И НематериальныеАктивы.ВключенВРеестрРоссийскогоПО
		|		И НематериальныеАктивы.НомерРеестрРоссийскогоПО <> """"
		|		И НематериальныеАктивы.ДатаРеестрРоссийскогоПО <> ДАТАВРЕМЯ(1, 1, 1)
		|		И НематериальныеАктивы.ДатаРеестрРоссийскогоПО <= Реквизиты.Дата КАК ПрименяетсяПовышающийКоэффициент
		|ПОМЕСТИТЬ ТаблицаПрава
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Права КАК ТаблицаПрава
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НематериальныеАктивы КАК НематериальныеАктивы
		|		ПО ТаблицаПрава.ОбъектПрава = НематериальныеАктивы.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидСубконтоСтатьяЗатрат
		|		ПО ТаблицаПрава.Счет = ВидСубконтоСтатьяЗатрат.Ссылка
		|			И (ВидСубконтоСтатьяЗатрат.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат))
		|ГДЕ
		|	ТаблицаПрава.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ПодготовитьТаблицыДокументаРасчетыВВалюте(Запрос, Реквизиты)
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокументаРасчетыВВалюте(НомераТаблиц, Реквизиты);
	Результат    = Запрос.ВыполнитьПакет();
	
	ТекстЗапроса = "";
	Если Реквизиты.ЕстьТовары Тогда
		СуммыТаблицыТовары = Результат[НомераТаблиц["СуммыТаблицыТовары"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыТовары, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыТовары", СуммыТаблицыТовары);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеТовары();
	КонецЕсли;
	Если Реквизиты.ЕстьУслуги Тогда
		СуммыТаблицыУслуги = Результат[НомераТаблиц["СуммыТаблицыУслуги"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыУслуги, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыУслуги", СуммыТаблицыУслуги);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеУслуги();
	КонецЕсли;
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		СуммыТаблицыАгентскиеУслуги = Результат[НомераТаблиц["СуммыТаблицыАгентскиеУслуги"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыАгентскиеУслуги, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыАгентскиеУслуги", СуммыТаблицыАгентскиеУслуги);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеАгентскиеУслуги();
	КонецЕсли;
	Если Реквизиты.ЕстьОборудование Тогда
		СуммыТаблицыОборудование = Результат[НомераТаблиц["СуммыТаблицыОборудование"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыОборудование, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыОборудование", СуммыТаблицыОборудование);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеОборудование();
	КонецЕсли;
	Если Реквизиты.ЕстьОбъектыСтроительства Тогда
		СуммыТаблицыОбъектыСтроительства = Результат[НомераТаблиц["СуммыТаблицыОбъектыСтроительства"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыОбъектыСтроительства, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыОбъектыСтроительства", СуммыТаблицыОбъектыСтроительства);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеОбъектыСтроительства();
	КонецЕсли;
	Если Реквизиты.ЕстьТара Тогда
		СуммыТаблицыТара = Результат[НомераТаблиц["СуммыТаблицыТара"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыТара, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыТара", СуммыТаблицыТара);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеТара();
	КонецЕсли;
	Если Реквизиты.ЕстьОсновныеСредства Тогда
		СуммыТаблицыОсновныеСредства = Результат[НомераТаблиц["СуммыТаблицыОсновныеСредства"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыОсновныеСредства, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыОсновныеСредства", СуммыТаблицыОсновныеСредства);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеОсновныеСредства();
	КонецЕсли;
	Если Реквизиты.ЕстьПрава Тогда
		СуммыТаблицыПрава = Результат[НомераТаблиц["СуммыТаблицыПрава"]].Выгрузить();
		УчетВзаиморасчетов.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыПрава, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыПрава", СуммыТаблицыПрава);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеПрава();
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результат    = Запрос.ВыполнитьПакет();
	
КонецПроцедуры

Функция ТекстЗапросаВременныеТаблицыДокументаРасчетыВВалюте(НомераТаблиц, Реквизиты)

	ТекстЗапроса = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
	
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаТовары", "ПОМЕСТИТЬ ВременнаяТаблицаТовары");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаТовары.Ссылка = &Ссылка", "ТаблицаТовары.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыТовары", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.НомерСтроки,
		|	ВременнаяТаблицаТовары.СтавкаНДС,
		|	ВременнаяТаблицаТовары.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаТовары.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаТовары.СуммаРуб,
		|	ВременнаяТаблицаТовары.СуммаНДСРуб,
		|	ВременнаяТаблицаТовары.СуммаПодтвержденнаяРуб,
		|	ВременнаяТаблицаТовары.СуммаНДСПодтвержденнаяРуб
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаУслуги", "ПОМЕСТИТЬ ВременнаяТаблицаУслуги");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаУслуги.Ссылка = &Ссылка", "ТаблицаУслуги.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаУслуги.НомерСтроки,
		|	ВременнаяТаблицаУслуги.СтавкаНДС,
		|	ВременнаяТаблицаУслуги.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаУслуги.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаУслуги.СуммаРуб,
		|	ВременнаяТаблицаУслуги.СуммаНДСРуб,
		|	ВременнаяТаблицаУслуги.СуммаПодтвержденнаяРуб,
		|	ВременнаяТаблицаУслуги.СуммаНДСПодтвержденнаяРуб
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаАгентскиеУслуги", "ПОМЕСТИТЬ ВременнаяТаблицаАгентскиеУслуги");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаАгентскиеУслуги.Ссылка = &Ссылка", "ТаблицаАгентскиеУслуги.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыАгентскиеУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаАгентскиеУслуги.НомерСтроки,
		|	ВременнаяТаблицаАгентскиеУслуги.СтавкаНДС,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаРуб,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаНДСРуб,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаПодтвержденнаяРуб,
		|	ВременнаяТаблицаАгентскиеУслуги.СуммаНДСПодтвержденнаяРуб
		|ИЗ
		|	ВременнаяТаблицаАгентскиеУслуги КАК ВременнаяТаблицаАгентскиеУслуги"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьОборудование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаОборудование", "ПОМЕСТИТЬ ВременнаяТаблицаОборудование");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаОборудование.Ссылка = &Ссылка", "ТаблицаОборудование.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыОборудование", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаОборудование.НомерСтроки,
		|	ВременнаяТаблицаОборудование.СтавкаНДС,
		|	ВременнаяТаблицаОборудование.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаОборудование.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаОборудование.СуммаРуб,
		|	ВременнаяТаблицаОборудование.СуммаНДСРуб
		|ИЗ
		|	ВременнаяТаблицаОборудование КАК ВременнаяТаблицаОборудование"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьОбъектыСтроительства Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаОбъектыСтроительства", "ПОМЕСТИТЬ ВременнаяТаблицаОбъектыСтроительства");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаОбъектыСтроительства.Ссылка = &Ссылка", "ТаблицаОбъектыСтроительства.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыОбъектыСтроительства", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаОбъектыСтроительства.НомерСтроки,
		|	ВременнаяТаблицаОбъектыСтроительства.СтавкаНДС,
		|	ВременнаяТаблицаОбъектыСтроительства.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаОбъектыСтроительства.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаОбъектыСтроительства.СуммаРуб,
		|	ВременнаяТаблицаОбъектыСтроительства.СуммаНДСРуб
		|ИЗ
		|	ВременнаяТаблицаОбъектыСтроительства КАК ВременнаяТаблицаОбъектыСтроительства"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьТара Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаТара", "ПОМЕСТИТЬ ВременнаяТаблицаТара");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаТара.Ссылка = &Ссылка", "ТаблицаТара.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыТара", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаТара.НомерСтроки,
		|	ВременнаяТаблицаТара.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаТара.СуммаРуб
		|ИЗ
		|	ВременнаяТаблицаТара КАК ВременнаяТаблицаТара"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьОсновныеСредства Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаОсновныеСредства", "ПОМЕСТИТЬ ВременнаяТаблицаОсновныеСредства");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаОсновныеСредства.Ссылка = &Ссылка", "ТаблицаОсновныеСредства.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыОсновныеСредства", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаОсновныеСредства.НомерСтроки,
		|	ВременнаяТаблицаОсновныеСредства.СтавкаНДС,
		|	ВременнаяТаблицаОсновныеСредства.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаОсновныеСредства.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаОсновныеСредства.СуммаРуб,
		|	ВременнаяТаблицаОсновныеСредства.СуммаНДСРуб
		|ИЗ
		|	ВременнаяТаблицаОсновныеСредства КАК ВременнаяТаблицаОсновныеСредства"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	Если Реквизиты.ЕстьПрава Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаПрава", "ПОМЕСТИТЬ ВременнаяТаблицаПрава");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаПрава.Ссылка = &Ссылка", "ТаблицаПрава.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыПрава", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаПрава.НомерСтроки,
		|	ВременнаяТаблицаПрава.СтавкаНДС,
		|	ВременнаяТаблицаПрава.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаПрава.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаПрава.СуммаРуб,
		|	ВременнаяТаблицаПрава.СуммаНДСРуб
		|ИЗ
		|	ВременнаяТаблицаПрава КАК ВременнаяТаблицаПрава"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРасчетыВВалютеТовары()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыТовары.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыТовары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаРуб КАК СуммаРуб,
	|	СуммыТаблицыТовары.СуммаНДСРуб КАК СуммаНДСРуб,
	|	СуммыТаблицыТовары.СуммаПодтвержденнаяРуб КАК СуммаПодтвержденнаяРуб,
	|	СуммыТаблицыТовары.СуммаНДСПодтвержденнаяРуб КАК СуммаНДСПодтвержденнаяРуб
	|ПОМЕСТИТЬ СуммыТаблицыТовары
	|ИЗ
	|	&СуммыТаблицыТовары КАК СуммыТаблицыТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаТовары.Ссылка КАК Ссылка,
	|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаТовары.Количество КАК Количество,
	|	СуммыТаблицыТовары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаРуб КАК СуммаРуб,
	|	СуммыТаблицыТовары.СуммаНДСРуб КАК СуммаНДСРуб,
	|	СуммыТаблицыТовары.СуммаПодтвержденнаяРуб КАК СуммаПодтвержденнаяРуб,
	|	СуммыТаблицыТовары.СуммаНДСПодтвержденнаяРуб КАК СуммаНДСПодтвержденнаяРуб,
	|	ВременнаяТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаТовары.ЦенаВРознице КАК ЦенаВРознице,
	|	ВременнаяТаблицаТовары.СуммаВРознице КАК СуммаВРознице,
	|	ВременнаяТаблицаТовары.СтавкаНДСВРознице КАК СтавкаНДСВРознице,
	|	ВременнаяТаблицаТовары.НомерГТД КАК НомерГТД,
	|	ВременнаяТаблицаТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВременнаяТаблицаТовары.СчетУчета КАК СчетУчета,
	|	ВременнаяТаблицаТовары.СчетУчетаЗабалансовый КАК СчетУчетаЗабалансовый,
	|	ВременнаяТаблицаТовары.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ВременнаяТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ВременнаяТаблицаТовары.ОтражениеВУСН КАК ОтражениеВУСН,
	|	ВременнаяТаблицаТовары.СчетЗатрат КАК СчетЗатрат,
	|	ВременнаяТаблицаТовары.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|	ВременнаяТаблицаТовары.Субконто1 КАК Субконто1,
	|	ВременнаяТаблицаТовары.Субконто2 КАК Субконто2,
	|	ВременнаяТаблицаТовары.Субконто3 КАК Субконто3,
	|	ВременнаяТаблицаТовары.Малоценка КАК Малоценка,
	|	ВременнаяТаблицаТовары.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаТовары.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВременнаяТаблицаТовары.СчетРасчетов КАК СчетРасчетов,
	|	ВременнаяТаблицаТовары.КомиссияПоЗакупке КАК КомиссияПоЗакупке,
	|	ВременнаяТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВременнаяТаблицаТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|   ВЫБОР 
	|		КОГДА ВременнаяТаблицаТовары.Ссылка.СкладВСписке
	|			ТОГДА ВременнаяТаблицаТовары.Склад
	|		ИНАЧЕ ВременнаяТаблицаТовары.Ссылка.Склад
	|	КОНЕЦ КАК Склад
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыТовары КАК СуммыТаблицыТовары
	|		ПО ВременнаяТаблицаТовары.НомерСтроки = СуммыТаблицыТовары.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеУслуги()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыУслуги.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаРуб,
	|	СуммыТаблицыУслуги.СуммаНДСРуб,
	|	СуммыТаблицыУслуги.СуммаПодтвержденнаяРуб,
	|	СуммыТаблицыУслуги.СуммаНДСПодтвержденнаяРуб
	|ПОМЕСТИТЬ СуммыТаблицыУслуги
	|ИЗ
	|	&СуммыТаблицыУслуги КАК СуммыТаблицыУслуги
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаУслуги.Ссылка,
	|	ВременнаяТаблицаУслуги.НомерСтроки,
	|	ВременнаяТаблицаУслуги.Номенклатура,
	|	ВременнаяТаблицаУслуги.Содержание,
	|	ВременнаяТаблицаУслуги.Количество,
	|	СуммыТаблицыУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаРуб,
	|	СуммыТаблицыУслуги.СуммаНДСРуб,
	|	СуммыТаблицыУслуги.СуммаПодтвержденнаяРуб,
	|	СуммыТаблицыУслуги.СуммаНДСПодтвержденнаяРуб,
	|	ВременнаяТаблицаУслуги.СтавкаНДС,
	|	ВременнаяТаблицаУслуги.СчетУчетаНДС,
	|	ВременнаяТаблицаУслуги.СпособУчетаНДС,
	|	ВременнаяТаблицаУслуги.ПодразделениеЗатрат,
	|	ВременнаяТаблицаУслуги.СчетЗатрат,
	|	ВременнаяТаблицаУслуги.Субконто1,
	|	ВременнаяТаблицаУслуги.Субконто2,
	|	ВременнаяТаблицаУслуги.Субконто3,
	|	ВременнаяТаблицаУслуги.СчетЗатратНУ,
	|	ВременнаяТаблицаУслуги.СубконтоНУ1,
	|	ВременнаяТаблицаУслуги.СубконтоНУ2,
	|	ВременнаяТаблицаУслуги.СубконтоНУ3,
	|	ВременнаяТаблицаУслуги.ОтражениеВУСН,
	|	ВременнаяТаблицаУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаУслуги
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыУслуги КАК СуммыТаблицыУслуги
	|		ПО ВременнаяТаблицаУслуги.НомерСтроки = СуммыТаблицыУслуги.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеАгентскиеУслуги()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыАгентскиеУслуги.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыАгентскиеУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыАгентскиеУслуги.СуммаРуб,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСРуб,
	|	СуммыТаблицыАгентскиеУслуги.СуммаПодтвержденнаяРуб,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСПодтвержденнаяРуб
	|ПОМЕСТИТЬ СуммыТаблицыАгентскиеУслуги
	|ИЗ
	|	&СуммыТаблицыАгентскиеУслуги КАК СуммыТаблицыАгентскиеУслуги
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаАгентскиеУслуги.Ссылка,
	|	ВременнаяТаблицаАгентскиеУслуги.НомерСтроки,
	|	ВременнаяТаблицаАгентскиеУслуги.Номенклатура,
	|	ВременнаяТаблицаАгентскиеУслуги.Содержание,
	|	ВременнаяТаблицаАгентскиеУслуги.Количество,
	|	СуммыТаблицыАгентскиеУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыАгентскиеУслуги.СуммаРуб,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСРуб,
	|	СуммыТаблицыАгентскиеУслуги.СуммаПодтвержденнаяРуб,
	|	СуммыТаблицыАгентскиеУслуги.СуммаНДСПодтвержденнаяРуб,
	|	ВременнаяТаблицаАгентскиеУслуги.Контрагент,
	|	ВременнаяТаблицаАгентскиеУслуги.ДоговорКонтрагента,
	|	ВременнаяТаблицаАгентскиеУслуги.СчетРасчетов,
	|	ВременнаяТаблицаАгентскиеУслуги.СтавкаНДС,
	|	ВременнаяТаблицаАгентскиеУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ТаблицаАгентскиеУслуги
	|ИЗ
	|	ВременнаяТаблицаАгентскиеУслуги КАК ВременнаяТаблицаАгентскиеУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыАгентскиеУслуги КАК СуммыТаблицыАгентскиеУслуги
	|		ПО ВременнаяТаблицаАгентскиеУслуги.НомерСтроки = СуммыТаблицыАгентскиеУслуги.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеОборудование()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыОборудование.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыОборудование.СуммаВзаиморасчетов,
	|	СуммыТаблицыОборудование.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыОборудование.СуммаРуб,
	|	СуммыТаблицыОборудование.СуммаНДСРуб
	|ПОМЕСТИТЬ СуммыТаблицыОборудование
	|ИЗ
	|	&СуммыТаблицыОборудование КАК СуммыТаблицыОборудование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОборудование.Ссылка,
	|	ВременнаяТаблицаОборудование.НомерСтроки,
	|	ВременнаяТаблицаОборудование.Номенклатура,
	|	ВременнаяТаблицаОборудование.Количество,
	|	СуммыТаблицыОборудование.СуммаВзаиморасчетов,
	|	СуммыТаблицыОборудование.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыОборудование.СуммаРуб,
	|	СуммыТаблицыОборудование.СуммаНДСРуб,
	|	ВременнаяТаблицаОборудование.СтавкаНДС,
	|	ВременнаяТаблицаОборудование.НомерГТД,
	|	ВременнаяТаблицаОборудование.СтранаПроисхождения,
	|	ВременнаяТаблицаОборудование.СчетУчета,
	|	ВременнаяТаблицаОборудование.СчетУчетаНДС,
	|	ВременнаяТаблицаОборудование.СпособУчетаНДС,
	|	ВременнаяТаблицаОборудование.ИдентификаторСтроки,
	|	ВременнаяТаблицаОборудование.ПрослеживаемыйКомплект
	|ПОМЕСТИТЬ ТаблицаОборудование
	|ИЗ
	|	ВременнаяТаблицаОборудование КАК ВременнаяТаблицаОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыОборудование КАК СуммыТаблицыОборудование
	|		ПО ВременнаяТаблицаОборудование.НомерСтроки = СуммыТаблицыОборудование.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеОбъектыСтроительства()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыОбъектыСтроительства.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыОбъектыСтроительства.СуммаВзаиморасчетов,
	|	СуммыТаблицыОбъектыСтроительства.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыОбъектыСтроительства.СуммаРуб,
	|	СуммыТаблицыОбъектыСтроительства.СуммаНДСРуб
	|ПОМЕСТИТЬ СуммыТаблицыОбъектыСтроительства
	|ИЗ
	|	&СуммыТаблицыОбъектыСтроительства КАК СуммыТаблицыОбъектыСтроительства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОбъектыСтроительства.Ссылка,
	|	ВременнаяТаблицаОбъектыСтроительства.НомерСтроки,
	|	ВременнаяТаблицаОбъектыСтроительства.ОбъектСтроительства,
	|	ВременнаяТаблицаОбъектыСтроительства.СтатьяЗатрат,
	|	СуммыТаблицыОбъектыСтроительства.СуммаВзаиморасчетов,
	|	СуммыТаблицыОбъектыСтроительства.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыОбъектыСтроительства.СуммаРуб,
	|	СуммыТаблицыОбъектыСтроительства.СуммаНДСРуб,
	|	ВременнаяТаблицаОбъектыСтроительства.СтавкаНДС,
	|	ВременнаяТаблицаОбъектыСтроительства.СчетУчета,
	|	ВременнаяТаблицаОбъектыСтроительства.СчетУчетаНДС,
	|	ВременнаяТаблицаОбъектыСтроительства.СпособУчетаНДС
	|ПОМЕСТИТЬ ТаблицаОбъектыСтроительства
	|ИЗ
	|	ВременнаяТаблицаОбъектыСтроительства КАК ВременнаяТаблицаОбъектыСтроительства
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыОбъектыСтроительства КАК СуммыТаблицыОбъектыСтроительства
	|		ПО ВременнаяТаблицаОбъектыСтроительства.НомерСтроки = СуммыТаблицыОбъектыСтроительства.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеТара()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыТара.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыТара.СуммаВзаиморасчетов,
	|	СуммыТаблицыТара.СуммаРуб
	|ПОМЕСТИТЬ СуммыТаблицыТара
	|ИЗ
	|	&СуммыТаблицыТара КАК СуммыТаблицыТара
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаТара.Ссылка,
	|	ВременнаяТаблицаТара.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаТара.Номенклатура,
	|	ВременнаяТаблицаТара.Количество,
	|	СуммыТаблицыТара.СуммаВзаиморасчетов,
	|	СуммыТаблицыТара.СуммаРуб,
	|	ВременнаяТаблицаТара.СчетУчета,
	|	ВременнаяТаблицаТара.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаТара
	|ИЗ
	|	ВременнаяТаблицаТара КАК ВременнаяТаблицаТара
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыТара КАК СуммыТаблицыТара
	|		ПО ВременнаяТаблицаТара.НомерСтроки = СуммыТаблицыТара.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеПрава()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыПрава.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыПрава.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СуммыТаблицыПрава.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыПрава.СуммаРуб КАК СуммаРуб,
	|	СуммыТаблицыПрава.СуммаНДСРуб КАК СуммаНДСРуб
	|ПОМЕСТИТЬ СуммыТаблицыПрава
	|ИЗ
	|	&СуммыТаблицыПрава КАК СуммыТаблицыПрава
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаПрава.Ссылка КАК Ссылка,
	|	ВременнаяТаблицаПрава.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаПрава.Содержание КАК Содержание,
	|	ВременнаяТаблицаПрава.ОбъектПрава КАК ОбъектПрава,
	|	ВременнаяТаблицаПрава.Количество КАК Количество,
	|	СуммыТаблицыПрава.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СуммыТаблицыПрава.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыПрава.СуммаРуб КАК СуммаРуб,
	|	СуммыТаблицыПрава.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ВременнаяТаблицаПрава.Цена КАК Цена,
	|	ВременнаяТаблицаПрава.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаПрава.СчетЗатрат КАК СчетЗатрат,
	|	ВременнаяТаблицаПрава.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
	|	ВременнаяТаблицаПрава.Субконто1 КАК Субконто1,
	|	ВременнаяТаблицаПрава.Субконто2 КАК Субконто2,
	|	ВременнаяТаблицаПрава.Субконто3 КАК Субконто3,
	|	ВременнаяТаблицаПрава.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ВременнаяТаблицаПрава.ПорядокВключенияСтоимостиВСоставРасходовУСН КАК ПорядокВключенияСтоимостиВСоставРасходовУСН,
	|	ВременнаяТаблицаПрава.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ВременнаяТаблицаПрава.ОтражениеБУ КАК ОтражениеБУ,
	|	ВременнаяТаблицаПрава.ОтражениеНУ КАК ОтражениеНУ,
	|	ВременнаяТаблицаПрава.СрокИспользования КАК СрокИспользования,
	|	ВременнаяТаблицаПрава.ДатаНачалаСписания КАК ДатаНачалаСписания,
	|	ВременнаяТаблицаПрава.ДатаОкончанияСписания КАК ДатаОкончанияСписания,
	|	ВременнаяТаблицаПрава.ЭтоМалоценныйНМА КАК ЭтоМалоценныйНМА,
	|	ВременнаяТаблицаПрава.ПрименяетсяПовышающийКоэффициент КАК ПрименяетсяПовышающийКоэффициент
	|ПОМЕСТИТЬ ТаблицаПрава
	|ИЗ
	|	ВременнаяТаблицаПрава КАК ВременнаяТаблицаПрава
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыПрава КАК СуммыТаблицыПрава
	|		ПО ВременнаяТаблицаПрава.НомерСтроки = СуммыТаблицыПрава.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Реквизиты.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовРеквизиты",        Неопределено);
		ПараметрыПроведения.Вставить("ЗачетАвансовТаблицаДокумента", Неопределено);
		ПараметрыПроведения.Вставить("ТаблицаЗачетАвансов",          Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСуммВзаиморасчетов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовРеквизиты",        НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовТаблицаДокумента", НомераТаблиц.Количество());
	
	ТекстЗапросаТаблицаСуммВзаиморасчетов = "
	|ВЫБРАТЬ
	|		0 КАК СуммаВзаиморасчетов,
	|		0 КАК СуммаРуб";
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапросаТаблицаСуммВзаиморасчетов = ТекстЗапросаТаблицаСуммВзаиморасчетов + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаТовары.СуммаВзаиморасчетов - ТаблицаТовары.СуммаНДСВзаиморасчетов
		|			ИНАЧЕ ТаблицаТовары.СуммаВзаиморасчетов
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаТовары.СуммаРуб - ТаблицаТовары.СуммаНДСРуб
		|			ИНАЧЕ ТаблицаТовары.СуммаРуб
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаТовары КАК ТаблицаТовары";
		
	КонецЕсли; 
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапросаТаблицаСуммВзаиморасчетов = ТекстЗапросаТаблицаСуммВзаиморасчетов + "
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаУслуги.СуммаВзаиморасчетов - ТаблицаУслуги.СуммаНДСВзаиморасчетов
		|			ИНАЧЕ ТаблицаУслуги.СуммаВзаиморасчетов
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаУслуги.СуммаРуб - ТаблицаУслуги.СуммаНДСРуб
		|			ИНАЧЕ ТаблицаУслуги.СуммаРуб
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаУслуги КАК ТаблицаУслуги";
	
	КонецЕсли; 
	
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ТекстЗапросаТаблицаСуммВзаиморасчетов = ТекстЗапросаТаблицаСуммВзаиморасчетов + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаАгентскиеУслуги.СуммаВзаиморасчетов - ТаблицаАгентскиеУслуги.СуммаНДСВзаиморасчетов
		|			ИНАЧЕ ТаблицаАгентскиеУслуги.СуммаВзаиморасчетов
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаАгентскиеУслуги.СуммаРуб - ТаблицаАгентскиеУслуги.СуммаНДСРуб
		|			ИНАЧЕ ТаблицаАгентскиеУслуги.СуммаРуб
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги";
	КонецЕсли; 
	
	Если Реквизиты.ЕстьОборудование Тогда
		ТекстЗапросаТаблицаСуммВзаиморасчетов = ТекстЗапросаТаблицаСуммВзаиморасчетов + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаОборудование.СуммаВзаиморасчетов - ТаблицаОборудование.СуммаНДСВзаиморасчетов
		|			ИНАЧЕ ТаблицаОборудование.СуммаВзаиморасчетов
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаОборудование.СуммаРуб - ТаблицаОборудование.СуммаНДСРуб
		|			ИНАЧЕ ТаблицаОборудование.СуммаРуб
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаОборудование КАК ТаблицаОборудование";
	КонецЕсли; 
	
	Если Реквизиты.ЕстьОбъектыСтроительства Тогда
		ТекстЗапросаТаблицаСуммВзаиморасчетов = ТекстЗапросаТаблицаСуммВзаиморасчетов + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаОбъектыСтроительства.СуммаВзаиморасчетов - ТаблицаОбъектыСтроительства.СуммаНДСВзаиморасчетов
		|			ИНАЧЕ ТаблицаОбъектыСтроительства.СуммаВзаиморасчетов
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаОбъектыСтроительства.СуммаРуб - ТаблицаОбъектыСтроительства.СуммаНДСРуб
		|			ИНАЧЕ ТаблицаОбъектыСтроительства.СуммаРуб
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаОбъектыСтроительства КАК ТаблицаОбъектыСтроительства";
	КонецЕсли; 
	Если Реквизиты.ЕстьОсновныеСредства Тогда
		ТекстЗапросаТаблицаСуммВзаиморасчетов = ТекстЗапросаТаблицаСуммВзаиморасчетов + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаОсновныеСредства.СуммаВзаиморасчетов - ТаблицаОсновныеСредства.СуммаНДСВзаиморасчетов
		|			ИНАЧЕ ТаблицаОсновныеСредства.СуммаВзаиморасчетов
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаОсновныеСредства.СуммаРуб - ТаблицаОсновныеСредства.СуммаНДСРуб
		|			ИНАЧЕ ТаблицаОсновныеСредства.СуммаРуб
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаОсновныеСредства КАК ТаблицаОсновныеСредства";
	КонецЕсли; 
	Если Реквизиты.ЕстьТопливо Тогда
		ТекстЗапросаТаблицаСуммВзаиморасчетов = ТекстЗапросаТаблицаСуммВзаиморасчетов + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаТопливо.Сумма - ТаблицаТопливо.СуммаНДС
		|			ИНАЧЕ ТаблицаТопливо.Сумма
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаТопливо.Сумма - ТаблицаТопливо.СуммаНДС
		|			ИНАЧЕ ТаблицаТопливо.Сумма
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаТопливо КАК ТаблицаТопливо";
	КонецЕсли;
	
	Если Реквизиты.ЕстьПрава Тогда
		ТекстЗапросаТаблицаСуммВзаиморасчетов = ТекстЗапросаТаблицаСуммВзаиморасчетов + "
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаПрава.СуммаВзаиморасчетов - ТаблицаПрава.СуммаНДСВзаиморасчетов
		|			ИНАЧЕ ТаблицаПрава.СуммаВзаиморасчетов
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &УчетАгентскогоНДС
		|				ТОГДА ТаблицаПрава.СуммаРуб - ТаблицаПрава.СуммаНДСРуб
		|			ИНАЧЕ ТаблицаПрава.СуммаРуб
		|		КОНЕЦ
		|	ИЗ
		|		ТаблицаПрава КАК ТаблицаПрава";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаРуб) КАК СуммаРуб
	|ПОМЕСТИТЬ ТаблицаСуммВзаиморасчетов
	|ИЗ
	|	#ТаблицаСуммВзаиморасчетов КАК ТаблицаСуммВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.СчетУчетаРасчетовСКомиссионером КАК СчетРасчетовСКомиссионером,
	|	&ПрименяетсяУСН КАК УчитыватьЗадолженностьУСН,
	|	&ПрименяетсяПСН КАК УчитыватьЗадолженностьУСНПатент,
	|	ЛОЖЬ КАК ДеятельностьНаПатенте,
	|	ЛОЖЬ КАК ДеятельностьНаТорговомСборе,
	|	""Поступление"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	Реквизиты.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	&УчетАгентскогоНДС КАК УчетАгентскогоНДС
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК ДокументРасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетРасчетов,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВЫБОР
	|		КОГДА Реквизиты.ЭлектронныеУслуги
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ Реквизиты.РасчетыВУсловныхЕдиницах
	|	КОНЕЦ КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	&РасчетыВВалюте КАК РасчетыВВалюте,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаСуммВзаиморасчетов.СуммаРуб КАК СуммаРуб,
	|	0 КАК СуммаВзаиморасчетовКомитента,
	|	0 КАК СуммаВзаиморасчетовТорговыйСбор,
	|	0 КАК СуммаВзаиморасчетовЕНВД,
	|	0 КАК СуммаВзаиморасчетовПатент
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСуммВзаиморасчетов КАК ТаблицаСуммВзаиморасчетов
	|		ПО (ИСТИНА)"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаСуммВзаиморасчетов", "("+ ТекстЗапросаТаблицаСуммВзаиморасчетов+ ")");
	
	Если Реквизиты.СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		ПараметрыПроведения.Вставить("ТаблицаЗачетАвансов", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаЗачетАвансов", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса
		+ "ВЫБРАТЬ
		|	ПоступлениеТоваровУслугЗачетАвансов.НомерСтроки КАК НомерСтроки,
		|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
		|	Реквизиты.Контрагент,
		|	Реквизиты.ДоговорКонтрагента,
		|	ПоступлениеТоваровУслугЗачетАвансов.ДокументАванса,
		|	ПоступлениеТоваровУслугЗачетАвансов.СуммаЗачета
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.ЗачетАвансов КАК ПоступлениеТоваровУслугЗачетАвансов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ПоступлениеТоваровУслугЗачетАвансов.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
		
КонецФункции

Функция ТекстЗапросаЗачетАвансовКомиссияПоЗакупке(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если НЕ Реквизиты.ЕстьТовары
		И НЕ Реквизиты.ЕстьАгентскиеУслуги
		ИЛИ Реквизиты.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом 
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовКомиссияПоЗакупкеРеквизиты", Неопределено);
		Возврат "";
	Иначе
		НомераТаблиц.Вставить("ЗачетАвансовКомиссияПоЗакупкеРеквизиты", НомераТаблиц.Количество());
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Дата КАК Период,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
		|	ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически) КАК СпособЗачетаАвансов,
		|	ЛОЖЬ КАК УчитыватьЗадолженностьУСН,
		|	ЛОЖЬ КАК УчитыватьЗадолженностьУСНПатент,
		|	""Выбытие"" КАК НаправлениеДвижения,
		|	ЛОЖЬ КАК ЭтоВозврат,
		|	Реквизиты.НДСВключенВСтоимость
		|ИЗ
		|	Реквизиты КАК Реквизиты";
		Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
		
КонецФункции

Функция ТекстЗапросаПоступление(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	ТекстЗапроса = "";
	
	Если НЕ Реквизиты.ЕстьТовары
		ИЛИ Реквизиты.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом 
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		ПараметрыПроведения.Вставить("ТаблицаТовары", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаТовары", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	""Товары"" КАК ИмяСписка,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаТовары.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаТовары.СуммаРуб КАК СуммаРуб,
		|	ТаблицаТовары.СуммаРуб КАК СуммаБУ,
		|	ТаблицаТовары.СуммаРуб КАК СуммаНУ,
		|	ТаблицаТовары.СуммаНДСРуб КАК СуммаНДСРуб,
		|	ТаблицаТовары.СуммаПодтвержденнаяРуб КАК СуммаПодтвержденнаяРуб,
		|	ТаблицаТовары.СуммаНДСПодтвержденнаяРуб КАК СуммаНДСПодтвержденнаяРуб,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.СчетУчета КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчетаЗабалансовый
		|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
		|	КОНЕЦ КАК КорСчет,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчетаЗабалансовый
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
		|	КОНЕЦ КАК ВидКорСубконто1,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчетаЗабалансовый
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
		|	КОНЕЦ КАК ВидКорСубконто2,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчетаЗабалансовый
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами)
		|	КОНЕЦ КАК ВидКорСубконто3,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчетаЗабалансовый
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК КорСубконто1,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчетаЗабалансовый
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.ДоговорКонтрагента
		|	КОНЕЦ КАК КорСубконто2,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.СчетУчетаЗабалансовый
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Ссылка
		|	КОНЕЦ КАК КорСубконто3,
		|	ТаблицаТовары.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаТовары.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
		|	ТаблицаТовары.Субконто1 КАК Субконто1,
		|	ТаблицаТовары.Субконто2 КАК Субконто2,
		|	ТаблицаТовары.Субконто3 КАК Субконто3,
		|	ТаблицаТовары.Малоценка КАК Малоценка,
		|	ТаблицаТовары.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТовары.ЦенаВРознице КАК ЦенаВРознице,
		|	ТаблицаТовары.СуммаВРознице КАК СуммаВРознице,
		|	ТаблицаТовары.СтавкаНДСВРознице КАК СтавкаНДСВРознице,
		|	ТаблицаТовары.ИдентификаторСтроки Как ИдентификаторСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Ссылка.СкладВСписке
		|			ТОГДА ТаблицаТовары.Склад
		|		ИНАЧЕ ТаблицаТовары.Ссылка.Склад
		|   КОНЕЦ КАК Склад
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ТаблицаТовары.КомиссияПоЗакупке
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Не Реквизиты.ЕстьУслуги Тогда
		ПараметрыПроведения.Вставить("ТаблицаУслуги", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаУслуги.Ссылка,
		|	""Услуги"" КАК ИмяСписка,
		|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
		|	ТаблицаУслуги.Содержание,
		|	ТаблицаУслуги.СуммаВзаиморасчетов,
		|	ТаблицаУслуги.СуммаНДСВзаиморасчетов,
		|	ТаблицаУслуги.СуммаРуб КАК СуммаБУ,
		|	ТаблицаУслуги.СуммаРуб КАК СуммаНУ,
		|	ТаблицаУслуги.СуммаРуб,
		|	ТаблицаУслуги.СуммаНДСРуб,
		|	ТаблицаУслуги.СуммаПодтвержденнаяРуб,
		|	ТаблицаУслуги.СуммаНДСПодтвержденнаяРуб,
		|	ТаблицаУслуги.СчетЗатрат,
		|	ТаблицаУслуги.СчетУчетаНДС,
		|	ТаблицаУслуги.СпособУчетаНДС,
		|	ТаблицаУслуги.СтавкаНДС,
		|	ТаблицаУслуги.Субконто1,
		|	ТаблицаУслуги.Субконто2,
		|	ТаблицаУслуги.Субконто3,
		|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
		|	Реквизиты.Контрагент КАК КорСубконто1,
		|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
		|	Реквизиты.Ссылка КАК КорСубконто3,
		|	ТаблицаУслуги.СчетЗатратНУ,
		|	ТаблицаУслуги.СубконтоНУ1,
		|	ТаблицаУслуги.СубконтоНУ2,
		|	ТаблицаУслуги.СубконтоНУ3,
		|	ТаблицаУслуги.ПодразделениеЗатрат,
		|	ТаблицаУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;

	Если Не Реквизиты.ЕстьПрава Тогда
		ПараметрыПроведения.Вставить("ТаблицаПрава", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ВременнаяТаблицаНазначенийИспользования", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ВременнаяТаблицаНазначенияИспользованияАмортизация", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ТаблицаПрава", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаПрава.НомерСтроки КАК НомерСтроки,
		|	МАКСИМУМ(НазначенияИспользованияНМА.Ссылка) КАК НазначениеИспользования
		|ПОМЕСТИТЬ ВременнаяТаблицаНазначенийИспользования
		|ИЗ
		|	ТаблицаПрава КАК ТаблицаПрава
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НазначенияИспользованияНМА КАК НазначенияИспользованияНМА
		|		ПО ТаблицаПрава.ОбъектПрава = НазначенияИспользованияНМА.Владелец
		|			И ТаблицаПрава.СчетЗатрат = НазначенияИспользованияНМА.Счет
		|			И ТаблицаПрава.ПодразделениеЗатрат = НазначенияИспользованияНМА.Подразделение
		|			И ТаблицаПрава.Субконто1 = НазначенияИспользованияНМА.Субконто1
		|			И ТаблицаПрава.Субконто2 = НазначенияИспользованияНМА.Субконто2
		|			И ТаблицаПрава.Субконто3 = НазначенияИспользованияНМА.Субконто3
		|			И ТаблицаПрава.ДатаНачалаСписания = НазначенияИспользованияНМА.ДатаНачалаСписания
		|			И ТаблицаПрава.ДатаОкончанияСписания = НазначенияИспользованияНМА.ДатаОкончанияСписания
		|			И (НЕ НазначенияИспользованияНМА.ПометкаУдаления)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПрава.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НазначенияИспользованияАмортизация.НомерСтроки КАК НомерСтроки,
		|	МАКСИМУМ(НазначенияИспользованияАмортизация.НазначениеИспользованияАмортизация) КАК НазначениеИспользованияАмортизация
		|ПОМЕСТИТЬ ВременнаяТаблицаНазначенияИспользованияАмортизация
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаПрава.НомерСтроки КАК НомерСтроки,
		|		НазначенияИспользованияНМА.Ссылка КАК НазначениеИспользованияАмортизация
		|	ИЗ
		|		ТаблицаПрава КАК ТаблицаПрава
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НазначенияИспользованияНМА КАК НазначенияИспользованияНМА
		|			ПО ТаблицаПрава.ОбъектПрава = НазначенияИспользованияНМА.Владелец
		|				И ТаблицаПрава.СчетЗатрат = НазначенияИспользованияНМА.Счет
		|				И ТаблицаПрава.ПодразделениеЗатрат = НазначенияИспользованияНМА.Подразделение
		|				И ТаблицаПрава.ДатаНачалаСписания = НазначенияИспользованияНМА.ДатаНачалаСписания
		|				И ТаблицаПрава.ДатаОкончанияСписания = НазначенияИспользованияНМА.ДатаОкончанияСписания
		|				И (НЕ НазначенияИспользованияНМА.ПометкаУдаления)
		|				И (НазначенияИспользованияНМА.Субконто1 = &СтатьяЗатратАмортизация)
		|				И ТаблицаПрава.Субконто2 = НазначенияИспользованияНМА.Субконто2
		|				И ТаблицаПрава.Субконто3 = НазначенияИспользованияНМА.Субконто3
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|			ПО ТаблицаПрава.СчетЗатрат = ХозрасчетныйВидыСубконто.Ссылка
		|				И (ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат))
		|	ГДЕ
		|		ХозрасчетныйВидыСубконто.НомерСтроки = 1
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПрава.НомерСтроки,
		|		НазначенияИспользованияНМА.Ссылка
		|	ИЗ
		|		ТаблицаПрава КАК ТаблицаПрава
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НазначенияИспользованияНМА КАК НазначенияИспользованияНМА
		|			ПО ТаблицаПрава.ОбъектПрава = НазначенияИспользованияНМА.Владелец
		|				И ТаблицаПрава.СчетЗатрат = НазначенияИспользованияНМА.Счет
		|				И ТаблицаПрава.ПодразделениеЗатрат = НазначенияИспользованияНМА.Подразделение
		|				И ТаблицаПрава.ДатаНачалаСписания = НазначенияИспользованияНМА.ДатаНачалаСписания
		|				И ТаблицаПрава.ДатаОкончанияСписания = НазначенияИспользованияНМА.ДатаОкончанияСписания
		|				И (НЕ НазначенияИспользованияНМА.ПометкаУдаления)
		|				И (НазначенияИспользованияНМА.Субконто2 = &СтатьяЗатратАмортизация)
		|				И ТаблицаПрава.Субконто1 = НазначенияИспользованияНМА.Субконто1
		|				И ТаблицаПрава.Субконто3 = НазначенияИспользованияНМА.Субконто3
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|			ПО ТаблицаПрава.СчетЗатрат = ХозрасчетныйВидыСубконто.Ссылка
		|				И (ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат))
		|	ГДЕ
		|		ХозрасчетныйВидыСубконто.НомерСтроки = 2
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПрава.НомерСтроки,
		|		НазначенияИспользованияНМА.Ссылка
		|	ИЗ
		|		ТаблицаПрава КАК ТаблицаПрава
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НазначенияИспользованияНМА КАК НазначенияИспользованияНМА
		|			ПО ТаблицаПрава.ОбъектПрава = НазначенияИспользованияНМА.Владелец
		|				И ТаблицаПрава.СчетЗатрат = НазначенияИспользованияНМА.Счет
		|				И ТаблицаПрава.ПодразделениеЗатрат = НазначенияИспользованияНМА.Подразделение
		|				И ТаблицаПрава.ДатаНачалаСписания = НазначенияИспользованияНМА.ДатаНачалаСписания
		|				И ТаблицаПрава.ДатаОкончанияСписания = НазначенияИспользованияНМА.ДатаОкончанияСписания
		|				И (НЕ НазначенияИспользованияНМА.ПометкаУдаления)
		|				И (НазначенияИспользованияНМА.Субконто3 = &СтатьяЗатратАмортизация)
		|				И ТаблицаПрава.Субконто1 = НазначенияИспользованияНМА.Субконто1
		|				И ТаблицаПрава.Субконто2 = НазначенияИспользованияНМА.Субконто2
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|			ПО ТаблицаПрава.СчетЗатрат = ХозрасчетныйВидыСубконто.Ссылка
		|				И (ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат))
		|	ГДЕ
		|		ХозрасчетныйВидыСубконто.НомерСтроки = 3
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаПрава.НомерСтроки,
		|		НазначенияИспользованияНМА.Ссылка
		|	ИЗ
		|		ТаблицаПрава КАК ТаблицаПрава
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НазначенияИспользованияНМА КАК НазначенияИспользованияНМА
		|			ПО ТаблицаПрава.ОбъектПрава = НазначенияИспользованияНМА.Владелец
		|				И ТаблицаПрава.СчетЗатрат = НазначенияИспользованияНМА.Счет
		|				И ТаблицаПрава.ПодразделениеЗатрат = НазначенияИспользованияНМА.Подразделение
		|				И ТаблицаПрава.ДатаНачалаСписания = НазначенияИспользованияНМА.ДатаНачалаСписания
		|				И ТаблицаПрава.ДатаОкончанияСписания = НазначенияИспользованияНМА.ДатаОкончанияСписания
		|				И (НЕ НазначенияИспользованияНМА.ПометкаУдаления)
		|				И ТаблицаПрава.Субконто1 = НазначенияИспользованияНМА.Субконто1
		|				И ТаблицаПрава.Субконто2 = НазначенияИспользованияНМА.Субконто2
		|				И ТаблицаПрава.Субконто3 = НазначенияИспользованияНМА.Субконто3
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|			ПО ТаблицаПрава.СчетЗатрат = ХозрасчетныйВидыСубконто.Ссылка
		|				И (ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат))
		|	ГДЕ
		|		ХозрасчетныйВидыСубконто.НомерСтроки ЕСТЬ NULL) КАК НазначенияИспользованияАмортизация
		|
		|СГРУППИРОВАТЬ ПО
		|	НазначенияИспользованияАмортизация.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПрава.Ссылка КАК Ссылка,
		|	""Права"" КАК ИмяСписка,
		|	ТаблицаПрава.НомерСтроки КАК НомерСтроки,
		|	Реквизиты.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
		|	ТаблицаПрава.ДатаНачалаСписания КАК ДатаНачалаСписания,
		|	ТаблицаПрава.ДатаОкончанияСписания КАК ДатаОкончанияСписания,
		|	ТаблицаПрава.ОбъектПрава КАК НематериальныйАктив,
		|	ТаблицаПрава.Содержание КАК Содержание,
		|	ТаблицаПрава.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаПрава.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаПрава.СуммаРуб КАК СуммаРуб,
		|	ТаблицаПрава.СуммаРуб КАК СуммаБУ,
		|	ТаблицаПрава.СуммаРуб КАК СуммаНУ,
		|	ТаблицаПрава.СуммаНДСРуб КАК СуммаНДСРуб,
		|	0 КАК СуммаПодтвержденнаяРуб,
		|	0 КАК СуммаНДСПодтвержденнаяРуб,
		|	ТаблицаПрава.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаПрава.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ТаблицаПрава.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаПрава.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаПрава.ПодразделениеЗатрат КАК ПодразделениеЗатрат,
		|	ТаблицаПрава.Субконто1 КАК Субконто1,
		|	ТаблицаПрава.Субконто2 КАК Субконто2,
		|	ТаблицаПрава.Субконто3 КАК Субконто3,
		|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
		|	Реквизиты.Контрагент КАК КорСубконто1,
		|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
		|	Реквизиты.Ссылка КАК КорСубконто3,
		|	ТаблицаПрава.СрокИспользования КАК СрокИспользования,
		|	ТаблицаПрава.ОтражениеБУ КАК ОтражениеБУ,
		|	ТаблицаПрава.ОтражениеНУ КАК ОтражениеНУ,
		|	ЕСТЬNULL(НазначенияИспользования.НазначениеИспользования, ЗНАЧЕНИЕ(Справочник.НазначенияИспользованияНМА.ПустаяСсылка)) КАК НазначениеИспользования,
		|	ТаблицаПрава.ЭтоМалоценныйНМА КАК ЭтоМалоценныйНМА,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Аванс)
		|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеПравНаИспользованиеРезультатовИнтеллектуальнойДеятельности)
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеНематериальныхАктивов)
		|		ИНАЧЕ ТаблицаПрава.СчетЗатрат
		|	КОНЕЦ КАК СчетОтражениеБУ,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Аванс)
		|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидСубконтоОтражениеБУ1,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Аванс)
		|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидСубконтоОтражениеБУ2,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Аванс)
		|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НазначенияИспользованияНМА)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидСубконтоОтражениеБУ3,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Аванс)
		|			ТОГДА Реквизиты.Контрагент
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив)
		|			ТОГДА ТаблицаПрава.ОбъектПрава
		|		ИНАЧЕ ТаблицаПрава.Субконто1
		|	КОНЕЦ КАК СубконтоОтражениеБУ1,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Аванс)
		|			ТОГДА Реквизиты.ДоговорКонтрагента
		|		ИНАЧЕ ТаблицаПрава.Субконто2
		|	КОНЕЦ КАК СубконтоОтражениеБУ2,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Аванс)
		|			ТОГДА ЕСТЬNULL(НазначенияИспользования.НазначениеИспользования, ЗНАЧЕНИЕ(Справочник.НазначенияИспользованияНМА.ПустаяСсылка))
		|		ИНАЧЕ ТаблицаПрава.Субконто3
		|	КОНЕЦ КАК СубконтоОтражениеБУ3,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|			ТОГДА ТаблицаПрава.СчетЗатрат
		|		КОГДА ТаблицаПрава.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеПравНаИспользованиеРезультатовИнтеллектуальнойДеятельности)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеНематериальныхАктивов)
		|	КОНЕЦ КАК СчетОтражениеНУ,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|			ТОГДА 1
		|		КОГДА ТаблицаПрава.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы)
		|	КОНЕЦ КАК ВидСубконтоОтражениеНУ1,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|			ТОГДА 2
		|		КОГДА ТаблицаПрава.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидСубконтоОтражениеНУ2,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|			ТОГДА 3
		|		КОГДА ТаблицаПрава.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НазначенияИспользованияНМА)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидСубконтоОтражениеНУ3,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|			ТОГДА ТаблицаПрава.Субконто1
		|		КОГДА ТаблицаПрава.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА Реквизиты.Контрагент
		|		ИНАЧЕ ТаблицаПрава.ОбъектПрава
		|	КОНЕЦ КАК СубконтоОтражениеНУ1,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|			ТОГДА ТаблицаПрава.Субконто2
		|		КОГДА ТаблицаПрава.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА Реквизиты.ДоговорКонтрагента
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоОтражениеНУ2,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу)
		|			ТОГДА ТаблицаПрава.Субконто3
		|		КОГДА ТаблицаПрава.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|			ТОГДА ЕСТЬNULL(НазначенияИспользования.НазначениеИспользования, ЗНАЧЕНИЕ(Справочник.НазначенияИспользованияНМА.ПустаяСсылка))
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоОтражениеНУ3,
		|	ЕСТЬNULL(НазначенияИспользованияАмортизация.НазначениеИспользованияАмортизация, ЗНАЧЕНИЕ(Справочник.НазначенияИспользованияНМА.ПустаяСсылка)) КАК НазначениеИспользованияАмортизация,
		|	ТаблицаПрава.ПорядокВключенияСтоимостиВСоставРасходовУСН КАК ПорядокВключенияСтоимостиВСоставРасходовУСН,
		|	ТаблицаПрава.ПрименяетсяПовышающийКоэффициент КАК ПрименяетсяПовышающийКоэффициент
		|ИЗ
		|	ТаблицаПрава КАК ТаблицаПрава
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНазначенийИспользования КАК НазначенияИспользования
		|		ПО ТаблицаПрава.НомерСтроки = НазначенияИспользования.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНазначенияИспользованияАмортизация КАК НазначенияИспользованияАмортизация
		|		ПО ТаблицаПрава.НомерСтроки = НазначенияИспользованияАмортизация.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Не Реквизиты.ЕстьОборудование Тогда
		ПараметрыПроведения.Вставить("ТаблицаОборудование", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаОборудование", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	Оборудование.Ссылка,
		|	""Оборудование"" КАК ИмяСписка,
		|	Оборудование.НомерСтроки КАК НомерСтроки,
		|	Оборудование.Номенклатура,
		|	Оборудование.СуммаВзаиморасчетов,
		|	Оборудование.СуммаНДСВзаиморасчетов,
		|	Оборудование.СуммаРуб,
		|	Оборудование.СуммаРуб КАК СуммаБУ,
		|	Оборудование.СуммаРуб КАК СуммаНУ,
		|	Оборудование.СуммаНДСРуб,
		|	0 КАК СуммаПодтвержденнаяРуб,
		|	0 КАК СуммаНДСПодтвержденнаяРуб,
		|	Оборудование.Количество,
		|	Оборудование.СчетУчета,
		|	Оборудование.СчетУчетаНДС,
		|	Оборудование.СпособУчетаНДС,
		|	Оборудование.СтавкаНДС,
		|	Оборудование.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Оборудование.Ссылка.Склад КАК Склад
		|ИЗ
		|	ТаблицаОборудование КАК Оборудование
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если НЕ Реквизиты.ЕстьОбъектыСтроительства Тогда
		ПараметрыПроведения.Вставить("ТаблицаОбъектыСтроительства", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаОбъектыСтроительства", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ОбъектыСтроительства.Ссылка,
		|	""ОбъектыСтроительства"" КАК ИмяСписка,
		|	ОбъектыСтроительства.НомерСтроки КАК НомерСтроки,
		|	ОбъектыСтроительства.ОбъектСтроительства,
		|	ОбъектыСтроительства.СтатьяЗатрат,
		|	ОбъектыСтроительства.СуммаВзаиморасчетов,
		|	ОбъектыСтроительства.СуммаНДСВзаиморасчетов,
		|	ОбъектыСтроительства.СуммаРуб,
		|	ОбъектыСтроительства.СуммаРуб КАК СуммаБУ,
		|	ОбъектыСтроительства.СуммаРуб КАК СуммаНУ,
		|	ОбъектыСтроительства.СуммаНДСРуб,
		|	0 КАК СуммаПодтвержденнаяРуб,
		|	0 КАК СуммаНДСПодтвержденнаяРуб,
		|	ОбъектыСтроительства.СчетУчета,
		|	ОбъектыСтроительства.СчетУчетаНДС,
		|	ОбъектыСтроительства.СпособУчетаНДС,
		|	ОбъектыСтроительства.СтавкаНДС
		|ИЗ
		|	ТаблицаОбъектыСтроительства КАК ОбъектыСтроительства
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если НЕ Реквизиты.ЕстьОсновныеСредства Тогда
		ПараметрыПроведения.Вставить("ТаблицаОсновныеСредства", Неопределено);
		ПараметрыПроведения.Вставить("ТаблицаГрупповыеОбъектыОС", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаОсновныеСредства", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ТаблицаГрупповыеОбъектыОС", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ОсновныеСредства.Ссылка КАК Ссылка,
		|	""ОсновныеСредства"" КАК ИмяСписка,
		|	ОсновныеСредства.НомерСтроки КАК НомерСтроки,
		|	ОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|	ОсновныеСредства.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ОсновныеСредства.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ОсновныеСредства.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|	ОсновныеСредства.СуммаРуб КАК СуммаРуб,
		|	ОсновныеСредства.СуммаРуб КАК СуммаБУ,
		|	ОсновныеСредства.СуммаРуб КАК СуммаНУ,
		|	ОсновныеСредства.СуммаНДСРуб КАК СуммаНДСРуб,
		|	0 КАК СуммаПодтвержденнаяРуб,
		|	0 КАК СуммаНДСПодтвержденнаяРуб,
		|	ОсновныеСредства.СчетУчета КАК СчетУчета,
		|	ОсновныеСредства.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
		|	ОсновныеСредства.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ОсновныеСредства.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ОсновныеСредства.СтавкаНДС КАК СтавкаНДС,
		|	ОсновныеСредства.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
		|	ОсновныеСредства.ИдентификаторСтроки Как ИдентификаторСтроки,
		|	ОсновныеСредства.ПроцентИнвестиционногоВычета КАК ПроцентИнвестиционногоВычета
		|ИЗ
		|	ТаблицаОсновныеСредства КАК ОсновныеСредства
		|ГДЕ
		|	ОсновныеСредства.ЕдиницаУчета <> ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеСредства.Ссылка КАК Ссылка,
		|	""ОсновныеСредства"" КАК ИмяСписка,
		|	ОсновныеСредства.НомерСтроки КАК НомерСтроки,
		|	ОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
		|	ОсновныеСредства.Количество КАК Количество,
		|	ОсновныеСредства.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ОсновныеСредства.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|	ОсновныеСредства.СуммаРуб КАК СуммаРуб,
		|	ОсновныеСредства.СуммаРуб КАК СуммаБУ,
		|	ОсновныеСредства.СуммаРуб КАК СуммаНУ,
		|	ОсновныеСредства.СуммаНДСРуб КАК СуммаНДСРуб,
		|	0 КАК СуммаПодтвержденнаяРуб,
		|	0 КАК СуммаНДСПодтвержденнаяРуб,
		|	ОсновныеСредства.СчетУчета КАК СчетУчета,
		|	ОсновныеСредства.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
		|	ОсновныеСредства.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ОсновныеСредства.СпособУчетаНДС КАК СпособУчетаНДС,
		|	ОсновныеСредства.СтавкаНДС КАК СтавкаНДС,
		|	ОсновныеСредства.СрокПолезногоИспользования КАК СрокПолезногоИспользования
		|ИЗ
		|	ТаблицаОсновныеСредства КАК ОсновныеСредства
		|ГДЕ
		|	ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если НЕ Реквизиты.ЕстьТопливо Тогда
		ПараметрыПроведения.Вставить("ТаблицаТопливо", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаТопливо", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаТопливо.Ссылка КАК Ссылка,
		|	ТаблицаТопливо.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТопливо.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ТаблицаТопливо.Номенклатура КАК Номенклатура,
		|	ТаблицаТопливо.Сумма КАК СуммаРуб,
		|	ТаблицаТопливо.Сумма КАК СуммаБУ,
		|	ТаблицаТопливо.Сумма КАК СуммаНУ,
		|	ТаблицаТопливо.СуммаНДСВзаиморасчетов КАК СуммаНДСРуб,
		|	ТаблицаТопливо.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаТопливо.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|	0 КАК СуммаПодтвержденнаяРуб,
		|	0 КАК СуммаНДСПодтвержденнаяРуб,
		|	ТаблицаТопливо.Количество КАК Количество,
		|	ТаблицаТопливо.СчетУчета КАК СчетУчета,
		|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты) КАК ВидКорСубконто1,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры) КАК ВидКорСубконто2,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами) КАК ВидКорСубконто3,
		|	Реквизиты.Контрагент КАК КорСубконто1,
		|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
		|	Реквизиты.Ссылка КАК КорСубконто3,
		|	ТаблицаТопливо.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТопливо.СчетУчетаНДС КАК СчетУчетаНДС,
		|	ТаблицаТопливо.СпособУчетаНДС КАК СпособУчетаНДС
		|ИЗ
		|	ТаблицаТопливо КАК ТаблицаТопливо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаТопливо.Ссылка = Реквизиты.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Не Реквизиты.ЕстьТовары
		И Не Реквизиты.ЕстьАгентскиеУслуги
		Или Реквизиты.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом 
		Или Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		ПараметрыПроведения.Вставить("ТаблицаТоварыУслугиКомиссияПоЗакупке", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаТоварыУслугиКомиссияПоЗакупке", НомераТаблиц.Количество());
		Если Реквизиты.ЕстьТовары Тогда
			ТекстЗапроса = ТекстЗапроса + 
			"ВЫБРАТЬ
			|	Товары.Ссылка,
			|	1 КАК НомерСписка,
			|	""Товары"" КАК ИмяСписка,
			|	Товары.НомерСтроки КАК НомерСтроки,
			|	Товары.СчетУчета,
			|	Товары.Номенклатура,
			|	ЛОЖЬ КАК ЭтоУслуга,
			|	Товары.Количество,
			|	ВЫРАЗИТЬ(&СодержаниеПоступлениеТоваровКомитентов КАК СТРОКА(100)) КАК Содержание,
			|	Товары.СуммаВзаиморасчетов,
			|	Товары.СуммаНДСВзаиморасчетов,
			|	Товары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетовКомитент,
			|	Товары.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетовКомитент,
			|	Товары.СуммаРуб КАК СуммаБУ,
			|	Товары.СуммаРуб КАК СуммаНУ,
			|	Товары.СуммаРуб,
			|	Товары.СуммаНДСРуб,
			|	0 КАК СуммаПодтвержденнаяРуб,
			|	0 КАК СуммаНДСПодтвержденнаяРуб,
			|	Товары.СчетРасчетов КАК СчетРасчетовКомитента,
			|	Товары.Контрагент КАК Комитент,
			|	Товары.ДоговорКонтрагента КАК ДоговорКомитента,
			|	Товары.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаРасчетовСКомитентом,
			|	Реквизиты.Ссылка КАК ДокументРасчетовКомитента,
			|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетРасчетовПоставщика,
			|	Реквизиты.Контрагент КАК Поставщик,
			|	Реквизиты.ДоговорКонтрагента КАК ДоговорПоставщика,
			|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетовПоставщика,
			|	Реквизиты.Ссылка КАК ДокументРасчетовПоставщика,
			|	Товары.ИдентификаторСтроки Как ИдентификаторСтроки
			|ИЗ
			|	ТаблицаТовары КАК Товары
			|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
			|		ПО (ИСТИНА)
			|ГДЕ
			|	Товары.КомиссияПоЗакупке";
		КонецЕсли;
		Если Реквизиты.ЕстьТовары 
			И Реквизиты.ЕстьАгентскиеУслуги Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		Если Реквизиты.ЕстьАгентскиеУслуги Тогда
			ТекстЗапроса = ТекстЗапроса + 
			"ВЫБРАТЬ
			|	АгентскиеУслуги.Ссылка,
			|	2 КАК НомерСписка,
			|	""АгентскиеУслуги"" КАК ИмяСписка,
			|	АгентскиеУслуги.НомерСтроки КАК НомерСтроки,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
			|	АгентскиеУслуги.Номенклатура,
			|	ИСТИНА КАК ЭтоУслуга,
			|	АгентскиеУслуги.Количество,
			|	АгентскиеУслуги.Содержание,
			|	АгентскиеУслуги.СуммаВзаиморасчетов,
			|	АгентскиеУслуги.СуммаНДСВзаиморасчетов,
			|	АгентскиеУслуги.СуммаВзаиморасчетов КАК СуммаВзаиморасчетовКомитент,
			|	АгентскиеУслуги.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетовКомитент,
			|	АгентскиеУслуги.СуммаРуб КАК СуммаБУ,
			|	АгентскиеУслуги.СуммаРуб КАК СуммаНУ,
			|	АгентскиеУслуги.СуммаРуб,
			|	АгентскиеУслуги.СуммаНДСРуб,
			|	0 КАК СуммаПодтвержденнаяРуб,
			|	0 КАК СуммаНДСПодтвержденнаяРуб,
			|	АгентскиеУслуги.СчетРасчетов КАК СчетРасчетовКомитента,
			|	АгентскиеУслуги.Контрагент КАК Комитент,
			|	АгентскиеУслуги.ДоговорКонтрагента КАК ДоговорКомитента,
			|	АгентскиеУслуги.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаРасчетовСКомитентом,
			|	Реквизиты.Ссылка КАК ДокументРасчетовКомитента,
			|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетРасчетовПоставщика,
			|	Реквизиты.Контрагент КАК Поставщик,
			|	Реквизиты.ДоговорКонтрагента КАК ДоговорПоставщика,
			|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетовПоставщика,
			|	Реквизиты.Ссылка КАК ДокументРасчетовПоставщика,
			|	АгентскиеУслуги.ИдентификаторСтроки КАК ИдентификаторСтроки
			|ИЗ
			|	ТаблицаАгентскиеУслуги КАК АгентскиеУслуги
			|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
			|		ПО (ИСТИНА)";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСписка,
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоступлениеТоваровНаКомиссию(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ЕстьТовары
		ИЛИ Реквизиты.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
		ПараметрыПроведения.Вставить("ПоступлениеТоваровНаКомиссиюТаблицаТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПоступлениеТоваровНаКомиссиюТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.СуммаРуб,
	|	ТаблицаТовары.СуммаНДСРуб,
	|	ТаблицаТовары.СуммаРуб КАК СуммаБУ,
	|	ТаблицаТовары.СуммаРуб - ТаблицаТовары.СуммаНДСРуб КАК СуммаБезНДСРуб,
	|	ТаблицаТовары.СчетУчета,
	|	ТаблицаТовары.Количество,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.СкладВСписке
	|			ТОГДА ТаблицаТовары.Склад
	|	    ИНАЧЕ ТаблицаТовары.Ссылка.Склад
	|	КОНЕЦ КАК Склад
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоступлениеТоваровВПереработку(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ЕстьТовары
		ИЛИ Реквизиты.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		ПараметрыПроведения.Вставить("ПоступлениеТоваровВПереработкуТаблицаТовары", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПоступлениеТоваровВПереработкуТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""Товары"" КАК ИмяСписка,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.СуммаРуб,
	|	ТаблицаТовары.СчетУчета,
	|	ТаблицаТовары.Количество
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоступлениеТоваровТорговаяНаценка(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ЕстьТовары 
		ИЛИ НЕ Реквизиты.УчетПоПродажнойСтоимости 
		ИЛИ Реквизиты.ТипСклада = Перечисления.ТипыСкладов.ОптовыйСклад Тогда
		ПараметрыПроведения.Вставить("ПоступлениеТоваровТорговаяНаценкаРеквизиты", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ПоступлениеТоваровТорговаяНаценкаРеквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.Склад,
	|	Реквизиты.ТипСклада,
	|	Реквизиты.ТипЦенРозничнойТорговли,
	|	Реквизиты.НомерВходящегоДокумента,
	|	Реквизиты.ДатаВходящегоДокумента
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоступлениеТоваровГТД(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если (НЕ Реквизиты.ЕстьТовары И НЕ Реквизиты.ЕстьОборудование)
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		ПараметрыПроведения.Вставить("НомераГТД", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("НомераГТД", НомераТаблиц.Количество());
	
	ТекстЗапроса = "";
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	1 КАК НомерСписка,
		|	ТаблицаТовары.НомерСтроки,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.СчетУчета,
		|	ТаблицаТовары.НомерГТД,
		|	ТаблицаТовары.СтранаПроисхождения,
		|	ТаблицаТовары.Количество
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
		|	И ТаблицаТовары.СчетУчета В (&МассивСчетовДляГТД)";
	КонецЕсли;
	Если Реквизиты.ЕстьТовары
		И Реквизиты.ЕстьОборудование Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|";
	КонецЕсли;
	Если Реквизиты.ЕстьОборудование Тогда
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	2 КАК НомерСписка,
		|	ТаблицаОборудование.НомерСтроки,
		|	ТаблицаОборудование.Номенклатура,
		|	ТаблицаОборудование.СчетУчета,
		|	ТаблицаОборудование.НомерГТД,
		|	ТаблицаОборудование.СтранаПроисхождения,
		|	ТаблицаОборудование.Количество
		|ИЗ
		|	ТаблицаОборудование КАК ТаблицаОборудование
		|ГДЕ
		|	ТаблицаОборудование.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
		|	И ТаблицаОборудование.СчетУчета В (&МассивСчетовДляГТД)";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСписка,
	|	НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоступлениеТары(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ЕстьТара Тогда
		ПараметрыПроведения.Вставить("ТаблицаТара", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ТаблицаТара", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""ВозвратнаяТара"" КАК ИмяСписка,
	|	ТаблицаТара.Ссылка,
	|	ТаблицаТара.НомерСтроки,
	|	ТаблицаТара.Номенклатура,
	|	ТаблицаТара.СуммаВзаиморасчетов,
	|	ТаблицаТара.СуммаРуб,
	|	ТаблицаТара.Количество,
	|	ТаблицаТара.СчетУчета,
	|	ТаблицаТара.Склад
	|ИЗ
	|	ТаблицаТара КАК ТаблицаТара
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаТара.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоступлениеМПЗИП(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если Не Реквизиты.ПлательщикНДФЛ Тогда
		ПараметрыПроведения.Вставить("ИПРеквизиты",               Неопределено);
		ПараметрыПроведения.Вставить("ИПТаблицаТоваров",          Неопределено);
		ПараметрыПроведения.Вставить("ИПТаблицаУслуг",            Неопределено);
		ПараметрыПроведения.Вставить("ИПТаблицаОборудования",     Неопределено);
		ПараметрыПроведения.Вставить("ИПТаблицаОсновныеСредства", Неопределено);
		ПараметрыПроведения.Вставить("ИПТаблицаПрочее",           Неопределено);
		ПараметрыПроведения.Вставить("ИПТаблицаНМА",              Неопределено);
		Возврат "";
	КонецЕсли;

	НомераТаблиц.Вставить("ИПРеквизиты", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	""ПоступлениеТоваровУслуг"" КАК ТипПоступления,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ТипСклада КАК ТипСклада,
	|	Реквизиты.НДСВключенВСтоимость,
	|	Реквизиты.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	Реквизиты.ВидОперации КАК ВидОперации
	|ИЗ
	|	Реквизиты КАК Реквизиты"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Если НЕ Реквизиты.ЕстьТовары Тогда
		ПараметрыПроведения.Вставить("ИПТаблицаТоваров", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ИПТаблицаТоваров", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимТовары КАК СинонимСписка,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.СуммаРуб КАК Сумма,
		|	ТаблицаТовары.СуммаНДСРуб КАК НДС,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.СчетУчета КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
		|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
		|		ИНАЧЕ ТаблицаТовары.Номенклатура.НоменклатурнаяГруппа
		|	КОНЕЦ КАК НоменклатурнаяГруппа,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК ВидРасходовНУ,
		|	ИСТИНА КАК ПринятиеКналоговомуУчету,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Партия
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"		
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Не Реквизиты.ЕстьУслуги И Не Реквизиты.ЕстьПрава Тогда
		ПараметрыПроведения.Вставить("ИПТаблицаУслуг", Неопределено);
	ИначеЕсли Реквизиты.ЕстьУслуги Тогда
		НомераТаблиц.Вставить("ИПТаблицаУслуг", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	""Услуги"" КАК ИмяСписка,
		|	&СинонимУслуги КАК СинонимСписка,
		|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
		|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
		|	ТаблицаУслуги.СуммаРуб КАК Сумма,
		|	ТаблицаУслуги.СуммаНДСРуб КАК НДС,
		|	ТаблицаУслуги.Количество КАК Количество,
		|	ТаблицаУслуги.СчетЗатрат КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
		|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
		|		КОГДА ТаблицаУслуги.Субконто1 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаУслуги.Субконто1
		|		КОГДА ТаблицаУслуги.Субконто2 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаУслуги.Субконто2
		|		КОГДА ТаблицаУслуги.Субконто3 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаУслуги.Субконто3
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НоменклатурнаяГруппа,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|		ИЛИ ТаблицаУслуги.Субконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|		ИЛИ ТаблицаУслуги.Субконто1 ССЫЛКА Справочник.РасходыБудущихПериодов
		|			ТОГДА ТаблицаУслуги.Субконто1
		|		КОГДА ТаблицаУслуги.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|		ИЛИ ТаблицаУслуги.Субконто2 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|		ИЛИ ТаблицаУслуги.Субконто2 ССЫЛКА Справочник.РасходыБудущихПериодов
		|			ТОГДА ТаблицаУслуги.Субконто2
		|		КОГДА ТаблицаУслуги.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|		ИЛИ ТаблицаУслуги.Субконто3 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|		ИЛИ ТаблицаУслуги.Субконто3 ССЫЛКА Справочник.РасходыБудущихПериодов
		|			ТОГДА ТаблицаУслуги.Субконто3
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СтатьяЗатрат,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		КОГДА ТаблицаУслуги.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто2 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		КОГДА ТаблицаУслуги.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто3 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидРасходовНУ,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.Субконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКналоговомуУчету
		|		КОГДА ТаблицаУслуги.Субконто2 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто2 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКналоговомуУчету
		|		КОГДА ТаблицаУслуги.Субконто3 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто3 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКналоговомуУчету
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ПринятиеКналоговомуУчету,
		|	ВЫБОР
		|		КОГДА ТаблицаУслуги.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто1 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаУслуги.Субконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаУслуги.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто2 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаУслуги.Субконто2 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто2 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаУслуги.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто3 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаУслуги.Субконто3 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Субконто3 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Партия
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	Иначе
		// Для целей налогового учета приобретение прав может отражаться так же как поступление услуг.
		НомераТаблиц.Вставить("ИПТаблицаУслуг", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	""НМА"" КАК ИмяСписка,
		|	&СинонимНМА КАК СинонимСписка,
		|	ТаблицаПрава.НомерСтроки КАК НомерСтроки,
		|	ТаблицаПрава.ОбъектПрава КАК Номенклатура,
		|	ТаблицаПрава.СуммаРуб КАК Сумма,
		|	ТаблицаПрава.СуммаНДСРуб КАК НДС,
		|	1 КАК Количество,
		|	ТаблицаПрава.СчетЗатрат КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
		|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаПрава.Субконто1
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаПрава.Субконто2
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаПрава.Субконто3
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НоменклатурнаяГруппа,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ТаблицаПрава.Субконто1
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ТаблицаПрава.Субконто2
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ТаблицаПрава.Субконто3
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СтатьяЗатрат,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто2 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто3 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидРасходовНУ,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКНалоговомуУчету
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто2 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКНалоговомуУчету
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто3 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКНалоговомуУчету
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ПринятиеКналоговомуУчету,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто1 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто2 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто2 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто3 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто3 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Партия
		|ИЗ
		|	ТаблицаПрава КАК ТаблицаПрава
		|ГДЕ
		|	ТаблицаПрава.ОтражениеНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"		
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Не Реквизиты.ЕстьОборудование Тогда
		ПараметрыПроведения.Вставить("ИПТаблицаОборудования", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ИПТаблицаОборудования", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	""Оборудование"" КАК ИмяСписка,
		|	&СинонимОборудование КАК СинонимСписка,
		|	ТаблицаОборудование.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОборудование.Номенклатура КАК Номенклатура,
		|	ТаблицаОборудование.СуммаРуб КАК Сумма,
		|	ТаблицаОборудование.СуммаНДСРуб КАК НДС,
		|	ТаблицаОборудование.Количество КАК Количество,
		|	ТаблицаОборудование.СчетУчета КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
		|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
		|		ИНАЧЕ ТаблицаОборудование.Номенклатура.НоменклатурнаяГруппа
		|	КОНЕЦ КАК НоменклатурнаяГруппа,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК ВидРасходовНУ,
		|	ИСТИНА КАК ПринятиеКналоговомуУчету,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Партия
		|ИЗ
		|	ТаблицаОборудование КАК ТаблицаОборудование
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Не Реквизиты.ЕстьОсновныеСредства Тогда
		ПараметрыПроведения.Вставить("ИПТаблицаОсновныеСредства", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ИПТаблицаОсновныеСредства", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	""ОсновныеСредства"" КАК ИмяСписка,
		|	&СинонимОсновныеСредства КАК СинонимСписка,
		|	ТаблицаОсновныеСредства.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОсновныеСредства.ОсновноеСредство КАК Номенклатура,
		|	ТаблицаОсновныеСредства.СуммаРуб КАК Сумма,
		|	ТаблицаОсновныеСредства.СуммаНДСРуб КАК НДС,
		|	1 КАК Количество,
		|	ТаблицаОсновныеСредства.СчетУчета КАК СчетУчета,
		|	&ОсновнаяНоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	НЕОПРЕДЕЛЕНО КАК СтатьяЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК ВидРасходовНУ,
		|	ИСТИНА КАК ПринятиеКналоговомуУчету,
		|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Партия
		|ИЗ
		|	ТаблицаОсновныеСредства КАК ТаблицаОсновныеСредства
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Не Реквизиты.ЕстьПрава Тогда
		ПараметрыПроведения.Вставить("ИПТаблицаНМА", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ИПТаблицаНМА", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	""НМА"" КАК ИмяСписка,
		|	&СинонимНМА КАК СинонимСписка,
		|	ТаблицаПрава.НомерСтроки КАК НомерСтроки,
		|	ТаблицаПрава.ОбъектПрава КАК Номенклатура,
		|	ТаблицаПрава.СуммаРуб КАК Сумма,
		|	ТаблицаПрава.СуммаНДСРуб КАК НДС,
		|	1 КАК Количество,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НематериальныеАктивыОрганизации) КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
		|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаПрава.Субконто1
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаПрава.Субконто2
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.НоменклатурныеГруппы
		|			ТОГДА ТаблицаПрава.Субконто3
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НоменклатурнаяГруппа,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ТаблицаПрава.Субконто1
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ТаблицаПрава.Субконто2
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ТаблицаПрава.Субконто3
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СтатьяЗатрат,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто2 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто3 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидРасходовНУ,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКНалоговомуУчету
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто2 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКНалоговомуУчету
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто3 КАК Справочник.ПрочиеДоходыИРасходы).ПринятиеКНалоговомуУчету
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ПринятиеКналоговомуУчету,
		|	ВЫБОР
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто1 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто1 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто1 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто2 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто2 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто2 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.СтатьиЗатрат
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто3 КАК Справочник.СтатьиЗатрат).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		КОГДА ТаблицаПрава.Субконто3 ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|			ТОГДА ВЫРАЗИТЬ(ТаблицаПрава.Субконто3 КАК Справочник.ПрочиеДоходыИРасходы).ВидДеятельностиДляНалоговогоУчетаЗатрат
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Партия
		|ИЗ
		|	ТаблицаПрава КАК ТаблицаПрава
		|ГДЕ
		|	ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Реквизиты.ЕстьТопливо Тогда
		
		НомераТаблиц.Вставить("ИПТаблицаПрочее", НомераТаблиц.Количество() + 1);
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	СтатьиЗатрат.Ссылка КАК Ссылка,
		|	СтатьиЗатрат.ВидРасходовНУ КАК ВидРасходовНУ,
		|	СтатьиЗатрат.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК ВидДеятельностиДляНалоговогоУчетаЗатрат
		|ПОМЕСТИТЬ СтатьяЗатратТопливо
		|ИЗ
		|	Справочник.СтатьиЗатрат КАК СтатьиЗатрат
		|ГДЕ
		|	СтатьиЗатрат.Ссылка = &СтатьяЗатратРасходыГСМ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Прочее"" КАК ИмяСписка,
		|	&СинонимПрочее КАК СинонимСписка,
		|	ТаблицаТопливо.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТопливо.Номенклатура КАК Номенклатура,
		|	ТаблицаТопливо.Сумма КАК Сумма,
		|	ТаблицаТопливо.СуммаНДС КАК НДС,
		|	ТаблицаТопливо.Количество КАК Количество,
		|	&СчетЗатратПоУмолчанию КАК СчетУчета,
		|	ВЫБОР
		|		КОГДА НЕ &ВестиУчетПоВидамДеятельностиИП
		|			ТОГДА &ОсновнаяНоменклатурнаяГруппа
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК НоменклатурнаяГруппа,
		|	ЕСТЬNULL(СтатьяЗатратТопливо.Ссылка, ЗНАЧЕНИЕ(Справочник.СтатьиЗатрат.ПустаяСсылка)) КАК СтатьяЗатрат,
		|	ЕСТЬNULL(СтатьяЗатратТопливо.ВидРасходовНУ, ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПрочиеРасходы)) КАК ВидРасходовНУ,
		|	ИСТИНА КАК ПринятиеКНалоговомуУчету,
		|	ЕСТЬNULL(СтатьяЗатратТопливо.ВидДеятельностиДляНалоговогоУчетаЗатрат, ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)) КАК ВидДеятельностиДляНалоговогоУчетаЗатрат,
		|	НЕОПРЕДЕЛЕНО КАК Партия
		|ИЗ
		|	ТаблицаТопливо КАК ТаблицаТопливо
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтатьяЗатратТопливо КАК СтатьяЗатратТопливо
		|		ПО (ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СтатьяЗатратТопливо"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Иначе
		ПараметрыПроведения.Вставить("ИПТаблицаПрочее", Неопределено);
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	// Вычет по электронным услугам иностранцев регистрируется особым образом.
	// Необходимую для вычета информацию регистрируем в журнале счетов-фактур.
	Если НЕ Реквизиты.ЭлектронныеУслуги Тогда
		ПараметрыПроведения.Вставить("РегистрацияВЖурналеУчетаСчетовФактур", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ИтогиДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РегистрацияВЖурналеУчетаСчетовФактур", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СУММА(ИтогиДокумента.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ИтогиДокумента.СуммаНДСВзаиморасчетов) КАК СуммаНДСВзаиморасчетов
	|ПОМЕСТИТЬ ИтогиДокумента
	|ИЗ
	|	(ВЫБРАТЬ
	|		0 КАК СуммаВзаиморасчетов,
	|		0 КАК СуммаНДСВзаиморасчетов"
	+ ?(Реквизиты.ЕстьУслуги, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаУслуги.СуммаВзаиморасчетов,
	|		ТаблицаУслуги.СуммаНДСВзаиморасчетов
	|	ИЗ
	|		ТаблицаУслуги КАК ТаблицаУслуги", "")
	+ ?(Реквизиты.ЕстьТовары, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТовары.СуммаВзаиморасчетов,
	|		ТаблицаТовары.СуммаНДСВзаиморасчетов
	|	ИЗ
	|		ТаблицаТовары КАК ТаблицаТовары", "")
	+ ?(Реквизиты.ЕстьПрава, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПрава.СуммаВзаиморасчетов,
	|		ТаблицаПрава.СуммаНДСВзаиморасчетов
	|	ИЗ
	|		ТаблицаПрава КАК ТаблицаПрава", "") + ") КАК ИтогиДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	"""" КАК КППКонтрагента,
	|	"""" КАК ИННКонтрагента,
	|	Реквизиты.Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры) КАК ЧастьЖурнала,
	|	Реквизиты.Дата КАК ДатаВыставленияПолучения,
	|	1 КАК КодСпособаВыставленияПолучения,
	|	""45"" КАК КодВидаОперации,
	|	ВЫБОР
	|		КОГДА Реквизиты.НомерВходящегоДокумента <> """"
	|			ТОГДА Реквизиты.НомерВходящегоДокумента
	|		КОГДА Реквизиты.НомерДоговора <> """"
	|			ТОГДА Реквизиты.НомерДоговора
	|		ИНАЧЕ Реквизиты.НаименованиеДоговора
	|	КОНЕЦ КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА Реквизиты.ДатаВходящегоДокумента <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Реквизиты.ДатаВходящегоДокумента
	|		ИНАЧЕ Реквизиты.ДатаДоговора
	|	КОНЕЦ КАК ДатаСчетаФактуры,
	|	Реквизиты.ВалютаДокумента КАК Валюта,
	|	ИтогиДокумента.СуммаВзаиморасчетов КАК СуммаПоСчетуФактуре,
	|	ИтогиДокумента.СуммаНДСВзаиморасчетов КАК СуммаНДС,
	|	НЕОПРЕДЕЛЕНО КАК НомерКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК ПоСтавкеБезНДС,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураНеВыставляется,
	|	Реквизиты.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	0 КАК ИндексСтроки,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОснование,
	|	НЕОПРЕДЕЛЕНО КАК Субкомиссионер,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураВыданныйПокупателю,
	|	НЕОПРЕДЕЛЕНО КАК КодВидаСделки,
	|	НЕОПРЕДЕЛЕНО КАК Посредник,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактурыПродавца,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактурыПродавца,
	|	НЕОПРЕДЕЛЕНО КАК КодВидаОперацииКомиссия,
	|	НЕОПРЕДЕЛЕНО КАК Продавец,
	|	0 КАК СуммаПоСчетуФактуреКомиссия,
	|	0 КАК СуммаНДСКомиссия,
	|	0 КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	0 КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	0 КАК СуммаНДСРазницаУвеличение,
	|	0 КАК СуммаНДСРазницаУменьшение,
	|	0 КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	0 КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	0 КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	0 КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	"""" КАК КодВидаОперацииНаУменьшение,
	|	ЛОЖЬ КАК ИсправлениеСобственнойОшибки,
	|	НЕОПРЕДЕЛЕНО КАК ИсправляемыйСчетФактура,
	|	НЕОПРЕДЕЛЕНО КАК ИсправленныйСчетФактура,
	|	НЕОПРЕДЕЛЕНО КАК СчетФактураПолученныйОтПродавца,
	|	ЛОЖЬ КАК Сторно,
	|	"""" КАК ИННПродавца,
	|	"""" КАК КПППродавца,
	|	"""" КАК ИННСубкомиссионера,
	|	"""" КАК КППСубкомиссионера
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИтогиДокумента КАК ИтогиДокумента
	|		ПО (ИСТИНА)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Процедура ДобавитьКолонкуСодержание(ТаблицаЗначений, Реквизиты) Экспорт
	
	Если ТаблицаЗначений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщееСодержание = " по вх.д. " + Реквизиты.НомерВходящегоДокумента 
		+ " от " + Формат(Реквизиты.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy");
	
	Если ТаблицаЗначений.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить("Содержание", ОбщегоНазначения.ОписаниеТипаСтрока(150));
		СодержанияПроводок = Новый Соответствие;
		Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
			СодержаниеПроводки = СодержанияПроводок[СтрокаТаблицы.СчетУчета];
			Если СодержаниеПроводки = Неопределено Тогда
				СодержаниеПроводки = "Поступление " 
					+ БухгалтерскийУчетПовтИсп.НазваниеОбъектаПоСчетуУчета(СтрокаТаблицы.СчетУчета) 
					+ ОбщееСодержание;
				СодержанияПроводок.Вставить(СтрокаТаблицы.СчетУчета, СодержаниеПроводки);
			КонецЕсли;
			СтрокаТаблицы.Содержание = СодержаниеПроводки;
		КонецЦикла;
	Иначе	
		Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
			СтрокаТаблицы.Содержание = СтрокаТаблицы.Содержание + ОбщееСодержание;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПодготовитьТаблицыНДС(ТаблицыДокумента) Экспорт
	
	Товары = Неопределено;
	Услуги = Неопределено;
	
	Для каждого СтрокаТаблицыДокумента Из ТаблицыДокумента Цикл
		
		Если СтрокаТаблицыДокумента.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаТаблицыДокумента.Ключ = "ТаблицаТоварыУслугиКомиссияПоЗакупке" Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицыДокумента.Ключ = "ТаблицаУслуги" Или СтрокаТаблицыДокумента.Ключ = "ТаблицаПрава" Тогда
			
			Услуги = СтрокаТаблицыДокумента.Значение.Скопировать();
			Услуги.Колонки.Добавить("СтатьяЗатрат", Новый ОписаниеТипов("СправочникСсылка.СтатьиЗатрат"));
			Для каждого СтрокаУслуги Из Услуги Цикл
				Если ТипЗнч(СтрокаУслуги.Субконто1) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
					СтрокаУслуги.СтатьяЗатрат = СтрокаУслуги.Субконто1;
				ИначеЕсли ТипЗнч(СтрокаУслуги.Субконто2) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
					СтрокаУслуги.СтатьяЗатрат = СтрокаУслуги.Субконто2;
				ИначеЕсли ТипЗнч(СтрокаУслуги.Субконто3) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
					СтрокаУслуги.СтатьяЗатрат = СтрокаУслуги.Субконто3;
				КонецЕсли;
				
				// При приобретении прав итоговый счета учета может отличаться от счета затрат, указанного в документе.
				// Так, например, это может быть счета учета НМА (для объектов прав, существенных по стоимости) или
				// счет приобретения прав на использование результатов интеллектуальной деятельности.
				Если СтрокаТаблицыДокумента.Ключ = "ТаблицаПрава" Тогда
					Если СтрокаУслуги.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив
						И Не СтрокаУслуги.ЭтоМалоценныйНМА Тогда
						СтрокаУслуги.СчетЗатрат = ПланыСчетов.Хозрасчетный.НематериальныеАктивыОрганизации;
						СтрокаУслуги.ПодразделениеЗатрат = СтрокаУслуги.ПодразделениеОрганизации;
					ИначеЕсли СтрокаУслуги.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс Тогда
						СтрокаУслуги.СчетЗатрат = СтрокаУслуги.СчетОтражениеБУ;
					КонецЕсли;
					
					Если СтрокаУслуги.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив
							И Не СтрокаУслуги.ЭтоМалоценныйНМА
						Или СтрокаУслуги.ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс Тогда
						СтрокаУслуги.Субконто1 = СтрокаУслуги.СубконтоОтражениеБУ1;
						СтрокаУслуги.Субконто2 = СтрокаУслуги.СубконтоОтражениеБУ2;
						СтрокаУслуги.Субконто3 = СтрокаУслуги.СубконтоОтражениеБУ3;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		Иначе
			
			Если Товары = Неопределено Тогда
				Товары = СтрокаТаблицыДокумента.Значение.Скопировать();
				Если СтрокаТаблицыДокумента.Ключ = "ТаблицаОбъектыСтроительства" Тогда
					Товары.Колонки.Добавить("Номенклатура");
					Товары.ЗагрузитьКолонку(Товары.ВыгрузитьКолонку("ОбъектСтроительства"), "Номенклатура");
					Товары.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
				ИначеЕсли СтрокаТаблицыДокумента.Ключ = "ТаблицаОсновныеСредства" Тогда
					Товары.Колонки.Добавить("Номенклатура");
					Товары.ЗагрузитьКолонку(Товары.ВыгрузитьКолонку("ОсновноеСредство"), "Номенклатура");
					Товары.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
				КонецЕсли;
			Иначе
				Для каждого СтрокаДокумента Из СтрокаТаблицыДокумента.Значение Цикл
					НоваяСтрока = Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДокумента);
					Если СтрокаТаблицыДокумента.Ключ = "ТаблицаОбъектыСтроительства" Тогда
						НоваяСтрока.Номенклатура = СтрокаДокумента.ОбъектСтроительства;
					ИначеЕсли СтрокаТаблицыДокумента.Ключ = "ТаблицаОсновныеСредства" Тогда
						НоваяСтрока.Номенклатура = СтрокаДокумента.ОсновноеСредство;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Новый Структура("Товары,Услуги", Товары, Услуги);
		
КонецФункции	

Функция ПодготовитьТаблицуТоварыУслугиКомиссияПоЗакупке(ИсходнаяТаблица, ТаблицаПараметры, Реквизиты) Экспорт
	
	Если Не ЗначениеЗаполнено(ИсходнаяТаблица) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Реквизиты) Тогда 
		Возврат ИсходнаяТаблица;
	КонецЕсли;
	
	ИсключатьНДСИзВзаиморасчетов = Реквизиты[0].УчетАгентскогоНДС
		И Реквизиты[0].ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.Нерезидент;
	МассивВалютРасчетовСКомитентами = ИсходнаяТаблица.ВыгрузитьКолонку("ВалютаРасчетовСКомитентом");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ТаблицаПараметры[0].Период); 
	Запрос.УстановитьПараметр("МассивВалют", МассивВалютРасчетовСКомитентами);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта В (&МассивВалют)) КАК КурсыВалютСрезПоследних";
	
	ВыборкаВалют = Запрос.Выполнить().Выбрать();
	
	ТаблицаРезультат = ИсходнаяТаблица.Скопировать();
	Для каждого СтрокаТаблицы Из ТаблицаРезультат Цикл
		
		Если ИсключатьНДСИзВзаиморасчетов Тогда
			СтрокаТаблицы.СуммаНДСРуб = 0;
			СтрокаТаблицы.СуммаНДСВзаиморасчетов = 0;
			СтрокаТаблицы.СуммаВзаиморасчетовКомитент = 
				СтрокаТаблицы.СуммаВзаиморасчетовКомитент - СтрокаТаблицы.СуммаНДСВзаиморасчетовКомитент;
			СтрокаТаблицы.СуммаНДСВзаиморасчетовКомитент = 0;
		КонецЕсли;
		
		Если НЕ ТаблицаПараметры[0].НДСВключенВСтоимость Тогда
			
			СтрокаТаблицы.СуммаБУ = СтрокаТаблицы.СуммаБУ + СтрокаТаблицы.СуммаНДСРуб;
			СтрокаТаблицы.СуммаНУ = СтрокаТаблицы.СуммаНУ + СтрокаТаблицы.СуммаНДСРуб;
			СтрокаТаблицы.СуммаВзаиморасчетов = СтрокаТаблицы.СуммаВзаиморасчетов + СтрокаТаблицы.СуммаНДСВзаиморасчетов;
				
		КонецЕсли;
		
		// Если валюты расчетов не совпадают, то необходимо пересчитать сумму взаиморасчетов комитента по курсу.
		Если СтрокаТаблицы.ВалютаРасчетовСКомитентом <> СтрокаТаблицы.ВалютаВзаиморасчетовПоставщика Тогда
			ВыборкаВалют.Сбросить();
			Если ВыборкаВалют.НайтиСледующий(СтрокаТаблицы.ВалютаРасчетовСКомитентом, "Валюта") Тогда
				
				Если НЕ ВыборкаВалют.Курс = 0 Тогда
					СтрокаТаблицы.СуммаВзаиморасчетовКомитент = Окр(СтрокаТаблицы.СуммаБУ*ВыборкаВалют.Кратность/ВыборкаВалют.Курс,2);
					СтрокаТаблицы.СуммаНДСВзаиморасчетовКомитент = Окр(СтрокаТаблицы.СуммаНДСРуб*ВыборкаВалют.Кратность/ВыборкаВалют.Курс,2); 
				Иначе
					СтрокаТаблицы.СуммаВзаиморасчетовКомитент = 0;
					СтрокаТаблицы.СуммаНДСВзаиморасчетовКомитент = 0;
				КонецЕсли;
				
			Иначе
				СтрокаТаблицы.СуммаВзаиморасчетовКомитент = 0;
				СтрокаТаблицы.СуммаНДСВзаиморасчетовКомитент = 0;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаРезультат;

КонецФункции 

Функция ПодготовитьТаблицуЗачетАвансовКомиссияПоЗакупке(ТаблицаТоварыУслугиКомиссияПоЗакупке) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаТоварыУслугиКомиссияПоЗакупке) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДанных.Комитент КАК Контрагент,
	|	ТаблицаДанных.ДоговорКомитента КАК ДоговорКонтрагента,
	|	ТаблицаДанных.ДокументРасчетовКомитента КАК ДокументРасчетов,
	|	ТаблицаДанных.СчетРасчетовКомитента КАК СчетРасчетов,
	|	ТаблицаДанных.СчетРасчетовКомитента КАК СчетАвансов,
	|	ТаблицаДанных.СуммаВзаиморасчетовКомитент КАК СуммаВзаиморасчетов,
	|	ТаблицаДанных.СуммаБУ КАК СуммаРуб
	|ПОМЕСТИТЬ ТаблицаДанных
	|ИЗ
	|	&ТаблицаДанных КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.Контрагент КАК Контрагент,
	|	ТаблицаДанных.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку) КАК ВидДоговора,
	|	ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).ВалютаВзаиморасчетов = &ВалютаРеглУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ НЕ ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).РасчетыВУсловныхЕдиницах
	|	КОНЕЦ КАК РасчетыВВалюте,
	|	ТаблицаДанных.ДокументРасчетов КАК ДокументРасчетов,
	|	ТаблицаДанных.СчетРасчетов КАК СчетРасчетов,
	|	ТаблицаДанных.СчетАвансов КАК СчетАвансов,
	|	ВЫРАЗИТЬ(ТаблицаДанных.ДокументРасчетов КАК Документ.ПоступлениеТоваровУслуг).ПодразделениеОрганизации КАК Подразделение,
	|	СУММА(ТаблицаДанных.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ТаблицаДанных.СуммаРуб) КАК СуммаРуб,
	|	0 КАК СуммаВзаиморасчетовКомитента,
	|	0 КАК СуммаВзаиморасчетовТорговыйСбор,
	|	0 КАК СуммаВзаиморасчетовЕНВД,
	|	0 КАК СуммаВзаиморасчетовПатент
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Контрагент,
	|	ТаблицаДанных.ДоговорКонтрагента,
	|	ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).ВалютаВзаиморасчетов,
	|	ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).РасчетыВУсловныхЕдиницах,
	|	ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).УчетАгентскогоНДС,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).ВалютаВзаиморасчетов = &ВалютаРеглУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ НЕ ВЫРАЗИТЬ(ТаблицаДанных.ДоговорКонтрагента КАК Справочник.ДоговорыКонтрагентов).РасчетыВУсловныхЕдиницах
	|	КОНЕЦ,
	|	ТаблицаДанных.ДокументРасчетов,
	|	ВЫРАЗИТЬ(ТаблицаДанных.ДокументРасчетов КАК Документ.ПоступлениеТоваровУслуг).ПодразделениеОрганизации,
	|	ТаблицаДанных.СчетРасчетов,
	|	ТаблицаДанных.СчетАвансов";
	
	ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос.УстановитьПараметр("ТаблицаДанных",   ТаблицаТоварыУслугиКомиссияПоЗакупке);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ИспользуетсяОтложенноеПроведение
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку
		ИЛИ Реквизиты.ЭтоОтложенноеПроведение Тогда
		ПараметрыПроведения.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", Неопределено);
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "";
	
	Если Реквизиты.ЕстьТовары Тогда
		// Если в табличной части Товары введены только товары, учитываемые на забалансовых счетах,
		// то такой документ не двигает взаиморасчеты с контрагентами, его не надо актуализировать.
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	Реквизиты.ВидДоговора КАК ВидДоговора,
		|	Реквизиты.Дата КАК Дата
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	(НЕ ТаблицаТовары.СчетУчетаЗабалансовый
		|			ИЛИ ТаблицаТовары.КомиссияПоЗакупке)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаТовары.Контрагент,
		|	ТаблицаТовары.ДоговорКонтрагента,
		|	ТаблицаТовары.ДоговорКонтрагента.ВалютаВзаиморасчетов,
		|	ТаблицаТовары.ДоговорКонтрагента.ВидДоговора,
		|	Реквизиты.Дата
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ТаблицаТовары.КомиссияПоЗакупке";
	КонецЕсли;
	
	Если Реквизиты.ЕстьТара 
		Или Реквизиты.ЕстьОборудование
		Или Реквизиты.ЕстьОбъектыСтроительства
		Или Реквизиты.ЕстьУслуги
		Или Реквизиты.ЕстьАгентскиеУслуги
		Или Реквизиты.ЕстьОсновныеСредства
		Или Реквизиты.ЕстьПрава Тогда
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	Реквизиты.ВидДоговора КАК ВидДоговора,
		|	Реквизиты.Дата КАК Дата
		|ИЗ
		|	Реквизиты";
	КонецЕсли;
	
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда

		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
	
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаАгентскиеУслуги.Контрагент,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ТаблицаАгентскиеУслуги.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
		|	Реквизиты.Дата
		|ИЗ
		|	ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
		НомераТаблиц.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	Иначе
		ПараметрыПроведения.Вставить("РасчетыСКонтрагентамиОтложенноеПроведение", Неопределено);
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаРегистрацияОтложенныхРасчетовВПоследовательности(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ИспользуетсяОтложенноеПроведение
		ИЛИ Реквизиты.ЭтоОтложенноеПроведение Тогда
	    ПараметрыПроведения.Вставить("РеквизитыРегистрацияВПоследовательности", Неопределено);
		ПараметрыПроведения.Вставить("ТоварыРегистрацияВПоследовательности", Неопределено);
		ПараметрыПроведения.Вставить("УслугиРегистрацияВПоследовательности", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("РеквизитыРегистрацияВПоследовательности", НомераТаблиц.Количество());

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.ВидОперации,
	|	Реквизиты.Организация,
	|	Реквизиты.Дата КАК Период
	|ИЗ
	|	Реквизиты КАК Реквизиты";
	
	Если Реквизиты.ЕстьОборудование ИЛИ Реквизиты.ЕстьТовары Тогда

		ТекстЗапросаТовары = "";
		НомераТаблиц.Вставить("ТоварыРегистрацияВПоследовательности", НомераТаблиц.Количество());

		Если Реквизиты.ЕстьОборудование Тогда

			ТекстЗапросаТовары = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Оборудование.СчетУчета КАК СчетУчета,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчетСписания
			|ИЗ
			|	ТаблицаОборудование КАК Оборудование";
		
		КонецЕсли;

		Если Реквизиты.ЕстьТовары Тогда
	
			Если НЕ ПустаяСтрока(ТекстЗапросаТовары) Тогда
				ТекстЗапросаТовары = ТекстЗапросаТовары +
				"
				|ОБЪЕДИНИТЬ ВСЕ
				|";
			КонецЕсли;

			ТекстЗапросаТовары = ТекстЗапросаТовары +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаТовары.СчетУчета КАК СчетУчета,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК КорСчетСписания
			|ИЗ
			|	ТаблицаТовары КАК ТаблицаТовары";
		
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса 
			+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() 
			+ ТекстЗапросаТовары;
		
	Иначе
		ПараметрыПроведения.Вставить("ТоварыРегистрацияВПоследовательности", Неопределено);
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги Тогда
		НомераТаблиц.Вставить("УслугиРегистрацияВПоследовательности", НомераТаблиц.Количество());
	
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() +
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаУслуги.СчетЗатрат
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги";
	Иначе
		ПараметрыПроведения.Вставить("УслугиРегистрацияВПоследовательности", Неопределено);
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура ЗарегистрироватьОтложенныеРасчетыВПоследовательности(Объект, ПараметрыПроведения, Отказ) Экспорт

	ВключенВПоследовательность = РаботаСПоследовательностями.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(
		Объект,
		Отказ,
		ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение,
		ПараметрыПроведения.ТоварыРегистрацияВПоследовательности,
		Перечисления.ВидыРегламентныхОпераций.ПустаяСсылка());

	Если ВключенВПоследовательность Тогда
		// Документ включен в последовательность, больше с ним не требуется ничего делать.
		Возврат;
	КонецЕсли;

	// Ниже проверяем особые виды операций.
	// Для остальных видов операций действует общая логика регистрации как и в других документах.

	ОбязательноРегистрироватьВПоследовательности = Ложь;

	Параметры = ПодготовитьПараметрыЗарегистрироватьОтложенныеРасчетыВПоследовательности(
		ПараметрыПроведения.РеквизитыРегистрацияВПоследовательности,
		ПараметрыПроведения.УслугиРегистрацияВПоследовательности);
		
	ТаблицаУслуги = Параметры.ТаблицаУслуги;
		
	Реквизиты = Параметры.Реквизиты[0];

	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства Тогда
		// Поступление объектов строительства региструем в последовательности всегда.
		ОбязательноРегистрироватьВПоследовательности = Истина;
	КонецЕсли;

	// Регистрация в последовательности.
	Если ОбязательноРегистрироватьВПоследовательности Тогда
		РаботаСПоследовательностями.ЗарегистрироватьВПоследовательности(Объект, Отказ, Ложь);

	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства Тогда
		// При поступлении ОС помечаем как неактуальную рег.операцию по начислению амортизации.
		РаботаСПоследовательностями.ЗарегистрироватьУстареваниеОперации(Объект, Перечисления.ВидыРегламентныхОпераций.АмортизацияИИзносОС);
	ИначеЕсли Реквизиты.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права Тогда
		РаботаСПоследовательностями.ЗарегистрироватьУстареваниеОперации(Объект, Перечисления.ВидыРегламентныхОпераций.АмортизацияНМАИсписаниеРасходовПоНИОКР);
		РаботаСПоследовательностями.ЗарегистрироватьУстареваниеОперации(Объект, Перечисления.ВидыРегламентныхОпераций.ЗакрытиеСчета97);
		РаботаСПоследовательностями.ЗарегистрироватьУстареваниеОперации(Объект, Перечисления.ВидыРегламентныхОпераций.ПризнаниеРасходовНаПриобретениеНМАДляУСН);
	Иначе
	
		// Если документ регистрирует поступление услуг и при этом не был включен в последовательность,
		// то сбросим актуальность для тех рег.операций, которые зависят от соответствующих счетов.
		РаботаСПоследовательностями.ЗарегистрироватьУстареваниеРегламентныхОперацийПриПоступленииЗатрат(
			Объект, ТаблицаУслуги.ВыгрузитьКолонку("СчетЗатрат"));
			
	КонецЕсли;
		
КонецПроцедуры

Функция ПодготовитьПараметрыЗарегистрироватьОтложенныеРасчетыВПоследовательности(ТаблицаРеквизиты, ТаблицаУслуги)

	Параметры = Новый Структура;
	
	// Реквизиты
	СписокОбязательныхКолонок = ""
	+ "ВидОперации,"                  // <Перечисление.ВидыОперацийПоступлениеТоваровУслуг> - Вид операции документа.
	+ "Организация,"                  // <Справочник.Организации> - Организация документа.
	+ "Период"                  	  // <Дата> - Дата документа.
	;

	Параметры.Вставить("Реквизиты",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));

	// Услуги
	СписокОбязательныхКолонок = ""
	+ "СчетЗатрат"                    // <ПланСчетовСсылка.Хозрасчетный> - Счет учета поступивших услуг.
	;

	Параметры.Вставить("ТаблицаУслуги",
		ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(ТаблицаУслуги, СписокОбязательныхКолонок));
		
	Возврат Параметры;

КонецФункции

Процедура ОбработкаОтложенногоПроведения(Параметры, Отказ) Экспорт
	
	ПараметрыПроведения = ПодготовитьПараметрыПроведения(
		Параметры.Регистратор,
		Отказ,
		Параметры.ДоговорКонтрагента);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;	
	
	// Алгоритмы формирования проводок этого документа рассчитывают суммы проводок налогового учета.
	Параметры.Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("СуммыНалоговогоУчетаЗаполнены", Истина);
	
	ТаблицаВзаиморасчетов = УчетВзаиморасчетовОтложенноеПроведение.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		Параметры,
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
		ПараметрыПроведения.ТаблицаЗачетАвансов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Отказ);

	УчетВзаиморасчетовОтложенноеПроведение.СформироватьДвиженияЗачетАвансов(
		Параметры,
		ТаблицаВзаиморасчетов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Отказ);

КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", 
			"Приходная накладная", ПечатьПоступлениеТоваров(МассивОбъектов, ОбъектыПечати),,
			"Документ.ПоступлениеТоваровУслуг.ПФ_MXL_Накладная");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ12") Тогда
		ВключатьУслуги = Истина;
		ТаблицаСведенийТОРГ12 = ПолучитьТаблицуСведенийТОРГ12Покупатель(МассивОбъектов, ВключатьУслуги);
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаПоступления_ТОРГ12");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ12", "ТОРГ-12 (Товарная накладная за поставщика с услугами)",
			ПечатьТорговыхДокументов.ПечатьТОРГ12(ТаблицаСведенийТОРГ12, ОбъектыПечати, ПараметрыПечати),,"ОбщийМакет.ПФ_MXL_ТОРГ12");
		ПараметрыВывода.Вставить("ИмяФайлаПоВходящимНомерам", Истина);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ12_БезУслуг") Тогда
		ВключатьУслуги = Ложь;
		ТаблицаСведенийТОРГ12 = ПолучитьТаблицуСведенийТОРГ12Покупатель(МассивОбъектов, ВключатьУслуги);
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаПоступления_ТОРГ12");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ12_БезУслуг", "ТОРГ-12 (Товарная накладная за поставщика)",
			ПечатьТорговыхДокументов.ПечатьТОРГ12(ТаблицаСведенийТОРГ12, ОбъектыПечати, ПараметрыПечати),,"ОбщийМакет.ПФ_MXL_ТОРГ12");
		ПараметрыВывода.Вставить("ИмяФайлаПоВходящимНомерам", Истина);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ4") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ4", 
			"ТОРГ-4 (Акт о приемке товара без счета поставщика)", ПечатьТОРГ4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_ТОРГ4");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М4") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "М4",
			"М-4 (Приходный ордер)", ПечатьМ4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_М4");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОС1",
			"Акт о приеме-передаче объекта ОС (ОС-1)", ПечатьОС1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,
			"ОбщийМакет.ПФ_MXL_ОС1");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктОПринятииКУчетуОС") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктОПринятииКУчетуОС", 
			"Акт о принятии к учету основных средств", ПечатьАктОПринятииКУчетуОС(МассивОбъектов, ОбъектыПечати),,
			"Документ.ПоступлениеТоваровУслуг.ПФ_MXL_АктОПринятииКУчетуОС");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС14") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОС14",
			"ОС-14 (Акт о приеме оборудования)", ПечатьОС14(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,
			"Документ.ПоступлениеТоваровУслуг.ПФ_MXL_ОС14");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Акт") Тогда
		ТаблицаСведенийАктаОбОказанииУслуг = ПолучитьТаблицуСведенийАктаОбОказанииУслуг(МассивОбъектов);
		ПараметрыПечати.Вставить("ВидДокументаПечати", "ПоступлениеТоваровУслуг");
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_Акт");
		ПараметрыПечати.Вставить("ИмяМакетаПечати", "Документ.ПоступлениеТоваровУслуг.ПФ_MXL_Акт");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Акт", "Акт об оказании услуг за поставщика", 
			ПечатьТорговыхДокументов.ПечатьАктаОбОказанииУслуг(ТаблицаСведенийАктаОбОказанииУслуг, ОбъектыПечати, ПараметрыПечати),, 
					"Документ.ПоступлениеТоваровУслуг.ПФ_MXL_Акт");
		ПараметрыВывода.Вставить("ИмяФайлаПоВходящимНомерам", Истина);
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПоступление") Тогда
		МассивДокументов = Новый Массив;
		Для каждого Документ Из МассивОбъектов Цикл
			Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") 
				И Документ.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги Тогда
				МассивДокументов.Добавить(Документ);
			КонецЕсли;
		КонецЦикла;
		ТаблицаСведенийАктаОбОказанииУслуг = ПолучитьТаблицуСведенийАктаОбОказанииУслуг(МассивДокументов);
		ПараметрыПечати.Вставить("ВидДокументаПечати", "ПоступлениеТоваровУслуг");
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_Акт");
		ПараметрыПечати.Вставить("ИмяМакетаПечати", "Документ.ПоступлениеТоваровУслуг.ПФ_MXL_Акт");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктПоступление", "Акт (поступление)", 
			ПечатьТорговыхДокументов.ПечатьАктаОбОказанииУслуг(ТаблицаСведенийАктаОбОказанииУслуг, ОбъектыПечати, ПараметрыПечати),, 
					"Документ.ПоступлениеТоваровУслуг.ПФ_MXL_Акт");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НакладнаяПоступление") Тогда
		ВключатьУслуги = Истина;
		МассивДокументов = Новый Массив;
		Для каждого Документ Из МассивОбъектов Цикл
			Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") 
				И Документ.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги Тогда
				МассивДокументов.Добавить(Документ);
			КонецЕсли;
		КонецЦикла;
		ТаблицаСведенийТОРГ12 = ПолучитьТаблицуСведенийТОРГ12Покупатель(МассивДокументов, ВключатьУслуги);
		ПараметрыПечати.Вставить("ИмяПараметровПечати", "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаПоступления_ТОРГ12");
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "НакладнаяПоступление", "Накладная (поступление)",
			ПечатьТорговыхДокументов.ПечатьТОРГ12(ТаблицаСведенийТОРГ12, ОбъектыПечати, ПараметрыПечати),,"ОбщийМакет.ПФ_MXL_ТОРГ12");
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УниверсальныйПередаточныйДокументКомплект") Тогда
		ИменаФайлов = Неопределено;
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УниверсальныйПередаточныйДокумент", "УПД",
			УчетНДС.ПечатьКомплектаУПД(КоллекцияПечатныхФорм, МассивОбъектов, ОбъектыПечати, ИменаФайлов, ПараметрыПечати),,, ИменаФайлов);
		ПараметрыВывода.Вставить("ИмяФайлаПоВходящимНомерам", Истина);
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

#Область ПечатьУПД

// Возвращает текст запроса для отбора документов к печати УПД по релевантной форме.
// Добавлена во все документы, для которых доступна печать УПД.
// Параметры:
// ВерсияПостановленияНДС1137 - см.УчетНДСПереопределяемый.ВерсияПостановленияНДС1137()
// ЕстьПрослеживаемыеТовары - Булево - признак того, что в документе есть прослеживаемый товар.
//
// Возвращаемое значение: 
// ТекстЗапроса - строка с необходимым текстом запроса.
//
Функция ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов(ВерсияПостановленияНДС1137 = Неопределено, ЕстьПрослеживаемыеТовары = Ложь) Экспорт
	
	ОтбиратьВсеСФ1137 = Ложь;
	Если ВерсияПостановленияНДС1137 = Неопределено Тогда
		ОтбиратьВсеСФ1137 = Истина;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Документ.СчетФактураПолученный.ПустаяСсылка) КАК СчетФактура,
	|	ПоступлениеТоваровУслуг.ДатаВходящегоДокумента КАК Дата,
	|	ПоступлениеТоваровУслуг.НомерВходящегоДокумента КАК Номер,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаФактуры,
	|	"""" КАК Руководитель,
	|	"""" КАК ГлавныйБухгалтер,
	|	ИСТИНА КАК СчетФактураБезНДС,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК УдалитьПрефиксыИзНомера,
	|	ЛОЖЬ КАК ЭтоСчетФактураВыданный,
	|	ИСТИНА КАК ВыводитьСуммуБезНДС,
	|	ПоступлениеТоваровУслуг.Ссылка КАК ДокументОснование,
	|	1 КАК НомерСтроки,
	|	ПоступлениеТоваровУслуг.Контрагент КАК Контрагент,
	|	ПоступлениеТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ПоступлениеТоваровУслуг.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
	|	"""" КАК ИдентификаторГосКонтракта,
	|	"""" КАК КППСчетаФактуры,
	|	"""" КАК КПППродавца,
	|	ПоступлениеТоваровУслуг.Дата КАК ДатаСведений
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ ПЕРВЫЕ 1
	|			ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка КАК Ссылка,
	|			ИСТИНА КАК ЕстьПрослеживаемыеТовары
	|		ИЗ
	|			Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
	|		ГДЕ
	|			ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка В(&МассивОбъектов)) КАК ПрослеживаемыеТовары
	|		ПО ПоступлениеТоваровУслуг.Ссылка = ПрослеживаемыеТовары.Ссылка
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	И ПоступлениеТоваровУслуг.ДатаВходящегоДокумента > &УсловиеПоДате
	|	И &ЕстьПрослеживаемыеТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер";
	
	Если ОтбиратьВсеСФ1137 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
				">= &НачалоПримененияПостановления1137");
	ИначеЕсли ВерсияПостановленияНДС1137 = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"< &НачалоПримененияПостановления1137");
	ИначеЕсли ВерсияПостановленияНДС1137 >= 1 И ВерсияПостановленияНДС1137 <= 3 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ &НачалоПримененияПостановления1137 И ДАТАВРЕМЯ(2017,09,30,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 4 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ ДАТАВРЕМЯ(2017,10,1) И ДАТАВРЕМЯ(2021,06,30,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 5 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ ДАТАВРЕМЯ(2021,7,1) И ДАТАВРЕМЯ(2023,09,30,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 6 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ ДАТАВРЕМЯ(2023,10,1) И ДАТАВРЕМЯ(2024,09,30,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 7 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			"МЕЖДУ ДАТАВРЕМЯ(2024,10,1) И ДАТАВРЕМЯ(2025,12,31,23,59,59)");
	ИначеЕсли ВерсияПостановленияНДС1137 = 8 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "> &УсловиеПоДате",
			">= ДАТАВРЕМЯ(2026,1,1)");
	КонецЕсли;
	
	Если ОтбиратьВсеСФ1137 Тогда
		// Не накладываем отбор.
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕстьПрослеживаемыеТовары", "ИСТИНА");
	ИначеЕсли ЕстьПрослеживаемыеТовары Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕстьПрослеживаемыеТовары", "ЕСТЬNULL(ПрослеживаемыеТовары.ЕстьПрослеживаемыеТовары, ЛОЖЬ) = ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕстьПрослеживаемыеТовары", "ЕСТЬNULL(ПрослеживаемыеТовары.ЕстьПрослеживаемыеТовары, ЛОЖЬ) = ЛОЖЬ");
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Определяет какие из переданных документов, на основании которых формируется печатная форма
// универсального передаточного документа на печать, можно распечатать без выписки счета-фактуры.
// В конфигурации не поддерживается печать универсального передаточного документа в статусе "2" 
// (только первичный документ), если в документе выделена сумма НДС и сумма налога не относится
// к посреднической деятельности.
//
// Параметры:
//   МассивДокументов - Массив - массив документов, по которым формируется печатная форма.
//
// Возвращаемое значение:
//   ДокументыСчетФактураНеТребуются - Массив - документы, для печати которых не требуется выписка счета-фактуры.
Функция СчетаФактурыНеТребуются(МассивДокументов) Экспорт
	
	ДокументыСчетФактураНеТребуются = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(МассивДокументов) Тогда
		Возврат ДокументыСчетФактураНеТребуются;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ИСТИНА КАК ЕстьНДС
	|ПОМЕСТИТЬ ВТ_ДокументыСНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И ПоступлениеТоваровУслуг.СуммаНДС > 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И ПоступлениеТоваровУслуг.СуммаНДС > 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И ПоступлениеТоваровУслуг.СуммаНДС > 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ИСТИНА
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Права КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И ПоступлениеТоваровУслуг.СуммаНДС > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И ПоступлениеТоваровУслуг.ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ЕСТЬNULL(ВТ_ДокументыСНДС.ЕстьНДС, ЛОЖЬ) КАК ЕстьНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыСНДС КАК ВТ_ДокументыСНДС
	|		ПО (ВТ_ДокументыСНДС.Ссылка = ПоступлениеТоваровУслуг.Ссылка)
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивДокументов)
	|	И ПоступлениеТоваровУслуг.ДоговорКонтрагента.ВидДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом)";
	
	Результат = Запрос.ВыполнитьПакет();
	
	// Поступление товаров/услуг от комитента
	ВыборкаКомиссия = Результат[1].Выбрать();
	Пока ВыборкаКомиссия.Следующий() Цикл
		ДокументыСчетФактураНеТребуются.Добавить(ВыборкаКомиссия.Ссылка);
	КонецЦикла;
	
	// Обычные поступления
	ВыборкаРеализация = Результат[2].Выбрать();
	Пока ВыборкаРеализация.Следующий() Цикл
		Если НЕ ВыборкаРеализация.ЕстьНДС Тогда
			ДокументыСчетФактураНеТребуются.Добавить(ВыборкаРеализация.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументыСчетФактураНеТребуются;
	
КонецФункции

// Возвращаяет массив документов с прослеживаемыми товарами.
//
// Параметры:
// МассивДокументов - массив проверяемых документов.
//
// Возвращаемое значение:
// ДокументыСПрослеживаемымиТоварами - массив документов с прослеживаемыми товарами.
//
Функция ДокументыСПрослеживаемымиТоварами(МассивДокументов) Экспорт
	
	ДокументыСПрослеживаемымиТоварами = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(МассивДокументов) Тогда
		Возврат ДокументыСПрослеживаемымиТоварами
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка В(&МассивДокументов)
	|	И ПоступлениеТоваровУслугТовары.ПрослеживаемыйТовар = ИСТИНА";
	
	ДокументыСПрослеживаемымиТоварами = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат ДокументыСПрослеживаемымиТоварами;
	
КонецФункции

#КонецОбласти

Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыПоступлениеТоваров()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗапросПоОборудованию.Ссылка.Номер КАК Номер,
	|	ЗапросПоОборудованию.Ссылка КАК Ссылка,
	|	ЗапросПоОборудованию.Ссылка.Дата КАК Дата,
	|	ЗапросПоОборудованию.Ссылка.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ЗапросПоОборудованию.Ссылка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ЗапросПоОборудованию.Ссылка.Контрагент КАК Контрагент,
	|	ЗапросПоОборудованию.Ссылка.Контрагент КАК Поставщик,
	|	ЗапросПоОборудованию.Ссылка.Организация КАК Организация,
	|	ЗапросПоОборудованию.Ссылка.Организация КАК Покупатель,
	|	ЗапросПоОборудованию.Ссылка.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ЗапросПоОборудованию.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	ЗапросПоОборудованию.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	ЗапросПоОборудованию.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ЗапросПоОборудованию.Номенклатура,
	|	ЗапросПоОборудованию.Номенклатура.НаименованиеПолное КАК Товар,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ЗапросПоОборудованию.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ЗапросПоОборудованию.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ КАК КодАртикул,
	|	ЗапросПоОборудованию.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаХранения,
	|	ЗапросПоОборудованию.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмерения,
	|	ЗапросПоОборудованию.Количество КАК КоличествоМест,
	|	ЗапросПоОборудованию.Количество КАК Количество,
	|	ЗапросПоОборудованию.Коэффициент,
	|	ЗапросПоОборудованию.Цена,
	|	ЗапросПоОборудованию.Сумма,
	|	ЗапросПоОборудованию.СуммаНДС,
	|	ЗапросПоОборудованию.СтавкаНДС,
	|	ЗапросПоОборудованию.НомерСтроки,
	|	1 КАК ID
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ЗапросПоОборудованию
	|ГДЕ
	|	ЗапросПоОборудованию.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапросПоОбъектамСтройки.Ссылка.Номер,
	|	ЗапросПоОбъектамСтройки.Ссылка,
	|	ЗапросПоОбъектамСтройки.Ссылка.Дата,
	|	ЗапросПоОбъектамСтройки.Ссылка.ДатаВходящегоДокумента,
	|	ЗапросПоОбъектамСтройки.Ссылка.ДоговорКонтрагента,
	|	ЗапросПоОбъектамСтройки.Ссылка.Контрагент,
	|	ЗапросПоОбъектамСтройки.Ссылка.Контрагент,
	|	ЗапросПоОбъектамСтройки.Ссылка.Организация,
	|	ЗапросПоОбъектамСтройки.Ссылка.Организация,
	|	ЗапросПоОбъектамСтройки.Ссылка.ПодразделениеОрганизации,
	|	ЗапросПоОбъектамСтройки.Ссылка.СуммаДокумента,
	|	ЗапросПоОбъектамСтройки.Ссылка.ВалютаДокумента,
	|	ЗапросПоОбъектамСтройки.Ссылка.СуммаВключаетНДС,
	|	ЗапросПоОбъектамСтройки.ОбъектСтроительства,
	|	ЗапросПоОбъектамСтройки.ОбъектСтроительства.Наименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|		ИНАЧЕ ЗапросПоОбъектамСтройки.ОбъектСтроительства.Код
	|	КОНЕЦ,
	|	""шт."",
	|	""шт."",
	|	1,
	|	1,
	|	1,
	|	ЗапросПоОбъектамСтройки.Сумма,
	|	ЗапросПоОбъектамСтройки.Сумма,
	|	ЗапросПоОбъектамСтройки.СуммаНДС,
	|	ЗапросПоОбъектамСтройки.СтавкаНДС,
	|	ЗапросПоОбъектамСтройки.НомерСтроки,
	|	2
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ЗапросПоОбъектамСтройки
	|ГДЕ
	|	ЗапросПоОбъектамСтройки.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапросПоТоварам.Ссылка.Номер,
	|	ЗапросПоТоварам.Ссылка,
	|	ЗапросПоТоварам.Ссылка.Дата,
	|	ЗапросПоТоварам.Ссылка.ДатаВходящегоДокумента,
	|	ЗапросПоТоварам.Ссылка.ДоговорКонтрагента,
	|	ЗапросПоТоварам.Ссылка.Контрагент,
	|	ЗапросПоТоварам.Ссылка.Контрагент,
	|	ЗапросПоТоварам.Ссылка.Организация,
	|	ЗапросПоТоварам.Ссылка.Организация,
	|	ЗапросПоТоварам.Ссылка.ПодразделениеОрганизации,
	|	ЗапросПоТоварам.Ссылка.СуммаДокумента,
	|	ЗапросПоТоварам.Ссылка.ВалютаДокумента,
	|	ЗапросПоТоварам.Ссылка.СуммаВключаетНДС,
	|	ЗапросПоТоварам.Номенклатура,
	|	ЗапросПоТоварам.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ЗапросПоТоварам.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ЗапросПоТоварам.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	ЗапросПоТоварам.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ЗапросПоТоварам.ЕдиницаИзмерения.Представление,
	|	ЗапросПоТоварам.КоличествоМест,
	|	ЗапросПоТоварам.Количество,
	|	ЗапросПоТоварам.Коэффициент,
	|	ЗапросПоТоварам.Цена,
	|	ЗапросПоТоварам.Сумма,
	|	ЗапросПоТоварам.СуммаНДС,
	|	ЗапросПоТоварам.СтавкаНДС,
	|	ЗапросПоТоварам.НомерСтроки,
	|	3
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|		ПоступлениеТоваровУслуг.Номенклатура КАК Номенклатура,
	|		СУММА(ПоступлениеТоваровУслуг.КоличествоМест) КАК КоличествоМест,
	|		СУММА(ПоступлениеТоваровУслуг.Количество) КАК Количество,
	|		ПоступлениеТоваровУслуг.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ПоступлениеТоваровУслуг.Коэффициент КАК Коэффициент,
	|		ПоступлениеТоваровУслуг.Цена КАК Цена,
	|		СУММА(ПоступлениеТоваровУслуг.Сумма) КАК Сумма,
	|		СУММА(ПоступлениеТоваровУслуг.СуммаНДС) КАК СуммаНДС,
	|		ПоступлениеТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|		МИНИМУМ(ПоступлениеТоваровУслуг.НомерСтроки) КАК НомерСтроки
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслуг
	|	ГДЕ
	|		ПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПоступлениеТоваровУслуг.Ссылка,
	|		ПоступлениеТоваровУслуг.Номенклатура,
	|		ПоступлениеТоваровУслуг.СтавкаНДС,
	|		ПоступлениеТоваровУслуг.Цена,
	|		ПоступлениеТоваровУслуг.ЕдиницаИзмерения,
	|		ПоступлениеТоваровУслуг.Коэффициент) КАК ЗапросПоТоварам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗапросПоУслугам.Ссылка.Номер,
	|	ЗапросПоУслугам.Ссылка,
	|	ЗапросПоУслугам.Ссылка.Дата,
	|	ЗапросПоУслугам.Ссылка.ДатаВходящегоДокумента,
	|	ЗапросПоУслугам.Ссылка.ДоговорКонтрагента,
	|	ЗапросПоУслугам.Ссылка.Контрагент,
	|	ЗапросПоУслугам.Ссылка.Контрагент,
	|	ЗапросПоУслугам.Ссылка.Организация,
	|	ЗапросПоУслугам.Ссылка.Организация,
	|	ЗапросПоУслугам.Ссылка.ПодразделениеОрганизации,
	|	ЗапросПоУслугам.Ссылка.СуммаДокумента,
	|	ЗапросПоУслугам.Ссылка.ВалютаДокумента,
	|	ЗапросПоУслугам.Ссылка.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА НЕ ЗапросПоУслугам.Содержание ПОДОБНО """"
	|			ТОГДА ЗапросПоУслугам.Содержание
	|		КОГДА НЕ ЗапросПоУслугам.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ЗапросПоУслугам.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ЗапросПоУслугам.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ЗапросПоУслугам.Содержание ПОДОБНО """"
	|			ТОГДА ЗапросПоУслугам.Содержание
	|		КОГДА НЕ ЗапросПоУслугам.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ЗапросПоУслугам.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ЗапросПоУслугам.Номенклатура.Наименование
	|	КОНЕЦ,
	|	NULL,
	|	ЗапросПоУслугам.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	NULL,
	|	NULL,
	|	ЗапросПоУслугам.Количество,
	|	1,
	|	ЗапросПоУслугам.Цена,
	|	ЗапросПоУслугам.Сумма,
	|	ЗапросПоУслугам.СуммаНДС,
	|	ЗапросПоУслугам.СтавкаНДС,
	|	ЗапросПоУслугам.НомерСтроки,
	|	4
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ЗапросПоУслугам
	|ГДЕ
	|	ЗапросПоУслугам.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Дата,
	|	ID,
	|	ЗапросПоОборудованию.НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ с печатной формой накладной,
// разработанной методистами
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьПоступлениеТоваров(МассивОбъектов, ОбъектыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_Накладная";
	
	ДополнительнаяКолонкаПечатныхФормДокументов = Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить();
	Если НЕ ЗначениеЗаполнено(ДополнительнаяКолонкаПечатныхФормДокументов) Тогда
		ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить;
	КонецЕсли;
 	ВыводитьКоды = ДополнительнаяКолонкаПечатныхФормДокументов <> Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", ДополнительнаяКолонкаПечатныхФормДокументов);
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыПоступлениеТоваров();
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	// Определение количества строк в документах.
	КоличествоСтрокДокумента = Новый Соответствие;
	Пока Шапка.СледующийПоЗначениюПоля("Ссылка") Цикл
		КоличествоСтрок = 0;
		Пока Шапка.Следующий() Цикл
			Если Шапка.Номенклатура <> Null Тогда 
				КоличествоСтрок = КоличествоСтрок + 1;
			КонецЕсли;
		КонецЦикла;
		КоличествоСтрокДокумента.Вставить(Шапка.Ссылка, КоличествоСтрок);
	КонецЦикла;
	Шапка.Сбросить();
	
	ПервыйДокумент = Истина;
	
	Пока Шапка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
 		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваровУслуг.ПФ_MXL_Накладная");

		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		// Выводим шапку накладной

		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.ТекстЗаголовка = ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(Шапка, "Накладная");
		ТабличныйДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
		СведенияОПоставщике     = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Поставщик, Шапка.Дата);
		ПредставлениеПоставщика = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПоставщике, "НаименованиеДляПечатныхФорм,");
		ОбластьМакета.Параметры.ПредставлениеПоставщика = ПредставлениеПоставщика;
		ОбластьМакета.Параметры.Поставщик = Шапка.Контрагент;
		ТабличныйДокумент.Вывести(ОбластьМакета);

		ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
		СведенияОПокупателе     = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Покупатель, Шапка.Дата);
		ПредставлениеПокупателя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПокупателе, "НаименованиеДляПечатныхФорм,");
		ОбластьМакета.Параметры.ПредставлениеПолучателя = ПредставлениеПокупателя;
		ОбластьМакета.Параметры.Получатель = Шапка.Организация;
		ТабличныйДокумент.Вывести(ОбластьМакета);

		// Вывести табличную часть
		ОбластьШапки = ?(ВыводитьКоды, "ШапкаСКодом", "ШапкаТаблицы");
		ОбластьМакета = Макет.ПолучитьОбласть(ОбластьШапки);
		Если ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
			ОбластьМакета.Параметры.ИмяКодАртикул = "Артикул";
		ИначеЕсли ДополнительнаяКолонкаПечатныхФормДокументов = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Код Тогда
			ОбластьМакета.Параметры.ИмяКодАртикул = "Код";
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьМакета);

		ОбластьСтроки = ?(ВыводитьКоды,	"СтрокаСКодом", "Строка");
		ОбластьМакета = Макет.ПолучитьОбласть(ОбластьСтроки);
		Сумма    = 0;
		СуммаНДС = 0;

		НомерСтроки = 1;
		Пока Шапка.Следующий() Цикл
			
			Если Шапка.Номенклатура = Null Тогда 
				Продолжить;
			КонецЕсли;

			ОбластьМакета.Параметры.Заполнить(Шапка);
			ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
			ОбластьМакета.Параметры.Товар       = СокрЛП(Шапка.Товар);

			ТабличныйДокумент.Вывести(ОбластьМакета);

			Сумма    = Сумма    + ?(ЗначениеЗаполнено(Шапка.Сумма), Шапка.Сумма, 0);
			СуммаНДС = СуммаНДС + ?(ЗначениеЗаполнено(Шапка.СуммаНДС), Шапка.СуммаНДС, 0);
			НомерСтроки = НомерСтроки + 1;

		КонецЦикла;

		// Вывести Итого
		ОбластьМакета = Макет.ПолучитьОбласть("Итого");
		ОбластьМакета.Параметры.Всего = ОбщегоНазначенияБПВызовСервера.ФорматСумм(Сумма);
		ТабличныйДокумент.Вывести(ОбластьМакета);

		// Вывести ИтогоНДС
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоНДС");
		ОбластьМакета.Параметры.ВсегоНДС = ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаНДС);
		ОбластьМакета.Параметры.НДС = ?(Шапка.СуммаВключаетНДС, "В том числе НДС:", "Сумма НДС:");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Вывести Сумму прописью
		ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
		СуммаКПрописи = Сумма + ?(Шапка.СуммаВключаетНДС, 0, СуммаНДС);
		ОбластьМакета.Параметры.ИтоговаяСтрока ="Всего наименований " + КоличествоСтрокДокумента.Получить(Шапка.Ссылка)
		+ ", на сумму " + ОбщегоНазначенияБПВызовСервера.ФорматСумм(СуммаКПрописи, Шапка.ВалютаДокумента);
		ОбластьМакета.Параметры.СуммаПрописью = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(СуммаКПрописи, Шапка.ВалютаДокумента);
		ТабличныйДокумент.Вывести(ОбластьМакета);

		// Вывести подписи
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьМакета);

		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции // ПечатьПоступлениеТоваров()

// Функция выполняет расчет рублевых сумм для вывода таблиц документа на печать
//
Функция ПодготовитьТаблицуДокументаДляПечати(ВыборкаСтрок, ТаблицаПоТоварам, МассивIDТабличныхЧастей)

	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	СуммаВзаиморасчетов = 0;
	СуммаВзаиморасчетовНДС = 0;
	ТаблицаПоТоварам.Очистить();
	
	НуженПересчетВРубли = Ложь;
	
	Если ВыборкаСтрок.Проведен 
		И (ВыборкаСтрок.РасчетыВУсловныхЕдиницах
		ИЛИ (ВыборкаСтрок.ВалютаДокумента <> ВалютаРегламентированногоУчета И ВыборкаСтрок.Дата >= '20090101000000')) Тогда
		НуженПересчетВРубли = Истина;
	КонецЕсли;
	
	Пока ВыборкаСтрок.Следующий() Цикл
		
		СтрокаТаблицы = ТаблицаПоТоварам.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаСтрок);
		
		Если НуженПересчетВРубли Тогда
			
			СтрокаТаблицы.СуммаНДС = ?(СтрокаТаблицы.НДСРуб = 0, СтрокаТаблицы.СуммаНДС, СтрокаТаблицы.НДСРуб);
			
			// В случае если сумма включает НДС, то сумма без НДС будет рассчитана позже
			Если СтрокаТаблицы.СуммаВключаетНДС Тогда
				СтрокаТаблицы.Сумма = ?(СтрокаТаблицы.ВсегоРуб = 0, СтрокаТаблицы.Сумма, СтрокаТаблицы.ВсегоРуб);
			// Для валютных документов для Суммы необходимо применять средний курс, 
			// так как сумма документа рассчитывается по курсу аванса,
			// а сумма НДС по курсу текущего документа
			ИначеЕсли НЕ ВыборкаСтрок.РасчетыВУсловныхЕдиницах Тогда
				СуммаРуб            = ?(СтрокаТаблицы.ВсегоРуб = 0, СтрокаТаблицы.Сумма, СтрокаТаблицы.ВсегоРуб);
				СтрокаТаблицы.Сумма = СуммаРуб - СтрокаТаблицы.СуммаНДС;
			// Для условных единиц и сумма НДС и сумма документа рассчитывается по курсу аванса
			Иначе
				СтрокаТаблицы.Сумма = ?(СтрокаТаблицы.СуммаБезНДСРуб = 0, СтрокаТаблицы.Сумма, СтрокаТаблицы.СуммаБезНДСРуб);
			КонецЕсли; 
			
			СтрокаТаблицы.Цена      = ?(СтрокаТаблицы.Количество = 0, 0, СтрокаТаблицы.Сумма/СтрокаТаблицы.Количество);
			
		КонецЕсли;	
		
	КонецЦикла;

	// Исключим строки из табличный частей, которые не удовлетворяют условиям печати. При выборке запросом их необходимо получать, 
	// чтобы распределять сумма взаиморасчетов в рублях на все строки документа.
	Если МассивIDТабличныхЧастей <> Неопределено
		И МассивIDТабличныхЧастей.Количество() <> 0 Тогда
		Инд = ТаблицаПоТоварам.Количество() - 1;
		Пока Инд >= 0 Цикл
			СтрокаТовар = ТаблицаПоТоварам[Инд];
			
			Если МассивIDТабличныхЧастей.Найти(СтрокаТовар.ID) = Неопределено Тогда
				ТаблицаПоТоварам.Удалить(Инд);
			КонецЕсли;
			
			Инд = Инд - 1;
		КонецЦикла;
	КонецЕсли;

	Возврат ТаблицаПоТоварам;

КонецФункции

Функция ПолучитьТекстЗапросаДляФормированияТаблицыСведенийТОРГ12()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Дата КАК ДатаДокумента
	|ПОМЕСТИТЬ ДокументыДляПечати
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	И НЕ ПоступлениеТоваровУслуг.ЕстьРасхождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Документ,
	|	ПоступлениеТоваровУслуг.Дата КАК ДатаДляПолученияСведений,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	ПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ПоступлениеТоваровУслуг.ПодразделениеОрганизации КАК Подразделение,
	|	ПоступлениеТоваровУслуг.Контрагент КАК Поставщик,
	|	ПоступлениеТоваровУслуг.Организация КАК Покупатель,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровУслуг.Организация
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровУслуг.Контрагент
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ПоступлениеТоваровУслуг.ДоговорКонтрагента КАК Договор,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.ДоговорКонтрагента.Представление, """") КАК Основание,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.ДоговорКонтрагента.Номер, """") КАК ОснованиеНомер,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.ДоговорКонтрагента.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ОснованиеДата,
	|	ПоступлениеТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	ПоступлениеТоваровУслуг.КурсВзаиморасчетов КАК Курс,
	|	ПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК Кратность
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПоступлениеТоваровУслуг.Организация = ДанныеПервичныхДокументов.Организация
	|			И ПоступлениеТоваровУслуг.Ссылка = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыДляПечати.Ссылка
	|			ИЗ
	|				ДокументыДляПечати КАК ДокументыДляПечати)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Документ,
	|	ДокументыДляПечати.ДатаДокумента КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Оборудование) КАК ТабличнаяЧасть,
	|	1 КАК ПорядокТабличнойЧасти,
	|	ПоступлениеТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеТоваровУслуг.Номенклатура КАК Товар,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ КАК ТоварКод,
	|	ПоступлениеТоваровУслуг.Количество КАК Количество,
	|	0 КАК КоличествоМест,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	ПоступлениеТоваровУслуг.ЕдиницаИзмерения КАК ВидУпаковки,
	|	ПоступлениеТоваровУслуг.Коэффициент КАК Коэффициент,
	|	ПоступлениеТоваровУслуг.Коэффициент КАК КоличествоВОдномМесте,
	|	ПоступлениеТоваровУслуг.Цена КАК Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма - ПоступлениеТоваровУслуг.СуммаНДС
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ПоступлениеТоваровУслуг.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоВРублях,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСВРублях,
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СуществуютЗаписиРублевыхСумм
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Оборудование))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары),
	|	2,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.КоличествоМест,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	ПоступлениеТоваровУслуг.ЕдиницаИзмерения,
	|	ПоступлениеТоваровУслуг.Коэффициент,
	|	ПоступлениеТоваровУслуг.Коэффициент,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма - ПоступлениеТоваровУслуг.СуммаНДС
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Сумма
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.СуммаНДС,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги),
	|	3,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Количество,
	|	0,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	NULL,
	|	1,
	|	1,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма - ПоступлениеТоваровУслуг.СуммаНДС
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Сумма
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.СуммаНДС,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|ГДЕ
	|	&ВключатьУслуги = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги),
	|	4,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Количество,
	|	0,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	NULL,
	|	1,
	|	1,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма - ПоступлениеТоваровУслуг.СуммаНДС
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Сумма
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.СуммаНДС,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|ГДЕ
	|	&ВключатьУслуги = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ВозвратнаяТара),
	|	5,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Количество,
	|	0,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	NULL,
	|	0,
	|	0,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ПоступлениеТоваровУслуг.Сумма,
	|	0,
	|	NULL,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.ВозвратнаяТара КАК ПоступлениеТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ВозвратнаяТара))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ,
	|	ПорядокТабличнойЧасти,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТаблицуСведенийТОРГ12Покупатель(Знач МассивОбъектов, Знач ВключатьУслуги) Экспорт
	
	ТаблицаСведений = ПечатьТорговыхДокументов.ПолучитьОписаниеТОРГ12();
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	Запрос.Параметры.Вставить("ВключатьУслуги", ВключатьУслуги);
	
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияТаблицыСведенийТОРГ12(); 
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	РеквизитыВалютыРегУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВалютаРегУчета, "Код, Наименование");
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	СтрокиДокументов = РезультатыЗапроса[3].Выгрузить();
	СтрокиДокументов.Индексы.Добавить("Документ");
	
	Пока Выборка.Следующий() Цикл
		
		СведенияОДокументе = ТаблицаСведений.Добавить();
		ЗаполнитьЗначенияСвойств(СведенияОДокументе, Выборка);
		СведенияОДокументе.Валюта             = ВалютаРегУчета;
		СведенияОДокументе.ВалютаКод          = РеквизитыВалютыРегУчета.Код;
		СведенияОДокументе.ВалютаНаименование = РеквизитыВалютыРегУчета.Наименование;
		
		ТаблицаДокумента = ПечатьТорговыхДокументов.ПолучитьОписаниеТаблицыТОРГ12();
		
		Отбор = Новый Структура("Документ", Выборка.Документ);
		СтрокиДокумента = СтрокиДокументов.НайтиСтроки(Отбор);
		
		НуженПересчетВРубли = Ложь;
		Если Выборка.ВалютаДокумента <> ВалютаРегУчета Тогда
			НуженПересчетВРубли	= Истина;
		КонецЕсли;
		
		Для Каждого Строка Из СтрокиДокумента Цикл
			
			СтрокаТаблицыДокумента = ТаблицаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокумента, Строка);
			
			Если НуженПересчетВРубли Тогда
				Если Строка.СуществуютЗаписиРублевыхСумм Тогда
					СтрокаТаблицыДокумента.СуммаНДС 	= Строка.НДСВРублях;
					СтрокаТаблицыДокумента.СуммаБезНДС 	= Строка.ВсегоВРублях - Строка.НДСВРублях;
				Иначе
					Кратность = ?(Выборка.Кратность 	= 0, 1, Выборка.Кратность);
					СтрокаТаблицыДокумента.СуммаНДС 	= Строка.СуммаНДС * Выборка.Курс / Кратность;;
					СтрокаТаблицыДокумента.СуммаБезНДС 	= (Строка.СуммаБезНДС + Строка.СуммаНДС) * Выборка.Курс / Кратность - СтрокаТаблицыДокумента.СуммаНДС;
				КонецЕсли;
			КонецЕсли;
			СтрокаТаблицыДокумента.СуммаСНДС = СтрокаТаблицыДокумента.СуммаБезНДС + СтрокаТаблицыДокумента.СуммаНДС;
			СтрокаТаблицыДокумента.Цена = ?(СтрокаТаблицыДокумента.Количество = 0, СтрокаТаблицыДокумента.СуммаБезНДС, СтрокаТаблицыДокумента.СуммаБезНДС / СтрокаТаблицыДокумента.Количество);
			
		КонецЦикла;
		
		СведенияОДокументе.ТаблицаДокумента = ТаблицаДокумента;
		
	КонецЦикла;
	
	Возврат ТаблицаСведений;
	
КонецФункции

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыТОРГ4()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка.Номер КАК Номер,
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.Ссылка.Проведен КАК Проведен,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата КАК Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата КАК ДатаДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.ДатаВходящегоДокумента КАК ДатаДокументаСопровождения,
	|	ПоступлениеТоваровУслуг.Ссылка.НомерВходящегоДокумента КАК НомерДокументаСопровождения,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация КАК Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация КАК ЮрФизЛицо,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация КАК Руководители,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент КАК Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент КАК Поставщик,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент КАК Грузоотправитель,
	|	ПоступлениеТоваровУслуг.Ссылка.Склад КАК МестоПриемки,
	|	ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Ссылка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации.НаименованиеПолное КАК ПредставлениеПодразделения,
	|	ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов КАК Курс,
	|	ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК Кратность,
	|	ПоступлениеТоваровУслуг.Ссылка.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ КАК ТоварКод,
	|	ПоступлениеТоваровУслуг.КоличествоМест,
	|	ПоступлениеТоваровУслуг.Количество КАК КоличествоШтук,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКодПоОКЕИ,
	|	ПоступлениеТоваровУслуг.Коэффициент КАК КоличествоВОдномМесте,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ПоступлениеТоваровУслуг.Сумма,
	|	ПоступлениеТоваровУслуг.СуммаНДС,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	2 КАК ID,
	|	ПоступлениеТоваровУслуг.ЭтоКомиссия КАК ЭтоКомиссия,
	|	ПоступлениеТоваровУслуг.ВсегоРуб КАК ВсегоРуб,
	|	ПоступлениеТоваровУслуг.СуммаБезНДСРуб КАК СуммаБезНДСРуб,
	|	ПоступлениеТоваровУслуг.НДСРуб КАК НДСРуб
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|		ПоступлениеТоваровУслуг.Номенклатура КАК Номенклатура,
	|		СУММА(ЕСТЬNULL(ПоступлениеТоваровУслуг.КоличествоМест, 0)) КАК КоличествоМест,
	|		СУММА(ЕСТЬNULL(ПоступлениеТоваровУслуг.Количество, 0)) КАК Количество,
	|		ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ПоступлениеТоваровУслуг.Коэффициент КАК Коэффициент,
	|		ЕСТЬNULL(ВЫБОР
	|				КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|					ТОГДА ПоступлениеТоваровУслуг.Цена
	|				КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|					ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Цена * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|			КОНЕЦ, 0) КАК Цена,
	|		СУММА(ЕСТЬNULL(ВЫБОР
	|					КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|						ТОГДА ПоступлениеТоваровУслуг.Сумма
	|					КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|						ТОГДА 0
	|					ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|				КОНЕЦ, 0)) КАК Сумма,
	|		СУММА(ЕСТЬNULL(ВЫБОР
	|					КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|						ТОГДА ПоступлениеТоваровУслуг.СуммаНДС
	|					КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|						ТОГДА 0
	|					ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаНДС * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|				КОНЕЦ, 0)) КАК СуммаНДС,
	|		ЕСТЬNULL(ПоступлениеТоваровУслуг.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)) КАК СтавкаНДС,
	|		ЕСТЬNULL(ПоступлениеТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ) КАК ЭтоКомиссия,
	|		МИНИМУМ(ЕСТЬNULL(ПоступлениеТоваровУслуг.НомерСтроки, 0)) КАК НомерСтроки,
	|		СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0)) КАК ВсегоРуб,
	|		СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)) КАК СуммаБезНДСРуб,
	|		СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0)) КАК НДСРуб
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслуг
	|			ПО (ПоступлениеТоваровУслуг.Ссылка = ДокументПоступлениеТоваровУслуг.Ссылка)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|			ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|				И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|				И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|				И (ДокументПоступлениеТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|	ГДЕ
	|		ДокументПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДокументПоступлениеТоваровУслуг.Ссылка,
	|		ПоступлениеТоваровУслуг.Номенклатура,
	|		ЕСТЬNULL(ПоступлениеТоваровУслуг.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)),
	|		ПоступлениеТоваровУслуг.ЕдиницаИзмерения,
	|		ПоступлениеТоваровУслуг.Коэффициент,
	|		ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|		ЕСТЬNULL(ВЫБОР
	|				КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|					ТОГДА ПоступлениеТоваровУслуг.Цена
	|				КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|					ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Цена * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|			КОНЕЦ, 0),
	|		ЕСТЬNULL(ПоступлениеТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ)) КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	НЕ ПоступлениеТоваровУслуг.НомерСтроки ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка.Номер,
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ПоступлениеТоваровУслуг.Ссылка.Проведен,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.ДатаВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.НомерВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Склад,
	|	ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Ссылка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации,
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации.НаименованиеПолное,
	|	ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов,
	|	ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов,
	|	ПоступлениеТоваровУслуг.Ссылка.СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.КоличествоМест,
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Представление,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ПоступлениеТоваровУслуг.Коэффициент,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ПоступлениеТоваровУслуг.Сумма,
	|	ПоступлениеТоваровУслуг.СуммаНДС,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	1,
	|	ПоступлениеТоваровУслуг.ЭтоКомиссия,
	|	ПоступлениеТоваровУслуг.ВсегоРуб,
	|	ПоступлениеТоваровУслуг.СуммаБезНДСРуб,
	|	ПоступлениеТоваровУслуг.НДСРуб
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|		ПоступлениеТоваровУслуг.Номенклатура КАК Номенклатура,
	|		СУММА(ЕСТЬNULL(ПоступлениеТоваровУслуг.Количество, 0)) КАК КоличествоМест,
	|		СУММА(ЕСТЬNULL(ПоступлениеТоваровУслуг.Количество, 0)) КАК Количество,
	|		ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ПоступлениеТоваровУслуг.Коэффициент КАК Коэффициент,
	|		ЕСТЬNULL(ВЫБОР
	|				КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|					ТОГДА ПоступлениеТоваровУслуг.Цена
	|				КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|					ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Цена * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|			КОНЕЦ, 0) КАК Цена,
	|		СУММА(ЕСТЬNULL(ВЫБОР
	|					КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|						ТОГДА ПоступлениеТоваровУслуг.Сумма
	|					КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|						ТОГДА 0
	|					ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|				КОНЕЦ, 0)) КАК Сумма,
	|		СУММА(ЕСТЬNULL(ВЫБОР
	|					КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|						ТОГДА ПоступлениеТоваровУслуг.СуммаНДС
	|					КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|						ТОГДА 0
	|					ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаНДС * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|				КОНЕЦ, 0)) КАК СуммаНДС,
	|		ЕСТЬNULL(ПоступлениеТоваровУслуг.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)) КАК СтавкаНДС,
	|		ЕСТЬNULL(ПоступлениеТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ) КАК ЭтоКомиссия,
	|		МИНИМУМ(ЕСТЬNULL(ПоступлениеТоваровУслуг.НомерСтроки, 0)) КАК НомерСтроки,
	|		СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0)) КАК ВсегоРуб,
	|		СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)) КАК СуммаБезНДСРуб,
	|		СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0)) КАК НДСРуб
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслуг
	|			ПО (ПоступлениеТоваровУслуг.Ссылка = ДокументПоступлениеТоваровУслуг.Ссылка)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|			ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|				И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|				И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Оборудование))
	|				И (ДокументПоступлениеТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|	ГДЕ
	|		ДокументПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДокументПоступлениеТоваровУслуг.Ссылка,
	|		ПоступлениеТоваровУслуг.Номенклатура,
	|		ЕСТЬNULL(ПоступлениеТоваровУслуг.СтавкаНДС, ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)),
	|		ПоступлениеТоваровУслуг.ЕдиницаИзмерения,
	|		ПоступлениеТоваровУслуг.Коэффициент,
	|		ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|		ЕСТЬNULL(ВЫБОР
	|				КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|					ТОГДА ПоступлениеТоваровУслуг.Цена
	|				КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|					ТОГДА 0
	|				ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Цена * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|			КОНЕЦ, 0),
	|		ЕСТЬNULL(ПоступлениеТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ)) КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	НЕ ПоступлениеТоваровУслуг.НомерСтроки ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка.Номер,
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ПоступлениеТоваровУслуг.Ссылка.Проведен,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.ДатаВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.НомерВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Склад,
	|	ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Ссылка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации,
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации.НаименованиеПолное,
	|	ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов,
	|	ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов,
	|	ПоступлениеТоваровУслуг.Ссылка.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Номенклатура.Код,
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	1,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслуг.Цена
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Цена * ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов / ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов / ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслуг.СуммаНДС
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаНДС * ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов / ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	3,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0)
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|			И (ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка.Номер,
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ПоступлениеТоваровУслуг.Ссылка.Проведен,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.ДатаВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.НомерВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Склад,
	|	ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Ссылка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации,
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации.НаименованиеПолное,
	|	ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов,
	|	ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов,
	|	ПоступлениеТоваровУслуг.Ссылка.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Номенклатура.Код,
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	1,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслуг.Цена
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Цена * ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов / ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов / ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслуг.СуммаНДС
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаНДС * ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов / ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	4,
	|	ИСТИНА,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0)
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|			И (ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка.Номер,
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ПоступлениеТоваровУслуг.Ссылка.Проведен,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка.ДатаВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.НомерВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Организация,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Контрагент,
	|	ПоступлениеТоваровУслуг.Ссылка.Склад,
	|	ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Ссылка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации,
	|	ПоступлениеТоваровУслуг.Ссылка.ПодразделениеОрганизации.НаименованиеПолное,
	|	ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов,
	|	ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов,
	|	ПоступлениеТоваровУслуг.Ссылка.СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ПоступлениеТоваровУслуг.Номенклатура.Код,
	|	1,
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	1,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслуг.Цена
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Цена * ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов / ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма
	|		КОГДА ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ПоступлениеТоваровУслуг.Ссылка.КурсВзаиморасчетов / ПоступлениеТоваровУслуг.Ссылка.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	0,
	|	NULL,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	5,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0)
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ВозвратнаяТара КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ВозвратнаяТара))
	|			И (ПоступлениеТоваровУслуг.Ссылка.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Ссылка,
	|	ID,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ унифицированной формы ТОРГ-4
// "Акт о приемке товара, поступившего без счета поставщика"
//
// Возвращаемое значение:
//  Табличный документ - печатная форма ТОРГ-4
//
Функция ПечатьТОРГ4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	Перем ПодразделениеОтветственныхЛиц;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегламентированногоУчета   = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ПолеСверху			= 0;
	ТабличныйДокумент.ПолеСнизу				= 0;
	ТабличныйДокумент.ПолеСправа			= 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_ТОРГ4";

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ТОРГ4");

	СисИнфо = Новый СистемнаяИнформация;
	Если ПустаяСтрока(СисИнфо.ИнформацияПрограммыПросмотра) Тогда 
		ТабличныйДокумент.ПолеСлева          = 0;
	Иначе
		ТабличныйДокумент.ПолеСлева          = 10;
	КонецЕсли;

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыТОРГ4();
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	ОписаниеТиповЧисло15_3 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3);	
	ОписаниеТиповЧисло15_2 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);	

	ТаблицаПоТоварам = Новый ТаблицаЗначений; 		
	ТаблицаПоТоварам.Колонки.Добавить("НомерСтроки");
	ТаблицаПоТоварам.Колонки.Добавить("Номенклатура");
	ТаблицаПоТоварам.Колонки.Добавить("ТоварНаименование");
	ТаблицаПоТоварам.Колонки.Добавить("ТоварКод");
	ТаблицаПоТоварам.Колонки.Добавить("Количество", 				ОписаниеТиповЧисло15_3);
	ТаблицаПоТоварам.Колонки.Добавить("КоличествоМест");
	ТаблицаПоТоварам.Колонки.Добавить("КоличествоШтук");
	ТаблицаПоТоварам.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ТаблицаПоТоварам.Колонки.Добавить("ЕдиницаИзмеренияКодПоОКЕИ");
	ТаблицаПоТоварам.Колонки.Добавить("ВидУпаковки");
	ТаблицаПоТоварам.Колонки.Добавить("Коэффициент");
	ТаблицаПоТоварам.Колонки.Добавить("КоличествоВОдномМесте");
	ТаблицаПоТоварам.Колонки.Добавить("СуммаВключаетНДС");
	ТаблицаПоТоварам.Колонки.Добавить("Цена", 						ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("Сумма", 						ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаНДС", 					ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаВВалютеДокумента", 		ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаНДСВВалютеДокумента", 	ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СтавкаНДС");
	ТаблицаПоТоварам.Колонки.Добавить("ID");
	ТаблицаПоТоварам.Колонки.Добавить("СуммаБезНДС",				ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаРублевая", 				ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("Стоимость", 					ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("ЭтоКомиссия", 				Новый ОписаниеТипов("Булево"));
	ТаблицаПоТоварам.Колонки.Добавить("ВсегоРуб",                   ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("НДСРуб",                     ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаБезНДСРуб",             ОписаниеТиповЧисло15_2);
	
	МассивIDТабличныхЧастей = Новый Массив;
	МассивIDТабличныхЧастей.Добавить(2); // Товары
	МассивIDТабличныхЧастей.Добавить(3); // Услуги
	МассивIDТабличныхЧастей.Добавить(4); // АгентскиеУслуги
	МассивIDТабличныхЧастей.Добавить(5); // ВозвратнаяТара
	
	// Определение количества строк в документах.
	КоличествоСтрокДокумента = Новый Соответствие;
	Пока Шапка.СледующийПоЗначениюПоля("Ссылка") Цикл
		КоличествоСтрок = 0;
		Пока Шапка.Следующий() Цикл
			Если МассивIDТабличныхЧастей.Найти(Шапка.ID) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			КоличествоСтрок = КоличествоСтрок + 1;
		КонецЦикла;
		КоличествоСтрокДокумента.Вставить(Шапка.Ссылка, КоличествоСтрок);
	КонецЦикла;
	Шапка.Сбросить();
	
	Пока Шапка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало 	= ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ТаблицаПоТоварам 	= ПодготовитьТаблицуДокументаДляПечати(Шапка, ТаблицаПоТоварам, МассивIDТабличныхЧастей);
		
		ОбластьМакетаШапка            = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакетаЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ОбластьМакетаСтрока           = Макет.ПолучитьОбласть("Строка");
		ОбластьМакетаИтогоПоСтранице  = Макет.ПолучитьОбласть("ИтогиПоСтранице");
		ОбластьМакетаВсего            = Макет.ПолучитьОбласть("Всего");
		ОбластьМакетаПодвал           = Макет.ПолучитьОбласть("Подвал");
		
		// Выводим общие реквизиты шапки
		СведенияОПокупателе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.ЮрФизЛицо, Шапка.ДатаДокумента);

		Если ЗначениеЗаполнено(Шапка.Контрагент) Тогда
			СведенияОПоставщике = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Контрагент, Шапка.ДатаДокумента);
			ОбластьМакетаШапка.Параметры.ПредставлениеПоставщика       = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПоставщике);
			ОбластьМакетаШапка.Параметры.ОписаниеПоставщика            = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
				СведенияОПоставщике, "НаименованиеДляПечатныхФорм,ЮридическийАдрес,Телефоны,");
			ОбластьМакетаШапка.Параметры.ПредставлениеГрузоотправителя = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
				СведенияОПоставщике, "НаименованиеДляПечатныхФорм,ФактическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет");
			ОбластьМакетаШапка.Параметры.ПоставщикПоОКПО               = СведенияОПоставщике.КодПоОКПО;
			ОбластьМакетаШапка.Параметры.ГрузоотправительПоОКПО        = СведенияОПоставщике.КодПоОКПО;
		КонецЕсли;

		ОбластьМакетаШапка.Параметры.Заполнить(Шапка);
		ОбластьМакетаШапка.Параметры.ДатаДокумента            = Формат(Шапка.ДатаДокумента, "ДЛФ=DD");
		ОбластьМакетаШапка.Параметры.НомерДокумента           = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер, Истина, Ложь);
		ОбластьМакетаШапка.Параметры.ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОПокупателе, "НаименованиеДляПечатныхФорм,ЮридическийАдрес,");
		ОбластьМакетаШапка.Параметры.ОрганизацияПоОКПО        = СведенияОПокупателе.КодПоОКПО;
		ОбластьМакетаШапка.Параметры.РеквизитыСопроводительногоДокумента = "сопроводительный документ № "
																		 + Шапка.НомерДокументаСопровождения 
																		 + " от " 
																		 + Формат(Шапка.ДатаДокументаСопровождения, "ДФ='дд ММММ гггг'");

		ПодразделениеОтветственныхЛиц = Шапка.ПодразделениеОрганизации;

		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Руководители, Шапка.ДатаДокумента, ПодразделениеОтветственныхЛиц);
		
		ОбластьМакетаШапка.Параметры.ФИОРуководителя       = Руководители.РуководительПредставление;
		ОбластьМакетаШапка.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжностьПредставление;

		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
		
		// Инициализация счетчика страниц
		НомерСтраницы = 1;
		
		// Инициализация счетчика строк
		НомерСтроки = 0;
		КоличествоСтрок = КоличествоСтрокДокумента.Получить(Шапка.Ссылка);
		
		// инициализация итогов по странице
		ИтогоМестПоСтранице        = 0;
		ИтогоШтукПоСтранице        = 0;
		ИтогоМассаБруттоПоСтранице = 0;
		ИтогоМассаНеттоПоСтранице  = 0;
		ИтогоСтоимостьПоСтранице   = 0;

		// инициализация итогов по документу
		ИтогоМест        = 0;
		ИтогоШтук        = 0;
		ИтогоМассаБрутто = 0;
		ИтогоМассаНетто  = 0;
		ИтогоСтоимость   = 0;
		Ном              = 0;

		// Выводим многострочную часть документа
		Для Каждого СтрокаТовар Из ТаблицаПоТоварам Цикл
			
			НомерСтроки = НомерСтроки + 1;
			
			ОбластьМакетаСтрока.Параметры.Заполнить(СтрокаТовар);
			
			КоличествоШтук = ?(ЗначениеЗаполнено(СтрокаТовар.КоличествоШтук), СтрокаТовар.КоличествоШтук, 0);
			
			ОбластьМакетаСтрока.Параметры.КоличествоШтук = КоличествоШтук;
			
			Если НЕ ЗначениеЗаполнено(СтрокаТовар.КоличествоМест) Тогда
				ОбластьМакетаСтрока.Параметры.КоличествоВОдномМесте = "";
			КонецЕсли;
			
			Кратность = ?(Шапка.Кратность = 0, 1, Шапка.Кратность);
			СуммаСНДС = СтрокаТовар.Сумма + ?(Шапка.СуммаВключаетНДС, 0, СтрокаТовар.СуммаНДС);
			ОбластьМакетаСтрока.Параметры.Цена      = ?(КоличествоШтук <> 0, Окр(СуммаСНДС / КоличествоШтук, 2), СуммаСНДС);
			ОбластьМакетаСтрока.Параметры.Стоимость = СуммаСНДС;
			ОбластьМакетаСтрока.Параметры.ТоварНаименование = СокрЛП(СтрокаТовар.ТоварНаименование);
		
			// Проверим вывод
			СтрокаСПодвалом = Новый Массив;
			Если НомерСтроки = 1 Тогда
				СтрокаСПодвалом.Добавить(ОбластьМакетаЗаголовокТаблицы); // если первая строка, то должен
			КонецЕсли;                                                   // помещаться заголовок
			СтрокаСПодвалом.Добавить(ОбластьМакетаСтрока);
			СтрокаСПодвалом.Добавить(ОбластьМакетаИтогоПоСтранице);
			Если НомерСтроки = КоличествоСтрок Тогда           // если последняя строка, должен
				СтрокаСПодвалом.Добавить(ОбластьМакетаВсего);  // помещаться и повал документа
				СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал);
			КонецЕсли;

			Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом) Тогда

				// Выводим итоги по странице
				Если НЕ (НомерСтроки = 1) Тогда
				
					ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоМестПоСтранице        = ИтогоМестПоСтранице;
					ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоШтукПоСтранице        = ИтогоШтукПоСтранице;
					ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице;
					ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоМассаНеттоПоСтранице  = ИтогоМассаНеттоПоСтранице;
					ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоСтоимостьПоСтранице   = ИтогоСтоимостьПоСтранице;
					
					ТабличныйДокумент.Вывести(ОбластьМакетаИтогоПоСтранице);
					
				КонецЕсли;
				
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();			
				
				// Очистим итоги по странице
				ИтогоМестПоСтранице        = 0;
				ИтогоШтукПоСтранице        = 0;
				ИтогоМассаБруттоПоСтранице = 0;
				ИтогоМассаНеттоПоСтранице  = 0;
				ИтогоСтоимостьПоСтранице   = 0;
				
				// Выведем заголовок таблицы
				НомерСтраницы = НомерСтраницы + 1;
				ОбластьМакетаЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
				
			ИначеЕсли НомерСтроки = 1 Тогда // первая срока, все помещается
				
				ОбластьМакетаЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
				ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
				
			КонецЕсли;

			ТабличныйДокумент.Вывести(ОбластьМакетаСтрока);

			// Обновим итоги по странице
			ИтогоМестПоСтранице        = ИтогоМестПоСтранице        + ?(ЗначениеЗаполнено(СтрокаТовар.КоличествоМест), СтрокаТовар.КоличествоМест, 0);
			ИтогоШтукПоСтранице        = ИтогоШтукПоСтранице        + КоличествоШтук;
			ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице + 0;
			ИтогоМассаНеттоПоСтранице  = ИтогоМассаНеттоПоСтранице  + 0;
			ИтогоСтоимостьПоСтранице   = ИтогоСтоимостьПоСтранице   + СуммаСНДС;

			// Обновим итогов по документу
			ИтогоМест        = ИтогоМест        + ?(ЗначениеЗаполнено(СтрокаТовар.КоличествоМест), СтрокаТовар.КоличествоМест, 0);
			ИтогоШтук        = ИтогоШтук        + КоличествоШтук;
			ИтогоМассаБрутто = ИтогоМассаБрутто + 0;
			ИтогоМассаНетто  = ИтогоМассаНетто  + 0;
			ИтогоСтоимость   = ИтогоСтоимость   + СуммаСНДС;

		КонецЦикла;

		// Выводим итоги по последней странице
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоМестПоСтранице        = ИтогоМестПоСтранице;
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоШтукПоСтранице        = ИтогоШтукПоСтранице;
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоМассаБруттоПоСтранице = ИтогоМассаБруттоПоСтранице;
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоМассаНеттоПоСтранице  = ИтогоМассаНеттоПоСтранице;
		ОбластьМакетаИтогоПоСтранице.Параметры.ИтогоСтоимостьПоСтранице   = ИтогоСтоимостьПоСтранице;
		ТабличныйДокумент.Вывести(ОбластьМакетаИтогоПоСтранице);

		// Выводим итоги по документу в общем
		ОбластьМакетаВсего.Параметры.ИтогоМест        = ИтогоМест;
		ОбластьМакетаВсего.Параметры.ИтогоШтук        = ИтогоШтук;
		ОбластьМакетаВсего.Параметры.ИтогоМассаБрутто = ИтогоМассаБрутто;
		ОбластьМакетаВсего.Параметры.ИтогоМассаНетто  = ИтогоМассаНетто;
		ОбластьМакетаВсего.Параметры.ИтогоСтоимость   = ИтогоСтоимость;
		ТабличныйДокумент.Вывести(ОбластьМакетаВсего);
		
		// Выводим подвал документа
		ОбластьМакетаПодвал.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьМакетаПодвал);

		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции // ПечатьТОРГ4()

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыМ4()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслуг.Номер КАК Номер,
	|	ДокументПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Проведен КАК Проведен,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ДокументПоступлениеТоваровУслуг.Дата КАК ДатаСоставления,
	|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента КАК НомерСопроводительногоДокумента,
	|	ДокументПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ДокументПоступлениеТоваровУслуг.Организация КАК ЮрФизЛицо,
	|	ВЫБОР 
	|		КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке 
	|			ТОГДА ПоступлениеТоваровУслуг.Склад
	|       ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ КАК МестоПриемки,
	|	ВЫБОР 
	|		КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке 
    |   		ТОГДА ПоступлениеТоваровУслуг.Склад.Представление
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад.Представление
	|	КОНЕЦ КАК СкладНаименование,
	|	ДокументПоступлениеТоваровУслуг.Контрагент.Код КАК ПоставщикКод,
	|	ДокументПоступлениеТоваровУслуг.Контрагент КАК Поставщик,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента КАК ВалютаДокумента,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное КАК ПредставлениеПодразделения,
	|	ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов КАК Курс,
	|	ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК Кратность,
	|	ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом КАК СубСчет,
	|	ПоступлениеТоваровУслуг.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ КАК ТоварКод,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ПоступлениеТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Цена,
	|	СУММА(ПоступлениеТоваровУслуг.Количество) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|				ТОГДА ПоступлениеТоваровУслуг.Сумма
	|			КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|				ТОГДА ПоступлениеТоваровУслуг.СуммаНДС
	|			КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаНДС * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) КАК СуммаНДС,
	|	МИНИМУМ(ПоступлениеТоваровУслуг.НомерСтроки) КАК НомерСтроки,
	|	2 КАК ID,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ) КАК ЭтоКомиссия,
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0)) КАК ВсегоРуб,
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)) КАК СуммаБезНДСРуб,
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0)) КАК НДСРуб
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслуг
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = ДокументПоступлениеТоваровУслуг.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|			И (ДокументПоступлениеТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	И НЕ ПоступлениеТоваровУслуг.НомерСтроки ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументПоступлениеТоваровУслуг.Номер,
	|	ДокументПоступлениеТоваровУслуг.Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Проведен,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ВЫБОР 
	|		КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке 
	|			ТОГДА ПоступлениеТоваровУслуг.Склад
	|       ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ,
    |	ВЫБОР 
	|		КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке 
    |   		ТОГДА ПоступлениеТоваровУслуг.Склад.Представление
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад.Представление
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Контрагент.Код,
	|	ДокументПоступлениеТоваровУслуг.Контрагент,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное,
	|	ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.ЕдиницаИзмерения,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслуг.Номер,
	|	ДокументПоступлениеТоваровУслуг.Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Проведен,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ВЫБОР 
	|   	КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Склад.Представление,
	|	ДокументПоступлениеТоваровУслуг.Контрагент.Код,
	|	ДокументПоступлениеТоваровУслуг.Контрагент,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное,
	|	ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Цена,
	|	СУММА(ПоступлениеТоваровУслуг.Количество),
	|	СУММА(ВЫБОР
	|			КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|				ТОГДА ПоступлениеТоваровУслуг.Сумма
	|			КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|				ТОГДА ПоступлениеТоваровУслуг.СуммаНДС
	|			КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаНДС * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|		КОНЕЦ),
	|	МИНИМУМ(ПоступлениеТоваровУслуг.НомерСтроки),
	|	1,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ),
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0)),
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)),
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0))
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслуг
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = ДокументПоступлениеТоваровУслуг.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Оборудование))
	|			И (ДокументПоступлениеТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	И НЕ ПоступлениеТоваровУслуг.НомерСтроки ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументПоступлениеТоваровУслуг.Номер,
	|	ДокументПоступлениеТоваровУслуг.Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Проведен,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ВЫБОР 
	|   	КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Склад.Представление,
	|	ДокументПоступлениеТоваровУслуг.Контрагент.Код,
	|	ДокументПоступлениеТоваровУслуг.Контрагент,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное,
	|	ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.ЕдиницаИзмерения,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.СчетУчета.Забалансовый, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслуг.Номер,
	|	ДокументПоступлениеТоваровУслуг.Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Проведен,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ВЫБОР 
	|   	КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Склад.Представление,
	|	ДокументПоступлениеТоваровУслуг.Контрагент.Код,
	|	ДокументПоступлениеТоваровУслуг.Контрагент,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное,
	|	ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Цена,
	|	СУММА(ПоступлениеТоваровУслуг.Количество),
	|	СУММА(ВЫБОР
	|			КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|				ТОГДА ПоступлениеТоваровУслуг.Сумма
	|			КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|				ТОГДА ПоступлениеТоваровУслуг.СуммаНДС
	|			КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаНДС * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|		КОНЕЦ),
	|	МИНИМУМ(ПоступлениеТоваровУслуг.НомерСтроки),
	|	3,
	|	ЛОЖЬ,
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0)),
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)),
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0))
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслуг
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = ДокументПоступлениеТоваровУслуг.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|			И (ДокументПоступлениеТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	И НЕ ПоступлениеТоваровУслуг.НомерСтроки ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументПоступлениеТоваровУслуг.Номер,
	|	ДокументПоступлениеТоваровУслуг.Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Проведен,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ВЫБОР 
	|   	КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Склад.Представление,
	|	ДокументПоступлениеТоваровУслуг.Контрагент.Код,
	|	ДокументПоступлениеТоваровУслуг.Контрагент,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное,
	|	ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслуг.Номер,
	|	ДокументПоступлениеТоваровУслуг.Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Проведен,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ВЫБОР 
	|   	КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Склад.Представление,
	|	ДокументПоступлениеТоваровУслуг.Контрагент.Код,
	|	ДокументПоступлениеТоваровУслуг.Контрагент,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное,
	|	ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Цена,
	|	СУММА(ПоступлениеТоваровУслуг.Количество),
	|	СУММА(ВЫБОР
	|			КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|				ТОГДА ПоступлениеТоваровУслуг.Сумма
	|			КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.Сумма * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|		КОНЕЦ),
	|	СУММА(ВЫБОР
	|			КОГДА ДокументПоступлениеТоваровУслуг.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|				ТОГДА ПоступлениеТоваровУслуг.СуммаНДС
	|			КОГДА ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов = 0
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслуг.СуммаНДС * ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|		КОНЕЦ),
	|	МИНИМУМ(ПоступлениеТоваровУслуг.НомерСтроки),
	|	4,
	|	ИСТИНА,
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0)),
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0)),
	|	СУММА(ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0))
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслуг
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = ДокументПоступлениеТоваровУслуг.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|			И (ДокументПоступлениеТоваровУслуг.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|	И НЕ ПоступлениеТоваровУслуг.НомерСтроки ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументПоступлениеТоваровУслуг.Номер,
	|	ДокументПоступлениеТоваровУслуг.Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Проведен,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ВЫБОР 
	|   	КОГДА ДокументПоступлениеТоваровУслуг.СкладВСписке
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Склад.Представление,
	|	ДокументПоступлениеТоваровУслуг.Контрагент.Код,
	|	ДокументПоступлениеТоваровУслуг.Контрагент,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ДокументПоступлениеТоваровУслуг.ВалютаДокумента,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное,
	|	ДокументПоступлениеТоваровУслуг.КурсВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.КратностьВзаиморасчетов,
	|	ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код,
	|	ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Код
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ДокументПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	ID,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ унифицированной формы М-4
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме М-4 (приходный ордер).
//
Функция ПечатьМ4(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент 						= Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_М4";
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_М4");

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов"  , МассивОбъектов);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыМ4();
	
	Шапка = Запрос.Выполнить().Выгрузить();
	
	ПервыйДокумент = Истина;
	
	ОписаниеТиповЧисло15_2 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	
	ТаблицаПоТоварам = Новый ТаблицаЗначений; 		
	ТаблицаПоТоварам.Колонки.Добавить("НомерСтроки");
	ТаблицаПоТоварам.Колонки.Добавить("Номенклатура");
	ТаблицаПоТоварам.Колонки.Добавить("ТоварНаименование");
	ТаблицаПоТоварам.Колонки.Добавить("ТоварКод");
	ТаблицаПоТоварам.Колонки.Добавить("Количество");
	ТаблицаПоТоварам.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ТаблицаПоТоварам.Колонки.Добавить("ЕдиницаИзмеренияКод");
	ТаблицаПоТоварам.Колонки.Добавить("СуммаВключаетНДС");
	ТаблицаПоТоварам.Колонки.Добавить("Цена", 						ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("Сумма", 						ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СтавкаНДС");
	ТаблицаПоТоварам.Колонки.Добавить("СуммаНДС", 					ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаНДСВВалютеДокумента", 	ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаБезНДС", 				ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаРублевая", 				ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("ID");
	ТаблицаПоТоварам.Колонки.Добавить("ЭтоКомиссия", 				Новый ОписаниеТипов("Булево"));
	ТаблицаПоТоварам.Колонки.Добавить("ВсегоРуб",                   ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("НДСРуб",                     ОписаниеТиповЧисло15_2);
	ТаблицаПоТоварам.Колонки.Добавить("СуммаБезНДСРуб",             ОписаниеТиповЧисло15_2);
	
	ТаблицаДокументов =  Шапка.Скопировать();
	ТаблицаДокументов.Свернуть("Ссылка");
	
	Для Каждого СтрокаТаблицы Из ТаблицаДокументов Цикл 
		СтрокаДокумента 		= Шапка.Найти(СтрокаТаблицы.Ссылка, "Ссылка");
		ОтборПоДокументу 		= Новый Структура("Ссылка", СтрокаТаблицы.Ссылка);
		ТаблицаСклады 			= Шапка.Скопировать(ОтборПоДокументу);
		ТаблицаСкладыДокумента 	= ТаблицаСклады.Скопировать();
		ТаблицаСкладыДокумента.Свернуть("МестоПриемки");
		КоличествоСкладовПоДокументу = ТаблицаСкладыДокумента.Количество();
		Для Каждого СтрокаСклад Из ТаблицаСкладыДокумента Цикл
			
			СуффиксНомера = ТаблицаСкладыДокумента.Индекс(СтрокаСклад) + 1;	
			
			Если НЕ ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент = Ложь;
			// Запомним номер строки, с которой начали выводить текущий документ.
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			ОтборПоСкладу = Новый Структура("МестоПриемки", СтрокаСклад.МестоПриемки);
			ТоварыПоСкладам = ТаблицаСклады.НайтиСтроки(ОтборПоСкладу);
			
			ПодготовитьТаблицуДокументаДляПечатиМ4(ТоварыПоСкладам, ТаблицаПоТоварам);

			ОбластьМакетаШапка              = Макет.ПолучитьОбласть("Шапка");
			ОбластьМакетаЗаголовокДокумента = Макет.ПолучитьОбласть("ЗаголовокДокумента");
			ОбластьМакетаЗаголовокТаблицы   = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
			ОбластьМакетаСтрока             = Макет.ПолучитьОбласть("Строка");
			ОбластьМакетаПодвалСтрок        = Макет.ПолучитьОбласть("ПодвалСтрок");
			ОбластьМакетаИтого              = Макет.ПолучитьОбласть("Итого");
			ОбластьМакетаПодвал             = Макет.ПолучитьОбласть("Подвал");
			
			// Выводим общие реквизиты шапки
			СведенияОПокупателе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(СтрокаДокумента.ЮрФизЛицо, СтрокаДокумента.ДатаСоставления);

			ОбластьМакетаШапка.Параметры.Заполнить(СтрокаДокумента);
			ОбластьМакетаШапка.Параметры.ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПокупателе);
			ОбластьМакетаШапка.Параметры.ОрганизацияПоОКПО        = СведенияОПокупателе.КодПоОКПО;
			ОбластьМакетаШапка.Параметры.НомерДокумента           = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаДокумента.Номер, Истина, Ложь) + ?(КоличествоСкладовПоДокументу > 1, "/" + СуффиксНомера, "");
			
			ТабличныйДокумент.Вывести(ОбластьМакетаШапка);

			// Выводим заголовок документа
			ОбластьМакетаЗаголовокДокумента.Параметры.Заполнить(СтрокаДокумента);
			ОбластьМакетаЗаголовокДокумента.Параметры.ДатаСоставления = СтрокаДокумента.ДатаСоставления;
			ПредставлениеКонтрагента = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
				БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(СтрокаДокумента.Поставщик, СтрокаДокумента.ДатаСоставления), "НаименованиеДляПечатныхФорм,"); 
			ОбластьМакетаЗаголовокДокумента.Параметры.ПоставщикНаименование = ПредставлениеКонтрагента;
			ОбластьМакетаЗаголовокДокумента.Параметры.СкладНаименование = ?(СтрокаДокумента.Ссылка.СкладВСписке, СтрокаСклад.МестоПриемки.Наименование, СтрокаДокумента.СкладНаименование);
			
			ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокДокумента);
			//
			// Выводим заголовок таблицы
			ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);

			// Инициализация итогов в документе
			ИтогоКоличествоПринято = 0;
			ИтогоСуммаБезНДС       = 0;
			ИтогоСуммаНДС          = 0;
			ИтогоВсегоСНДС         = 0;
			Ном                    = 0;

			// Инициализация счетчиков страниц и строк
			НомерСтраницы   = 1;
			НомерСтроки     = 0;
			КоличествоСтрок = ТаблицаПоТоварам.Количество();
			
			// Выводим многострочную часть документа
			Для Каждого ВыборкаСтрок Из ТаблицаПоТоварам Цикл

				НомерСтроки = НомерСтроки + 1;

				ОбластьМакетаСтрока.Параметры.Заполнить(ВыборкаСтрок);

				Кратность = ?(СтрокаДокумента.Кратность = 0, 1, СтрокаДокумента.Кратность);
				ВсегоСНДС = (?(ЗначениеЗаполнено(ВыборкаСтрок.Сумма), ВыборкаСтрок.Сумма, 0)
						  + ?(СтрокаДокумента.СуммаВключаетНДС, 0, ?(ЗначениеЗаполнено(ВыборкаСтрок.СуммаНДС), ВыборкаСтрок.СуммаНДС, 0)));

				КоличествоПринято = ?(ЗначениеЗаполнено(ВыборкаСтрок.Количество), ВыборкаСтрок.Количество, 0);
				СуммаНДС          = ?(ЗначениеЗаполнено(ВыборкаСтрок.СуммаНДС), ВыборкаСтрок.СуммаНДС, 0);
				СуммаБезНДС       = ВсегоСНДС - СуммаНДС;
				
				Если СтрокаДокумента.СуммаВключаетНДС И СуммаНДС <> 0 Тогда
					Цена = ?(КоличествоПринято = 0,
						СуммаБезНДС,
						СуммаБезНДС / КоличествоПринято);
						
				Иначе
						
					Цена = ВыборкаСтрок.Цена;
						
				КонецЕсли;

				ОбластьМакетаСтрока.Параметры.КоличествоПринято = КоличествоПринято;
				ОбластьМакетаСтрока.Параметры.ВсегоСНДС         = ВсегоСНДС;
				ОбластьМакетаСтрока.Параметры.СуммаБезНДС       = СуммаБезНДС;
				ОбластьМакетаСтрока.Параметры.СуммаНДС          = СуммаНДС;
				ОбластьМакетаСтрока.Параметры.Цена              = Цена;
				ОбластьМакетаСтрока.Параметры.ТоварНаименование = СокрЛП(ВыборкаСтрок.ТоварНаименование);

				// Проверим вывод
				СтрокаСПодвалом = Новый Массив;
				СтрокаСПодвалом.Добавить(ОбластьМакетаСтрока);
				Если НомерСтроки = КоличествоСтрок Тогда           // если последняя строка, должен
					СтрокаСПодвалом.Добавить(ОбластьМакетаИтого);  // помещаться и подвал документа
					СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал);
				Иначе                                              // иначе - только подвал строк
					СтрокаСПодвалом.Добавить(ОбластьМакетаПодвалСтрок);
				КонецЕсли;

				Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом) Тогда

					ТабличныйДокумент.Вывести(ОбластьМакетаПодвалСтрок);
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					
					
					НомерСтраницы = НомерСтраницы + 1;
					ОбластьМакетаЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
					
					ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);

				КонецЕсли;

				ТабличныйДокумент.Вывести(ОбластьМакетаСтрока);

				ИтогоКоличествоПринято = ИтогоКоличествоПринято + КоличествоПринято;
				ИтогоСуммаБезНДС       = ИтогоСуммаБезНДС       + СуммаБезНДС;
				ИтогоСуммаНДС          = ИтогоСуммаНДС          + СуммаНДС;
				ИтогоВсегоСНДС         = ИтогоВсегоСНДС         + ВсегоСНДС;

			КонецЦикла;

			// Выводим итоги по документу
			ОбластьМакетаИтого.Параметры.ИтогоКоличествоПринято = ИтогоКоличествоПринято;
			ОбластьМакетаИтого.Параметры.ИтогоСуммаБезНДС       = ИтогоСуммаБезНДС;
			ОбластьМакетаИтого.Параметры.ИтогоСуммаНДС          = ИтогоСуммаНДС;
			ОбластьМакетаИтого.Параметры.ИтогоВсегоСНДС         = ИтогоВсегоСНДС;
			ТабличныйДокумент.Вывести(ОбластьМакетаИтого);

			// Выводим итоги по документу
			ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
			ДанныеПодвал = Новый Структура;
			Если ЗначениеЗаполнено(СтрокаСклад.МестоПриемки) Тогда 
				МОЛ = ОтветственныеЛицаБП.ОтветственноеЛицоНаСкладе(СтрокаСклад.МестоПриемки, СтрокаДокумента.ДатаСоставления);
				ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(СтрокаДокумента.Организация, МОЛ, СтрокаДокумента.ДатаСоставления);				
				ДанныеПодвал.Вставить("ПринялДолжность", ДанныеФизЛица.Должность);
				ДанныеПодвал.Вставить("ПринялФИО",       ДанныеФизЛица.Представление);
			КонецЕсли;
			ОбластьМакетаПодвал.Параметры.Заполнить(СтрокаДокумента);
			ОбластьМакетаПодвал.Параметры.Заполнить(ДанныеПодвал);
			ТабличныйДокумент.Вывести(ОбластьМакетаПодвал);


			КонецЦикла;
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, СтрокаДокумента.Ссылка);

    КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции // ПечатьМ4()

// Формирует и возвращает текст запроса для выборки данных,
// необходимых для формирования печатной формы
Функция ПолучитьТекстЗапросаДляФормированияПечатнойФормыОС14()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслугОборудование.Дата КАК ДатаДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.Дата КАК Дата,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка КАК Ссылка,
	|	ДокументПоступлениеТоваровУслугОборудование.Проведен КАК Проведен,
	|	""0306006"" КАК КодПоОКУД,
	|	ДокументПоступлениеТоваровУслугОборудование.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслугОборудование.Номер КАК НомерДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.Организация КАК ОрганизацияПолучатель,
	|	ДокументПоступлениеТоваровУслугОборудование.Организация.КодПоОКПО КАК ОрганизацияПолучательКодПоОКПО,
	|	ДокументПоступлениеТоваровУслугОборудование.Склад КАК МестоПриема,
	|	ДокументПоступлениеТоваровУслугОборудование.Склад.Представление КАК МестоПриемаНаименование,
	|	ДокументПоступлениеТоваровУслугОборудование.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ДокументПоступлениеТоваровУслугОборудование.СуммаДокумента КАК СуммаДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент КАК ОрганизацияПоставщик,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент КАК ОрганизацияГрузоотправитель,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент.КодПоОКПО КАК ОрганизацияПоставщикКодПоОКПО,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.Представление, """") КАК Основание,
	|	ДокументПоступлениеТоваровУслугОборудование.НомерВходящегоДокумента КАК ОснованиеНомер,
	|	ДокументПоступлениеТоваровУслугОборудование.ДатаВходящегоДокумента КАК ОснованиеДата,
	|	ДокументПоступлениеТоваровУслугОборудование.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслугОборудование.ПодразделениеОрганизации.НаименованиеПолное КАК ПодразделениеНаименование,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.ВалютаВзаиморасчетов, &ВалютаРегламентированногоУчета) = &ВалютаРегламентированногоУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РасчетыВВалюте,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ПоступлениеТоваровУслугОборудование.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура КАК Оборудование,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура.НаименованиеПолное КАК ОборудованиеНаименование,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ПоступлениеТоваровУслугОборудование.Коэффициент,
	|	ПоступлениеТоваровУслугОборудование.СтавкаНДС,
	|	ПоступлениеТоваровУслугОборудование.Количество,
	|	ПоступлениеТоваровУслугОборудование.Количество КАК ФактическиПринято,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.Цена
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.Цена * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СтоимостьЗаЕдиницу,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.Сумма
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.Сумма * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.СуммаНДС
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.СуммаНДС * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаНДС,
	|	ПоступлениеТоваровУслугОборудование.Сумма КАК СуммаВВалютеДокумента,
	|	ПоступлениеТоваровУслугОборудование.СуммаНДС КАК СуммаНДСВВалютеДокумента,
	|	1 КАК ID,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугОборудование.СчетУчета.Забалансовый, ЛОЖЬ) КАК ЭтоКомиссия,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0) КАК СуммаБезНДСРуб,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСРуб,
	|	ДокументПоступлениеТоваровУслугОборудование.НДСВключенВСтоимость КАК НДСВключенВСтоимость,
	|	ПоступлениеТоваровУслугОборудование.СпособУчетаНДС КАК СпособУчетаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслугОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
	|		ПО (ПоступлениеТоваровУслугОборудование.Ссылка = ДокументПоступлениеТоваровУслугОборудование.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслугОборудование.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслугОборудование.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Оборудование))
	|			И (ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслугОборудование.Дата,
	|	ДокументПоступлениеТоваровУслугОборудование.Дата,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка,
	|	ДокументПоступлениеТоваровУслугОборудование.Проведен,
	|	""0306006"",
	|	ДокументПоступлениеТоваровУслугОборудование.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслугОборудование.Номер,
	|	ДокументПоступлениеТоваровУслугОборудование.Организация,
	|	ДокументПоступлениеТоваровУслугОборудование.Организация.КодПоОКПО,
	|	ДокументПоступлениеТоваровУслугОборудование.Склад,
	|	ДокументПоступлениеТоваровУслугОборудование.Склад.Представление,
	|	ДокументПоступлениеТоваровУслугОборудование.СуммаВключаетНДС,
	|	ДокументПоступлениеТоваровУслугОборудование.СуммаДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент.КодПоОКПО,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.Представление, """"),
	|	ДокументПоступлениеТоваровУслугОборудование.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.ДатаВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслугОборудование.ПодразделениеОрганизации.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.ВалютаВзаиморасчетов, &ВалютаРегламентированногоУчета) = &ВалютаРегламентированногоУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ПоступлениеТоваровУслугОборудование.НомерСтроки,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура.НаименованиеПолное,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПоступлениеТоваровУслугОборудование.Коэффициент,
	|	ПоступлениеТоваровУслугОборудование.СтавкаНДС,
	|	ПоступлениеТоваровУслугОборудование.Количество,
	|	ПоступлениеТоваровУслугОборудование.Количество,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.Цена
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.Цена * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.Сумма
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.Сумма * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.СуммаНДС
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.СуммаНДС * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслугОборудование.Сумма,
	|	ПоступлениеТоваровУслугОборудование.СуммаНДС,
	|	2,
	|	ЕСТЬNULL(ПоступлениеТоваровУслугОборудование.СчетУчета.Забалансовый, ЛОЖЬ),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ДокументПоступлениеТоваровУслугОборудование.НДСВключенВСтоимость,
	|	ПоступлениеТоваровУслугОборудование.СпособУчетаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслугОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугОборудование
	|		ПО (ПоступлениеТоваровУслугОборудование.Ссылка = ДокументПоступлениеТоваровУслугОборудование.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслугОборудование.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслугОборудование.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|			И (ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслугОборудование.Дата,
	|	ДокументПоступлениеТоваровУслугОборудование.Дата,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка,
	|	ДокументПоступлениеТоваровУслугОборудование.Проведен,
	|	""0306006"",
	|	ДокументПоступлениеТоваровУслугОборудование.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслугОборудование.Номер,
	|	ДокументПоступлениеТоваровУслугОборудование.Организация,
	|	ДокументПоступлениеТоваровУслугОборудование.Организация.КодПоОКПО,
	|	ДокументПоступлениеТоваровУслугОборудование.Склад,
	|	ДокументПоступлениеТоваровУслугОборудование.Склад.Представление,
	|	ДокументПоступлениеТоваровУслугОборудование.СуммаВключаетНДС,
	|	ДокументПоступлениеТоваровУслугОборудование.СуммаДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент.КодПоОКПО,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.Представление, """"),
	|	ДокументПоступлениеТоваровУслугОборудование.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.ДатаВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслугОборудование.ПодразделениеОрганизации.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.ВалютаВзаиморасчетов, &ВалютаРегламентированногоУчета) = &ВалютаРегламентированногоУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ПоступлениеТоваровУслугОборудование.НомерСтроки,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура.НаименованиеПолное,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	1,
	|	ПоступлениеТоваровУслугОборудование.СтавкаНДС,
	|	ПоступлениеТоваровУслугОборудование.Количество,
	|	ПоступлениеТоваровУслугОборудование.Количество,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.Цена
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.Цена * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.Сумма
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.Сумма * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.СуммаНДС
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.СуммаНДС * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслугОборудование.Сумма,
	|	ПоступлениеТоваровУслугОборудование.СуммаНДС,
	|	3,
	|	ЛОЖЬ,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ДокументПоступлениеТоваровУслугОборудование.НДСВключенВСтоимость,
	|	ПоступлениеТоваровУслугОборудование.СпособУчетаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслугОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугОборудование
	|		ПО (ПоступлениеТоваровУслугОборудование.Ссылка = ДокументПоступлениеТоваровУслугОборудование.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслугОборудование.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслугОборудование.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|			И (ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслугОборудование.Дата,
	|	ДокументПоступлениеТоваровУслугОборудование.Дата,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка,
	|	ДокументПоступлениеТоваровУслугОборудование.Проведен,
	|	""0306006"",
	|	ДокументПоступлениеТоваровУслугОборудование.СчетУчетаРасчетовСКонтрагентом,
	|	ДокументПоступлениеТоваровУслугОборудование.Номер,
	|	ДокументПоступлениеТоваровУслугОборудование.Организация,
	|	ДокументПоступлениеТоваровУслугОборудование.Организация.КодПоОКПО,
	|	ДокументПоступлениеТоваровУслугОборудование.Склад,
	|	ДокументПоступлениеТоваровУслугОборудование.Склад.Представление,
	|	ДокументПоступлениеТоваровУслугОборудование.СуммаВключаетНДС,
	|	ДокументПоступлениеТоваровУслугОборудование.СуммаДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент,
	|	ДокументПоступлениеТоваровУслугОборудование.Контрагент.КодПоОКПО,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.Представление, """"),
	|	ДокументПоступлениеТоваровУслугОборудование.НомерВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.ДатаВходящегоДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.ПодразделениеОрганизации,
	|	ДокументПоступлениеТоваровУслугОборудование.ПодразделениеОрганизации.НаименованиеПолное,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.ВалютаВзаиморасчетов, &ВалютаРегламентированногоУчета) = &ВалютаРегламентированногоУчета
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка.ВалютаДокумента,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслугОборудование.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ),
	|	ПоступлениеТоваровУслугОборудование.НомерСтроки,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура.НаименованиеПолное,
	|	ПоступлениеТоваровУслугОборудование.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	1,
	|	ПоступлениеТоваровУслугОборудование.СтавкаНДС,
	|	ПоступлениеТоваровУслугОборудование.Количество,
	|	ПоступлениеТоваровУслугОборудование.Количество,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.Цена
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.Цена * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.Сумма
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.Сумма * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента = &ВалютаРегламентированногоУчета
	|			ТОГДА ПоступлениеТоваровУслугОборудование.СуммаНДС
	|		КОГДА ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПоступлениеТоваровУслугОборудование.СуммаНДС * ДокументПоступлениеТоваровУслугОборудование.КурсВзаиморасчетов / ДокументПоступлениеТоваровУслугОборудование.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2))
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслугОборудование.Сумма,
	|	ПоступлениеТоваровУслугОборудование.СуммаНДС,
	|	4,
	|	ИСТИНА,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ДокументПоступлениеТоваровУслугОборудование.НДСВключенВСтоимость,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.ПустаяСсылка)
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслугОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслугОборудование
	|		ПО (ПоступлениеТоваровУслугОборудование.Ссылка = ДокументПоступлениеТоваровУслугОборудование.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслугОборудование.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслугОборудование.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|			И (ДокументПоступлениеТоваровУслугОборудование.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	ДокументПоступлениеТоваровУслугОборудование.Ссылка,
	|	ID,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует табличный документ унифицированной формы ОС-14
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме ОС-14 (приходный ордер).
//
Функция ПечатьОС14(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	Перем ПодразделениеОтветственныхЛиц;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.КлючПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_ОС14";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваровУслуг.ПФ_MXL_ОС14");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияПечатнойФормыОС14();
	
	ВыборкаПоШапке = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	ОписаниеТиповЧисло15_2 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	ОписаниеТиповЧисло15_3 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3);
	
	ТаблицаПоОборудованию = Новый ТаблицаЗначений;
	ТаблицаПоОборудованию.Колонки.Добавить("НомерСтроки");
	ТаблицаПоОборудованию.Колонки.Добавить("Оборудование");
	ТаблицаПоОборудованию.Колонки.Добавить("ОборудованиеНаименование");
	ТаблицаПоОборудованию.Колонки.Добавить("Цена",                        ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("Количество",                  ОписаниеТиповЧисло15_3);
	ТаблицаПоОборудованию.Колонки.Добавить("ФактическиПринято",           ОписаниеТиповЧисло15_3);
	ТаблицаПоОборудованию.Колонки.Добавить("ЕдиницаИзмеренияНаименование");
	ТаблицаПоОборудованию.Колонки.Добавить("Коэффициент");
	ТаблицаПоОборудованию.Колонки.Добавить("СтавкаНДС");
	ТаблицаПоОборудованию.Колонки.Добавить("СуммаВключаетНДС");
	ТаблицаПоОборудованию.Колонки.Добавить("Сумма",                       ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("СуммаНДС",                    ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("СуммаБезНДС",                 ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("СуммаВВалютеДокумента",       ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("СуммаНДСВВалютеДокумента",    ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("СуммаБезНДСВВалютеДокумента", ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("СуммаРублевая",               ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("ID");
	ТаблицаПоОборудованию.Колонки.Добавить("ЭтоКомиссия",                 Новый ОписаниеТипов("Булево"));
	ТаблицаПоОборудованию.Колонки.Добавить("ВсегоРуб",                    ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("НДСРуб",                      ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("СуммаБезНДСРуб",              ОписаниеТиповЧисло15_2);
	ТаблицаПоОборудованию.Колонки.Добавить("НДСВключенВСтоимость",        Новый ОписаниеТипов("Булево"));
	ТаблицаПоОборудованию.Колонки.Добавить("СпособУчетаНДС",              Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС"));
	
	МассивIDТабличныхЧастей = Новый Массив;
	МассивIDТабличныхЧастей.Добавить(1); // Оборудование
	
	Пока ВыборкаПоШапке.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ТаблицаПоОборудованию = ПодготовитьТаблицуДокументаДляПечати(ВыборкаПоШапке, ТаблицаПоОборудованию, МассивIDТабличныхЧастей);

		РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(
			ВыборкаПоШапке.ОрганизацияПолучатель, ВыборкаПоШапке.Дата);
		
		Шапка         = Макет.ПолучитьОбласть("Шапка");
		ШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
		СтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
		Подвал        = Макет.ПолучитьОбласть("Подвал");
		
		СведенияОПолучателе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			ВыборкаПоШапке.ОрганизацияПолучатель, ВыборкаПоШапке.ДатаДокумента);
		ОрганизацияПолучательНаименование = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОПолучателе, "НаименованиеДляПечатныхФорм,");
		ОрганизацияПолучательТелефоны = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОПолучателе, "Телефоны,");
		ОрганизацияПолучательБанковскиеРеквизиты = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОПолучателе, "НомерСчета,Банк,БИК,КоррСчет,");
		
		СведенияОПоставщике = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			ВыборкаПоШапке.ОрганизацияПоставщик, ВыборкаПоШапке.ДатаДокумента);
		ОрганизацияПоставщикНаименование = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОПоставщике, "ПолноеНаименование,");
		ОрганизацияПоставщикТелефоны = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОПоставщике, "Телефоны,");
		ОрганизацияПоставщикБанковскиеРеквизиты = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОПоставщике, "НомерСчета,Банк,БИК,КоррСчет,");
		
		СведенияОГрузоотправителе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			ВыборкаПоШапке.ОрганизацияГрузоотправитель, ВыборкаПоШапке.ДатаДокумента);
		ОрганизацияГрузоотправительНаименование = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОГрузоотправителе, "НаименованиеДляПечатныхФорм,");
		ОрганизацияГрузоотправительБанковскиеРеквизиты = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(
			СведенияОГрузоотправителе, "Телефоны,НомерСчета,Банк,БИК,КоррСчет,");
		
		Шапка.Параметры.Заполнить(ВыборкаПоШапке);
		Шапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоШапке.НомерДокумента, Истина, Ложь);
		
		Шапка.Параметры.ОрганизацияПолучательНаименование               = ОрганизацияПолучательНаименование;
		Шапка.Параметры.ОрганизацияПолучательТелефоны                   = ОрганизацияПолучательТелефоны;
		Шапка.Параметры.ОрганизацияПолучательБанковскиеРеквизиты        = ОрганизацияПолучательБанковскиеРеквизиты;
		Шапка.Параметры.ОрганизацияПоставщикНаименование                = ОрганизацияПоставщикНаименование;
		Шапка.Параметры.ОрганизацияПоставщикТелефоны                    = ОрганизацияПоставщикТелефоны;
		Шапка.Параметры.ОрганизацияПоставщикБанковскиеРеквизиты         = ОрганизацияПоставщикБанковскиеРеквизиты;
		Шапка.Параметры.ОрганизацияГрузоотправительНаименование         = ОрганизацияГрузоотправительНаименование;
		Шапка.Параметры.ОрганизацияГрузоотправительБанковскиеРеквизиты  = ОрганизацияГрузоотправительБанковскиеРеквизиты;

		ПодразделениеОтветственныхЛиц = ВыборкаПоШапке.ПодразделениеОрганизации;
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(
			ВыборкаПоШапке.ОрганизацияПолучатель, ВыборкаПоШапке.ДатаДокумента, ПодразделениеОтветственныхЛиц);

		Шапка.Параметры.РуководительДолжность    = ОтветственныеЛица.РуководительДолжностьПредставление;
		Шапка.Параметры.РуководительНаименование = ОтветственныеЛица.РуководительПредставление;
		
		ТабличныйДокумент.Вывести(Шапка);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ТабличныйДокумент.Вывести(ШапкаТаблицы);
		
		Для Каждого СтрокаОборудование Из ТаблицаПоОборудованию Цикл

			СтрокаТаблицы.Параметры.Заполнить(СтрокаОборудование);
			СтрокаТаблицы.Параметры.ОборудованиеНаименование = СокрЛП(СтрокаОборудование.ОборудованиеНаименование);
			
			Если Не РаздельныйУчетНДСНаСчете19 Тогда
				НДСВключенВСтоимость = СтрокаОборудование.НДСВключенВСтоимость;
			Иначе
				НДСВключенВСтоимость = СтрокаОборудование.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.УчитываетсяВCтоимости;
			КонецЕсли;
			
			Если ВыборкаПоШапке.СуммаВключаетНДС И Не НДСВключенВСтоимость Тогда
				СтрокаТаблицы.Параметры.СтоимостьВсего = СтрокаОборудование.Сумма - СтрокаОборудование.СуммаНДС;
				СтрокаТаблицы.Параметры.СтоимостьЗаЕдиницу = ?(СтрокаОборудование.Количество > 0,
					(СтрокаОборудование.Сумма - СтрокаОборудование.СуммаНДС) / СтрокаОборудование.Количество,
					СтрокаОборудование.Сумма - СтрокаОборудование.СуммаНДС);
			ИначеЕсли Не ВыборкаПоШапке.СуммаВключаетНДС И НДСВключенВСтоимость Тогда
				СтрокаТаблицы.Параметры.СтоимостьВсего = СтрокаОборудование.Сумма + СтрокаОборудование.СуммаНДС;
				СтрокаТаблицы.Параметры.СтоимостьЗаЕдиницу = ?(СтрокаОборудование.Количество > 0,
					(СтрокаОборудование.Сумма + СтрокаОборудование.СуммаНДС) / СтрокаОборудование.Количество,
					СтрокаОборудование.Сумма + СтрокаОборудование.СуммаНДС);
			Иначе
				СтрокаТаблицы.Параметры.СтоимостьВсего = СтрокаОборудование.Сумма;
				СтрокаТаблицы.Параметры.СтоимостьЗаЕдиницу = ?(СтрокаОборудование.Количество > 0,
					СтрокаОборудование.Сумма / СтрокаОборудование.Количество, СтрокаОборудование.Сумма);
			КонецЕсли;
			ТабличныйДокумент.Вывести(СтрокаТаблицы);
			
		КонецЦикла;
		
		Подвал.Параметры.ГлавныйБухгалтерНаименование = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		
		ТабличныйДокумент.Вывести(Подвал);

		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоШапке.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции 

Функция ПодготовитьТекстЗапросаДляПечатиСправкиРасчетаРублевыеСуммыДокументовВВалюте(НомераТаблиц) Экспорт
	
	НомераТаблиц.Вставить("ВТ_ТаблицаПоШапкеДокумента",                                НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаРеквизитов",                                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РегистрСведенийРублевыеСуммыДокументовВВалюте",             НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ПоДокументамЗачетнныхАвансов",                           НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПредоплат",                                          НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_ВТ_ПоДокументамЗачетнныхАвансов",               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_ВТ_ТаблицаПоШапкеДокумента",                    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаСумм",                                               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_РегистрСведенийРублевыеСуммыДокументовВВалюте", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеОбрабатываемогоДокумента.Ссылка КАК Ссылка,
	|	ДанныеОбрабатываемогоДокумента.Дата КАК Дата,
	|	ДанныеОбрабатываемогоДокумента.Проведен КАК Проведен,
	|	ДанныеОбрабатываемогоДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ДанныеОбрабатываемогоДокумента.Организация КАК Организация,
	|	ДанныеОбрабатываемогоДокумента.Контрагент КАК Контрагент,
	|	ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДанныеОбрабатываемогоДокумента.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ДанныеОбрабатываемогоДокумента.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	ДанныеОбрабатываемогоДокумента.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ДанныеОбрабатываемогоДокумента.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	"""" КАК НомерВходящегоДокумента,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах
	|			И НЕ ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.ЭлектронныеУслуги, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ЕСТЬNULL(ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента.УчетАгентскогоНДС, ЛОЖЬ) КАК УчетАгентскогоНДС,
	|	ДанныеОбрабатываемогоДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС
	|ПОМЕСТИТЬ ВТ_ТаблицаПоШапкеДокумента
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДанныеОбрабатываемогоДокумента
	|ГДЕ
	|	ДанныеОбрабатываемогоДокумента.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаПоШапкеДокумента.Ссылка КАК Ссылка,
	|	ВТ_ТаблицаПоШапкеДокумента.Дата КАК Дата,
	|	ВТ_ТаблицаПоШапкеДокумента.Проведен КАК Проведен,
	|	ВТ_ТаблицаПоШапкеДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаПоШапкеДокумента.Контрагент КАК Контрагент,
	|	ВТ_ТаблицаПоШапкеДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ТаблицаПоШапкеДокумента.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ВТ_ТаблицаПоШапкеДокумента.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	ВТ_ТаблицаПоШапкеДокумента.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ВТ_ТаблицаПоШапкеДокумента.УчетАгентскогоНДС КАК УчетАгентскогоНДС,
	|	ВТ_ТаблицаПоШапкеДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	0 КАК ВсегоВал,
	|	0 КАК НДСВал
	|ИЗ
	|	ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РублевыеСуммыДокументовВВалюте.Всего КАК Всего,
	|	РублевыеСуммыДокументовВВалюте.НДС КАК НДС,
	|	РублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РублевыеСуммыДокументовВВалюте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|		ПО РублевыеСуммыДокументовВВалюте.Регистратор = ВТ_ТаблицаПоШапкеДокумента.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ТабличнаяЧастьДокумента,
	|	НомерСтрокиДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Регистратор КАК Ссылка,
	|	Хозрасчетный.Сумма КАК СуммаПредоплатыРуб,
	|	Хозрасчетный.ВалютнаяСуммаДт КАК СуммаПредоплатыВал,
	|	ХозрасчетныйСубконто.Значение КАК Документ,
	|	ПРЕДСТАВЛЕНИЕ(ХозрасчетныйСубконто.Значение) КАК ДокументПредоплатыПредставление,
	|	ВТ_ТаблицаПоШапкеДокумента.Организация КАК Организация
	|ПОМЕСТИТЬ ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|		ПО Хозрасчетный.Регистратор = ВТ_ТаблицаПоШапкеДокумента.Ссылка
	|			И Хозрасчетный.СчетКт = ВТ_ТаблицаПоШапкеДокумента.СчетУчетаРасчетовПоАвансам
	|			И Хозрасчетный.СчетДт = ВТ_ТаблицаПоШапкеДокумента.СчетУчетаРасчетовСКонтрагентом
	|			И (ВТ_ТаблицаПоШапкеДокумента.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор
	|			И Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит))
	|			И (ХозрасчетныйСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Ссылка КАК Ссылка,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.СуммаПредоплатыРуб КАК СуммаПредоплатыРуб,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.СуммаПредоплатыВал КАК СуммаПредоплатыВал,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Документ КАК Документ,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.ДокументПредоплатыПредставление КАК ДокументПредоплатыПредставление,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерВходящегоДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВходящегоДокумента
	|ИЗ
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента КАК ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Организация = ДанныеПервичныхДокументов.Организация
	|			И ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Документ = ДанныеПервичныхДокументов.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Ссылка
	|ИТОГИ
	|	СУММА(СуммаПредоплатыРуб),
	|	СУММА(СуммаПредоплатыВал)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаПоШапкеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка КАК Ссылка,
	|	2 КАК ПорядокТабличныхЧастей,
	|	ОбрабатываемаяТаблица.НомерСтроки КАК НомерСтроки,
	|	ОбрабатываемаяТаблица.Номенклатура КАК Товар,
	|	ОбрабатываемаяТаблица.Номенклатура.Наименование КАК ТоварНаименование,
	|	ОбрабатываемаяТаблица.Сумма КАК ВсегоВал,
	|	ОбрабатываемаяТаблица.СуммаНДС КАК НДСВал,
	|	ОбрабатываемаяТаблица.СтавкаНДС КАК СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего КАК ВсегоРуб,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС КАК НДСРуб,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС КАК НалоговаяБазаНДСРуб
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка,
	|	1,
	|	ОбрабатываемаяТаблица.НомерСтроки,
	|	ОбрабатываемаяТаблица.Номенклатура,
	|	ОбрабатываемаяТаблица.Номенклатура.Наименование,
	|	ОбрабатываемаяТаблица.Сумма,
	|	ОбрабатываемаяТаблица.СуммаНДС,
	|	ОбрабатываемаяТаблица.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка,
	|	3,
	|	ОбрабатываемаяТаблица.НомерСтроки,
	|	ОбрабатываемаяТаблица.Номенклатура,
	|	ОбрабатываемаяТаблица.Номенклатура.Наименование,
	|	ОбрабатываемаяТаблица.Сумма,
	|	ОбрабатываемаяТаблица.СуммаНДС,
	|	ОбрабатываемаяТаблица.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка,
	|	1,
	|	ОбрабатываемаяТаблица.НомерСтроки,
	|	ОбрабатываемаяТаблица.Номенклатура,
	|	ОбрабатываемаяТаблица.Номенклатура.Наименование,
	|	ОбрабатываемаяТаблица.Сумма,
	|	ОбрабатываемаяТаблица.СуммаНДС,
	|	ОбрабатываемаяТаблица.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Оборудование))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка,
	|	1,
	|	ОбрабатываемаяТаблица.НомерСтроки,
	|	ОбрабатываемаяТаблица.ОбъектСтроительства,
	|	ОбрабатываемаяТаблица.ОбъектСтроительства.Наименование,
	|	ОбрабатываемаяТаблица.Сумма,
	|	ОбрабатываемаяТаблица.СуммаНДС,
	|	ОбрабатываемаяТаблица.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ОбъектыСтроительства))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка,
	|	1,
	|	ОбрабатываемаяТаблица.НомерСтроки,
	|	ОбрабатываемаяТаблица.ОсновноеСредство,
	|	ОбрабатываемаяТаблица.ОсновноеСредство.Наименование,
	|	ОбрабатываемаяТаблица.Сумма,
	|	ОбрабатываемаяТаблица.СуммаНДС,
	|	ОбрабатываемаяТаблица.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.ОсновныеСредства))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОбрабатываемаяТаблица.Ссылка,
	|	1,
	|	ОбрабатываемаяТаблица.НомерСтроки,
	|	ОбрабатываемаяТаблица.ОбъектПрава,
	|	ОбрабатываемаяТаблица.ОбъектПрава.Наименование,
	|	ОбрабатываемаяТаблица.Сумма,
	|	ОбрабатываемаяТаблица.СуммаНДС,
	|	ОбрабатываемаяТаблица.СтавкаНДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Права КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО ОбрабатываемаяТаблица.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор
	|			И ОбрабатываемаяТаблица.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Права))
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ПорядокТабличныхЧастей,
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(ВсегоВал),
	|	СУММА(НДСВал),
	|	СУММА(ВсегоРуб),
	|	СУММА(НДСРуб),
	|	СУММА(НалоговаяБазаНДСРуб)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте";
	
	Возврат ТекстЗапроса;
	
КонецФункции	

Функция ПолучитьДополнительныеРеквизитыДляРеестра() Экспорт
	
	Результат = Новый Структура("Информация", "Контрагент");
	
	ПолеТовары = "Таб.Товары.Ссылка";
	Результат.Вставить("ЕстьТовары",          ПолеТовары);
	Результат.Вставить("ЕстьОборудование",    СтрЗаменить(ПолеТовары, "Товары", "Оборудование"));
	Результат.Вставить("ЕстьУслуги",          СтрЗаменить(ПолеТовары, "Товары", "Услуги"));
	Результат.Вставить("ЕстьАгентскиеУслуги", СтрЗаменить(ПолеТовары, "Товары", "АгентскиеУслуги"));
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТекстЗапросаДляФормированияТаблицыСведенийАктаОбОказанииУслуг()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС,
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.НалоговаяБазаНДС
	|ПОМЕСТИТЬ РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтрокиДокумента,
	|	ТабличнаяЧастьДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ПоступлениеТоваровУслуг.Дата КАК ДатаДокумента
	|ПОМЕСТИТЬ ДокументыДляПечати
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	НЕ ПоступлениеТоваровУслуг.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.Товары))
	|	И ПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов) И НЕ ПоступлениеТоваровУслуг.ЕстьРасхождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Документ,
	|	ПоступлениеТоваровУслуг.Дата КАК ДатаДляПолученияСведений,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	ПоступлениеТоваровУслуг.Контрагент КАК Поставщик,
	|	ПРЕДСТАВЛЕНИЕ(ПоступлениеТоваровУслуг.ДоговорКонтрагента) КАК ПредставлениеДоговора,
	|	ПоступлениеТоваровУслуг.Организация КАК Получатель,
	|	ПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ПоступлениеТоваровУслуг.ПодразделениеОрганизации КАК Подразделение,
	|	"""" КАК БанковскийСчетПродавца,
	|	ПоступлениеТоваровУслуг.ВалютаДокумента КАК Валюта,
	|	ПоступлениеТоваровУслуг.КурсВзаиморасчетов КАК Курс,
	|	ПоступлениеТоваровУслуг.КратностьВзаиморасчетов КАК Кратность,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.ВалютаДокумента.Код, """") КАК ВалютаКод,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.ВалютаДокумента.Наименование, """") КАК ВалютаНаименование,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	ПоступлениеТоваровУслуг.СуммаВключаетНДС,
	|	"""" КАК Руководитель,
	|	"""" КАК ЗаРуководителяНаОсновании
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПоступлениеТоваровУслуг.Организация = ДанныеПервичныхДокументов.Организация
	|			И ПоступлениеТоваровУслуг.Ссылка = ДанныеПервичныхДокументов.Документ
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыДляПечати.Ссылка
	|			ИЗ
	|				ДокументыДляПечати)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Документ,
	|	ДокументыДляПечати.ДатаДокумента КАК ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги) КАК ТабличнаяЧасть,
	|	1 КАК ПорядокТабличнойЧасти,
	|	ПоступлениеТоваровУслуг.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ КАК НоменклатураКод,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """") КАК ЕдиницаИзмеренияКод,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.Цена КАК Цена,
	|	ПоступлениеТоваровУслуг.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма - ПоступлениеТоваровУслуг.СуммаНДС
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ПоступлениеТоваровУслуг.СуммаНДС КАК СуммаНДС,
	|	ПоступлениеТоваровУслуг.СтавкаНДС КАК СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0) КАК ВсегоВРублях,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0) КАК НДСВРублях,
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СуществуютЗаписиРублевыхСумм
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Услуги))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ДокументыДляПечати.ДатаДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги),
	|	2,
	|	ПоступлениеТоваровУслуг.НомерСтроки,
	|	ПоступлениеТоваровУслуг.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Содержание ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Содержание
	|		КОГДА НЕ ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.НаименованиеПолное
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Номенклатура.Наименование
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Артикул
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.Код)
	|			ТОГДА ПоступлениеТоваровУслуг.Номенклатура.Код
	|		КОГДА &ДополнительнаяКолонкаПечатныхФормДокументов = ЗНАЧЕНИЕ(Перечисление.ДополнительнаяКолонкаПечатныхФормДокументов.НеВыводить)
	|			ТОГДА """"
	|	КОНЕЦ,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Код, """"),
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.Номенклатура.ЕдиницаИзмерения.Наименование, """"),
	|	ПоступлениеТоваровУслуг.Количество,
	|	ПоступлениеТоваровУслуг.Цена,
	|	ПоступлениеТоваровУслуг.Сумма,
	|	ВЫБОР
	|		КОГДА ДокументыДляПечати.СуммаВключаетНДС
	|			ТОГДА ПоступлениеТоваровУслуг.Сумма - ПоступлениеТоваровУслуг.СуммаНДС
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Сумма
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.СуммаНДС,
	|	ПоступлениеТоваровУслуг.СтавкаНДС,
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.Всего, 0),
	|	ЕСТЬNULL(РегистрСведенийРублевыеСуммыДокументовВВалюте.НДС, 0),
	|	ВЫБОР
	|		КОГДА РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	ДокументыДляПечати КАК ДокументыДляПечати
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслуг
	|		ПО ДокументыДляПечати.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведенийРублевыеСуммыДокументовВВалюте КАК РегистрСведенийРублевыеСуммыДокументовВВалюте
	|		ПО (ПоступлениеТоваровУслуг.Ссылка = РегистрСведенийРублевыеСуммыДокументовВВалюте.Регистратор)
	|			И (ПоступлениеТоваровУслуг.НомерСтроки = РегистрСведенийРублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента)
	|			И (РегистрСведенийРублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.АгентскиеУслуги))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Документ,
	|	ПорядокТабличнойЧасти,
	|	НомерСтроки";
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТаблицуСведенийАктаОбОказанииУслуг(Знач МассивОбъектов) Экспорт
	
	ТаблицаСведений = ПечатьТорговыхДокументов.ПолучитьОписаниеАктаОбОказанииУслуг();
	
	Запрос = Новый Запрос();
	Запрос.Текст = ПолучитьТекстЗапросаДляФормированияТаблицыСведенийАктаОбОказанииУслуг();
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ДополнительнаяКолонкаПечатныхФормДокументов", Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить());

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатыЗапроса[2].Выбрать();
	СтрокиДокументов = РезультатыЗапроса[3].Выгрузить();
	СтрокиДокументов.Индексы.Добавить("Документ");
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	РеквизитыВалютыРегУчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВалютаРегУчета, "Код, Наименование");
	
	Пока Выборка.Следующий() Цикл
		
		СведенияОДокументе = ТаблицаСведений.Добавить();
		ЗаполнитьЗначенияСвойств(СведенияОДокументе, Выборка);
		
		Если Выборка.РасчетыВУсловныхЕдиницах Тогда
			СведенияОДокументе.Валюта             = ВалютаРегУчета;
			СведенияОДокументе.ВалютаКод          = РеквизитыВалютыРегУчета.Код;
			СведенияОДокументе.ВалютаНаименование = РеквизитыВалютыРегУчета.Наименование;
		КонецЕсли;
		
		ТаблицаДокумента = ПечатьТорговыхДокументов.ПолучитьОписаниеТаблицыАктаОбОказанииУслуг();
		
		Отбор = Новый Структура("Документ", Выборка.Документ);
		СтрокиДокумента = СтрокиДокументов.НайтиСтроки(Отбор);
		
		НуженПересчетВРубли = Ложь;
		Если Выборка.Валюта <> ВалютаРегУчета
			И Выборка.РасчетыВУсловныхЕдиницах Тогда
			НуженПересчетВРубли = Истина;
		КонецЕсли;
		
		Для Каждого Строка Из СтрокиДокумента Цикл
			
			СтрокаТаблицыДокумента = ТаблицаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыДокумента, Строка);
			
			Если НуженПересчетВРубли Тогда
				Если Строка.СуществуютЗаписиРублевыхСумм Тогда
					СтрокаТаблицыДокумента.СуммаНДС = Строка.НДСВРублях;
					СтрокаТаблицыДокумента.СуммаБезНДС = Строка.ВсегоВРублях - Строка.НДСВРублях;
				Иначе
					Кратность = ?(Выборка.Кратность = 0, 1, Выборка.Кратность);
					СтрокаТаблицыДокумента.СуммаНДС = Строка.СуммаНДС * Выборка.Курс / Кратность;;
					СтрокаТаблицыДокумента.СуммаБезНДС = (Строка.СуммаБезНДС + Строка.СуммаНДС) * Выборка.Курс / Кратность - СтрокаТаблицыДокумента.СуммаНДС;
				КонецЕсли;
				СтрокаТаблицыДокумента.Сумма = СтрокаТаблицыДокумента.СуммаБезНДС + ?(Выборка.СуммаВключаетНДС, СтрокаТаблицыДокумента.СуммаНДС, 0);
				СтрокаТаблицыДокумента.Цена  = ?(СтрокаТаблицыДокумента.Количество=0, СтрокаТаблицыДокумента.Сумма, СтрокаТаблицыДокумента.Сумма / СтрокаТаблицыДокумента.Количество);
			КонецЕсли;
			СтрокаТаблицыДокумента.СуммаСНДС = СтрокаТаблицыДокумента.СуммаБезНДС + СтрокаТаблицыДокумента.СуммаНДС;
			
		КонецЦикла;
		
		СведенияОДокументе.ТаблицаДокумента = ТаблицаДокумента;
		
		ПечатьТорговыхДокументов.ЗаполнитьДанныеОтветственныхЛиц(СведенияОДокументе, Новый Структура("ЗаполнятьРуководителя"));
		
	КонецЦикла;
	
	Возврат ТаблицаСведений;
	
КонецФункции

Функция КоличествоДокументовЗаПериод(Организация, НачалоПериода, КонецПериода) Экспорт
	
	ТаблицаДанных = ПерсонализированныеПредложенияСервисов.ТаблицаДанных();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеТоваровУслуг.Ссылка) КАК КоличествоДокументов
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Организация = &Организация
	|	И ПоступлениеТоваровУслуг.Дата >= &НачалоПериода
	|	И ПоступлениеТоваровУслуг.Дата <= &КонецПериода
	|	И ПоступлениеТоваровУслуг.Проведен = ИСТИНА";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КоличествоДокументов = Выборка.КоличествоДокументов;
	Иначе
		КоличествоДокументов = 0;
	КонецЕсли;
	
	// Добавляем итог по разделу
	СтрокаДанных = ТаблицаДанных.Добавить();
	СтрокаДанных.Порядок            = 0;
	СтрокаДанных.ЗначениеПоказателя = КоличествоДокументов;

	Возврат ТаблицаДанных;
	
КонецФункции

Функция ТекстЗапросаРасчетыВВалютеОсновныеСредства()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыОсновныеСредства.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыОсновныеСредства.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СуммыТаблицыОсновныеСредства.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыОсновныеСредства.СуммаРуб КАК СуммаРуб,
	|	СуммыТаблицыОсновныеСредства.СуммаНДСРуб КАК СуммаНДСРуб
	|ПОМЕСТИТЬ СуммыТаблицыОсновныеСредства
	|ИЗ
	|	&СуммыТаблицыОсновныеСредства КАК СуммыТаблицыОсновныеСредства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОсновныеСредства.Ссылка КАК Ссылка,
	|	ВременнаяТаблицаОсновныеСредства.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
	|	ВременнаяТаблицаОсновныеСредства.Количество КАК Количество,
	|	ВременнаяТаблицаОсновныеСредства.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ВременнаяТаблицаОсновныеСредства.ЕдиницаУчета КАК ЕдиницаУчета,
	|	СуммыТаблицыОсновныеСредства.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СуммыТаблицыОсновныеСредства.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыОсновныеСредства.СуммаРуб КАК СуммаРуб,
	|	СуммыТаблицыОсновныеСредства.СуммаНДСРуб КАК СуммаНДСРуб,
	|	ВременнаяТаблицаОсновныеСредства.СтавкаНДС КАК СтавкаНДС,
	|	ВременнаяТаблицаОсновныеСредства.СчетУчета КАК СчетУчета,
	|	ВременнаяТаблицаОсновныеСредства.СчетУчетаНДС КАК СчетУчетаНДС,
	|	ВременнаяТаблицаОсновныеСредства.СпособУчетаНДС КАК СпособУчетаНДС,
	|	ВременнаяТаблицаОсновныеСредства.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
	|	ВременнаяТаблицаОсновныеСредства.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ВременнаяТаблицаОсновныеСредства.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВременнаяТаблицаОсновныеСредства.ПроцентИнвестиционногоВычета КАК ПроцентИнвестиционногоВычета
	|ПОМЕСТИТЬ ТаблицаОсновныеСредства
	|ИЗ
	|	ВременнаяТаблицаОсновныеСредства КАК ВременнаяТаблицаОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыОсновныеСредства КАК СуммыТаблицыОсновныеСредства
	|		ПО ВременнаяТаблицаОсновныеСредства.НомерСтроки = СуммыТаблицыОсновныеСредства.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПроверкаСостоянияОС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если НЕ Реквизиты.ЕстьОсновныеСредства Тогда
		ПараметрыПроведения.Вставить("ПроверкаСостоянияОСРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ПроверкаСостоянияОСТаблица",   Неопределено);
		Возврат "";
	КонецЕсли;

	НомераТаблиц.Вставить("ПроверкаСостоянияОСРеквизиты", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	""ОсновныеСредства"" КАК ИмяСписка,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК СостояниеОС,
	|	Реквизиты.Ссылка КАК Регистратор
	|ИЗ
	|	Реквизиты КАК Реквизиты"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

	НомераТаблиц.Вставить("ПроверкаСостоянияОСТаблица", НомераТаблиц.Количество());
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ТаблицаОсновныеСредства.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОсновныеСредства.ОсновноеСредство.ЕдиницаУчета КАК ЕдиницаУчета
	|ИЗ
	|	ТаблицаОсновныеСредства КАК ТаблицаОсновныеСредства
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаПроверкаСостоянияНМА(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьПрава Тогда
		ПараметрыПроведения.Вставить("ПроверкаСостоянияНМАРеквизиты", Неопределено);
		ПараметрыПроведения.Вставить("ПроверкаСостоянияНМАТаблица", Неопределено);
		Возврат "";
	КонецЕсли;

	НомераТаблиц.Вставить("ПроверкаСостоянияНМАРеквизиты", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	""Права"" КАК ИмяСписка,
	|	""ОбъектПрава"" КАК ИмяПоля,
	|	Реквизиты.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету) КАК Состояние,
	|	Реквизиты.Ссылка КАК Регистратор
	|ИЗ
	|	Реквизиты КАК Реквизиты"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	НомераТаблиц.Вставить("ПроверкаСостоянияНМАТаблица", НомераТаблиц.Количество());
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ТаблицаПрава.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПрава.ОбъектПрава КАК НематериальныйАктив
	|ИЗ
	|	ТаблицаПрава КАК ТаблицаПрава
	|ГДЕ
	|	ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.НематериальныйАктив)
	|				И НЕ ТаблицаПрава.ЭтоМалоценныйНМА
	|			ИЛИ ТаблицаПрава.ОтражениеНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;

КонецФункции

// Функция формирует табличный документ унифицированной формы ОС-1
//
// Возвращаемое значение:
//  Табличный документ по форме ОС-1 (приходный ордер).
//
Функция ПечатьОС1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)

	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваровУслуг_ОС1";

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС1");
	МакетОС1а = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС1а");
	МакетОС1б = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ОС1б");

	// Области
	ОбластьМакетаЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакетаЗаголовок_а = МакетОС1а.ПолучитьОбласть("Заголовок");

	Шапка1ОС1б     = МакетОС1б.ПолучитьОбласть("Шапка1");
	Шапка2ОС1б     = МакетОС1б.ПолучитьОбласть("Шапка2");
	Строка2ОС1б    = МакетОС1б.ПолучитьОбласть("Строка2");
	Строка2ПОС1б   = МакетОС1б.ПолучитьОбласть("Строка2П");
	Подвал2        = МакетОС1б.ПолучитьОбласть("Подвал2");
	Шапка3ОС1б     = МакетОС1б.ПолучитьОбласть("Шапка3");
	Строка3ОС1б    = МакетОС1б.ПолучитьОбласть("Строка3");
	Строка3ПОС1б   = МакетОС1б.ПолучитьОбласть("Строка3П");
	Подвал3        = МакетОС1б.ПолучитьОбласть("Подвал3");
	Шапка4ОС1б     = МакетОС1б.ПолучитьОбласть("Шапка4");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Организация КАК Организация,
	|	ПоступлениеТоваровУслугОсновныеСредства.НомерСтроки КАК НомерСтроки,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
	|	ПоступлениеТоваровУслугОсновныеСредства.Количество КАК Количество,
	|	ПоступлениеТоваровУслугОсновныеСредства.Сумма КАК Сумма,
	|	ПоступлениеТоваровУслугОсновныеСредства.СчетУчета КАК СчетУчета,
	|	ПоступлениеТоваровУслугОсновныеСредства.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПоступлениеТоваровУслугОсновныеСредства.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.АмортизационнаяГруппа.Порядок + 1 КАК НомерГруппы,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.Изготовитель КАК Изготовитель,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.ЕдиницаУчета КАК ЕдиницаУчета,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.НаименованиеПолное КАК НаименованиеОС,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.КодПоОКОФ.Код КАК КодОКОФ,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	МАКСИМУМ(ПервоначальныеСведенияОСБухгалтерскийУчет.Период) КАК МаксПериод
	|ПОМЕСТИТЬ ВТ_МаксПериодыСведенийОСБУ
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ПоступлениеТоваровУслугОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ПО ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство
	|			И ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Организация = ПервоначальныеСведенияОСБухгалтерскийУчет.Организация
	|			И (ПервоначальныеСведенияОСБухгалтерскийУчет.Период <= КОНЕЦПЕРИОДА(ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Дата, МЕСЯЦ))
	|ГДЕ
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство,
	|	ПоступлениеТоваровУслугОсновныеСредства.Количество,
	|	ПоступлениеТоваровУслугОсновныеСредства.Сумма,
	|	ПоступлениеТоваровУслугОсновныеСредства.СчетУчета,
	|	ПоступлениеТоваровУслугОсновныеСредства.СрокПолезногоИспользования,
	|	ПоступлениеТоваровУслугОсновныеСредства.ИнвентарныйНомер,
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Организация,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.АмортизационнаяГруппа.Порядок + 1,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.Изготовитель,
	|	ПоступлениеТоваровУслугОсновныеСредства.НомерСтроки,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.ГруппаОС,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.ЕдиницаУчета,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.НаименованиеПолное,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.ЗаводскойНомер,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.КодПоОКОФ.Код,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство.ДатаВыпуска
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка КАК Ссылка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство) КАК КоличествоСтрок
	|ПОМЕСТИТЬ ВТ_ПоступлениеТоваровУслуг_КоличествоОС
	|ИЗ
	|	ВТ_МаксПериодыСведенийОСБУ КАК ПоступлениеТоваровУслугОсновныеСредства
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ДокументПоступлениеТоваровУслуг.Номер КАК НомерАкта,
	|	ДокументПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ДокументПоступлениеТоваровУслуг.Дата КАК ДатаВвода,
	|	ДокументПоступлениеТоваровУслуг.Дата КАК ДатаВводаПриПередаче,
	|	0 КАК КоэффициентУскоренияБУ,
	|	ДокументПоступлениеТоваровУслуг.МОЛ КАК МОЛБУ,
	|	0 КАК НормаАморт,
	|	ДокументПоступлениеТоваровУслуг.Ответственный КАК Ответственный,
	|	ДокументПоступлениеТоваровУслуг.МестонахождениеОС КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ДокументПоступлениеТоваровУслуг.МестонахождениеОС.НаименованиеПолное ПОДОБНО """"""""
	|			ТОГДА ДокументПоступлениеТоваровУслуг.МестонахождениеОС.Наименование
	|		ИНАЧЕ ДокументПоступлениеТоваровУслуг.МестонахождениеОС.НаименованиеПолное
	|	КОНЕЦ КАК ПодрПолучателя,
	|	ДокументПоступлениеТоваровУслуг.Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыПоступленияАктивов.ПриобретениеЗаПлату) КАК СпособПоступления,
	|	ЕСТЬNULL(Сведения.ПервоначальнаяСтоимость, 0) КАК СтоимостьБУ,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный) КАК СпособАмортизации,
	|	ДокументПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВТ_МаксПериодыСведенийОСБУ.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА ""-""
	|		ИНАЧЕ ЕСТЬNULL(Сведения.ИнвентарныйНомер, ВТ_МаксПериодыСведенийОСБУ.ИнвентарныйНомер)
	|	КОНЕЦ КАК ИнвНомер,
	|	ЕСТЬNULL(Сведения.ПервоначальнаяСтоимость, ВТ_МаксПериодыСведенийОСБУ.Сумма) КАК НачСтоимость,
	|	ВТ_МаксПериодыСведенийОСБУ.СчетУчета КАК СчетУчетаОС,
	|	ВТ_МаксПериодыСведенийОСБУ.СрокПолезногоИспользования КАК СрокПолезнИспПриПост,
	|	ВТ_МаксПериодыСведенийОСБУ.НомерГруппы КАК НомерГруппы,
	|	ВТ_МаксПериодыСведенийОСБУ.Изготовитель КАК Изготовитель,
	|	ВТ_МаксПериодыСведенийОСБУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_МаксПериодыСведенийОСБУ.Количество КАК Количество,
	|	ВТ_МаксПериодыСведенийОСБУ.НомерСтроки КАК НомерСтроки,
	|	ВТ_МаксПериодыСведенийОСБУ.ГруппаОС КАК ГруппаОС,
	|	ВТ_МаксПериодыСведенийОСБУ.ЕдиницаУчета КАК ЕдиницаУчета,
	|	ВТ_МаксПериодыСведенийОСБУ.НаименованиеОС КАК НаименованиеОС,
	|	ВТ_МаксПериодыСведенийОСБУ.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ВТ_МаксПериодыСведенийОСБУ.ДатаВыпуска КАК ГодВыпуска,
	|	ВТ_МаксПериодыСведенийОСБУ.КодОКОФ КАК КодОКОФ,
	|	ЕСТЬNULL(ПоступлениеТоваровУслуг.КоличествоСтрок, 0) КАК КоличествоСтрок
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоступлениеТоваровУслуг_КоличествоОС КАК ПоступлениеТоваровУслуг
	|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МаксПериодыСведенийОСБУ КАК ВТ_МаксПериодыСведенийОСБУ
	|		ПО ДокументПоступлениеТоваровУслуг.Ссылка = ВТ_МаксПериодыСведенийОСБУ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК Сведения
	|		ПО (ВТ_МаксПериодыСведенийОСБУ.ОсновноеСредство = Сведения.ОсновноеСредство)
	|			И (ВТ_МаксПериодыСведенийОСБУ.Организация = Сведения.Организация)
	|			И (ВТ_МаксПериодыСведенийОСБУ.МаксПериод = Сведения.Период)
	|ГДЕ
	|	ДокументПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки";

	ВыборкаПоОС = Запрос.Выполнить().Выбрать();

	ПервыйДокумент = Истина;

	Пока ВыборкаПоОС.СледующийПоЗначениюПоля("Ссылка") Цикл

		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		Если ВыборкаПоОС.КоличествоСтрок <= 1 Тогда

			ВыборкаПоОС.Следующий();

			Если НЕ(ВыборкаПоОС.ГруппаОС = Перечисления.ГруппыОС.Здания 
				ИЛИ ВыборкаПоОС.ГруппаОС = Перечисления.ГруппыОС.Сооружения) Тогда
				
				ОбластьМакетаЗаголовок.Параметры.Заполнить(ВыборкаПоОС);
				
				ОбластьМакетаЗаголовок.Параметры.НомерАкта = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоОС.НомерАкта, Истина, Ложь);
				
				НаименованиеОС = ?(ПустаяСтрока(ВыборкаПоОС.НаименованиеОС),
					СокрЛП(ВыборкаПоОС.ОсновноеСредство), ВыборкаПоОС.НаименованиеОС);
				
				ОбластьМакетаЗаголовок.Параметры.НаименованиеОС = НаименованиеОС;

				Если ВыборкаПоОС.ЕдиницаУчета = Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект Тогда
					ОбластьМакетаЗаголовок.Параметры.НаименованиеОС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1, %2 шт.'", ОбщегоНазначения.КодОсновногоЯзыка()),
						НаименованиеОС,
						ВыборкаПоОС.Количество);
				КонецЕсли;
				
				ОбластьМакетаЗаголовок.Параметры.ПодрПолучателя = Строка(ВыборкаПоОС.ПодразделениеОрганизации);
				Документы.ПринятиеКУчетуОС.ЗаполнитьДанныеОрганизацииПолучателя(ВыборкаПоОС, ОбластьМакетаЗаголовок);
				
				ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаПоОС.Организация,ВыборкаПоОС.МОЛБУ,ВыборкаПоОС.Дата);
				ОбластьМакетаЗаголовок.Параметры.МОЛДолжность = ДанныеФизЛица.Должность;
				ОбластьМакетаЗаголовок.Параметры.МОЛРасшифровкаПодписи = ДанныеФизЛица.Представление;

				ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовок);

			Иначе
				
				ОбластьМакетаЗаголовок_а.Параметры.Заполнить(ВыборкаПоОС);
				
				ОбластьМакетаЗаголовок_а.Параметры.НомерАкта = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоОС.НомерАкта, Истина, Ложь);
				
				Если ПустаяСтрока(ВыборкаПоОС.НаименованиеОС) Тогда
					ОбластьМакетаЗаголовок_а.Параметры.НаименованиеОС = СокрЛП(ВыборкаПоОС.ОсновноеСредство);
				КонецЕсли;

				ОбластьМакетаЗаголовок_а.Параметры.ПодрПолучателя = Строка(ВыборкаПоОС.ПодразделениеОрганизации);
				
				ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаПоОС.Организация,ВыборкаПоОС.МОЛБУ,ВыборкаПоОС.Дата);
				ОбластьМакетаЗаголовок_а.Параметры.МОЛДолжность = ДанныеФизЛица.Должность;
				ОбластьМакетаЗаголовок_а.Параметры.МОЛРасшифровкаПодписи = ДанныеФизЛица.Представление;
				
				Документы.ПринятиеКУчетуОС.ЗаполнитьДанныеОрганизацииПолучателя(ВыборкаПоОС, ОбластьМакетаЗаголовок_а);
				ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовок_а);

			КонецЕсли;

		Иначе // Объектов много - групповая печать.

			ОС1б_Страница1 = Новый ТабличныйДокумент();
			ОС1б_Страница2 = Новый ТабличныйДокумент();
			ОС1б_Страница3 = Новый ТабличныйДокумент();
			ОС1б_Страница4 = Новый ТабличныйДокумент();

			Шапка1ОС1б.Параметры.Заполнить(ВыборкаПоОС);
			Документы.ПринятиеКУчетуОС.ЗаполнитьДанныеОрганизацииПолучателя(ВыборкаПоОС, Шапка1ОС1б);
			Шапка1ОС1б.Параметры.НомерАкта = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаПоОС.НомерАкта, Истина, Ложь);
			ОС1б_Страница1.Вывести(Шапка1ОС1б);

			Шапка2ОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница2.Вывести(Шапка2ОС1б);

			Шапка3ОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница3.Вывести(Шапка3ОС1б);

			Шапка4ОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ДанныеФизЛица = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(ВыборкаПоОС.Организация,ВыборкаПоОС.МОЛБУ,ВыборкаПоОС.Дата);
			Шапка4ОС1б.Параметры.МОЛДолжность = ДанныеФизЛица.Должность;
			Шапка4ОС1б.Параметры.МОЛРасшифровкаПодписи = ДанныеФизЛица.Представление;			
			ОС1б_Страница4.Вывести(Шапка4ОС1б);

			НомПП            = 0;
			ИтогНачСтоимость = 0;

			Пока ВыборкаПоОС.Следующий() Цикл

				ИтогНачСтоимость = ИтогНачСтоимость + ВыборкаПоОС.НачСтоимость;

				НомПП = НомПП + 1;
				Строка2ОС1б.Параметры.Нс = НомПП;

				Строка2ОС1б.Параметры.Заполнить(ВыборкаПоОС);
				
				НаименованиеОС = ?(ПустаяСтрока(ВыборкаПоОС.НаименованиеОС),
					СокрЛП(ВыборкаПоОС.ОсновноеСредство), ВыборкаПоОС.НаименованиеОС);
				
				Строка2ОС1б.Параметры.НаименованиеОС = НаименованиеОС;

				Если ВыборкаПоОС.ЕдиницаУчета = Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект Тогда
					Строка2ОС1б.Параметры.НаименованиеОС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1, %2 шт.'", ОбщегоНазначения.КодОсновногоЯзыка()),
						НаименованиеОС,
						ВыборкаПоОС.Количество);
				КонецЕсли;
					
				ОС1б_Страница2.Вывести(Строка2ОС1б);

				Строка3ОС1б.Параметры.Заполнить(ВыборкаПоОС);
				ОС1б_Страница3.Вывести(Строка3ОС1б);

			КонецЦикла;

			Строка2ПОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница2.Вывести(Строка2ПОС1б);

			Подвал2.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница2.Вывести(Подвал2);

			Строка3ПОС1б.Параметры.Заполнить(ВыборкаПоОС);
			ОС1б_Страница3.Вывести(Строка3ПОС1б);

			Подвал3.Параметры.Заполнить(ВыборкаПоОС);
			Подвал3.Параметры.ИтогНачСтоимость = ИтогНачСтоимость;
			ОС1б_Страница3.Вывести(Подвал3);

			ТабличныйДокумент.Вывести(ОС1б_Страница1);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабличныйДокумент.Вывести(ОС1б_Страница2);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабличныйДокумент.Вывести(ОС1б_Страница3);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабличныйДокумент.Вывести(ОС1б_Страница4);
			
		КонецЕсли;

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоОС.Ссылка);
	КонецЦикла;
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("ПоступлениеТоваровУслуг.Печать.ОС1");
	
	Возврат ТабличныйДокумент;

КонецФункции

#Область СтатусыСчетовПоставщика

Функция ПоступленияПоСчету(СчетНаОплату) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("СчетНаОплату", СчетНаОплату);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СвязанныеДокументы.Ссылка КАК Ссылка
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&СчетНаОплату) КАК СвязанныеДокументы
	|ГДЕ
	|	СвязанныеДокументы.Ссылка ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|	И ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.ПоступлениеТоваровУслуг).Проведен";
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Результат;
	
КонецФункции

Функция ТоварыУслугиКПоступлениюПоСчету(СчетНаОплату, ПоступленияПоСчету, ЗаполнятьПризнакПрослеживаемости = Ложь, Дата = '00010101') Экспорт
	
	ТоварыУслуги = Новый ТаблицаЗначений;
	ТоварыУслуги.Колонки.Добавить("ЭтоУслуга",     Новый ОписаниеТипов("Булево"));
	ТоварыУслуги.Колонки.Добавить("Номенклатура",  Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТоварыУслуги.Колонки.Добавить("Содержание",    ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТоварыУслуги.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.КлассификаторЕдиницИзмерения"));
	ТоварыУслуги.Колонки.Добавить("Коэффициент",   ОбщегоНазначения.ОписаниеТипаЧисло(10, 3));
	ТоварыУслуги.Колонки.Добавить("Количество",    ОбщегоНазначения.ОписаниеТипаЧисло(10, 3));
	ТоварыУслуги.Колонки.Добавить("Цена",          ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТоварыУслуги.Колонки.Добавить("Сумма",         ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТоварыУслуги.Колонки.Добавить("СтавкаНДС",     Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	ТоварыУслуги.Колонки.Добавить("СуммаНДС",      ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТоварыУслуги.Колонки.Добавить("НомерГТД",      Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
	ТоварыУслуги.Колонки.Добавить("СтранаПроисхождения", Новый ОписаниеТипов("СправочникСсылка.СтраныМира"));
	ТоварыУслуги.Колонки.Добавить("ОтражениеВУСН", Новый ОписаниеТипов("ПеречислениеСсылка.ОтражениеВУСН"));
	ТоварыУслуги.Колонки.Добавить("ПрослеживаемыйТовар", Новый ОписаниеТипов("Булево"));
	ТоварыУслуги.Колонки.Добавить("ПрослеживаемыйКомплект", Новый ОписаниеТипов("Булево"));

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетНаОплату",       СчетНаОплату);
	Запрос.УстановитьПараметр("ПоступленияПоСчету", ПоступленияПоСчету);
	Запрос.УстановитьПараметр("ЗаполнятьПризнакПрослеживаемости", ЗаполнятьПризнакПрослеживаемости);
	
	НомераТаблиц = Новый Массив;
	
	Запрос.Текст = ТекстЗапросаРеквизитыСчета(НомераТаблиц) 
		+ ТекстЗапросаПоступившаяНоменклатура(НомераТаблиц)
		+ ТекстЗапросаТоварыУслугиКПоступлениюПоСчету(НомераТаблиц);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура;
	Для НомерТаблицы = 0 По НомераТаблиц.Количество() - 1 Цикл
		Результат.Вставить(НомераТаблиц[НомерТаблицы], РезультатЗапроса[НомерТаблицы].Выгрузить());
	КонецЦикла;
	
	ПоступившаяНоменклатура = Результат.ПоступившаяНоменклатура;
		
	РеквизитыСчета = Новый Структура("СуммаВключаетНДС,ЭтоКомиссия", Истина, Ложь);
	Если Результат.РеквизитыСчета.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыСчета, Результат.РеквизитыСчета[0]);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат.ТоварыУслуги, ТоварыУслуги);
		
	СписокПолейОтбора = Новый Массив;
	СписокПолейОтбора.Добавить("ЭтоУслуга, Номенклатура, Содержание, Цена");
	СписокПолейОтбора.Добавить("ЭтоУслуга, Номенклатура, Содержание");
	
	Если ЗаполнятьПризнакПрослеживаемости Тогда
		МассивНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(ТоварыУслуги, "Номенклатура", Истина);
		РеквизитыНоменклатурыИзИстории = Справочники.Номенклатура.ПризнакПрослеживаемостиНаДатуИзИстории(
		МассивНоменклатуры, Дата);
	КонецЕсли;
	
	Для каждого ПоляОтбора Из СписокПолейОтбора Цикл
		
		ПолученныеПолностью = Новый Массив;
		
		ПоступившаяНоменклатура.Свернуть(ПоляОтбора, "Количество");
		ПоступившаяНоменклатура.Индексы.Добавить(ПоляОтбора);
		
		Отбор = Новый Структура(ПоляОтбора);
		
		Для каждого СтрокаСчета Из ТоварыУслуги Цикл
			
			// Если количество не указано, считаем, что услуга получается один раз.
			КоличествоВСчете = ?(СтрокаСчета.ЭтоУслуга И СтрокаСчета.Количество = 0, 1, СтрокаСчета.Количество);
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаСчета);
			Если ЗаполнятьПризнакПрослеживаемости Тогда
				РеквизитыТекущейНоменклатуры = РеквизитыНоменклатурыИзИстории[СтрокаСчета.Номенклатура];
				Если РеквизитыТекущейНоменклатуры <> Неопределено Тогда
					СтрокаСчета.ПрослеживаемыйТовар = РеквизитыТекущейНоменклатуры.ПрослеживаемыйТовар
					Или РеквизитыТекущейНоменклатуры.ПрослеживаемыйКомплект;
					СтрокаСчета.ПрослеживаемыйКомплект = РеквизитыТекущейНоменклатуры.ПрослеживаемыйКомплект;
				КонецЕсли;
			КонецЕсли;

			НайденныеСтроки = ПоступившаяНоменклатура.НайтиСтроки(Отбор);
			
			Если НайденныеСтроки.Количество() <> 0 Тогда
				
				ПолученоРанее = НайденныеСтроки[0].Количество;
				
				Если ПолученоРанее < КоличествоВСчете Тогда
					// Товар получен частично.
					Количество = Макс(КоличествоВСчете - ПолученоРанее, 0);
					СтрокаСчета.Количество = Количество;
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаСчета, РеквизитыСчета.СуммаВключаетНДС);
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаСчета, РеквизитыСчета.СуммаВключаетНДС);
				Иначе
					ПолученныеПолностью.Добавить(СтрокаСчета);
				КонецЕсли;
				
				НайденныеСтроки[0].Количество = Макс(ПолученоРанее - КоличествоВСчете, 0);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ВГраница = ПолученныеПолностью.Количество() - 1;
		Для Индекс = 0 По ВГраница Цикл
			ТоварыУслуги.Удалить(ПолученныеПолностью[ВГраница - Индекс]);
		КонецЦикла;
	
	КонецЦикла; 
	
	Если РеквизитыСчета.ЭтоКомиссия Тогда
		ТоварыУслуги.ЗаполнитьЗначения(Перечисления.ОтражениеВУСН.НеПринимаются, "ОтражениеВУСН");
	Иначе
		ТоварыУслуги.ЗаполнитьЗначения(Перечисления.ОтражениеВУСН.Принимаются, "ОтражениеВУСН");
	КонецЕсли;
	
	Возврат ТоварыУслуги;
	
КонецФункции

Функция ВозвратнаяТараКПоступлениюПоСчету(СчетНаОплату, ПоступленияПоСчету) Экспорт
	
	ВозвратнаяТара = Новый ТаблицаЗначений;
	ВозвратнаяТара.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ВозвратнаяТара.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(10, 3));
	ВозвратнаяТара.Колонки.Добавить("Цена", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ВозвратнаяТара.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетНаОплату", СчетНаОплату);
	Запрос.УстановитьПараметр("ПоступленияПоСчету", ПоступленияПоСчету);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПолученнаяТара.Номенклатура КАК Номенклатура,
	|	СУММА(ПолученнаяТара.Количество) КАК Количество
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ВозвратнаяТара КАК ПолученнаяТара
	|ГДЕ
	|	ПолученнаяТара.Ссылка В(&ПоступленияПоСчету)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПолученнаяТара.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратнаяТара.Номенклатура,
	|	ВозвратнаяТара.Количество,
	|	ВозвратнаяТара.Цена,
	|	ВозвратнаяТара.Сумма
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика.ВозвратнаяТара КАК ВозвратнаяТара
	|ГДЕ
	|	ВозвратнаяТара.Ссылка = &СчетНаОплату";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ПолученнаяНоменклатура = Результат[0].Выгрузить();
	
	ПоляОтбора = "Номенклатура";
	ПолученнаяНоменклатура.Индексы.Добавить(ПоляОтбора);
	Отбор = Новый Структура(ПоляОтбора);
	
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, Выборка);
		НайденныеСтроки = ПолученнаяНоменклатура.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			НоваяСтрока = ВозвратнаяТара.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		Иначе
			
			ПолученоРанее = НайденныеСтроки[0].Количество;
			НайденныеСтроки[0].Количество = Макс(ПолученоРанее - Выборка.Количество, 0);
			
			Если ПолученоРанее < Выборка.Количество Тогда
				НоваяСтрока = ВозвратнаяТара.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.Количество = Выборка.Количество - ПолученоРанее;
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(НоваяСтрока);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВозвратнаяТара;
	
КонецФункции

Функция ТекстЗапросаПоступлениеПоСчету(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если НЕ ЗначениеЗаполнено(Реквизиты.СчетНаОплатуПоставщика) 
		ИЛИ Реквизиты.ЭтоКомиссия Тогда
		ПараметрыПроведения.Вставить("ПоступлениеТоваровУслугПоСчету",   Неопределено);
		ПараметрыПроведения.Вставить("ПоступлениеВозвратнойТарыПоСчету", Неопределено);
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "";
	
	Если Реквизиты.ЕстьТовары 
		ИЛИ Реквизиты.ЕстьОборудование 
		ИЛИ Реквизиты.ЕстьУслуги 
		ИЛИ Реквизиты.ЕстьАгентскиеУслуги Тогда
		
		НомераТаблиц.Вставить("ПоступлениеТоваровУслугПоСчету", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.ЭтоУслуга,
			|	ВложенныйЗапрос.Номенклатура,
			|	ВложенныйЗапрос.Содержание,
			|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
			|ИЗ
			|	(";
		Если Реквизиты.ЕстьТовары Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|		ЛОЖЬ КАК ЭтоУслуга,
			|		ТаблицаТовары.Номенклатура КАК Номенклатура,
			|		"""" КАК Содержание,
			|		ТаблицаТовары.Количество КАК Количество
			|	ИЗ
			|		ТаблицаТовары КАК ТаблицаТовары";
		КонецЕсли;
		Если Реквизиты.ЕстьОборудование Тогда
			Если Реквизиты.ЕстьТовары Тогда
				ТекстЗапроса = ТекстЗапроса +" ОБЪЕДИНИТЬ ВСЕ ";
			КонецЕсли;
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|		ЛОЖЬ КАК ЭтоУслуга,
			|		ТаблицаОборудование.Номенклатура КАК Номенклатура,
			|		"""" КАК Содержание,
			|		ТаблицаОборудование.Количество КАК Количество
			|	ИЗ
			|		ТаблицаОборудование КАК ТаблицаОборудование";
		КонецЕсли;
		Если Реквизиты.ЕстьУслуги Тогда
			Если Реквизиты.ЕстьТовары ИЛИ Реквизиты.ЕстьОборудование Тогда
				ТекстЗапроса = ТекстЗапроса +" ОБЪЕДИНИТЬ ВСЕ ";
			КонецЕсли;
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ИСТИНА КАК ЭтоУслуга,
			|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
			|	ТаблицаУслуги.Содержание КАК Содержание,
			|	ВЫБОР
			|		КОГДА ТаблицаУслуги.Количество = 0
			|			ТОГДА 1
			|		ИНАЧЕ ТаблицаУслуги.Количество
			|	КОНЕЦ КАК Количество
			|ИЗ
			|	ТаблицаУслуги КАК ТаблицаУслуги";
		КонецЕсли;
		Если Реквизиты.ЕстьАгентскиеУслуги Тогда
			Если Реквизиты.ЕстьТовары ИЛИ Реквизиты.ЕстьОборудование ИЛИ Реквизиты.ЕстьУслуги Тогда
				ТекстЗапроса = ТекстЗапроса +" ОБЪЕДИНИТЬ ВСЕ ";
			КонецЕсли;
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ИСТИНА КАК ЭтоУслуга,
			|	ТаблицаАгентскиеУслуги.Номенклатура КАК Номенклатура,
			|	ТаблицаАгентскиеУслуги.Содержание КАК Содержание,
			|	ВЫБОР
			|		КОГДА ТаблицаАгентскиеУслуги.Количество = 0
			|			ТОГДА 1
			|		ИНАЧЕ ТаблицаАгентскиеУслуги.Количество
			|	КОНЕЦ КАК Количество
			|ИЗ
			|	ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + ") КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.ЭтоУслуга,
			|	ВложенныйЗапрос.Номенклатура,
			|	ВложенныйЗапрос.Содержание" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	Иначе
		ПараметрыПроведения.Вставить("ПоступлениеТоваровУслугПоСчету", Неопределено);
	КонецЕсли;
	
	Если Реквизиты.ЕстьТара Тогда
		НомераТаблиц.Вставить("ПоступлениеВозвратнойТарыПоСчету", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ТаблицаТара.Номенклатура КАК Номенклатура,
			|	СУММА(ТаблицаТара.Количество) КАК Количество
			|ИЗ
			|	ТаблицаТара КАК ТаблицаТара
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТара.Номенклатура" + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	Иначе
		ПараметрыПроведения.Вставить("ПоступлениеВозвратнойТарыПоСчету", Неопределено);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРеквизитыСчета(ИменаТаблиц)
	ИменаТаблиц.Добавить("ВТ_ПоступленияПоСчету");
	ИменаТаблиц.Добавить("РеквизитыСчета");
	ИменаТаблиц.Добавить("ТоварыУслуги");
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслуг.Ссылка КАК ПоступлениеТоваров,
	|	ПоступлениеТоваровУслуг.СчетНаОплатуПоставщика КАК СчетНаОплату
	|ПОМЕСТИТЬ ВТ_ПоступленияПоСчету
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&ПоступленияПоСчету)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыСчета.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ЕСТЬNULL(РеквизитыСчета.ДоговорКонтрагента.ВидДоговора, ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)) = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом) КАК ЭтоКомиссия
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК РеквизитыСчета
	|ГДЕ
	|	РеквизитыСчета.Ссылка = &СчетНаОплату
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТоварыУслуги.Номенклатура.Услуга, ИСТИНА) КАК ЭтоУслуга,
	|	ТоварыУслуги.Номенклатура КАК Номенклатура,
	|	ТоварыУслуги.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТоварыУслуги.Номенклатура.НомерГТД КАК НомерГТД,
	|	ТоварыУслуги.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТоварыУслуги.Содержание КАК Содержание,
	|	1 КАК Коэффициент,
	|	ТоварыУслуги.Количество КАК Количество,
	|	ТоварыУслуги.Цена КАК Цена,
	|	ТоварыУслуги.Сумма КАК Сумма,
	|	ТоварыУслуги.СтавкаНДС КАК СтавкаНДС,
	|	ТоварыУслуги.СуммаНДС КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &ЗаполнятьПризнакПрослеживаемости
	|			ТОГДА ТоварыУслуги.Номенклатура.ПрослеживаемыйТовар Или ТоварыУслуги.Номенклатура.ПрослеживаемыйКомплект 
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПрослеживаемыйТовар,
	|	ВЫБОР
	|		КОГДА &ЗаполнятьПризнакПрослеживаемости
	|			ТОГДА ТоварыУслуги.Номенклатура.ПрослеживаемыйКомплект 
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПрослеживаемыйКомплект
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика.Товары КАК ТоварыУслуги
	|ГДЕ
	|	ТоварыУслуги.Ссылка = &СчетНаОплату
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТоварыУслуги.НомерСтроки";
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

Функция ТекстЗапросаПоступившаяНоменклатура(ИменаТаблиц) Экспорт
	ИменаТаблиц.Добавить("ВТ_ПоступившаяНоменклатура");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.СчетНаОплату КАК СчетНаОплату,
	|	ВложенныйЗапрос.ПоступлениеТоваров КАК ПоступлениеТоваров,
	|	ВложенныйЗапрос.ЭтоУслуга КАК ЭтоУслуга,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Содержание КАК Содержание,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	ВложенныйЗапрос.Цена КАК Цена,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ВложенныйЗапрос.СуммаНДС) КАК СуммаНДС,
	|	ВложенныйЗапрос.НомерТаможеннойДекларации КАК НомерТаможеннойДекларации,
	|	ВложенныйЗапрос.КодСтраныПроисхожденияТовара КАК КодСтраныПроисхожденияТовара
	|ПОМЕСТИТЬ ВТ_ПоступившаяНоменклатура
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ПоступленияПоСчету.СчетНаОплату КАК СчетНаОплату,
	|		ВТ_ПоступленияПоСчету.ПоступлениеТоваров КАК ПоступлениеТоваров,
	|		ЛОЖЬ КАК ЭтоУслуга,
	|		ПолученныеТовары.Номенклатура КАК Номенклатура,
	|		"""" КАК Содержание,
	|		ЕСТЬNULL(ПолученныеТовары.НомерГТД.РегистрационныйНомер, НЕОПРЕДЕЛЕНО) КАК НомерТаможеннойДекларации,
	|		ЕСТЬNULL(ПолученныеТовары.СтранаПроисхождения.Код, НЕОПРЕДЕЛЕНО) КАК КодСтраныПроисхожденияТовара,
	|		ПолученныеТовары.Количество КАК Количество,
	|		ПолученныеТовары.Цена КАК Цена,
	|		ПолученныеТовары.Сумма КАК Сумма,
	|		ПолученныеТовары.СтавкаНДС КАК СтавкаНДС,
	|		ПолученныеТовары.СуммаНДС КАК СуммаНДС
	|	ИЗ
	|		ВТ_ПоступленияПоСчету КАК ВТ_ПоступленияПоСчету
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК ПолученныеТовары
	|			ПО ВТ_ПоступленияПоСчету.ПоступлениеТоваров = ПолученныеТовары.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ПоступленияПоСчету.СчетНаОплату,
	|		ВТ_ПоступленияПоСчету.ПоступлениеТоваров,
	|		ЛОЖЬ,
	|		ПолученноеОборудование.Номенклатура,
	|		"""",
	|		ЕСТЬNULL(ПолученноеОборудование.НомерГТД.РегистрационныйНомер, НЕОПРЕДЕЛЕНО),
	|		ЕСТЬNULL(ПолученноеОборудование.СтранаПроисхождения.Наименование, НЕОПРЕДЕЛЕНО),
	|		ПолученноеОборудование.Количество,
	|		ПолученноеОборудование.Цена,
	|		ПолученноеОборудование.Сумма,
	|		ПолученноеОборудование.СтавкаНДС,
	|		ПолученноеОборудование.СуммаНДС
	|	ИЗ
	|		ВТ_ПоступленияПоСчету КАК ВТ_ПоступленияПоСчету
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПолученноеОборудование
	|			ПО ВТ_ПоступленияПоСчету.ПоступлениеТоваров = ПолученноеОборудование.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ПоступленияПоСчету.СчетНаОплату,
	|		ВТ_ПоступленияПоСчету.ПоступлениеТоваров,
	|		ИСТИНА,
	|		ПолученныеУслуги.Номенклатура,
	|		ПолученныеУслуги.Содержание,
	|		НЕОПРЕДЕЛЕНО,
	|		НЕОПРЕДЕЛЕНО,
	|		ВЫБОР
	|			КОГДА ПолученныеУслуги.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПолученныеУслуги.Количество
	|		КОНЕЦ,
	|		ПолученныеУслуги.Цена,
	|		ПолученныеУслуги.Сумма,
	|		ПолученныеУслуги.СтавкаНДС,
	|		ПолученныеУслуги.СуммаНДС
	|	ИЗ
	|		ВТ_ПоступленияПоСчету КАК ВТ_ПоступленияПоСчету
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК ПолученныеУслуги
	|			ПО ВТ_ПоступленияПоСчету.ПоступлениеТоваров = ПолученныеУслуги.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ПоступленияПоСчету.СчетНаОплату,
	|		ВТ_ПоступленияПоСчету.ПоступлениеТоваров,
	|		ИСТИНА,
	|		ПолученныеАгентскиеУслуги.Номенклатура,
	|		ПолученныеАгентскиеУслуги.Содержание,
	|		НЕОПРЕДЕЛЕНО,
	|		НЕОПРЕДЕЛЕНО,
	|		ВЫБОР
	|			КОГДА ПолученныеАгентскиеУслуги.Количество = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПолученныеАгентскиеУслуги.Количество
	|		КОНЕЦ,
	|		ПолученныеАгентскиеУслуги.Цена,
	|		ПолученныеАгентскиеУслуги.Сумма,
	|		ПолученныеАгентскиеУслуги.СтавкаНДС,
	|		ПолученныеАгентскиеУслуги.СуммаНДС
	|	ИЗ
	|		ВТ_ПоступленияПоСчету КАК ВТ_ПоступленияПоСчету
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПолученныеАгентскиеУслуги
	|			ПО ВТ_ПоступленияПоСчету.ПоступлениеТоваров = ПолученныеАгентскиеУслуги.Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.СчетНаОплату,
	|	ВложенныйЗапрос.ПоступлениеТоваров,
	|	ВложенныйЗапрос.ЭтоУслуга,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Содержание,
	|	ВложенныйЗапрос.Цена,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.НомерТаможеннойДекларации,
	|	ВложенныйЗапрос.КодСтраныПроисхожденияТовара";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

Функция ТекстЗапросаТоварыУслугиКПоступлениюПоСчету(ИменаТаблиц)
	ИменаТаблиц.Добавить("ПоступившаяНоменклатура");
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВТ_ПоступившаяНоменклатура.СчетНаОплату КАК СчетНаОплату,
	|	ВТ_ПоступившаяНоменклатура.ЭтоУслуга КАК ЭтоУслуга,
	|	ВТ_ПоступившаяНоменклатура.Номенклатура КАК Номенклатура,
	|	ВТ_ПоступившаяНоменклатура.Содержание КАК Содержание,
	|	ВТ_ПоступившаяНоменклатура.Количество КАК Количество,
	|	ВТ_ПоступившаяНоменклатура.Цена КАК Цена
	|ИЗ
	|	ВТ_ПоступившаяНоменклатура КАК ВТ_ПоступившаяНоменклатура";
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
КонецФункции

#КонецОбласти

Функция ТекстЗапросаДанныеДляПечатиЧека(ИменаТаблиц) Экспорт
	
	ИменаТаблиц.Добавить("ВТ_ОплачиваемыеДокументы");
	ИменаТаблиц.Добавить("ОплачиваемаяНоменклатура");
	ИменаТаблиц.Добавить("ОплачиваемыеДокументы");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_РасшифровкаПлатежа.Сделка КАК Документ,
	|	ДокументПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ДокументПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС, ИСТИНА) КАК СуммаВключаетНДС,
	|	0 КАК СуммаСкидкиПоДокументу,
	|	ВТ_РасшифровкаПлатежа.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ВалютаДокумента, &ВалютаРегламентированногоУчета) КАК ВалютаДокумента,
	|	ВТ_РеквизитыШапки.ВалютаДокумента КАК ВалютаПлатежа,
	|	СУММА(ВТ_РасшифровкаПлатежа.СуммаПлатежа) КАК СуммаОплаты,
	|	СУММА(ВТ_РасшифровкаПлатежа.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВТ_РасшифровкаПлатежа.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	ВТ_РасшифровкаПлатежа.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ВТ_ОплачиваемыеДокументы
	|ИЗ
	|	ВТ_РасшифровкаПлатежа КАК ВТ_РасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РеквизитыШапки КАК ВТ_РеквизитыШапки
	|		ПО ВТ_РасшифровкаПлатежа.Ссылка = ВТ_РеквизитыШапки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ДокументПоступлениеТоваровУслуг
	|		ПО ВТ_РасшифровкаПлатежа.Сделка = ДокументПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ВТ_РасшифровкаПлатежа.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РасшифровкаПлатежа.Сделка,
	|	ВТ_РасшифровкаПлатежа.СтавкаНДС,
	|	ДокументПоступлениеТоваровУслуг.Организация,
	|	ДокументПоступлениеТоваровУслуг.Дата,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.СуммаВключаетНДС, ИСТИНА),
	|	ВТ_РеквизитыШапки.ВалютаДокумента,
	|	ВТ_РасшифровкаПлатежа.ДоговорКонтрагента,
	|	ЕСТЬNULL(ДокументПоступлениеТоваровУслуг.ВалютаДокумента, &ВалютаРегламентированногоУчета),
	|	ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах,
	|	ДоговорыКонтрагентов.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПоступившаяНоменклатура.ПоступлениеТоваров КАК Документ,
	|	ВТ_ПоступившаяНоменклатура.ПоступлениеТоваров.Дата КАК ДатаДокумента,
	|	ВТ_ПоступившаяНоменклатура.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СправочникНоменклатура.КодНоменклатурнойКлассификацииККТ.Код, НЕОПРЕДЕЛЕНО) КАК КодВидаНоменклатурнойКлассификации,
	|	ЕСТЬNULL(СправочникНоменклатура.КодВидаТРУ, НЕОПРЕДЕЛЕНО) КАК КодВидаТРУ,
	|	ЕСТЬNULL(СправочникНоменклатура.Артикул, НЕОПРЕДЕЛЕНО) КАК Артикул,
	|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Код, НЕОПРЕДЕЛЕНО) КАК КодЕдиницыИзмерения,
	|	&ЧастьЗапросаДляВыбораСодержанияУслуг КАК Наименование,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ПоступившаяНоменклатура.Количество = 0
	|					И ЕСТЬNULL(ВТ_ПоступившаяНоменклатура.Номенклатура.Услуга, ИСТИНА)
	|				ТОГДА 1
	|			ИНАЧЕ ВТ_ПоступившаяНоменклатура.Количество
	|		КОНЕЦ) КАК Количество,
	|	СУММА(ВТ_ПоступившаяНоменклатура.Количество) КАК КоличествоОтгружено,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВТ_ОплачиваемыеДокументы.СуммаВключаетНДС
	|				ТОГДА ВТ_ПоступившаяНоменклатура.Цена
	|			КОГДА ВТ_ПоступившаяНоменклатура.Количество = 0
	|				ТОГДА ВТ_ПоступившаяНоменклатура.Сумма + ВТ_ПоступившаяНоменклатура.СуммаНДС
	|			ИНАЧЕ (ВТ_ПоступившаяНоменклатура.Сумма + ВТ_ПоступившаяНоменклатура.СуммаНДС) / ВТ_ПоступившаяНоменклатура.Количество
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена,
	|	0 КАК СуммаСкидок,
	|	ВТ_ПоступившаяНоменклатура.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ВТ_ПоступившаяНоменклатура.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ОплачиваемыеДокументы.СуммаВключаетНДС
	|				ТОГДА ВТ_ПоступившаяНоменклатура.Сумма
	|			ИНАЧЕ ВТ_ПоступившаяНоменклатура.Сумма + ВТ_ПоступившаяНоменклатура.СуммаНДС
	|		КОНЕЦ) КАК Сумма,
	|	НЕОПРЕДЕЛЕНО КАК ДоговорПлатежногоАгента,
	|	НЕОПРЕДЕЛЕНО КАК ПлатежныйАгент,
	|	ВТ_ПоступившаяНоменклатура.НомерТаможеннойДекларации КАК НомерТаможеннойДекларации,
	|	ВТ_ПоступившаяНоменклатура.КодСтраныПроисхожденияТовара КАК КодСтраныПроисхожденияТовара
	|ИЗ
	|	ВТ_ПоступившаяНоменклатура КАК ВТ_ПоступившаяНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОплачиваемыеДокументы КАК ВТ_ОплачиваемыеДокументы
	|		ПО ВТ_ПоступившаяНоменклатура.ПоступлениеТоваров = ВТ_ОплачиваемыеДокументы.Документ
	|			И ВТ_ПоступившаяНоменклатура.СтавкаНДС = ВТ_ОплачиваемыеДокументы.СтавкаНДС
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|			ПО ВТ_ПоступившаяНоменклатура.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	(ВТ_ПоступившаяНоменклатура.Количество <> 0
	|			ИЛИ ЕСТЬNULL(ВТ_ПоступившаяНоменклатура.Номенклатура.Услуга, ИСТИНА))
	|	И НЕ ВТ_ОплачиваемыеДокументы.Документ ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПоступившаяНоменклатура.ПоступлениеТоваров,
	|	ЕСТЬNULL(СправочникНоменклатура.КодНоменклатурнойКлассификацииККТ.Код, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СправочникНоменклатура.КодВидаТРУ, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СправочникНоменклатура.Артикул, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Код, НЕОПРЕДЕЛЕНО),
	|	&ЧастьЗапросаДляВыбораСодержанияУслуг,
	|	ВТ_ПоступившаяНоменклатура.ПоступлениеТоваров.Дата,
	|	ВТ_ПоступившаяНоменклатура.СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ВТ_ОплачиваемыеДокументы.СуммаВключаетНДС
	|				ТОГДА ВТ_ПоступившаяНоменклатура.Цена
	|			КОГДА ВТ_ПоступившаяНоменклатура.Количество = 0
	|				ТОГДА ВТ_ПоступившаяНоменклатура.Сумма + ВТ_ПоступившаяНоменклатура.СуммаНДС
	|			ИНАЧЕ (ВТ_ПоступившаяНоменклатура.Сумма + ВТ_ПоступившаяНоменклатура.СуммаНДС) / ВТ_ПоступившаяНоменклатура.Количество
	|		КОНЕЦ КАК ЧИСЛО(15, 2)),
	|	ВТ_ПоступившаяНоменклатура.Номенклатура,
	|	ВТ_ПоступившаяНоменклатура.НомерТаможеннойДекларации,
	|	ВТ_ПоступившаяНоменклатура.КодСтраныПроисхожденияТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплачиваемыеДокументы.Документ КАК Документ,
	|	ОплачиваемыеДокументы.Организация КАК Организация,
	|	ОплачиваемыеДокументы.Дата КАК Дата,
	|	ОплачиваемыеДокументы.СуммаСкидкиПоДокументу КАК СуммаСкидкиПоДокументу,
	|	ОплачиваемыеДокументы.РасчетыВУсловныхЕдиницах
	|		И ОплачиваемыеДокументы.ВалютаДокумента <> ОплачиваемыеДокументы.ВалютаПлатежа КАК РасчетыВУсловныхЕдиницах,
	|	ОплачиваемыеДокументы.СтавкаНДС КАК СтавкаНДС,
	|	ОплачиваемыеДокументы.СуммаНДС КАК СуммаНДС,
	|	ОплачиваемыеДокументы.СуммаОплаты КАК СуммаОплаты,
	|	ОплачиваемыеДокументы.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	СУММА(ЕСТЬNULL(РасходныйКассовыйОрдерРасшифровкаПлатежа.СуммаВзаиморасчетов, 0)) КАК СуммаОплатыВсего
	|ИЗ
	|	ВТ_ОплачиваемыеДокументы КАК ОплачиваемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйКассовыйОрдерРасшифровкаПлатежа
	|		ПО ОплачиваемыеДокументы.Документ = РасходныйКассовыйОрдерРасшифровкаПлатежа.Сделка
	|			И ОплачиваемыеДокументы.СтавкаНДС = РасходныйКассовыйОрдерРасшифровкаПлатежа.СтавкаНДС
	|			И (РасходныйКассовыйОрдерРасшифровкаПлатежа.Ссылка.Проведен)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОплачиваемыеДокументы.Документ,
	|	ОплачиваемыеДокументы.Организация,
	|	ОплачиваемыеДокументы.Дата,
	|	ОплачиваемыеДокументы.СуммаСкидкиПоДокументу,
	|	ОплачиваемыеДокументы.СтавкаНДС,
	|	ОплачиваемыеДокументы.СуммаНДС,
	|	ОплачиваемыеДокументы.СуммаОплаты,
	|	ОплачиваемыеДокументы.СуммаВзаиморасчетов,
	|	ОплачиваемыеДокументы.РасчетыВУсловныхЕдиницах
	|		И ОплачиваемыеДокументы.ВалютаДокумента <> ОплачиваемыеДокументы.ВалютаПлатежа";
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	ЧастьЗапросаДляВыбораСодержанияУслуг = ОбщегоНазначенияБПВызовСервера.ПолучитьЧастьЗапросаДляВыбораСодержанияУслуг("ВТ_ПоступившаяНоменклатура");
	Возврат СтрЗаменить(ТекстЗапроса, "&ЧастьЗапросаДляВыбораСодержанияУслуг", ЧастьЗапросаДляВыбораСодержанияУслуг);
КонецФункции

// Определяет предъявлена ли сумма НДС по документу
//
// Параметры:
// ТекущийДокумент - документ, сумму НДС которого нужно вычислить
//
// Возвращаемое значение:
// Булево - если сумма НДС в документе не нулевая или документ составлен по ставке 0%.
Функция НаличиеСуммыНДС(ТекущийДокумент) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СуммаНДС > 0
	|				ИЛИ ПоступлениеТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДС,
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СуммаНДС > 0
	|				ИЛИ ПоступлениеТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СуммаНДС > 0
	|				ИЛИ ПоступлениеТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СуммаНДС > 0
	|				ИЛИ ПоступлениеТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СуммаНДС > 0
	|				ИЛИ ПоступлениеТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СуммаНДС > 0
	|				ИЛИ ПоступлениеТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СуммаНДС > 0
	|				ИЛИ ПоступлениеТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Топливо КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СуммаНДС > 0
	|				ИЛИ ПоступлениеТоваровУслуг.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Права КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТаблицаНДС.СуммаНДС) КАК СуммаНДС,
	|	ТаблицаНДС.Ссылка КАК Ссылка
	|ИЗ
	|	ТаблицаНДС КАК ТаблицаНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНДС.Ссылка";

	Запрос.УстановитьПараметр("СсылкаНаДокумент", ТекущийДокумент);
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		Возврат Результат.СуммаНДС > 0;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

// Подготоваливает необходимые таблицы для счет фактуры полученный.
// Данные таблиц необходимы для формирования движений по регистру НДС Предъявленный.
//
// Параметры:
// ТекущийДокумент - документ по которому нужно получить движения
// Отказ - флаг отказа.
//
// Возвращаемое значение:
// Структура - содержатся таблицы по документу.
Функция ПодготовитьПараметрыПроведенияДляСчетФактуры(ТекущийДокумент, Отказ = Ложь) Экспорт

	ПараметрыПроведения = Документы.ПоступлениеТоваровУслуг.ПодготовитьПараметрыПроведения(ТекущийДокумент, Отказ);
	
	Если Отказ Тогда
		Возврат "";
	КонецЕсли;
	
		// Таблица взаиморасчетов
	ТаблицаВзаиморасчеты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
		ПараметрыПроведения.ТаблицаЗачетАвансов, ПараметрыПроведения.ЗачетАвансовРеквизиты, Отказ);

	// Таблицы документа с корректировкой сумм по курсу авансов
	СтруктураТаблицДокумента = УчетДоходовРасходов.ПодготовитьТаблицыПоступленияПоКурсуАвансов(
		ПараметрыПроведения.СтруктураТаблицДокумента,
		ТаблицаВзаиморасчеты, ПараметрыПроведения.ЗачетАвансовРеквизиты);
		
	ТаблицыНДС = Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицыНДС(СтруктураТаблицДокумента);

	ПараметрыПроведения.Вставить("ТаблицыНДС", ТаблицыНДС);
	ПараметрыПроведения.Вставить("ТаблицаВзаиморасчеты", ТаблицаВзаиморасчеты);
	ПараметрыПроведения.Вставить("СтруктураТаблицДокумента", СтруктураТаблицДокумента);

	Возврат ПараметрыПроведения
КонецФункции

Функция ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьТовары
		И Не Реквизиты.ЕстьОборудование 
		ИЛИ Не Реквизиты.ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ВТ_ПрослеживаемостьПредварительная", Неопределено);
		ПараметрыПроведения.Вставить("ВТ_Прослеживаемость",                Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДСПредварительная",      Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДС",                     Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперации",             Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеТовары",               Неопределено);
		ПараметрыПроведения.Вставить("РНПТ",                               Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_ПрослеживаемостьПредварительная", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",                НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДСПредварительная",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДС",                     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперации",             НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеТовары",               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РНПТ",                               НомераТаблиц.Количество());
	
	// Шаблон запроса для сбора временной таблицы ВТ_ПрослеживаемостьПредварительная
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	0 КАК Количество,
	|	НЕОПРЕДЕЛЕНО КАК СтранаПроисхождения,
	|	НЕОПРЕДЕЛЕНО КАК РНПТ,
	|	0 КАК КоличествоПрослеживаемости,
	|	НЕОПРЕДЕЛЕНО КАК ОтражениеВОтчетности,
	|	НЕОПРЕДЕЛЕНО КАК КодОперации,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОперации,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК ПериодОперации,
	|	НЕОПРЕДЕЛЕНО КАК НомерДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ДатаДокумента,
	|	НЕОПРЕДЕЛЕНО КАК ТипДокументаВПрослеживаемости,
	|	НЕОПРЕДЕЛЕНО КАК Комитент,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК ЭтоЗакупкаДляКомитента,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО КАК Комплектующие,
	|	Ложь КАК ПрослеживаемыйКомплект,
	|	0 КАК СуммаБезНДС
	|ПОМЕСТИТЬ ВТ_ПрослеживаемостьПредварительная";

	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	ЕСТЬNULL(ПоступлениеТоваровУслугСведенияПрослеживаемости.Количество, ПоступлениеТоваровУслугТовары.Количество) КАК Количество,
		|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
		|	ПоступлениеТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	ВЫБОР
		|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				И НЕ &БезвозмездноеПоступление
		|			ТОГДА ВЫБОР
		|					КОГДА ПоступлениеТоваровУслугТовары.СчетУчета.Забалансовый
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
		|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &ЕстьНДС
		|						И ПоступлениеТоваровУслугТовары.СчетУчета.Забалансовый
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
		|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
		|			КОНЕЦ
		|	КОНЕЦ КАК ОтражениеВОтчетности,
		|	ВЫБОР
		|		КОГДА &БезвозмездноеПоступление
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.БезвозмездноеПолучение)
		|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				ИЛИ &ЕстьНДС
		|					И ПоступлениеТоваровУслугТовары.СчетУчета.Забалансовый
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
		|		КОГДА ПоступлениеТоваровУслугТовары.СчетУчета.Забалансовый
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПриобретениеТовараДляКомитента)
		|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				И &ЭтоКомиссияНаЗакупку
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолучениеКомитентомСведенийОПриобретенииТовараКомиссионером)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Приобретение)
		|	КОНЕЦ КАК КодОперации,
		|	ПоступлениеТоваровУслугТовары.Ссылка КАК ДокументОперации,
		|	ПоступлениеТоваровУслугТовары.Ссылка.Контрагент КАК Контрагент,
		|	НАЧАЛОПЕРИОДА(ПоступлениеТоваровУслугТовары.Ссылка.Дата, КВАРТАЛ) КАК ПериодОперации,
		|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокумента,
		|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
		|	ВЫБОР
		|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				ИЛИ &БезвозмездноеПоступление
		|			ТОГДА ВЫБОР
		|					КОГДА ПоступлениеТоваровУслугТовары.Ссылка.ЭтоУниверсальныйДокумент
		|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
		|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная)
		|				КОНЕЦ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ТипДокументаВПрослеживаемости,
		|	ВЫБОР
		|		КОГДА &ЭтоКомиссия
		|			ТОГДА ПоступлениеТоваровУслугТовары.Ссылка.Контрагент
		|		ИНАЧЕ ПоступлениеТоваровУслугТовары.Контрагент
		|	КОНЕЦ КАК Комитент,
		|	ВЫБОР
		|		КОГДА &ЭтоКомиссия
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗапасовПрослеживаемыхТоваров.ТоварыОтКомитентаНаРеализацию)
		|		КОГДА ПоступлениеТоваровУслугТовары.ДоговорКонтрагента <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗапасовПрослеживаемыхТоваров.ТоварыЗакупленныеДляКомитента)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВидЗапасов,
		|	ПоступлениеТоваровУслугТовары.СчетУчета.Забалансовый КАК ЭтоЗакупкаДляКомитента,
		|	ПоступлениеТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Номенклатура КАК Комплектующие,
		|	ПоступлениеТоваровУслугТовары.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
		|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Сумма КАК СуммаБезНДС
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
		|		ПО ПоступлениеТоваровУслугТовары.Ссылка = ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка
		|			И ПоступлениеТоваровУслугТовары.ИдентификаторСтроки = ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО (НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				ИЛИ &БезвозмездноеПоступление)
		|			И ПоступлениеТоваровУслугТовары.Ссылка = ДанныеПервичныхДокументов.Документ
		|			И ПоступлениеТоваровУслугТовары.Ссылка.Организация = ДанныеПервичныхДокументов.Организация
		|ГДЕ
		|	ПоступлениеТоваровУслугТовары.ПрослеживаемыйТовар
		|	И ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка";
	КонецЕсли;

	Если Реквизиты.ЕстьОборудование Тогда
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслугОборудование.Номенклатура КАК Номенклатура,
		|	ЕСТЬNULL(ПоступлениеТоваровУслугСведенияПрослеживаемости.Количество, ПоступлениеТоваровУслугОборудование.Количество) КАК Количество,
		|	ПоступлениеТоваровУслугОборудование.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
		|	ПоступлениеТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
		|	ВЫБОР
		|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				И НЕ &БезвозмездноеПоступление
		|			ТОГДА ВЫБОР
		|					КОГДА ПоступлениеТоваровУслугОборудование.СчетУчета.Забалансовый
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
		|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &ЕстьНДС
		|						И ПоступлениеТоваровУслугОборудование.СчетУчета.Забалансовый
		|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
		|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
		|			КОНЕЦ
		|	КОНЕЦ КАК ОтражениеВОтчетности,
		|	ВЫБОР
		|		КОГДА &БезвозмездноеПоступление
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.БезвозмездноеПолучение)
		|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				ИЛИ &ЕстьНДС
		|					И ПоступлениеТоваровУслугОборудование.СчетУчета.Забалансовый
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
		|		КОГДА ПоступлениеТоваровУслугОборудование.СчетУчета.Забалансовый
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПриобретениеТовараДляКомитента)
		|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				И &ЭтоКомиссияНаЗакупку
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПолучениеКомитентомСведенийОПриобретенииТовараКомиссионером)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Приобретение)
		|	КОНЕЦ КАК КодОперации,
		|	ПоступлениеТоваровУслугОборудование.Ссылка КАК ДокументОперации,
		|	ПоступлениеТоваровУслугОборудование.Ссылка.Контрагент КАК Контрагент,
		|	НАЧАЛОПЕРИОДА(ПоступлениеТоваровУслугОборудование.Ссылка.Дата, КВАРТАЛ) КАК ПериодОперации,
		|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокумента,
		|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
		|	ВЫБОР
		|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				ИЛИ &БезвозмездноеПоступление
		|			ТОГДА ВЫБОР
		|					КОГДА ПоступлениеТоваровУслугОборудование.Ссылка.ЭтоУниверсальныйДокумент
		|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
		|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная)
		|				КОНЕЦ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ТипДокументаВПрослеживаемости,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комитент,
		|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
		|	ЛОЖЬ КАК ЭтоЗакупкаДляКомитента,
		|	ПоступлениеТоваровУслугОборудование.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Номенклатура КАК Комплектующие,
		|	ПоступлениеТоваровУслугОборудование.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
		|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Сумма КАК СуммаБезНДС
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
		|		ПО ПоступлениеТоваровУслугОборудование.Ссылка = ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка
		|			И ПоступлениеТоваровУслугОборудование.ИдентификаторСтроки = ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО (НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
		|				ИЛИ &БезвозмездноеПоступление)
		|			И ПоступлениеТоваровУслугОборудование.Ссылка = ДанныеПервичныхДокументов.Документ
		|			И ПоступлениеТоваровУслугОборудование.Ссылка.Организация = ДанныеПервичныхДокументов.Организация
		|ГДЕ
		|	ПоступлениеТоваровУслугОборудование.ПрослеживаемыйТовар
		|	И ПоступлениеТоваровУслугОборудование.Ссылка = &Ссылка";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ВТ_ПрослеживаемостьПредварительная.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ_ПрослеживаемостьПредварительная.Количество) КАК Количество,
	|	ВТ_ПрослеживаемостьПредварительная.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВТ_ПрослеживаемостьПредварительная.РНПТ КАК РНПТ,
	|	СУММА(ВТ_ПрослеживаемостьПредварительная.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВТ_ПрослеживаемостьПредварительная.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
	|	ВТ_ПрослеживаемостьПредварительная.КодОперации КАК КодОперации,
	|	ВТ_ПрослеживаемостьПредварительная.ДокументОперации КАК ДокументОперации,
	|	ВТ_ПрослеживаемостьПредварительная.Контрагент КАК Контрагент,
	|	ВТ_ПрослеживаемостьПредварительная.ПериодОперации КАК ПериодОперации,
	|	ВТ_ПрослеживаемостьПредварительная.НомерДокумента КАК НомерДокумента,
	|	ВТ_ПрослеживаемостьПредварительная.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_ПрослеживаемостьПредварительная.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
	|	ВТ_ПрослеживаемостьПредварительная.Комитент КАК Комитент,
	|	ВТ_ПрослеживаемостьПредварительная.ВидЗапасов КАК ВидЗапасов,
	|	ВТ_ПрослеживаемостьПредварительная.ЭтоЗакупкаДляКомитента КАК ЭтоЗакупкаДляКомитента,
	|	ВТ_ПрослеживаемостьПредварительная.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_ПрослеживаемостьПредварительная.Комплектующие КАК Комплектующие,
	|	ВТ_ПрослеживаемостьПредварительная.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект,
	|	СУММА(ВТ_ПрослеживаемостьПредварительная.СуммаБезНДС) КАК СуммаБезНДС
	|ПОМЕСТИТЬ ВТ_Прослеживаемость
	|ИЗ
	|	ВТ_ПрослеживаемостьПредварительная КАК ВТ_ПрослеживаемостьПредварительная
	|ГДЕ
	|	ВТ_ПрослеживаемостьПредварительная.Номенклатура <> НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПрослеживаемостьПредварительная.ДокументОперации,
	|	ВТ_ПрослеживаемостьПредварительная.КодОперации,
	|	ВТ_ПрослеживаемостьПредварительная.ТипДокументаВПрослеживаемости,
	|	ВТ_ПрослеживаемостьПредварительная.РНПТ,
	|	ВТ_ПрослеживаемостьПредварительная.СтранаПроисхождения,
	|	ВТ_ПрослеживаемостьПредварительная.Номенклатура,
	|	ВТ_ПрослеживаемостьПредварительная.НомерДокумента,
	|	ВТ_ПрослеживаемостьПредварительная.ДатаДокумента,
	|	ВТ_ПрослеживаемостьПредварительная.ОтражениеВОтчетности,
	|	ВТ_ПрослеживаемостьПредварительная.ПериодОперации,
	|	ВТ_ПрослеживаемостьПредварительная.Контрагент,
	|	ВТ_ПрослеживаемостьПредварительная.Комитент,
	|	ВТ_ПрослеживаемостьПредварительная.ВидЗапасов,
	|	ВТ_ПрослеживаемостьПредварительная.ЭтоЗакупкаДляКомитента,
	|	ВТ_ПрослеживаемостьПредварительная.ИдентификаторСтроки,
	|	ВТ_ПрослеживаемостьПредварительная.Комплектующие,
	|	ВТ_ПрослеживаемостьПредварительная.ПрослеживаемыйКомплект";
	
	// Шаблон запроса для сбора временной таблицы ВТ_СуммыБезНДСПредварительная.
	// Суммы без НДС необходимы для отчета об операциях и для регистрации 
	// прослеживаемого товара к получению РНПТ при импорте из ЕАЭС.
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	Неопределено КАК Номенклатура,
	|	0 КАК СуммаБезНДС,
	|	Неопределено КАК ЭтоЗакупкаДляКомитента,
	|	"""" КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТ_СуммыБезНДСПредварительная";
	
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.СуммаРуб - ТаблицаТовары.СуммаНДСРуб КАК СуммаБезНДС,
		|	ПоступлениеТоваровУслугТовары.СчетУчета.Забалансовый КАК ЭтоЗакупкаДляКомитента,
		|	ПоступлениеТоваровУслугТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТовары КАК ТаблицаТовары
		|		ПО ПоступлениеТоваровУслугТовары.ИдентификаторСтроки = ТаблицаТовары.ИдентификаторСтроки
		|ГДЕ
		|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
		|	И ПоступлениеТоваровУслугТовары.ПрослеживаемыйТовар";
	КонецЕсли;
	
	Если Реквизиты.ЕстьОборудование Тогда
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслугОборудование.Номенклатура КАК Номенклатура,
		|	ТаблицаОборудование.СуммаРуб - ТаблицаОборудование.СуммаНДСРуб КАК СуммаБезНДС,
		|	ПоступлениеТоваровУслугОборудование.СчетУчета.Забалансовый КАК ЭтоЗакупкаДляКомитента,
		|	ПоступлениеТоваровУслугОборудование.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОборудование КАК ТаблицаОборудование
		|		ПО ПоступлениеТоваровУслугОборудование.ИдентификаторСтроки = ТаблицаОборудование.ИдентификаторСтроки
		|ГДЕ
		|	ПоступлениеТоваровУслугОборудование.Ссылка = &Ссылка
		|	И ПоступлениеТоваровУслугОборудование.ПрослеживаемыйТовар";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ВТ_СуммыБезНДСПредварительная.Номенклатура КАК Номенклатура,
	|	ВТ_СуммыБезНДСПредварительная.СуммаБезНДС КАК СуммаБезНДС,
	|	ВТ_СуммыБезНДСПредварительная.ЭтоЗакупкаДляКомитента КАК ЭтоЗакупкаДляКомитента,
	|	ВТ_СуммыБезНДСПредварительная.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТ_СуммыБезНДС
	|ИЗ
	|	ВТ_СуммыБезНДСПредварительная КАК ВТ_СуммыБезНДСПредварительная
	|ГДЕ
	|	ВТ_СуммыБезНДСПредварительная.Номенклатура <> НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.ПрослеживаемыйКомплект
	|			ТОГДА ВТ_Прослеживаемость.Комплектующие
	|		ИНАЧЕ ВТ_Прослеживаемость.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВТ_Прослеживаемость.Количество КАК Количество,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
	|	ВТ_Прослеживаемость.КодОперации КАК КодОперации,
	|	ВТ_Прослеживаемость.ДокументОперации КАК ДокументОперации,
	|	ВТ_Прослеживаемость.Контрагент КАК Контрагент,
	|	ВТ_Прослеживаемость.ПериодОперации КАК ПериодОперации,
	|	ВТ_Прослеживаемость.НомерДокумента КАК НомерДокумента,
	|	ВТ_Прослеживаемость.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.ПрослеживаемыйКомплект
	|			ТОГДА ВТ_Прослеживаемость.СуммаБезНДС
	|		ИНАЧЕ ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВТ_Прослеживаемость.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_Прослеживаемость.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВТ_Прослеживаемость.ИдентификаторСтроки = ВТ_СуммыБезНДС.ИдентификаторСтроки
	|			И ВТ_Прослеживаемость.ЭтоЗакупкаДляКомитента = ВТ_СуммыБезНДС.ЭтоЗакупкаДляКомитента
	|ГДЕ
	|	ВТ_Прослеживаемость.РНПТ <> НЕОПРЕДЕЛЕНО
	|	И НЕ &ЭтоИмпорт
	|	И НЕ &ЭтоКомиссия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.Номенклатура КАК Номенклатура,
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВТ_Прослеживаемость.Комитент КАК Комитент,
	|	ВТ_Прослеживаемость.ВидЗапасов КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Комиссионер,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|	НЕОПРЕДЕЛЕНО КАК ОснованиеДляВозврата,
	|	ВТ_Прослеживаемость.Комплектующие КАК Комплектующие
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|ГДЕ
	|	ВТ_Прослеживаемость.РНПТ <> НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.Номенклатура,
	|	ВТ_Прослеживаемость.СтранаПроисхождения,
	|	ВТ_Прослеживаемость.Комитент,
	|	ВТ_Прослеживаемость.ВидЗапасов,
	|	ВТ_Прослеживаемость.РНПТ,
	|	ВТ_Прослеживаемость.Комплектующие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|ГДЕ
	|	ВТ_Прослеживаемость.РНПТ <> НЕОПРЕДЕЛЕНО";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#Область ПрослеживаемыйТовар

//Формирует текст запроса для определения прослеживаемого товара в документе
//
// Параметры:
// ДокументОснование - ДокументСсылка - Документ поступления товаров и услуг
// ДополнительныйРеквизит - Булево - Дополнительный реквизит. не используется в этом модуле
//
// Возвращаемые параметры:
// Строка - Текст сформированного запроса
//
Функция ТекстЗапросаТаблицаПрослеживаемыхТоваровПоОснованию() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента КАК ТабличнаяЧастьДокумента,
	|	РублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	РублевыеСуммыДокументовВВалюте.Всего КАК СуммаРуб,
	|	РублевыеСуммыДокументовВВалюте.НДС КАК СуммаНДСРуб,
	|	РублевыеСуммыДокументовВВалюте.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_РублевыеСуммыДокументовВВалюте
	|ИЗ
	|	РегистрСведений.РублевыеСуммыДокументовВВалюте КАК РублевыеСуммыДокументовВВалюте
	|ГДЕ
	|	РублевыеСуммыДокументовВВалюте.Регистратор = &Основание
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТабличнаяЧастьДокумента,
	|	НомерСтрокиДокумента,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ПоступлениеТоваровУслугТовары.Количество) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ РублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|					И ПоступлениеТоваровУслуг.СуммаВключаетНДС
	|				ТОГДА РублевыеСуммыДокументовВВалюте.СуммаРуб - РублевыеСуммыДокументовВВалюте.СуммаНДСРуб
	|			КОГДА НЕ РублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|				ТОГДА РублевыеСуммыДокументовВВалюте.СуммаРуб
	|			КОГДА ПоступлениеТоваровУслуг.СуммаВключаетНДС
	|				ТОГДА ПоступлениеТоваровУслугТовары.Сумма - ПоступлениеТоваровУслугТовары.СуммаНДС
	|			ИНАЧЕ ПоступлениеТоваровУслугТовары.Сумма
	|		КОНЕЦ) КАК Сумма,
	|	ПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ПоступлениеТоваровУслуг.Дата КАК ПериодСобытия,
	|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваров
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ПО ПоступлениеТоваровУслугТовары.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РублевыеСуммыДокументовВВалюте КАК РублевыеСуммыДокументовВВалюте
	|		ПО ПоступлениеТоваровУслугТовары.Ссылка = РублевыеСуммыДокументовВВалюте.Регистратор
	|			И ПоступлениеТоваровУслугТовары.НомерСтроки = РублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Товары))
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &Основание
	|	И ПоступлениеТоваровУслугТовары.ПрослеживаемыйТовар
	|	И ПоступлениеТоваровУслуг.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслуг.Организация,
	|	ПоступлениеТоваровУслуг.Дата,
	|	ПоступлениеТоваровУслугТовары.СтранаПроисхождения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугОборудование.Номенклатура,
	|	СУММА(ПоступлениеТоваровУслугОборудование.Количество),
	|	СУММА(ВЫБОР
	|			КОГДА НЕ РублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|					И ПоступлениеТоваровУслуг.СуммаВключаетНДС
	|				ТОГДА РублевыеСуммыДокументовВВалюте.СуммаРуб - РублевыеСуммыДокументовВВалюте.СуммаНДСРуб
	|			КОГДА НЕ РублевыеСуммыДокументовВВалюте.Регистратор ЕСТЬ NULL
	|				ТОГДА РублевыеСуммыДокументовВВалюте.СуммаРуб
	|			КОГДА ПоступлениеТоваровУслуг.СуммаВключаетНДС
	|				ТОГДА ПоступлениеТоваровУслугОборудование.Сумма - ПоступлениеТоваровУслугОборудование.СуммаНДС
	|			ИНАЧЕ ПоступлениеТоваровУслугОборудование.Сумма
	|		КОНЕЦ),
	|	ПоступлениеТоваровУслуг.Организация,
	|	ПоступлениеТоваровУслуг.Дата,
	|	ПоступлениеТоваровУслугОборудование.СтранаПроисхождения
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ПО ПоступлениеТоваровУслугОборудование.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РублевыеСуммыДокументовВВалюте КАК РублевыеСуммыДокументовВВалюте
	|		ПО ПоступлениеТоваровУслугОборудование.Ссылка = РублевыеСуммыДокументовВВалюте.Регистратор
	|			И ПоступлениеТоваровУслугОборудование.НомерСтроки = РублевыеСуммыДокументовВВалюте.НомерСтрокиДокумента
	|			И (РублевыеСуммыДокументовВВалюте.ТабличнаяЧастьДокумента = ЗНАЧЕНИЕ(Перечисление.ТабличныеЧастиДокументов.Оборудование))
	|ГДЕ
	|	ПоступлениеТоваровУслугОборудование.Ссылка = &Основание
	|	И ПоступлениеТоваровУслугОборудование.ПрослеживаемыйТовар
	|	И ПоступлениеТоваровУслуг.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровУслугОборудование.Номенклатура,
	|	ПоступлениеТоваровУслуг.Организация,
	|	ПоступлениеТоваровУслуг.Дата,
	|	ПоступлениеТоваровУслугОборудование.СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураСправочник.КодТНВЭД КАК ТНВЭД,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.Сумма КАК Сумма,
	|	ТаблицаТоваров.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРегистрацииПрослеживаемыхТоваров.ВвозТоваровИзЕАЭС) КАК ВидОперации,
	|	ТаблицаТоваров.ПериодСобытия КАК ПериодСобытия,
	|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
	|	&ПустаяСсылкаРнпт КАК РНПТ,
	|	NULL КАК ПервичныйДокумент,
	|	ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка) КАК ОсновноеСредство,
	|	0 КАК ВсегоКоличествоКомплектующих,
	|	0 КАК ВсегоКомлектующихНаСумму
	|ПОМЕСТИТЬ ВТ_ТоварыПоОснованию
	|ИЗ
	|	ВТ_ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО ТаблицаТоваров.Номенклатура = НоменклатураСправочник.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаТоваров.Количество КАК Количество,
	|	ВТ_ТаблицаТоваров.Сумма КАК Сумма,
	|	ВТ_ТаблицаТоваров.Организация КАК Организация,
	|	ВТ_ТаблицаТоваров.ПериодСобытия КАК ПериодСобытия,
	|	ЛОЖЬ КАК РанееРегистрировалсяВПрослеживаемости,
	|	0 КАК КоличествоУчет,
	|	ВТ_ТаблицаТоваров.ТНВЭД КАК ТНВЭД,
	|	ВТ_ТаблицаТоваров.ВидОперации КАК ВидОперации,
	|	ВТ_ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровРазмеченный
	|ИЗ
	|	ВТ_ТоварыПоОснованию КАК ВТ_ТаблицаТоваров";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Функция ТекстЗапросаПрослеживаемыеОС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьОсновныеСредства
		ИЛИ Не Реквизиты.ВедетсяУчетПрослеживаемыхТоваров Тогда
		ПараметрыПроведения.Вставить("ВТ_Прослеживаемость",                Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДСПредварительная",      Неопределено);
		ПараметрыПроведения.Вставить("ВТ_СуммыБезНДС",                     Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОперацииОС",           Неопределено);
		ПараметрыПроведения.Вставить("ПрослеживаемыеОС",                   Неопределено);
		ПараметрыПроведения.Вставить("РНПТОС",                             Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВТ_Прослеживаемость",                НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДСПредварительная",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СуммыБезНДС",                     НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОперацииОС",           НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрослеживаемыеОС",                   НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РНПТОС",                             НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство КАК ОС,
	|	СУММА(ПоступлениеТоваровУслугСведенияПрослеживаемости.Количество) КАК Количество,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	СУММА(ПоступлениеТоваровУслугСведенияПрослеживаемости.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВЫБОР
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				И НЕ &БезвозмездноеПоступление
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПокупок)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|	КОНЕЦ КАК ОтражениеВОтчетности,
	|	ВЫБОР
	|		КОГДА &БезвозмездноеПоступление
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.БезвозмездноеПолучение)
	|		КОГДА &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Приобретение)
	|	КОНЕЦ КАК КодОперации,
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка КАК ДокументОперации,
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Контрагент КАК Контрагент,
	|	НАЧАЛОПЕРИОДА(ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Дата, КВАРТАЛ) КАК ПериодОперации,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """") КАК НомерДокумента,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ &БезвозмездноеПоступление
	|			ТОГДА ВЫБОР
	|					КОГДА ПоступлениеТоваровУслугОсновныеСредства.Ссылка.ЭтоУниверсальныйДокумент
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ТипДокументаВПрослеживаемости,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Номенклатура КАК Комплектующие,
	|	СУММА(ПоступлениеТоваровУслугСведенияПрослеживаемости.Сумма) КАК Сумма,
	|	ЛОЖЬ КАК ЭтоЗакупкаДляКомитента,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ГрупповойОбъект,
	|	ЛОЖЬ КАК ПрослеживаемыйКомплект
	|ПОМЕСТИТЬ ВТ_Прослеживаемость
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ПоступлениеТоваровУслугОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.СведенияПрослеживаемости КАК ПоступлениеТоваровУслугСведенияПрослеживаемости
	|		ПО ПоступлениеТоваровУслугОсновныеСредства.Ссылка = ПоступлениеТоваровУслугСведенияПрослеживаемости.Ссылка
	|			И ПоступлениеТоваровУслугОсновныеСредства.ИдентификаторСтроки = ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО (НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ &БезвозмездноеПоступление)
	|			И ПоступлениеТоваровУслугОсновныеСредства.Ссылка = ДанныеПервичныхДокументов.Документ
	|			И ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Организация = ДанныеПервичныхДокументов.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ПО ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство = ОсновныеСредства.Ссылка
	|ГДЕ
	|	ПоступлениеТоваровУслугОсновныеСредства.ПрослеживаемыйТовар
	|	И ПоступлениеТоваровУслугОсновныеСредства.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Номер, """"),
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.Дата, ДАТАВРЕМЯ(1, 1, 1)),
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.Номенклатура,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.РНПТ,
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.СтранаПроисхождения,
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство,
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Контрагент,
	|	НАЧАЛОПЕРИОДА(ПоступлениеТоваровУслугОсновныеСредства.Ссылка.Дата, КВАРТАЛ),
	|	ВЫБОР
	|		КОГДА НЕ &ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС
	|				ИЛИ &БезвозмездноеПоступление
	|			ТОГДА ВЫБОР
	|					КОГДА ПоступлениеТоваровУслугОсновныеСредства.Ссылка.ЭтоУниверсальныйДокумент
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.УПД)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная)
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслугСведенияПрослеживаемости.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровУслугОсновныеСредства.ОсновноеСредство КАК ОС,
	|	ТаблицаОсновныеСредства.СуммаРуб - ТаблицаОсновныеСредства.СуммаНДСРуб КАК СуммаБезНДС,
	|	ПоступлениеТоваровУслугОсновныеСредства.СчетУчета.Забалансовый КАК ЭтоЗакупкаДляКомитента
	|ПОМЕСТИТЬ ВТ_СуммыБезНДСПредварительная
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ПоступлениеТоваровУслугОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОсновныеСредства КАК ТаблицаОсновныеСредства
	|		ПО ПоступлениеТоваровУслугОсновныеСредства.НомерСтроки = ТаблицаОсновныеСредства.НомерСтроки
	|ГДЕ
	|	ПоступлениеТоваровУслугОсновныеСредства.Ссылка = &Ссылка
	|	И ПоступлениеТоваровУслугОсновныеСредства.ПрослеживаемыйТовар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СуммыБезНДСПредварительная.ОС КАК ОС,
	|	СУММА(ВТ_СуммыБезНДСПредварительная.СуммаБезНДС) КАК СуммаБезНДС
	|ПОМЕСТИТЬ ВТ_СуммыБезНДС
	|ИЗ
	|	ВТ_СуммыБезНДСПредварительная КАК ВТ_СуммыБезНДСПредварительная
	|ГДЕ
	|	ВТ_СуммыБезНДСПредварительная.ОС <> НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СуммыБезНДСПредварительная.ОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.Комплектующие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|			ТОГДА ВТ_Прослеживаемость.ОС
	|		ИНАЧЕ ВТ_Прослеживаемость.Комплектующие
	|	КОНЕЦ КАК Номенклатура,
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.ОтражениеВОтчетности КАК ОтражениеВОтчетности,
	|	ВТ_Прослеживаемость.КодОперации КАК КодОперации,
	|	ВТ_Прослеживаемость.ДокументОперации КАК ДокументОперации,
	|	ВТ_Прослеживаемость.Контрагент КАК Контрагент,
	|	ВТ_Прослеживаемость.ПериодОперации КАК ПериодОперации,
	|	ВТ_Прослеживаемость.НомерДокумента КАК НомерДокумента,
	|	ВТ_Прослеживаемость.ДатаДокумента КАК ДатаДокумента,
	|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости КАК ТипДокументаВПрослеживаемости,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_Прослеживаемость.Сумма = 0
	|				ТОГДА ЕСТЬNULL(ВТ_СуммыБезНДС.СуммаБезНДС, 0)
	|			ИНАЧЕ ВТ_Прослеживаемость.Сумма
	|		КОНЕЦ) КАК СуммаБезНДС,
	|	ВТ_Прослеживаемость.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВТ_Прослеживаемость.ГрупповойОбъект КАК ГрупповойОбъект,
	|	ВТ_Прослеживаемость.ПрослеживаемыйКомплект КАК ПрослеживаемыйКомплект
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВТ_Прослеживаемость.ОС = ВТ_СуммыБезНДС.ОС
	|ГДЕ
	|	ВТ_Прослеживаемость.РНПТ <> НЕОПРЕДЕЛЕНО
	|	И НЕ &ЭтоИмпорт
	|	И НЕ &ЭтоКомиссия
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.РНПТ,
	|	ВТ_Прослеживаемость.ОтражениеВОтчетности,
	|	ВТ_Прослеживаемость.КодОперации,
	|	ВТ_Прослеживаемость.ДокументОперации,
	|	ВТ_Прослеживаемость.Контрагент,
	|	ВТ_Прослеживаемость.ПериодОперации,
	|	ВТ_Прослеживаемость.НомерДокумента,
	|	ВТ_Прослеживаемость.ДатаДокумента,
	|	ВТ_Прослеживаемость.ТипДокументаВПрослеживаемости,
	|	ВЫБОР
	|		КОГДА ВТ_Прослеживаемость.Комплектующие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяССылка)
	|			ТОГДА ВТ_Прослеживаемость.ОС
	|		ИНАЧЕ ВТ_Прослеживаемость.Комплектующие
	|	КОНЕЦ,
	|	ВТ_Прослеживаемость.ИдентификаторСтроки,
	|	ВТ_Прослеживаемость.ГрупповойОбъект,
	|	ВТ_Прослеживаемость.ПрослеживаемыйКомплект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Прослеживаемость.ОС КАК ОсновноеСредство,
	|	СУММА(ВТ_Прослеживаемость.Количество) КАК Количество,
	|	ВТ_Прослеживаемость.СтранаПроисхождения КАК СтранаПроисхождения,
	|	СУММА(ВТ_Прослеживаемость.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ,
	|	ВТ_Прослеживаемость.Комплектующие КАК Комплектующие
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуммыБезНДС КАК ВТ_СуммыБезНДС
	|		ПО ВТ_Прослеживаемость.ОС = ВТ_СуммыБезНДС.ОС
	|ГДЕ
	|	ВТ_Прослеживаемость.РНПТ <> НЕОПРЕДЕЛЕНО
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Прослеживаемость.ОС,
	|	ВТ_Прослеживаемость.СтранаПроисхождения,
	|	ВТ_Прослеживаемость.РНПТ,
	|	ВТ_Прослеживаемость.Комплектующие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Прослеживаемость.РНПТ КАК РНПТ
	|ИЗ
	|	ВТ_Прослеживаемость КАК ВТ_Прослеживаемость
	|ГДЕ
	|	ВТ_Прослеживаемость.РНПТ <> НЕОПРЕДЕЛЕНО";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаАктОПринятииКУчетуОС()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Номер КАК НомерДокумента,
	|	ПоступлениеТоваровУслуг.Дата КАК ДатаДокумента,
	|	ПоступлениеТоваровУслуг.Организация КАК Организация,
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.ПодразделениеОрганизации.Наименование
	|		ИНАЧЕ ПоступлениеТоваровУслуг.ПодразделениеОрганизации.НаименованиеПолное
	|	КОНЕЦ КАК ПредставлениеПодразделения,
	|	ПоступлениеТоваровУслуг.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.Контрагент.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПоступлениеТоваровУслуг.Контрагент.Наименование
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Контрагент.НаименованиеПолное
	|	КОНЕЦ КАК КонтрагентПредставление,
	|	ПоступлениеТоваровУслуг.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ПоступлениеТоваровУслуг.МОЛ КАК МОЛ,
	|	ПоступлениеТоваровУслуг.ОсновныеСредства.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ОсновноеСредство КАК ОсновноеСредство,
	|		ВЫБОР
	|			КОГДА ПоступлениеТоваровУслуг.ОсновныеСредства.ИнвентарныйНомер ПОДОБНО """"
	|				ТОГДА ""-""
	|			ИНАЧЕ ПоступлениеТоваровУслуг.ОсновныеСредства.ИнвентарныйНомер
	|		КОНЕЦ КАК ОСКод,
	|		ВЫБОР
	|			КОГДА ПоступлениеТоваровУслуг.ОсновныеСредства.ОсновноеСредство.НаименованиеПолное ПОДОБНО """"
	|				ТОГДА ПоступлениеТоваровУслуг.ОсновныеСредства.ОсновноеСредство.Наименование
	|			ИНАЧЕ ПоступлениеТоваровУслуг.ОсновныеСредства.ОсновноеСредство.НаименованиеПолное
	|		КОНЕЦ КАК ОСНаименование,
	|		&ОсновнаяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Количество КАК Количество,
	|		СрокПолезногоИспользования КАК СрокПолезногоИспользования
	|	) КАК ОсновныеСредства
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоступлениеТоваровУслуг.Дата,
	|	ПоступлениеТоваровУслуг.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Проводки01.Регистратор КАК Регистратор,
	|	Проводки01.НомерСтроки КАК НомерСтроки,
	|	Проводки01.Сумма КАК Сумма,
	|	ЕСТЬNULL(Проводки01.КоличествоДт, 0) КАК Количество
	|ПОМЕСТИТЬ Проводки01
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Проводки01
	|ГДЕ
	|	Проводки01.Регистратор В(&МассивОбъектов)
	|	И Проводки01.СчетДт В(&Счета01)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Проводки01.Регистратор КАК Регистратор,
	|	Субконто01.Значение КАК ОсновноеСредство,
	|	Проводки01.Сумма КАК Сумма,
	|	Проводки01.Количество КАК Количество
	|ПОМЕСТИТЬ ДанныеПоОС
	|ИЗ
	|	Проводки01 КАК Проводки01
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК Субконто01
	|		ПО Проводки01.Регистратор = Субконто01.Регистратор
	|			И Проводки01.НомерСтроки = Субконто01.НомерСтроки
	|			И (Субконто01.Вид = &ВидСубконтоОС)
	|			И (Субконто01.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоОС.Регистратор КАК Ссылка,
	|	ДанныеПоОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ДанныеПоОС.Сумма) КАК ПервоначальнаяСтоимость,
	|	СУММА(ДанныеПоОС.Количество) КАК Количество
	|ИЗ
	|	ДанныеПоОС КАК ДанныеПоОС
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоОС.Регистратор,
	|	ДанныеПоОС.ОсновноеСредство";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПечатьАктОПринятииКУчетуОС(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АктОПринятииКУчетуОС";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваровУслуг.ПФ_MXL_АктОПринятииКУчетуОС");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = ТекстЗапросаАктОПринятииКУчетуОС();
	ОсновнаяЕдиницаИзмерения = Справочники.КлассификаторЕдиницИзмерения.ПолучитьЕдиницуИзмеренияПоУмолчанию();
	Запрос.УстановитьПараметр("ОсновнаяЕдиницаИзмерения", ОсновнаяЕдиницаИзмерения);
	Счета01 = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	Запрос.УстановитьПараметр("Счета01", Счета01);
	Запрос.УстановитьПараметр("ВидСубконтоОС", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаСумм = РезультатыЗапроса[3].Выгрузить();
	ТаблицаСумм.Индексы.Добавить("Ссылка,ОсновноеСредство");
	
	ПервыйДокумент = Истина;
	ВыборкаДокумент = РезультатыЗапроса[0].Выбрать();
	Пока ВыборкаДокумент.Следующий() Цикл
		
		ОсновныеСредства = ВыборкаДокумент.ОсновныеСредства.Выгрузить();
		ОсновныеСредства.Сортировать("НомерСтроки");
		
		Если ОсновныеСредства.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		
		ОбластьМакета.Параметры.Заполнить(ВыборкаДокумент);
		
		ПараметрыЗаполнения = Новый Структура;
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			ВыборкаДокумент.Организация, ВыборкаДокумент.ДатаДокумента);
		ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации);
		ПараметрыЗаполнения.Вставить("ПредставлениеОрганизации", ПредставлениеОрганизации);
		
		НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДокумент.НомерДокумента, Истина, Ложь);
		ПараметрыЗаполнения.Вставить("НомерДокумента", НомерДокумента);
		ДатаДокумента = Формат(ВыборкаДокумент.ДатаДокумента, "ДФ='dd.MM.yyyy ""г.""'");
		ПараметрыЗаполнения.Вставить("ДатаДокумента", ДатаДокумента);
		
		ПодразделениеОтветственныхЛиц = ВыборкаДокумент.ПодразделениеОрганизации;
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(
			ВыборкаДокумент.Организация, ВыборкаДокумент.ДатаДокумента, ПодразделениеОтветственныхЛиц);
		ФИОРуководителя = Руководители.РуководительПредставление;
		ПараметрыЗаполнения.Вставить("ФИОРуководителя", ФИОРуководителя);
		
		ЭтоФизЛицо = СведенияОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		Если ЭтоФизЛицо Тогда
			СтатусФизическогоЛица = Справочники.Организации.СтатусФизическогоЛицаНаДату(
				ВыборкаДокумент.Организация, ВыборкаДокумент.ДатаДокумента);
		Иначе
			СтатусФизическогоЛица = Неопределено;
		КонецЕсли;
		
		ЭтоСамозанятый = СтатусФизическогоЛица = Перечисления.СтатусыФизическихЛиц.Самозанятый;
		
		ЭтоИндивидуальныйПредприниматель = 
			СведенияОбОрганизации.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
			И СведенияОбОрганизации.ИндивидуальныйПредприниматель = Руководители.Руководитель;
		
		Если ЭтоСамозанятый Тогда
			ДолжностьРуководителя = "";
		Иначе
			ДолжностьРуководителя = ?(ЭтоИндивидуальныйПредприниматель,
				НСтр("ru = 'Индивидуальный предприниматель'"),
				Руководители.РуководительДолжностьПредставление);
		КонецЕсли;
		ПараметрыЗаполнения.Вставить("ДолжностьРуководителя", ДолжностьРуководителя);
		
		КонтрагентПредставление = ВыборкаДокумент.КонтрагентПредставление;
		ПараметрыЗаполнения.Вставить("КонтрагентПредставление", КонтрагентПредставление);
		
		Если ЗначениеЗаполнено(ВыборкаДокумент.ДатаВходящегоДокумента) 
			И ЗначениеЗаполнено(ВыборкаДокумент.НомерВходящегоДокумента) Тогда
			НомерВходящегоДокумента = ВыборкаДокумент.НомерВходящегоДокумента;
			ДатаВходящегоДокумента = Формат(ВыборкаДокумент.ДатаВходящегоДокумента, "ДФ='dd.MM.yyyy ""г.""'");
			ДокументПредставление = СтрШаблон(НСтр("ru='№ %1 от %2'"), НомерВходящегоДокумента, ДатаВходящегоДокумента);
		Иначе
			ДокументПредставление = "";
		КонецЕсли;
		ПараметрыЗаполнения.Вставить("ДокументПредставление", ДокументПредставление);
		
		ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		ИтогоПервоначальнаяСтоимость = 0;
		
		Для каждого СтрокаОС Из ОсновныеСредства Цикл
			
			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("НомерСтроки", СтрокаОС.НомерСтроки);
			ПараметрыЗаполнения.Вставить("ОСНаименование", СтрокаОС.ОСНаименование);
			ПараметрыЗаполнения.Вставить("ОСКод", СтрокаОС.ОСКод);
			ПараметрыЗаполнения.Вставить("ЕдиницаИзмерения", СтрокаОС.ЕдиницаИзмерения);
			ПараметрыЗаполнения.Вставить("Количество", СтрокаОС.Количество);
			ПараметрыЗаполнения.Вставить("СрокПолезногоИспользования", СтрокаОС.СрокПолезногоИспользования);
			
			ОтборСтрок = Новый Структура("Ссылка,ОсновноеСредство", СтрокаОС.Ссылка, СтрокаОС.ОсновноеСредство);
			СтрокиСумм = ТаблицаСумм.НайтиСтроки(ОтборСтрок);
			Если СтрокиСумм.Количество() = 0 Тогда
				ПервоначальнаяСтоимость = 0;
			Иначе
				Коэффициент = ?(СтрокиСумм[0].Количество = 0, 1, СтрокаОС.Количество / СтрокиСумм[0].Количество);
				ПервоначальнаяСтоимость = Окр(СтрокиСумм[0].ПервоначальнаяСтоимость * Коэффициент, 2);
			КонецЕсли;
			ПараметрыЗаполнения.Вставить("ПервоначальнаяСтоимость", ПервоначальнаяСтоимость);
			
			ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			ИтогоПервоначальнаяСтоимость = ИтогоПервоначальнаяСтоимость + ПервоначальнаяСтоимость;
			
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("КонецТаблицы");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим итоги по документу
		ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("ИтогоПервоначальнаяСтоимость", ИтогоПервоначальнаяСтоимость);
		ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим подпись МОЛ
		ОбластьМакета = Макет.ПолучитьОбласть("ПодписьМОЛ");
		Если ЗначениеЗаполнено(ВыборкаДокумент.МОЛ) Тогда
			ПараметрыЗаполнения = Новый Структура;
			ДанныеМОЛ = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(
				ВыборкаДокумент.Организация, ВыборкаДокумент.МОЛ, ВыборкаДокумент.ДатаДокумента);
			ДолжностьМОЛ = ДанныеМОЛ.Должность;
			ФИОМОЛ = ДанныеМОЛ.Представление;
			ПараметрыЗаполнения.Вставить("ДолжностьМОЛ", ДолжностьМОЛ);
			ПараметрыЗаполнения.Вставить("ФИОМОЛ", ФИОМОЛ);
			ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
		КонецЕсли;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим подпись главного бухгалтера
		Если Не ЭтоИндивидуальныйПредприниматель Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ПодписьГлавногоБухгалтера");
			ПараметрыЗаполнения = Новый Структура;
			ФИОГлавногоБухгалтера = Руководители.ГлавныйБухгалтерПредставление;
			ПараметрыЗаполнения.Вставить("ФИОГлавногоБухгалтера", ФИОГлавногоБухгалтера);
			ОбластьМакета.Параметры.Заполнить(ПараметрыЗаполнения);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокумент.Ссылка);
			
	КонецЦикла;
	
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие("ПоступлениеТоваровУслуг.Печать.АктОПринятииКУчетуОС");
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция ПодготовитьТаблицуВзаиморасчетыКомиссия(ТаблицаВзаиморасчетов, ЗачетАвансовРеквизиты, Отказ) Экспорт
	ТаблицаВзаиморасчетыКомиссия = УчетВзаиморасчетов.ПустаяТаблицаВзаиморасчетовЗачетАвансов();
	Если НЕ ЗначениеЗаполнено(ЗачетАвансовРеквизиты) Тогда
		Возврат ТаблицаВзаиморасчетыКомиссия;
	КонецЕсли; 
	
	Реквизиты = ЗачетАвансовРеквизиты[0];
	
	Для каждого СтрокаВзаиморасчеты Из ТаблицаВзаиморасчетов Цикл
		Если СтрокаВзаиморасчеты.СчетАвансов = Реквизиты.СчетУчетаРасчетовСКомиссионером 
			И СтрокаВзаиморасчеты.ДокументРасчетов <> Реквизиты.Регистратор Тогда
		
			ЗаполнитьЗначенияСвойств(ТаблицаВзаиморасчетыКомиссия.Добавить(), СтрокаВзаиморасчеты);
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат ТаблицаВзаиморасчетыКомиссия;
КонецФункции

Функция ТекстЗапросаЗачетАвансовКомиссияПоПродаже(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Реквизиты.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		ПараметрыПроведения.Вставить("ЗачетВыручкиКомиссионераРеквизиты",        Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ЗачетВыручкиКомиссионераРеквизиты",        НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СпособЗачетаАвансов КАК СпособЗачетаАвансов,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.СчетУчетаРасчетовСКомиссионером КАК СчетУчетаРасчетовСКомиссионером,
	|	&ПрименяетсяУСН КАК УчитыватьЗадолженностьУСН,
	|	&ПрименяетсяПСН КАК УчитыватьЗадолженностьУСНПатент,
	|	ЛОЖЬ КАК ДеятельностьНаПатенте,
	|	ЛОЖЬ КАК ДеятельностьНаТорговомСборе,
	|	""Поступление"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	Реквизиты.НДСВключенВСтоимость,
	|	&УчетАгентскогоНДС КАК УчетАгентскогоНДС
	|ИЗ
	|	Реквизиты КАК Реквизиты" 
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Если Реквизиты.СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		ПараметрыПроведения.Вставить("ТаблицаЗачетАвансовВыручкаКомиссионера", Неопределено);
	Иначе
		НомераТаблиц.Вставить("ТаблицаЗачетАвансовВыручкаКомиссионера", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса
		+ "ВЫБРАТЬ
		|	ПоступлениеТоваровУслугЗачетАвансов.НомерСтроки КАК НомерСтроки,
		|	Реквизиты.СчетУчетаРасчетовСКомиссионером КАК СчетАвансов,
		|	Реквизиты.Контрагент,
		|	Реквизиты.ДоговорКонтрагента,
		|	ПоступлениеТоваровУслугЗачетАвансов.ДокументАванса,
		|	ПоступлениеТоваровУслугЗачетАвансов.СуммаЗачета
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.ЗачетАвансов КАК ПоступлениеТоваровУслугЗачетАвансов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ПоступлениеТоваровУслугЗачетАвансов.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
КонецФункции

// Определяет есть ли в документе позиции по ставкам НДС по УСН
//
// Параметры:
// ТекущийДокумент - документ, ставку по которому надо проверить
// СтавкаНДСПоУСН - искомая ставка НДС по УСН
//
// Возвращаемое значение:
// Булево - если есть позиции по искомой ставке НДС по УСН.
Функция НаличиеСтавкиНДСПоУСН(ТекущийДокумент, СтавкаНДСПоУСН) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СтавкаНДС В (&СтавкаНДСПоУСН)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтавкаНДСПоУСН,
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаНДС
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СтавкаНДС В (&СтавкаНДСПоУСН)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СтавкаНДС В (&СтавкаНДСПоУСН)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СтавкаНДС В (&СтавкаНДСПоУСН)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СтавкаНДС В (&СтавкаНДСПоУСН)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СтавкаНДС В (&СтавкаНДСПоУСН)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СтавкаНДС В (&СтавкаНДСПоУСН)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Топливо КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СтавкаНДС В (&СтавкаНДСПоУСН)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПоступлениеТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Права КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ссылка = &СсылкаНаДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНДС.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ТаблицаНДС.СтавкаНДСПоУСН) КАК СтавкаНДСПоУСН
	|ИЗ
	|	ТаблицаНДС КАК ТаблицаНДС
	|ГДЕ
	|	ТаблицаНДС.СтавкаНДСПоУСН = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНДС.Ссылка";

	Запрос.УстановитьПараметр("СсылкаНаДокумент", ТекущийДокумент);
	Запрос.УстановитьПараметр("СтавкаНДСПоУСН",   СтавкаНДСПоУСН);
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		Возврат Результат.СтавкаНДСПоУСН = Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

// Список операций, при которых возможно применение инвестиционного вычета.
// 
// Возвращаемое значение:
//  СписокЗначений из ПеречислениеСсылка.ВидыОперацийПоступлениеТоваровУслуг
//
Функция СписокОперацийПрименяетсяИнвестиционныйВычет()
	
	СписокОпераций = Новый СписокЗначений;
	СписокОпераций.Добавить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства);
	Возврат СписокОпераций;
	
КонецФункции

// Функция выполняет расчет рублевых сумм для вывода таблиц документа на печать
//Параметры: 
//			-ВыборкаСтрок Тип("Массив")
//			- ТаблицаПоТоварам Тип("ТаблицаЗначений")
//
//Возвращаемое значение: Нет
//
Процедура ПодготовитьТаблицуДокументаДляПечатиМ4(ВыборкаСтрок, ТаблицаПоТоварам)

	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	СуммаВзаиморасчетов 	= 0; 
	СуммаВзаиморасчетовНДС 	= 0; 
	ТаблицаПоТоварам.Очистить();
	
	НуженПересчетВРубли = Ложь;
	
	Если ВыборкаСтрок[0].Проведен 
		И (ВыборкаСтрок[0].РасчетыВУсловныхЕдиницах
		ИЛИ (ВыборкаСтрок[0].ВалютаДокумента <> ВалютаРегламентированногоУчета И ВыборкаСтрок[0].Дата >= '20090101000000')) Тогда
		НуженПересчетВРубли = Истина;
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из ВыборкаСтрок Цикл	
		
		СтрокаТаблицы = ТаблицаПоТоварам.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаТовары);
		
		Если НуженПересчетВРубли Тогда
			
			СтрокаТаблицы.СуммаНДС = ?(СтрокаТаблицы.НДСРуб = 0, СтрокаТаблицы.СуммаНДС, СтрокаТаблицы.НДСРуб);
			
			// В случае если сумма включает НДС, то сумма без НДС будет рассчитана позже
			Если СтрокаТаблицы.СуммаВключаетНДС Тогда
				СтрокаТаблицы.Сумма = ?(СтрокаТаблицы.ВсегоРуб = 0, СтрокаТаблицы.Сумма, СтрокаТаблицы.ВсегоРуб);
			// Для валютных документов для Суммы необходимо применять средний курс, 
			// так как сумма документа рассчитывается по курсу аванса,
			// а сумма НДС по курсу текущего документа
			ИначеЕсли НЕ СтрокаТовары.РасчетыВУсловныхЕдиницах Тогда
				СуммаРуб            = ?(СтрокаТаблицы.ВсегоРуб = 0, СтрокаТаблицы.Сумма, СтрокаТаблицы.ВсегоРуб);
				СтрокаТаблицы.Сумма = СуммаРуб - СтрокаТаблицы.СуммаНДС;
			// Для условных единиц и сумма НДС и сумма документа рассчитывается по курсу аванса
			Иначе
				СтрокаТаблицы.Сумма = ?(СтрокаТаблицы.СуммаБезНДСРуб = 0, СтрокаТаблицы.Сумма, СтрокаТаблицы.СуммаБезНДСРуб);
			КонецЕсли; 
			
			СтрокаТаблицы.Цена      = ?(СтрокаТаблицы.Количество = 0, 0, СтрокаТаблицы.Сумма/СтрокаТаблицы.Количество);
			
		КонецЕсли;	
		
	КонецЦикла;

	// Исключим строки из табличный частей, которые не удовлетворяют условиям печати. При выборке запросом их необходимо получать, 
	// чтобы распределять сумма взаиморасчетов в рублях на все строки документа.
	Инд = ТаблицаПоТоварам.Количество() - 1;
	Пока Инд >= 0 Цикл
		СтрокаТовар = ТаблицаПоТоварам[Инд];
		Если СтрокаТовар.ID <> 2 Тогда
			ТаблицаПоТоварам.Удалить(Инд);
		КонецЕсли;
		Инд = Инд - 1;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаменитьВидыОперацийНаПростыеПриПереходеС20Отложенно(Параметры) Экспорт

	Запрос = Новый Запрос;
	
	КонецПериодаВыборки = '20191231235959';
	
	Если Параметры.Свойство("КонецПериодаВыборки")
		И ТипЗнч(Параметры.КонецПериодаВыборки) = Тип("Дата") Тогда
		КонецПериодаВыборки = Параметры.КонецПериодаВыборки;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("КонецПериодаВыборки", 	КонецПериодаВыборки);	
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ПоступлениеТоваровУслуг.Дата
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Дата <= &КонецПериодаВыборки
	|	И ПоступлениеТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия)
	|	И ПоступлениеТоваровУслуг.ДоговорКонтрагента.ВидДоговора В (ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком), ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоступлениеТоваровУслуг.Дата УБЫВ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПоступлениеТоваровУслуг.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Ссылка,
	|	ТаблицаДокументов.Дата,
	|	СУММА(ТаблицаДокументов.ЕстьОборудование) КАК ЕстьОборудование,
	|	СУММА(ТаблицаДокументов.ЕстьОбъектыСтроительства) КАК ЕстьОбъектыСтроительства,
	|	СУММА(ТаблицаДокументов.ЕстьТовары) КАК ЕстьТовары,
	|	СУММА(ТаблицаДокументов.ЕстьВозвратнаяТара) КАК ЕстьВозвратнаяТара,
	|	СУММА(ТаблицаДокументов.ЕстьУслуги) КАК ЕстьУслуги,
	|	СУММА(ТаблицаДокументов.ЕстьАгентскиеУслуги) КАК ЕстьАгентскиеУслуги,
	|	СУММА(ТаблицаДокументов.ЕстьКомитентыНаЗакупку) КАК ЕстьКомитентыНаЗакупку
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_Документы.Ссылка КАК Ссылка,
	|		ВТ_Документы.Дата КАК Дата,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0) КАК ЕстьОборудование,
	|		0 КАК ЕстьОбъектыСтроительства,
	|		0 КАК ЕстьТовары,
	|		0 КАК ЕстьВозвратнаяТара,
	|		0 КАК ЕстьУслуги,
	|		0 КАК ЕстьАгентскиеУслуги,
	|		0 КАК ЕстьКомитентыНаЗакупку
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Оборудование КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Документы.Ссылка,
	|		ВТ_Документы.Дата,
	|		0,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0),
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.ОбъектыСтроительства КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Документы.Ссылка,
	|		ВТ_Документы.Дата,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА СтрокиТЧ.НомерСтроки = 1
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		0,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА СтрокиТЧ.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Документы.Ссылка,
	|		ВТ_Документы.Дата,
	|		0,
	|		0,
	|		0,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0),
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.ВозвратнаяТара КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Документы.Ссылка,
	|		ВТ_Документы.Дата,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0),
	|		0,
	|		0
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Услуги КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Документы.Ссылка,
	|		ВТ_Документы.Дата,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ЕСТЬNULL(СтрокиТЧ.НомерСтроки, 0),
	|		0
	|	ИЗ
	|		ВТ_Документы КАК ВТ_Документы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.АгентскиеУслуги КАК СтрокиТЧ
	|			ПО ВТ_Документы.Ссылка = СтрокиТЧ.Ссылка
	|				И (СтрокиТЧ.НомерСтроки = 1)) КАК ТаблицаДокументов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументов.Ссылка,
	|	ТаблицаДокументов.Дата
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДокументов.ЕстьОборудование) + СУММА(ТаблицаДокументов.ЕстьОбъектыСтроительства) + СУММА(ТаблицаДокументов.ЕстьТовары) + СУММА(ТаблицаДокументов.ЕстьВозвратнаяТара) + СУММА(ТаблицаДокументов.ЕстьУслуги) + СУММА(ТаблицаДокументов.ЕстьАгентскиеУслуги) = 1 И
	|	СУММА(ТаблицаДокументов.ЕстьКомитентыНаЗакупку) = 0 И
	|	(СУММА(ТаблицаДокументов.ЕстьТовары) = 1
	|		ИЛИ СУММА(ТаблицаДокументов.ЕстьУслуги) = 1)";

	УстановитьПривилегированныйРежим(Истина);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	ВыборкаПоДокументам = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Попытка
			
			ЗаменитьВДокументеВидОперацииНаПростойПриПереходеС20(ВыборкаПоДокументам);
			ОбъектовОбработано = ОбъектовОбработано + 1;
			КонецПериодаВыборки = ВыборкаПоДокументам.Дата; // Запоминаем дату, с которой будем начинать в следующий раз.
			
		Исключение
			// Если не удалось обработать какой-либо документ, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать поступление товаров и услуг: %1 по причине:
					|%2'"),
					ВыборкаПоДокументам.Ссылка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.РеализацияТоваровУслуг, ВыборкаПоДокументам.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	// Запоминаем дату, с которой будем начинать в следующий раз.
	Параметры.Вставить("КонецПериодаВыборки", КонецПериодаВыборки);
	
	Если ОбъектовОбработано = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедуре ЗаменитьВидыОперацийНаПростыеПриПереходеС20
				|не удалось обработать некоторые поступления товаров и услуг (пропущены): %1'"),
				ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.РеализацияТоваровУслуг,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура ЗаменитьВидыОперацийНаПростыеПриПереходеС20
					|обработала очередную порцию поступлений товаров и услуг: %1 документов'"), ОбъектовОбработано));
	КонецЕсли;


КонецПроцедуры

Процедура ЗаменитьВДокументеВидОперацииНаПростойПриПереходеС20(ВыборкаПоДокументам)
	
	НачатьТранзакцию();
	Попытка
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПоступлениеТоваровУслуг");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаПоДокументам.Ссылка);
		Блокировка.Заблокировать();
		
		ДокументОбъект = ВыборкаПоДокументам.Ссылка.ПолучитьОбъект();
		
		// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
		Если ДокументОбъект = Неопределено Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		НовыйВидОперации = Неопределено;
		
		Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия Тогда
			Если ДокументОбъект.Товары.Количество() > 0
				И ДокументОбъект.Оборудование.Количество() = 0
				И ДокументОбъект.ОбъектыСтроительства.Количество() = 0
				И ДокументОбъект.ВозвратнаяТара.Количество() = 0
				И ДокументОбъект.Услуги.Количество() = 0
				И ДокументОбъект.АгентскиеУслуги.Количество() = 0 Тогда

				Если НЕ ЕстьКомитентыНаЗакупку(ДокументОбъект) Тогда
					НовыйВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары;
				КонецЕсли;
			
			ИначеЕсли ДокументОбъект.Товары.Количество() = 0
				И ДокументОбъект.Оборудование.Количество() = 0
				И ДокументОбъект.ОбъектыСтроительства.Количество() = 0
				И ДокументОбъект.ВозвратнаяТара.Количество() = 0
				И ДокументОбъект.Услуги.Количество() > 0
				И ДокументОбъект.АгентскиеУслуги.Количество() = 0 Тогда

				НовыйВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги;
				
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(НовыйВидОперации) Тогда
			// Документ уже обработан либо его данные перестали удовлетворять условию замены вида операции
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		ДокументОбъект.ВидОперации = НовыйВидОперации;
		
		// Запись обработанного объекта (без перепроведения).
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Устанавливаем счета расчетов с комитентом в документах поступления комиссионных товаров
// Если ранее счет учета в документе задан не был, а сейчас он задан в настройке счетов учета с контрагентами
Процедура ЗаполнитьСчетаРасчетовСКомитентами() Экспорт
	
	Запрос = Новый Запрос;
	
	// Получаем только документы поступления по комиссии на реализацию, в которых не заполнен счет расчетов.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка,
	|	ПоступлениеТоваровУслуг.Организация,
	|	ПоступлениеТоваровУслуг.Контрагент КАК Комитент,
	|	ПоступлениеТоваровУслуг.ДоговорКонтрагента КАК ДоговорКомиссии
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ПоступлениеТоваровУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом)
	|	И ПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	ВыборкаИзРезультатовЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаИзРезультатовЗапроса.Следующий() Цикл
		
		ДокументОбъект = ВыборкаИзРезультатовЗапроса.Ссылка.ПолучитьОбъект();
		
		Если ДокументОбъект = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СчетаУчетаРасчетовСКонтрагентом = БухгалтерскийУчетПереопределяемый.ПолучитьСчетаРасчетовСКонтрагентом(
			ВыборкаИзРезультатовЗапроса.Организация, ВыборкаИзРезультатовЗапроса.Комитент, ВыборкаИзРезультатовЗапроса.ДоговорКомиссии);
		
		Если СчетаУчетаРасчетовСКонтрагентом.Свойство("СчетРасчетовСКомитентом") 
			И ЗначениеЗаполнено(СчетаУчетаРасчетовСКонтрагентом.СчетРасчетовСКомитентом) Тогда
			
			ДокументОбъект.СчетУчетаРасчетовСКонтрагентом = СчетаУчетаРасчетовСКонтрагентом.СчетРасчетовСКомитентом;
			
			Попытка
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
				
			Исключение
				ШаблонСообщения = НСтр("ru = 'Не удалось записать документ.
                                        |%1'");
				
				ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(
					ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Ошибка, 
					Метаданные.Документы.ПоступлениеТоваровУслуг,
					ВыборкаИзРезультатовЗапроса.Ссылка, 
					ТекстСообщения);
			КонецПопытки;
			
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ЗаполнитьСведенияПрослеживаемостиПриПереходеС20() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДокументыКОбработке
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.УдалитьРНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеТоваровУслугОборудование.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК ПоступлениеТоваровУслугОборудование
	|ГДЕ
	|	ПоступлениеТоваровУслугОборудование.УдалитьРНПТ <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыКОбработке.Ссылка КАК Ссылка
	|ИЗ
	|	ДокументыКОбработке КАК ДокументыКОбработке";
	
	РезультатЗапроса = Запрос.Выполнить();;
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// Если есть документы с РНПТ, то установим константу "ВестиУчетПрослеживаемыхТоваров".
	Если НЕ Константы.ВестиУчетПрослеживаемыхТоваров.Получить() Тогда
		Константы.ВестиУчетПрослеживаемыхТоваров.Установить(Истина);
	КонецЕсли;
	
	ВыборкаПоДокументам = РезультатЗапроса.Выбрать();
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		ДокументОбъект = ВыборкаПоДокументам.Ссылка.ПолучитьОбъект();
		
		Если ДокументОбъект = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Обработаем табличные части "Товары" и "Оборудование"
		Для Каждого ИмяТабличнойЧасти Из СтрРазделить("Товары,Оборудование", ",") Цикл
			Для Каждого СтрокаДокумента Из ДокументОбъект[ИмяТабличнойЧасти] Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаДокумента.ИдентификаторСтроки) Тогда
					СтрокаДокумента.ИдентификаторСтроки = Новый УникальныйИдентификатор;
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаДокумента.УдалитьРНПТ) Тогда
					СтрокаДокумента.ПрослеживаемыйТовар = Истина;
					СтрокаСведенийПрослеживаемости       = ДокументОбъект.СведенияПрослеживаемости.Добавить();
					СтрокаСведенийПрослеживаемости.ИдентификаторСтроки        = СтрокаДокумента.ИдентификаторСтроки;
					СтрокаСведенийПрослеживаемости.РНПТ                       = СтрокаДокумента.УдалитьРНПТ;
					СтрокаСведенийПрослеживаемости.Количество                 = СтрокаДокумента.Количество;
					СтрокаСведенийПрослеживаемости.КоличествоПрослеживаемости = СтрокаДокумента.Количество;
					СтрокаДокумента.УдалитьРНПТ = Неопределено;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать документ: %1 по причине:
					|%2'"),
					ВыборкаПоДокументам.Ссылка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПоступлениеТоваровУслуг, ВыборкаПоДокументам.Ссылка, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПустыеКоличествоЦенаОсновныхСредств(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ПоступлениеОС.Ссылка КАК Ссылка,
	|	ПоступлениеОС.Дата КАК Дата
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОсновныеСредства КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеОС
	|		ПО ТаблицаОС.Ссылка = ПоступлениеОС.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК СправочникОС
	|		ПО ТаблицаОС.ОсновноеСредство = СправочникОС.Ссылка
	|ГДЕ
	|	(ТаблицаОС.Количество = 0
	|			ИЛИ ТаблицаОС.Цена = 0
	|				И ТаблицаОС.Сумма <> 0)
	|	И СправочникОС.ЕдиницаУчета <> ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ,
	|	Ссылка УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбъектовОбработано = ОбъектовОбработано + 1;
		
		НачатьТранзакцию();
		
		// Блокируем объект от изменения другими сеансами.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ПоступлениеТоваровУслуг");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		
		Попытка
		
			Блокировка.Заблокировать();
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Для Каждого СтрокаТЧ Из ДокументОбъект.ОсновныеСредства Цикл
				СтрокаТЧ.Количество = ?(СтрокаТЧ.Количество = 0, 1, СтрокаТЧ.Количество);
				СтрокаТЧ.Цена = ?(СтрокаТЧ.Цена = 0, СтрокаТЧ.Сумма / СтрокаТЧ.Количество, СтрокаТЧ.Цена);
			КонецЦикла;
		
			// Запись обработанного объекта.
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ЗафиксироватьТранзакцию();
		Исключение
			// Если не удалось обработать какой-либо объект, повторяем попытку снова.
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В процедуре Документы.ПоступлениеТоваровУслуг.ЗаполнитьПустоеКоличествоОсновныхСредств() не удалось обработать элемент по причине:
					|%1'"),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПоступлениеТоваровУслуг, Выборка.Ссылка, ТекстСообщения);
				
		КонецПопытки; 
		
	КонецЦикла;
	
	Если ПроблемныхОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В процедуре Документы.ПоступлениеТоваровУслуг.ЗаполнитьПустоеКоличествоОсновныхСредств() не удалось обработать все элементы: в %1 из %2 возникли ошибки'"),
				ПроблемныхОбъектов,
				ОбъектовОбработано);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,
			Метаданные.Документы.ПоступлениеТоваровУслуг, ,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедура Документы.ПоступлениеТоваровУслуг.ЗаполнитьПустоеКоличествоОсновныхСредств() обработала очередную порцию: %1 элементов'"), 
					ОбъектовОбработано));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция ПолучитьСоответствиеВидовОперацийФормам() Экспорт

	ФормыПоступлениеТоваровУслуг = Новый Соответствие;
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары, "ФормаДокументаТовары");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги, "ФормаДокументаУслуги");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиФакторинга, "ФормаДокументаУслуги");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды, "ФормаДокументаАренда");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга, "ФормаДокументаАренда");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия, "ФормаДокументаОбщая");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку, "ФормаДокументаОбщая");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование, "ФормаДокументаОбщая");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства, "ФормаДокументаОбщая");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства, "ФормаДокументаОсновныеСредства");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков, "ФормаДокументаОсновныеСредства");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Топливо, "ФормаДокументаТопливо");
	ФормыПоступлениеТоваровУслуг.Вставить(Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права, "ФормаДокументаПрава");
	
	Возврат ФормыПоступлениеТоваровУслуг;

КонецФункции 

// Перечень табличных частей, данные которых не используются 
// в контексте документа, скрыты от пользователя
Функция НеИспользуемыеТабличныеЧасти(ВидОперации, Комиссия, ВозможнаКомиссияПоЗакупке) Экспорт
	
	ТабличныеЧасти = Новый Массив;
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары Тогда
	
		// Все, кроме товаров, не используется.
		ТабличныеЧасти.Добавить("Оборудование");
		ТабличныеЧасти.Добавить("ОбъектыСтроительства");
		ТабличныеЧасти.Добавить("Услуги");
		ТабличныеЧасти.Добавить("ВозвратнаяТара");
		ТабличныеЧасти.Добавить("АгентскиеУслуги");
		ТабличныеЧасти.Добавить("ОсновныеСредства");
		ТабличныеЧасти.Добавить("Топливо");
		ТабличныеЧасти.Добавить("Права");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда

		// Все, кроме услуг, не используется.
		ТабличныеЧасти.Добавить("Оборудование");
		ТабличныеЧасти.Добавить("ОбъектыСтроительства");
		ТабличныеЧасти.Добавить("Товары");
		ТабличныеЧасти.Добавить("ВозвратнаяТара");
		ТабличныеЧасти.Добавить("АгентскиеУслуги");
		ТабличныеЧасти.Добавить("ОсновныеСредства");
		ТабличныеЧасти.Добавить("Топливо");
		ТабличныеЧасти.Добавить("Права");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков Тогда

		// Все, кроме основных средств, не используется.
		ТабличныеЧасти.Добавить("Оборудование");
		ТабличныеЧасти.Добавить("ОбъектыСтроительства");
		ТабличныеЧасти.Добавить("Товары");
		ТабличныеЧасти.Добавить("Услуги");
		ТабличныеЧасти.Добавить("ВозвратнаяТара");
		ТабличныеЧасти.Добавить("АгентскиеУслуги");
		ТабличныеЧасти.Добавить("Топливо");
		ТабличныеЧасти.Добавить("Права");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Топливо Тогда
		
		// Все, кроме топлива, не используются.
		ТабличныеЧасти.Добавить("Оборудование");
		ТабличныеЧасти.Добавить("ОбъектыСтроительства");
		ТабличныеЧасти.Добавить("Товары");
		ТабличныеЧасти.Добавить("Услуги");
		ТабличныеЧасти.Добавить("ВозвратнаяТара");
		ТабличныеЧасти.Добавить("АгентскиеУслуги");
		ТабличныеЧасти.Добавить("ОсновныеСредства");
		ТабличныеЧасти.Добавить("Права");
	
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права Тогда
		
		// Никакие, кроме прав, не используются.
		ТабличныеЧасти.Добавить("Оборудование");
		ТабличныеЧасти.Добавить("ОбъектыСтроительства");
		ТабличныеЧасти.Добавить("Товары");
		ТабличныеЧасти.Добавить("Услуги");
		ТабличныеЧасти.Добавить("ВозвратнаяТара");
		ТабличныеЧасти.Добавить("АгентскиеУслуги");
		ТабличныеЧасти.Добавить("ОсновныеСредства");
		ТабличныеЧасти.Добавить("Топливо");
		
	Иначе
	
		// Товары доступны всегда, кроме объектов строительства и основных средств
		Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства Тогда
			ТабличныеЧасти.Добавить("Товары");
		КонецЕсли;
		
		// Услуги доступны всегда, кроме переработки и комиссии
		Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку 
			ИЛИ Комиссия Тогда
			ТабличныеЧасти.Добавить("Услуги");
		КонецЕсли;
		
		Если НЕ ВозможнаКомиссияПоЗакупке Тогда
			ТабличныеЧасти.Добавить("АгентскиеУслуги");
		КонецЕсли;
		
		// Возвратная тара доступна только для двух видов операции и то,
		// если включена соответствующая опция
		Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
			ТабличныеЧасти.Добавить("ВозвратнаяТара");
		ИначеЕсли ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
			И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование Тогда
			ТабличныеЧасти.Добавить("ВозвратнаяТара");
		КонецЕсли;
		
		// Оборудование, ОбъектыСтроительства, ОсновныеСредства доступны только в "своих" операциях
		Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование Тогда
			ТабличныеЧасти.Добавить("Оборудование");
		КонецЕсли;
		
		Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства Тогда
			ТабличныеЧасти.Добавить("ОбъектыСтроительства");
		КонецЕсли;
		
		Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства
			И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков Тогда
			ТабличныеЧасти.Добавить("ОсновныеСредства");
		КонецЕсли;
		
		ТабличныеЧасти.Добавить("Топливо");
		ТабличныеЧасти.Добавить("Права");
		
	КонецЕсли;
	
	Возврат ТабличныеЧасти;
	
КонецФункции

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ, ДоговорДляОтложенногоПроведения = Неопределено) Экспорт

	ПараметрыПроведения = Новый Структура;

	ЭтоОтложенноеПроведение = ЗначениеЗаполнено(ДоговорДляОтложенногоПроведения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц, ЭтоОтложенноеПроведение);
	Результат    = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	Если Не УчетнаяПолитика.Существует(Реквизиты.Организация, Реквизиты.Период, Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	НомераТаблиц = Новый Структура;

	ИспользуетсяОтложенноеПроведение = ПроведениеСервер.ИспользуетсяОтложенноеПроведение(
		Реквизиты.Организация, Реквизиты.Период);
	
	Реквизиты.Вставить("ВалютаРеглУчета", ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	Реквизиты.Вставить("РасчетыВВалюте",  Реквизиты.ВалютаВзаиморасчетов <> Реквизиты.ВалютаРеглУчета);
	Реквизиты.Вставить("ИспользуетсяОтложенноеПроведение", ИспользуетсяОтложенноеПроведение);
	Реквизиты.Вставить("ЭтоОтложенноеПроведение", ЭтоОтложенноеПроведение);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Реквизиты.ВалютаРеглУчета);
	Запрос.УстановитьПараметр("РасчетыВВалюте",  Реквизиты.РасчетыВВалюте);
	Запрос.УстановитьПараметр("ЕстьРасхождения", Реквизиты.ЕстьРасхождения);
	Запрос.УстановитьПараметр("СчетУчетаТопливоВБаке", ПланыСчетов.Хозрасчетный.ТопливоВБаке);
	Запрос.УстановитьПараметр("ПрименяетсяФСБУ5_2019", УчетнаяПолитика.ПрименяетсяФСБУ5_2019(Реквизиты.Период));
	Запрос.УстановитьПараметр("СчетУчетаМалоценки", ПланыСчетов.Хозрасчетный.ПриобретениеМалоценногоОборудованияИЗапасов);
	Запрос.УстановитьПараметр("РаздельныйУчетНаСчете19", УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Реквизиты.Организация, Реквизиты.Период));
	
	НастройкиСущественностиВНА =
		Справочники.НастройкиСущественностиВнеоборотныхАктивов.НастройкиСущественностиВНА(Реквизиты);
	Запрос.УстановитьПараметр("УказанЛимитСтоимости",
		НастройкиСущественностиВНА.СпособУказанияЛимита = Перечисления.СпособыУказанияЛимитаСущественностиВНА.ПоСтоимости);
	Запрос.УстановитьПараметр("ЛимитСтоимостиСущественныхНМА", НастройкиСущественностиВНА.ЛимитСтоимости);
	// Отражение в бухгалтерском учете вида операции "Приобретение прав" для ИП на ОСНО определяется реквизитом ОтражениеНУ справочника НМА.
	Запрос.УстановитьПараметр("ЭтоПлательщикНДФЛ", УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ДатаДокумента", НачалоДня(Реквизиты.Период));
	Запрос.УстановитьПараметр("ВидРасходовПовышающийКоэффициентНУ", Перечисления.ВидыРасходовНУ.РасходыНаКомпьютерныеЛицензииСПовышающимКоэффициентом);
		
	КритерииПримененияВычета = ИнвестиционныйНалоговыйВычет.НовыйКритерииПримененияВычета();
	ЗаполнитьЗначенияСвойств(КритерииПримененияВычета, Реквизиты);
	КритерииПримененияВычета.Дата = Реквизиты.Период;
	КритерииПримененияВычета.МенеджерДокумента = Документы.ПоступлениеТоваровУслуг;
	
	Запрос.УстановитьПараметр("ПроцентИнвестиционногоВычета", ИнвестиционныйНалоговыйВычет.ПроцентИнвестиционногоВычета(КритерииПримененияВычета));
	
	Если Реквизиты.РасчетыВВалюте Тогда
		ПодготовитьТаблицыДокументаРасчетыВВалюте(Запрос, Реквизиты);
	Иначе
		Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
		Если Не ПустаяСтрока(Запрос.Текст) Тогда
			Результат = Запрос.ВыполнитьПакет();
		КонецЕсли;
	КонецЕсли;
	
	Реквизиты.Вставить("УчетПоПродажнойСтоимости", 
		УчетнаяПолитика.СпособОценкиТоваровВРознице(Реквизиты.Организация, Реквизиты.Период) 
			= Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости);
	Реквизиты.Вставить("ВедетсяУчетПрослеживаемыхТоваров", ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Реквизиты.Период));
	Реквизиты.Вставить("ПрименяетсяУСНДоходыМинусРасходы", 
		УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ПрименяетсяУСНДоходыМинусРасходы", Реквизиты.ПрименяетсяУСНДоходыМинусРасходы);
	Реквизиты.Вставить("ПрименяетсяАУСН",
		УчетнаяПолитика.ПрименяетсяАУСН(Реквизиты.Организация, Реквизиты.Период));
		
	ПлательщикНДСНаУСНСВычетами        = УчетнаяПолитика.ПлательщикНДСНаУСНСВычетами(Реквизиты.Организация, Реквизиты.Период);
	ПлательщикНДС                      = УчетнаяПолитика.ПлательщикНДС(Реквизиты.Организация, Реквизиты.Период) ИЛИ ПлательщикНДСНаУСНСВычетами;
	ПрименяетсяОсвобождениеОтУплатыНДС = УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(Реквизиты.Организация, Реквизиты.Период);
	Реквизиты.Вставить("ПлательщикНДС", ПлательщикНДС);
	Запрос.УстановитьПараметр("МассивСчетовДляГТД", УчетНДСВызовСервераПовтИсп.СчетаУчетаДляГТД());
	
	Запрос.УстановитьПараметр("ПрименяетсяУСН",
		УчетнаяПолитика.ПрименяетсяУСН(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ПрименяетсяПСН",
		УчетнаяПолитика.ПрименяетсяУСНПатент(Реквизиты.Организация, Реквизиты.Период));
	
	Запрос.УстановитьПараметр("УчетАгентскогоНДС",  Реквизиты.УчетАгентскогоНДС);
	
	Запрос.УстановитьПараметр("Счета10", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Материалы));
	Запрос.УстановитьПараметр("Счета97", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов));
	Запрос.УстановитьПараметр("СчетаАО", ПланыСчетов.Хозрасчетный.СчетаАрендныхОбязательств());
	Запрос.УстановитьПараметр("СчетЗатратПоУмолчанию", 
		УчетнаяПолитика.ОсновнойСчетУчетаЗатрат(Реквизиты.Организация, Реквизиты.Период));
	
	Запрос.УстановитьПараметр("СтатьяЗатратРасходыГСМ",
		Справочники.СтатьиЗатрат.СтатьяЗатратПоНазначению("СлужебныйАвтотранспорт"));
	
	Запрос.УстановитьПараметр("СинонимТовары",       НСтр("ru = 'Товары'"));
	Запрос.УстановитьПараметр("СинонимУслуги",       НСтр("ru = 'Услуги'"));
	Запрос.УстановитьПараметр("СинонимОборудование", НСтр("ru = 'Оборудование'"));
	Запрос.УстановитьПараметр("СинонимОсновныеСредства", НСтр("ru = 'Основные средства'"));
	Запрос.УстановитьПараметр("СинонимНМА",          НСтр("ru = 'Нематериальные активы'"));
	Запрос.УстановитьПараметр("СинонимПрочее",       НСтр("ru = 'Прочее'"));
	
	Реквизиты.Вставить("ПлательщикНДФЛ", 
		УчетнаяПолитика.ПлательщикНДФЛ(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ПлательщикНДФЛ", Реквизиты.ПлательщикНДФЛ);
	Запрос.УстановитьПараметр("ВестиУчетПоВидамДеятельностиИП", УчетнаяПолитика.ВестиУчетПоВидамДеятельностиИП(Реквизиты.Организация, Реквизиты.Период));
	Запрос.УстановитьПараметр("ОсновнаяНоменклатурнаяГруппа",   УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Реквизиты.Организация, Реквизиты.Период));
	
	Запрос.УстановитьПараметр("СодержаниеПоступлениеТоваровКомитентов", "Поступление запасов");
	Запрос.УстановитьПараметр("ПлательщикНДС", ПлательщикНДС);
	Запрос.УстановитьПараметр("ЕстьНДС", Реквизиты.ЕстьНДС);
	Запрос.УстановитьПараметр("ЭтоИмпорт", Реквизиты.СтранаРегистрации <> Справочники.СтраныМира.Россия);
	Запрос.УстановитьПараметр("ЭтоПоступлениеИзЕАЭС",
		Справочники.СтраныМира.ГосударствоЧленТаможенногоСоюза(Реквизиты.СтранаРегистрации));
	Запрос.УстановитьПараметр("МесяцРегистрации", НачалоМесяца(Реквизиты.Период));
	ПравоНаВычетНДС = ПлательщикНДС Или ПлательщикНДСНаУСНСВычетами;
	Запрос.УстановитьПараметр("ПрослеживаемыеОперацииОтображаютсяВДекларацииНДС",
		ПравоНаВычетНДС И Реквизиты.ЕстьНДС И НЕ ПрименяетсяОсвобождениеОтУплатыНДС);
	СчетБезвозмездногоПолучения = ПланыСчетов.Хозрасчетный.БезвозмездныеПоступления; //98.02
	Запрос.УстановитьПараметр("БезвозмездноеПоступление", 
		?(Реквизиты.СчетУчетаРасчетовСКонтрагентом = СчетБезвозмездногоПолучения, Истина, Ложь));
	Запрос.УстановитьПараметр("ЭтоКомиссия", Реквизиты.ЭтоКомиссия);
	Запрос.УстановитьПараметр("ЭтоКомиссияНаЗакупку", Реквизиты.ЭтоКомиссияНаЗакупку);
	Запрос.УстановитьПараметр("СтатьяЗатратАмортизация", Справочники.СтатьиЗатрат.ПредопределенныйЭлемент("Амортизация"));
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаЗачетАвансовКомиссияПоЗакупке(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступление(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеТоваровНаКомиссию(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеТоваровВПереработку(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеТоваровТорговаяНаценка(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеТоваровГТД(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеТары(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеРасходовУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРасходыАУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеМПЗИП(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовСКонтрагентами(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаРегистрацияОтложенныхРасчетовВПоследовательности(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПроверкаСостоянияОС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПроверкаСостоянияНМА(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПоступлениеПоСчету(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПрослеживаемыеТовары(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаПрослеживаемыеОС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
		+ ТекстЗапросаЗачетАвансовКомиссияПоПродаже(НомераТаблиц, ПараметрыПроведения, Реквизиты);
		
	Если Не ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	СтруктураТаблицДокумента = Новый Структура;
	СтруктураТаблицДокумента.Вставить("ТаблицаТовары",                 ПараметрыПроведения.ТаблицаТовары);
	СтруктураТаблицДокумента.Вставить("ТаблицаОборудование",           ПараметрыПроведения.ТаблицаОборудование);
	СтруктураТаблицДокумента.Вставить("ТаблицаОбъектыСтроительства",   ПараметрыПроведения.ТаблицаОбъектыСтроительства);
	СтруктураТаблицДокумента.Вставить("ТаблицаУслуги",                 ПараметрыПроведения.ТаблицаУслуги);
	СтруктураТаблицДокумента.Вставить("ТаблицаТоварыУслугиКомиссияПоЗакупке", ПараметрыПроведения.ТаблицаТоварыУслугиКомиссияПоЗакупке);
	СтруктураТаблицДокумента.Вставить("ТаблицаОсновныеСредства",       ПараметрыПроведения.ТаблицаОсновныеСредства);
	СтруктураТаблицДокумента.Вставить("ТаблицаГрупповыеОбъектыОС",     ПараметрыПроведения.ТаблицаГрупповыеОбъектыОС);
	СтруктураТаблицДокумента.Вставить("ТаблицаТопливо",                ПараметрыПроведения.ТаблицаТопливо);
	СтруктураТаблицДокумента.Вставить("ТаблицаПрава",                  ПараметрыПроведения.ТаблицаПрава);
	
	ПараметрыПроведения.Вставить("СтруктураТаблицДокумента", СтруктураТаблицДокумента);
	
	Для каждого ТаблицаСтруктуры Из ПараметрыПроведения.СтруктураТаблицДокумента Цикл
		ДобавитьКолонкуСодержание(ТаблицаСтруктуры.Значение, Реквизиты);
	КонецЦикла;
	ДобавитьКолонкуСодержание(ПараметрыПроведения.НомераГТД,   Реквизиты);
	ДобавитьКолонкуСодержание(ПараметрыПроведения.ТаблицаТара, Реквизиты);
	ДобавитьКолонкуСодержание(ПараметрыПроведения.ПоступлениеТоваровНаКомиссиюТаблицаТовары,   Реквизиты);
	ДобавитьКолонкуСодержание(ПараметрыПроведения.ПоступлениеТоваровВПереработкуТаблицаТовары, Реквизиты);

	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаПоступлениеРасходовУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	Если НЕ Реквизиты.ПрименяетсяУСНДоходыМинусРасходы Тогда
		ПараметрыПроведения.Вставить("УСНРеквизиты",       Неопределено);
		ПараметрыПроведения.Вставить("УСНТаблицаРасходов", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("УСНРеквизиты", НомераТаблиц.Количество());
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ВалютаВзаиморасчетов КАК Валюта,
	|	Реквизиты.УчетАгентскогоНДС,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	ЛОЖЬ КАК РасходыПредпринимателя
	|ИЗ
	|	Реквизиты КАК Реквизиты"
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	УчестьРасходыУСНУслуги = Реквизиты.ЕстьУслуги
		И Реквизиты.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	УчестьРасходыУСНТовары = Реквизиты.ЕстьТовары
		И Реквизиты.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства
		И Реквизиты.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		И Реквизиты.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И Реквизиты.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга
		И Реквизиты.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку
		И Реквизиты.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом
		И (НЕ Реквизиты.УчетПоПродажнойСтоимости 
			ИЛИ Реквизиты.ТипСклада <> Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка);
	УчестьРасходыУСНОсновныеСредства = Реквизиты.ЕстьОсновныеСредства;
	
	Если Не УчестьРасходыУСНТовары
		И Не УчестьРасходыУСНУслуги
		И Не УчестьРасходыУСНОсновныеСредства
		И Не Реквизиты.ЕстьТопливо
		И Не Реквизиты.ЕстьПрава Тогда
		ПараметрыПроведения.Вставить("УСНТаблицаРасходов", Неопределено);
		Возврат ТекстЗапроса;
	Иначе
		НомераТаблиц.Вставить("УСНТаблицаРасходов", НомераТаблиц.Количество());
		Если УчестьРасходыУСНТовары Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Номенклатура) КАК ВидРасхода,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
			|	ТаблицаТовары.Номенклатура КАК ЭлементРасхода,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.СчетУчета В (&Счета10)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК ЭтоТовар,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.СчетУчета В (&Счета10)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоМатериал,
			|	ТаблицаТовары.СчетУчета КАК СчетУчета,
			|	ТаблицаТовары.Ссылка КАК Партия,
			|	ТаблицаТовары.Количество КАК Количество,
			|	ТаблицаТовары.СуммаВзаиморасчетов КАК Сумма,
			|	ТаблицаТовары.СуммаНДСВзаиморасчетов КАК НДС,
			|	ТаблицаТовары.СпособУчетаНДС КАК СпособУчетаНДС,
			|	ТаблицаТовары.ОтражениеВУСН КАК ОтражениеВУСН
			|ИЗ
			|	ТаблицаТовары КАК ТаблицаТовары
			|ГДЕ
			|	НЕ ТаблицаТовары.КомиссияПоЗакупке";
		КонецЕсли;
		Если УчестьРасходыУСНТовары
			И УчестьРасходыУСНУслуги Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		Если УчестьРасходыУСНУслуги Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ТаблицаУслуги.СчетЗатрат В (&Счета97)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.РБП)
			|		КОГДА ТаблицаУслуги.СчетЗатрат В (&СчетаАО)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.ЛизинговыеПлатежи)
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Услуги)
			|	КОНЕЦ КАК ВидРасхода,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
			|	ВЫБОР
			|		КОГДА ТаблицаУслуги.СчетЗатрат В (&Счета97)
			|			ТОГДА ТаблицаУслуги.Субконто1
			|		ИНАЧЕ ТаблицаУслуги.Номенклатура
			|	КОНЕЦ КАК ЭлементРасхода,
			|	ЛОЖЬ КАК ЭтоТовар,
			|	ЛОЖЬ КАК ЭтоМатериал,
			|	ТаблицаУслуги.СчетЗатрат КАК СчетУчета,
			|	НЕОПРЕДЕЛЕНО КАК Партия,
			|	0 КАК Количество,
			|	ТаблицаУслуги.СуммаВзаиморасчетов КАК Сумма,
			|	ТаблицаУслуги.СуммаНДСВзаиморасчетов КАК НДС,
			|	ТаблицаУслуги.СпособУчетаНДС КАК СпособУчетаНДС,
			|	ТаблицаУслуги.ОтражениеВУСН КАК ОтражениеВУСН
			|ИЗ
			|	ТаблицаУслуги КАК ТаблицаУслуги";
		КонецЕсли;
		Если (УчестьРасходыУСНТовары Или УчестьРасходыУСНУслуги)
			И УчестьРасходыУСНОсновныеСредства Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|";
		КонецЕсли;
		Если УчестьРасходыУСНОсновныеСредства Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.ОС) КАК ВидРасхода,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
			|	ТаблицаОсновныеСредства.ОсновноеСредство КАК ЭлементРасхода,
			|	ЛОЖЬ КАК ЭтоТовар,
			|	ЛОЖЬ КАК ЭтоМатериал,
			|	ТаблицаОсновныеСредства.СчетУчета КАК СчетУчета,
			|	НЕОПРЕДЕЛЕНО КАК Партия,
			|	0 КАК Количество,
			|	ТаблицаОсновныеСредства.СуммаВзаиморасчетов КАК Сумма,
			|	ТаблицаОсновныеСредства.СуммаНДСВзаиморасчетов КАК НДС,
			|	ТаблицаОсновныеСредства.СпособУчетаНДС КАК СпособУчетаНДС,
			|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются) КАК ОтражениеВУСН
			|ИЗ
			|	ТаблицаОсновныеСредства КАК ТаблицаОсновныеСредства";
		КонецЕсли;
		
		Если Реквизиты.ЕстьТопливо Тогда
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Номенклатура) КАК ВидРасхода,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
			|	ТаблицаТопливо.Номенклатура КАК ЭлементРасхода,
			|	ЛОЖЬ КАК ЭтоТовар,
			|	ЛОЖЬ КАК ЭтоМатериал,
			|	&СчетУчетаТопливоВБаке КАК СчетУчета,
			|	ТаблицаТопливо.Ссылка КАК Партия,
			|	ТаблицаТопливо.Количество КАК Количество,
			|	ТаблицаТопливо.СуммаВзаиморасчетов КАК Сумма,
			|	ТаблицаТопливо.СуммаНДСВзаиморасчетов КАК НДС,
			|	ТаблицаТопливо.СпособУчетаНДС КАК СпособУчетаНДС,
			|	ТаблицаТопливо.ОтражениеВУСН КАК ОтражениеВУСН
			|ИЗ
			|	ТаблицаТопливо КАК ТаблицаТопливо";
		КонецЕсли;
		
		Если Реквизиты.ЕстьПрава Тогда
			// Элементы расхода далее при необходимости буду заменены назначением использования НМА
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ТаблицаПрава.ПорядокВключенияСтоимостиВСоставРасходовУСН = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСиНМАВСоставРасходовУСН.ВключитьВСоставАмортизируемогоИмущества)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.НМА)
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Услуги)
			|	КОНЕЦ КАК ВидРасхода,
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные) КАК СтатусыПартийУСН,
			|	ТаблицаПрава.ОбъектПрава КАК ЭлементРасхода,
			|	ЛОЖЬ КАК ЭтоТовар,
			|	ЛОЖЬ КАК ЭтоМатериал,
			|	ВЫБОР
			|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Аванс)
			|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеПравНаИспользованиеРезультатовИнтеллектуальнойДеятельности)
			|		КОГДА ТаблицаПрава.ОтражениеБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Расходы)
			|				ИЛИ ТаблицаПрава.ЭтоМалоценныйНМА
			|			ТОГДА ТаблицаПрава.СчетЗатрат
			|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НематериальныеАктивыОрганизации)
			|	КОНЕЦ КАК СчетУчета,
			|	НЕОПРЕДЕЛЕНО КАК Партия,
			|	0 КАК Количество,
			|	ТаблицаПрава.СуммаВзаиморасчетов КАК Сумма,
			|	ТаблицаПрава.СуммаНДСВзаиморасчетов КАК НДС,
			|	ТаблицаПрава.СпособУчетаНДС КАК СпособУчетаНДС,
			|	ВЫБОР
			|		КОГДА ТаблицаПрава.ПорядокВключенияСтоимостиВСоставРасходовУСН = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСиНМАВСоставРасходовУСН.ВключитьВСоставАмортизируемогоИмущества)
			|				ИЛИ ТаблицаПрава.ПорядокВключенияСтоимостиВСоставРасходовУСН = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСиНМАВСоставРасходовУСН.ВключитьВСоставРасходов)
			|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются)
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются)
			|	КОНЕЦ КАК ОтражениеВУСН
			|ИЗ
			|	ТаблицаПрава КАК ТаблицаПрава";
		КонецЕсли;
		
		Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаРасходыАУСН(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ПрименяетсяАУСН
		Или Реквизиты.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
		ПараметрыПроведения.Вставить("РасходыАУСН", Неопределено);
		Возврат "";
	КонецЕсли;
	
	ТекстыВложенныхЗапросов = Новый Массив;
	
	Если Реквизиты.ЕстьТовары Тогда
		ТекстыВложенныхЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.Ссылка КАК ДокументРасчетов,
		|	ТаблицаТовары.ОтражениеВУСН КАК НалоговыйУчет,
		|	ТаблицаТовары.СуммаВзаиморасчетов КАК Сумма
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаТовары.Ссылка = Реквизиты.Ссылка");
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстыВложенныхЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.Ссылка КАК ДокументРасчетов,
		|	ТаблицаУслуги.ОтражениеВУСН КАК НалоговыйУчет,
		|	ТаблицаУслуги.СуммаВзаиморасчетов КАК Сумма
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаУслуги.Ссылка = Реквизиты.Ссылка");
	КонецЕсли;
	
	Если Реквизиты.ЕстьАгентскиеУслуги Тогда
		ТекстыВложенныхЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.Ссылка КАК ДокументРасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются) КАК НалоговыйУчет,
		|	ТаблицаАгентскиеУслуги.СуммаВзаиморасчетов КАК Сумма
		|ИЗ
		|	ТаблицаАгентскиеУслуги КАК ТаблицаАгентскиеУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаАгентскиеУслуги.Ссылка = Реквизиты.Ссылка");
	КонецЕсли;
	
	Если Реквизиты.ЕстьОборудование Тогда
		ТекстыВложенныхЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.Ссылка КАК ДокументРасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются) КАК НалоговыйУчет,
		|	ТаблицаОборудование.СуммаВзаиморасчетов КАК Сумма
		|ИЗ
		|	ТаблицаОборудование КАК ТаблицаОборудование
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаОборудование.Ссылка = Реквизиты.Ссылка");
	КонецЕсли;
	
	Если Реквизиты.ЕстьОбъектыСтроительства Тогда
		ТекстыВложенныхЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.Ссылка КАК ДокументРасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются) КАК НалоговыйУчет,
		|	ТаблицаОбъектыСтроительства.СуммаВзаиморасчетов КАК Сумма
		|ИЗ
		|	ТаблицаОбъектыСтроительства КАК ТаблицаОбъектыСтроительства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаОбъектыСтроительства.Ссылка = Реквизиты.Ссылка");
	КонецЕсли;
	
	Если Реквизиты.ЕстьОсновныеСредства Тогда
		ТекстыВложенныхЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.Ссылка КАК ДокументРасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются) КАК НалоговыйУчет,
		|	ТаблицаОсновныеСредства.СуммаВзаиморасчетов КАК Сумма
		|ИЗ
		|	ТаблицаОсновныеСредства КАК ТаблицаОсновныеСредства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаОсновныеСредства.Ссылка = Реквизиты.Ссылка");
	КонецЕсли;
	
	Если Реквизиты.ЕстьТопливо Тогда
		ТекстыВложенныхЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.Ссылка КАК ДокументРасчетов,
		|	ТаблицаТопливо.ОтражениеВУСН КАК НалоговыйУчет,
		|	ТаблицаТопливо.СуммаВзаиморасчетов КАК Сумма
		|ИЗ
		|	ТаблицаТопливо КАК ТаблицаТопливо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаТопливо.Ссылка = Реквизиты.Ссылка");
	КонецЕсли;
	
	Если Реквизиты.ЕстьПрава Тогда
		ТекстыВложенныхЗапросов.Добавить(
		"ВЫБРАТЬ
		|	Реквизиты.Контрагент КАК Контрагент,
		|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Реквизиты.Ссылка КАК ДокументРасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.Принимаются) КАК НалоговыйУчет,
		|	ТаблицаПрава.СуммаВзаиморасчетов КАК Сумма
		|ИЗ
		|	ТаблицаПрава КАК ТаблицаПрава
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаПрава.Ссылка = Реквизиты.Ссылка");
	КонецЕсли;
	
	Если ТекстыВложенныхЗапросов.Количество() > 0 Тогда
		
		НомераТаблиц.Вставить("РасходыАУСН", НомераТаблиц.Количество());
		
		ТекстВложенногоЗапроса = СтрСоединить(ТекстыВложенныхЗапросов,
			"
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|");
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Контрагент,
		|	ВложенныйЗапрос.ДоговорКонтрагента,
		|	ВложенныйЗапрос.ДокументРасчетов,
		|	ВложенныйЗапрос.НалоговыйУчет,
		|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма
		|ИЗ
		|	(" + ТекстВложенногоЗапроса + ") КАК ВложенныйЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Контрагент,
		|	ВложенныйЗапрос.ДоговорКонтрагента,
		|	ВложенныйЗапрос.ДокументРасчетов,
		|	ВложенныйЗапрос.НалоговыйУчет"
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	Иначе
		ПараметрыПроведения.Вставить("РасходыАУСН", Неопределено);
		ТекстЗапроса = "";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Приходный ордер (М-4)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М4";
	КомандаПечати.Представление = НСтр("ru = 'Приходный ордер (М-4)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаПечати.Порядок       = 10;
	
	// Акт о принятии к учету ОС по произвольной форме
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОПринятииКУчетуОС";
	КомандаПечати.Представление = НСтр("ru = 'Акт о принятии к учету основных средств'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОсновныеСредства";
	КомандаПечати.Порядок       = 10;
	КомандаПечати.ФункциональныеОпции = "ВедетсяУчетОсновныхСредств";
	
	// Акт о приеме-передаче ОС (ОС-1)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС1";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приеме-передаче ОС (ОС-1)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаОсновныеСредства";
	КомандаПечати.Порядок       = 20;
	КомандаПечати.ФункциональныеОпции = "ВедетсяУчетОсновныхСредств";
	
	// Акт о приеме оборудования (ОС-14)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС14";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приеме оборудования (ОС-14)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая";
	КомандаПечати.Порядок       = 20;
	КомандаПечати.ФункциональныеОпции = "ВедетсяУчетОсновныхСредств";
	
	// Акт о приемке товара без счета поставщика (ТОРГ-4)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ4";
	КомандаПечати.Представление = НСтр("ru = 'Акт о приемке товара без счета поставщика (ТОРГ-4)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаПечати.Порядок       = 30;
	
	// Приходная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Приходная накладная'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаПечати.Порядок       = 40;
	
	// Справка-расчет "Рублевые суммы документа в валюте"
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьРублевыхСуммДокументовВВалюте";
	КомандаПечати.Идентификатор = "РублевыеСуммыДокументаВВалюте";
	КомандаПечати.Представление = НСтр("ru = 'Справка-расчет ""Рублевые суммы документа в валюте""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.ФункциональныеОпции = "ИспользоватьВалютныйУчет";
	КомандаПечати.Порядок       = 50;
	
	// Товарная накладная (ТОРГ-12) за поставщика
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ12_БезУслуг";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12) за поставщика'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары";
	КомандаПечати.Порядок       = 60;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	
	// Товарная накладная (ТОРГ-12) с услугами за поставщика
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ12";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12) с услугами за поставщика'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая";
	КомандаПечати.Порядок       = 70;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Акт";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг за поставщика'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаУслуги";
	КомандаПечати.Порядок       = 80;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	
	// Счет-фактура от поставщика
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетФактураПолученный";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура за поставщика'");
	КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиСчетовФактурПолученных";
	КомандаПечати.Порядок       = 90;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЭтоУниверсальныйДокумент", Ложь);
	
	// Универсальный передаточный документ за поставщика
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "УниверсальныйПередаточныйДокумент";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД) за поставщика'");
	КомандаПечати.Обработчик    = "УчетНДСКлиент.ВыполнитьКомандуПечатиУниверсальныхПередаточныхДокументов";
	КомандаПечати.ДополнительныеПараметры.Вставить("ИдентификаторВКомплекте","УниверсальныйПередаточныйДокументКомплект");
	КомандаПечати.Порядок = 100;
	
	// Товарная накладная (ТОРГ-2)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ2";
	КомандаПечати.Представление = НСтр("ru = 'Акт о расхождениях (ТОРГ-2)'");
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьТОРГ2";
	КомандаПечати.ФункциональныеОпции = "ИспользоватьКорректировочныеДокументы";
	КомандаПечати.Порядок       = 110;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ЕстьРасхождения", Истина);
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.Контрагенты) Тогда
		// Печать конвертов
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Конверт";
		КомандаПечати.Представление = НСтр("ru = 'Конверт'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиКонверта";
		КомандаПечати.СписокФорм    = "ФормаСписка,ФормаВыбора,ФормаДокументаОбщая,ФормаДокументаТовары,ФормаДокументаУслуги,ФормаДокументаАренда,ФормаДокументаПрава";
		КомандаПечати.Порядок       = 120;
	КонецЕсли;
	
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьЭтикеток) Тогда
		// Этикетки
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ЭтикеткиОбщая";
		КомандаПечати.Представление = НСтр("ru = 'Этикетки'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Этикетки'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиЭтикеток";
		КомандаПечати.СписокФорм    = "ФормаДокументаОбщая";
		КомандаПечати.Порядок       = 130;
		
		УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ВидОперации", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия);
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ЭтикеткиТовары";
		КомандаПечати.Представление = НСтр("ru = 'Этикетки'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Этикетки'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиЭтикеток";
		КомандаПечати.СписокФорм    = "ФормаДокументаТовары";
		КомандаПечати.Порядок       = 130;
		
		//Инвентарные этикетки
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ИнвентарныеЭтикетки";
		КомандаПечати.Представление = НСтр("ru = 'Инвентарные этикетки'");
		КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Инвентарные этикетки'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиИнвентаризационныхЭтикеток";
		КомандаПечати.СписокФорм    = "ФормаДокументаОсновныеСредства";
		КомандаПечати.Порядок       = 140;
		УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ВидОперации", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства);
	КонецЕсли;
	
	// Принятие расходов УСН
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор       = "РасходыУСН";
	КомандаПечати.Представление       = НСтр("ru = 'Признание расходов УСН'");
	КомандаПечати.Обработчик          = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРасходовУСН";
	КомандаПечати.СписокФорм          = "ФормаДокументаОбщая,ФормаДокументаТовары,ФормаДокументаУслуги,ФормаДокументаАренда,ФормаДокументаПрава,ФормаДокументаТопливо,ФормаДокументаОсновныеСредства";
	КомандаПечати.ФункциональныеОпции = "ПрименяетсяУСНДоходыМинусРасходы";
	КомандаПечати.Порядок             = 150;
	УчетУСН.ДобавитьУсловиеВидимостиПечатиРасходыУСН(КомандаПечати);

	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Реестр";
	КомандаПечати.Представление = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы= НСтр("ru = 'Реестр документов ""Поступление (акт, накладная, УПД)""'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм    = "ФормаСписка";
	КомандаПечати.Порядок       = 160;
	
КонецПроцедуры

//Получает склад Документа поступления товаров и услуг для номенлкатуры в табличной части
//Параметры: 
//			ДокументСсылка Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
//			Номенклатура Тип("СправочникСсылка.Номенклатура")
//
//Возвращаемое значение: Склад Тип("СправочникСсылка.Склады")
//						Если для номенклатуры определен один склад то возвращает его,
//						Если одна и та же номенклатура идет на несколько складов - то ПустаяСсылка
//
Функция ПолучитьСкладТабличнойЧастиДокументаПоступления(ДокументСсылка, Номенклатура) Экспорт
	
	Склад = Справочники.Склады.ПустаяСсылка();
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СкладВСписке
	|			ТОГДА ПоступлениеТоваровУслугТовары.Склад
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ КАК Склад
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|		ПО ПоступлениеТоваровУслугТовары.Ссылка = ПоступлениеТоваровУслуг.Ссылка
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Номенклатура = &Номенклатура
	|	И ПоступлениеТоваровУслугТовары.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровУслуг.СкладВСписке
	|			ТОГДА ПоступлениеТоваровУслугТовары.Склад
	|		ИНАЧЕ ПоступлениеТоваровУслуг.Склад
	|	КОНЕЦ");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Склады = Запрос.Выполнить().Выгрузить();
	Если Склады.Количество() = 1 Тогда
		Склад = Склады[0].Склад;
	КонецЕсли;
	
	Возврат Склад;
	
КонецФункции 

#КонецЕсли