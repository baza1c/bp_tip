
#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;
&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

&НаКлиенте
Перем ЭтоНоваяСтрока;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоступлениеТоваровУслугФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
		
	УстановитьВидимостьСчетовУчета();
	
	УстановитьУсловноеОформление();
	
	УчетНДС.ПрименитьПраваДоступаСчетаФактуры(
		СчетФактура,
		Элементы.СчетФактураПросмотр,
		Элементы.СчетФактураРедактирование);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереДокумент(ЭтотОбъект, Параметры);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.ПоступлениеТоваровУслуг",
		"ФормаДокументаУслуги",
		НСтр("ru='Новости: Поступление (акт, накладная, УПД)'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(
			ЭтотОбъект,
			Элементы.ГруппаГлобальныеКоманды);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	УправлениеПанельюПодсказки.ПриСозданииНаСервере(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.СчетНаОплатуПоставщика.Форма.ФормаВыбора" Тогда
		ОбработкаЗаполненияПоСчетуНаКлиенте(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ВыборПорядкаУчетаРасчетов" Тогда
		ОбработкаВыбораПорядокУчетаРасчетовНаСервере(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.ОтчетКомиссионераОПродажах.Форма.ФормаПорядокУчетаРасчетов" Тогда
		ОбработкаВыбораПорядокУчетаРасчетовСКомиссионеромНаСервере(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.РедактированиеСчетаЗатрат" Тогда
		ОбработкаВыбораАналитикиУчетаНаКлиенте(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.УниверсальныйПередаточныйДокумент" Тогда
		ОбработкаВыбораУниверсальныйПередаточныйДокументНаСервере(ВыбранноеЗначение);
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СчетФактураПолученный"
		И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ДокументыОснования") И Параметр.ДокументыОснования.Найти(Объект.Ссылка) <> Неопределено Тогда
		ПоступлениеТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыПроСчетФактуру(ЭтотОбъект, Параметр.РеквизитыСФ);
		СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
			Объект.Ссылка, СчетФактура);
		ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ОбновитьДокументИБПослеЗаполнения" И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
		Прочитать();
	ИначеЕсли ИмяСобытия = "ИзмененСтатусДокументов" Тогда
		// Статус документа изменен в форме списка поступлений или счетов-фактур.
		Если Не Объект.Ссылка.Пустая() 
			И (Параметр.Найти(Объект.Ссылка) <> Неопределено ИЛИ Параметр.Найти(СчетФактура) <> Неопределено) Тогда
			СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
				Объект.Ссылка, СчетФактура);
			ОригиналПолучен      = СтатусыОригиналовДокументов[Объект.Ссылка];
			ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
			ПолученЧекНПД        = СтатусыОригиналовДокументов[ЧекиНПДКлиентСервер.ИмяСтатусаЧекаНПД()];
			УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(
				ЭтотОбъект, ОригиналПолучен);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = РаботаСДоговорамиКонтрагентовБПКлиент.ИмяСобытияЗаписьДоговора()
		И Объект.ДоговорКонтрагента = Источник Тогда
		
		ДоговорКонтрагентаПриИзмененииНаСервере();
		УправлениеФормой(ЭтотОбъект);
	
		ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
	ИначеЕсли ИмяСобытия = "ДанныеСкопированыВБуферОбмена" Тогда
		
		УстановитьДоступностьКомандыВставки(ЭтотОбъект, Истина);
	ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоПолучен()
		И Объект.ДоговорКонтрагента = Источник Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДокумента(
			ЭтотОбъект, Параметр.ПлательщикНПД, Параметр.ДатаРегистрации);
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоОшибка()
		И Объект.ДоговорКонтрагента = Источник Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДокумента(
			ЭтотОбъект, Ложь, , Параметр);
		УправлениеФормой(ЭтотОбъект);
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если УчетОригиналовПервичныхДокументовБПКлиент.ЭтоСобытиеИзмененияСостоянияОриганалаПервичногоДокумента(
			Объект.Ссылка, ИмяСобытия, Источник) Тогда
		СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
			Объект.Ссылка, СчетФактура);
		ОригиналПолучен = СтатусыОригиналовДокументов[Объект.Ссылка];
		УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(
			ЭтотОбъект, ОригиналПолучен);
	КонецЕсли;
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
	
	УправлениеПанельюПодсказкиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// Подсистема "ОбменСКонтрагентами"
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец Подсистема "ОбменСКонтрагентами"
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПрочитатьСтатусСамозанятого();
	
	ПоступлениеТоваровУслугФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ПриЧтенииФормыДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект, "ГруппаКомментарийОтветственный", "Ответственный");

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормыКлиент.ПередЗаписью(
		ЭтотОбъект, Отказ, ПараметрыЗаписи);
			
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПоступлениеТоваровУслугФормы.ПослеЗаписиНаСервере(
		ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
	УчетОригиналовПервичныхДокументовБПКлиентСервер.УстановитьДоступностьДекорацииСостояния(ЭтотОбъект);
	
	УчетНДСКлиентСервер.ОтобразитьПолеУПД(ЭтотОбъект, ТекущийОбъект.ЭтоУниверсальныйДокумент);
		
	УстановитьСостояниеДокумента();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
	РаботаСДоговорамиКонтрагентовБП.СохранитьПараметрыДоговора(
		ТекущийОбъект.ДоговорКонтрагента,
		ТекущийОбъект.ЭтоУниверсальныйДокумент,
		ТекущийОбъект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ПоступлениеТоваровУслугФормы.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормы.ПриЗаписиНаСервере(
		ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормыКлиент.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтотОбъект, ПараметрыЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерВходящегоДокументаПриИзменении(Элемент)
	
	ПоступлениеТоваровУслугФормыКлиент.НомерВходящегоДокументаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВходящегоДокументаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДатаВходящегоДокумента) И НЕ Объект.Проведен Тогда
		Объект.Дата = Объект.ДатаВходящегоДокумента + (Объект.Дата - НачалоДня(Объект.Дата));
		ДатаПриИзмененииНаКлиенте();
	КонецЕсли;
	
	ДатаВходящегоДокументаПриИзмененииНаСервере();
	
	ПоступлениеТоваровУслугФормыКлиент.ДатаВходящегоДокументаПриИзменении(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)

	ДатаПриИзмененииНаКлиенте();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		КонтрагентПриИзмененииНаСервере();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элемент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораКонтрагенты");

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		
		ДоговорКонтрагентаПриИзмененииНаСервере();
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Истина;
	ОбщегоНазначенияБПКлиент.ОткрытьДоговорКонтрагента(ЭтотОбъект, Элемент, Объект.ДоговорКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(Объект.ДоговорКонтрагента),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(
		Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, ПредлагатьНовыйДоговор, СтандартнаяОбработка);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУчетаРасчетовНажатие(Элемент, СтандартнаяОбработка)
	
	АдресХранилищаЗачетАвансов = ПоместитьЗачетАвансовВоВременноеХранилищеНаСервере();

	ПоступлениеТоваровУслугФормыКлиент.ПорядокУчетаРасчетовНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка, АдресХранилищаЗачетАвансов);

КонецПроцедуры

&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)

	ПоступлениеТоваровУслугФормыКлиент.ЦеныИВалютаНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДОНажатие(Элемент, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ЕстьРасхожденияПриИзменении(Элемент)
	ПоступлениеТоваровУслугФормыКлиентСервер.УстановитьВидимостьКолонокРасхождения(ЭтотОбъект, "Услуги");
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, ЭтотОбъект.Объект);
КонецПроцедуры

&НаКлиенте
Процедура НомерСчетаФактурыПолученногоПриИзменении(Элемент)
	
	ПоступлениеТоваровУслугФормыКлиент.НомерСчетаФактурыПолученногоПриИзменении(ЭтотОбъект, Элемент);	
	
КонецПроцедуры

&НаКлиенте
Процедура АктОРасхожденияхНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(АктОРасхожденияхСсылка) Тогда
		ОткрытьФорму("Документ.АктОРасхождениях.Форма.ФормаДокумента", Новый Структура("Ключ", АктОРасхожденияхСсылка));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УПДПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, ЭтотОбъект.Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУПДНажатие(Элемент, СтандартнаяОбработка)
	
	АдресХранилищаПродавцы = ПоместитьТаблицуПродавцыВоВременноеХранилищеНаСервере();
	УчетНДСКлиент.ОткрытьФормуРеквизитовУПД(ЭтотОбъект, СтандартнаяОбработка, АдресХранилищаПродавцы);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОригиналПолученПриИзменении(Элемент)
	
	УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(
		ЭтотОбъект, ОригиналПолучен);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеОригиналаБПНажатие(Элемент)
	
	СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
		Объект.Ссылка, СчетФактура);
	Если ОригиналПолучен <> СтатусыОригиналовДокументов[Объект.Ссылка] Тогда
		// Пользователь изменил статус документа.
		// Перед открытием оригиналов первичных документов БСП нужно сначала сохранить статус,
		// чтобы обновились статусы первичных документов БСП.
		ПоступлениеТоваровУслугФормыВызовСервера.ЗаписатьСтатусДокументаНаСервере(Объект.Ссылка, ОригиналПолучен);
	КонецЕсли;
	
	УчетОригиналовПервичныхДокументовКлиент.ОткрытьФормуИзмененияСостоянийПечатныхФорм(Объект.Ссылка);
	
КонецПроцедуры

#Область Панель_Подсказки

&НаКлиенте
Процедура СоветПоРаботе1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоветПоРаботе3ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеПанельюПодсказкиКлиент.СоветПоРаботеОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ДоговорКонтрагентаПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки =
		ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ИмяСобытияВызовСервиса() Тогда
		СтандартнаяОбработка = Ложь;
		ИННСамозанятого = РаботаСДоговорамиКонтрагентовБПВызовСервера.ИННВладельца(Объект.ДоговорКонтрагента);
		ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБПКлиент.ДатаПолученияСтатуса(
			Объект.Дата, Объект.ДатаВходящегоДокумента);
		Если ИнтеграцияСтатусСамозанятогоКлиент.НачатьПолучениеСтатусаСамозанятого(
			Объект.ДоговорКонтрагента, ЭтотОбъект, ИННСамозанятого, ДатаПолученияСтатуса) Тогда
			Элемент.Заголовок = ИнтеграцияСтатусСамозанятогоФормыБПКлиент.ЗаголовокПодсказкиПолучениеСтатусаВПроцессе();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученЧекНПДПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	Если Не ПолученЧекНПД Тогда
		ИнтеграцияСПлатформойСамозанятыеБПКлиент.ОчиститьРеквизитыЧекаНПД(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура УслугиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Поле.Имя = "УслугиАналитикаУчета" Тогда
		ОткрытьФормуРедактированияСтроки(ВыбраннаяСтрока);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриАктивизацииЯчейки(Элемент)

	Если Элементы.Услуги.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоНоваяСтрока Тогда
		ТекущаяКолонкаТаблицы = Элементы.Услуги.ТекущийЭлемент;
		Если ТекущаяКолонкаТаблицы.Имя = "УслугиАналитикаУчета"
			И НЕ Элементы.Услуги.ТекущиеДанные.АналитикаУчетаЗаполнена Тогда
			ВыбраннаяСтрока = Элементы.Услуги.ТекущаяСтрока;
			ОткрытьФормуРедактированияСтроки(ВыбраннаяСтрока);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ЭтоНоваяСтрока = НоваяСтрока;
	
	Если Копирование Тогда
		Элемент.ТекущиеДанные.ИдентификаторСтроки = "";
	КонецЕсли; 

	Если НЕ НоваяСтрока ИЛИ Копирование Тогда
		Возврат;
	КонецЕсли;

	СтрокаТаблицы	= Элементы.Услуги.ТекущиеДанные;
	СтрокаТаблицы.ОтражениеВУСН	= ПоступлениеТоваровУслугФормыКлиентСервер.ОтражениеВУСН(Объект.ВидОперации, Ложь);
	СтрокаТаблицы.АналитикаУчета = "<...>";

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования И НоваяСтрока Тогда
		// Закроем форму редактирования строки, т.к. пользователь отменяет ввод
		Оповестить("ФормаРедактированияСтроки_Закрыть", Неопределено, ЭтотОбъект);
	ИначеЕсли Объект.НДСНеВыделять Тогда
		ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
		Если Не ЗначениеЗаполнено(ТекущиеДанные.СтавкаНДС) тогда
			ТекущиеДанные.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС")
		КонецЕсли;
	КонецЕсли;

	ЭтоНоваяСтрока = Ложь;

	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)

	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;

	// Получим общие параметры обработки для реквизитов документа
	ПараметрыОбработки = ПоступлениеТоваровУслугФормыКлиент.ПодготовитьПараметрыОбработкиУслугиНоменклатураПриИзменении(
		ЭтотОбъект, ТекущиеДанные);
	
	// Дополнительные поля, требующиеся для заполнения добавленных колонок табличного поля текущей формы.
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Всего", ТекущиеДанные.Всего);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("АналитикаУчета", ТекущиеДанные.АналитикаУчета);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("АналитикаУчетаЗаполнена", ТекущиеДанные.АналитикаУчетаЗаполнена);
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбработки.ДанныеОбъекта);
		
	УслугиНоменклатураПриИзмененииНаСервере(
		ПараметрыОбработки.ДанныеСтрокиТаблицы,
		ПараметрыОбработки.ДанныеОбъекта,
		ПараметрыОбработки.СчетаУчетаКЗаполнению);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыОбработки.ДанныеСтрокиТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораНоменклатура");

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиентБП.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСодержаниеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	РаботаСНоменклатуройКлиентБП.СодержаниеПриИзменении(ТекущиеДанные, ТекущаяДатаДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	ЗначениеПустогоКоличества = 1;
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	Если Объект.ЕстьРасхождения И ТекущиеДанные <> Неопределено Тогда
		ЗначениеПустогоКоличества = ?(ТекущиеДанные.КоличествоПоДокументу = 0, 1, 0);
	КонецЕсли;
	
	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииКоличество(ЭтотОбъект, "Услуги", ПрименяютсяСтавки4и2, ЗначениеПустогоКоличества);
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)

	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииЦена(ЭтотОбъект, "Услуги", ПрименяютсяСтавки4и2, 1);

КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)

	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "Услуги", ПрименяютсяСтавки4и2, 1);

КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)

	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииСтавкаНДС(ЭтотОбъект, "Услуги", ПрименяютсяСтавки4и2);
	РаботаСДоговорамиКонтрагентовБПКлиент.НастроитьСпособЗаполненияСтавкиНДС(ЭтотОбъект, "Услуги");

КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаНДСПриИзменении(Элемент)

	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииСуммаНДС(ЭтотОбъект, "Услуги");

КонецПроцедуры

&НаКлиенте
Процедура УслугиАналитикаУчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ВыбраннаяСтрока = Элементы.Услуги.ТекущаяСтрока;
	ОткрытьФормуРедактированияСтроки(ВыбраннаяСтрока);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НадписьСчетФактураНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	БухгалтерскийУчетКлиентПереопределяемый.ОткрытьСчетФактуру(ЭтотОбъект, СчетФактура, "СчетФактураПолученный");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборУслуги(Команда)

	ПоступлениеТоваровУслугФормыКлиент.ПодборНоменклатуры(ЭтотОбъект, "Услуги", Команда);

КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСчетФактуру(Команда)
	
	ПоступлениеТоваровУслугФормыКлиент.ЗарегистрироватьСчетФактуру(ЭтотОбъект, Команда);	
	УправлениеФормой(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.НадписьСчетФактура);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельПодсказок(Команда)
	
	ИзменитьВидимостьПодсказокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЧекНПД(Команда)
	
	ИнтеграцияСПлатформойСамозанятыеБПКлиент.ЗагрузитьЧекНПД(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(ИмяТаблицы = "") Экспорт

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	Если ПустаяСтрока(ИмяТаблицы) ИЛИ ИмяТаблицы = "Услуги" Тогда
		Для Каждого СтрокаТаблицы Из Объект.Услуги Цикл
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, ПараметрыОбъекта);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиНаСервере() Экспорт

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере() Экспорт
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьСостояниеДокумента();
	
	ТекущаяДатаДокумента = Объект.Дата;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Если Не ЗначениеЗаполнено(Объект.ДоговорКонтрагента) 
		И ЗначениеЗаполнено(Объект.Контрагент)
		И Не ЗначениеЗаполнено(Объект.Ссылка) 
		И Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ПоступлениеТоваровУслугФормы.КонтрагентПриИзменении(ЭтотОбъект);
	ИначеЕсли ВестиУчетПоДоговорам
		И ПоступлениеТоваровУслугФормы.ДоступноСозданиеОсновногоДоговораПоВидуОперации(Объект.ВидОперации) Тогда
		ПредлагатьНовыйДоговор = РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(
			Объект.Организация, Объект.Контрагент);
	КонецЕсли;
	
	УстановитьФакторинговуюКомпаниюПоОрганизации();
	
	ПоступлениеТоваровУслугФормы.УстановитьПараметрыПоДоговору(ЭтотОбъект);
	
	УстановитьПараметрыВыбора();
	
	ЗаполнитьДобавленныеКолонкиТаблиц();

	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);

	ПоступлениеТоваровУслугФормыКлиентСервер.СформироватьНадписьЦеныИВалюта(ЭтотОбъект);
	
	ОбновитьИтоги(ЭтотОбъект);
	
	ПоступлениеТоваровУслугФормыКлиентСервер.УстановитьТребуетсяСчетФактура(ЭтотОбъект);
	ПоступлениеТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыПроСчетФактуру(ЭтотОбъект);
	ПоступлениеТоваровУслугФормы.ЗаполнитьРеквизитыУПД(ЭтотОбъект);
	
	СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
		Объект.Ссылка, СчетФактура);
	ОригиналПолучен      = СтатусыОригиналовДокументов[Объект.Ссылка];
	ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
	ПолученЧекНПД        = СтатусыОригиналовДокументов[ЧекиНПДКлиентСервер.ИмяСтатусаЧекаНПД()];
	
	УчетОригиналовПервичныхДокументовБП.ПриСозданииНаСервере_ФормаДокумента(ЭтотОбъект, ОригиналПолучен);
	
	ПоступлениеТоваровУслугФормы.ЗаполнитьСписокВыбораУслугиОтражениеВУСН(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
	УстановитьВидимость();
	
	ПоступлениеТоваровУслугФормыКлиентСервер.УстановитьВидимостьКолонокРасхождения(ЭтотОбъект, "Услуги");
	
	ПоступлениеТоваровУслугФормы.УстановитьЗаголовокФормы(ЭтотОбъект);
	
	// Проверка буфера обмена на наличие скопированных строк
	УстановитьДоступностьКомандыВставки(ЭтотОбъект, Не ОбщегоНазначения.ПустойБуферОбмена());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФакторинговуюКомпаниюПоОрганизации()

	Если Объект.ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиФакторинга Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) ИЛИ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	// Ищем факторинговую компанию и договор факторинга.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ДоговорыКонтрагентов.Ссылка,
	|	ДоговорыКонтрагентов.Владелец
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СФакторинговойКомпанией)";
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();
			Объект.Контрагент = Выборка.Владелец;
			Объект.ДоговорКонтрагента = Выборка.Ссылка;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбора()

	МассивВидовДоговоров = Новый ФиксированныйМассив(
		ПоступлениеТоваровУслугФормы.ВидыДоговоровДокумента(Объект.ВидОперации));

	НовыйМассивПараметров = Новый Массив();
	НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора", МассивВидовДоговоров));
	
	ПоступлениеТоваровУслугФормы.ПараметрыВыбораПоВидуОперации(НовыйМассивПараметров, Объект.ВидОперации);

	НовыеПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассивПараметров);
	Элементы.ДоговорКонтрагента.ПараметрыВыбора = НовыеПараметрыВыбора;

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦеныИВалюты(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь) Экспорт

	Если ПерезаполнитьЦены ИЛИ ПересчитатьЦены ИЛИ ПересчитатьНДС Тогда
		ПриИзмененииЦеныИВалютыНаСервере(ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены, ПересчитатьЦены, ПересчитатьНДС);
	КонецЕсли;
	
	ПоступлениеТоваровУслугФормыКлиентСервер.УстановитьТребуетсяСчетФактура(ЭтотОбъект);
	
	ОбновитьИтоги(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура УправлениеФормойНаСервере() Экспорт
	
	УправлениеФормой(ЭтотОбъект);
	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораДляСчетовУчета() Экспорт
	
	// В форме услуг параметры выбора счетов учета не используются.
	// Процедура необходима для совместимости с другими формами документа.
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы() Экспорт

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	ИспользоватьКорректировочныеДокументы = ПолучитьФункциональнуюОпцию("ИспользоватьКорректировочныеДокументы");
	ПлательщикНДСНаУСНСВычетами           = УчетнаяПолитика.ПлательщикНДСНаУСНСВычетами(Объект.Организация, Объект.Дата);
	ПлательщикНДС                         = УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата) ИЛИ ПлательщикНДСНаУСНСВычетами;
	РаздельныйУчетНДСНаСчете19            = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Объект.Организация, Объект.Дата);
	ПлательщикНалогаНаПрибыль             = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата);
	ПрименениеУСН                         = УчетнаяПолитика.ПрименяетсяУСН(Объект.Организация, Объект.Дата);
	ПрименениеУСНДоходы                   = УчетнаяПолитика.ПрименяетсяУСНДоходы(Объект.Организация, Объект.Дата);
	ПрименяетсяАУСН                       = УчетнаяПолитика.ПрименяетсяАУСН(Объект.Организация, Объект.Дата);
	СпособОценкиТоваровВРознице           = УчетнаяПолитика.СпособОценкиТоваровВРознице(Объект.Организация, Объект.Дата);
	ИспользоватьТипыЦенНоменклатуры       = ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры");
	РаздельныйУчетНДС                     = УчетнаяПолитика.РаздельныйУчетНДС(Объект.Организация, Объект.Дата);
	РаздельныйУчетНДСНаСчете19            = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Объект.Организация, Объект.Дата);
	ЕстьВалютныйУчет                      = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
	ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров = 
										ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров");
	ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров = 
										ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров");
	ИспользуетсяПланированиеПлатежей          = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеПлатежейПоставщикам");
	ИспользоватьДоставкуТранспортнойКомпанией = ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуТранспортнойКомпанией");
	ВестиУчетПоДоговорам                      = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ОбратноеНачислениеНДС                     = БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()

	НастройкиУсловногоОформления = Новый Структура();

	УсловноеОформление.Элементы.Очистить();

	ОбновитьУсловноеОформление(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьУсловноеОформление(Форма)

	НадоИнициализировать = Ложь;
	
	Если НЕ Форма.НастройкиУсловногоОформления.Свойство("ОбщееПроинициализировано") Тогда
		НадоИнициализировать = Истина;
	КонецЕсли;

	Если НЕ Форма.НастройкиУсловногоОформления.Свойство("ПрименяютсяСтавки4и2Проинициализировано") Тогда
		НадоИнициализировать = Истина;
	КонецЕсли;
	
	Если НадоИнициализировать Тогда
		Форма.УстановитьУсловноеОформлениеДокумента();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьУсловноеОформлениеНаСервере() Экспорт

	Если ТипЗнч(НастройкиУсловногоОформления) = Тип("Структура") Тогда
		// При смене вида операции процедура вызывается из общего модуля 
		// ПоступлениеТоваровУслугФормы раньше, чем отработает 
		// УстановитьУсловноеОформление(). Поэтому вызываем обновление только
		// тогда, когда структура НастройкиУсловногоОформления уже была инициализирована.
		ОбновитьУсловноеОформление(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеДокумента() Экспорт

	Если НЕ НастройкиУсловногоОформления.Свойство("ОбщееПроинициализировано") Тогда
		УстановитьУсловноеОформлениеОбщее();
	КонецЕсли;
	
	Если НЕ НастройкиУсловногоОформления.Свойство("ПрименяютсяСтавки4и2Проинициализировано") Тогда
		УстановитьУсловноеОформлениеПрименяютсяСтавки4и2();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеОбщее()

	НастройкиУсловногоОформления.Вставить("ОбщееПроинициализировано", Истина);
	
	ПоступлениеТоваровУслугФормы.УстановитьУсловноеОформлениеРеквизитыДокумента(ЭтотОбъект);
	
	ПоступлениеТоваровУслугФормы.УстановитьУсловноеОформлениеУслуги(ЭтотОбъект);


	// УслугиСуммаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	ПоступлениеТоваровУслугФормы.ДобавитьОформляемоеПолеРасхождения(ЭлементУО.Поля, "УслугиСуммаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// УслугиСтавкаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСтавкаНДС");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НДСВключенВСтоимость", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"РаздельныйУчетНДСНаСчете19", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);


	// УслугиАналитикаУчета

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиАналитикаУчета");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Услуги.АналитикаУчетаЗаполнена", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);


	// ПорядокУчетаРасчетов

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПорядокУчетаРасчетов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СчетаРасчетовЗаполнены", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
	// Скрыть НДС при применении УСН доходы

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСтавкаНДС");
	
	ПоступлениеТоваровУслугФормы.ДобавитьОформляемоеПолеРасхождения(ЭлементУО.Поля, "УслугиСуммаНДС");
	ПоступлениеТоваровУслугФормы.ДобавитьОформляемоеПолеРасхождения(ЭлементУО.Поля, "УслугиВсего");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НДСНеВыделять", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПрименяютсяСтавки4и2()

	Если НЕ ПрименяютсяСтавки4и2 Тогда
		Возврат;
	КонецЕсли;

	НастройкиУсловногоОформления.Вставить("ПрименяютсяСтавки4и2Проинициализировано", Истина);


	// УслугиСтавкаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСтавкаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ПрименяютсяСтавки4и2", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС18);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС18_118);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '4 %'"));


	// УслугиСтавкаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСтавкаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ПрименяютсяСтавки4и2", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС10);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС10_110);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '2 %'"));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Если Объект.ЭтоУниверсальныйДокумент Тогда
		НеОтображатьРеквизитыСчетаФактуры = Истина;
	Иначе
		НеОтображатьРеквизитыСчетаФактуры = Объект.НДСНеВыделять;
	КонецЕсли;
	
	// Счет-фактура
	УчетНДСКлиентСервер.НастроитьПоляСчетаФактуры(
		Элементы.СчетФактураКнопка,
		Элементы.СчетФактураСсылка,
		Элементы.НадписьСчетФактура,
		НеОтображатьРеквизитыСчетаФактуры,
		Форма.ТребуетсяСчетФактура,
		Форма.СчетФактура);
	
	Элементы.ОригиналСчетаФактуры.Видимость = Форма.ТребуетсяСчетФактура И Не НеОтображатьРеквизитыСчетаФактуры;
	Элементы.ГруппаЧекНПД.Видимость = Форма.ЭтоДоговорССамозанятым;
	Элементы.УПД.Видимость = Не Форма.ЭтоДоговорССамозанятым;
	
	УчетНДСКлиентСервер.НастроитьПолеПродавецПоСчетуФактуре(
		Элементы.Продавец,
		Форма.ЭтоКомиссияНаЗакупку);
	
	УчетНДСКлиентСервер.ОтобразитьПолеУПД(Форма, Объект.ЭтоУниверсальныйДокумент);
	
	Элементы.ИтогиВсегоНДС.Видимость = Не Объект.НДСНеВыделять;
	
	ПоступлениеТоваровУслугФормыКлиентСервер.СформироватьНадписьЦеныИВалюта(Форма);
	
	ЭтоУслугиФакторинга = 
		(Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.УслугиФакторинга"));
	Элементы.УслугиКоличество.Видимость       = Не ЭтоУслугиФакторинга;
	Элементы.УслугиЦена.Видимость             = Не ЭтоУслугиФакторинга;
	Элементы.СчетНаОплатуПоставщика.Видимость = Не ЭтоУслугиФакторинга;
	Элементы.УслугиНоменклатураЕдиницаИзмерения.Видимость = Не ЭтоУслугиФакторинга;
	
	Элементы.АктОРасхождениях.Видимость = Объект.ЕстьРасхождения;
		
	Элементы.ДоговорКонтрагента.Доступность = 
		ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Контрагент);
	
	Элементы.ПодразделениеОрганизации.Доступность = ЗначениеЗаполнено(Объект.Организация);
	Элементы.ГруппаРасхождения.Видимость          = Форма.ИспользоватьКорректировочныеДокументы И НЕ ЭтоУслугиФакторинга;
	
	ДоступностьПорядокУчетаРасчетов = (Не Форма.ВестиУчетПоДоговорам И ЗначениеЗаполнено(Объект.Контрагент))
		Или ЗначениеЗаполнено(Объект.ДоговорКонтрагента);
	Элементы.ПорядокУчетаРасчетов.Доступность = ДоступностьПорядокУчетаРасчетов;
	Элементы.ПорядокУчетаРасчетов.Гиперссылка = ДоступностьПорядокУчетаРасчетов;
	
	УчетОригиналовПервичныхДокументовБПКлиентСервер.УстановитьДоступностьДекорацииСостояния(Форма);

	СтатусСамозанятого = Форма.СтатусСамозанятого;
	Если СтатусСамозанятого <> Неопределено Тогда
		ОформлениеСтатусаНПД =
			ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ДанныеДляОформленияСтатусаСамозанятогоНаФорме(
				СтатусСамозанятого.ПлательщикНПД, СтатусСамозанятого.Описание, СтатусСамозанятого.ТекстОшибки);
		Элементы.ГруппаДоговор.ЦветФона = ОформлениеСтатусаНПД.ЦветФона;
		Элементы.ДоговорКонтрагентаРасширеннаяПодсказка.Заголовок = ОформлениеСтатусаНПД.ЗаголовокПодсказки;
	Иначе
		Элементы.ГруппаДоговор.ЦветФона = Новый Цвет;
		Элементы.ДоговорКонтрагентаРасширеннаяПодсказка.Заголовок = "";
	КонецЕсли;
	ИнтеграцияСПлатформойСамозанятыеФормыБПКлиентСервер.НастроитьЭлементыЧекаНПД(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	ПоказыватьКонтрагентаСчетаФактуры = (НЕ ЗначениеЗаполнено(ЭтотОбъект.СчетФактура)) И ЭтоКомиссияНаЗакупку;
	Если Элементы.Продавец.Видимость <> ПоказыватьКонтрагентаСчетаФактуры Тогда
		Элементы.Продавец.Видимость = ПоказыватьКонтрагентаСчетаФактуры;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УслугиНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)

	ПоступлениеТоваровУслугФормы.УслугиНоменклатураПриИзменении(СтрокаТабличнойЧасти, ДанныеОбъекта, СчетаУчетаКЗаполнению);
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТабличнойЧасти, ДанныеОбъекта);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)

	Объект = Форма.Объект;

	Форма.ИтогиВсего    = Объект.Услуги.Итог("Всего");
	Форма.ИтогиВсегоНДС = Объект.Услуги.Итог("СуммаНДС");

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.ОрганизацияПриИзменении(ЭтотОбъект);
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзмененииНаКлиенте()

	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента, Объект.ВалютаДокумента, ВалютаРегламентированногоУчета);
		
	ТребуетсяПерерасчитатьСрокОплаты = 
		ИспользуетсяПланированиеПлатежей И АвторасчетСрокаОплаты И (НачалоДня(Объект.Дата) <> НачалоДня(ТекущаяДатаДокумента));
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера ИЛИ ТребуетсяПерерасчитатьСрокОплаты Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.ДатаПриИзменении(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ДатаВходящегоДокументаПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.ДатаВходящегоДокументаПриИзменении(ЭтотОбъект);

КонецПроцедуры


&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.КонтрагентПриИзменении(ЭтотОбъект);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	УстановитьВидимость();

КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	
	ПоступлениеТоваровУслугФормы.ДоговорКонтрагентаПриИзменении(ЭтотОбъект);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	ТребуетсяПерерасчитатьСрокОплаты =
		ИспользуетсяПланированиеПлатежей И АвторасчетСрокаОплаты;
			
	Если ТребуетсяПерерасчитатьСрокОплаты Тогда
		ОбновитьСрокОплатыНаСервере();
	КонецЕсли;
	
	УстановитьВидимость();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, Знач ПараметрыОбъекта)
	
	СтрокаТаблицы.Всего              = СтрокаТаблицы.Сумма + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	СтрокаТаблицы.ВсегоПоДокументу   = СтрокаТаблицы.СуммаПоДокументу + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДСПоДокументу);
	СтрокаТаблицы.ВсегоОтклонение    = СтрокаТаблицы.СуммаОтклонение + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДСОтклонение);
	
	ПоступлениеТоваровУслугФормыКлиентСервер.ЗаполнитьЗаголовкиСтрокРасхождения(СтрокаТаблицы);
		
	ЗаполнитьДобавленныеКолонкиСтрокиАналитикиУчета(СтрокаТаблицы, ПараметрыОбъекта);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(Знач Форма, ПараметрыОбъекта)

	Объект = Форма.Объект;
	
	Если ТипЗнч(ПараметрыОбъекта) <> Тип("Структура") Тогда
		ПараметрыОбъекта = Новый Структура;
	КонецЕсли;
	
	ПараметрыОбъекта.Вставить("СуммаВключаетНДС",               Объект.СуммаВключаетНДС);
	ПараметрыОбъекта.Вставить("НДСВключенВСтоимость",           Объект.НДСВключенВСтоимость);
	ПараметрыОбъекта.Вставить("ПлательщикНалогаНаПрибыль",      Форма.ПлательщикНалогаНаПрибыль);
	ПараметрыОбъекта.Вставить("ПрименениеУСН",                  Форма.ПрименениеУСН);
	ПараметрыОбъекта.Вставить("ПрименениеУСНДоходы",            Форма.ПрименениеУСНДоходы);
	ПараметрыОбъекта.Вставить("ПрименяетсяАУСН",                Форма.ПрименяетсяАУСН);
	ПараметрыОбъекта.Вставить("СчетУчетаРасчетовСКонтрагентом", Объект.СчетУчетаРасчетовСКонтрагентом);
	ПараметрыОбъекта.Вставить("УчетАгентскогоНДС",              Форма.УчетАгентскогоНДС);
	ПараметрыОбъекта.Вставить("ЕстьРасхождения",                Объект.ЕстьРасхождения);
	ПараметрыОбъекта.Вставить("РаздельныйУчетНДСНаСчете19",     Форма.РаздельныйУчетНДСНаСчете19);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиАналитикиУчета(ДанныеСтроки, Знач ПараметрыОбъекта)

	АналитикаУчетаТекст 	= "";
	АналитикаУчетаЗаполнена = Истина;

	ИспользоватьОднуНоменклатурнуюГруппу = БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу();
	СтроковыеПредставленияСубконто = Новый Структура();
	
	Если ДанныеСтроки.Свойство("СчетЗатрат") Тогда
		Если ЗначениеЗаполнено(ДанныеСтроки.СчетЗатрат) Тогда
			СвойстваСчета 		= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ДанныеСтроки.СчетЗатрат);
			
			АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + СвойстваСчета.Код;
			
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				Если ЗначениеЗаполнено(ДанныеСтроки.ПодразделениеЗатрат) Тогда
					ТекстСтроки 			= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСтроки.ПодразделениеЗатрат, "Наименование");
					АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", " + ТекстСтроки;
				Иначе
					АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
				КонецЕсли;
			КонецЕсли;
			
			Для Н = 1 По СвойстваСчета.КоличествоСубконто Цикл
				ЗначениеСубконто = ДанныеСтроки["Субконто" + Н];
				Если ЗначениеЗаполнено(ЗначениеСубконто) Тогда
					ПредставлениеСубконто = "";
					ТипСубконто = ТипЗнч(ЗначениеСубконто);
					Если ИспользоватьОднуНоменклатурнуюГруппу И ТипСубконто = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
						Продолжить;
					ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипСубконто) Тогда
						ПредставлениеСубконто = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеСубконто, "Представление");
					ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипСубконто) Тогда
						ПредставлениеСубконто = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеСубконто, "Представление");
					Иначе
						ПредставлениеСубконто = Строка(ЗначениеСубконто);
					КонецЕсли;
					
					СтроковыеПредставленияСубконто.Вставить("ЗначениеСубконто" + Н, ЗначениеСубконто);
					СтроковыеПредставленияСубконто.Вставить("ПредставлениеСубконто" + Н, ПредставлениеСубконто);
					
					АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + ПредставлениеСубконто;
				Иначе
					АналитикаУчетаТекст = АналитикаУчетаТекст + ", <...>";
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
			АналитикаУчетаЗаполнена = Ложь;
		КонецЕсли;
	КонецЕсли;

	// СчетУчетаНДС - до затрат НУ, т.к. аналитика НУ чаще всего совпадает с БУ
	Если ДанныеСтроки.Свойство("СчетУчетаНДС") Тогда
		Если ПараметрыОбъекта.НДСВключенВСтоимость
			И НЕ ПараметрыОбъекта.УчетАгентскогоНДС
			И НЕ ПараметрыОбъекта.РаздельныйУчетНДСНаСчете19 Тогда
			// в этих случаях счет учета НДС не показывается
		Иначе
			Если ЗначениеЗаполнено(ДанныеСтроки.СчетУчетаНДС) Тогда
				СвойстваСчета 		= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ДанныеСтроки.СчетУчетаНДС);
				АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + СвойстваСчета.Код;
			Иначе
				Если ДанныеСтроки.СуммаНДС <> 0 Тогда
					АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
					АналитикаУчетаЗаполнена = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеСтроки.Свойство("СпособУчетаНДС") Тогда
		Если ПараметрыОбъекта.РаздельныйУчетНДСНаСчете19 Тогда
			Если ЗначениеЗаполнено(ДанныеСтроки.СпособУчетаНДС) Тогда
				АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + Строка(ДанныеСтроки.СпособУчетаНДС);
			Иначе
				Если ДанныеСтроки.СуммаНДС <> 0 Тогда
					АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
					АналитикаУчетаЗаполнена = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// СчетУчетаЗатратНУ
	Если ДанныеСтроки.Свойство("СчетЗатратНУ") Тогда
		Если ПараметрыОбъекта.ПлательщикНалогаНаПрибыль Тогда
			Если ЗначениеЗаполнено(ДанныеСтроки.СчетЗатратНУ) Тогда
				СвойстваСчетаНУ 	= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ДанныеСтроки.СчетЗатратНУ);
				
				АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + СвойстваСчетаНУ.Код;
				
				Для Н = 1 По СвойстваСчетаНУ.КоличествоСубконто Цикл
					
					ЗначениеСубконтоНУ 	= ДанныеСтроки["СубконтоНУ" + Н];
					
					Если ЗначениеЗаполнено(ЗначениеСубконтоНУ) Тогда

						ПредставлениеСубконтоНУ = "";
						
						// Если совпадает с субконто БУ, то представление выводим как для БУ.
						ЗначениеСубконтоБУ = Неопределено;
						Если СтроковыеПредставленияСубконто.Свойство("ЗначениеСубконто" + Н, ЗначениеСубконтоБУ) Тогда
							Если ЗначениеСубконтоБУ = ЗначениеСубконтоНУ Тогда
								ПредставлениеСубконтоНУ = СтроковыеПредставленияСубконто["ПредставлениеСубконто" + Н];
							КонецЕсли;
						КонецЕсли;
						
						Если ПустаяСтрока(ПредставлениеСубконтоНУ) Тогда
							ТипСубконто = ТипЗнч(ЗначениеСубконтоНУ);
							Если ИспользоватьОднуНоменклатурнуюГруппу И ТипСубконто = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
								Продолжить;
							ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипСубконто) Тогда
								ПредставлениеСубконтоНУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеСубконтоНУ, "Представление");
							ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипСубконто) Тогда
								ПредставлениеСубконтоНУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеСубконтоНУ, "Представление");
							Иначе
								ПредставлениеСубконтоНУ = Строка(ЗначениеСубконтоНУ);
							КонецЕсли;
						КонецЕсли;
					
						АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + ПредставлениеСубконтоНУ;
					
					Иначе
						АналитикаУчетаТекст = АналитикаУчетаТекст + ", <...>";
					КонецЕсли;
					
				КонецЦикла;

			Иначе
				АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
				АналитикаУчетаЗаполнена = Ложь;
			КонецЕсли;

		КонецЕсли;
	КонецЕсли;

	ДанныеСтроки.АналитикаУчета 			= Сред(АналитикаУчетаТекст, 3);
	ДанныеСтроки.АналитикаУчетаЗаполнена 	= АналитикаУчетаЗаполнена;

КонецПроцедуры

&НаСервере
Процедура ПриИзмененииЦеныИВалютыНаСервере(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь)

	ПоступлениеТоваровУслугФормы.ЗаполнитьРассчитатьСуммы(
		ЭтотОбъект,
		ВалютаДоИзменения,
		КурсДоИзменения,
		КратностьДоИзменения,
		ПерезаполнитьЦены,
		ПересчитатьЦены,
		ПересчитатьНДС,
		ПрименяютсяСтавки4и2);

	ЗаполнитьДобавленныеКолонкиТаблиц();

КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПорядокУчетаРасчетовНаСервере(ВыбранноеЗначение)

	ПоступлениеТоваровУслугФормы.ОбработкаВыбораПорядокУчетаРасчетов(ЭтотОбъект, ВыбранноеЗначение);

	ЗаполнитьДобавленныеКолонкиТаблиц();

КонецПроцедуры


&НаСервере
Процедура ОбработкаВыбораУниверсальныйПередаточныйДокументНаСервере(ВыбранноеЗначение)
	
	ПоступлениеТоваровУслугФормы.ОбработкаВыбораУниверсальныйПередаточныйДокумент(ЭтотОбъект, ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицуПродавцыВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(ЭтотОбъект.Продавцы.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаСервере
Функция ПоместитьЗачетАвансовВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(Объект.ЗачетАвансов.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаКлиенте
Процедура ОткрытьФормуРедактированияСтроки(ВыбранноеЗначение)

	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Не ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;

	ДанныеСтроки 	= Объект.Услуги.НайтиПоИдентификатору(ВыбранноеЗначение);
			
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",       ТолькоПросмотр);
	ПараметрыФормы.Вставить("ДатаДокумента",        Объект.Дата);
	ПараметрыФормы.Вставить("Организация",          Объект.Организация);
	ПараметрыФормы.Вставить("СчетУчетаНДС",         ДанныеСтроки.СчетУчетаНДС);
	ПараметрыФормы.Вставить("СпособУчетаНДС",       ДанныеСтроки.СпособУчетаНДС);
	ПараметрыФормы.Вставить("СуммаНДС",             ДанныеСтроки.СуммаНДС);
	ПараметрыФормы.Вставить("НДСВключенВСтоимость", Объект.НДСВключенВСтоимость);
	ПараметрыФормы.Вставить("УчетАгентскогоНДС",    УчетАгентскогоНДС);
	
	ПараметрыФормы.Вставить("СчетЗатрат",          ДанныеСтроки.СчетЗатрат);
	ПараметрыФормы.Вставить("ПодразделениеЗатрат", ДанныеСтроки.ПодразделениеЗатрат);
	ПараметрыФормы.Вставить("Субконто1",           ДанныеСтроки.Субконто1);
	ПараметрыФормы.Вставить("Субконто2",           ДанныеСтроки.Субконто2);
	ПараметрыФормы.Вставить("Субконто3",           ДанныеСтроки.Субконто3);
	ПараметрыФормы.Вставить("СчетЗатратНУ",        ДанныеСтроки.СчетЗатратНУ);
	ПараметрыФормы.Вставить("СубконтоНУ1",         ДанныеСтроки.СубконтоНУ1);
	ПараметрыФормы.Вставить("СубконтоНУ2",         ДанныеСтроки.СубконтоНУ2);
	ПараметрыФормы.Вставить("СубконтоНУ3",         ДанныеСтроки.СубконтоНУ3);
	ПараметрыФормы.Вставить("Номенклатура",        ДанныеСтроки.Номенклатура);
	ПараметрыФормы.Вставить("Контрагент",          Объект.Контрагент);
	ПараметрыФормы.Вставить("ДоговорКонтрагента",  Объект.ДоговорКонтрагента);
	
	ОткрытьФорму("ОбщаяФорма.РедактированиеСчетаЗатрат", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораАналитикиУчетаНаКлиенте(ВыбранноеЗначение)

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	ДанныеСтроки = Новый Структура("Сумма, СчетУчетаНДС, СуммаНДС, СпособУчетаНДС,
		|СчетЗатрат, Субконто1, Субконто2, Субконто3,
		|СчетЗатратНУ, СубконтоНУ1, СубконтоНУ2, СубконтоНУ3,
		|ПодразделениеЗатрат,
		|ВсегоПоДокументу, СуммаПоДокументу, СуммаНДСПоДокументу,
		|ВсегоОтклонение, СуммаОтклонение, СуммаНДСОтклонение,
		|НадписьПоДокументу, НадписьПоФакту, НадписьОтклонение,
		|Всего, АналитикаУчета, АналитикаУчетаЗаполнена");
		
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ВыбранноеЗначение);
	
	ДанныеСтроки.Сумма               = ТекущиеДанные.Сумма;
	ДанныеСтроки.СуммаНДС            = ТекущиеДанные.СуммаНДС;
	ДанныеСтроки.СуммаПоДокументу    = ТекущиеДанные.СуммаПоДокументу;
	ДанныеСтроки.СуммаНДСПоДокументу = ТекущиеДанные.СуммаНДСПоДокументу;
	ДанныеСтроки.СуммаОтклонение     = ТекущиеДанные.СуммаОтклонение;
	ДанныеСтроки.СуммаНДСОтклонение  = ТекущиеДанные.СуммаНДСОтклонение;
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(ДанныеСтроки, ПараметрыОбъекта);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтроки, , 
		"Сумма, СуммаНДС, СуммаПоДокументу, СуммаНДСПоДокументу, СуммаОтклонение, СуммаНДСОтклонение");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаполненияПоСчетуНаКлиенте(ВыбранноеЗначение, ТабличнаяЧасть)

	ТекстВопроса = "Заполнить документ по счету?";
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
	ДополнительныеПараметры.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);

	Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьДокументПоСчетуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);

КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьДокументПоСчетуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОбработкаЗаполненияПоСчетуНаСервере(ДополнительныеПараметры.ВыбранноеЗначение, ДополнительныеПараметры.ТабличнаяЧасть);
	Иначе
		Объект.СчетНаОплатуПоставщика = ДополнительныеПараметры.ВыбранноеЗначение;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаЗаполненияПоСчетуНаСервере(ВыбранноеЗначение, ТабличнаяЧасть)

	ПоступлениеТоваровУслугФормы.ОбработкаЗаполненияПоСчету(ЭтотОбъект, ВыбранноеЗначение, ТабличнаяЧасть);

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	УправлениеФормой(ЭтотОбъект);
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СчетНаОплатуПоставщикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПоступлениеТоваровУслугФормыКлиент.ОткрытьФормуВыбораСчетаНаОплату(ЭтотОбъект, "Услуги");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетНаОплатуПоставщикаПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.СчетНаОплатуПоставщика) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаЗаполненияПоСчетуНаКлиенте(Объект.СчетНаОплатуПоставщика, "Услуги");
КонецПроцедуры


&НаСервере
Процедура ОбновитьСрокОплатыНаСервере()
	
	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
КонецПроцедуры

#Область ПроверкаКонтрагентов

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания) Экспорт	
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтотОбъект, ПараметрыФоновогоЗадания);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область СчетаУчета

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив(); 
	ЭлементыСчетов.Добавить("УслугиАналитикаУчета");
	
	// Поле формы ПорядокУчетаРасчетов содержит счета и другие реквизиты.
	// Поэтому видимостью этого поля управляем в УчетВзаиморасчетовФормы.УстановитьПорядокУчетаРасчетов()
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентов(Команда)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентовВДокументеПоКнопке(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура СкопироватьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КоличествоСтрок = Элементы[ИмяТаблицы].ВыделенныеСтроки.Количество();
	Если КоличествоСтрок = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	СкопироватьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОКопированииСтрокВБуферОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(Команда)
	
	ИмяТаблицы = ПолучитьИмяТекущейТабличнойЧасти();
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		
		Возврат;
		
	КонецЕсли;
	КоличествоСтрок = ВставитьСтрокиНаСервере(ИмяТаблицы);
	ОбработкаТабличныхЧастейКлиент.ОповеститьОВставкеСтрокИзБуфераОбмена(ЭтотОбъект, КоличествоСтрок);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ВыбранноеЗначение, ИмяТаблицы)

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	ДобавленныеСтроки = ПоступлениеТоваровУслугФормы.ОбработкаВыбораПодборВставкаИзБуфера(ЭтотОбъект, ВыбранноеЗначение, ИмяТаблицы);
	
	Для Каждого СтрокаТаблицы Из ДобавленныеСтроки.Услуги Цикл
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, ПараметрыОбъекта);
	КонецЦикла;
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

#Область КопированиеВставкаСтрокЧерезБуферОбмена

&НаСервере
Процедура СкопироватьСтрокиНаСервере(ИмяТаблицы)
	
	ОбщегоНазначения.СкопироватьСтрокиВБуферОбмена(Объект[ИмяТаблицы], 
		Элементы[ИмяТаблицы].ВыделенныеСтроки, Объект.Ссылка.Метаданные().Имя);

КонецПроцедуры

&НаСервере
Функция ВставитьСтрокиНаСервере(ИмяТаблицы)
	
	ПараметрыВставки = ОбработкаТабличныхЧастей.ПолучитьПараметрыВставкиДанныхИзБуфераОбмена(Объект.Ссылка, ИмяТаблицы);
	ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки);
	ОбработкаВыбораПодборВставкаИзБуфераНаСервере(ПараметрыВставки, ПараметрыВставки.ИмяТаблицы);
	
	Возврат ПараметрыВставки.КоличествоДобавленныхСтрок;
	
КонецФункции

&НаКлиенте
Функция ПолучитьИмяТекущейТабличнойЧасти()
	
	Возврат "Услуги";
	
КонецФункции

&НаСервере
Процедура ОпределитьСписокСвойствДляВставкиИзБуфера(ПараметрыВставки)

	СписокСвойств = Новый Массив;
	СписокСвойств.Добавить("Номенклатура");
	СписокСвойств.Добавить("Содержание");
	СписокСвойств.Добавить("Количество");
	СписокСвойств.Добавить("Цена");
	СписокСвойств.Добавить("СтавкаНДС");
	Если ПараметрыВставки.ПоказыватьСчетаУчетаВДокументах И ПараметрыВставки.ИсточникИДокументОдногоВида Тогда
		
		СписокСвойств.Добавить("СчетЗатрат");
		СписокСвойств.Добавить("ПодразделениеЗатрат");
		СписокСвойств.Добавить("Субконто1");
		СписокСвойств.Добавить("Субконто2");
		СписокСвойств.Добавить("Субконто3");
		СписокСвойств.Добавить("СчетЗатратНУ");
		СписокСвойств.Добавить("СубконтоНУ1");
		СписокСвойств.Добавить("СубконтоНУ2");
		СписокСвойств.Добавить("СубконтоНУ3");
		СписокСвойств.Добавить("СчетУчетаНДС");
		СписокСвойств.Добавить("СпособУчетаНДС");
		
	КонецЕсли;
	
	ПараметрыВставки.СписокСвойств = ОбработкаТабличныхЧастей.ПолучитьСписокСвойствИмеющихсяВТаблицеДанных(
		ПараметрыВставки.Данные, СписокСвойств);
	
КонецПроцедуры
	
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандыВставки(Форма, Доступность)

	Доступность = Не Форма.ТолькоПросмотр И Доступность;
	Элементы = Форма.Элементы;
	Элементы.УслугиВставитьСтроки.Доступность					 = Доступность;
	Элементы.УслугиКонтекстноеМенюВставитьСтроки.Доступность	 = Доступность;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПоДокументуПриИзменении(Элемент)
	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииКоличество(ЭтотОбъект, "Услуги", ПрименяютсяСтавки4и2, 1, "ПоДокументу");
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПоДокументуПриИзменении(Элемент)
	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииЦена(ЭтотОбъект, "Услуги", ПрименяютсяСтавки4и2, 1, "ПоДокументу");
КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПоДокументуПриИзменении(Элемент)
	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "Услуги", ПрименяютсяСтавки4и2, 1, "ПоДокументу");
КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаНДСПоДокументуПриИзменении(Элемент)
	ПоступлениеТоваровУслугФормыКлиентСервер.ПриИзмененииСуммаНДС(ЭтотОбъект, "Услуги", "ПоДокументу");
КонецПроцедуры


// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти


&НаСервере
Процедура ОбработкаВыбораПорядокУчетаРасчетовСКомиссионеромНаСервере(ВыбранноеЗначение)
	
	Объект.СчетУчетаРасчетовСКонтрагентом  = ВыбранноеЗначение.СчетУчетаРасчетовЗаПосредническиеУслуги;
	Объект.СчетУчетаРасчетовПоАвансам      = ВыбранноеЗначение.СчетУчетаРасчетовПоАвансамВыданным;
	Объект.СчетУчетаРасчетовСКомиссионером = ВыбранноеЗначение.СчетУчетаРасчетовСКонтрагентом;
	
	Объект.СпособЗачетаАвансов = ВыбранноеЗначение.СпособЗачетаАвансов;
	
	Если ПолучитьФункциональнуюОпцию("УправлениеЗачетомАвансовПогашениемЗадолженности") Тогда
		ТаблицаЗачетАвансов = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресХранилищаЗачетАвансов);
		Объект.ЗачетАвансов.Загрузить(ТаблицаЗачетАвансов);
	КонецЕсли;
	
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ИзменитьВидимостьПодсказокНаСервере()
	
	УправлениеПанельюПодсказки.ИзменитьВидимостьПодсказок(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыСозданияНовогоДоговора()
	
	Возврат ПоступлениеТоваровУслугФормы.ПараметрыСозданияНовогоДоговора(ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_НачатьПолучениеСтатусаСамозанятого()
	
	Если ПолученЧекНПД Тогда
		СтатусСамозанятого = Неопределено;
		УправлениеФормой(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБПКлиент.ДатаПолученияСтатуса(
		Объект.Дата, Объект.ДатаВходящегоДокумента);
	СохраненныйСтатус = ИнтеграцияСтатусСамозанятогоБПКлиент.СтатусСамозанятого(
		ЭтотОбъект, Объект.ДоговорКонтрагента, ДатаПолученияСтатуса);
	
	Если СохраненныйСтатус <> Неопределено Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДокумента(
			ЭтотОбъект, СохраненныйСтатус.ПлательщикНПД, СохраненныйСтатус.ДатаРегистрации);
	Иначе
		СтатусСамозанятого = Неопределено;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСтатусСамозанятого()
	
	Если Не ПолученЧекНПД Тогда
		ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБП.ДатаПолученияСтатуса(
			Объект.Дата, Объект.ДатаВходящегоДокумента);
		СохраненныйСтатус = ИнтеграцияСтатусСамозанятогоБП.СохраненныйСтатус(
			Объект.ДоговорКонтрагента, ДатаПолученияСтатуса);
		Если СохраненныйСтатус <> Неопределено Тогда
			СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДокумента(
				ЭтотОбъект, СохраненныйСтатус.ПлательщикНПД, СохраненныйСтатус.ДатаРегистрации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеФормыЧекаСамозанятогоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИнтеграцияСПлатформойСамозанятыеБПКлиент.НовыеДанныеЧекаЗагружены(Результат, ЭтотОбъект) Тогда
		Модифицированность = Истина;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	АдаптироватьМКСписокФормы();
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	Элементы.ДатаВходящегоДокумента.Заголовок = НСтр("ru = 'Акт, УПД Дата'");
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаВхНомерДата);
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьНомерДатаФормаДокумента(Элементы, Элементы.ГруппаНомерДата);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаНомерДата);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Организация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаКонтрагент);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ДоговорКонтрагента);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.СчетНаОплатуПоставщика);
	
	Элементы.ЦеныИВалюта.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ЦеныИВалюта.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ЦеныИВалюта);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаЛевая);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаПравая);
	
	Элементы.УПД.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.УПД);
	
	Элементы.ГруппаСчетФактураНомерДата.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.ДатаСчетаФактурыПолученного.Заголовок = НСтр("ru = 'Счет-фактура от'");
	Элементы.ЗарегистрироватьСчетФактуру.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.СчетФактураПросмотр);
	
	Элементы.СчетФактураРедактирование.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.Переместить(Элементы.ЗарегистрироватьСчетФактуру, Элементы.СчетФактураРедактирование);
	
	Элементы.ОригиналПолучен.Заголовок = НСтр("ru = 'Оригинал получен'");
	Элементы.ОригиналПолучен.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.ОригиналСчетаФактуры.Заголовок = НСтр("ru = 'Оригинал СФ получен'");
	Элементы.ОригиналСчетаФактуры.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.ПолученЧекНПД.Заголовок = НСтр("ru = 'Оригинал Чека НПД получен'");
	Элементы.ПолученЧекНПД.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.ГруппаОригиналы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	//Скроем пустую группу
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ДекорацияОригинал);
	
	Элементы.ГруппаКомментарийОтветственный.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаПодвал);
	
	Элементы.ВалютаДокументаИтогиВсего.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ВалютаДокументаИтогиВсего.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВалютаДокументаИтогиВсего.РастягиватьПоГоризонтали = Истина;
	Элементы.ВалютаДокументаИтогиВсего.Ширина = 0;
	
	Элементы.ГруппаИтогиВсегоСФоном.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
	Элементы.ГруппаИтогиВсегоСФоном.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаИтогиВсего.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Авто;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаИтоги);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаКоманднаяПанель.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, Элементы.ПодменюПечать.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "Услуги");
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыПодбор ="УслугиПодборУслуги";
	КомандыПоля.ИмяКомандыДобавитьСтроку = "УслугиДобавить";
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаУслуги;
	ОписаниеГруппыПоля.ПутьКДаннымЗаголовка = "Объект.Услуги.КоличествоСтрок";
	ОписаниеГруппыПоля.ПоказатьГруппуНового = Истина;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.КолонкаНомерСтроки = "УслугиНомерСтроки";
	ОписаниеКолонокПоля.ВажныеКолонки = "ГруппаУслугиНоменклатура,УслугиНоменклатура,УслугиСодержание";
	ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = "УслугиНоменклатура,УслугиСодержание,УслугиАналитикаУчета";
	ОписаниеКолонокПоля.СкрытьКолонки = "УслугиОтступ,УслугиГруппаЗаголовкиСтрок";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
		Элементы.Услуги, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
	
	Элементы.ЕстьРасхождения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	//МобильныйКлиентБП.ВстроитьЭлементФормыВГруппаМКФормаТабличныеЧасти(Элементы,
	//	Элементы.ГруппаРасхождения, Элементы.Услуги, ОписаниеГруппыПоля);
	
	Элементы.ГруппаУслугиНоменклатура.Высота = 2;
	Элементы.УслугиСодержание.Ширина = 20;
	Элементы.УслугиНоменклатура.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
	
	Элементы.УслугиКоличество.Формат = "ЧФ='Кол-во: Ч'";
	Элементы.УслугиЦена.Формат = "ЧФ='Цена: Ч'";
	Элементы.УслугиСумма.Формат = "ЧФ='Сумма: Ч'";
	Элементы.УслугиСуммаНДС.Формат = "ЧФ='НДС: Ч'";
	Элементы.УслугиВсего.Формат = "ЧФ='Всего: Ч'";
	
КонецПроцедуры

#КонецОбласти

#Область  РедактированиеКомментарияВЗакрытомПериоде

&НаКлиенте
Процедура Подключаемый_РедактироватьКомментарий(Команда) 
	
	КомментарииДокументовЗакрытогоПериодаКлиент.ИзменитьКомментарий(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ЭтоНоваяСтрока = Ложь;

#КонецОбласти

