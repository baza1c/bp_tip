
#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;
&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

&НаКлиенте
Перем ЭтоНоваяСтрока;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоступлениеТоваровУслугФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	Если ЗначениеЗаполнено(Объект.Услуги) И Не ЗначениеЗаполнено(Объект.Права) Тогда
		Объект.Права.Загрузить(Объект.Услуги.Выгрузить());
		Объект.Услуги.Очистить();
	КонецЕсли;
	
	УстановитьВидимостьСчетовУчета();
	
	УстановитьУсловноеОформление();
	
	УчетНДС.ПрименитьПраваДоступаСчетаФактуры(
		СчетФактура,
		Элементы.СчетФактураПросмотр,
		Элементы.СчетФактураРедактирование);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереДокумент(ЭтотОбъект, Параметры);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.ПоступлениеТоваровУслуг",
		"ФормаДокументаПрава",
		НСтр("ru='Новости: Приобретение прав (акт, УПД)'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ФормаДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами".
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(
			ЭтотОбъект,
			Элементы.ГруппаГлобальныеКоманды);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ИсточникВыбора.ИмяФормы = "Документ.СчетНаОплатуПоставщика.Форма.ФормаВыбора" Тогда
		ОбработкаЗаполненияПоСчетуНаКлиенте(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ВыборПорядкаУчетаРасчетов" Тогда
		ОбработкаВыбораПорядокУчетаРасчетовНаСервере(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаРедактированиеНазначенияИспользования" Тогда
		ОбработкаВыбораАналитикиУчетаНаКлиенте(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.УниверсальныйПередаточныйДокумент" Тогда
		ОбработкаВыбораУниверсальныйПередаточныйДокументНаСервере(ВыбранноеЗначение);
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Обработка.ПодборНематериальныхАктивов.Форма.Форма" Тогда
		ОбработкаВыбораПодборОбъектовПравНаСервере(ВыбранноеЗначение);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_СчетФактураПолученный"
		И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ДокументыОснования") И Параметр.ДокументыОснования.Найти(Объект.Ссылка) <> Неопределено Тогда
		ПоступлениеТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыПроСчетФактуру(ЭтотОбъект, Параметр.РеквизитыСФ);
		СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
			Объект.Ссылка, СчетФактура);
		ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ОбновитьДокументИБПослеЗаполнения" И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
		Прочитать();
	ИначеЕсли ИмяСобытия = "ИзмененСтатусДокументов" Тогда
		// Статус документа изменен в форме списка поступлений или счетов-фактур.
		Если Не Объект.Ссылка.Пустая() 
			И (Параметр.Найти(Объект.Ссылка) <> Неопределено ИЛИ Параметр.Найти(СчетФактура) <> Неопределено) Тогда
			СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
				Объект.Ссылка, СчетФактура);
			ОригиналПолучен      = СтатусыОригиналовДокументов[Объект.Ссылка];
			УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(
				ЭтотОбъект, ОригиналПолучен);
			ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
			ПолученЧекНПД        = СтатусыОригиналовДокументов[ЧекиНПДКлиентСервер.ИмяСтатусаЧекаНПД()];
		КонецЕсли;
	ИначеЕсли ИмяСобытия = РаботаСДоговорамиКонтрагентовБПКлиент.ИмяСобытияЗаписьДоговора()
		И Объект.ДоговорКонтрагента = Источник Тогда
		
		ДоговорКонтрагентаПриИзмененииНаСервере();
		УправлениеФормой(ЭтотОбъект);
		
		ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
		
	ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоПолучен()
		И Объект.ДоговорКонтрагента = Источник Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДокумента(
			ЭтотОбъект, Параметр.ПлательщикНПД, Параметр.ДатаРегистрации);
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = ИнтеграцияСтатусСамозанятогоКлиент.ИмяСобытия_СтатусСамозанятогоОшибка()
		И Объект.ДоговорКонтрагента = Источник Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДокумента(
			ЭтотОбъект, Ложь, , Параметр);
		УправлениеФормой(ЭтотОбъект);
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если УчетОригиналовПервичныхДокументовБПКлиент.ЭтоСобытиеИзмененияСостоянияОриганалаПервичногоДокумента(
			Объект.Ссылка, ИмяСобытия, Источник) Тогда
		СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
			Объект.Ссылка, СчетФактура);
		ОригиналПолучен = СтатусыОригиналовДокументов[Объект.Ссылка];
		УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(
			ЭтотОбъект, ОригиналПолучен);
	КонецЕсли;
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыОповещения = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаДокумента();
	ПараметрыОповещения.Форма = ЭтотОбъект;
	ПараметрыОповещения.ДокументСсылка = Объект.Ссылка;
	ПараметрыОповещения.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыОповещения.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// Подсистема "ОбменСКонтрагентами"
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец Подсистема "ОбменСКонтрагентами"
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПрочитатьСтатусСамозанятого();
	
	ПоступлениеТоваровУслугФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиБП.КомандыЭДО_ПриЧтенииФормыДокумента(ЭтотОбъект);
	// Конец подсистема "ОбменСКонтрагентами". 
	
	КомментарииДокументовЗакрытогоПериодаВызовСервера.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект, "ГруппаКомментарийОтветственный", "Ответственный");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормыКлиент.ПередЗаписью(
		ЭтотОбъект, Отказ, ПараметрыЗаписи);
			
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПоступлениеТоваровУслугФормы.ПослеЗаписиНаСервере(
		ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
	УчетОригиналовПервичныхДокументовБПКлиентСервер.УстановитьДоступностьДекорацииСостояния(ЭтотОбъект);
	
	УчетНДСКлиентСервер.ОтобразитьПолеУПД(ЭтотОбъект, ТекущийОбъект.ЭтоУниверсальныйДокумент);
	
	УстановитьСостояниеДокумента();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// Подсистема "ОбменСКонтрагентами".
	ПараметрыПослеЗаписи = ОбменСКонтрагентами.ПараметрыПослеЗаписиНаСервере();
	ПараметрыПослеЗаписи.Форма = ЭтотОбъект;
	ПараметрыПослеЗаписи.ДокументСсылка = Объект.Ссылка;
	ПараметрыПослеЗаписи.КонтроллерСостояниеЭДО = Элементы.СостояниеЭДО;
	ПараметрыПослеЗаписи.ГруппаСостояниеЭДО = Элементы.ГруппаСостояниеЭДО;
	
	ОбменСКонтрагентами.ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи, ПараметрыПослеЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
	РаботаСДоговорамиКонтрагентовБП.СохранитьПараметрыДоговора(
		ТекущийОбъект.ДоговорКонтрагента,
		ТекущийОбъект.ЭтоУниверсальныйДокумент,
		ТекущийОбъект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ПоступлениеТоваровУслугФормы.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормы.ПриЗаписиНаСервере(
		ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормыКлиент.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
	// Подсистема "ОбменСКонтрагентами".
	ОбменСКонтрагентамиКлиент.ПослеЗаписи_ФормаДокумента(ЭтотОбъект, ПараметрыЗаписи);
	// Конец подсистема "ОбменСКонтрагентами".
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерВходящегоДокументаПриИзменении(Элемент)
	
	ПоступлениеТоваровУслугФормыКлиент.НомерВходящегоДокументаПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВходящегоДокументаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДатаВходящегоДокумента) И НЕ Объект.Проведен Тогда
		Объект.Дата = Объект.ДатаВходящегоДокумента + (Объект.Дата - НачалоДня(Объект.Дата));
		ДатаПриИзмененииНаКлиенте();
	КонецЕсли;
	
	ПоступлениеТоваровУслугФормыКлиент.ДатаВходящегоДокументаПриИзменении(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)

	ДатаПриИзмененииНаКлиенте();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		КонтрагентПриИзмененииНаСервере();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элемент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОткрытиеФормыВыбораКонтрагенты", , Истина)

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		
		ДоговорКонтрагентаПриИзмененииНаСервере();
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбщегоНазначенияБПКлиент.ОткрытьДоговорКонтрагента(ЭтотОбъект, Элемент, Объект.ДоговорКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаСоздание(Элемент, СтандартнаяОбработка)
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(Объект.ДоговорКонтрагента),
		"", Элемент.ТекстРедактирования);
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорСоздание(
		Элемент, ВведенноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПараметрыДоговора = ПараметрыСозданияНовогоДоговора();
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОбработкаВыбора(
		Элемент, ВыбранноеЗначение, ПараметрыДоговора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ЗаполнитьСписокВыбора(
		Элемент, Текст, ПредлагатьНовыйДоговор, СтандартнаяОбработка);
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовБПКлиент.ДоговорОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУчетаРасчетовНажатие(Элемент, СтандартнаяОбработка)
	
	АдресХранилищаЗачетАвансов = ПоместитьЗачетАвансовВоВременноеХранилищеНаСервере();

	ПоступлениеТоваровУслугФормыКлиент.ПорядокУчетаРасчетовНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка, АдресХранилищаЗачетАвансов);

КонецПроцедуры

&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)

	ПоступлениеТоваровУслугФормыКлиент.ЦеныИВалютаНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДОНажатие(Элемент, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НомерСчетаФактурыПолученногоПриИзменении(Элемент)
	
	ПоступлениеТоваровУслугФормыКлиент.НомерСчетаФактурыПолученногоПриИзменении(ЭтотОбъект, Элемент);	
	
КонецПроцедуры

&НаКлиенте
Процедура УПДПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУПДНажатие(Элемент, СтандартнаяОбработка)
	
	АдресХранилищаПродавцы = ПоместитьТаблицуПродавцыВоВременноеХранилищеНаСервере();
	УчетНДСКлиент.ОткрытьФормуРеквизитовУПД(ЭтотОбъект, СтандартнаяОбработка, АдресХранилищаПродавцы);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентСоздание(Элемент, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СчетНаОплатуПоставщикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПоступлениеТоваровУслугФормыКлиент.ОткрытьФормуВыбораСчетаНаОплату(ЭтотОбъект, "Права");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетНаОплатуПоставщикаПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.СчетНаОплатуПоставщика) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаЗаполненияПоСчетуНаКлиенте(Объект.СчетНаОплатуПоставщика, "Права");
КонецПроцедуры

&НаКлиенте
Процедура НадписьСчетФактураНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	БухгалтерскийУчетКлиентПереопределяемый.ОткрытьСчетФактуру(ЭтотОбъект, СчетФактура, "СчетФактураПолученный");
	
КонецПроцедуры

&НаКлиенте
Процедура ОригиналПолученПриИзменении(Элемент)
	
	УчетОригиналовПервичныхДокументовБПКлиент.ЗаполнитьДекорациюСостояния(
		ЭтотОбъект, ОригиналПолучен);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеОригиналаБПНажатие(Элемент)
	
	СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
		Объект.Ссылка, СчетФактура);
	Если ОригиналПолучен <> СтатусыОригиналовДокументов[Объект.Ссылка] Тогда
		// Пользователь изменил статус документа.
		// Перед открытием оригиналов первичных документов БСП нужно сначала сохранить статус,
		// чтобы обновились статусы первичных документов БСП.
		ПоступлениеТоваровУслугФормыВызовСервера.ЗаписатьСтатусДокументаНаСервере(Объект.Ссылка, ОригиналПолучен);
	КонецЕсли;
	
	УчетОригиналовПервичныхДокументовКлиент.ОткрытьФормуИзмененияСостоянийПечатныхФорм(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки =
		ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ИмяСобытияВызовСервиса() Тогда
		СтандартнаяОбработка = Ложь;
		ИННСамозанятого = РаботаСДоговорамиКонтрагентовБПВызовСервера.ИННВладельца(Объект.ДоговорКонтрагента);
		ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБПКлиент.ДатаПолученияСтатуса(
			Объект.Дата, Объект.ДатаВходящегоДокумента);
		Если ИнтеграцияСтатусСамозанятогоКлиент.НачатьПолучениеСтатусаСамозанятого(
			Объект.ДоговорКонтрагента, ЭтотОбъект, ИННСамозанятого, ДатаПолученияСтатуса) Тогда
			Элемент.Заголовок = ИнтеграцияСтатусСамозанятогоФормыБПКлиент.ЗаголовокПодсказкиПолучениеСтатусаВПроцессе();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученЧекНПДПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_НачатьПолучениеСтатусаСамозанятого", 0.1, Истина);
	Если Не ПолученЧекНПД Тогда
		ИнтеграцияСПлатформойСамозанятыеБПКлиент.ОчиститьРеквизитыЧекаНПД(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПрава

&НаКлиенте
Процедура ПраваПриАктивизацииЯчейки(Элемент)
	ТекущиеДанные = Элементы.Права.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоНоваяСтрока Тогда
		ТекущаяКолонкаТаблицы = Элементы.Права.ТекущийЭлемент;
		Если ТекущаяКолонкаТаблицы.Имя = "ПраваНазначениеИспользованияПредставление"
			И Не НазначениеИспользованияЗаполнено(ТекущиеДанные) Тогда
			ВыбраннаяСтрока = Элементы.Права.ТекущаяСтрока;
			ОткрытьФормуРедактированияНазначенияИспользования(ВыбраннаяСтрока);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПраваПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ЭтоНоваяСтрока = НоваяСтрока;
	
	Если Не НоваяСтрока Или Копирование Тогда
		Возврат;
	КонецЕсли;

	СтрокаТаблицы = Элементы.Права.ТекущиеДанные;
	СтрокаТаблицы.НазначениеИспользованияПредставление = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования И НоваяСтрока Тогда
		// Закроем форму редактирования строки, т.к. пользователь отменяет ввод
		Оповестить("ФормаРедактированияСтроки_Закрыть", Неопределено, ЭтотОбъект);
	ИначеЕсли Объект.НДСНеВыделять Тогда
		ТекущиеДанные = Элементы.Права.ТекущиеДанные;
		Если Не ЗначениеЗаполнено(ТекущиеДанные.СтавкаНДС) тогда
			ТекущиеДанные.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС")
		КонецЕсли;
	КонецЕсли;

	ЭтоНоваяСтрока = Ложь;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваПриИзменении(Элемент)
	ОбновитьИтоги(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПраваВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Поле.Имя = "ПраваНазначениеИспользованияПредставление" Тогда
		ОткрытьФормуРедактированияНазначенияИспользования(ВыбраннаяСтрока);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПраваОбъектПраваСоздание(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Права.ТекущиеДанные;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);

	ТекстЗаполнения = Элемент.ТекстРедактирования;

	Если Не ЗначениеЗаполнено(Элемент.ТекстРедактирования) Тогда
		ТекстЗаполнения = ТекущиеДанные.Содержание;
	КонецЕсли;

	ПараметрыФормы.Вставить("ТекстЗаполнения", ТекстЗаполнения);
	ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущиеДанные.ОбъектПрава);
	ПараметрыФормы.Вставить("ОтражениеБУ", ПредопределенноеЗначение("Перечисление.ВариантыОтраженияНематериальныхАктивовБУ.Расходы"));
	ПараметрыФормы.Вставить("ОтражениеНУ", ПредопределенноеЗначение("Перечисление.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования"));
	
	ОткрытьФорму("Справочник.НематериальныеАктивы.Форма.ФормаЭлемента", ПараметрыФормы, Элемент);
		
КонецПроцедуры

&НаКлиенте
Процедура ПраваКоличествоПриИзменении(Элемент)
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Права");
КонецПроцедуры

&НаКлиенте
Процедура ПраваЦенаПриИзменении(Элемент)
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "Права", 1);
КонецПроцедуры

&НаКлиенте
Процедура ПраваСуммаПриИзменении(Элемент)
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "Права", 1);
КонецПроцедуры

&НаКлиенте
Процедура ПраваСтавкаНДСПриИзменении(Элемент)
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСтавкаНДС(ЭтотОбъект, "Права");
КонецПроцедуры

&НаКлиенте
Процедура ПраваСуммаНДСПриИзменении(Элемент)
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаНДС(ЭтотОбъект, "Права");
КонецПроцедуры

&НаКлиенте
Процедура ПраваНазначениеИспользованияПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ВыбраннаяСтрока = Элементы.Права.ТекущаяСтрока;
	ОткрытьФормуРедактированияНазначенияИспользования(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПраваОбъектПраваПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Права.ТекущиеДанные;
	
	КонтейнерОбъект = Новый Структура;
	КонтейнерОбъект.Вставить("ВидОперации", Объект.ВидОперации);
	КонтейнерОбъект.Вставить("ЭтоКомиссия", ЭтоКомиссия);
	КонтейнерОбъект.Вставить("Дата", Объект.Дата);
	
	КонтейнерСтрока = НовыйДанныеСтроки();
	ЗаполнитьЗначенияСвойств(КонтейнерСтрока, ТекущиеДанные);
	
	ПараметрыЗаполненияСчетовУчета = ПоступлениеТоваровУслугФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"Права.ОбъектПрава",
		Объект,
		ТекущиеДанные,
		КонтейнерОбъект,
		КонтейнерСтрока);
	
	Изменения = ПраваОбъектПравПриИзмененииНаСервере(КонтейнерСтрока, КонтейнерОбъект, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Изменения);

	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборОбъектовПрав(Команда)

	СтруктураПараметров = Новый Структура;
	Если Объект.Права.Количество() > 0 Тогда
		СтруктураПараметров.Вставить("АдресВХранилище", ПоместитьОбъектыПравВХранилище());
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ПодборНематериальныхАктивов.Форма.Форма", СтруктураПараметров, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСчетФактуру(Команда)
	
	ПоступлениеТоваровУслугФормыКлиент.ЗарегистрироватьСчетФактуру(ЭтотОбъект, Команда);	
	УправлениеФормой(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.НадписьСчетФактура);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентов(Команда)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентовВДокументеПоКнопке(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЧекНПД(Команда)
	
	ИнтеграцияСПлатформойСамозанятыеБПКлиент.ЗагрузитьЧекНПД(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(ИмяТаблицы = "") Экспорт

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	Если ПустаяСтрока(ИмяТаблицы) Или ИмяТаблицы = "Права" Тогда
		Для Каждого СтрокаТаблицы Из Объект.Права Цикл
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПрава(СтрокаТаблицы, ПараметрыОбъекта);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиНаСервере() Экспорт

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере() Экспорт
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьСостояниеДокумента();
	
	ТекущаяДатаДокумента = Объект.Дата;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента) 
		И ЗначениеЗаполнено(Объект.Контрагент)
		И Не ЗначениеЗаполнено(Объект.Ссылка) 
		И Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ПоступлениеТоваровУслугФормы.КонтрагентПриИзменении(ЭтотОбъект);
	ИначеЕсли ВестиУчетПоДоговорам И ПоступлениеТоваровУслугФормы.ДоступноСозданиеОсновногоДоговораПоВидуОперации(Объект.ВидОперации) Тогда
		ПредлагатьНовыйДоговор = РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент);
	КонецЕсли;
	
	ПоступлениеТоваровУслугФормы.УстановитьПараметрыПоДоговору(ЭтотОбъект);
	
	УстановитьПараметрыВыбора();
	
	ЗаполнитьДобавленныеКолонкиТаблиц();

	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);

	ПоступлениеТоваровУслугФормыКлиентСервер.СформироватьНадписьЦеныИВалюта(ЭтотОбъект);
	
	ОбновитьИтоги(ЭтотОбъект);
	
	ПоступлениеТоваровУслугФормыКлиентСервер.УстановитьТребуетсяСчетФактура(ЭтотОбъект);
	ПоступлениеТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыПроСчетФактуру(ЭтотОбъект);
	ПоступлениеТоваровУслугФормы.ЗаполнитьРеквизитыУПД(ЭтотОбъект);
	
	СтатусыОригиналовДокументов = ПоступлениеТоваровУслугФормыВызовСервера.СтатусыОригиналовДокументов(
		Объект.Ссылка, СчетФактура);
	ОригиналПолучен      = СтатусыОригиналовДокументов[Объект.Ссылка];
	ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
	ПолученЧекНПД        = СтатусыОригиналовДокументов[ЧекиНПДКлиентСервер.ИмяСтатусаЧекаНПД()];
	
	УчетОригиналовПервичныхДокументовБП.ПриСозданииНаСервере_ФормаДокумента(ЭтотОбъект, ОригиналПолучен);
	
	УправлениеФормой(ЭтотОбъект);
	
	УстановитьВидимость();
	
	ПоступлениеТоваровУслугФормы.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбора()

	МассивВидовДоговоров = Новый ФиксированныйМассив(
		ПоступлениеТоваровУслугФормы.ВидыДоговоровДокумента(Объект.ВидОперации));

	НовыйМассивПараметров = Новый Массив();
	НовыйМассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора", МассивВидовДоговоров));
	
	ПоступлениеТоваровУслугФормы.ПараметрыВыбораПоВидуОперации(НовыйМассивПараметров, Объект.ВидОперации);

	НовыеПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассивПараметров);
	Элементы.ДоговорКонтрагента.ПараметрыВыбора = НовыеПараметрыВыбора;

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦеныИВалюты(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь) Экспорт

	Если ПерезаполнитьЦены Или ПересчитатьЦены Или ПересчитатьНДС Тогда
		ПриИзмененииЦеныИВалютыНаСервере(ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены, ПересчитатьЦены, ПересчитатьНДС);
	КонецЕсли;
	
	ПоступлениеТоваровУслугФормыКлиентСервер.УстановитьТребуетсяСчетФактура(ЭтотОбъект);
	
	ОбновитьИтоги(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура УправлениеФормойНаСервере() Экспорт
	
	УправлениеФормой(ЭтотОбъект);
	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораДляСчетовУчета() Экспорт
	
	// В форме услуг параметры выбора счетов учета не используются.
	// Процедура необходима для совместимости с другими формами документа.
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы() Экспорт

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	ИспользоватьКорректировочныеДокументы = ПолучитьФункциональнуюОпцию("ИспользоватьКорректировочныеДокументы");
	ПлательщикНДСНаУСНСВычетами           = УчетнаяПолитика.ПлательщикНДСНаУСНСВычетами(Объект.Организация, Объект.Дата);
	ПлательщикНДС                         = УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата) ИЛИ ПлательщикНДСНаУСНСВычетами;
	ПлательщикНалогаНаПрибыль             = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата);
	ПрименениеУСН                         = УчетнаяПолитика.ПрименяетсяУСН(Объект.Организация, Объект.Дата);
	ПрименениеУСНДоходы                   = УчетнаяПолитика.ПрименяетсяУСНДоходы(Объект.Организация, Объект.Дата);
	ПрименяетсяАУСН                       = УчетнаяПолитика.ПрименяетсяАУСН(Объект.Организация, Объект.Дата);
	ИспользоватьТипыЦенНоменклатуры       = ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры");
	РаздельныйУчетНДС                     = УчетнаяПолитика.РаздельныйУчетНДС(Объект.Организация, Объект.Дата);
	РаздельныйУчетНДСНаСчете19            = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Объект.Организация, Объект.Дата);
	ЕстьВалютныйУчет                      = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
	ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров = 
										ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров");
	ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров = 
										ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров");
	ИспользуетсяПланированиеПлатежей          = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеПлатежейПоставщикам");
	ИспользоватьДоставкуТранспортнойКомпанией = ПолучитьФункциональнуюОпцию("ИспользоватьДоставкуТранспортнойКомпанией");
	ВестиУчетПоДоговорам                      = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ОбратноеНачислениеНДС                     = БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ОбработкаВыбораПодборОбъектовПравНаСервере(ВыбранноеЗначение)
	
	ТаблицаНМА = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресВХранилище);
	
	ИсходныеДанные = Объект.Права.Выгрузить();
	ИсходныеДанные.Индексы.Добавить("ОбъектПрава");
	
	ДобавленныеСтроки = Новый Массив;
	
	// В форму подбора передаются все объекты прав из документа. Некоторые из них могут быть там удалены.
	// При возвращении на форму документа строки, соответствующие удаленным объектам, также удаляются из табличной части.
	Объект.Права.Очистить();
	Для Каждого СтрокаТаблицы Из ТаблицаНМА Цикл
		ЭлементКоллекции = Объект.Права.Добавить();
		
		СтрокаИсходныхДанных = ИсходныеДанные.Найти(СтрокаТаблицы.НематериальныйАктив, "ОбъектПрава");
		Если СтрокаИсходныхДанных <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЭлементКоллекции, СтрокаИсходныхДанных);
		Иначе
			ДобавленныеСтроки.Добавить(ЭлементКоллекции);
		КонецЕсли;
			
		ЗаполнитьЗначенияСвойств(ЭлементКоллекции, СтрокаТаблицы);
		ЭлементКоллекции.ОбъектПрава = СтрокаТаблицы.НематериальныйАктив;
	КонецЦикла;
	
	КонтейнерОбъект = Новый Структура;
	КонтейнерОбъект.Вставить("ВидОперации", Объект.ВидОперации);
	КонтейнерОбъект.Вставить("ЭтоКомиссия", ЭтоКомиссия);
	КонтейнерОбъект.Вставить("Дата", Объект.Дата);

	СчетаУчетаВДокументах.ЗаполнитьСтроки(ДобавленныеСтроки, "Права", КонтейнерОбъект, Документы.ПоступлениеТоваровУслуг);
	
	Для Каждого ДанныеСтроки Из ДобавленныеСтроки Цикл
		
		ИнформацияОбОбъектеПрава =
			Справочники.НематериальныеАктивы.ИнформацияОбОбъектеПрава(ДанныеСтроки.ОбъектПрава, Объект.Дата);
			
		Результат = Новый Структура;
		Результат.Вставить("Содержание", ИнформацияОбОбъектеПрава.Наименование);
		Результат.Вставить("СрокИспользования", ИнформацияОбОбъектеПрава.Срок);
		
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, Результат);
		
		ЗаполнитьДобавленныеКолонкиСтрокиНазначениеИспользования(ДанныеСтроки, Объект);
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НазначениеИспользованияЗаполнено(СтрокаТаблицы)
	Возврат ЗначениеЗаполнено(СтрокаТаблицы.Счет) И ЗначениеЗаполнено(СтрокаТаблицы.СрокИспользования);
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	НастройкиУсловногоОформления = Новый Структура();

	УсловноеОформление.Элементы.Очистить();

	ОбновитьУсловноеОформление(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьУсловноеОформление(Форма)

	НадоИнициализировать = Ложь;
	
	Если НЕ Форма.НастройкиУсловногоОформления.Свойство("ОбщееПроинициализировано") Тогда
		НадоИнициализировать = Истина;
	КонецЕсли;
	
	Если НадоИнициализировать Тогда
		Форма.УстановитьУсловноеОформлениеДокумента();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПраваОбъектПравПриИзмененииНаСервере(Знач ДанныеСтроки, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)
	
	Результат = Новый Структура;
	
	Результат.Вставить("НазначениеИспользованияПредставление", "");
	Результат.Вставить("Счет", ДанныеСтроки.Счет);
	Результат.Вставить("Подразделение", ДанныеСтроки.Подразделение);
		
	ЗаполненныеСчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.ПоступлениеТоваровУслуг,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"Права",
		ДанныеСтроки);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ДанныеСтроки, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ЗаполненныеСчетаУчета, Истина);
	
	ИнформацияОбОбъектеПрава =
		Справочники.НематериальныеАктивы.ИнформацияОбОбъектеПрава(ДанныеСтроки.ОбъектПрава, ДанныеОбъекта.Дата);
		
	Результат.Вставить("Содержание", ИнформацияОбОбъектеПрава.Наименование);
	Результат.Вставить("СрокИспользования", ИнформацияОбОбъектеПрава.Срок);
			
	УстановитьСтатьюЗатратЛицензииОтечественноеПО(Результат, ИнформацияОбОбъектеПрава);
	
	ЗаполнитьДобавленныеКолонкиСтрокиНазначениеИспользования(Результат, ДанныеОбъекта);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОбновитьУсловноеОформлениеНаСервере() Экспорт

	Если ТипЗнч(НастройкиУсловногоОформления) = Тип("Структура") Тогда
		// При смене вида операции процедура вызывается из общего модуля 
		// ПоступлениеТоваровУслугФормы раньше, чем отработает 
		// УстановитьУсловноеОформление(). Поэтому вызываем обновление только
		// тогда, когда структура НастройкиУсловногоОформления уже была инициализирована.
		ОбновитьУсловноеОформление(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеДокумента() Экспорт

	Если НЕ НастройкиУсловногоОформления.Свойство("ОбщееПроинициализировано") Тогда
		УстановитьУсловноеОформлениеОбщее();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеОбщее()
	
	ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	
	НастройкиУсловногоОформления.Вставить("ОбщееПроинициализировано", Истина);
	
	ПоступлениеТоваровУслугФормы.УстановитьУсловноеОформлениеРеквизитыДокумента(ЭтотОбъект);
	
	// ПраваСуммаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
			"Объект.Права.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
			"Объект.Права.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ПраваСтавкаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПраваСтавкаНДС");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НДСВключенВСтоимость", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"РаздельныйУчетНДСНаСчете19", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	// ПраваНазначениеИспользованияПредставление

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПраваНазначениеИспользованияПредставление");

	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
		Если ПользовательУправляетСчетамиУчета Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
				"Объект.Права.Счет", ВидСравненияКомпоновкиДанных.НеЗаполнено);
		КонецЕсли;

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
			"Объект.Права.СрокИспользования", ВидСравненияКомпоновкиДанных.НеЗаполнено);
			
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
		Если ПользовательУправляетСчетамиУчета Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
				"Объект.Права.Счет", ВидСравненияКомпоновкиДанных.НеЗаполнено);
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора,
			"Объект.Права.СрокИспользования", ВидСравненияКомпоновкиДанных.НеЗаполнено);
			
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначения());

	// ПорядокУчетаРасчетов

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПорядокУчетаРасчетов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СчетаРасчетовЗаполнены", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
	// Скрыть НДС при применении УСН доходы

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПраваСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПраваСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПраваВсего");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НДСНеВыделять", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Если Объект.ЭтоУниверсальныйДокумент Тогда
		НеОтображатьРеквизитыСчетаФактуры = Истина;
	Иначе
		НеОтображатьРеквизитыСчетаФактуры = Объект.НДСНеВыделять;
	КонецЕсли;
	
	// Счет-фактура
	УчетНДСКлиентСервер.НастроитьПоляСчетаФактуры(
		Элементы.СчетФактураКнопка,
		Элементы.СчетФактураСсылка,
		Элементы.НадписьСчетФактура,
		НеОтображатьРеквизитыСчетаФактуры,
		Форма.ТребуетсяСчетФактура,
		Форма.СчетФактура);
		
	Элементы.ОригиналСчетаФактуры.Видимость = Форма.ТребуетсяСчетФактура И Не НеОтображатьРеквизитыСчетаФактуры;
	Элементы.ГруппаЧекНПД.Видимость = Форма.ЭтоДоговорССамозанятым;
	Элементы.УПД.Видимость = Не Форма.ЭтоДоговорССамозанятым;
	
	УчетНДСКлиентСервер.НастроитьПолеПродавецПоСчетуФактуре(
		Элементы.Продавец,
		Форма.ЭтоКомиссияНаЗакупку);
		
	УчетНДСКлиентСервер.ОтобразитьПолеУПД(Форма, Объект.ЭтоУниверсальныйДокумент);
	
	Элементы.ИтогиВсегоНДС.Видимость = Не Объект.НДСНеВыделять;
	
	ПоступлениеТоваровУслугФормыКлиентСервер.СформироватьНадписьЦеныИВалюта(Форма);
	
	Элементы.ДоговорКонтрагента.Доступность = 
		ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Контрагент);
	
	Элементы.ПодразделениеОрганизации.Доступность = ЗначениеЗаполнено(Объект.Организация);
	
	ДоступностьПорядокУчетаРасчетов = (НЕ Форма.ВестиУчетПоДоговорам И ЗначениеЗаполнено(Объект.Контрагент)) ИЛИ ЗначениеЗаполнено(Объект.ДоговорКонтрагента);
	Элементы.ПорядокУчетаРасчетов.Доступность = ДоступностьПорядокУчетаРасчетов;
	Элементы.ПорядокУчетаРасчетов.Гиперссылка = ДоступностьПорядокУчетаРасчетов;
	
	УчетОригиналовПервичныхДокументовБПКлиентСервер.УстановитьДоступностьДекорацииСостояния(Форма);
	
	СтатусСамозанятого = Форма.СтатусСамозанятого;
	Если СтатусСамозанятого <> Неопределено Тогда
		ОформлениеСтатусаНПД =
			ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.ДанныеДляОформленияСтатусаСамозанятогоНаФорме(
				СтатусСамозанятого.ПлательщикНПД, СтатусСамозанятого.Описание, СтатусСамозанятого.ТекстОшибки);
		Элементы.ГруппаДоговор.ЦветФона = ОформлениеСтатусаНПД.ЦветФона;
		Элементы.ДоговорКонтрагентаРасширеннаяПодсказка.Заголовок = ОформлениеСтатусаНПД.ЗаголовокПодсказки;
	Иначе
		Элементы.ГруппаДоговор.ЦветФона = Новый Цвет;
		Элементы.ДоговорКонтрагентаРасширеннаяПодсказка.Заголовок = "";
	КонецЕсли;
	ИнтеграцияСПлатформойСамозанятыеФормыБПКлиентСервер.НастроитьЭлементыЧекаНПД(Форма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	ПоказыватьКонтрагентаСчетаФактуры = (Не ЗначениеЗаполнено(СчетФактура)) И ЭтоКомиссияНаЗакупку;
	Если Элементы.Продавец.Видимость <> ПоказыватьКонтрагентаСчетаФактуры Тогда
		Элементы.Продавец.Видимость = ПоказыватьКонтрагентаСчетаФактуры;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)

	Объект = Форма.Объект;

	Форма.ИтогиВсего    = Объект.Права.Итог("Всего");
	Форма.ИтогиВсегоНДС = Объект.Права.Итог("СуммаНДС");

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.ОрганизацияПриИзменении(ЭтотОбъект);
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзмененииНаКлиенте()

	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента, Объект.ВалютаДокумента, ВалютаРегламентированногоУчета);
		
	ТребуетсяПерерасчитатьСрокОплаты = 
		ИспользуетсяПланированиеПлатежей И АвторасчетСрокаОплаты И (НачалоДня(Объект.Дата) <> НачалоДня(ТекущаяДатаДокумента));
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера ИЛИ ТребуетсяПерерасчитатьСрокОплаты Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.ДатаПриИзменении(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.КонтрагентПриИзменении(ЭтотОбъект);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	УстановитьВидимость();

КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	
	ПоступлениеТоваровУслугФормы.ДоговорКонтрагентаПриИзменении(ЭтотОбъект);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиБП.ПриИзмененииКлючевыхРеквизитовЭДО(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	ТребуетсяПерерасчитатьСрокОплаты =
			ИспользуетсяПланированиеПлатежей И АвторасчетСрокаОплаты;
			
	Если ТребуетсяПерерасчитатьСрокОплаты Тогда
		ОбновитьСрокОплатыНаСервере();
	КонецЕсли;
	
	УстановитьВидимость();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПрава(СтрокаТаблицы, Знач ПараметрыОбъекта)
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
		
	ЗаполнитьДобавленныеКолонкиСтрокиНазначениеИспользования(СтрокаТаблицы, ПараметрыОбъекта);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(Знач Форма, ПараметрыОбъекта)

	Объект = Форма.Объект;
	
	Если ТипЗнч(ПараметрыОбъекта) <> Тип("Структура") Тогда
		ПараметрыОбъекта = Новый Структура;
	КонецЕсли;
	
	ПараметрыОбъекта.Вставить("Дата",             Объект.Дата);
	ПараметрыОбъекта.Вставить("Организация",      Объект.Организация);
	ПараметрыОбъекта.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиНазначениеИспользования(ДанныеСтроки, Знач ПараметрыОбъекта)
	
	ПредставлениеСрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Срок: %1'"),
		?(ЗначениеЗаполнено(ДанныеСтроки.СрокИспользования),
			Формат(ДанныеСтроки.СрокИспользования,"Л=ru;") + НСтр("ru=' мес.'"),
			ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначения()));
	
	ЧастиСтрокиНазначениеИспользования = Новый Массив;
	ЧастиСтрокиНазначениеИспользования.Добавить(ПредставлениеСрока);
	
	// Если в настройках пользователя отключено отображение счетов учета, то в поле "Назначение использования" отображается
	// только срок использования.
	Если СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета() Тогда
		ДанныеДляПредставленияАналитики = НовыйДанныеПредставленияАналитики();
		ЗаполнитьЗначенияСвойств(ДанныеДляПредставленияАналитики, ПараметрыОбъекта);
	
		ЗаполнитьДанныеАналитики(ДанныеДляПредставленияАналитики, ДанныеСтроки);
		
		ПредставлениеАналитики = БухгалтерскийУчет.ПредставлениеАналитикиУчетаЗатрат(ДанныеДляПредставленияАналитики);
		
		ЧастиСтрокиНазначениеИспользования.Добавить(ПредставлениеАналитики);
	КонецЕсли;
	
	ДанныеСтроки.НазначениеИспользованияПредставление = СтрСоединить(ЧастиСтрокиНазначениеИспользования, "; ");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДанныеАналитики(ДанныеДляПредставленияАналитики, ДанныеСтроки)
	ЗаполнитьЗначенияСвойств(ДанныеДляПредставленияАналитики, ДанныеСтроки);
	ДанныеДляПредставленияАналитики.СчетЗатрат = ДанныеСтроки.Счет;
	ДанныеДляПредставленияАналитики.ПодразделениеЗатрат = ДанныеСтроки.Подразделение;
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовыйДанныеПредставленияАналитики()

	Данные = Новый Структура();
	
	Данные.Вставить("Организация");
	Данные.Вставить("Дата");
	Данные.Вставить("СчетЗатрат");
	Данные.Вставить("ПодразделениеЗатрат");
	Данные.Вставить("Субконто1");
	Данные.Вставить("Субконто2");
	Данные.Вставить("Субконто3");
	
	Возврат Данные;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииЦеныИВалютыНаСервере(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь)

	ПоступлениеТоваровУслугФормы.ЗаполнитьРассчитатьСуммы(
		ЭтотОбъект,
		ВалютаДоИзменения,
		КурсДоИзменения,
		КратностьДоИзменения,
		ПерезаполнитьЦены,
		ПересчитатьЦены,
		ПересчитатьНДС);

КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПорядокУчетаРасчетовНаСервере(ВыбранноеЗначение)

	ПоступлениеТоваровУслугФормы.ОбработкаВыбораПорядокУчетаРасчетов(ЭтотОбъект, ВыбранноеЗначение);

	ЗаполнитьДобавленныеКолонкиТаблиц();

КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораУниверсальныйПередаточныйДокументНаСервере(ВыбранноеЗначение)
	
	ПоступлениеТоваровУслугФормы.ОбработкаВыбораУниверсальныйПередаточныйДокумент(ЭтотОбъект, ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицуПродавцыВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(Продавцы.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаСервере
Функция ПоместитьЗачетАвансовВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(Объект.ЗачетАвансов.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаКлиенте
Процедура ОткрытьФормуРедактированияНазначенияИспользования(ВыбранноеЗначение)

	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Не ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;

	ДанныеСтроки   = Объект.Права.НайтиПоИдентификатору(ВыбранноеЗначение);
	
	ПараметрыФормы = ПараметрыФормыНазначениеИспользования();
	
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Объект);
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ДанныеСтроки);
	ЗаполнитьЗначенияСвойств(ПараметрыФормы.НазначениеИспользования, ДанныеСтроки);
	
	ПараметрыФормы.Стоимость = ДанныеСтроки.Всего - ДанныеСтроки.СуммаНДС;
	
	Если РаздельныйУчетНДСНаСчете19
		И (ДанныеСтроки.СпособУчетаНДС = ПредопределенноеЗначение("Перечисление.СпособыУчетаНДС.УчитываетсяВCтоимости")
			Или ДанныеСтроки.СпособУчетаНДС = ПредопределенноеЗначение("Перечисление.СпособыУчетаНДС.Распределяется"))Тогда
		ПараметрыФормы.Стоимость = ДанныеСтроки.Всего;
	КонецЕсли;
	
	ОткрытьФорму(
		"Документ.ПоступлениеТоваровУслуг.Форма.ФормаРедактированиеНазначенияИспользования",
		ПараметрыФормы,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("РедактированиеНазначенияИспользованияЗавершение", ЭтотОбъект));

КонецПроцедуры

&НаКлиенте
Процедура РедактированиеНазначенияИспользованияЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = Элементы.Права.ТекущиеДанные;
	
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Или ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, РезультатЗакрытия);
	
	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);
	
	ДанныеСтроки = НовыйДанныеСтроки();
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ТекущиеДанные);
	ЗаполнитьДобавленныеКолонкиСтрокиНазначениеИспользования(ДанныеСтроки, ПараметрыОбъекта);
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтроки);
КонецПроцедуры

&НаКлиенте
Функция ПараметрыФормыНазначениеИспользования()

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр");
	ПараметрыФормы.Вставить("Дата");
	ПараметрыФормы.Вставить("Организация");
	
	ПараметрыФормы.Вставить("ОбъектПрава");
	ПараметрыФормы.Вставить("Стоимость");
	ПараметрыФормы.Вставить("НазначениеИспользования", НовыйНазначениеИспользования());
	ПараметрыФормы.Вставить("КурсВзаиморасчетов");
	ПараметрыФормы.Вставить("КратностьВзаиморасчетов");
	ПараметрыФормы.Вставить("ВалютаДокумента");
		
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НовыйНазначениеИспользования()
	
	Сведения = Новый Структура;
	
	Сведения.Вставить("Счет");
	Сведения.Вставить("Подразделение");
	Сведения.Вставить("Субконто1");
	Сведения.Вставить("Субконто2");
	Сведения.Вставить("Субконто3");
	Сведения.Вставить("СрокИспользования");
	
	Возврат Сведения;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораАналитикиУчетаНаКлиенте(ВыбранноеЗначение)

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	ДанныеСтроки = НовыйДанныеСтроки();
		
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ВыбранноеЗначение);
	
	ДанныеСтроки.Сумма               = ТекущиеДанные.Сумма;
	ДанныеСтроки.СуммаНДС            = ТекущиеДанные.СуммаНДС;
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПрава(ДанныеСтроки, ПараметрыОбъекта);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтроки, , 
		"Сумма, СуммаНДС");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция НовыйДанныеСтроки()
	
	ДанныеСтроки = Новый Структура;
	ДанныеСтроки.Вставить("Счет");
	ДанныеСтроки.Вставить("СчетУчетаНДС");
	ДанныеСтроки.Вставить("СуммаНДС");
	ДанныеСтроки.Вставить("СпособУчетаНДС");
	ДанныеСтроки.Вставить("Субконто1");
	ДанныеСтроки.Вставить("Субконто2");
	ДанныеСтроки.Вставить("Субконто3");
	ДанныеСтроки.Вставить("Подразделение");
	ДанныеСтроки.Вставить("Всего");
	ДанныеСтроки.Вставить("СрокИспользования");
	ДанныеСтроки.Вставить("НазначениеИспользованияПредставление");
	ДанныеСтроки.Вставить("ОбъектПрава");

	Возврат ДанныеСтроки;
		
КонецФункции

&НаКлиенте
Процедура ОбработкаЗаполненияПоСчетуНаКлиенте(ВыбранноеЗначение, ТабличнаяЧасть)

	ТекстВопроса = "Заполнить документ по счету?";
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
	ДополнительныеПараметры.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);

	Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьДокументПоСчетуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);

КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьДокументПоСчетуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОбработкаЗаполненияПоСчетуНаСервере(ДополнительныеПараметры.ВыбранноеЗначение, ДополнительныеПараметры.ТабличнаяЧасть);
	Иначе
		Объект.СчетНаОплатуПоставщика = ДополнительныеПараметры.ВыбранноеЗначение;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаЗаполненияПоСчетуНаСервере(ВыбранноеЗначение, ТабличнаяЧасть)

	ПоступлениеТоваровУслугФормы.ОбработкаЗаполненияПоСчету(ЭтотОбъект, ВыбранноеЗначение, ТабличнаяЧасть);

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	УправлениеФормой(ЭтотОбъект);
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ОбновитьСрокОплатыНаСервере()
	
	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьОбъектыПравВХранилище()

	ТаблицаНМА = Объект.Права.Выгрузить(, "НомерСтроки, ОбъектПрава");
	// Форма подбора рассчитана на название реквизита "Нематериальный актив".
	ТаблицаНМА.Колонки.Найти("ОбъектПрава").Имя = "НематериальныйАктив";
	Возврат ПоместитьВоВременноеХранилище(ТаблицаНМА);

КонецФункции

#Область ПроверкаКонтрагентов

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
// 
// Параметры:
//  ПараметрыФоновогоЗадания - см. ПроверкаКонтрагентовКлиентСервер.ПараметрыФоновогоЗадания
&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания) Экспорт
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтотОбъект, ПараметрыФоновогоЗадания);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область СчетаУчета

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив(); 
	ЭлементыСчетов.Добавить("ПраваСчетУчетаНДС");
	ЭлементыСчетов.Добавить("ПраваСпособУчетаНДС");
	
	// Поле формы ПорядокУчетаРасчетов содержит счета и другие реквизиты.
	// Поэтому видимостью этого поля управляем в УчетВзаиморасчетовФормы.УстановитьПорядокУчетаРасчетов()
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
	Если Не СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета() Тогда
		Элементы.ПраваНазначениеИспользованияПредставление.Заголовок = НСтр("ru = 'Срок использования'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
	МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаСервере
Функция ПараметрыСозданияНовогоДоговора()
	
	Возврат ПоступлениеТоваровУслугФормы.ПараметрыСозданияНовогоДоговора(ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_НачатьПолучениеСтатусаСамозанятого()
	
	Если ПолученЧекНПД Тогда
		СтатусСамозанятого = Неопределено;
		УправлениеФормой(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБПКлиент.ДатаПолученияСтатуса(
		Объект.Дата, Объект.ДатаВходящегоДокумента);
	СохраненныйСтатус = ИнтеграцияСтатусСамозанятогоБПКлиент.СтатусСамозанятого(
		ЭтотОбъект, Объект.ДоговорКонтрагента, ДатаПолученияСтатуса);
	
	Если СохраненныйСтатус <> Неопределено Тогда
		СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДокумента(
			ЭтотОбъект, СохраненныйСтатус.ПлательщикНПД, СохраненныйСтатус.ДатаРегистрации);
	Иначе
		СтатусСамозанятого = Неопределено;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСтатусСамозанятого()
	
	Если Не ПолученЧекНПД Тогда
		ДатаПолученияСтатуса = ИнтеграцияСтатусСамозанятогоБП.ДатаПолученияСтатуса(
			Объект.Дата, Объект.ДатаВходящегоДокумента);
		СохраненныйСтатус = ИнтеграцияСтатусСамозанятогоБП.СохраненныйСтатус(
			Объект.ДоговорКонтрагента, ДатаПолученияСтатуса);
		Если СохраненныйСтатус <> Неопределено Тогда
			СтатусСамозанятого = ИнтеграцияСтатусСамозанятогоФормыБПКлиентСервер.РезультатПроверкиСтатусаСамозанятогоДляДокумента(
				ЭтотОбъект, СохраненныйСтатус.ПлательщикНПД, СохраненныйСтатус.ДатаРегистрации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатьюЗатратЛицензииОтечественноеПО(Результат, ИнформацияОбОбъектеПрава)
	
	Если ПлательщикНалогаНаПрибыль
		И УчетНМА.ПовышающийКоэффициентНалоговогоУчета(Объект.Дата) > 1 Тогда
		Если ИнформацияОбОбъектеПрава.ЭтоЛицензияНаОтечественноеПО Тогда
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Результат.Счет);
			НомерСубконто = СвойстваСчета.ИдентификаторыСубконто.Получить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
			Если ЗначениеЗаполнено(НомерСубконто) Тогда
				// Статья должна быть уже создана, но в исключительных случаях, например, при использовании РИБ это могло еще не произойти.
				УстановитьПривилегированныйРежим(Истина);
				Результат["Субконто" + НомерСубконто] = Справочники.СтатьиЗатрат.ПредопределенныйЭлемент("ПриобретениеКомпьютерныхЛицензийСПовышающимКоэффициентом");
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеФормыЧекаСамозанятогоЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИнтеграцияСПлатформойСамозанятыеБПКлиент.НовыеДанныеЧекаЗагружены(Результат, ЭтотОбъект) Тогда
		Модифицированность = Истина;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	АдаптироватьМКСписокФормы();
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы, Элементы.ГруппаКоманднаяПанель.ПодчиненныеЭлементы);
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВПодменюМКПоделиться(Элементы, Элементы.ПодменюПечать.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()

	Элементы.ДатаВходящегоДокумента.Заголовок = НСтр("ru = 'Накладная, УПД Дата'");
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаВхНомерДата);
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьНомерДатаФормаДокумента(Элементы, Элементы.ГруппаНомерДата);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаНомерДата);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.Организация);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаКонтрагент);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ДоговорКонтрагента);
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.СчетНаОплатуПоставщика);
	
	Элементы.ЦеныИВалюта.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ЦеныИВалюта.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ЦеныИВалюта);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаЛевая);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаШапкаПравая);
	
	Элементы.УПД.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.УПД);
	
	Элементы.ГруппаСчетФактураНомерДата.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.ДатаСчетаФактурыПолученного.Заголовок = НСтр("ru = 'Счет-фактура от'");
	Элементы.ЗарегистрироватьСчетФактуру.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПрочее(Элементы, Элементы.СчетФактураПросмотр);
	
	Элементы.СчетФактураРедактирование.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.Переместить(Элементы.ЗарегистрироватьСчетФактуру, Элементы.СчетФактураРедактирование);
	
	Элементы.ОригиналПолучен.Заголовок = НСтр("ru = 'Оригинал получен'");
	Элементы.ОригиналПолучен.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.ОригиналСчетаФактуры.Заголовок = НСтр("ru = 'Оригинал СФ получен'");
	Элементы.ОригиналСчетаФактуры.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.ПолученЧекНПД.Заголовок = НСтр("ru = 'Оригинал Чека НПД получен'");
	Элементы.ПолученЧекНПД.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.ГруппаОригиналы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ДекорацияОригинал);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиЭлементыВГруппаМКФормаПрочее(Элементы,
		Элементы.ГруппаПодвал);
	
	//Скроем пустую группу
	МобильныйКлиентБПФормаОбъекта.СкрытьЭлементФормы(Элементы, Элементы.ГруппаОригиналы);
	
	Элементы.ВалютаДокументаИтогиВсего.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ВалютаДокументаИтогиВсего.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ВалютаДокументаИтогиВсего.РастягиватьПоГоризонтали = Истина;
	Элементы.ВалютаДокументаИтогиВсего.Ширина = 0;
	
	Элементы.ГруппаИтогиВсегоСФоном.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
	Элементы.ГруппаИтогиВсегоСФоном.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаИтоги.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Авто;
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаПодвал(Элементы, Элементы.ГруппаИтоги);
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.АдаптироватьЭлементыМКФормы(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект, "", "Права");
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКомандТабличноеПолеФормы();
	КомандыПоля.ИмяКомандыДобавитьСтроку = "ПраваДобавить";
	
	ОписаниеГруппыПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеГруппыТабличноеПолеФормы();
	ОписаниеГруппыПоля.ЭлементГруппаПоля = Элементы.ГруппаПрава;
	ОписаниеГруппыПоля.ПутьКДаннымЗаголовка = "Объект.Права.КоличествоСтрок";
	ОписаниеГруппыПоля.ПоказатьГруппуНового = Истина;
	
	ОписаниеКолонокПоля = МобильныйКлиентБПФормаОбъекта.НовыйОписаниеКолонокПоляТабличноеПолеФормы();
	ОписаниеКолонокПоля.КолонкаНомерСтроки = "ПраваНомерСтроки";
	ОписаниеКолонокПоля.ВажныеКолонки = "ПраваОбъектПрава,ПраваСодержание";
	ОписаниеКолонокПоля.УстановитьВерхниеЗаголовки = 
		"ПраваОбъектПрава,ПраваСодержание";
	ОписаниеКолонокПоля.СкрытьКолонки = "ПраваОтступ";
	
	МобильныйКлиентБПФормаОбъекта.ПодготовитьТабличноеПолеФормыДокумента(ЭтотОбъект,
		Элементы.Права, ОписаниеГруппыПоля, КомандыПоля, ОписаниеКолонокПоля);
	
	Элементы.ПраваОбъектПрава.ФиксацияВТаблице = ФиксацияВТаблице.Лево; 
	Элементы.ГруппаОбъектПрава.Высота = 2;
	Элементы.ПраваСодержание.Ширина = 20;
	
	Элементы.ПраваКоличество.Формат = "ЧФ='Кол-во: Ч'";
	Элементы.ПраваЦена.Формат = "ЧФ='Цена: Ч'";
	Элементы.ПраваСумма.Формат = "ЧФ='Сумма: Ч'";
	Элементы.ПраваСуммаНДС.Формат = "ЧФ='НДС: Ч'";
	Элементы.ПраваВсего.Формат = "ЧФ='Всего: Ч'";
	
КонецПроцедуры

#КонецОбласти

#Область  РедактированиеКомментарияВЗакрытомПериоде

&НаКлиенте
Процедура Подключаемый_РедактироватьКомментарий(Команда) 
	
	КомментарииДокументовЗакрытогоПериодаКлиент.ИзменитьКомментарий(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ЭтоНоваяСтрока = Ложь

#КонецОбласти

