
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Организация, Дата, ТолькоПросмотр, ОбъектПрава, Стоимость, ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов");
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.НазначениеИспользования);
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
		ДатаНачалаДействияПрава = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НастроитьФормуПоОбъектуПрава();
	
	ЭтоПлательщикНДФЛ = УчетнаяПолитика.ПлательщикНДФЛ(Организация, Дата);	
	
	Если СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета() Тогда
		
		КонтекстУчетнойПолитики = КонтекстУчетнойПолитики();
		ЗаполнитьЗначенияСвойств(КонтекстУчетнойПолитики, ЭтотОбъект);
		КонтекстУчетнойПолитики.Период = Дата;

		ЭтоМалоценныйНМА = УчетНМА.ЭтоМалоценныйНМА(КонтекстУчетнойПолитики) И Не ЭтоПлательщикНДФЛ;

		Настройки = Справочники.НастройкиУчетаЗатрат.ИсполняемыеНастройкиУчетаЗатратУчетнойПолитики(Дата, Организация);
			
		Если Не ЗначениеЗаполнено(Счет) И ЗначениеЗаполнено(Настройки.СчетаУчета) Тогда
			Счет = Настройки.СчетаУчета[0];
			Модифицированность = Истина;
		КонецЕсли;
		
		ИспользоватьСуммыНалогНаПрибыль =
			БухгалтерскийУчет.ИспользоватьСуммыНалогНаПрибыль(Организация, Дата) <> "НеИспользовать";
		
		НастроитьФормуПоСчету();
		
		НастроитьОтражениеВУчете();
	Иначе
		Элементы.ГруппаЦельИспользования.Видимость = Ложь;
		Элементы.ГруппаДекорацияНадписьОтражениеВУчете.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_НематериальныйАктив" И Параметр.НематериальныйАктив = ОбъектПрава Тогда
		ОбработатьИзменениеОбъектаНаСервере();
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СчетПриИзменении(Элемент)
	НастроитьФормуПоСчету();
КонецПроцедуры

&НаКлиенте
Процедура СчетОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура Субконто1ПриИзменении(Элемент)
	ПриИзмененииСубконто(1);
КонецПроцедуры

&НаКлиенте
Процедура Субконто2ПриИзменении(Элемент)
	ПриИзмененииСубконто(2);
КонецПроцедуры

&НаКлиенте
Процедура Субконто3ПриИзменении(Элемент)
	ПриИзмененииСубконто(3);
КонецПроцедуры

&НаКлиенте
Процедура Субконто1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Субконто2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Субконто3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СрокИспользованияПриИзменении(Элемент)
	СрокИспользованияПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗакрытия = Неопределено;
	Если Модифицированность Тогда
		РезультатЗакрытия = РезультатЗакрытияФормы();
	КонецЕсли;
	
	Закрыть(РезультатЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьИзменениеОбъектаНаСервере()
	НастроитьФормуПоОбъектуПрава();
	Если СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета() Тогда
		НастроитьОтражениеВУчете();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОбъектуПрава()
	
	НаименованиеОбъекта = "";
	Если ЗначениеЗаполнено(ОбъектПрава) Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектПрава, "Наименование, ОтражениеБУ, ОтражениеНУ, НачалоДействияПрава,
		|ЭтоИсключительноеПраво, ВключенВРеестрРоссийскогоПО, НомерРеестрРоссийскогоПО, ДатаРеестрРоссийскогоПО");
		НаименованиеОбъекта = Реквизиты.Наименование;
		ОтражениеБУ = Реквизиты.ОтражениеБУ;
		ОтражениеНУ = Реквизиты.ОтражениеНУ;
		ДатаНачалаДействияПрава = Реквизиты.НачалоДействияПрава;
		
		ЭтоЛицензияНаПОИзРеестраРоссийскихПрограмм =
			Не Реквизиты.ЭтоИсключительноеПраво И Реквизиты.ВключенВРеестрРоссийскогоПО И Реквизиты.ДатаРеестрРоссийскогоПО <= Дата;
		РеквизитыРеестраЗаполненыКорректно = 
			Не Реквизиты.ЭтоИсключительноеПраво И Реквизиты.ВключенВРеестрРоссийскогоПО
			И Реквизиты.ДатаРеестрРоссийскогоПО <= Дата
			И ЗначениеЗаполнено(Реквизиты.ДатаРеестрРоссийскогоПО)
			И ОбщегоНазначенияКлиентСервер.ЭтоЧисло(СтрЗаменить(Реквизиты.НомерРеестрРоссийскогоПО, " ", ""));
	КонецЕсли;
	
	Если ПустаяСтрока(НаименованиеОбъекта) Тогда
		Заголовок = НСтр("ru = 'Назначение использования'");
	Иначе
		ШаблонЗаголовка = НСтр("ru = 'Назначение использования: %1'");
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонЗаголовка, НаименованиеОбъекта);
	КонецЕсли;
			
	НастроитьДатыИспользования();
	
КонецПроцедуры

&НаКлиенте
Функция РезультатЗакрытияФормы()
	
	Результат = Новый Структура;
	Результат.Вставить("Счет");
	Результат.Вставить("Подразделение");
	Результат.Вставить("Субконто1");
	Результат.Вставить("Субконто2");
	Результат.Вставить("Субконто3");
	Результат.Вставить("СрокИспользования");
	
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СрокИспользованияПриИзмененииНаСервере()
	НастроитьОтражениеВУчете();
	НастроитьДатыИспользования();
КонецПроцедуры

&НаСервере
Процедура НастроитьДатыИспользования()
	
	Элементы.НадписьДатыИспользования.Видимость = ЗначениеЗаполнено(СрокИспользования);
	ШаблонЗаголовка = НСтр("ru = '(с %1 по %2)'");
	ДатаНачала = Макс(Дата, ДатаНачалаДействияПрава);
	ДатаОкончания = НачалоДня(ДобавитьМесяц(ДатаНачала, СрокИспользования)) - 1;
	Элементы.НадписьДатыИспользования.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонЗаголовка,
		Формат(ДатаНачала, "ДФ=dd.MM.yyyy;"),
		Формат(ДатаОкончания, "ДФ=dd.MM.yyyy;"))

КонецПроцедуры

&НаСервере
Процедура НастроитьОтражениеВУчете()
	
	Предупреждение = Не ЗначениеЗаполнено(СрокИспользования) Или Не ЗначениеЗаполнено(ОбъектПрава);
	ВидРасходовНУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяЗатрат, "ВидРасходовНУ");
	СрокБольше12Месяцев = (СрокИспользования > 12);
	ЭтоСтатьяЗатратНаПриобретениеКомпьютерныхЛицензийИзРеестра =
		ВидРасходовНУ = Перечисления.ВидыРасходовНУ.РасходыНаКомпьютерныеЛицензииСПовышающимКоэффициентом;;
	ОтражениеНУСоответствуетЛицензииИзРеестра =
		(ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеПоМереИспользования);

	ЧастиНадписи = Новый Массив;
	Если Не ЗначениеЗаполнено(ОбъектПрава) Тогда
		ЧастиНадписи.Добавить(НСтр("ru = 'Чтобы увидеть описание отражения в учете, укажите объект права в строке документа.'"));
	ИначеЕсли Не ЗначениеЗаполнено(СрокИспользования) Тогда
		ЧастиНадписи.Добавить(НСтр("ru = 'Укажите срок использования.
			|Если срок использования в документах не установлен, то укажите примерный срок, в течение которого планируете использовать объект.
			|В дальнейшем вы сможете пересмотреть и изменить этот срок.'"));
	ИначеЕсли Не ИспользоватьСуммыНалогНаПрибыль Тогда
		Если ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Расходы
			Или ЭтоПлательщикНДФЛ И ОтражениеНУ <> Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив Тогда
			ЧастиНадписи.Добавить(НСтр("ru = 'Затраты на приобретение права признаются расходом текущего периода единовременно.'"));
		ИначеЕсли ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс И Не ЭтоПлательщикНДФЛ Тогда
			ЧастиНадписи.Добавить(НСтр("ru = 'Затраты на приобретение права признаются в течение срока использования ежемесячно равными частями.
				|В бухгалтерской отчетности до окончания списания эти затраты отражаются в составе дебиторской задолженности.'"));
		ИначеЕсли Не СрокБольше12Месяцев Тогда
			ЧастиНадписи.Добавить(НСтр("ru = 'Срок использования объекта не превышает 12 месяцев.
				|Затраты на приобретение права признаются расходом текущего периода единовременно.'"));
		ИначеЕсли ЭтоМалоценныйНМА Тогда
			ЧастиНадписи.Добавить(НСтр("ru = 'Объект признается нематериальным активом, но его стоимость меньше установленного лимита,'"));
			ЧастиНадписи.Добавить(НСтр("ru = 'информация о таких объектах не существенна для бухгалтерской отчетности.
				|Затраты на его приобретение признаются расходом текущего периода единовременно.'"));
		Иначе
			ЧастиНадписи.Добавить(НСтр("ru = 'Объект признается нематериальным активом.
				|В течение срока использования начисляется амортизация линейным способом.'"));
		КонецЕсли;
	Иначе
		Если ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Аванс Тогда
			Если ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Затраты на приобретение права в бухгалтерском признаются в течение срока использования ежемесячно равными частями,
					|а в налоговом учете - в полной сумме сразу с начала использования.
					|В бухгалтерской отчетности до окончания списания эти затраты отражаются в составе дебиторской задолженности.'"));
			ИначеЕсли ОтражениеНУ <> Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Затраты на приобретение права в бухгалтерском и налоговом учете признаются в течение срока использования ежемесячно равными частями.
					|В бухгалтерской отчетности до окончания списания эти затраты отражаются в составе дебиторской задолженности.'"));
			ИначеЕсли Не СрокБольше12Месяцев Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Срок использования объекта не превышает 12 месяцев, объект не является нематериальным активом.
					|Затраты на приобретение права в бухгалтерском и налоговом учете признаются в течение срока использования ежемесячно равными частями.
					|В бухгалтерской отчетности до окончания списания эти затраты отражаются в составе дебиторской задолженности.'"));
			Иначе
				ЧастиНадписи.Добавить(НСтр("ru = 'Для целей налогового учета объект права относится к нематериальным активам.
					|Затраты на приобретение права в бухгалтерском учете признаются в течение срока использования ежемесячно равными частями,'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'до окончания списания эти затраты отражаются в составе дебиторской задолженности,'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'а в налоговом учете - начисляется амортизация.'"));
			КонецЕсли;	
		ИначеЕсли ОтражениеБУ = Перечисления.ВариантыОтраженияНематериальныхАктивовБУ.Расходы Тогда
			Если ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Затраты на приобретение права в бухгалтерском и налоговом учете признаются расходом текущего периода единовременно.'"));
			ИначеЕсли ОтражениеНУ <> Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Затраты на приобретение права в бухгалтерском учете признаются расходом текущего периода единовременно,'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'а в налоговом учете - учитываются в течение срока использования ежемесячно равными частями.'"));
			ИначеЕсли Не СрокБольше12Месяцев Тогда
				ЧастиНадписи.Добавить(ТекстОтраженияРасходыБУиНУ());
			Иначе
				ЧастиНадписи.Добавить(НСтр("ru = 'Для целей налогового учета объект относится к нематериальным активам.
					|Затраты на приобретение права в бухгалтерском учете признаются расходом текущего периода единовременно,'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'а в налоговом учете - начисляется амортизация  в течение срока использования.'"));
			КонецЕсли;
		ИначеЕсли ОтражениеНУ = Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.ИныеРасходыСписаниеСразу Тогда
			Если СрокБольше12Месяцев И Не ЭтоМалоценныйНМА Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Объект признается нематериальным активом в бухгалтерском учете.
					|В течение указанного срока начисляется амортизация линейным способом.
					|В налоговом учете расходы на приобретение признаются в полной сумме сразу с начала использования.'"));
			ИначеЕсли СрокБольше12Месяцев И ЭтоМалоценныйНМА Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Объект признается нематериальным активом в бухгалтерском учете, но его стоимость меньше установленного лимита.'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'Информация о таких объектах не существенна для бухгалтерской отчетности.
					|В бухгалтерском и налоговом учете затраты на его приобретение признаются расходом текущего периода единовременно.'"));
			Иначе
				ЧастиНадписи.Добавить(НСтр("ru = 'Срок использования объекта не превышает 12 месяцев.
					|В бухгалтерском и налоговом учете затраты на приобретение права признаются расходом текущего периода единовременно.'"));
			КонецЕсли;
		Иначе
			Если ОтражениеНУ <> Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив И СрокБольше12Месяцев И Не ЭтоМалоценныйНМА Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Объект признается нематериальным активом в бухгалтерском учете.
					|В течение указанного срока начисляется амортизация линейным способом.
					|В налоговом учете расходы на приобретение учитываются в течение срока использования ежемесячно равными частями.'"));
			ИначеЕсли ОтражениеНУ <> Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив И СрокБольше12Месяцев И ЭтоМалоценныйНМА Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Объект признается нематериальным активом в бухгалтерском учете, но его стоимость меньше установленного лимита.'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'Информация о таких объектах не существенна для бухгалтерской отчетности.
					|В бухгалтерском учете затраты на его приобретение признаются расходом текущего периода единовременно,'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'а в налоговом учете - в течение срока использования ежемесячно равными частями.'"));
			ИначеЕсли ОтражениеНУ <> Перечисления.ВариантыОтраженияНематериальныхАктивовНУ.НематериальныйАктив И Не СрокБольше12Месяцев Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Срок использования объекта не превышает 12 месяцев.
					|В бухгалтерском учете затраты на приобретение права признаются расходом текущего периода единовременно.
					|В налоговом учете расходы на приобретение учитываются в течение срока использования ежемесячно равными частями.'"));
			ИначеЕсли Не СрокБольше12Месяцев Тогда
				ЧастиНадписи.Добавить(ТекстОтраженияРасходыБУиНУ());
			ИначеЕсли ЭтоМалоценныйНМА Тогда
				ЧастиНадписи.Добавить(НСтр("ru = 'Объект признается нематериальным активом в бухгалтерском и налоговом учете,'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'но в бухгалтерском учете его стоимость меньше установленного лимита,'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'информация о таких объектах не существенна для бухгалтерской отчетности.
					|В бухгалтерском учете затраты на его приобретение признаются расходом текущего периода единовременно,'"));
				ЧастиНадписи.Добавить(НСтр("ru = 'а в налоговом учете - начисляется амортизация  в течение срока использования.'"));
			Иначе
				ЧастиНадписи.Добавить( НСтр("ru = 'Объект признается нематериальным активом в бухгалтерском и налоговом учете.'"));
				ЧастиНадписи.Добавить( НСтр("ru = 'В течение срока использования начисляется амортизация: в бухгалтерском учете - линейным способом,'"));
				ЧастиНадписи.Добавить( НСтр("ru = 'а в налоговом учете - в соответствии с настройкой учетной политики налога на прибыль.'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЧастиНадписиПовышающийКоэффициент = Новый Массив;
	ПредупреждениеПовышающийКоэффициент = Ложь;
	Если ИспользоватьСуммыНалогНаПрибыль И УчетНМА.ПовышающийКоэффициентНалоговогоУчета(Дата) > 1 Тогда
		Если ЭтоСтатьяЗатратНаПриобретениеКомпьютерныхЛицензийИзРеестра
			И ОтражениеНУСоответствуетЛицензииИзРеестра
			И РеквизитыРеестраЗаполненыКорректно Тогда
			ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'Указана статья затрат с видом ""Расходы на приобретение компьютерных лицензий с повышающим коэффициентом"",'"));
			ЧастиНадписиПовышающийКоэффициент.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'поэтому в налоговом учете будет применен повышающий коэффициент %1 в соответствии с подп. 26 п. 1 ст. 264 НК РФ.'"),
				УчетНМА.ПовышающийКоэффициентНалоговогоУчета(Дата)));
		ИначеЕсли ЭтоСтатьяЗатратНаПриобретениеКомпьютерныхЛицензийИзРеестра
			И Не РеквизитыРеестраЗаполненыКорректно Тогда
				ПредупреждениеПовышающийКоэффициент = Истина;
				ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'Указана статья затрат с видом ""Расходы на приобретение компьютерных лицензий с повышающим коэффициентом"",'"));
				ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'однако реквизиты объекта заполнены некорректно: для применения повышающего коэффициента'"));
				ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'объект должен являться неисключительным правом с указанием номера'"));
				ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'и даты включения в реестр, а также быть включенным в реестр на дату приобретения права.
																|Повышающий коэффициент в соответствии с подп. 26 п. 1 ст. 264 НК РФ применен не будет.'"));
		ИначеЕсли ЭтоСтатьяЗатратНаПриобретениеКомпьютерныхЛицензийИзРеестра
			И Не ОтражениеНУСоответствуетЛицензииИзРеестра Тогда
				ПредупреждениеПовышающийКоэффициент = Истина;
				ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'Указана статья затрат с видом ""Расходы на приобретение компьютерных лицензий с повышающим коэффициентом"",'"));
				ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'однако в карточке объекта порядок отражения для целей налогового учета не соответствует порядку списания прочих расходов.
																|Повышающий коэффициент в соответствии с подп. 26 п. 1 ст. 264 НК РФ применен не будет.'"));
		ИначеЕсли Не ЭтоСтатьяЗатратНаПриобретениеКомпьютерныхЛицензийИзРеестра
			И ЭтоЛицензияНаПОИзРеестраРоссийскихПрограмм Тогда
				ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'Чтобы в налоговом учете применить повышающий коэффициент в соответствии с подп. 26 п. 1 ст. 264 НК РФ,'"));
				ЧастиНадписиПовышающийКоэффициент.Добавить(НСтр("ru = 'укажите статью затрат с видом ""Расходы на приобретение компьютерных лицензий с повышающим коэффициентом"".'"));
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ДекорацияПредупреждениеОтражения.Видимость = Предупреждение;
	Элементы.НадписьОтражениеВУчете.Заголовок = СтрСоединить(ЧастиНадписи, " ");
	Элементы.ДекорацияПредупреждениеПовышающийКоэффициент.Видимость = ПредупреждениеПовышающийКоэффициент;
	Элементы.НадписьПовышающийКоэффициент.Заголовок = СтрСоединить(ЧастиНадписиПовышающийКоэффициент, " ");
	
КонецПроцедуры

&НаСервере
Функция ТекстОтраженияРасходыБУиНУ()
	
	ЧастиНадписи = Новый Массив;
	ЧастиНадписи.Добавить(НСтр("ru = 'Срок использования объекта не превышает 12 месяцев, объект не является нематериальным активом.
		|Затраты на приобретение права в бухгалтерском учете признаются расходом текущего периода единовременно,'"));
	ЧастиНадписи.Добавить(НСтр("ru = 'а в налоговом учете - учитываются в течение срока использования ежемесячно равными частями.'"));

	Возврат СтрСоединить(ЧастиНадписи, " ");
КонецФункции

&НаСервере
Процедура НастроитьФормуПоСчету()
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСчета(
		ЭтотОбъект,
		ЭтотОбъект,
		ПараметрыУстановкиСвойствСубконто());
	НастроитьОтражениеВУчете();
КонецПроцедуры

&НаСервере
Функция ПараметрыУстановкиСвойствСубконто()
	
	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"Субконто",
		"Подразделение",
		"Субконто",
		"Подразделение",
		"Счет");
	
	Результат.ЗначенияПоУмолчанию.Вставить(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат,
		Справочники.СтатьиЗатрат.ОсновнаяСтатьяЗатрат());
	
	СтатьяЗатрат = Справочники.СтатьиЗатрат.ПустаяСсылка();
	Если ЗначениеЗаполнено(Счет) Тогда
		СвойстваСчета = БухгалтерскийУчетКлиентСервер.СвойстваИАналитикаСчета(
			Счет,
			Организация,
			Дата);
		Результат.ДанныеСчета = СвойстваСчета.ДанныеСчета;
		Результат.ОтображаемаяАналитика = СвойстваСчета.ОтображаемаяАналитика;
		НомерСубконтоСтатьяЗатрат =
			Результат.ДанныеСчета.ИдентификаторыСубконто[ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат];
		Если ЗначениеЗаполнено(НомерСубконтоСтатьяЗатрат) Тогда
			СтатьяЗатрат = ЭтотОбъект["Субконто" + НомерСубконтоСтатьяЗатрат];
		КонецЕсли;
	КонецЕсли;
			
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииСубконто(НомерСубконто)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоШапкиПриИзмененииСубконто(
		ЭтотОбъект,
		ЭтотОбъект,
		НомерСубконто,
		ПараметрыУстановкиСвойствСубконто());
	НастроитьОтражениеВУчете();
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		ЭтотОбъект,
		ПараметрыУстановкиСвойствСубконто());
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

&НаСервере
Функция КонтекстУчетнойПолитики()
	
	Перем КонтекстУчетнойПолитики;
	
	КонтекстУчетнойПолитики = Новый Структура;
	КонтекстУчетнойПолитики.Вставить("Организация");
	КонтекстУчетнойПолитики.Вставить("Период");
	КонтекстУчетнойПолитики.Вставить("ВалютаДокумента");
	КонтекстУчетнойПолитики.Вставить("КурсВзаиморасчетов");
	КонтекстУчетнойПолитики.Вставить("КратностьВзаиморасчетов");
	КонтекстУчетнойПолитики.Вставить("Стоимость");
	Возврат КонтекстУчетнойПолитики;

КонецФункции

#КонецОбласти
