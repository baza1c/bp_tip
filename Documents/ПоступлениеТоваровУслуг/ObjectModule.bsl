#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

// Признак для передачи в счет-фактуру для того, чтобы не устанавливать статус повторно.
Перем УстановленСтатусДокумента;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	ЕстьРасхождения = Ложь;
	
	НомерВходящегоДокумента = "";
	ДатаВходящегоДокумента  = '00010101';

	ЗачетАвансов.Очистить();
	
	СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
	ВалютаДокумента, Дата);
	
	КурсВзаиморасчетов      = СтруктураКурсаВзаиморасчетов.Курс;
	КратностьВзаиморасчетов = СтруктураКурсаВзаиморасчетов.Кратность;
	
	СчетНаОплатуПоставщика = Документы.СчетНаОплатуПоставщика.ПустаяСсылка();
	УдалитьТТНВходящаяЕГАИС = Документы.ТТНВходящаяЕГАИС.ПустаяСсылка();
	
	Если ОбъектКопирования.ЕстьМаркируемаяПродукцияГИСМ Тогда
		Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "КиЗ_ГИСМ");
	КонецЕсли;
	
	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Дата);
		
	СведенияПрослеживаемости.Очистить();
	ШтрихкодыУпаковок.Очистить();
	
	СсылкаНаЧек = "";
	НомерЧека = "";
	ЧекСамозанятого = Справочники.ПоступлениеТоваровУслугПрисоединенныеФайлы.ПустаяСсылка();
	
	РаботаСНоменклатуройБП.ОбновитьСодержаниеУслуг(Услуги, Дата);
	РаботаСНоменклатуройБП.ОбновитьСодержаниеУслуг(АгентскиеУслуги, Дата);
	Если Не ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда 
		РаботаСНоменклатуройБП.ОбновитьПризнакПрослеживаемости(Товары, ВедетсяУчетПрослеживаемыхТоваров, Дата);
	КонецЕсли;
	РаботаСНоменклатуройБП.ОбновитьПризнакПрослеживаемости(Оборудование, ВедетсяУчетПрослеживаемыхТоваров, Дата);
	РаботаСНоменклатуройБП.ОбновитьПризнакПрослеживаемости(ОсновныеСредства, ВедетсяУчетПрослеживаемыхТоваров, Дата, Истина);
		
	ЗаполнениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено Тогда
		
		Если ТипДанныхЗаполнения <> Тип("Структура")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
			ДокументОснование = ДанныеЗаполнения;
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
			И ДанныеЗаполнения.Свойство("Основание")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Основание.Метаданные()) Тогда
			ДокументОснование = ДанныеЗаполнения.Основание;
		КонецЕсли;
		
		Если ТипДанныхЗаполнения = Тип("ДокументСсылка.НакладнаяСАТУРН") 
			Или ТипДанныхЗаполнения = Тип("ДокументСсылка.ИмпортПродукцииСАТУРН") Тогда
			ДокументОснование = ДанныеЗаполнения;
		КонецЕсли;
		
		Если ДокументОснование <> Неопределено Тогда
			ЗаполнитьПоДокументуОснованию(ДокументОснование);
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
			И ДанныеЗаполнения.Свойство("СуммаВключаетНДС") Тогда
			СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
		Иначе
			СуммаВключаетНДС =
				БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
					"ОсновнойВариантРасчетаВходящегоНДС") = Перечисления.ВариантыРасчетаНДС.НДСВСумме;
		КонецЕсли;
	Иначе
		СуммаВключаетНДС =
				БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(
					"ОсновнойВариантРасчетаВходящегоНДС") = Перечисления.ВариантыРасчетаНДС.НДСВСумме;
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	
	ЭтоКомиссия = ЗначениеЗаполнено(ДоговорКонтрагента)
		И ВидДоговораКонтрагента = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	ВозможнаКомиссияПоЗакупке = ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия 
		И НЕ ЭтоКомиссия
		И ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов");
	ЭтоВвозИзЕАЭС = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(Контрагент);
	НеИспользуемыеТабличныеЧасти = Документы.ПоступлениеТоваровУслуг.НеИспользуемыеТабличныеЧасти(
		ВидОперации,
		ЭтоКомиссия,
		ВозможнаКомиссияПоЗакупке);
		
	ОбщегоНазначенияБП.ОчиститьНеиспользуемыеТабличныеЧасти(ЭтотОбъект, НеИспользуемыеТабличныеЧасти);
	
	СпособОценкиТоваровВРознице = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата);

	ДанныеОТипеСклада = Неопределено;
	
	//Поступление на несколько складов
    Если Не ПолучитьФункциональнуюОпцию("ПоступлениеНаНесколькоСкладов") И СкладВСписке Тогда
		СкладВСписке = Ложь;
	КонецЕсли;
	ПредставлениеСклада = "";
	Если СкладВСписке Тогда
		Для Каждого Строка Из Товары Цикл
			Если УчетТоваров.РозницаВПродажныхЦенах(СпособОценкиТоваровВРознице, Строка.Склад, ЭтоКомиссия, ДанныеОТипеСклада, ВидОперации) Тогда
				Если ДанныеОТипеСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
					Строка.СчетУчета = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ;
				Иначе
					Строка.СчетУчета = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ;
				КонецЕсли;
	            Строка.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
			КонецЕсли;
		КонецЦикла;
		
		Склады = ОбщегоНазначения.ВыгрузитьКолонку(Товары, "Склад", Истина);
		Если Склады.Количество() > 0 Тогда
			ПредставлениеСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склады[0], "Наименование"); 
			КоличествоСкладов 	= Склады.Количество() - 1;
			ПредставлениеСклада = ?(КоличествоСкладов > 0, ПредставлениеСклада + " и еще " + КоличествоСкладов, ПредставлениеСклада);
		КонецЕсли;
	Иначе
		Если УчетТоваров.РозницаВПродажныхЦенах(СпособОценкиТоваровВРознице, Склад, ЭтоКомиссия, ДанныеОТипеСклада, ВидОперации) Тогда
			Для Каждого Строка Из Товары Цикл
				Если ДанныеОТипеСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
					Строка.СчетУчета    = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ;
					Строка.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
				Иначе
					Строка.СчетУчета = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ;
					Строка.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	//Конец Поступление на несколько складов
	
	Если ЭтоКомиссия И ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов") Тогда
		Для Каждого Строка Из Товары Цикл
			Строка.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
			Строка.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
			Строка.СчетРасчетов = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		КонецЦикла;
	КонецЕсли;

	Если УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата) Тогда
		НДСВключенВСтоимость = Ложь;
	КонецЕсли;
	
	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(ЭтотОбъект, РежимЗаписи);
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);

	ДоговорУказан = ЗначениеЗаполнено(ДоговорКонтрагента);
	Если ДоговорУказан Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДоговорКонтрагента, "ВидДоговора, УчетАгентскогоНДС, ВидАгентскогоДоговора, НалогообложениеКонтрагента");
		ЭтоКомиссия       = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
		УчетАгентскогоНДС = РеквизитыДоговора.УчетАгентскогоНДС;
		НДСИсчисляетсяНалоговымАгентом = 
			РеквизитыДоговора.ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.РеализацияТоваров
			И БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
		ЭтоДоговорССамозанятым =
			РеквизитыДоговора.НалогообложениеКонтрагента = Перечисления.СистемыНалогообложения.НалогНаПрофессиональныйДоход;
	Иначе
		ЭтоКомиссия       = Ложь;
		УчетАгентскогоНДС = Ложь;
		НДСИсчисляетсяНалоговымАгентом = Ложь;
		ЭтоДоговорССамозанятым = Ложь;
	КонецЕсли;
		
	ТребуетсяСчетФактура = Не ЭтоДоговорССамозанятым И Не УчетАгентскогоНДС И Не ЭтоКомиссия
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку
		Или НДСИсчисляетсяНалоговымАгентом;
	
	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ЭтотОбъект, "СчетФактураПолученный");
	Если Не ТребуетсяСчетФактура Тогда 	
		ПараметрыДействия.СостояниеФлага = Истина;
	КонецЕсли;
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);
	
	Документы.КорректировкаПоступления.ОбновитьРеквизитыСвязанныхДокументовКорректировки(ЭтотОбъект, Отказ);
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И ВедетсяУчетПредметовАренды
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		Для Каждого Строка Из Услуги Цикл
			Строка.Количество = 0;
			Строка.Цена = Строка.Сумма;
			Строка.ПодразделениеЗатрат = ПодразделениеОрганизации;
			Строка.Субконто1 = Контрагент;
			Строка.Субконто2 = ДоговорКонтрагента;
			Строка.СчетЗатратНУ = Строка.СчетЗатрат;
			Строка.СубконтоНУ1 = Контрагент;
			Строка.СубконтоНУ2 = ДоговорКонтрагента;
		КонецЦикла;
	КонецЕсли;
	
	ИнтеграцияГИСМБП.УстановитьПризнакЕстьМаркируемаяПродукцияГИСМ(ЭтотОбъект);
	
	УчетОС.УстановитьАмортизационнуюГруппаОС(
		ОсновныеСредства.Выгрузить( ,"ОсновноеСредство, СрокПолезногоИспользования"));
	
	Если (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков)
		И БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям() Тогда
		ПодразделениеОрганизации = МестонахождениеОС;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги 
		И Справочники.Склады.ИспользуетсяНесколькоСкладов() Тогда
		
		Склад = Справочники.Склады.ПустаяСсылка();
	КонецЕсли; 

	ПрослеживаемыйТовар = Товары.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ПрослеживаемоеОборудование = Оборудование.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ПрослеживаемоеОС = ОсновныеСредства.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ЕстьПрослеживаемыйТовар = ПрослеживаемыйТовар.Количество() <> 0
		ИЛИ ПрослеживаемоеОборудование.Количество() <> 0
		Или ПрослеживаемоеОС.Количество() <> 0;
	Если НЕ ЕстьПрослеживаемыйТовар 
		ИЛИ ЭтоВвозИзЕАЭС Тогда
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	// Дополнительно очистим номера ТД для прослеживаемого товара.
	Если НЕ ЭтоВвозИзЕАЭС Тогда
		Для Каждого СтрокаСТоваром ИЗ ПрослеживаемыйТовар Цикл
			СтрокаСТоваром.НомерГТД = Неопределено;
		КонецЦикла;
		Для Каждого СтрокаСОборудованием ИЗ ПрослеживаемоеОборудование Цикл
			СтрокаСОборудованием.НомерГТД = Неопределено;
		КонецЦикла;
	КонецЕсли;
	
	ИменаТабличныхЧастей = Новый Массив;
	ИменаТабличныхЧастей.Добавить("Товары");
	ИменаТабличныхЧастей.Добавить("Услуги");
	ИменаТабличныхЧастей.Добавить("АгентскиеУслуги");
	ИменаТабличныхЧастей.Добавить("Оборудование");
	ИменаТабличныхЧастей.Добавить("ОсновныеСредства");
	ОбщегоНазначенияБПКлиентСервер.ЗаполнитьИдентификаторыСтрок(ЭтотОбъект, ИменаТабличныхЧастей);
	
	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Дата);

	Если ВедетсяУчетПрослеживаемыхТоваров Тогда
		ИменаТабличныхЧастей = Новый Массив;
		ИменаТабличныхЧастей.Добавить("Товары");
		ИменаТабличныхЧастей.Добавить("Оборудование");
		ОбщегоНазначенияБП.ОчиститьЛишниеДанныеПоПрослеживаемости(ЭтотОбъект, ИменаТабличныхЧастей);
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства Тогда
		
		МассивОС = ОсновныеСредства.ВыгрузитьКолонку("ОсновноеСредство");
		
		Если ЗначениеЗаполнено(МассивОС) Тогда
			ЕдиницыУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивОС, "ЕдиницаУчета");
			Для Каждого СтрокаТаблицы Из ОсновныеСредства Цикл
				Если Не СтрокаТаблицы.ПрименяетсяИнвестиционныйВычет Тогда
					Продолжить;
				КонецЕсли;
				ЕдиницаУчета = ЕдиницыУчета[СтрокаТаблицы.ОсновноеСредство];
				Если ЕдиницаУчета <> Перечисления.ЕдиницыУчетаОС.ИнвентарныйОбъект Тогда
					СтрокаТаблицы.ПрименяетсяИнвестиционныйВычет = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Перем ДанныеТабличныхЧастейРасхождения;
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСтатусДокумента();
	
	УстановитьСтатусПроверкиРНПТ();
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		УчетНДСПереопределяемый.СинхронизироватьРеквизитыСчетаФактурыПолученного(ЭтотОбъект, НЕ УстановленСтатусДокумента);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ДанныеТабличныхЧастейРасхождения", ДанныеТабличныхЧастейРасхождения) Тогда
		ДанныеЗаполненияАктОРасхождениях = Новый Структура("Товары, Услуги, АгентскиеУслуги");
		
		ЗаполнитьЗначенияСвойств(ДанныеЗаполненияАктОРасхождениях, ДанныеТабличныхЧастейРасхождения);
		
		ДанныеЗаполненияАктОРасхождениях.Вставить("Ссылка", Ссылка);
		
		СоздатьОбновитьАктОРасхождениях(ДанныеЗаполненияАктОРасхождениях);
	ИначеЕсли ЕстьРасхождения Тогда
		СинхронизироватьРеквизитыАктаОРасхождениях(Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ЭлектроннаяТранспортнаяНакладная") Тогда
		ОбменСГИСЭПДБП.ПроверитьДобавитьОснованиеЭТрН(ДополнительныеСвойства.ЭлектроннаяТранспортнаяНакладная, Ссылка);
	КонецЕсли; 
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	//Поступление на несколько складов
	Если ПолучитьФункциональнуюОпцию("ПоступлениеНаНесколькоСкладов") И СкладВСписке Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Склад"); 
    Иначе
		Если ПроверяемыеРеквизиты.Найти("Склад") = Неопределено Тогда
			ПроверяемыеРеквизиты.Добавить("Склад");
		КонецЕсли; 
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Склад");
		МассивНепроверяемыхРеквизитов.Добавить("ВозвратнаяТара.Склад");
	КонецЕсли;
	
	// при наличии акта о расхождениях количество товара может отстуствовать
	Если ЕстьРасхождения Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
	КонецЕсли;
	
	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	ДанныеОТипеСклада      = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ТипСклада");
	ВестиУчетПоДоговорам   = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ЭтоКомиссия = ВестиУчетПоДоговорам И ЗначениеЗаполнено(ДоговорКонтрагента)
		И ВидДоговораКонтрагента = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	ВозможнаКомиссияПоЗакупке = ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия 
		И НЕ ЭтоКомиссия
		И ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов");

	ЭтоВвозИзЕАЭС = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(Контрагент);
	Если НЕ ЭтоВвозИзЕАЭС 
		И ЗначениеЗаполнено(Контрагент) Тогда
		СтранаРегистрацииКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "СтранаРегистрации");
		ЭтоВвозИзПрочихСтран = СтранаРегистрацииКонтрагента <> Справочники.СтраныМира.Россия;
	Иначе
		ЭтоВвозИзПрочихСтран = Ложь;
	КонецЕсли;
	
	ПрослеживаемыйТовар = Товары.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ПрослеживаемоеОборудование = Оборудование.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ЕстьПрослеживаемыйТовар = ПрослеживаемыйТовар.Количество() <> 0
		ИЛИ ПрослеживаемоеОборудование.Количество() <> 0;
	КонтролироватьРеквизитыВходящегоДокумента = ЭтоВвозИзЕАЭС И ЕстьПрослеживаемыйТовар;
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата);
	
	ПроверятьСчетНДС = НЕ НДСВключенВСтоимость ИЛИ РаздельныйУчетНДСНаСчете19;
	
	МассивНепроверяемыхРеквизитов.Добавить("Оборудование.СтранаПроисхождения");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.СтранаПроисхождения");

	Если НЕ ВестиУчетПоДоговорам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	Если НЕ ЭтоУниверсальныйДокумент 
	   И НЕ КонтролироватьРеквизитыВходящегоДокумента Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НомерВходящегоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаВходящегоДокумента");
	КонецЕсли;
	
	// Не проверяем заполненность табличных частей (включая реквизиты), 
	// которые не используются при определенных видах операции и будут очищены в ПередЗаписью
	НеИспользуемыеТабличныеЧасти = Документы.ПоступлениеТоваровУслуг.НеИспользуемыеТабличныеЧасти(
		ВидОперации, ЭтоКомиссия, ВозможнаКомиссияПоЗакупке);
	
	ОбщегоНазначенияБП.ИсключитьИзПроверкиНеиспользуемыеТабличныеЧасти(
		ПроверяемыеРеквизиты,
		НеИспользуемыеТабличныеЧасти);
		
	// Документ без данных о поступивших ценностях, соответствующих виду операции,
	// считаем заполненным некорректно.
	
	// Оставим в проверяемых реквизитах только основные табличные части  
	// выбранного вида операции
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия 
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары");
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Услуги");
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства");
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Права");
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия Тогда
		// Если заполнен любой из "основных" списков, то проверять оставшиеся не следует
		ОсновныеСписки = Новый Массив();
		ОсновныеСписки.Добавить("Товары");
		ОсновныеСписки.Добавить("Услуги");
		ОсновныеСписки.Добавить("АгентскиеУслуги");
		ОсновныеСписки.Добавить("ВозвратнаяТара");
		
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ОсновныеСписки, НеИспользуемыеТабличныеЧасти);
		
		ОбщегоНазначенияБП.ИсключитьИзПроверкиОсновныеТабличныеЧасти(
			ЭтотОбъект, 
			ОсновныеСписки, 
			ПроверяемыеРеквизиты);
		
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Топливо Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Топливо");
	КонецЕсли;
	
	// Установка значений переменных для дальнейшей проверки
	
	Счет4112 = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ;
	РазделятьПоСтавкамНДС = Счет4112.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС,
		"ВидСубконто") <> Неопределено;
	
	УчетВПродажныхЦенах = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата)
		= Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;
	ОптовыйСклад = ДанныеОТипеСклада = Перечисления.ТипыСкладов.ОптовыйСклад;
	СкладНТТ = ДанныеОТипеСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка;
	
	РозницаВПродажныхЦенах = УчетВПродажныхЦенах
		И (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары)
		И НЕ ОптовыйСклад
		И НЕ ЭтоКомиссия;
	
	НТТ = РозницаВПродажныхЦенах И СкладНТТ;
	
	// Проверка реквизитов шапки документа
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права Тогда
		// Объекты строительства не приходуются на склад
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
	КонецЕсли;
	
	Если Товары.Количество() = 0 И ВозвратнаяТара.Количество() = 0 И Оборудование.Количество() = 0 Тогда
		// Нет материальных ценностей - нечего приходовать на склад
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
	КонецЕсли;
	
	Если ВестиУчетПоДоговорам И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ТекстСообщения = "";
		Если НЕ УчетВзаиморасчетов.ПроверитьВозможностьПроведенияВРеглУчете(
			ЭтотОбъект, ДоговорКонтрагента, ТекстСообщения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность",
				НСтр("ru = 'Договор'"),,, ТекстСообщения);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
				"ДоговорКонтрагента", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// Табличная часть "Основные средства".
	Если НЕ ПроверятьСчетНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СтавкаНДС");
	КонецЕсли;
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.Количество");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СтавкаНДС");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СчетУчетаНДС");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СпособУчетаНДС");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СрокПолезногоИспользования");
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права
		Или Не УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Организация, Дата) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Права.ПорядокВключенияСтоимостиВСоставРасходовУСН");
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.Количество");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.ИнвентарныйНомер");
		МассивОС = Новый Массив;
		
		Для Каждого СтрокаТаблицы Из ОсновныеСредства Цикл
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ОсновноеСредство)
				Или МассивОС.Найти(СтрокаТаблицы.ОсновноеСредство) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			МассивОС.Добавить(СтрокаТаблицы.ОсновноеСредство);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(МассивОС) Тогда
			Префикс = "ОсновныеСредства";
			ИмяСписка = НСтр("ru = 'Основные средства'");
			ЕдиницыУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивОС, "ЕдиницаУчета");
			Для каждого СтрокаТаблицы Из ОсновныеСредства Цикл
				Если Не ЗначениеЗаполнено(СтрокаТаблицы.ОсновноеСредство) Тогда
					Продолжить;
				КонецЕсли;
				ЕдиницаУчета = ЕдиницыУчета[СтрокаТаблицы.ОсновноеСредство];
				Если ЕдиницаУчета = Перечисления.ЕдиницыУчетаОС.ГрупповойОбъект Тогда
					Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Количество) Тогда
						ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Количество'"),
							СтрокаТаблицы.НомерСтроки, ИмяСписка);
						Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Префикс, СтрокаТаблицы.НомерСтроки, "Количество");
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
					КонецЕсли;
				Иначе
					Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ИнвентарныйНомер) Тогда
						ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Инвентарный номер'"),
							СтрокаТаблицы.НомерСтроки, ИмяСписка);
						Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Префикс, СтрокаТаблицы.НомерСтроки, "ИнвентарныйНомер");
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		
	КонецЕсли;
	
	// Табличная часть "Оборудование".
	Если Не ПроверятьСчетНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Оборудование.СтавкаНДС");
	КонецЕсли;
	
	// Табличная часть "Объекты строительства".
	
	Если НЕ ПроверятьСчетНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОбъектыСтроительства.СтавкаНДС");
	КонецЕсли;
	
	// Табличная часть "Товары".
	Если НЕ ПроверятьСчетНДС
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтавкаНДС");
	КонецЕсли;
	
	Если НТТ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СуммаВРознице");
	КонецЕсли;
	
	Если Не НТТ ИЛИ Не РазделятьПоСтавкамНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтавкаНДСВРознице");
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ДоговорКонтрагента");
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		
		ИностранныйКонтрагент = ЭтоВвозИзЕАЭС ИЛИ ЭтоВвозИзПрочихСтран;
		ПроверитьПоляПрослеживаемости(ВозможнаКомиссияПоЗакупке, ИностранныйКонтрагент, "Товары", ЭтоКомиссия, Отказ);
		ПроверитьПоляПрослеживаемости(ВозможнаКомиссияПоЗакупке, ИностранныйКонтрагент, "Оборудование", ЭтоКомиссия, Отказ);
		ПроверитьПоляПрослеживаемости(ВозможнаКомиссияПоЗакупке, Ложь, "ОсновныеСредства", ЭтоКомиссия, Отказ);
		
		Для каждого СтрокаТаблицы Из Товары Цикл
			Префикс = "Товары[%1].";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
			
			ИмяСписка = НСтр("ru = 'Товары'");
			
			// Проверка договора и счета расчетов с комитентом
			Если ВозможнаКомиссияПоЗакупке И ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) Тогда
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Договор с комитентом'"),
						СтрокаТаблицы.НомерСтроки, ИмяСписка);
					Поле = Префикс + "ДоговорКонтрагента";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	// Табличная часть "Услуги"
	ПроверятьСтавкуНДС = (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства)
		И НЕ ЭтоКомиссия
		И (НЕ НДСВключенВСтоимость ИЛИ РаздельныйУчетНДСНаСчете19)
		И НЕ РозницаВПродажныхЦенах;
	
	Если НЕ ПроверятьСтавкуНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Услуги.СтавкаНДС");
	КонецЕсли;
	
	// Табличная часть "Зачет авансов"
	Если СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	ИначеЕсли ЗачетАвансов.Количество() = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	
		ТекстСообщения = НСтр("ru = 'Не введено ни одной строки с документом аванса!'");
		Поле = "ПорядокУчетаРасчетов";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , Поле, Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОсновныеСредства", Новый Структура("ОсновноеСредство"), Отказ);
	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "Права", Новый Структура("ОбъектПрава"), Отказ);
	
	РеквизитыЗаСсылками = Документы.ПоступлениеТоваровУслуг.РеквизитыЗаСсылками(ВидОперации);
	
	СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);
	
	ПроверкаЗаполненияДокументов.ПроверитьРеквизитыЗаСсылками(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроверитьКорректностьЗаполненияТабличнойЧастиТовары(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.ПоступлениеТоваровУслуг.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УчетНМА.ПроверитьВозможностьИзмененияСостоянияНМА(
		ПараметрыПроведения.ПроверкаСостоянияНМАРеквизиты, ПараметрыПроведения.ПроверкаСостоянияНМАТаблица, Отказ);
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ (АНАЛИЗ ОСТАТКОВ И Т.П.)

	// Таблица взаиморасчетов
	ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером Тогда
		ТаблицаВзаиморасчеты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансовРасчетыСКомиссионером(ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
			ПараметрыПроведения.ТаблицаЗачетАвансов, ПараметрыПроведения.ЗачетАвансовРеквизиты, Отказ);
	Иначе
		ТаблицаВзаиморасчеты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
			ПараметрыПроведения.ТаблицаЗачетАвансов, ПараметрыПроведения.ЗачетАвансовРеквизиты, Отказ);
	КонецЕсли; 

	// Таблицы документа с корректировкой сумм по курсу авансов
	СтруктураТаблицДокумента = УчетДоходовРасходов.ПодготовитьТаблицыПоступленияПоКурсуАвансов(ПараметрыПроведения.СтруктураТаблицДокумента,
		ТаблицаВзаиморасчеты, ПараметрыПроведения.ЗачетАвансовРеквизиты);
	
	// Инвестиционный вычет
	ТаблицаИнвестиционныйВычет = ИнвестиционныйНалоговыйВычет.ПодготовитьТаблицуИнвестиционныйВычетПоступлениеТоваровУслуг(
		СтруктураТаблицДокумента.ТаблицаОсновныеСредства, ПараметрыПроведения.Реквизиты);
		
	// Таблица приобретения прав
	Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицуПриобретениеПрав(
		СтруктураТаблицДокумента.ТаблицаПрава,
		ПараметрыПроведения.Реквизиты);
			
	ТаблицыНДС = Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицыНДС(СтруктураТаблицДокумента);
	
	// Взаиморасчеты с комиссионером по продаже
	ТаблицаВзаиморасчетыКомиссияПоПродаже = Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицуВзаиморасчетыКомиссия(ТаблицаВзаиморасчеты, 
		ПараметрыПроведения.ЗачетВыручкиКомиссионераРеквизиты, Отказ);
	
	ТаблицаНДСКомиссияПоПродаже = УчетУСН.ПодготовитьТаблицуНДСПоРеализацииНеплательщиком(
		ТаблицаВзаиморасчетыКомиссияПоПродаже,
		ПараметрыПроведения.ЗачетВыручкиКомиссионераРеквизиты);
	
	// Товары и услуги, закупленные для комитентов
	ТаблицаТоварыУслугиКомиссияПоЗакупке = Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицуТоварыУслугиКомиссияПоЗакупке(
		СтруктураТаблицДокумента.ТаблицаТоварыУслугиКомиссияПоЗакупке, 
		ПараметрыПроведения.ЗачетАвансовКомиссияПоЗакупкеРеквизиты,
		ПараметрыПроведения.Реквизиты);

	// Взаиморасчеты с комитентами по договорам комиссии на закупку
	ТаблицаЗачетАвансовКомиссияПоЗакупке = Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицуЗачетАвансовКомиссияПоЗакупке(
		ТаблицаТоварыУслугиКомиссияПоЗакупке);
	ТаблицаВзаиморасчетыКомиссияПоЗакупке = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		ТаблицаЗачетАвансовКомиссияПоЗакупке, Неопределено, ПараметрыПроведения.ЗачетАвансовКомиссияПоЗакупкеРеквизиты, Отказ);
	
	// Структура таблиц для отражения в налоговом учете УСН
	СтруктураТаблицУСН = Новый Структура;
	СтруктураТаблицУСН.Вставить("ТаблицаРасчетов", ТаблицаВзаиморасчеты);
	СтруктураТаблицУСН.Вставить("ДолгПокупателя" , ТаблицаВзаиморасчетыКомиссияПоПродаже);
	СтруктураТаблицУСН.Вставить("ТаблицаНДСПродажи", ТаблицаНДСКомиссияПоПродаже);
	
	// Учет УСН-онлайн
	ТаблицаРазметкиАУСН = РазметкаОперацийАУСН.ПодготовитьТаблицуЗачетАвансаПоставщику(
		ПараметрыПроведения.Реквизиты,
		ПараметрыПроведения.РасходыАУСН,
		ТаблицаВзаиморасчеты);

	// Учет доходов и расходов ИП
	ТаблицаТоваровИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаТоваров,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаУслугИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаУслуг,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаОборудованияИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаОборудования,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаОсновныеСредстваИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаОсновныеСредства,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаПоступленияОСИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияВнеоборотныхАктивов(
		ТаблицаОсновныеСредстваИП,
		ТаблицаВзаиморасчеты,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаОплатыОСИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОплатыОСиНМА(
		Новый Структура("МПЗ", ТаблицаПоступленияОСИП), 
		ПараметрыПроведения.Реквизиты);
					
	ТаблицаНМАИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаНМА,
		ПараметрыПроведения.ИПРеквизиты);
	
	ТаблицаПоступленияНМАИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияВнеоборотныхАктивов(
		ТаблицаНМАИП,
		ТаблицаВзаиморасчеты,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаОплатыНМАИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОплатыОСиНМА(
		Новый Структура("МПЗ", ТаблицаПоступленияНМАИП), 
		ПараметрыПроведения.Реквизиты);
		
	ТаблицаПрочееИП  = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаПрочее,
		ПараметрыПроведения.ИПРеквизиты);
	
	СтруктураТаблицМПЗ = Новый Структура;
	СтруктураТаблицМПЗ.Вставить("ТаблицаТоваров",      ТаблицаТоваровИП);
	СтруктураТаблицМПЗ.Вставить("ТаблицаУслуг",        ТаблицаУслугИП);
	СтруктураТаблицМПЗ.Вставить("ТаблицаОборудования", ТаблицаОборудованияИП);
	СтруктураТаблицМПЗ.Вставить("ТаблицаПрочее",       ТаблицаПрочееИП);
	
	ТаблицаПрочихРасчетов = УчетВзаиморасчетов.ПустаяТаблицаПоПрочимРасчетам();
	
	ТаблицыПоступленияОтФакторинговойКомпанииИП =
		УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыПоступленияОтФакторинговойКомпании(
			ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
			ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
	
	Если ЗначениеЗаполнено(ТаблицыПоступленияОтФакторинговойКомпанииИП.ТаблицаПрочиеРасчеты) Тогда
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(
			ТаблицыПоступленияОтФакторинговойКомпанииИП.ТаблицаПрочиеРасчеты, ТаблицаПрочихРасчетов);
	КонецЕсли;
		
	Если НЕ ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		ТаблицаСтатусовСчетов = СтатусыДокументов.ПодготовитьТаблицуСтатусовПоступленияПоСчетам(
			ПараметрыПроведения.ПоступлениеТоваровУслугПоСчету,
			ПараметрыПроведения.ПоступлениеВозвратнойТарыПоСчету,
			ПараметрыПроведения.Реквизиты);
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	// Алгоритмы формирования проводок этого документа учитывают малоценку
	Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("ДвиженияПоМалоценнымОбъектамСформированы", Истина);

	// Поступление товаров
	УчетТоваров.СформироватьДвиженияПоступлениеТоваров(СтруктураТаблицДокумента.ТаблицаТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетТоваров.СформироватьДвиженияНачислениеТорговойНаценкиПоступлениеТоваров(СтруктураТаблицДокумента.ТаблицаТовары,
		ПараметрыПроведения.ПоступлениеТоваровТорговаяНаценкаРеквизиты, Движения, Отказ);

	// Поступление топлива
	УчетТоваров.СформироватьДвиженияПоступлениеПоТопливнымКартам(СтруктураТаблицДокумента.ТаблицаТопливо,
		ПараметрыПроведения.Реквизиты, Движения);
	
	// Поступление оборудования
	УчетОС.СформироватьДвиженияПоступлениеОборудования(СтруктураТаблицДокумента.ТаблицаОборудование,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление объектов строительства
	УчетОС.СформироватьДвиженияПоступлениеОбъектовСтроительства(СтруктураТаблицДокумента.ТаблицаОбъектыСтроительства,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление основных средств
	УчетОС.ПроверитьВозможностьИзмененияСостоянияОС(ПараметрыПроведения.ПроверкаСостоянияОСТаблица,
		ПараметрыПроведения.ПроверкаСостоянияОСРеквизиты, Отказ);
	
	УчетОС.СформироватьДвиженияПоступлениеОсновныхСредств(СтруктураТаблицДокумента.ТаблицаОсновныеСредства,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетГрупповыхОС.СформироватьДвиженияПоступлениеГрупповыхОбъектовОС(СтруктураТаблицДокумента.ТаблицаГрупповыеОбъектыОС,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	ИнвестиционныйНалоговыйВычет.СформироватьДвиженияИнвестиционныйВычетСтоимостьОбъектов(
		ТаблицаИнвестиционныйВычет,
		ПараметрыПроведения.Реквизиты,
		Движения);
	
	// Поступление услуг
	УчетДоходовРасходов.СформироватьДвиженияПоступлениеУслуг(СтруктураТаблицДокумента.ТаблицаУслуги,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	// Приобретение прав
	УчетНМА.СформироватьДвиженияПриобретениеПрав(СтруктураТаблицДокумента.ТаблицаПрава,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Поступление тары
	УчетТоваров.СформироватьДвиженияПоступлениеТары(ПараметрыПроведения.ТаблицаТара,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление товаров на комиссию
	УчетТоваров.СформироватьДвиженияПоступлениеТоваровНаКомиссию(ПараметрыПроведения.ПоступлениеТоваровНаКомиссиюТаблицаТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление в переработку
	УчетТоваров.СформироватьДвиженияПоступлениеМатериаловВПереработку(ПараметрыПроведения.ПоступлениеТоваровВПереработкуТаблицаТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Поступление закупленных товаров и услуг комитентов
	УчетТоваров.СформироватьДвиженияПоступлениеЗакупленныхТоваровУслугКомитентов(
		ТаблицаТоварыУслугиКомиссияПоЗакупке, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Задолженность комитентов по закупленных для них товарам и услугам
	УчетВзаиморасчетов.СформироватьДвиженияПоступлениеЗакупленныхТоваровУслугКомитентов(
		ТаблицаТоварыУслугиКомиссияПоЗакупке, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	//Движения регистра "Рублевые суммы документов в валюте"
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаТовары, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаОборудование, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаОбъектыСтроительства, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаОсновныеСредства, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаГрупповыеОбъектыОС,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаУслуги, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаПрава,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеБезНДС(ПараметрыПроведения.ТаблицаТара, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ПараметрыПроведения.ПоступлениеТоваровНаКомиссиюТаблицаТовары, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеБезНДС(ПараметрыПроведения.ПоступлениеТоваровВПереработкуТаблицаТовары, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ТаблицаТоварыУслугиКомиссияПоЗакупке, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Движения регистра  ИмпортТоваровТаможенныйСоюз
	РегистрыСведений.ИмпортТоваровТаможенныйСоюз.СформироватьДвиженияИмпортТоваровТС(ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Учет НДС
	УчетНДС.СформироватьДвиженияПоступлениеТоваровУслугОтПоставщика(
		ТаблицыНДС.Товары, ТаблицыНДС.Услуги, ПараметрыПроведения.НомераГТД,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСРаздельный.СформироватьДвиженияПоступлениеТоваровУслугОтПоставщика(
		ТаблицыНДС.Товары, ТаблицыНДС.Услуги, ПараметрыПроведения.Реквизиты,
		Движения, Отказ);
		
	УчетНДС.СформироватьДвиженияЖурналУчетаСчетовФактурРегистрация(
		ПараметрыПроведения.РегистрацияВЖурналеУчетаСчетовФактур, Движения, Отказ);
		
	// Зачет аванса
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(ТаблицаВзаиморасчеты,
		ПараметрыПроведения.ЗачетАвансовРеквизиты, Движения, Отказ);
	
	// Зачет аванса комитентов 
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(ТаблицаВзаиморасчетыКомиссияПоЗакупке, 
		ПараметрыПроведения.ЗачетАвансовКомиссияПоЗакупкеРеквизиты, Движения, Отказ);
		
	// Учет прослеживаемых товаров
	ПрослеживаемыеОперации = ПараметрыПроведения.ПрослеживаемыеОперации;
	
	ПрослеживаемостьБП.РассчитатьТаблицуПрослеживаемыеОперацииВРублях(
		ПрослеживаемыеОперации,
		СтруктураТаблицДокумента.ТаблицаТовары,
		ПараметрыПроведения.Реквизиты);
		
	ПрослеживаемостьБП.РассчитатьТаблицуПрослеживаемыеОперацииВРублях(
		ПрослеживаемыеОперации,
		СтруктураТаблицДокумента.ТаблицаОборудование,
		ПараметрыПроведения.Реквизиты);
		
	ПрослеживаемостьБП.РассчитатьТаблицуПрослеживаемыеОперацииВРублях(
		ПрослеживаемыеОперации,
		ТаблицаТоварыУслугиКомиссияПоЗакупке,
		ПараметрыПроведения.Реквизиты);
		
	ПрослеживаемыеОперацииОС = ПараметрыПроведения.ПрослеживаемыеОперацииОС;
	ПрослеживаемостьБП.РассчитатьТаблицуПрослеживаемыеОперацииВРублях(
		ПрослеживаемыеОперацииОС,
		СтруктураТаблицДокумента.ТаблицаОсновныеСредства,
		ПараметрыПроведения.Реквизиты,
		Истина);
	
	ПрослеживаемостьБП.СформироватьДвиженияПоступлениеТоваров(
		ПараметрыПроведения.ПрослеживаемыеТовары, 
		ПрослеживаемыеОперации,
		ПараметрыПроведения.РНПТ,
		ПараметрыПроведения.Реквизиты, 
		Движения);
		
	ПрослеживаемостьБП.СформироватьДвиженияПоступлениеОС(
		ПараметрыПроведения.ПрослеживаемыеОС, 
		ПрослеживаемыеОперацииОС,
		ПараметрыПроведения.РНПТОС,
		ПараметрыПроведения.Реквизиты, 
		Движения);
		
	// УСН
	СуммаСторноРасхода = 0;
	УчетУСН.ПоступлениеРасходовУСН(ПараметрыПроведения.УСНТаблицаРасходов,
		ПараметрыПроведения.УСНРеквизиты, СуммаСторноРасхода, Движения, Отказ);

	Если НЕ Отказ И Движения.РасходыПриУСН.Количество() > 0 Тогда
		Движения.РасходыПриУСН.Записать(Истина);
		Движения.РасходыПриУСН.Записывать = Ложь;
	КонецЕсли;

	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект, СтруктураТаблицУСН);
	
	// УСН-онлайн
	РазметкаОперацийАУСН.СформироватьДвиженияПоступлениеРасходов(ПараметрыПроведения.Реквизиты,
		ПараметрыПроведения.РасходыАУСН, Движения);
	
	РазметкаОперацийАУСН.СформироватьДвиженияРазметки(ПараметрыПроведения.Реквизиты,
		ТаблицаРазметкиАУСН, Движения);
	
	// Учет доходов и расходов ИП
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияПоступлениеМПЗ(СтруктураТаблицМПЗ,
		ТаблицаВзаиморасчеты, ,ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
		
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияПоступлениеВнеоборотныхАктивов(ТаблицаПоступленияОСИП,
		ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
		
	УчетДоходовИРасходовПредпринимателя.РегистрацияСведенийОбОплатеОСиНМА(ТаблицаОплатыОСИП,
		ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
		
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияПоступлениеВнеоборотныхАктивов(ТаблицаПоступленияНМАИП,
		ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
		
	УчетДоходовИРасходовПредпринимателя.РегистрацияСведенийОбОплатеОСиНМА(ТаблицаОплатыНМАИП,
		ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
		
	УчетВзаиморасчетов.СформироватьДвиженияПоПрочимРасчетам(ТаблицаПрочихРасчетов, Движения, Отказ);
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияЗачетОплатыПокупателя(
		ТаблицыПоступленияОтФакторинговойКомпанииИП.ТаблицаИПМПЗОтгруженные,
		ТаблицыПоступленияОтФакторинговойКомпанииИП.ТаблицаВзаиморасчеты, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	// Переоценка валютных остатков - после формирования проводок всеми другими механизмами
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкиДвиженийДокумента(
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетДоходовРасходов.СформироватьДвиженияРасчетПереоценкиВалютныхСредств(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Статусы счетов на оплату поставщика.
	Если НЕ ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		СтатусыДокументов.СформироватьДвиженияСтатусовДокументов(
			ТаблицаСтатусовСчетов, ПараметрыПроведения.Реквизиты);
	КонецЕсли;
		
	// Отложенные расчеты с контрагентами.
	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентами(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);
		
	// Регистрация в последовательности
	Документы.ПоступлениеТоваровУслуг.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(
		ЭтотОбъект, ПараметрыПроведения, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
		
	Движения.Записать();

	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект, "СчетФактураПолученный");
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ, НЕ УстановленСтатусДокумента);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект, "СчетФактураПолученный");	
	ПараметрыДействия.СостояниеФлага = Ложь;
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);

	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

	ПрослеживаемостьБП.УдалениеПроведенияПервичногоДокумента(Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьОбновитьАктОРасхождениях(ДанныеЗаполнения)
	АктОРасхожденияхСсылка = Документы.ПоступлениеТоваровУслуг.АктОРасхожденияхПоДокументу(Ссылка);
	
	Если ЗначениеЗаполнено(АктОРасхожденияхСсылка) Тогда
		ДокументОбъект = АктОРасхожденияхСсылка.ПолучитьОбъект();
	Иначе
		ДокументОбъект = Документы.АктОРасхождениях.СоздатьДокумент();
	КонецЕсли;
	
	ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийАктОРасхождениях.РасхожденияПриПриемке;
	ДокументОбъект.Дата = Дата;
	
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	ДокументОбъект.ДополнительныеСвойства.Вставить("НеПроверятьВозможностьУдаления");
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧастиПоСчетуНаОплату(Счет, ИменаТабличныхЧастей)

	Если Не ЗначениеЗаполнено(Счет) Тогда
		Возврат;
	КонецЕсли;
	
	МассивИмен = СтрРазделить(ИменаТабличныхЧастей, ", ", Ложь);
	Для каждого ИмяТаблицы Из МассивИмен Цикл
		ЭтотОбъект[ИмяТаблицы].Очистить();
	КонецЦикла;
	ЗаполнитьТовары = (МассивИмен.Найти("Товары") <> Неопределено);
	ЗаполнитьУслуги = (МассивИмен.Найти("Услуги") <> Неопределено);
	ЗаполнитьТару   = (МассивИмен.Найти("ВозвратнаяТара") <> Неопределено);
	ЗаполнитьПрава  = (МассивИмен.Найти("Права") <> Неопределено);
	
	ПоступленияПоСчету = Документы.ПоступлениеТоваровУслуг.ПоступленияПоСчету(Счет);
	ТекущееПоступление = ПоступленияПоСчету.Найти(Ссылка);
	Если ТекущееПоступление <> Неопределено Тогда
		ПоступленияПоСчету.Удалить(ТекущееПоступление);
	КонецЕсли;
	
	ЗаполнятьПризнакПрослеживаемости = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Дата)
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку; // учет РНПТ ведет заказчик;
		
	// Товары и услуги
	ТоварыУслугиКПоступлению = Документы.ПоступлениеТоваровУслуг.ТоварыУслугиКПоступлениюПоСчету(
		Счет, ПоступленияПоСчету, ЗаполнятьПризнакПрослеживаемости, Дата);
	Для Каждого СтрокаОснования Из ТоварыУслугиКПоступлению Цикл
		Если ЗаполнитьУслуги И СтрокаОснования.ЭтоУслуга Тогда
			ЗаполнитьЗначенияСвойств(Услуги.Добавить(), СтрокаОснования);
		ИначеЕсли ЗаполнитьТовары И НЕ СтрокаОснования.ЭтоУслуга Тогда
			ЗаполнитьЗначенияСвойств(Товары.Добавить(), СтрокаОснования);
		ИначеЕсли ЗаполнитьПрава И ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Права Тогда
			ЗаполнитьЗначенияСвойств(Права.Добавить(), СтрокаОснования);
		КонецЕсли;
	КонецЦикла;
		
	// Пересчет сумм
	ОснованиеСуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Счет, "СуммаВключаетНДС");
	Если ОснованиеСуммаВключаетНДС <> СуммаВключаетНДС Тогда
		Для Каждого СтрокаТЧ Из Товары Цикл
			СтрокаТЧ.Сумма = СтрокаТЧ.Сумма + ?(СуммаВключаетНДС, СтрокаТЧ.СуммаНДС, -СтрокаТЧ.СуммаНДС);
			СтрокаТЧ.Цена = ?(СтрокаТЧ.Количество = 0, 0, СтрокаТЧ.Сумма/СтрокаТЧ.Количество);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, СуммаВключаетНДС);
		КонецЦикла;
		Для Каждого СтрокаТЧ Из Услуги Цикл
			СтрокаТЧ.Сумма = СтрокаТЧ.Сумма + ?(СуммаВключаетНДС, СтрокаТЧ.СуммаНДС, -СтрокаТЧ.СуммаНДС);
			СтрокаТЧ.Цена = ?(СтрокаТЧ.Количество = 0, СтрокаТЧ.Сумма, СтрокаТЧ.Сумма/СтрокаТЧ.Количество);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, СуммаВключаетНДС);
		КонецЦикла;
		Для Каждого СтрокаТЧ Из Права Цикл
			СтрокаТЧ.Сумма = СтрокаТЧ.Сумма + ?(СуммаВключаетНДС, СтрокаТЧ.СуммаНДС, -СтрокаТЧ.СуммаНДС);
			СтрокаТЧ.Цена = ?(СтрокаТЧ.Количество = 0, СтрокаТЧ.Сумма, СтрокаТЧ.Сумма/СтрокаТЧ.Количество);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, СуммаВключаетНДС);
		КонецЦикла;
	КонецЕсли;
	
	// Возвратная тара
	Если ЗаполнитьТару
		И ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
		ВозвратнаяТараКПоступлению = Документы.ПоступлениеТоваровУслуг.ВозвратнаяТараКПоступлениюПоСчету(
			Счет, ПоступленияПоСчету);
		ВозвратнаяТара.Загрузить(ВозвратнаяТараКПоступлению);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоСчету(ИмяТаблицы, Счет) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПоСчетуНаОплату(Счет, ИмяТаблицы);
	
	ТабличнаяЧасть = ЭтотОбъект[ИмяТаблицы];
	
	СчетаУчетаВДокументах.ЗаполнитьСтроки(ТабличнаяЧасть, ИмяТаблицы, ЭтотОбъект, Документы.ПоступлениеТоваровУслуг);
	
КонецПроцедуры

Процедура ЗаполнитьПоДокументуОснованию(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.Доверенность") Тогда
		
		// Склад сразу заполняется значением по умолчанию, т.к. в доверенности его нет,
		// а от склада зависит, какие счета учета будут подставлены в табличную часть.
		Склад = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнойСклад");
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
		ВидОперации = Документы.ПоступлениеТоваровУслуг.ОпределитьВидОперацииПоДокументуОснованию(Основание);
		
		Если ТипЗнч(Основание.Сделка) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
			СчетНаОплатуПоставщика = Основание.Сделка;
		КонецЕсли; 
		
		// Тип цен и валюту берем по умолчанию из договора, т.к. в доверенности нет таких реквизитов,
		// курс и кратность взаиморасчетов будут подставлены по валюте договора в ЗаполнитьПоОснованию().
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "ВалютаВзаиморасчетов, ТипЦен");
			ВалютаДокумента   = РеквизитыДоговора.ВалютаВзаиморасчетов;
			ТипЦен            = РеквизитыДоговора.ТипЦен;
		КонецЕсли;
		
		// Флаги включения налогов.
		Если НЕ ЗначениеЗаполнено(ТипЦен) Тогда
			СуммаВключаетНДС = Ложь;
		Иначе
			СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипЦен, "ЦенаВключаетНДС");
		КонецЕсли;
		
		ЗаполнитьТоварыПоДоверенности(Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
		
		// Заполним реквизиты шапки по документу основанию.
		СчетНаОплатуПоставщика = Основание;
		ВидОперации = Документы.ПоступлениеТоваровУслуг.ОпределитьВидОперацииПоДокументуОснованию(Основание);
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание, Истина); // Копировать подразделение = Истина
		
		ЗаполнитьТабличныеЧастиПоСчетуНаОплату(Основание, "Товары,Услуги,ВозвратнаяТара");
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
		
		Если ЗначениеЗаполнено(Основание.ТорговыйОбъект) Тогда
			Склад = Основание.ТорговыйОбъект;
		КонецЕсли;
		
		МассивКонтрагентов = Новый Массив;
		МассивКонтрагентов.Добавить(Основание.Грузоотправитель);
		МассивКонтрагентов.Добавить(Основание.Грузополучатель);
		Если НЕ Основание.Поставщик.Пустая() Тогда
			МассивКонтрагентов.Добавить(Основание.Поставщик);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Классификатор.Ссылка КАК Ссылка,
		|	Классификатор.Контрагент КАК Контрагент,
		|	Классификатор.СоответствуетОрганизации КАК СоответствуетОрганизации,
		|	Классификатор.ТорговыйОбъект КАК ТорговыйОбъект,
		|	0 КАК Приоритет
		|ИЗ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК Классификатор
		|ГДЕ
		|	Классификатор.Ссылка В(&МассивОрганизаций)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	КлассификаторОрганизацийЕГАИС.Ссылка,
		|	Контрагенты.Ссылка,
		|	ЛОЖЬ,
		|	КлассификаторОрганизацийЕГАИС.ТорговыйОбъект,
		|	1
		|ИЗ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО КлассификаторОрганизацийЕГАИС.ИНН = Контрагенты.ИНН
		|			И КлассификаторОрганизацийЕГАИС.КПП = Контрагенты.КПП
		|ГДЕ
		|	КлассификаторОрганизацийЕГАИС.Ссылка В(&МассивОрганизаций)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
		
		Запрос.УстановитьПараметр("МассивОрганизаций",МассивКонтрагентов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Ссылка = Основание.Грузоотправитель И Грузоотправитель.Пустая() Тогда
				Грузоотправитель = Выборка.Контрагент;
			ИначеЕсли Выборка.Ссылка = Основание.Грузополучатель Тогда
				
				Если Организация.Пустая() И Выборка.СоответствуетОрганизации Тогда
					Организация  = Выборка.Контрагент;
				ИначеЕсли Грузополучатель.Пустая() И НЕ Выборка.СоответствуетОрганизации Тогда 
					Грузополучатель  = Выборка.Контрагент;
				КонецЕсли;
				
				Если Склад.Пустая() Тогда
					Склад = Выборка.ТорговыйОбъект;
				КонецЕсли;
			ИначеЕсли (НЕ Основание.Поставщик.Пустая())
				И Выборка.Ссылка = Основание.Поставщик
				И Контрагент.Пустая()
				И (НЕ Выборка.СоответствуетОрганизации) Тогда
				Контрагент = Выборка.Контрагент;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
			Контрагент = Грузоотправитель;
		КонецЕсли;
		
		НомерВходящегоДокумента = Основание.НомерТТН;
		ДатаВходящегоДокумента  = Основание.ДатаТТН;
		
		ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия;
		
		// Тип цен и валюту берем по умолчанию из договора
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "ВалютаВзаиморасчетов, ТипЦен");
			ВалютаДокумента   = РеквизитыДоговора.ВалютаВзаиморасчетов;
			ТипЦен            = РеквизитыДоговора.ТипЦен;
		КонецЕсли;
		
		// Флаги включения налогов.
		Если НЕ ЗначениеЗаполнено(ТипЦен) Тогда
			СуммаВключаетНДС = Ложь;
		Иначе
			СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипЦен, "ЦенаВключаетНДС");
		КонецЕсли;
		
		ЗаполнитьТоварыЕГАИС(Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС") Тогда
		
		ИнтеграцияВЕТИСБП.ЗаполнитьПоступлениеПоВходящейОперацииВЕТИС(ЭтотОбъект, Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПриемкаТоваровИСМП") Тогда
		
		ИнтеграцияИСМПБП.ЗаполнитьПоступлениеПоПриемкаТоваровИСМП(ЭтотОбъект, Основание); 
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ИмпортПродукцииСАТУРН") Тогда
		
		ИнтеграцияСАТУРНБП.ЗаполнитьПоступлениеПоИмпортуПродукцииСАТУРН(ЭтотОбъект, Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.НакладнаяСАТУРН") Тогда
		
		ИнтеграцияСАТУРНБП.ЗаполнитьПоступлениеТоваровПоНакладнойСАТУРН(ЭтотОбъект, Основание);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТоварыЕГАИС(ТТНВходящаяЕГАИС)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Основание", ТТНВходящаяЕГАИС);
	
	Если УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Организация, Дата) Тогда
		ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
	Иначе
		ОтражениеВУСН = Неопределено;
	КонецЕсли;
	Запрос.УстановитьПараметр("ОтражениеВУСН", ОтражениеВУСН);
	Запрос.УстановитьПараметр("СтавкаНДСПоУмолчанию", УчетНДСКлиентСервер.СтавкаНДСПоУмолчанию(Дата));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТТНВходящаяЕГАИСТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИСТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА ТТНВходящаяЕГАИСТовары.КоличествоФакт
	|		ИНАЧЕ ТТНВходящаяЕГАИСТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ТТНВходящаяЕГАИСТовары.Цена КАК Цена,
	|	1 КАК КоэффициентПересчетаУпаковки,
	|	ТТНВходящаяЕГАИСТовары.Сумма,
	|	ТТНВходящаяЕГАИСТовары.НомерСтроки
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТТНВходящаяЕГАИСТовары
	|ГДЕ
	|	ТТНВходящаяЕГАИСТовары.Ссылка = &Основание
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КоэффициентПересчетаУпаковки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Цена,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	&СтавкаНДСПоУмолчанию КАК СтавкаНДС,
	|	&ОтражениеВУСН КАК ОтражениеВУСН
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.КоэффициентПересчетаУпаковки = 1
	|	И ТаблицаТовары.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Количество * ТаблицаТовары.КоэффициентПересчетаУпаковки,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Количество * ТаблицаТовары.КоэффициентПересчетаУпаковки = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма / (ТаблицаТовары.Количество * ТаблицаТовары.КоэффициентПересчетаУпаковки)
	|	КОНЕЦ,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.НомерСтроки,
	|	&СтавкаНДСПоУмолчанию,
	|	&ОтражениеВУСН
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.КоэффициентПересчетаУпаковки <> 1
	|	И ТаблицаТовары.Количество <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	 
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ДанныеОбъекта = Новый Структура(
		"Дата, ВидОперации, Организация, Склад, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ЭтотОбъект);
	
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина), ДанныеОбъекта, Ложь);
	
	
	Для каждого СтрокаТоваров Из ТаблицаТоваров Цикл
		
		СтрокаТабличнойЧасти = Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТоваров);
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре[СтрокаТоваров.Номенклатура];
		Если ЗначениеЗаполнено(СтрокаТоваров.Номенклатура) И СведенияОНоменклатуре <> Неопределено Тогда
			СтрокаТабличнойЧасти.ЕдиницаИзмерения		= СведенияОНоменклатуре.ЕдиницаИзмерения;
			СтрокаТабличнойЧасти.Коэффициент			= СведенияОНоменклатуре.Коэффициент;
			СтрокаТабличнойЧасти.СтавкаНДС				= СведенияОНоменклатуре.СтавкаНДС;
			СтрокаТабличнойЧасти.НомерГТД				= СведенияОНоменклатуре.НомерГТД;
			СтрокаТабличнойЧасти.СтранаПроисхождения	= СведенияОНоменклатуре.СтранаПроисхождения;
		КонецЕсли;
		
		СтавкаНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС);

		СтрокаТабличнойЧасти.Сумма = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(СтрокаТабличнойЧасти.Сумма, Истина, СуммаВключаетНДС, СтавкаНДС);
		СтрокаТабличнойЧасти.Цена  = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(СтрокаТабличнойЧасти.Цена, Истина, СуммаВключаетНДС, СтавкаНДС);
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьТоварыПоДоверенности(Основание)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпрНоменклатура.Ссылка КАК Номенклатура,
	|	ДоверенностьТовары.Количество КАК Количество,
	|	ДоверенностьТовары.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ДоверенностьТовары.ЕдиницаПоКлассификатору = СпрНоменклатура.ЕдиницаИзмерения
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СовпадаетЕдиницаИзмерения
	|ПОМЕСТИТЬ ВТ_Доверенность
	|ИЗ
	|	Документ.Доверенность.Товары КАК ДоверенностьТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ((ВЫРАЗИТЬ(ДоверенностьТовары.НаименованиеТовара КАК СТРОКА(1000))) = (ВЫРАЗИТЬ(СпрНоменклатура.НаименованиеПолное КАК СТРОКА(1000))))
	|ГДЕ
	|	ДоверенностьТовары.Ссылка = &Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Доверенность.НомерСтроки КАК НомерСтроки,
	|	МАКСИМУМ(ВТ_Доверенность.СовпадаетЕдиницаИзмерения) КАК СовпадаетЕдиницаИзмерения
	|ПОМЕСТИТЬ ВТ_СовпадениеПоСтрокам
	|ИЗ
	|	ВТ_Доверенность КАК ВТ_Доверенность
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Доверенность.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Доверенность.НомерСтроки КАК НомерСтроки,
	|	МАКСИМУМ(ВТ_Доверенность.Номенклатура) КАК Номенклатура,
	|	ВТ_Доверенность.Количество КАК Количество
	|ИЗ
	|	ВТ_Доверенность КАК ВТ_Доверенность
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СовпадениеПоСтрокам КАК ВТ_СовпадениеПоСтрокам
	|		ПО  ВТ_Доверенность.НомерСтроки = ВТ_СовпадениеПоСтрокам.НомерСтроки
	|			И ВТ_Доверенность.СовпадаетЕдиницаИзмерения = ВТ_СовпадениеПоСтрокам.СовпадаетЕдиницаИзмерения
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Доверенность.Количество,
	|	ВТ_Доверенность.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ДанныеОбъекта = Новый Структура(
		"Дата, ВидОперации, Организация, Склад, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов,
		|ЭтоКомиссия, ДоговорКонтрагента");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ЭтотОбъект);
	
	ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	
	ДанныеОбъекта.ЭтоКомиссия = ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
		И ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина), ДанныеОбъекта, Ложь);
	
	Если УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Организация, Дата) Тогда
		Если ДанныеОбъекта.ЭтоКомиссия Тогда
			ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются;
		ИначеЕсли ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
			ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеОбъекта.ЭтоКомиссия Тогда
		СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.НеЗачитывать;
	КонецЕсли;
	
	Для каждого СтрокаТоваров Из ТаблицаТоваров Цикл
		
		СтрокаТабличнойЧасти = Товары.Добавить();
		
		СтрокаТабличнойЧасти.Номенклатура	= СтрокаТоваров.Номенклатура;
		СтрокаТабличнойЧасти.Количество	= СтрокаТоваров.Количество;
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТоваров.Номенклатура);
		Если СведенияОНоменклатуре = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.ЕдиницаИзмерения		= СведенияОНоменклатуре.ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.Коэффициент			= СведенияОНоменклатуре.Коэффициент;
		СтрокаТабличнойЧасти.Цена					= СведенияОНоменклатуре.Цена;
		СтрокаТабличнойЧасти.СтавкаНДС				= СведенияОНоменклатуре.СтавкаНДС;
		СтрокаТабличнойЧасти.НомерГТД				= СведенияОНоменклатуре.НомерГТД;
		СтрокаТабличнойЧасти.СтранаПроисхождения	= СведенияОНоменклатуре.СтранаПроисхождения;
		СтрокаТабличнойЧасти.ОтражениеВУСН			= ОтражениеВУСН;
		
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
		
КонецЦикла;

КонецПроцедуры

Процедура ПроверитьКорректностьЗаполненияТабличнойЧастиТовары(Отказ)

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

	УчетВПродажныхЦенах =
		УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата) = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;

	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	
	//СкладВСписке
	Если СкладВСписке Тогда
		РозницаВПродажныхЦенах = УчетВПродажныхЦенах И (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
									ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары)
									И ВидДоговораКонтрагента <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
									
		Если Не РозницаВПродажныхЦенах Или Не ЕстьРозничныйМагазин(Товары) Тогда
			Возврат;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КурсыВалют.Валюта КАК Валюта,
		|	КурсыВалют.Курс КАК Курс,
		|	КурсыВалют.Кратность КАК Кратность
		|ПОМЕСТИТЬ ВТ_КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсыВалют
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КурсыВалют.Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	ЦеныНоменклатуры.ТипЦен КАК ТипЦен,
		|	ВЫБОР
		|		КОГДА ВТ_КурсыВалют.Валюта ЕСТЬ NULL
		|				ИЛИ ВТ_КурсыВалют.Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ЦеныНоменклатуры.Цена
		|		КОГДА НЕ ВТ_КурсыВалют.Валюта ЕСТЬ NULL
		|				И ВТ_КурсыВалют.Кратность = 0
		|			ТОГДА ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс
		|		ИНАЧЕ ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс * ВТ_КурсыВалют.Кратность
		|	КОНЕЦ КАК ЦенаВВалютеРегламентированногоУчета,
		|	ЦеныНоменклатуры.Валюта КАК Валюта
		|ПОМЕСТИТЬ ВТ_ЦеныНоменклатуры
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			&КонецПериода,
		|			Номенклатура В (&Номенклатура)
		|				И ТипЦен В (&ТипыЦенСкладов)) КАК ЦеныНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КурсыВалют КАК ВТ_КурсыВалют
		|		ПО ЦеныНоменклатуры.Валюта = ВТ_КурсыВалют.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЦеныНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	ЦеныНоменклатуры.ТипЦен КАК ТипЦен,
		|	ВЫБОР
		|		КОГДА ВТ_КурсыВалют.Валюта ЕСТЬ NULL
		|				ИЛИ ВТ_КурсыВалют.Валюта = &ВалютаРегламентированногоУчета
		|			ТОГДА ЦеныНоменклатуры.Цена
		|		КОГДА НЕ ВТ_КурсыВалют.Валюта ЕСТЬ NULL
		|				И ВТ_КурсыВалют.Кратность = 0
		|			ТОГДА ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс
		|		ИНАЧЕ ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс * ВТ_КурсыВалют.Кратность
		|	КОНЕЦ КАК ЦенаВВалютеРегламентированногоУчета,
		|	ЦеныНоменклатуры.Валюта КАК Валюта
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			&КонецПериода,
		|			Номенклатура В (&Номенклатура)
		|				И ТипЦен В (&ТипыЦенСкладов)) КАК ЦеныНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КурсыВалют КАК ВТ_КурсыВалют
		|		ПО ЦеныНоменклатуры.Валюта = ВТ_КурсыВалют.Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(Остатки.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
		|	ВЫРАЗИТЬ(Остатки.Субконто2 КАК Справочник.Склады) КАК Склад,
		|	ВЫБОР
		|		КОГДА Остатки.КоличествоОстатокДт <> 0
		|			ТОГДА ВЫРАЗИТЬ(Остатки.СуммаОстатокДт / Остатки.КоличествоОстатокДт КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК УчетнаяЦена,
		|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.Цена, 0) КАК Цена,
		|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.ЦенаВВалютеРегламентированногоУчета, 0) КАК ЦенаВВалютеРегламентированногоУчета,
		|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&КонецПериода,
		|			Счет = &СчетАТТ,
		|			&ПорядокСубконто,
		|			Организация = &Организация
		|				И Субконто1 В (&Номенклатура)) КАК Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныНоменклатуры КАК ВТ_ЦеныНоменклатуры
		|		ПО ((ВЫРАЗИТЬ(Остатки.Субконто1 КАК Справочник.Номенклатура)) = ВТ_ЦеныНоменклатуры.Номенклатура)
		|			И (ВЫРАЗИТЬ(Остатки.Субконто2 КАК Справочник.Склады).ТипЦенРозничнойТорговли = ВТ_ЦеныНоменклатуры.ТипЦен)
		|ГДЕ
		|	Остатки.КоличествоОстаток > 0";
		
		ПорядокСубконто = Новый Массив();
		ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
		
		ТаблицаТоваровПоСкладам = Товары.Выгрузить(, "Номенклатура, Склад");
		ТаблицаТоваровПоСкладам.Колонки.Добавить("ТипСклада");
		ТаблицаТоваровПоСкладам.Колонки.Добавить("ТипЦенРозничнойТорговли");
		Для Каждого СтрокаТовары Из ТаблицаТоваровПоСкладам Цикл
			РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТОвары.Склад,"ТипСклада, ТипЦенРозничнойТорговли"); 
			ЗаполнитьЗначенияСвойств(СтрокаТовары, РеквизитыСклада);
		КонецЦикла;
		ОтборСтрок 		= Новый Структура("ТипСклада", Перечисления.ТипыСкладов.РозничныйМагазин);
		ТаблицаТоваров 	= ТаблицаТоваровПоСкладам.Скопировать(ОтборСтрок);
		
		МассивНоменклатуры 	= ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура");
		ТипыЦенСкладов 		= ТаблицаТоваров.ВыгрузитьКолонку("ТипЦенРозничнойТорговли");
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("КонецПериода",                   Дата);
		Запрос.УстановитьПараметр("Организация",                    Организация);
		Запрос.УстановитьПараметр("ПорядокСубконто",                ПорядокСубконто);
		Запрос.УстановитьПараметр("Номенклатура",                   МассивНоменклатуры);
		Запрос.УстановитьПараметр("Дата",                           Дата);
		Запрос.УстановитьПараметр("ТипыЦенСкладов",                 ТипыЦенСкладов);
		Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
		Запрос.УстановитьПараметр("СчетАТТ",						ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ);

		
		Результат = Запрос.ВыполнитьПакет();
		ТаблицаЦенНоменклатуры = Результат[2].Выгрузить();
		ТаблицаЦенНоменклатуры.Индексы.Добавить("Номенклатура");
		ТаблицаУчетныхЦен      = Результат[3].Выгрузить();
		ТаблицаУчетныхЦен.Индексы.Добавить("Номенклатура");
		
		Для Каждого СтрокаТаблицы Из Товары Цикл
		
			Если СкладВСписке И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Склад, "ТипСклада") <> Перечисления.ТипыСкладов.РозничныйМагазин Тогда
				Продолжить;
			КонецЕсли;
		
			Префикс = "Товары[%1].";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
			
			ИмяСписка = НСтр("ru = 'Товары'");
			
			УчетнаяЦена   = 0;
			РозничнаяЦена = 0;
		
			Отбор = Новый Структура("Номенклатура, Склад", СтрокаТаблицы.Номенклатура, СтрокаТаблицы.Склад);
			СтрокаТаблицыЦен = ОбщегоНазначенияБП.НайтиСтрокуТаблицы(ТаблицаУчетныхЦен, Отбор);
			Если СтрокаТаблицыЦен <> Неопределено Тогда
				УчетнаяЦена = СтрокаТаблицыЦен.УчетнаяЦена;
			Иначе
				УчетнаяЦена = 0;	
			КонецЕсли;
		
			ПараметрыОтбора = Новый Структура("Номенклатура, ТипЦен", СтрокаТаблицы.Номенклатура, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Склад, "ТипЦенРозничнойТорговли"));
			СтрокаТаблицыРозничныхЦен = ОбщегоНазначенияБП.НайтиСтрокуТаблицы(ТаблицаЦенНоменклатуры, ПараметрыОтбора);
			
			Если СтрокаТаблицыРозничныхЦен <> Неопределено Тогда
				РозничнаяЦена = СтрокаТаблицыРозничныхЦен.ЦенаВВалютеРегламентированногоУчета;
			Иначе
				РозничнаяЦена = 0;
			КонецЕсли;
		
			//Допустимое отклонение цены составляет цену минимальной единицы количества (0.001)
			ДопустимоеОтклонение = УчетнаяЦена * 0.001;
		
		
			Если РозничнаяЦена = 0 Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Розничная цена товара ""%1"" из строки %2 списка ""%3"" не установлена.
				|Установите цену документом ""Установка цен номенклатуры"".'"),
				СтрокаТаблицы.Номенклатура, СтрокаТаблицы.НомерСтроки, ИмяСписка);
				Поле = Префикс + "Номенклатура";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			ИначеЕсли УчетнаяЦена = 0 Тогда
				// Учетная цена неизвестна.
			ИначеЕсли РозничнаяЦена - УчетнаяЦена > ДопустимоеОтклонение
				ИЛИ РозничнаяЦена - УчетнаяЦена < - ДопустимоеОтклонение Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Розничная цена товара ""%1"" из строки %2 списка ""%3"" (%4) не равна учетной цене (%5).
				|Измените цену документом ""Установка цен номенклатуры"" или проведите переоценку документом ""Переоценка товаров в рознице"".'"),
				СтрокаТаблицы.Номенклатура, СтрокаТаблицы.НомерСтроки, ИмяСписка, РозничнаяЦена, УчетнаяЦена);
				Поле = Префикс + "Номенклатура";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЦикла;
        //
	Иначе
		ДанныеСклада      = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "ТипСклада, ТипЦенРозничнойТорговли");
		Если ДанныеСклада.ТипСклада <> Перечисления.ТипыСкладов.ОптовыйСклад И УчетВПродажныхЦенах
			И (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
				ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары)
			И ВидДоговораКонтрагента <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
			РозницаВПродажныхЦенах = Истина;
		Иначе
			РозницаВПродажныхЦенах = Ложь;
		КонецЕсли;

		АТТ = (РозницаВПродажныхЦенах И ДанныеСклада.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин);

		Если АТТ Тогда
			// Установим параметры запроса
			ПорядокСубконто = Новый Массив();
			ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
			ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);

			МассивНоменклатуры = Товары.ВыгрузитьКолонку("Номенклатура");

			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("Склад",                          Склад);
			Запрос.УстановитьПараметр("КонецПериода",                   Дата);
			Запрос.УстановитьПараметр("Организация",                    Организация);
			Запрос.УстановитьПараметр("Подразделение",                  ПодразделениеОрганизации);
			Запрос.УстановитьПараметр("ПорядокСубконто",                ПорядокСубконто);
			Запрос.УстановитьПараметр("Номенклатура",                   МассивНоменклатуры);
			Запрос.УстановитьПараметр("Дата",                           Дата);
			Запрос.УстановитьПараметр("СкладТипЦен",                    ДанныеСклада.ТипЦенРозничнойТорговли);
			Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
			Запрос.УстановитьПараметр("СчетАТТ",						ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ);

			Запрос.Текст = "
			|// Курсы валют (временная таблица)
			|ВЫБРАТЬ
			|	КурсыВалют.Валюта,
			|	КурсыВалют.Курс,
			|	КурсыВалют.Кратность
			|ПОМЕСТИТЬ ВТ_КурсыВалют
			|ИЗ
			|	РегистрСведений.КурсыВалют.СрезПоследних(
			|		&КонецПериода,
			|	) КАК КурсыВалют
			|ИНДЕКСИРОВАТЬ ПО
			|	Валюта
			|;
			|
			|// Запрос к ценам номенклатуры (временная таблица)
			|ВЫБРАТЬ
			|	ЦеныНоменклатуры.Номенклатура,
			|	ЦеныНоменклатуры.Цена,
			|	ВЫБОР
			|		КОГДА (ВТ_КурсыВалют.Валюта ЕСТЬ NULL) ИЛИ (ВТ_КурсыВалют.Валюта = &ВалютаРегламентированногоУчета) ТОГДА
			|			ЦеныНоменклатуры.Цена
			|		КОГДА НЕ ВТ_КурсыВалют.Валюта ЕСТЬ NULL И ВТ_КурсыВалют.Кратность = 0 ТОГДА
			|			ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс
			|		ИНАЧЕ
			|			ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс * ВТ_КурсыВалют.Кратность
			|	КОНЕЦ КАК ЦенаВВалютеРегламентированногоУчета,
			|	ЦеныНоменклатуры.Валюта
			|ПОМЕСТИТЬ ВТ_ЦеныНоменклатуры
			|ИЗ
			|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
			|		&КонецПериода,
			|		Номенклатура В (&Номенклатура) И (ТипЦен = &СкладТипЦен)
			|	) КАК ЦеныНоменклатуры
			|
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	ВТ_КурсыВалют
			|	ПО
			|		ЦеныНоменклатуры.Валюта = ВТ_КурсыВалют.Валюта
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|
			|// Запрос к ценам номенклатуры
			|ВЫБРАТЬ
			|	ЦеныНоменклатуры.Номенклатура,
			|	ЦеныНоменклатуры.Цена,
			|	ВЫБОР
			|		КОГДА (ВТ_КурсыВалют.Валюта ЕСТЬ NULL) ИЛИ (ВТ_КурсыВалют.Валюта = &ВалютаРегламентированногоУчета) ТОГДА
			|			ЦеныНоменклатуры.Цена
			|		КОГДА НЕ ВТ_КурсыВалют.Валюта ЕСТЬ NULL И ВТ_КурсыВалют.Кратность = 0 ТОГДА
			|			ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс
			|		ИНАЧЕ
			|			ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс * ВТ_КурсыВалют.Кратность
			|	КОНЕЦ КАК ЦенаВВалютеРегламентированногоУчета,
			|	ЦеныНоменклатуры.Валюта
			|ИЗ
			|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
			|		&КонецПериода,
			|		Номенклатура В (&Номенклатура) И (ТипЦен = &СкладТипЦен)
			|	) КАК ЦеныНоменклатуры
			|
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	ВТ_КурсыВалют
			|	ПО
			|		ЦеныНоменклатуры.Валюта = ВТ_КурсыВалют.Валюта
			|;
			|
			|// Основной запрос
			|ВЫБРАТЬ
			|	ВЫРАЗИТЬ(Остатки.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
			|	ВЫБОР
			|		КОГДА Остатки.КоличествоОстатокДт <> 0
			|			ТОГДА ВЫРАЗИТЬ(Остатки.СуммаОстатокДт / Остатки.КоличествоОстатокДт КАК ЧИСЛО(15, 2))
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК УчетнаяЦена,
			|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.Цена, 0) КАК Цена,
			|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.ЦенаВВалютеРегламентированногоУчета, 0) КАК ЦенаВВалютеРегламентированногоУчета,
			|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет = &СчетАТТ,
			|		&ПорядокСубконто, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто1 В (&Номенклатура) И Субконто2 = &Склад) КАК Остатки
			|
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|	ВТ_ЦеныНоменклатуры
			|	ПО
			|		ВЫРАЗИТЬ(Остатки.Субконто1 КАК Справочник.Номенклатура) = ВТ_ЦеныНоменклатуры.Номенклатура
			|
			|ГДЕ
			|	Остатки.КоличествоОстаток > 0
			|";

			Результат = Запрос.ВыполнитьПакет();
			ТаблицаЦенНоменклатуры = Результат[2].Выгрузить();
			ТаблицаЦенНоменклатуры.Индексы.Добавить("Номенклатура");
			ТаблицаУчетныхЦен      = Результат[3].Выгрузить();
			ТаблицаУчетныхЦен.Индексы.Добавить("Номенклатура");
		
			Для Каждого СтрокаТаблицы Из Товары Цикл
			
				Префикс = "Товары[%1].";
				Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
				
				ИмяСписка = НСтр("ru = 'Товары'");
				
				УчетнаяЦена   = 0;
				РозничнаяЦена = 0;
				
				СтрокаТаблицыЦен = ТаблицаУчетныхЦен.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
				Если СтрокаТаблицыЦен <> Неопределено Тогда
					УчетнаяЦена = СтрокаТаблицыЦен.УчетнаяЦена;
				Иначе
					УчетнаяЦена = 0;
				КонецЕсли;
				СтрокаТаблицыРозничныхЦен = ТаблицаЦенНоменклатуры.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
				Если СтрокаТаблицыРозничныхЦен = Неопределено Тогда
					РозничнаяЦена = 0;
				Иначе
					РозничнаяЦена = СтрокаТаблицыРозничныхЦен.ЦенаВВалютеРегламентированногоУчета;
				КонецЕсли;
				
				//Допустимое отклонение цены составляет цену минимальной единицы количества (0.001)
				ДопустимоеОтклонение = УчетнаяЦена * 0.001;
				
				Если РозничнаяЦена = 0 Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Розничная цена товара ""%1"" из строки %2 списка ""%3"" не установлена.
					|Установите цену документом ""Установка цен номенклатуры"".'"),
					СтрокаТаблицы.Номенклатура, СтрокаТаблицы.НомерСтроки, ИмяСписка);
					Поле = Префикс + "Номенклатура";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				ИначеЕсли УчетнаяЦена = 0 Тогда
					// Учетная цена неизвестна.
				ИначеЕсли РозничнаяЦена - УчетнаяЦена > ДопустимоеОтклонение
					ИЛИ РозничнаяЦена - УчетнаяЦена < - ДопустимоеОтклонение Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Розничная цена товара ""%1"" из строки %2 списка ""%3"" (%4) не равна учетной цене (%5).
					|Измените цену документом ""Установка цен номенклатуры"" или проведите переоценку документом ""Переоценка товаров в рознице"".'"),
					СтрокаТаблицы.Номенклатура, СтрокаТаблицы.НомерСтроки, ИмяСписка, РозничнаяЦена, УчетнаяЦена);
					Поле = Префикс + "Номенклатура";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьСтатусДокумента()
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПодчиненныйСчетФактура = УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруПолученный(Ссылка);
	Если Не ДополнительныеСвойства.Свойство("СтатусДокумента") И ПодчиненныйСчетФактура <> Неопределено Тогда
		// Записываем из списка документов. Движения по статусам сформирует счет-фактура.
		Возврат;
	КонецЕсли;
	
	Статусы = Новый Структура("СтатусДокумента, СтатусСчетаФактуры, СтатусЧекаНПД");
	ЗаполнитьЗначенияСвойств(Статусы, ДополнительныеСвойства);
	
	// Статус наличия и проведения счета-фактуры.
	Если Не РаботаСДоговорамиКонтрагентовБП.ДокументСНДС(Ссылка) Тогда
		Статусы.СтатусСчетаФактуры = Перечисления.СтатусыСчетаФактуры.НеТребуется;
	ИначеЕсли Статусы.СтатусСчетаФактуры = Неопределено Или ПометкаУдаления Тогда
		// При записи из формы не определили статус счета-фактуры. Рассчитаем его по реквизитам документа.
		Статусы.СтатусСчетаФактуры = СтатусСчетаФактурыПоУмолчанию();
	КонецЕсли;
	// Статус оригинала счета-фактуры.
	СтатусОригиналаСчетаФактуры = Неопределено;
	Если ДополнительныеСвойства.Свойство("СтатусОригиналаСчетаФактуры") Тогда
		СтатусОригиналаСчетаФактуры = ДополнительныеСвойства.СтатусОригиналаСчетаФактуры;
	КонецЕсли;
	
	// Для оптимизации открытия списков входящих документов со статусами связанных с ними счетов-фактур 
	// записываем статусы счета-фактуры в одной строке вместе со статусом входящего документа.
	УстановленСтатусДокумента = Истина;
	СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
	СтатусыДокумента.Статус            = Статусы.СтатусДокумента;
	СтатусыДокумента.СтатусСФ          = Статусы.СтатусСчетаФактуры;
	СтатусыДокумента.СтатусОригиналаСФ = СтатусОригиналаСчетаФактуры;
	СтатусыДокумента.СтатусЧекаНПД     = Статусы.СтатусЧекаНПД;
	РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(Ссылка, СтатусыДокумента);
	
	// Устанавливаем статус связанного с документом счета-фактуры для отображения в списках счетов-фактур.
	Если СтатусОригиналаСчетаФактуры <> Неопределено
		И ЗначениеЗаполнено(ПодчиненныйСчетФактура) Тогда
		СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
		СтатусыДокумента.Статус = СтатусОригиналаСчетаФактуры;
		РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(ПодчиненныйСчетФактура, СтатусыДокумента);
	КонецЕсли;
	
КонецПроцедуры

Функция СтатусСчетаФактурыПоУмолчанию()
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		Возврат Перечисления.СтатусыСчетаФактуры.НеТребуется;
	КонецЕсли;
	
	Если НДСНеВыделять
		Или Не Справочники.ДоговорыКонтрагентов.ТребуетсяСчетФактураПолученный(ДоговорКонтрагента) Тогда
		Возврат Перечисления.СтатусыСчетаФактуры.НеТребуется;
	КонецЕсли;
	
	Возврат Перечисления.СтатусыСчетаФактуры.Отсутствует;
	
КонецФункции

Процедура СинхронизироватьРеквизитыАктаОРасхождениях(Отказ)
	АктОРасхожденияхСсылка = Документы.ПоступлениеТоваровУслуг.АктОРасхожденияхПоДокументу(Ссылка, НЕ ПометкаУдаления);
	
	Если ЗначениеЗаполнено(АктОРасхожденияхСсылка) Тогда
		
		ДокументОбъект = АктОРасхожденияхСсылка.ПолучитьОбъект();
		ДокументОбъект.ДополнительныеСвойства.Вставить("НеПроверятьВозможностьУдаления");
		
		Попытка
			ДокументОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
		Исключение
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось изменить документ ""Акт о расхождениях"". Попробуйте позже.'"),,,,Отказ);
		КонецПопытки;
	КонецЕсли; 
КонецПроцедуры

Процедура ПроверитьПоляПрослеживаемости(ВозможнаКомиссияПоЗакупке, ИностранныйКонтрагент, ИмяТаблицы, ЭтоКомиссия, Отказ)
	
	Для Каждого СтрокаТаблицы Из ЭтотОбъект[ИмяТаблицы] Цикл
		Префикс = ИмяТаблицы + "[%1].";
		Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
		
		// Проверка договора и счета расчетов с комитентом
		Если ИмяТаблицы = "Товары"
			И ВозможнаКомиссияПоЗакупке И ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Договор с комитентом'"),
				СтрокаТаблицы.НомерСтроки, ИмяТаблицы);
				Поле = Префикс + "ДоговорКонтрагента";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		НужноЗаполнениеКолонкиНоменклатура    = Ложь;
		НужноЗаполнениеКолонкиСумма           = Ложь;
		
		Если ИмяТаблицы = "ОсновныеСредства"
			И СтрокаТаблицы.ПрослеживаемыйТовар Тогда
			Отбор = Новый Структура("ИдентификаторСтроки", СтрокаТаблицы.ИдентификаторСтроки);
			МассивСтрокСведенияПрослеживаемости = СведенияПрослеживаемости.Выгрузить(Отбор);
			КоличествоРНПТ = МассивСтрокСведенияПрослеживаемости.Количество();
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.ОсновноеСредство, "ЕдиницаУчета") = Перечисления.ЕдиницыУчетаОС.ИнвентарныйОбъект Тогда
				Если КоличествоРНПТ = 0 Тогда
					ТекстСообщенияРасшифрвка = НСтр("ru = 'Для прослеживаемого основного средства необходимо указать номер РНПТ.'");
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка","Корректность", НСтр("ru = 'РНПТ'"),
					СтрокаТаблицы.НомерСтроки, НСтр("ru = 'Основные средства'"), ТекстСообщенияРасшифрвка);
					Поле = Префикс + "РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				ИначеЕсли КоличествоРНПТ >= 1 Или Не ЗначениеЗаполнено(МассивСтрокСведенияПрослеживаемости[0].Номенклатура) Тогда
					СуммаБезНДС = ?(СуммаВключаетНДС, СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС, СтрокаТаблицы.Сумма);
					Если СуммаБезНДС < МассивСтрокСведенияПрослеживаемости.Итог("Сумма") Тогда
						ТекстСообщенияРасшифрвка = НСтр("ru = 'Итоговая сумма по таблице с данными РНПТ превышает стоимость основного средства (без учета НДС).'");
						ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Список","Корректность", НСтр("ru = 'РНПТ'"),
						СтрокаТаблицы.НомерСтроки, НСтр("ru = 'РНПТ'"), ТекстСообщенияРасшифрвка);
						Поле = Префикс + "РНПТ";
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
					КонецЕсли;
				КонецЕсли;
				Если МассивСтрокСведенияПрослеживаемости.Итог("Количество") > 1
					Или (МассивСтрокСведенияПрослеживаемости.Итог("Количество") = 1
					И ЗначениеЗаполнено(МассивСтрокСведенияПрослеживаемости[0].Номенклатура)) Тогда
					НужноЗаполнениеКолонкиНоменклатура = Истина;
					НужноЗаполнениеКолонкиСумма = Истина;
				КонецЕсли;
			Иначе
				Если СтрокаТаблицы.Количество > 0 И МассивСтрокСведенияПрослеживаемости.Количество() = 0 
					ИЛИ (НЕ ЕстьРасхождения И ПрослеживаемостьБП.КоличествоРНПТКСтроке(МассивСтрокСведенияПрослеживаемости) <> СтрокаТаблицы.Количество) Тогда
					ТекстСообщенияРасшифрвка = НСтр("ru = 'Количество РНПТ не совпадает с количеством основных средств в строке.'");
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка","Корректность", НСтр("ru = 'РНПТ'"),
					СтрокаТаблицы.НомерСтроки, НСтр("ru = 'Основные средства'"), ТекстСообщенияРасшифрвка);
					Поле = Префикс + "РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
			
		Если СтрокаТаблицы.ПрослеживаемыйТовар
			И НЕ ИмяТаблицы = "ОсновныеСредства"
			И НЕ ИностранныйКонтрагент
			И СтрокаТаблицы.Количество > 0 Тогда
			
			Отбор = Новый Структура("ИдентификаторСтроки", СтрокаТаблицы.ИдентификаторСтроки);
			МассивСтрокСведенияПрослеживаемости = СведенияПрослеживаемости.Выгрузить(Отбор);

			Если СтрокаТаблицы.ПрослеживаемыйКомплект И МассивСтрокСведенияПрослеживаемости.Количество() = 0 Тогда
				ТекстСообщенияРасшифрвка = НСтр("ru = 'Не заполнены данные по РНПТ'");
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка","Корректность", НСтр("ru = 'РНПТ'"),
				СтрокаТаблицы.НомерСтроки, ИмяТаблицы, ТекстСообщенияРасшифрвка);
				Поле = Префикс + "РНПТ";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			ИначеЕсли Не СтрокаТаблицы.ПрослеживаемыйКомплект И (МассивСтрокСведенияПрослеживаемости.Количество() = 0 
					ИЛИ (НЕ ЕстьРасхождения И ПрослеживаемостьБП.КоличествоРНПТКСтроке(МассивСтрокСведенияПрослеживаемости) <> СтрокаТаблицы.Количество)) Тогда
					ТекстСообщенияРасшифрвка = НСтр("ru = 'Количество РНПТ не совпадает с количеством товара в строке.'");
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка","Корректность", НСтр("ru = 'РНПТ'"),
					СтрокаТаблицы.НомерСтроки, ИмяТаблицы, ТекстСообщенияРасшифрвка);
					Поле = Префикс + "РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
			
			НужноЗаполнениеКолонкиНоменклатура    = СтрокаТаблицы.ПрослеживаемыйКомплект;
			НужноЗаполнениеКолонкиСумма           = СтрокаТаблицы.ПрослеживаемыйКомплект И Не ЭтоКомиссия;
			
			// Для прослеживаемого товара обязательно указывается страна происхождения.
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтранаПроисхождения) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Страна происхождения'"),
					СтрокаТаблицы.НомерСтроки, ИмяТаблицы);
				Поле = Префикс + "СтранаПроисхождения";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
			
			Если СтрокаТаблицы.ПрослеживаемыйКомплект И Не ЭтоКомиссия Тогда
				СуммаБезНДС = ?(СуммаВключаетНДС, СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС, СтрокаТаблицы.Сумма);
				Если СуммаБезНДС < МассивСтрокСведенияПрослеживаемости.Итог("Сумма") Тогда 
					ТекстСообщения = НСтр("ru = 'Сумма без НДС по прослеживаемым комплектующим больше суммы без НДС по товару'");
					
					Поле = "" + ИмяТаблицы + "["+ Формат(СтрокаТаблицы.НомерСтроки-1,"ЧГ=") + "].РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаТаблицы.ПрослеживаемыйТовар
			И НЕ ИностранныйКонтрагент
			И СтрокаТаблицы.Количество > 0 Тогда
			
			Для Каждого Строка Из МассивСтрокСведенияПрослеживаемости Цикл
				Если НужноЗаполнениеКолонкиНоменклатура И Не ЗначениеЗаполнено(Строка.Номенклатура) Тогда 
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Комплектующие"" в данных по РНПТ'");
					
					Поле = "" + ИмяТаблицы + "["+ Формат(СтрокаТаблицы.НомерСтроки-1,"ЧГ=") + "].РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
				
				Если ИмяТаблицы = "ОсновныеСредства" И Не ЗначениеЗаполнено(Строка.СтранаПроисхождения) Тогда
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Страна происхождения"" в данных по РНПТ'");
					
					Поле = "" + ИмяТаблицы + "["+ Формат(СтрокаТаблицы.НомерСтроки-1,"ЧГ=") + "].РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(Строка.Количество) Тогда
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Количество"" в данных по РНПТ'");
					
					Поле = "" + ИмяТаблицы + "["+ Формат(СтрокаТаблицы.НомерСтроки-1,"ЧГ=") + "].РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
				
				Если НужноЗаполнениеКолонкиСумма И Не ЗначениеЗаполнено(Строка.Сумма) Тогда
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Сумма без НДС"" в данных по РНПТ'");
					
					Поле = "" + ИмяТаблицы + "["+ Формат(СтрокаТаблицы.НомерСтроки-1,"ЧГ=") + "].РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(Строка.РНПТ) Тогда
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""РНПТ"" в данных по РНПТ'");
					
					Поле = "" + ИмяТаблицы + "["+ Формат(СтрокаТаблицы.НомерСтроки-1,"ЧГ=") + "].РНПТ";
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		Если Не СтрокаТаблицы.ПрослеживаемыйТовар Тогда
			Отбор = Новый Структура("ИдентификаторСтроки", СтрокаТаблицы.ИдентификаторСтроки);
			СтрокиСРНПТ = СведенияПрослеживаемости.НайтиСтроки(Отбор);
			Для Каждого СтрокаСРНПТ Из СтрокиСРНПТ Цикл
				СведенияПрослеживаемости.Удалить(СтрокаСРНПТ);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура УстановитьСтатусПроверкиРНПТ()
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.СтатусыПроверокРНПТДокументов.УстановитьСтатусПроверкиРНПТ(ЭтотОбъект);
	
КонецПроцедуры

//Определяет есть склад с типом розничный магазин в табличной части Товары 
//Параметры: ТаблицаТоваров Тип("ТаблицаЗначений")
//
//Возвращаемое значение: ЕстьРозничныйМагазин Тип("Булево")
//
Функция ЕстьРозничныйМагазин(ТаблицаТовары)
	
	ЕстьРозничныйСклад = Ложь;
	
	Для Каждого Строка Из ТаблицаТовары Цикл
		Если Строка.Склад.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
			ЕстьРозничныйСклад = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьРозничныйСклад;
	
КонецФункции

#КонецОбласти

#Область Инициализация

УстановленСтатусДокумента = Ложь;

#КонецОбласти

#КонецЕсли