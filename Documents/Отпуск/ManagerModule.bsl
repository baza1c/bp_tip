#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке();
КонецФункции

#Область УчетПособий

// Возникает при заполнении документа ЗаявлениеСотрудникаНаВыплатуПособия на основании текущего документа.
Процедура ЗаполнитьЗаявлениеСотрудникаНаВыплатуПособияПоОснованию(Отпуск, Заявление, РеквизитыДокумента, ДанныеДляРасчетаСреднегоЗаработка) Экспорт
	
	ИменаРеквизитов =
	"Дата,
	|Номер,
	|Организация,
	|Сотрудник,
	|НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
	|КонецПериодаЗаКоторыйПредоставляетсяОтпуск";
	
	РеквизитыОтпуска = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Отпуск, ИменаРеквизитов);
	
	РеквизитыДокумента.Вставить("Организация",                       РеквизитыОтпуска.Организация);
	РеквизитыДокумента.Вставить("Сотрудник",                         РеквизитыОтпуска.Сотрудник);
	РеквизитыДокумента.Вставить("ДатаПредставленияПакетаДокументов", РеквизитыОтпуска.Дата);
	РеквизитыДокумента.Вставить("ДатаПриказаОПредоставленииОтпускаПоУходуЗаРебенком",  РеквизитыОтпуска.Дата);
	РеквизитыДокумента.Вставить("НомерПриказаОПредоставленииОтпускаПоУходуЗаРебенком", РеквизитыОтпуска.Номер);
	РеквизитыДокумента.Вставить("ДатаНачалаОтпускаПоУходуЗаРебенком",    РеквизитыОтпуска.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск);
	РеквизитыДокумента.Вставить("ДатаОкончанияОтпускаПоУходуЗаРебенком", РеквизитыОтпуска.КонецПериодаЗаКоторыйПредоставляетсяОтпуск);
	
КонецПроцедуры

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
//  Параметры                  - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т6";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о предоставлении отпуска (Т-6)'");
	КомандаПечати.Порядок = 10;		
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т60";
	КомандаПечати.Представление = НСтр("ru = 'Записка-расчет о предоставлении отпуска (Т-60)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 20;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьРасчетаСреднегоЗаработка";
	КомандаПечати.Идентификатор = "РасчетСреднегоЗаработка";
	КомандаПечати.Представление = НСтр("ru = 'Расчет среднего заработка'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 30;
	
КонецПроцедуры

Функция ДанныеДляРегистрацииВУчетаСтажаПФР(МассивСсылок) Экспорт
	ДанныеДляРегистрацииВУчете = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Отпуск.Сотрудник,
	|	Отпуск.ДатаНачалаОсновногоОтпуска КАК ДатаНачала,
	|	Отпуск.ДатаОкончанияОсновногоОтпуска КАК ДатаОкончания,
	|	Отпуск.Ссылка
	|ИЗ
	|	Документ.Отпуск КАК Отпуск
	|ГДЕ
	|	Отпуск.Ссылка В(&МассивСсылок)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеДляРегистрацииВУчетеПоДокументу = УчетСтажаПФР.ДанныеДляРегистрацииВУчетеСтажаПФР();
		ДанныеДляРегистрацииВУчете.Вставить(Выборка.Ссылка, ДанныеДляРегистрацииВУчетеПоДокументу);
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ДЛОТПУСК;		
						
		ОписаниеПериода = УчетСтажаПФР.ОписаниеРегистрируемогоПериода();
		ОписаниеПериода.Сотрудник = Выборка.Сотрудник;	
		ОписаниеПериода.ДатаНачалаПериода = Выборка.ДатаНачала;
		ОписаниеПериода.ДатаОкончанияПериода = Выборка.ДатаОкончания;
		ОписаниеПериода.Состояние = Перечисления.СостоянияСотрудника.ОтпускОсновной;
				
		РегистрируемыйПериод = УчетСтажаПФР.ДобавитьЗаписьВДанныеДляРегистрацииВУчета(ДанныеДляРегистрацииВУчетеПоДокументу, ОписаниеПериода);
												 					
		УчетСтажаПФР.УстановитьЗначениеРегистрируемогоРесурса(РегистрируемыйПериод, "ВидСтажаПФР", ВидСтажа);		
	КонецЦикла;	

	Возврат ДанныеДляРегистрацииВУчете;
														
КонецФункции	

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПроверитьДокументыДляПечати(МассивОбъектов);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Т6") Тогда
		ПечатьТ6(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Т60") Тогда
		ПечатьТ60(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПечатьТ6(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	ДанныеСсылок = ОписаниеДокументовКадровогоУчетаОтпусков(МассивОбъектов);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	КадровыйУчет.ВывестиНаПечатьТ6(ПараметрыПечати, ДокументРезультат, ДанныеСсылок.Строки,
		УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_Т6"), , ОбъектыПечати);
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_MXL_Т6",
		НСтр("ru = 'Приказ о предоставлении отпуска (Т-6)'"),
		ДокументРезультат,,);
	
КонецПроцедуры

Процедура ПечатьТ60(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
	Иначе
		Возврат;
	КонецЕсли;
	
	ДанныеСсылок = ОписаниеДокументовКадровогоУчетаОтпусков(МассивОбъектов);
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПараметрыПечати_ЗапискаРасчетОПредоставленииОтпуска";
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВидыНДФЛ", МодульРасчетЗарплатыДляНебольшихОрганизаций.ВидыНДФЛДополненияРасчетнойБазыУдержаний());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Отпуск.Ссылка КАК Ссылка,
	|	Отпуск.ПериодРегистрации КАК ПериодРегистрации,
	|	ВЫБОР
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска)
	|			ТОГДА ИСТИНА
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке)
	|			ТОГДА ИСТИНА
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОплатаОтпуска,
	|	НачисленияОтпуска.ДатаНачала КАК ДатаНачала,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке)
	|				ТОГДА 0
	|			КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке)
	|				ТОГДА 0
	|			ИНАЧЕ НачисленияОтпуска.ОплаченоДней
	|		КОНЕЦ) КАК ОплаченоДней,
	|	СУММА(НачисленияОтпуска.Результат) КАК Результат
	|ИЗ
	|	Документ.Отпуск КАК Отпуск
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск.Начисления КАК НачисленияОтпуска
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыРасчетов
	|			ПО НачисленияОтпуска.Начисление = ВидыРасчетов.Ссылка
	|		ПО Отпуск.Ссылка = НачисленияОтпуска.Ссылка
	|ГДЕ
	|	НачисленияОтпуска.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	Отпуск.Ссылка,
	|	ВЫБОР
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска)
	|			ТОГДА ИСТИНА
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиРайонногоКоэффициентаВСреднемЗаработке)
	|			ТОГДА ИСТИНА
	|		КОГДА ВидыРасчетов.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДолиСевернойНадбавкиВСреднемЗаработке)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	НачисленияОтпуска.ДатаНачала,
	|	Отпуск.ПериодРегистрации
	|ИТОГИ
	|	МИНИМУМ(ПериодРегистрации),
	|	СУММА(Результат)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НДФЛОтпуска.Регистратор КАК Ссылка,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛОтпуска.ЗачетАвансаНДФЛ
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛОтпуска.Сумма
	|		КОНЕЦ) КАК Налог,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛОтпуска.ЗачетАвансаНДФЛ
	|				ТОГДА НДФЛОтпуска.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЗачтеноАвансовыхПлатежей
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НДФЛОтпуска
	|ГДЕ
	|	НДФЛОтпуска.Регистратор В(&МассивОбъектов)
	|	И НДФЛОтпуска.НачислениеУдержание В(&ВидыНДФЛ)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛОтпуска.Регистратор
	|ИТОГИ ПО
	|	Ссылка";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	Начисления = МассивРезультатов[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	НДФЛ       = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_Т60");
	
	ОбластьШапка                         = Макет.ПолучитьОбласть("Шапка");
	ОбластьПодвал                        = Макет.ПолучитьОбласть("Подвал");
	ОбластьРасчетСреднего                = Макет.ПолучитьОбласть("РасчетСреднего");
	ОбластьРасчетСреднегоИтого           = Макет.ПолучитьОбласть("РасчетСреднегоИтого");
	ОбластьДополнительнаяСтрокаЗаработка = Макет.ПолучитьОбласть("ДополнительнаяСтрокаЗаработка");
	ОбластьПодвалРасчета                 = Макет.ПолучитьОбласть("ПодвалРасчета");
	
	ВалютаУчета = ЗарплатаКадры.ВалютаУчетаЗаработнойПлаты();
	
	ПервыйПриказ = Истина;
	
	Менеджер                = ОбщегоНазначения.МенеджерОбъектаПоСсылке(МассивОбъектов[0]);
	ДанныеДокументов        = Обработки.ПечатьРасчетаСреднегоЗаработка.ДанныеДокументовРасчетаСреднегоЗаработка(Менеджер, МассивОбъектов, "");
	ОписаниеДокумента       = Обработки.ПечатьРасчетаСреднегоЗаработка.ОписаниеДокументаРассчитывающегоСреднийЗаработок(Менеджер, "");
	ДанныеОСреднемЗаработке = Обработки.ПечатьРасчетаСреднегоЗаработка.ТаблицыДанныхОСреднемЗаработкеДокументов(ОписаниеДокумента, МассивОбъектов);
	
	ПараметрыПолученияДанныхСреднего = МодульРасчетЗарплатыДляНебольшихОрганизаций.ПараметрыПолученияДанныхСреднегоОбщего();
	
	Для Каждого ДанныеСотрудников Из ДанныеСсылок.Строки Цикл
		
		Для Каждого ДанныеНаПечать Из ДанныеСотрудников.Строки Цикл
			
			НомерСтрокиНачалаОбласти = ДокументРезультат.ВысотаТаблицы + 1;
			
			Параметры = СтруктураПараметровЗапискиРасчетаТ60();
			
			//////////////////////////////////////////////////////////////////////
			// Первая страница
			// Основной отпуск
			
			Параметры.НазваниеОрганизации = ДанныеНаПечать.РаботаОрганизацияНаименованиеСокращенное;
			Параметры.КодПоОКПО           = ДанныеНаПечать.РаботаОрганизацияКодПоОКПО;
			Параметры.ДатаДок             = ДанныеНаПечать.СсылкаНаОбъектДатаНаПечать;
			Параметры.НомерДок            = ДанныеНаПечать.СсылкаНаОбъектНомерНаПечать;
			Параметры.Работник            = ДанныеНаПечать.ЛичныеДанныеФИОФамилияИмяОтчествоВДательномПадеже;
			Параметры.ТабельныйНомер      = ДанныеНаПечать.РаботаСотрудникТабельныйНомерНаПечать;
			Параметры.Подразделение       = ДанныеНаПечать.РаботаПодразделениеНаПечать;
			Параметры.Должность           = ДанныеНаПечать.РаботаДолжность;
			
			Параметры.БухгалтерРасшифровкаПодписи              = ДанныеНаПечать.СсылкаНаОбъектБухгалтерРасшифровкаПодписи;
			Параметры.РаботникКадровойСлужбыРасшифровкаПодписи = ДанныеНаПечать.СсылкаНаОбъектРаботникКадровойСлужбыРасшифровкаПодписи;
			Параметры.ДолжностьРаботникаКадровойСлужбы         = ДанныеНаПечать.СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы;
			
			ПредставлениеДатыНачалаОсновногоОтпуска = Формат(ДанныеНаПечать.СсылкаНаОбъектДатаНачалаОсновногоОтпуска, "ДЛФ=DD");;
			Параметры.ДатаНачалаЧисло = ?(ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектДатаНачалаОсновногоОтпуска), СокрЛП(Лев(ПредставлениеДатыНачалаОсновногоОтпуска,2)), "      ");
			Параметры.ДатаНачалаМесяцГод = ?(ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектДатаНачалаОсновногоОтпуска), СокрЛП(Прав(ПредставлениеДатыНачалаОсновногоОтпуска, СтрДлина(ПредставлениеДатыНачалаОсновногоОтпуска)-2)), "                     " + НСтр("ru='г'") + ".");
			
			ПредставлениеДатыОкончанияОсновногоОтпуска = Формат(ДанныеНаПечать.СсылкаНаОбъектДатаОкончанияОсновногоОтпуска, "ДЛФ=DD");
			Параметры.ДатаОкончанияЧисло = ?(ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектДатаОкончанияОсновногоОтпуска), СокрЛП(Лев(ПредставлениеДатыОкончанияОсновногоОтпуска,2)), "      ");
			Параметры.ДатаОкончанияМесяцГод = ?(ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектДатаОкончанияОсновногоОтпуска), СокрЛП(Прав(ПредставлениеДатыОкончанияОсновногоОтпуска, СтрДлина(ПредставлениеДатыОкончанияОсновногоОтпуска)-2)), "                     " + НСтр("ru='г'") + ".");
			
			ПредставлениеДатыНачалаПериодаОтпуска = Формат(ДанныеНаПечать.СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск, "ДЛФ=DD");
			Параметры.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = ?(ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск), ПредставлениеДатыНачалаПериодаОтпуска, "      ");
			
			ПредставлениеДатыОкончанияПериодаОтпуска  = Формат(ДанныеНаПечать.СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск, "ДЛФ=DD");
			Параметры.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = ?(ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск), ПредставлениеДатыОкончанияПериодаОтпуска, "      ");
			
			Если ЗначениеЗаполнено(ДанныеНаПечать.СсылкаНаОбъектПродолжительностьОсновногоОтпуска) Тогда
				Параметры.Продолжительность      = ДанныеНаПечать.СсылкаНаОбъектПродолжительностьОсновногоОтпуска;
				Параметры.ПродолжительностьВсего = ДанныеНаПечать.СсылкаНаОбъектПродолжительностьОсновногоОтпуска;
			КонецЕсли;
			
			ДатаНачалаВсего = ДанныеНаПечать.СсылкаНаОбъектДатаНачалаОсновногоОтпуска;
			ПредставлениеДатыНачалаВсего  = Формат(ДатаНачалаВсего, "ДЛФ=DD");
			Параметры.ДатаНачалаВсегоЧисло = ?(ЗначениеЗаполнено(ДатаНачалаВсего), СокрЛП(Лев(ПредставлениеДатыНачалаВсего,2)), "      ");
			Параметры.ДатаНачалаВсегоМесяцГод = ?(ЗначениеЗаполнено(ДатаНачалаВсего),  СокрЛП(Прав(ПредставлениеДатыНачалаВсего, СтрДлина(ПредставлениеДатыНачалаВсего)-2)), "                     " + НСтр("ru='г'") + ".");
			
			ДатаОкончанияВсего = ДанныеНаПечать.СсылкаНаОбъектДатаОкончанияОсновногоОтпуска;
			ПредставлениеДатыОкончанияВсего  = Формат(ДатаОкончанияВсего, "ДЛФ=DD");
			Параметры.ДатаОкончанияВсегоЧисло = ?(ЗначениеЗаполнено(ДатаОкончанияВсего), СокрЛП(Лев(ПредставлениеДатыОкончанияВсего,2)), "      ");
			Параметры.ДатаОкончанияВсегоМесяцГод = ?(ЗначениеЗаполнено(ДатаОкончанияВсего),  СокрЛП(Прав(ПредставлениеДатыОкончанияВсего, СтрДлина(ПредставлениеДатыОкончанияВсего)-2)), "                     " + НСтр("ru='г'") + ".");
		
			//////////////////////////////////////////////////////////////////////
			// Подвал первой страницы
			
			НомерСтрокиНачало = ДокументРезультат.ВысотаТаблицы + 1;
			Если Не ПервыйПриказ Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			Иначе
				ПервыйПриказ = Ложь;
			КонецЕсли;

			ОбластьШапка.Параметры.Заполнить(Параметры);
			
			ДокументРезультат.Вывести(ОбластьШапка);
			
			ОбластьПодвал.Параметры.Заполнить(Параметры);
			ДокументРезультат.Вывести(ОбластьПодвал);
			
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			
			//////////////////////////////////////////////////////////////////////
			// Вторая страница
			// Расчет среднего заработка
			
			ДанныеДокумента                = ДанныеДокументов.Найти(ДанныеНаПечать.СсылкаНаОбъект, "Ссылка");
			ТаблицыДанныхОСреднемЗаработке = ДанныеОСреднемЗаработке[ДанныеНаПечать.СсылкаНаОбъект];
			ТаблицыПоСотруднику            = Обработки.ПечатьРасчетаСреднегоЗаработка.ТаблицыДанныхОСреднемЗаработкеПоСотруднику(ДанныеНаПечать.РаботаСотрудник, ТаблицыДанныхОСреднемЗаработке);
			
			ПараметрыПолученияДанныхСреднего.Вставить("ТаблицыПоСотруднику",  ТаблицыПоСотруднику); 
			ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаПериода",    ДанныеДокумента.НачалоРасчетногоПериода);
			ПараметрыПолученияДанныхСреднего.Вставить("ДатаОкончанияПериода", ДанныеДокумента.ОкончаниеРасчетногоПериода); 
			ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаСобытия",    ДанныеДокумента.ДатаНачалаСобытия);
			
			ДанныеРасчетаСреднегоЗаработка = МодульРасчетЗарплатыДляНебольшихОрганизаций.ДанныеРасчетаСреднегоЗаработкаОбщего(ПараметрыПолученияДанныхСреднего);
			
			ОбщаяТаблицаДанныхОСреднем = Новый ТаблицаЗначений;
			ОбщаяТаблицаДанныхОСреднем.Колонки.Добавить("Месяц", Новый ОписаниеТипов("Дата"));
			ОбщаяТаблицаДанныхОСреднем.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
		
			Для Каждого Элемент Из ДанныеРасчетаСреднегоЗаработка["Заработок"] Цикл
				НоваяСтрока = ОбщаяТаблицаДанныхОСреднем.Добавить();
				НоваяСтрока.Месяц = Элемент["Месяц"];
				НоваяСтрока.Сумма = Элемент["Учтено"];
			КонецЦикла;
			
			ОбщаяТаблицаДанныхОСреднем.Свернуть("Месяц", "Сумма");
			ОбщаяТаблицаДанныхОСреднем.Сортировать("Месяц");
			
			МесяцСреднегоЗаработка = 1;
			Для каждого ПериодСреднего Из ОбщаяТаблицаДанныхОСреднем Цикл
				Если МесяцСреднегоЗаработка > 12 Тогда
					Прервать;
				КонецЕсли;
				СтрокаГод = "Год" + МесяцСреднегоЗаработка;
				СтрокаМесяц = "Месяц" + МесяцСреднегоЗаработка;
				СтрокаСумма = "Сумма" + МесяцСреднегоЗаработка;
				Параметры.Вставить(СтрокаГод, Формат(ПериодСреднего.Месяц, "ДФ=yyyy"));
				Параметры.Вставить(СтрокаМесяц, Формат(ПериодСреднего.Месяц, "ДФ=MMMM"));
				Параметры.Вставить(СтрокаСумма, ПериодСреднего.Сумма);
				МесяцСреднегоЗаработка = МесяцСреднегоЗаработка + 1;
			КонецЦикла;
			
			МассивДополнительныхСтрокЗаработка = Новый Массив; 
			
			МесяцСреднегоЗаработка = 0;
			Для каждого ПериодСреднего Из ОбщаяТаблицаДанныхОСреднем Цикл
				МесяцСреднегоЗаработка = МесяцСреднегоЗаработка + 1;
				Если МесяцСреднегоЗаработка < 13 Тогда
					Продолжить;
				КонецЕсли;
				МассивДополнительныхСтрокЗаработка.Добавить(Новый Структура("Год,Месяц,Сумма", Формат(ПериодСреднего.Месяц, "ДФ=yyyy"), Формат(ПериодСреднего.Месяц, "ДФ=MMMM"), ПериодСреднего.Сумма));
			КонецЦикла;
			
			ВсегоВремени   = МодульРасчетЗарплатыДляНебольшихОрганизаций.ИтогиПоПолю(ДанныеРасчетаСреднегоЗаработка["ОтработанноеВремя"], "Учтено");
			ВсегоЗаработок = МодульРасчетЗарплатыДляНебольшихОрганизаций.ИтогиПоПолю(ДанныеРасчетаСреднегоЗаработка["Заработок"], "Учтено");
			
			Параметры.Вставить("ВсегоДнейРасчетногоПериода", ВсегоВремени);
			Параметры.Вставить("ВсегоУчтено",                ВсегоЗаработок);
			Параметры.Вставить("ЗаработокЗаЕдиницуВремени",  ?(ВсегоВремени = 0, 0, Окр(ВсегоЗаработок / ВсегоВремени, 2)));
			
			ОбластьРасчетСреднего.Параметры.Заполнить(Параметры);
			ДокументРезультат.Вывести(ОбластьРасчетСреднего);
			
			Для каждого ДополнительнаяСтрокаЗаработка Из МассивДополнительныхСтрокЗаработка Цикл
				ОбластьДополнительнаяСтрокаЗаработка.Параметры.Заполнить(ДополнительнаяСтрокаЗаработка);
				ДокументРезультат.Вывести(ОбластьДополнительнаяСтрокаЗаработка);
			КонецЦикла;	
			
			ОбластьРасчетСреднегоИтого.Параметры.Заполнить(Параметры);
			
			ДокументРезультат.Вывести(ОбластьРасчетСреднегоИтого);
			
			//////////////////////////////////////////////////////////////////////
			// Начисления
			
			Начисления.Сбросить();
			
			СтрТекущийМесяц = "";
			СтрБудущиеМесяцы = "";
			ДнейКОплатеЗаТекущийМесяц = 0;
			НачисленоОтпускныхЗаТекущийМесяц = 0;
			ДругиеНачисленияЗаТекущийМесяц = 0;
			ДнейКОплатеЗаБудущиеМесяцы = 0;
			НачисленоОтпускныхЗаБудущиеМесяцы = 0;
			ДругиеНачисленияЗаБудущиеМесяцы = 0;
			ВсегоНачислено = 0;
			
			БудущиеМесяцы = Новый СписокЗначений; 
			
			Если Начисления.НайтиСледующий(Новый Структура("Ссылка", ДанныеНаПечать.СсылкаНаОбъект)) Тогда
				
				СтрТекущийМесяц = Формат(НачалоМесяца(Начисления.ПериодРегистрации), "ДФ=MMMM");
				ВсегоНачислено = Начисления.Результат;
				ВыборкаНачислений = Начисления.Выбрать();
				Пока ВыборкаНачислений.Следующий() Цикл
					Если НачалоМесяца(ВыборкаНачислений.ПериодРегистрации) >= НачалоМесяца(ВыборкаНачислений.ДатаНачала) Тогда
						Если ВыборкаНачислений.ОплатаОтпуска Тогда
							ДнейКОплатеЗаТекущийМесяц = ДнейКОплатеЗаТекущийМесяц + ВыборкаНачислений.ОплаченоДней;
							НачисленоОтпускныхЗаТекущийМесяц = НачисленоОтпускныхЗаТекущийМесяц + ВыборкаНачислений.Результат;
						Иначе
							ДругиеНачисленияЗаТекущийМесяц = ДругиеНачисленияЗаТекущийМесяц + ВыборкаНачислений.Результат;
						КонецЕсли;
					Иначе 
						БудущиеМесяцы.Добавить(НачалоМесяца(ВыборкаНачислений.ДатаНачала));
						Если ВыборкаНачислений.ОплатаОтпуска Тогда
							ДнейКОплатеЗаБудущиеМесяцы = ДнейКОплатеЗаБудущиеМесяцы + ВыборкаНачислений.ОплаченоДней;
							НачисленоОтпускныхЗаБудущиеМесяцы = НачисленоОтпускныхЗаБудущиеМесяцы + ВыборкаНачислений.Результат;
						Иначе
							ДругиеНачисленияЗаБудущиеМесяцы = ДругиеНачисленияЗаБудущиеМесяцы + ВыборкаНачислений.Результат;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла; 
				
				БудущиеМесяцы.СортироватьПоЗначению();
				Для каждого БудущийМесяц Из БудущиеМесяцы Цикл
					СтрБудущиеМесяцы = СтрБудущиеМесяцы + Формат(БудущийМесяц.Значение, "ДФ=MMMM") + ", ";
				КонецЦикла;
				
				СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СтрБудущиеМесяцы, 2);
				
				Параметры.Вставить("СтрТекущийМесяц",                   СтрТекущийМесяц);
				Параметры.Вставить("СтрБудущиеМесяцы",                  СтрБудущиеМесяцы);
				Параметры.Вставить("ДнейКОплатеЗаТекущийМесяц",         ДнейКОплатеЗаТекущийМесяц);
				Параметры.Вставить("НачисленоОтпускныхЗаТекущийМесяц",  НачисленоОтпускныхЗаТекущийМесяц);
				Параметры.Вставить("ДругиеНачисленияЗаТекущийМесяц",    ДругиеНачисленияЗаТекущийМесяц);
				Параметры.Вставить("ДнейКОплатеЗаБудущиеМесяцы",        ДнейКОплатеЗаБудущиеМесяцы);
				Параметры.Вставить("НачисленоОтпускныхЗаБудущиеМесяцы", НачисленоОтпускныхЗаБудущиеМесяцы);
				Параметры.Вставить("ДругиеНачисленияЗаБудущиеМесяцы",   ДругиеНачисленияЗаБудущиеМесяцы);
				Параметры.Вставить("ВсегоНачислено",                    ВсегоНачислено);
				
			КонецЕсли; 
			
			ВсегоУдержано = 0;
			
			//////////////////////////////////////////////////////////////////////
			// НДФЛ
			
			НДФЛ.Сбросить();
			СуммаНДФЛ = 0;
			Если НДФЛ.НайтиСледующий(Новый Структура("Ссылка", ДанныеНаПечать.СсылкаНаОбъект)) Тогда 
				СуммаНДФЛ = НДФЛ.Налог + НДФЛ.ЗачтеноАвансовыхПлатежей;
				Параметры.Вставить("СуммаНДФЛ", СуммаНДФЛ);
			КонецЕсли;
			
			ВсегоУдержано = ВсегоУдержано + СуммаНДФЛ;
			Параметры.Вставить("ВсегоУдержано", ВсегоУдержано);
			
			КВыплате = ?(ВсегоНачислено - ВсегоУдержано > 0, ВсегоНачислено - ВсегоУдержано, 0);
			Параметры.Вставить("КВыплате", КВыплате);
			
			Параметры.Вставить("КВыплатеПрописью", РаботаСКурсамиВалют.СформироватьСуммуПрописью(КВыплате, ВалютаУчета));
			Параметры.Вставить("КВыплатеРубли",    Формат(Цел(КВыплате),"ЧДЦ=0; ЧН=-"));
			Параметры.Вставить("КВыплатеКопейки",  Формат((КВыплате - Цел(КВыплате)) * 100,"ЧЦ=2; ЧДЦ=0; ЧН=-"));
		
			//////////////////////////////////////////////////////////////////////
			// Подвал второй страницы
			
			ОбластьПодвалРасчета.Параметры.Заполнить(Параметры);
			ДокументРезультат.Вывести(ОбластьПодвалРасчета);
			
			ЭДОБЗК.ЗадатьДетальнуюОбластьПечати(ПараметрыПечати, ДокументРезультат, "ПФ_MXL_Т60", НомерСтрокиНачалаОбласти,
				ДанныеНаПечать, ДанныеНаПечать.СсылкаНаОбъект);
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НомерСтрокиНачало, ОбъектыПечати, ДанныеНаПечать.СсылкаНаОбъект);
			
		КонецЦикла;
		
	КонецЦикла;
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ПФ_MXL_Т60",
		НСтр("ru = 'Записка-расчет о предоставленииотпуска (Т-60)'"),
		ДокументРезультат,,);
	
КонецПроцедуры

Функция ОписаниеДокументовКадровогоУчетаОтпусков(МассивОбъектов);
	
	ДанныеСсылок = Новый ДеревоЗначений;
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъект");
	ДанныеСсылок.Колонки.Добавить("РаботаОрганизацияНаименованиеСокращенное");
	ДанныеСсылок.Колонки.Добавить("РаботаОрганизацияКодПоОКПО");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДатаНаПечать");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектНомерНаПечать");
	ДанныеСсылок.Колонки.Добавить("ЛичныеДанныеФИОФамилияИмяОтчествоВДательномПадеже");
	ДанныеСсылок.Колонки.Добавить("РаботаСотрудникТабельныйНомерНаПечать");
	ДанныеСсылок.Колонки.Добавить("РаботаПодразделениеНаПечать");
	ДанныеСсылок.Колонки.Добавить("РаботаДолжность");
	ДанныеСсылок.Колонки.Добавить("РаботаСотрудник");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпускНаПечать");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпускНаПечать");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДатаНачалаОсновногоОтпуска");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДатаНачалаОсновногоОтпускаНаПечать");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДатаОкончанияОсновногоОтпуска");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДатаОкончанияОсновногоОтпускаНаПечать");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДанныеОтпусковДатаНачала");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДанныеОтпусковДатаОкончания");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектПродолжительностьОсновногоОтпуска");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектПродолжительностьДополнительныхОтпусков");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДанныеОтпусковВидОтпускаОсновнойОтпуск");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектОснование");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДолжностьРуководителя");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектРуководительРасшифровкаПодписи");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДатаОзнакомленияРаботника");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДолжностьБухгалтера");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектБухгалтерРасшифровкаПодписи");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы");
	ДанныеСсылок.Колонки.Добавить("СсылкаНаОбъектРаботникКадровойСлужбыРасшифровкаПодписи");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПорядокЗаполнения = Новый Структура("ЗаполнятьРуководителя");
	КадровыйУчетПереопределяемый.СоздатьВТОтветственныхЛиц(Запрос.МенеджерВременныхТаблиц,
															МассивОбъектов,
															ПорядокЗаполнения);
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Отпуск.Сотрудник КАК Сотрудник,
		|	Отпуск.ДатаНачалаСобытия КАК Период
		|ПОМЕСТИТЬ ВТСотрудникиПериоды
		|ИЗ
		|	Документ.Отпуск КАК Отпуск
		|ГДЕ
		|	Отпуск.Ссылка В(&МассивОбъектов)";
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиПериоды");
	
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "ФизическоеЛицо,ФИОПолные,Пол,ТабельныйНомер,Организация,Подразделение,Должность");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадровыеДанныеСотрудников.Период КАК Период,
		|	КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
		|	КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Организация КАК Справочник.Организации) КАК Организация,
		|	КадровыеДанныеСотрудников.ФИОПолные КАК ФИОПолные,
		|	КадровыеДанныеСотрудников.Пол КАК Пол,
		|	КадровыеДанныеСотрудников.ТабельныйНомер КАК ТабельныйНомер,
		|	ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Подразделение КАК Справочник.ПодразделенияОрганизаций) КАК Подразделение,
		|	ВЫРАЗИТЬ(КадровыеДанныеСотрудников.Должность КАК Справочник.Должности) КАК Должность
		|ПОМЕСТИТЬ ВТДанныеДляПечати
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляПечати.Сотрудник КАК РаботаСотрудник,
		|	Отпуск.Организация КАК Организация,
		|	ДанныеДляПечати.Организация.НаименованиеСокращенное КАК РаботаОрганизацияНаименованиеСокращенное,
		|	ДанныеДляПечати.Организация.КодПоОКПО КАК РаботаОрганизацияКодПоОКПО,
		|	ДанныеДляПечати.ФИОПолные КАК ЛичныеДанныеФИОФамилияИмяОтчество,
		|	ДанныеДляПечати.Пол КАК ЛичныеДанныеПол,
		|	Отпуск.Номер КАК СсылкаНаОбъектНомер,
		|	Отпуск.Дата КАК СсылкаНаОбъектДата,
		|	ДанныеДляПечати.ТабельныйНомер КАК РаботаСотрудникКод,
		|	ЕСТЬNULL(ДанныеДляПечати.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК РаботаПодразделение,
		|	ЕСТЬNULL(ДанныеДляПечати.Должность.Наименование, """") КАК РаботаДолжность,
		|	Отпуск.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск КАК СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	Отпуск.КонецПериодаЗаКоторыйПредоставляетсяОтпуск КАК СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	Отпуск.ДатаНачалаОсновногоОтпуска КАК СсылкаНаОбъектДатаНачалаОсновногоОтпуска,
		|	Отпуск.ДатаОкончанияОсновногоОтпуска КАК СсылкаНаОбъектДатаОкончанияОсновногоОтпуска,
		|	Отпуск.КоличествоДнейОсновногоОтпуска КАК СсылкаНаОбъектПродолжительностьОсновногоОтпуска,
		|	ИСТИНА КАК СсылкаНаОбъектДанныеОтпусковВидОтпускаОсновнойОтпуск,
		|	Отпуск.Ссылка КАК СсылкаНаОбъект,
		|	Отпуск.Ссылка КАК Ссылка,
		|	Отпуск.ДатаНачалаСобытия КАК ДатаНачалаСобытия,
		|	Отпуск.Дата КАК Дата,
		|	Отпуск.Руководитель КАК СсылкаНаОбъектРуководитель,
		|	Отпуск.ДолжностьРуководителя КАК СсылкаНаОбъектДолжностьРуководителя,
		|	Отпуск.Бухгалтер КАК СсылкаНаОбъектБухгалтер,
		|	Отпуск.ДолжностьБухгалтера КАК СсылкаНаОбъектДолжностьБухгалтера,
		|	Отпуск.РаботникКадровойСлужбы КАК СсылкаНаОбъектРаботникКадровойСлужбы,
		|	Отпуск.ДолжностьРаботникаКадровойСлужбы КАК СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы,
		|	Отпуск.Основание КАК СсылкаНаОбъектОснование
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	ВТДанныеДляПечати КАК ДанныеДляПечати
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск КАК Отпуск
		|		ПО ДанныеДляПечати.Сотрудник = Отпуск.Сотрудник
		|			И ДанныеДляПечати.Период = Отпуск.ДатаНачалаСобытия
		|ГДЕ
		|	Отпуск.Ссылка В(&МассивОбъектов)";
	
	Запрос.Выполнить();
	
	ИменаПолей = Новый Массив;
	ИменаПолей.Добавить("СсылкаНаОбъектРуководитель");
	ИменаПолей.Добавить("СсылкаНаОбъектБухгалтер");
	ИменаПолей.Добавить("СсылкаНаОбъектРаботникКадровойСлужбы");
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, ИменаПолей, "ВТДанныеДокументов");
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеДокументов.РаботаСотрудник КАК РаботаСотрудник,
		|	ДанныеДокументов.РаботаОрганизацияНаименованиеСокращенное КАК РаботаОрганизацияНаименованиеСокращенное,
		|	ДанныеДокументов.РаботаОрганизацияКодПоОКПО КАК РаботаОрганизацияКодПоОКПО,
		|	ДанныеДокументов.ЛичныеДанныеФИОФамилияИмяОтчество КАК ЛичныеДанныеФИОПолные,
		|	ДанныеДокументов.ЛичныеДанныеПол КАК ЛичныеДанныеПол,
		|	ДанныеДокументов.СсылкаНаОбъектНомер КАК СсылкаНаОбъектНомер,
		|	ДанныеДокументов.СсылкаНаОбъектДата КАК СсылкаНаОбъектДата,
		|	ДанныеДокументов.РаботаСотрудникКод КАК РаботаСотрудникКод,
		|	ДанныеДокументов.РаботаПодразделение КАК РаботаПодразделение,
		|	ДанныеДокументов.РаботаДолжность КАК РаботаДолжность,
		|	ДанныеДокументов.СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск КАК СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	ДанныеДокументов.СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск КАК СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск,
		|	ДанныеДокументов.СсылкаНаОбъектДатаНачалаОсновногоОтпуска КАК СсылкаНаОбъектДатаНачалаОсновногоОтпуска,
		|	ДанныеДокументов.СсылкаНаОбъектДатаОкончанияОсновногоОтпуска КАК СсылкаНаОбъектДатаОкончанияОсновногоОтпуска,
		|	ДанныеДокументов.СсылкаНаОбъектПродолжительностьОсновногоОтпуска КАК СсылкаНаОбъектПродолжительностьОсновногоОтпуска,
		|	ДанныеДокументов.СсылкаНаОбъектДанныеОтпусковВидОтпускаОсновнойОтпуск КАК СсылкаНаОбъектДанныеОтпусковВидОтпускаОсновнойОтпуск,
		|	ДанныеДокументов.СсылкаНаОбъект КАК СсылкаНаОбъект,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДанныеДокументов.СсылкаНаОбъектДолжностьРуководителя, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ВТОтветственныеЛица.ДолжностьРуководителя, ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка))
		|		ИНАЧЕ ДанныеДокументов.СсылкаНаОбъектДолжностьРуководителя
		|	КОНЕЦ КАК СсылкаНаОбъектДолжностьРуководителя,
		|	ДанныеДокументов.СсылкаНаОбъектОснование КАК СсылкаНаОбъектОснование,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ФИОРуководителя.ФизическоеЛицо, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ВТОтветственныеЛица.Руководитель, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
		|		ИНАЧЕ ФИОРуководителя.РасшифровкаПодписи
		|	КОНЕЦ КАК СсылкаНаОбъектРуководительРасшифровкаПодписи,
		|	ДанныеДокументов.СсылкаНаОбъектДолжностьБухгалтера КАК СсылкаНаОбъектДолжностьБухгалтера,
		|	ФИОБухгалтера.РасшифровкаПодписи КАК СсылкаНаОбъектБухгалтерРасшифровкаПодписи,
		|	ДанныеДокументов.СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы КАК СсылкаНаОбъектДолжностьРаботникаКадровойСлужбы,
		|	ФИОРаботникаКадровойСлужбы.РасшифровкаПодписи КАК СсылкаНаОбъектРаботникКадровойСлужбыРасшифровкаПодписи
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОРуководителя
		|		ПО ДанныеДокументов.Дата = ФИОРуководителя.Дата
		|			И ДанныеДокументов.Ссылка = ФИОРуководителя.Ссылка
		|			И ДанныеДокументов.СсылкаНаОбъектРуководитель = ФИОРуководителя.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтветственныеЛица КАК ВТОтветственныеЛица
		|		ПО ДанныеДокументов.Ссылка = ВТОтветственныеЛица.Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОБухгалтера
		|		ПО ДанныеДокументов.Дата = ФИОБухгалтера.Дата
		|			И ДанныеДокументов.Ссылка = ФИОБухгалтера.Ссылка
		|			И ДанныеДокументов.СсылкаНаОбъектБухгалтер = ФИОБухгалтера.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОРаботникаКадровойСлужбы
		|		ПО ДанныеДокументов.Дата = ФИОРаботникаКадровойСлужбы.Дата
		|			И ДанныеДокументов.Ссылка = ФИОРаботникаКадровойСлужбы.Ссылка
		|			И ДанныеДокументов.СсылкаНаОбъектРаботникКадровойСлужбы = ФИОРаботникаКадровойСлужбы.ФизическоеЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	СсылкаНаОбъект";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("СсылкаНаОбъект") Цикл
		
		СтрокаСсылки = ДанныеСсылок.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаСсылки, Выборка);
		
		СтрокаСсылки.СсылкаНаОбъектДатаНаПечать = Формат(Выборка.СсылкаНаОбъектДата, "ДЛФ=D");
		СтрокаСсылки.СсылкаНаОбъектНомерНаПечать = ЗарплатаКадрыОтчеты.НомерНаПечать(Выборка.СсылкаНаОбъектНомер);
		
		СтрокаСсылки.ЛичныеДанныеФИОФамилияИмяОтчествоВДательномПадеже =
			ЗарплатаКадрыОтчеты.ПросклоненныеФИО(Выборка.ЛичныеДанныеФИОПолные, 3, Выборка.ЛичныеДанныеПол);
		
		СтрокаСсылки.РаботаСотрудникТабельныйНомерНаПечать = ЗарплатаКадрыОтчеты.ТабельныйНомерНаПечать(Выборка.РаботаСотрудникКод);
		СтрокаСсылки.РаботаПодразделениеНаПечать = ЗарплатаКадрыОтчеты.ПодразделениеНаПечать(Выборка.РаботаПодразделение);
		
		СтрокаСсылки.СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпускНаПечать = ЗарплатаКадрыОтчеты.ФорматДатыЧислоВКавычкахМесяцПрописью(Выборка.СсылкаНаОбъектНачалоПериодаЗаКоторыйПредоставляетсяОтпуск, Ложь);
		СтрокаСсылки.СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпускНаПечать = ЗарплатаКадрыОтчеты.ФорматДатыЧислоВКавычкахМесяцПрописью(Выборка.СсылкаНаОбъектКонецПериодаЗаКоторыйПредоставляетсяОтпуск, Ложь);
		
		СтрокаСсылки.СсылкаНаОбъектДатаНачалаОсновногоОтпускаНаПечать = ЗарплатаКадрыОтчеты.ФорматДатыЧислоВКавычкахМесяцПрописью(Выборка.СсылкаНаОбъектДатаНачалаОсновногоОтпуска);
		СтрокаСсылки.СсылкаНаОбъектДатаОкончанияОсновногоОтпускаНаПечать = ЗарплатаКадрыОтчеты.ФорматДатыЧислоВКавычкахМесяцПрописью(Выборка.СсылкаНаОбъектДатаОкончанияОсновногоОтпуска);
	
		СтрокаСсылки.СсылкаНаОбъектДатаОзнакомленияРаботника = ЗарплатаКадрыОтчеты.ФорматДатыЧислоВКавычкахМесяцПрописью(Выборка.СсылкаНаОбъектДата);
		
		Пока Выборка.Следующий() Цикл
			
			СтрокаСотрудника = СтрокаСсылки.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСотрудника, СтрокаСсылки);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеСсылок;
	
КонецФункции

Функция СтруктураПараметровЗапискиРасчетаТ60()
	
	Параметры = КадровыйУчет.ПараметрыКадровогоПриказа();
	
	Параметры.Вставить("НазваниеОрганизации");
	Параметры.Вставить("КодПоОКПО");
	Параметры.Вставить("Работник");
	Параметры.Вставить("НомерДок");
	Параметры.Вставить("ДатаДок");
	Параметры.Вставить("ТабельныйНомер");
	Параметры.Вставить("Подразделение");
	Параметры.Вставить("Должность");
	Параметры.Вставить("БухгалтерРасшифровкаПодписи");
	Параметры.Вставить("РаботникКадровойСлужбыРасшифровкаПодписи");
	Параметры.Вставить("ДолжностьРаботникаКадровойСлужбы");
	Параметры.Вставить("НачалоПериодаЗаКоторыйПредоставляетсяОтпуск");
	Параметры.Вставить("КонецПериодаЗаКоторыйПредоставляетсяОтпуск");
	Параметры.Вставить("Продолжительность");
	Параметры.Вставить("ДатаНачалаЧисло");
	Параметры.Вставить("ДатаНачалаМесяцГод");
	Параметры.Вставить("ДатаОкончанияЧисло");
	Параметры.Вставить("ДатаОкончанияМесяцГод");
	Параметры.Вставить("ПродолжительностьВсего");
	Параметры.Вставить("ДатаНачалаВсегоЧисло");
	Параметры.Вставить("ДатаНачалаВсегоМесяцГод");
	Параметры.Вставить("ДатаОкончанияВсегоЧисло");
	Параметры.Вставить("ДатаОкончанияВсегоМесяцГод");
	
	// Средний заработок
	Параметры.Вставить("ВсегоДнейРасчетногоПериода"); 
	Параметры.Вставить("ЗаработокЗаЕдиницуВремени"); 
	Параметры.Вставить("ВсегоУчтено");
	
	Сч  = 1;
	Для Сч = 1 По 12 Цикл
		СтрокаГод = "Год" + Сч;
		СтрокаМесяц = "Месяц" + Сч;
		СтрокаСумма = "Сумма" + Сч;
		Параметры.Вставить(СтрокаГод);
		Параметры.Вставить(СтрокаМесяц);
		Параметры.Вставить(СтрокаСумма);
	КонецЦикла;	
	
	Параметры.Вставить("СтрТекущийМесяц");
	Параметры.Вставить("СтрБудущиеМесяцы");
	Параметры.Вставить("ДнейКОплатеЗаТекущийМесяц");
	Параметры.Вставить("НачисленоОтпускныхЗаТекущийМесяц");
	Параметры.Вставить("ДругиеНачисленияЗаТекущийМесяц");
	Параметры.Вставить("ДнейКОплатеЗаБудущиеМесяцы");
	Параметры.Вставить("НачисленоОтпускныхЗаБудущиеМесяцы");
	Параметры.Вставить("ДругиеНачисленияЗаБудущиеМесяцы");
	Параметры.Вставить("ВсегоНачислено");
	Параметры.Вставить("СуммаНДФЛ");
	
	Параметры.Вставить("КВыплате");
	Параметры.Вставить("КВыплатеПрописью");
	Параметры.Вставить("КВыплатеРубли");
	Параметры.Вставить("КВыплатеКопейки");

	Возврат Параметры;
	
КонецФункции 

#Область ПечатьРасчетаСреднегоЗаработка

// Заполняет структуру - описание документа для формирования печатной формы расчета среднего заработка.
//
// Параметры:
//   ОписаниеДокумента - структура, определяется в
//                       Обработки.ПечатьРасчетаСреднегоЗаработка.ОписаниеДокументаРассчитывающегоСреднийЗаработок.
//
Процедура ЗаполнитьОписаниеДокументаРассчитывающегоСреднийЗаработок(ОписаниеДокумента, ИмяМакета) Экспорт
	МетаданныеДокумента = ПустаяСсылка().Метаданные();
	ОписаниеДокумента.Вставить("ИмяДокумента", МетаданныеДокумента.Имя);
	ОписаниеДокумента.Вставить("СинонимДокумента", МетаданныеДокумента.Синоним);
КонецПроцедуры 

// Заполняет таблицу значений - параметры формирования печатной формы расчета среднего заработка.
//
// Параметры:
//	 МассивСсылок 		- массив, печатаемые документы.
//   ДанныеДокумента 	- таблица значений, определяется в
//                      Обработки.ПечатьРасчетаСреднегоЗаработка.ДанныеДокументовРасчетаСреднегоЗаработка.
//   ИмяМакета          - строка, вариант печати расчета среднего заработка.
//
Процедура ЗаполнитьДанныеДокументовДляПечатиРасчетаСреднегоЗаработка(МассивСсылок, ДанныеДокументов, ИмяМакета) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Документ.Сотрудник,
	|	Документ.ДатаНачалаОсновногоОтпуска КАК ДатаНачалаСобытия,
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Организация,
	|	Документ.Дата КАК ДатаДокумента,
	|	Документ.Номер КАК НомерДокумента,
	|	Документ.ПериодРегистрации,
	|	Документ.ДатаНачалаОсновногоОтпуска КАК ДатаНачалаОтсутствия,
	|	Документ.ДатаОкончанияОсновногоОтпуска КАК ДатаОкончанияОтсутствия,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Документ.ДатаНачалаОсновногоОтпуска, МЕСЯЦ), МЕСЯЦ, -12) КАК НачалоРасчетногоПериода,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Документ.ДатаНачалаОсновногоОтпуска, МЕСЯЦ), СЕКУНДА, -1) КАК ОкончаниеРасчетногоПериода,
	|	Документ.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
	|	Документ.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
	|	Документ.КоличествоДнейОсновногоОтпуска КАК ДнейОсновногоОтпуска
	|ИЗ
	|	Документ.Отпуск КАК Документ
	|ГДЕ
	|	Документ.Ссылка В(&МассивСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	Документ.Ссылка,
	|	Документ.Сотрудник,
	|	Документ.ДатаНачалаОсновногоОтпуска,
	|	Документ.Организация,
	|	Документ.Дата,
	|	Документ.Номер,
	|	Документ.ПериодРегистрации,
	|	Документ.ДатаОкончанияОсновногоОтпуска,
	|	Документ.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск,
	|	Документ.КонецПериодаЗаКоторыйПредоставляетсяОтпуск,
	|	Документ.КоличествоДнейОсновногоОтпуска,
	|	Документ.ДатаНачалаОсновногоОтпуска
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		ДанныеДокумента = ДанныеДокументов.Добавить(); 
		
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка);
		ДанныеДокумента.ДнейОтпускаВсего 	= Выборка.ДнейОсновногоОтпуска;

		ОписанияСотрудников = Новый Соответствие;
		МассивСотрудников = Новый Массив;
		Пока Выборка.Следующий() Цикл
			МассивСотрудников.Добавить(Выборка.Сотрудник);
			ОписанияСотрудников.Вставить(Выборка.Сотрудник, Новый Структура("ИспользоватьСреднечасовойЗаработок", Ложь));
		КонецЦикла;
		ДанныеДокумента.МассивСотрудников 	= МассивСотрудников;
		ДанныеДокумента.ОписанияСотрудников = ОписанияСотрудников;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента) Экспорт
	
	ЗарплатаКадры.ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента, "ДатаНачалаСобытия");
	
КонецПроцедуры

Процедура ЗаполнитьДатыЗапрета(ПараметрыОбновления) Экспорт
	
	ОбновлениеВыполнено = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 100
		|	Отпуск.Ссылка КАК Ссылка,
		|	Отпуск.Дата КАК Дата
		|ИЗ
		|	Документ.Отпуск КАК Отпуск
		|ГДЕ
		|	Отпуск.ДатаЗапрета = ДАТАВРЕМЯ(1, 1, 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Отпуск.Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ОбновлениеВыполнено = Ложь;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
				ПараметрыОбновления, Выборка.Ссылка.Метаданные().ПолноеИмя(), "Ссылка", Выборка.Ссылка) Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
			
			МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Выборка.Ссылка);
			МенеджерДокумента.ЗаполнитьДатуЗапретаРедактирования(ОбъектДокумента);
			
			ОбъектДокумента.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектДокумента);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбновлениеВыполнено);
	
КонецПроцедуры

Функция ПланируемаяДатыВыплатыОтпуска(ДатаНачалаСобытия, Организация) Экспорт
	
	ПланируемаяДатыВыплатыОтпуска = Неопределено;
	
	Если ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда
		
		ДатаТриДняДоОтпуска = ДатаНачалаСобытия - (86400 * 4);
		Даты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДатаТриДняДоОтпуска);
		КалендарьОрганизации = РасчетЗарплатыДляНебольшихОрганизацийПовтИсп.КалендарьОрганизации(Организация);
		
		ДатыРабочихДней = КалендарныеГрафики.ДатыБлижайшихРабочихДней(КалендарьОрганизации, Даты, Истина, Ложь);
		
		Если ДатыРабочихДней <> Неопределено Тогда
			ПланируемаяДатыВыплатыОтпуска = ДатыРабочихДней.Получить(ДатаТриДняДоОтпуска);
		КонецЕсли;
		
	КонецЕсли;
	
	ПланируемаяДатыВыплатыОтпуска = УчетЗарплаты.БлижайшийРабочийДеньДляВыплаты(Организация, ПланируемаяДатыВыплатыОтпуска);
	
	Возврат ПланируемаяДатыВыплатыОтпуска;
	
КонецФункции

Процедура ПроверитьДокументыДляПечати(МассивОбъектов)
	
	МассивПроверенныхДокументов = Новый Массив;
	Для Каждого ПроверяемыйДокумент ИЗ МассивОбъектов Цикл
		ДокументОбъект = ПроверяемыйДокумент.ПолучитьОбъект();
		Если ДокументОбъект.ПроверитьЗаполнение() Тогда
			МассивПроверенныхДокументов.Добавить(ПроверяемыйДокумент);
		КонецЕсли;
	КонецЦикла;
	
	МассивОбъектов = МассивПроверенныхДокументов;
	
КонецПроцедуры

#Область РасчетДокумента

// Функция формирует набор параметров для передачи во вспомогательные формы,
// помещает их во временное хранилище и возвращает адрес этого хранилища.
//
// Параметры:
//	Объект                  - ДокументОбъект.Отпуск - исходый документ.
//	ДополнительныеПараметры - Структура - дополнительные параметры из формы документа.
//
// Возвращаемое значение:
//  Строка - адрес во временном хранилище.
//
Функция АдресПараметровВХранилище(Объект, ДополнительныеПараметры) Экспорт
	
	ПараметрыРасчетаСреднегоЗаработка = Новый Структура;
	ПараметрыРасчетаСреднегоЗаработка.Вставить("СериализованныйОбъект", ЗарплатаКадры.СериализоватьОбъектВДвоичныеДанные(Объект));
	
	ПараметрыРасчетаСреднегоЗаработка.Вставить("Подразделение",         ДополнительныеПараметры.Подразделение);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("НачислениеОтпуск",      ДополнительныеПараметры.НачислениеОтпуск);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("НачислениеОтпускРК",    ДополнительныеПараметры.НачислениеОтпускРК);
	ПараметрыРасчетаСреднегоЗаработка.Вставить("НачислениеОтпускСН",    ДополнительныеПараметры.НачислениеОтпускСН);
	
	Возврат ПоместитьВоВременноеХранилище(ПараметрыРасчетаСреднегоЗаработка, ДополнительныеПараметры.УникальныйИдентификатор);
	
КонецФункции

// Производит перерасчет начисления.
// 
// Параметры:
//  Объект                  - ДокументОбъект.Отпуск - исходый документ.
//  ДополнительныеПараметры - Структура - дополнительные параметры из формы документа.
//
Процедура РассчитатьНачисления(Объект, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		
		НачислениеОтпуск   = ДополнительныеПараметры.НачислениеОтпуск;
		НачислениеОтпускРК = ДополнительныеПараметры.НачислениеОтпускРК;
		НачислениеОтпускСН = ДополнительныеПараметры.НачислениеОтпускСН;
		Подразделение      = ДополнительныеПараметры.Подразделение;
		
		Объект.Начисления.Очистить();
		
		ДатаНачала = Объект.ДатаНачалаОсновногоОтпуска;
		ДатаОкончания = Объект.ДатаОкончанияОсновногоОтпуска;
		
		Пока ЗначениеЗаполнено(ДатаНачала) И ДатаНачала <= ДатаОкончания Цикл
			
			СтрокаНачислений = Объект.Начисления.Добавить();
			
			СтрокаНачислений.Начисление = НачислениеОтпуск;
			СтрокаНачислений.Сотрудник = Объект.Сотрудник;
			СтрокаНачислений.Подразделение = Подразделение;
			
			СтрокаНачислений.ДатаНачала = ДатаНачала;
			
			Если КонецМесяца(ДатаНачала) >= ДатаОкончания Тогда
				СтрокаНачислений.ДатаОкончания = ДатаОкончания;
			Иначе
				СтрокаНачислений.ДатаОкончания = КонецМесяца(ДатаНачала);
			КонецЕсли;
			
			СтрокаНачислений.ОплаченоДней = МодульРасчетЗарплатыДляНебольшихОрганизаций.КоличествоДнейОтпуска(
				СтрокаНачислений.ДатаНачала, СтрокаНачислений.ДатаОкончания, Объект.Организация);
				
			ДатаЗаконаПроРКиЕАЭС = УчетНДФЛ.ДатаЗаконаПроРКиЕАЭС();
			
			СреднийЗаработокСН = 0;
			СреднийЗаработокРК = 0;
			Если Объект.ПланируемаяДатаВыплаты >= ДатаЗаконаПроРКиЕАЭС Тогда
				Если ЗначениеЗаполнено(НачислениеОтпускСН) Тогда
					СреднийЗаработокСН = Объект.СреднийЗаработокСН;
				КонецЕсли;
				Если ЗначениеЗаполнено(НачислениеОтпускРК) Тогда
					СреднийЗаработокРК = Объект.СреднийЗаработокРК;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаНачислений.Результат = (Объект.СреднийЗаработок - СреднийЗаработокРК - СреднийЗаработокСН) * СтрокаНачислений.ОплаченоДней;
			
			РабочихДнейЧасов = МодульРасчетЗарплатыДляНебольшихОрганизаций.РабочихДнейЧасовВПериодеОрганизации(СтрокаНачислений.ДатаНачала, СтрокаНачислений.ДатаОкончания, Объект.Организация);
			
			Если РабочихДнейЧасов = Неопределено Тогда
				
				СтрокаНачислений.ОтработаноДней = 0;
				СтрокаНачислений.ОтработаноЧасов = 0;
				
			Иначе
				
				СтрокаНачислений.ОтработаноДней = РабочихДнейЧасов.Дней;
				СтрокаНачислений.ОтработаноЧасов = РабочихДнейЧасов.Часов;
				
			КонецЕсли;
			
			Если Объект.ПланируемаяДатаВыплаты >= ДатаЗаконаПроРКиЕАЭС Тогда
				
				Если ЗначениеЗаполнено(НачислениеОтпускРК) И Объект.СреднийЗаработокРК > 0 Тогда
					
					СтрокаНачисленийРК					= Объект.Начисления.Добавить();
					СтрокаНачисленийРК.Начисление		= НачислениеОтпускРК;
					СтрокаНачисленийРК.Сотрудник 		= СтрокаНачислений.Сотрудник;
					СтрокаНачисленийРК.Подразделение 	= СтрокаНачислений.Подразделение;
					СтрокаНачисленийРК.ДатаНачала 		= СтрокаНачислений.ДатаНачала;
					СтрокаНачисленийРК.ДатаОкончания 	= СтрокаНачислений.ДатаОкончания;
					СтрокаНачисленийРК.Результат 		= Объект.СреднийЗаработокРК * СтрокаНачислений.ОплаченоДней;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(НачислениеОтпускСН) И Объект.СреднийЗаработокСН > 0 Тогда
					
					СтрокаНачисленийСН					= Объект.Начисления.Добавить();
					СтрокаНачисленийСН.Начисление		= НачислениеОтпускСН;
					СтрокаНачисленийСН.Сотрудник 		= СтрокаНачислений.Сотрудник;
					СтрокаНачисленийСН.Подразделение 	= СтрокаНачислений.Подразделение;
					СтрокаНачисленийСН.ДатаНачала 		= СтрокаНачислений.ДатаНачала;
					СтрокаНачисленийСН.ДатаОкончания 	= СтрокаНачислений.ДатаОкончания;
					СтрокаНачисленийСН.Результат 		= Объект.СреднийЗаработокСН * СтрокаНачислений.ОплаченоДней;
					
				КонецЕсли;
				
			КонецЕсли;
			
			ДатаНачала = КонецМесяца(ДатаНачала) + 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура инициирует перерасчет НДФЛ.
// Вызывается из основной или вспомогательной формы документа.
//
// Параметры:
//  Объект      - ДанныеФормыСтруктура.
//  ОбъектФормы - ДокументОбъект.
//
Процедура ПересчитатьНДФЛ(Объект, ОбъектФормы) Экспорт
	
	УчетЗарплаты.ПересчитатьНДФЛ(Объект, ОбъектФормы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
