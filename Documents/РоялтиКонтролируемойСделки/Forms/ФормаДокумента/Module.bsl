#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	НастроитьВалютныеПоля();
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	
	ВерсияУведомления = Документы.УведомлениеОКонтролируемыхСделках.ВерсияУведомления(Объект.УведомлениеОКонтролируемойСделке);
	
	Если Параметры.Свойство("АктивироватьСтрокуТабличнойЧасти") Тогда
		АктивироватьСтрокуТабличнойЧасти = Параметры.АктивироватьСтрокуТабличнойЧасти;
		ТекущийЭлемент = Элементы[АктивироватьСтрокуТабличнойЧасти.ТабличнаяЧасть];
		ТекущийЭлемент.ТекущаяСтрока = АктивироватьСтрокуТабличнойЧасти.НомерСтроки - 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьТабличнуюЧастьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УведомлениеОКонтролируемойСделкеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.УведомлениеОКонтролируемойСделке) Тогда 
		ПриИзмененииУведмомленияОКонтролируемойСделкеНаСервере();
	Иначе
		Объект.Организация = Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ДоговорКонтрагентаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыСопутствующиеУслуги

&НаКлиенте
Процедура СделкиРоялтиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		
		СтрокаТаблицы = Элемент.ТекущиеДанные;
		Если СтрокаТаблицы = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаТаблицы.ТипРоялти = ПредопределенноеЗначение("Перечисление.ТипыРоялтиКонтролируемыхСделок.Процентом");
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицы(ЭтотОбъект, СтрокаТаблицы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СделкиРоялтиРасчетныйДокументНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.СделкиРоялти.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("УведомлениеОКонтролируемыхСделках", Объект.УведомлениеОКонтролируемойСделке);
	ПараметрыОткрытияФормы.Вставить("Организация", Объект.Организация);
	ПараметрыОткрытияФормы.Вставить("Контрагент", Объект.Контрагент);
	ПараметрыОткрытияФормы.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	ПараметрыОткрытияФормы.Вставить("ПредметСделки", ТекущиеДанные.ПредметСделки);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("РасчетныйДокументНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("РегистрНакопления.КонтролируемыеСделкиОрганизаций.Форма.ФормаДокументовСделки",
		ПараметрыОткрытияФормы, Элемент, , , , ОповещениеОЗакрытии);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьВалютныеПоля()
	
	ВалютаРегУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ПредставлениеРубли = Строка(ВалютаРегУчета);
	ПредставлениеВалюта = "";
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ВалютаРасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДоговорКонтрагента, "ВалютаВзаиморасчетов");
		Если ВалютаРасчетов <> ВалютаРегУчета Тогда
			ПредставлениеВалюта = Строка(ВалютаРасчетов);
		КонецЕсли;
	КонецЕсли;
	
	Элементы.СделкиРоялтиПредставлениеРубли.Видимость = ЗначениеЗаполнено(ПредставлениеВалюта);
	Элементы.СделкиРоялтиПредставлениеВалюта.Видимость = ЗначениеЗаполнено(ПредставлениеВалюта);
	Элементы.СделкиРоялтиВалютнаяСумма.Видимость = ЗначениеЗаполнено(ПредставлениеВалюта);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(Форма)
	
	Для Каждого СтрокаСделки Из Форма.Объект.СделкиРоялти Цикл
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицы(Форма, СтрокаСделки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицы(Форма, СтрокаСделки)
	
	СтрокаСделки.ПредставлениеРубли = Форма.ПредставлениеРубли;
	СтрокаСделки.ПредставлениеВалюта = Форма.ПредставлениеВалюта;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииУведмомленияОКонтролируемойСделкеНаСервере()
	
	ПараметрыУведомления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.УведомлениеОКонтролируемойСделке, "Организация,ВерсияУведомления");
	Объект.Организация = ПараметрыУведомления.Организация;
	ВерсияУведомления = ПараметрыУведомления.ВерсияУведомления;
	
КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()
	
	НастроитьВалютныеПоля();
	ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйДокументНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		ТекущиеДанные = Элементы.СделкиРоялти.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ТекущиеДанные.РасчетныйДокумент = РезультатЗакрытия;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// СуммаБазыДляРасчетаРоялти
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СделкиРоялтиСуммаБазыДляРасчетаРоялти");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.СделкиРоялти.ТипРоялти", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыРоялтиКонтролируемыхСделок.Процентом);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.СделкиРоялти.СуммаБазыДляРасчетаРоялти", ВидСравненияКомпоновкиДанных.Равно, 0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьНаСервере()
	
	СтарыеЗначения = Объект.СделкиРоялти.Выгрузить();
	Объект.СделкиРоялти.Очистить();
	
	ТаблицаСделокРоялти = РегистрыНакопления.КонтролируемыеСделкиОрганизаций.СделкиДоговораДляРоялти(
		Объект.УведомлениеОКонтролируемойСделке, Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
		
	Объект.СделкиРоялти.Загрузить(ТаблицаСделокРоялти);
	Для Каждого СтрокаСделкиРоялти Из Объект.СделкиРоялти Цикл
		
		Отбор = Новый Структура("ДатаСовершенияСделки,ПредметСделки,НаименованиеПредметаСделки,РасчетныйДокумент");
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаСделкиРоялти);
		
		НайденныеСтроки = СтарыеЗначения.НайтиСтроки(Отбор);
		Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
			СтрокаСделкиРоялти.ТипРоялти = Перечисления.ТипыРоялтиКонтролируемыхСделок.Процентом;
		Иначе
			СтрокаСделкиРоялти.ТипРоялти = НайденныеСтроки[0].ТипРоялти;
			СтрокаСделкиРоялти.СуммаБазыДляРасчетаРоялти = НайденныеСтроки[0].СуммаБазыДляРасчетаРоялти;
			СтарыеЗначения.Удалить(НайденныеСтроки[0]);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьДобавленныеКолонкиТаблиц(ЭтотОбъект);
	
	Модифицированность = Истина;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
