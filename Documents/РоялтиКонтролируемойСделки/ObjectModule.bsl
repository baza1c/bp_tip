#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Сумма = СделкиРоялти.Итог("Сумма");
	СуммаБазыДляРасчетаРоялти = СделкиРоялти.Итог("СуммаБазыДляРасчетаРоялти");
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УведомлениеОКонтролируемойСделке) Тогда
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УведомлениеОКонтролируемойСделке, "Организация");
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	ПроверитьЗаполнениеОбязательныхПолейТабличнойЧастиСделкиРоялти(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеОбязательныхПолейТабличнойЧастиСделкиРоялти(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов, Отказ)
	
	МассивНепроверяемыхРеквизитов.Добавить("СделкиРоялти.СуммаБазыДляРасчетаРоялти");
	
	ИмяСписка = НСтр("ru = 'Сделки роялти'");
	МетаданныеТЧ = Метаданные.Документы.РоялтиКонтролируемойСделки.ТабличныеЧасти.СделкиРоялти;
	
	Для Каждого СтрокаСделок Из СделкиРоялти Цикл
		
		Если СтрокаСделок.ТипРоялти = Перечисления.ТипыРоялтиКонтролируемыхСделок.Процентом
			И Не ЗначениеЗаполнено(СтрокаСделок.СуммаБазыДляРасчетаРоялти) Тогда
			
			ИмяПроверяемогоПоля = "СуммаБазыДляРасчетаРоялти";
			МетаданныеРеквизита = МетаданныеТЧ.Реквизиты.Найти(ИмяПроверяемогоПоля);
			Если МетаданныеРеквизита <> Неопределено Тогда
				СинонимРеквизита = МетаданныеРеквизита.Синоним;
			Иначе
				СинонимРеквизита = ИмяПроверяемогоПоля;
			КонецЕсли;
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, СинонимРеквизита,
				СтрокаСделок.НомерСтроки, ИмяСписка, НСтр("ru = 'Нужно указать сумму базы для роялти, рассчитываемого процентом'"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СделкиРоялти", СтрокаСделок.НомерСтроки, ИмяПроверяемогоПоля);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли