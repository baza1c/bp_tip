#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Считывает данные файла в табличный документ и определяет загружаемые колонки.
//
// Параметры:
//   ПараметрыЗадания - Структура - структура с ключами:
//      * ХранилищеДанныхФайла - ХранилищеЗначения - двоичные данные файла, упакованные в хранилище значений.
//      * РасширениеФайла - Строка - расширение файла.
//      * ОписаниеКолонок - ТаблицаЗначений - описание загружаемых колонок, см. НовыйОписаниеЗагружаемыхКолонок().
//   АдресРезультата - УникальныйИдентификатор - адрес во временном хранилище для сохранения результатов.
//
Процедура ОбработатьДанныеИзФайла(ПараметрыЗадания, АдресРезультата) Экспорт
	
	ПараметрыЗадания.Вставить("МакетЗаголовка", ПолучитьМакет("МакетЗаголовка"));
	
	ТабличныйДокумент = ЗагрузкаДанныхИзВнешнихФайлов.ЗагрузитьФайлВТабличныйДокумент(ПараметрыЗадания);
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеФайла", Новый ХранилищеЗначения(ТабличныйДокумент, Новый СжатиеДанных(9)));
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

// По данным табличного документа заполняет таблицу лидов для загрузки.
// Выполняет поиск существующих лидов.
//
// Параметры:
//   ПараметрыЗадания - Структура - структура с ключами:
//      * ХранилищеДанных - ХранилищеЗначения - табличный документ, содержащий данные.
//   АдресРезультата - УникальныйИдентификатор - адрес во временном хранилище для сохранения результатов.
//
Процедура ПолучитьТаблицуДанныхДляЗагрузки(ПараметрыЗадания, АдресРезультата) Экспорт
	
	ТабличныйДокумент = ПараметрыЗадания.ХранилищеДанных.Получить();
	
	ЗагружаемыеДанные = НовыйТаблицаЗагружаемыхДанных();
	
	ВысотаТаблицы = ТабличныйДокумент.ВысотаТаблицы;
	ШиринаТаблицы = ТабличныйДокумент.ШиринаТаблицы;
	
	// Строки с пустым наименованием будем удалять.
	СтрокиКУдалению = Новый Массив;
	
	Для НомерСтроки = 2 По ВысотаТаблицы Цикл
		
		НоваяСтрока = ЗагружаемыеДанные.Добавить();
		НоваяСтрока.НомерСтроки = НомерСтроки;
		
		Для НомерКолонки = 1 По ШиринаТаблицы Цикл
			
			ИмяКолонки = ТабличныйДокумент.Область(1, НомерКолонки).ПараметрРасшифровки;
			ТекущаяОбласть = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки);
			Если ТекущаяОбласть.ПараметрРасшифровки <> "НеЗагружать" Тогда
				НоваяСтрока.ЗначенияКолонок.Вставить(ИмяКолонки, СокрЛП(ТекущаяОбласть.Текст));
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, НоваяСтрока.ЗначенияКолонок);
		
		НоваяСтрока.Статус = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый();
		НоваяСтрока.Представление = НСтр("ru = 'Новый: '") + НоваяСтрока.Наименование;
		
		Если ПустаяСтрока(НоваяСтрока.Наименование) Тогда
			СтрокиКУдалению.Добавить(НоваяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		ЗагружаемыеДанные.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
	// Ищем лидов в базе и проставляем статусы для найденных строк.
	НайтиЛидов(ЗагружаемыеДанные);
	
	ПоместитьВоВременноеХранилище(ЗагружаемыеДанные, АдресРезультата);
	
КонецПроцедуры

// Загружает подготовленные данные в информационную базу.
// Создает новых лидов.
//
// Параметры:
//   ПараметрыЗадания - Структура - структура с ключами:
//      * ЗагружаемыеДанные - ТаблицаЗначений - таблица значений с данными для загрузки.
//   АдресРезультата - УникальныйИдентификатор - адрес во временном хранилище для сохранения результатов.
//
Процедура ЗагрузитьДанныеИзФайла(ПараметрыЗадания, АдресРезультата) Экспорт
	
	ЗагружаемыеДанные = ПараметрыЗадания.ЗагружаемыеДанные;
	
	Ошибки = Неопределено;
	ТекстОшибки = НСтр("ru = 'Не удалось записать лида %1 по причине:
		|%2'");
	
	Состояние = Справочники.СостоянияРаботыСЛидами.СостояниеПоУмолчанию();
	
	ДобавленныеЛиды = Новый Массив;
	
	Для Каждого СтрокаДанных Из ЗагружаемыеДанные Цикл
		
		Если ЗначениеЗаполнено(СтрокаДанных.Лид) Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			ДанныеЗаполненияЛида = ДанныеЗаполненияЛида(СтрокаДанных);
			
			НовыйЛид = Справочники.КонтактныеЛица.СоздатьЭлемент();
			НовыйЛид.Заполнить(ДанныеЗаполненияЛида);
			НовыйЛид.Записать();
			
			ДобавленныеЛиды.Добавить(НовыйЛид.Ссылка);
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			
			ТекстСообщения = СтрШаблон(ТекстОшибки, СтрокаДанных.Наименование, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "", ТекстСообщения, "");
			
			ТекстСообщения = СтрШаблон(ТекстОшибки, СтрокаДанных.Наименование, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ЗагрузкаДанныхИзВнешнихФайлов.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Справочники.КонтактныеЛица, , ТекстСообщения);
		КонецПопытки;
		
		РегистрыСведений.ИзменениеСостоянийРаботыСЛидами.УстановитьСостояниеРаботыСЛидами(ДобавленныеЛиды, Состояние);
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибки", Ошибки);
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

// Удаляет строки и колонки, не содержащие полезной информации для загрузки.
//
// Параметры:
//   ПараметрыЗадания - Структура - структура с ключами:
//      * ХранилищеДанных - ХранилищеЗначения - табличный документ, содержащий данные.
//   АдресРезультата - УникальныйИдентификатор - адрес во временном хранилище для сохранения результатов.
//
Процедура УдалитьВсеНенужныеСтрокиТаблицы(ПараметрыЗадания, АдресРезультата) Экспорт
	
	ТабличныйДокумент = ПараметрыЗадания.ХранилищеДанных.Получить();
	
	НайденыНенужныеСтроки = Ложь;
	ЗагрузкаДанныхИзВнешнихФайлов.УдалитьВсеНенужныеСтрокиТаблицы(ТабличныйДокумент, НайденыНенужныеСтроки);
	
	Результат = Новый Структура;
	Результат.Вставить("НайденыНенужныеСтроки", НайденыНенужныеСтроки);
	Результат.Вставить("ХранилищеДанных", Новый ХранилищеЗначения(ТабличныйДокумент, Новый СжатиеДанных(9)));
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Функция ОписаниеЗагружаемыхКолонок() Экспорт
	
	ОписаниеКолонок = ЗагрузкаДанныхИзВнешнихФайлов.НовыйОписаниеЗагружаемыхКолонок();
	
	// Наименование
	НоваяСтрока = ОписаниеКолонок.Добавить();
	НоваяСтрока.Идентификатор = "Наименование";
	НоваяСтрока.ПредставлениеКолонки = НСтр("ru = 'Наименование'");
	НоваяСтрока.ОбязательнаДляЗаполнения = Истина;
	НоваяСтрока.ПодходящиеЗаголовкиФайла = ПодходящиеЗаголовкиНаименования();
	
	// Телефон
	НоваяСтрока = ОписаниеКолонок.Добавить();
	НоваяСтрока.Идентификатор = "Телефон";
	НоваяСтрока.ПредставлениеКолонки = НСтр("ru = 'Телефон'");
	НоваяСтрока.ПодходящиеЗаголовкиФайла = ПодходящиеЗаголовкиТелефон();
	
	// Email
	НоваяСтрока = ОписаниеКолонок.Добавить();
	НоваяСтрока.Идентификатор = "Email";
	НоваяСтрока.ПредставлениеКолонки = НСтр("ru = 'Email'");
	НоваяСтрока.ПодходящиеЗаголовкиФайла = ПодходящиеЗаголовкиАдресЭлектроннойПочты();
	
	Возврат ОписаниеКолонок;
	
КонецФункции

#Область ОписаниеВозможныхЗаголовковВЗагружаемыхФайлах

Функция ПодходящиеЗаголовкиНаименования()
	
	МассивЗаголовков = Новый Массив;
	МассивЗаголовков.Добавить("Наименование");
	МассивЗаголовков.Добавить("ФИО");
	МассивЗаголовков.Добавить("Контактное лицо");
	МассивЗаголовков.Добавить("Контакт");
	
	Возврат МассивЗаголовков;
	
КонецФункции

Функция ПодходящиеЗаголовкиТелефон()
	
	МассивЗаголовков = Новый Массив;
	МассивЗаголовков.Добавить("Телефон");
	МассивЗаголовков.Добавить("Мобильный телефон");
	МассивЗаголовков.Добавить("Номер телефона");
	
	Возврат МассивЗаголовков;
	
КонецФункции

Функция ПодходящиеЗаголовкиАдресЭлектроннойПочты()
	
	МассивЗаголовков = Новый Массив;
	МассивЗаголовков.Добавить("Email");
	МассивЗаголовков.Добавить("E-mail");
	МассивЗаголовков.Добавить("Электронная почта");
	МассивЗаголовков.Добавить("Адрес электронной почты");
	
	Возврат МассивЗаголовков;
	
КонецФункции

#КонецОбласти

#Область ПоискИСозданиеЭлементов

Функция НовыйТаблицаЗагружаемыхДанных()
	
	ЗагружаемыеДанные = Новый ТаблицаЗначений;
	
	Для Каждого Реквизит Из Метаданные.Обработки.ЗагрузкаЛидовИзФайла.ТабличныеЧасти.ЗагружаемыеДанные.Реквизиты Цикл
	
		ЗагружаемыеДанные.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	
	КонецЦикла;
	
	ЗагружаемыеДанные.Колонки.Добавить("НомерСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ЗагружаемыеДанные.Колонки.Добавить("ЗначенияКолонок", Новый ОписаниеТипов("Структура"));
	
	Возврат ЗагружаемыеДанные;
	
КонецФункции

Функция ЗначениеКолонкиПоИдентификатору(ЗначенияКолонок, Идентификатор)
	
	ЗначениеКолонки = Неопределено;
	Если ПустаяСтрока(Идентификатор) Тогда
		Возврат ЗначениеКолонки;
	КонецЕсли;
	
	Для Каждого Элемент Из ЗначенияКолонок Цикл
		Если Элемент.Ключ = Идентификатор Тогда
			ЗначениеКолонки = Элемент.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначениеКолонки;
	
КонецФункции

#Область Лиды

Процедура НайтиЛидов(ЗагружаемыеДанные)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТаблицаДляПоиска", ЗагружаемыеДанные);
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.Наименование КАК Наименование,
	|	Таблица.Телефон КАК Телефон,
	|	Таблица.Email КАК Email
	|ПОМЕСТИТЬ ТаблицаДляПоиска
	|ИЗ
	|	&ТаблицаДляПоиска КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДляПоиска.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(Лиды.Ссылка, ЕСТЬNULL(ЛидыТелефон.Ссылка, ЛидыEmail.Ссылка)) КАК Ссылка,
	|	ТаблицаДляПоиска.Наименование КАК Наименование,
	|	ТаблицаДляПоиска.Телефон КАК Телефон,
	|	ТаблицаДляПоиска.Email КАК Email,
	|	ВЫБОР
	|		КОГДА НЕ Лиды.Ссылка ЕСТЬ NULL
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА НЕ ЛидыТелефон.Ссылка ЕСТЬ NULL
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА НЕ ЛидыEmail.Ссылка ЕСТЬ NULL
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВесВыборки
	|ИЗ
	|	ТаблицаДляПоиска КАК ТаблицаДляПоиска
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица КАК Лиды
	|		ПО ТаблицаДляПоиска.Наименование = Лиды.Наименование
	|			И (ТаблицаДляПоиска.Наименование <> &ПустаяСтрока)
	|			И (Лиды.Лид)
	|			И (НЕ Лиды.ПометкаУдаления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК ЛидыТелефон
	|		ПО ТаблицаДляПоиска.Телефон = ЛидыТелефон.Представление
	|			И (ТаблицаДляПоиска.Телефон <> &ПустаяСтрока)
	|			И (ЛидыТелефон.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
	|			И (ЛидыТелефон.Ссылка.Лид)
	|			И (НЕ ЛидыТелефон.Ссылка.ПометкаУдаления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛица.КонтактнаяИнформация КАК ЛидыEmail
	|		ПО ТаблицаДляПоиска.Email = ЛидыEmail.Представление
	|			И (ТаблицаДляПоиска.Email <> &ПустаяСтрока)
	|			И (ЛидыEmail.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты))
	|			И (ЛидыEmail.Ссылка.Лид)
	|			И (НЕ ЛидыEmail.Ссылка.ПометкаУдаления)
	|ГДЕ
	|	(НЕ Лиды.Ссылка ЕСТЬ NULL
	|			ИЛИ НЕ ЛидыТелефон.Ссылка ЕСТЬ NULL
	|			ИЛИ НЕ ЛидыEmail.Ссылка ЕСТЬ NULL)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	ВесВыборки УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПараметрыПоиска = Новый Структура("НомерСтроки");
	Для Каждого СтрокаТаблицы Из ЗагружаемыеДанные Цикл
		
		Выборка.Сбросить();
		ПараметрыПоиска.НомерСтроки = СтрокаТаблицы.НомерСтроки;
		Если Выборка.НайтиСледующий(ПараметрыПоиска) Тогда
			СтрокаТаблицы.Лид = Выборка.Ссылка;
			СтрокаТаблицы.Статус = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНайденный();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеЗаполненияЛида(СтрокаДанных)
	
	ДанныеЗаполнения = Новый Структура;
	
	ДанныеЗаполнения.Вставить("Лид", Истина);
	ДанныеЗаполнения.Вставить("Наименование", СтрокаДанных.Наименование);
	
	ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(СтрокаДанных.Наименование);
	ДанныеЗаполнения.Вставить("Фамилия", ФИО.Фамилия);
	ДанныеЗаполнения.Вставить("Имя", ФИО.Имя);
	ДанныеЗаполнения.Вставить("Отчество", ФИО.Отчество);
	
	// Контактная информация
	Телефон = ЗначениеКолонкиПоИдентификатору(СтрокаДанных.ЗначенияКолонок, "Телефон");
	Если Телефон <> Неопределено И Не ПустаяСтрока(Телефон) Тогда
		ТелефонЗначениеJSON = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(
			Телефон, Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйКонтактныеЛица);
		ДанныеЗаполнения.Вставить("ТелефонЗначениеJSON", ТелефонЗначениеJSON);
	КонецЕсли;
	
	Email = ЗначениеКолонкиПоИдентификатору(СтрокаДанных.ЗначенияКолонок, "Email");
	Если Email <> Неопределено И Не ПустаяСтрока(Email) Тогда
		EmailЗначениеJSON = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(
			Email, Справочники.ВидыКонтактнойИнформации.EmailКонтактныеЛица);
		ДанныеЗаполнения.Вставить("АдресЭлектроннойПочтыЗначениеJSON", EmailЗначениеJSON);
	КонецЕсли;
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли