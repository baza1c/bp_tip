#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяВзноса     = Параметры.ИмяВзноса;
	ВидВзноса     = Параметры.ВидВзноса;
	Всего         = Параметры.Всего;
	Начислено     = Параметры.Начислено;
	Уплачено      = Параметры.Уплачено;
	Переплата     = Параметры.Переплата;
	Сумма         = Параметры.Сумма;
	Организация   = Параметры.Организация;
	Период        = Параметры.Период;
	ПлательщикЕНП = Параметры.ПлательщикЕНП;
	КоэффициентПФРДобровольно = параметры.КоэффициентПФРДобровольно;
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресТаблицыПлатежей) Тогда
		ТаблицаПлатежей = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыПлатежей);
		Если ТипЗнч(ТаблицаПлатежей) = Тип("ТаблицаЗначений") Тогда
			Платежи.Загрузить(ТаблицаПлатежей);
			Для Каждого Платеж Из Платежи Цикл
				Платеж.ПредставлениеДокумента = ПомощникиПоУплатеНалоговИВзносов.ПредставлениеДокументаОплаты(Платеж, Истина);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ПомощникиПоУплатеНалоговИВзносов.ОтобразитьПлатежиССуммойВОтдельнойКолонке(ЭтотОбъект, Платежи, "Платеж");
	
	ИспользуетсяИнтеграцияСБанком = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
	Заголовок = Параметры.Заголовок;
	
	Элементы.ЗаголовокКОплате.Заголовок = СтрШаблон(НСтр("ru = 'К оплате за %1 квартал'"), Формат(Период, "ДФ=к"));
	
	УправлениеФормойНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеВыписки" 
		ИЛИ ИмяСобытия = "ОбновитьПоказателиРасчетаУСН" И Не ЗначениеЗаполнено(Параметр) Тогда
		
		ЗаполнитьСтраховыеВзносыНаСервере();
		
	ИначеЕсли ИмяСобытия = "Запись_ПлатежныйДокумент_УплатаНалогов" Тогда
		
		Налог = Неопределено;
		
		Если ТипЗнч(Параметр) = Тип("Структура")
			И Параметр.Свойство("Организация") И Параметр.Организация = Организация
			И Параметр.Свойство("Налог", Налог) И ЗначениеЗаполнено(Налог)
			И Параметр.Свойство("Оплачено") И Параметр.Оплачено = Истина
			И ЭтоФиксированныеВзносы(Налог) Тогда
			
			ЗаполнитьСтраховыеВзносыНаСервере();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ДатаРегистрации" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыОткрытияФормы = Новый Структура("Ключ", Организация);
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыОткрытияФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВсегоНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормыНалогиИОтчеты = Новый Структура;
	ПараметрыФормыНалогиИОтчеты.Вставить("Организация", Организация);
	ПараметрыФормыНалогиИОтчеты.Вставить("ТекущаяЗакладка", "СтраховыеВзносы_Предприниматель");
	ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты", ПараметрыФормыНалогиИОтчеты, , Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПлатежОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПлатежныхДокументов = ПомощникиПоУплатеНалоговИВзносовКлиент.ПараметрыОбработкиПлатежныхДокументов(
		Платежи, "Платеж", ОповещениеУдаленияПлатежногоДокумента());
	
	ПомощникиПоУплатеНалоговИВзносовКлиент.ОбработкаНавигационнойСсылкиПлатежногоДокумента(Элемент, НавигационнаяСсылкаФорматированнойСтроки, ПараметрыПлатежныхДокументов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереплатаПриИзменении(Элемент)
	ИзмененаПереплата = Истина;
	РассчитатьСуммуКУплате(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьОплатуЧерезБанк(Команда)
	
	Перем ВидНалога, БанковскийСчет;
	
	Если ПлательщикЕНП Тогда
		ВидНалога = ПредопределенноеЗначение("Перечисление.ВидыНалогов.ЕдиныйНалоговыйПлатеж");
	Иначе
		ВидНалога = ВидНалогаПоИмениВзноса();
		
	КонецЕсли;
	
	Если ИспользуетсяИнтеграцияСБанком Тогда
		БанковскийСчет = ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка");
	КонецЕсли;
	
	ПомощникиПоУплатеНалоговИВзносовКлиент.СоздатьОплатуЧерезБанк(
		Организация,
		Период,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидНалога),
		ПредопределенноеЗначение("Перечисление.ВидыПлатежейВГосБюджет.Налог"),
		БанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОплатуНаличными(Команда)
	
	Перем ВидНалога;
	
	Если ПлательщикЕНП Тогда
		ВидНалога = ПредопределенноеЗначение("Перечисление.ВидыНалогов.ЕдиныйНалоговыйПлатеж");
	Иначе
		ВидНалога = ВидНалогаПоИмениВзноса();
	КонецЕсли;
	
	ПомощникиПоУплатеНалоговИВзносовКлиент.СоздатьОплатуНаличными(
		Организация,
		Период,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидНалога),
		ПредопределенноеЗначение("Перечисление.ВидыПлатежейВГосБюджет.Налог"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если ИзмененаПереплата Тогда
		ИзмененаПереплата = Ложь;
		ИзменитьОстаткиРасчетовПоВзносуВФоне(Организация, Период, ВидВзноса, Переплата);
	КонецЕсли;
	
	ОповеститьОВыбореИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормойНаСервере()
	
	ПоказыватьПереплату = ПомощникиПоУплатеНалоговИВзносов.ПоказыватьПереплатуПоНалогуВзносу(Организация, Период);
	
	Элементы.ГруппаПереплата.Видимость = ПоказыватьПереплату;
	Элементы.ЗаголовокПереплата.Заголовок = СтрШаблон(НСтр("ru = 'Переплата на %1'"), Формат(НачалоГода(Период), "ДЛФ=D"));
	
	Если ПоказыватьПереплату Тогда
		Элементы.ЗаголовокУплачено.Заголовок = СтрШаблон(НСтр("ru = 'Уплачено с %1 г.'"), Формат(НачалоГода(Период), "ДЛФ=D"));
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = ИмяВзноса + Платежи.Количество();
	
	Если ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком() Тогда
		КлючСохраненияПоложенияОкна = КлючСохраненияПоложенияОкна + "ИнтеграцияСБанком";
	КонецЕсли;
	
	Если ПоказыватьПереплату Тогда
		КлючСохраненияПоложенияОкна = КлючСохраненияПоложенияОкна + "ПоказыватьПереплату";
	КонецЕсли;
	
	ЗаполнитьПояснения();
	
	Если ИмяВзноса = "СуммаВзносаПФРДобровольно" Тогда
		Элементы.ПояснениеФормулаРасчета.Видимость = Истина;
		СтрокиЗаголовка = Новый Массив;
		СтрокиЗаголовка.Добавить(СтрШаблон(НСтр(
			"ru = 'Рассчитывается как МРОТ * %1%% * 12 месяцев:
			|%2  * %1%% * 12'"),
			УчетСтраховыхВзносовИП.ПроцентУплачиваемыхДобровольныхВзносовВПФР(),
			УчетЗарплаты.МинимальныйРазмерОплатыТрудаРФ(НачалоГода(Период))));
		Если КоэффициентПФРДобровольно <> 1 Тогда
			СтрокиЗаголовка.Добавить(Символы.ПС);
			СтрокиЗаголовка.Добавить(СтрШаблон(НСтр(
				"ru = 'Ежемесячные платежи за каждый месяц, в котором договор добровольного пенсионного страхования действовал не полный срок, уменьшаются пропорционально количеству дней действия договора'")));
		КонецЕсли;
		Элементы.ПояснениеФормулаРасчета.Заголовок = СтрСоединить(СтрокиЗаголовка);
	Иначе
		Элементы.ПояснениеФормулаРасчета.Видимость = Ложь;
	КонецЕсли;
	
	Если УчетнаяПолитика.ПлательщикЕНП(Организация, Период) Тогда
		// Команды добавления ранее совершенных платежек недоступны,
		// С введением ЕНС недостаточно просто совершить платеж
		Элементы.ГруппаСумма.Видимость = Ложь;
		Элементы.КоманднаяПанель.Видимость = Ложь;
	Иначе
		Элементы.ГруппаСумма.Видимость = Истина;
		Элементы.КоманднаяПанель.Видимость = Истина;
	КонецЕсли;
	
	Если Период >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета() Тогда
		// Фактическая оплата взноса только после отражения на ЕНС и загрузки сведений об оплатах
		Элементы.ГруппаУплачено.Видимость = (Платежи.Количество() > 0);
		Элементы.ГруппаПлатежиКоманды.Видимость = Ложь;
		Элементы.Сумма.Вид = ВидПоляФормы.ПолеНадписи;
		Элементы.Сумма.Шрифт = Элементы.ЗаголовокКОплате.Шрифт;
	Иначе
		Элементы.ГруппаУплачено.Видимость = Истина;
		Элементы.ГруппаПлатежиКоманды.Видимость = Истина;
		Элементы.Сумма.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОВыбореИЗакрыть()
	
	Модифицированность = Ложь;
	
	Результат = Новый Структура;
	Результат.Вставить(ИмяВзноса, Сумма);
	Результат.Вставить(ИмяВзноса + "Переплата", Переплата);
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Функция ВидНалогаПоИмениВзноса()
	
	Если ИмяВзноса = "СуммаВзносаПФР" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть");
	ИначеЕсли ИмяВзноса = "СуммаВзносаФФОМС" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыНалогов.ФиксированныеВзносы_ФФОМС");
	ИначеЕсли ИмяВзноса = "СуммаВзносаПФРДобровольно" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыНалогов.ФиксированныеВзносы_ПФР_Добровольные"); 
	ИначеЕсли ИмяВзноса = "СуммаВзносаЕдиныйТариф" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыНалогов.ФиксированныеВзносы_СтраховыеВзносыЕдиныйТариф");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ОповещениеУдаленияПлатежногоДокумента()
	Возврат Новый ОписаниеОповещения("УдалитьДокументУплатыНаКлиентеЗавершение", ЭтотОбъект);
КонецФункции

&НаКлиенте
Процедура УдалитьДокументУплатыНаКлиентеЗавершение(ДокументУплатыДляУдаления, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДокументУплатыДляУдаления) Тогда
		УдалитьДокументУплаты(ДокументУплатыДляУдаления);
		Оповестить("УдалитьДокументУплаты", ДокументУплатыДляУдаления);
		Оповестить("ОбновитьПоказателиРасчетаУСН", , ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДокументУплаты(ДокументУплатыДляУдаления)
	
	ДокументУплатыОбъект = ДокументУплатыДляУдаления.ПолучитьОбъект();
	ДокументУплатыОбъект.УстановитьПометкуУдаления(Истина);
	
	УдалитьДокументУплатыИзТаблицыПлатежей(ДокументУплатыДляУдаления);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДокументУплатыИзТаблицыПлатежей(ДокументУплатыДляУдаления)
	
	СтрокиПлатежа = Платежи.НайтиСтроки(Новый Структура("Ссылка", ДокументУплатыДляУдаления));
	Для Каждого СтрокаПлатежа Из СтрокиПлатежа Цикл
		Уплачено = Уплачено - СтрокаПлатежа.Сумма;
		РассчитатьСуммуКУплате(ЭтотОбъект);
		Платежи.Удалить(СтрокаПлатежа);
	КонецЦикла;
	
	ПомощникиПоУплатеНалоговИВзносов.ОтобразитьПлатежиССуммойВОтдельнойКолонке(ЭтотОбъект, Платежи, "Платеж");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПояснения()
	
	ПоясненияРасчета = Обработки.РасчетСтраховыхВзносовИП.ПоясненияРасчетаВзносов(Организация, Период, Истина);
	
	Элементы.ПлатежРассчитанСДатыРегистрации.Заголовок = ПоясненияРасчета.ПлатежРассчитанСДатыРегистрации;
	Элементы.ПлатежРассчитанСДатыРегистрации.Видимость = ЗначениеЗаполнено(ПоясненияРасчета.ПлатежРассчитанСДатыРегистрации);
	
	Элементы.РекомендацияПоПериодичностиУплаты.Заголовок = ПоясненияРасчета.РекомендацияПоПериодичностиУплаты;
	Элементы.РекомендацияПоПериодичностиУплаты.Видимость =
		ЗначениеЗаполнено(ПоясненияРасчета.РекомендацияПоПериодичностиУплаты) И ВидВзноса <> "ПФРДобровольно";
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтраховыеВзносыНаСервере()
	
	Периодичность = УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, Период);
	
	ФиксированныеВзносыКУплате = Обработки.РасчетСтраховыхВзносовИП.СтраховыеВзносыКУплате(
		Организация, Период, Периодичность);
	
	ЗаполнитьПояснения();
	
	Всего     = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ФиксированныеВзносыКУплате, ИмяВзноса + "Всего");
	Уплачено  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ФиксированныеВзносыКУплате, ИмяВзноса + "Уплачено", 0);
	Переплата = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ФиксированныеВзносыКУплате, ИмяВзноса + "Переплата", 0);
	Сумма     = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ФиксированныеВзносыКУплате, ИмяВзноса, 0);
	
	СтраховыеВзносыУплаченные = ФиксированныеВзносыКУплате.ФиксированныеСтраховыеВзносыУплаченные;
	
	СчетаУчетаСтраховыхВзносов = УчетСтраховыхВзносовИП.СчетаУчетаСтраховыхВзносов();
	СчетУчета = СчетаУчетаСтраховыхВзносов[Сред(ИмяВзноса, 12)];
	ПараметрыОтбора = Новый Структура("СчетУчета", СчетУчета);
	
	ТаблицаПлатежей = СтраховыеВзносыУплаченные.Скопировать(СтраховыеВзносыУплаченные.НайтиСтроки(ПараметрыОтбора));
	ТаблицаПлатежей.Сортировать("ДатаДокументаОплаты, ДокументОплаты", Новый СравнениеЗначений);
	Если ТаблицаПлатежей <> Неопределено Тогда
		ТаблицаПлатежей.Колонки.ДокументОплаты.Имя = "Ссылка";
		ТаблицаПлатежей.Колонки.НомерДокументаОплаты.Имя = "Номер";
		ТаблицаПлатежей.Колонки.ДатаДокументаОплаты.Имя = "Дата";
		Платежи.Загрузить(ТаблицаПлатежей);
		Для Каждого Платеж Из Платежи Цикл
			Платеж.ПредставлениеДокумента = ПомощникиПоУплатеНалоговИВзносов.ПредставлениеДокументаОплаты(Платеж, Истина);
		КонецЦикла;
	КонецЕсли;
	
	ПомощникиПоУплатеНалоговИВзносов.ОтобразитьПлатежиССуммойВОтдельнойКолонке(ЭтотОбъект, Платежи, "Платеж");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьОстаткиРасчетовПоВзносуВФоне(Организация, Период, ВидВзноса, Переплата)
	
	СчетУчета = Неопределено;
	Если НЕ УчетСтраховыхВзносовИП.СчетаУчетаСтраховыхВзносов().Свойство(ВидВзноса, СчетУчета) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = ПомощникиПоУплатеНалоговИВзносов.НовыеПараметрыИзмененияОстатковРасчетовПоНалогамВзносамИП();
	ПараметрыЗадания.Организация       = Организация;
	ПараметрыЗадания.ПериодСобытия     = Период;
	ПараметрыЗадания.ТаблицаОстатков   = ТаблицаПереплатыПоВзносу(Организация, Период, ВидВзноса, СчетУчета, Переплата);
	ПараметрыЗадания.Комментарий = СтрШаблон(
		НСтр("ru = '#Создан автоматически обработкой ""Уплата фиксированных страховых взносов"" для отражения переплаты по взносу %1'"),
		ВидВзноса);
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	ПараметрыВыполненияВФоне.ЗапуститьВФоне    = Истина;
	
	ДлительныеОперации.ВыполнитьВФоне("ПомощникиПоУплатеНалоговИВзносов.СохранитьПереплатуПоНалогамВзносамИнтеграцияСБанком", 
		ПараметрыЗадания, ПараметрыВыполненияВФоне);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТаблицаПереплатыПоВзносу(Организация, Период, ВидВзноса, СчетУчета, Переплата)
	
	ТаблицаОстатков = ПомощникиПоУплатеНалоговИВзносов.НоваяТаблицаИзмененияРасчетовПоНалогамВзносамИП();
	
	СтрокаОстаток = ТаблицаОстатков.Добавить();
	СтрокаОстаток.СчетУчета = СчетУчета;
	СтрокаОстаток.ВидПлатежаВБюджет = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	СтрокаОстаток.Переплата = Переплата;
	
	ОстаткиРасчетов = УчетСтраховыхВзносовИП.ОстатокРасчетовПоВзносамЗаПредыдущиеСтраховыеПериодыИнтеграцияСБанком(Организация, Период);
	СтрокаОстаток.СуммаИзмененияПереплаты = Переплата - ОстаткиРасчетов[ВидВзноса];
	
	Возврат ТаблицаОстатков;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммуКУплате(Форма)
	
	Форма.Сумма = Макс(Форма.Начислено - Форма.Переплата - Форма.Уплачено, 0);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоФиксированныеВзносы(Знач Налог)
	
	Возврат Обработки.РасчетСтраховыхВзносовИП.ЭтоФиксированныеВзносы(Налог);
	
КонецФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ИзмененаПереплата Тогда
			ИзменитьОстаткиРасчетовПоВзносуВФоне(Организация, Период, ВидВзноса, Переплата);
		КонецЕсли;
		
		ОповеститьОВыбореИЗакрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти