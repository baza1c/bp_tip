#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Период = КонецКвартала(Параметры.ПериодСобытия);
	Организация = Параметры.Организация;
	Заголовок = Параметры.Заголовок;
	СкрытьФункцииОплаты = Параметры.СкрытьФункцииОплаты;
	Если СкрытьФункцииОплаты Тогда
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	КонецЕсли;
	
	ИспользуетсяИнтеграцияСБанком = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
	УстановитьФункциональныеОпцииФормы();
	ЗаполнитьСтраховыеВзносыНаСервере();
	УправлениеФормойПриСозданииНаСервере();
	
	СброситьРазмерыИПоложениеОкна();
	
	МобильныйКлиентАдаптацияФормы();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененВидДеятельностиОрганизации" Тогда
		
		Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Организации") И Параметр = Организация Тогда
			ЗаполнитьСтраховыеВзносыНаСервере();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзменениеВыписки"
		Или ИмяСобытия = "Запись_ПлатежныйДокумент_УплатаНалогов"
		Или ИмяСобытия = "ОбновитьПоказателиРасчетаУСН" И Не ЗначениеЗаполнено(Параметр)
		Или ИмяСобытия = "ИзменениеЗаписиКУДиР" Тогда
		
		ОбновитьПлатежныеДокументыНаФорме();
		
	ИначеЕсли ИмяСобытия = "АктуализацияЗавершенаУспешно"
		И ТипЗнч(Параметр) = Тип("Структура")
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Организация") = Организация Тогда
		
		ЗаполнитьСтраховыеВзносыНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СкрытьФункцииОплаты Тогда
		ДанныеУчетаАктуальны = Истина;
		УправлениеФормой();
	Иначе
		ДанныеУчетаАктуальны = Ложь;
		ПередНачаломДлительнойОперации();
	КонецЕсли;
	
	ЗапуститьОбновлениеБаннераСостоянияОтправки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстСостояниеЗагрузкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИнтеграцияСБанкамиФормыКлиент.ОбработкаНавигационнойСсылкиБаннера(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходаИПНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыРасходыИП();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходаУСННажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыУСН();
	
КонецПроцедуры

&НаКлиенте
Процедура ПотенциальноВозможныйДоходНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьПотенциальныйДоход();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОстатокЕНСПриИзменении(Элемент)
	
	Если ИспользоватьОстатокЕНС Тогда
		СуммаСписанияСЕНС = Мин(ОстатокЕНС, Всего, Сумма);
	Иначе
		СуммаСписанияСЕНС = 0;
	КонецЕсли;
	
	ИспользоватьОстатокЕНСПриИзмененииНаСервере();
	Оповестить("Запись_СуммаЗачетаЕНС");
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаСписанияСЕНСПриИзменении(Элемент)
	
	СуммаСписанияСЕНСПриИзмененииНаСервере();
	Оповестить("Запись_СуммаЗачетаЕНС");
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#Область ПодробныйРасчет

&НаКлиенте
Процедура ДоходПоУСННажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыУСН();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПоОСНОНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыРасходыИП();
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыПоОСНОНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыРасходыИП();
	
КонецПроцедуры

&НаКлиенте
Процедура ПотенциальныйДоходНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьПотенциальныйДоход();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховыеВзносыИПНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьСтраховыеВзносыИП();
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыУСННажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыУСН();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОплатитьСБанковскогоСчета(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СпособУплатыНалога = ПредопределенноеЗначение("Перечисление.СпособыУплатыНалогов.БанковскийПеревод");
	СоздатьДокументУплатыНалога(СпособУплатыНалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатитьНаличнымиПоКвитанции(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СпособУплатыНалога = ПредопределенноеЗначение("Перечисление.СпособыУплатыНалогов.НаличнымиПоКвитанции");
	СоздатьДокументУплатыНалога(СпособУплатыНалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПлатежиВБанк(Команда)
	
	ДокументыОплатыПоБанку = ПлатежныеДокументыДляОтправкиПоДиректБанку();
	
	Если ЗначениеЗаполнено(ДокументыОплатыПоБанку) Тогда
		ОбменСБанкамиКлиент.СформироватьПодписатьОтправитьЭД(ДокументыОплатыПоБанку);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПлатежи(Команда)
	
	ДокументыОплатыВФайл = ПлатежныеДокументыДляВыгрузкиВФайл();
	
	Оповещение = Новый ОписаниеОповещения("ВыгрузитьПлатежныеДокументыЗавершение", ЭтотОбъект);
	ПомощникиПоУплатеНалоговИВзносовКлиент.ПроверитьИВыгрузитьПлатежныеДокументы(
		Организация,
		ДокументыОплатыВФайл,
		Оповещение,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	НачалоПериода = НачалоГода(Период);
	КонецПериода  = КонецГода(Период);
	
	Если УчетнаяПолитика.Существует(Организация, КонецПериода) Тогда
		ПлательщикНДФЛ       = УчетнаяПолитика.ПлательщикНДФЛЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПрименяетсяУСН       = УчетнаяПолитика.ПрименяетсяУСНЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПрименяетсяУСНРасход = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходыЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Организация, НачалоПериода, КонецПериода);
	Иначе
		День = 24 * 60 * 60;
		ПлательщикНДФЛ       = УчетнаяПолитика.ПлательщикНДФЛ(Организация, КонецПериода + День);
		ПрименяетсяУСН       = УчетнаяПолитика.ПрименяетсяУСН(Организация, КонецПериода + День);
		ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, КонецПериода + День);
	КонецЕсли;
	
	Если ПлательщикНДФЛ Или УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходыЗаПериод(Организация, НачалоПериода, КонецПериода) Тогда
		УчитыватьРасходы = Истина;
	Иначе
		УчитыватьРасходы = Ложь;
	КонецЕсли;
	
	ТребуетсяАктуализироватьРасчетВзносов =
		(УчетСтраховыхВзносовИП.ПериодичностьНачисления(Организация, Период) = Перечисления.Периодичность.Квартал)
		И УчетСтраховыхВзносовИП.УчитыватьРасходыПриРасчетеВзносовСДоходов(Организация, Период);
	
	Правило = ПравилоУплатыВзноса(Организация, Период);
	
	ПериодОстатка = ОбщегоНазначения.ТекущаяДатаПользователя();
	ОстатокЕНС = ЕдиныйНалоговыйСчет.ОстатокНаЕдиномНалоговомСчете(Организация, ПериодОстатка);
	СуммаСписанияСЕНС = СуммаЗачетаЕНС(Организация, КонецГода(Период), Правило);
	ПоказыватьКомандыОплаты = ПомощникиПоУплатеНалоговИВзносов.ПоказыватьКомандыОплаты();
	ИсключатьВзносыИзРасходовУСН = УчетУСН.ИсключатьВзносыИППриРасчетеВзносаСДоходов(КонецПериода);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойПриСозданииНаСервере()
	
	Элементы.УменьшениеНалога.Видимость = ПлательщикНДФЛ;
	Элементы.ПояснениеУменьшения.Заголовок = ТекстПоясненияДляУменьшенияНалога(Организация, Период, ПлательщикНДФЛ);
	Элементы.ОрганизацияПредставление.Видимость = Справочники.Организации.ИспользуетсяНесколькоОрганизаций() И Не СкрытьФункцииОплаты;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	ВсеПлатежиПодготовлены = (Платежи.Итог("Сумма") >= Всего);
	СкрытьПолеКОплате = (Сумма <=0); 
	
	Элементы.СуммаДоходаИП.Видимость = ПлательщикНДФЛ;
	Элементы.СуммаДоходаУСН.Видимость = ПрименяетсяУСН;
	Элементы.ПотенциальноВозможныйДоход.Видимость = ПрименяетсяУСНПатент;
	
	// Реквизиты подробного расчета взноса
	Элементы.ПотенциальныйДоход.Видимость = ПрименяетсяУСНПатент;
	Элементы.ДоходыПоОСНО.Видимость = ПлательщикНДФЛ;
	Элементы.РасходыПоОСНО.Видимость = ПлательщикНДФЛ;
	Элементы.РасходыУСН.Видимость = ПрименяетсяУСНРасход;
	Элементы.ДоходПоУСН.Видимость = ПрименяетсяУСН;
	
	Элементы.ОплатаПоКвитанцииЧтоДелатьДальше.Видимость = Не ИспользуетсяИнтеграцияСБанком И ЕстьДокументыОплатыПоКассе();
	
	// Показываем неотрицательный остаток
	Элементы.СуммаСписанияСЕНС.Подсказка = СтрШаблон(НСтр("ru= 'из %1 руб.'"), Макс(ОстатокЕНС, 0));
	
	Если ВсеПлатежиПодготовлены Или ОстатокЕНС <= 0 Тогда
		// Если платежи все выполнены или остаток на ЕНС нулевой, то списать деньги с ЕНС нельзя
		Элементы.ИспользоватьОстатокЕНС.Доступность = Ложь;
	Иначе
		Элементы.ИспользоватьОстатокЕНС.Доступность = Истина;
	КонецЕсли;
	
	Элементы.СуммаСписанияСЕНС.Доступность = ИспользоватьОстатокЕНС;
	Элементы.ГруппаКнопкиОплаты.Видимость = ПоказыватьКомандыОплаты И Не ВсеПлатежиПодготовлены И Не СкрытьПолеКОплате;
	Элементы.ГруппаОтправкаПлатежейВБанк.Видимость = Не ИспользуетсяИнтеграцияСБанком;
	
	Если ПоказыватьКомандыОплаты И Не ВсеПлатежиПодготовлены Тогда
		Элементы.ЗаголовокКОплате.Видимость = Не СкрытьПолеКОплате;
		Элементы.Сумма.Видимость = Не СкрытьПолеКОплате;
	Иначе
		Элементы.ЗаголовокКОплате.Видимость = Ложь;
		Элементы.Сумма.Видимость = Ложь;
	КонецЕсли;
	
	ВыполняетсяАктуализацияРасчета = ТребуетсяАктуализироватьРасчетВзносов И Не ДанныеУчетаАктуальны;
	Элементы.ГруппаСтатусФоновогоЗадания.Видимость = ВыполняетсяАктуализацияРасчета;
	Элементы.ГруппаРасчетВзноса.Видимость = Не ВыполняетсяАктуализацияРасчета;
	Элементы.ГруппаУплата.Видимость = Не ВыполняетсяАктуализацияРасчета И Не СкрытьФункцииОплаты;
	
	Элементы.ГруппаВсегоДней.Видимость = КоличествоДнейЕдиныйТариф <> 0;
	Элементы.ГруппаДнейЕдиногоТарифа.Видимость = КоличествоДнейЕдиныйТариф < КоличествоДнейОсуществленияДеятельности;
	
	// Отображение элементов сложного расчета
	
	Если УчитыватьРасходы Тогда
		Элементы.РасчетУпрощенный.Заголовок = НСтр("ru = 'Доход по ОСНО (за вычетом расходов)'");
		Элементы.СуммаДоходаУСН.Заголовок = НСтр("ru = 'Доход по УСН (за вычетом расходов)'");
		Элементы.ДоходПоУСН.Заголовок = НСтр("ru = 'Доход по УСН (за вычетом расходов)'");
	Иначе
		Элементы.РасчетУпрощенный.Заголовок = НСтр("ru = 'Доход по ОСНО'");
		Элементы.СуммаДоходаУСН.Заголовок = НСтр("ru = 'Доход по УСН'");
		Элементы.ДоходПоУСН.Заголовок = НСтр("ru = 'Доход по УСН'");
	КонецЕсли;
	
	Если ПоказыватьПодробныйРасчет Тогда
		// Показываем подробный расчет с учетом расходов и предельной суммы взноса
		Элементы.РасчетУпрощенный.Видимость = Ложь;
		Элементы.ПодробныйРасчет.Видимость = Истина;
		Элементы.ПоясненияРасчета.ТекущаяСтраница = Элементы.ПодробныйРасчет;
		
		ФорматОтображенияЧисел = ФорматЧиселВПоясненииРасчета();
		Элементы.ДоходыПоОСНО.Формат = ФорматОтображенияЧисел;
		Элементы.РасходыПоОСНО.Формат = ФорматОтображенияЧисел;
		Элементы.ПотенциальныйДоход.Формат = ФорматОтображенияЧисел;
		Элементы.ПредельнаяСуммаВзноса.Формат = ФорматОтображенияЧисел;
		Элементы.СтраховыеВзносыИП.Формат = ФорматОтображенияЧисел;
		
		Если ЗначениеЗаполнено(ПотенциальноВозможныйДоход) Тогда
			Элементы.ДоходыПоОСНО.Заголовок = НСтр("ru = 'Доходы по ОСНО'");
			Элементы.ДоходПоУСН.Заголовок = НСтр("ru = 'Доходы по УСН'");
		Иначе
			// Совмещения с ПСН нет, есть только доход по ОСНО - в заголовке нет смысла выделять систему налогообложения
			Элементы.ДоходыПоОСНО.Заголовок = НСтр("ru = 'Доходы'");
			Элементы.ДоходПоУСН.Заголовок = НСтр("ru = 'Доходы'");
		КонецЕсли;
		
		// Профессиональный вычет и страховые взносы учавствуют в расчете для ИП на ОСНО
		Элементы.РасходыПоОСНО.Видимость = ПлательщикНДФЛ;
		Элементы.ПраваяКолонка.Видимость = ПлательщикНДФЛ Или ПрименяетсяУСН;
		Элементы.СтраховыеВзносыИП.Видимость = ПлательщикНДФЛ Или (ПрименяетсяУСНРасход И ИсключатьВзносыИзРасходовУСН);
		Элементы.ДоходыПоОСНО.Видимость = ПлательщикНДФЛ;
		
	Иначе
		Элементы.РасчетУпрощенный.Видимость = Истина;
		Элементы.ПодробныйРасчет.Видимость = Ложь;
		Элементы.ПоясненияРасчета.ТекущаяСтраница = Элементы.РасчетУпрощенный;
	КонецЕсли;
	
	Если ПоказыватьКомандыОплаты Тогда
		
		Если ТребуетсяОтправитьДокументыОплатыПоДиректБанку() Тогда
			Элементы.ОтправитьПлатежиВБанк.Видимость = Не ИспользуетсяИнтеграцияСБанком;
			Элементы.ВыгрузитьПлатежи.Видимость = Ложь;
			КнопкаПоУмолчаниюОтправитьВБанк = Не ИспользуетсяИнтеграцияСБанком;
		Иначе
			Элементы.ОтправитьПлатежиВБанк.Видимость = Ложь;
			Если ТребуетсяВыгрузитьДокументыОплаты() И Не ИспользуетсяИнтеграцияСБанком Тогда
				Элементы.ВыгрузитьПлатежи.Видимость = Истина;
				КнопкаПоУмолчаниюОтправитьВБанк = Истина;
			Иначе
				Элементы.ВыгрузитьПлатежи.Видимость = Ложь;
				КнопкаПоУмолчаниюОтправитьВБанк = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		КнопкаПоУмолчаниюУплата = Не КнопкаПоУмолчаниюОтправитьВБанк;
		
		УстановитьВидПоУмолчаниюОформлением(Элементы.ОтправитьПлатежиВБанк, КнопкаПоУмолчаниюОтправитьВБанк);
		УстановитьВидПоУмолчаниюОформлением(Элементы.ВыгрузитьПлатежи, КнопкаПоУмолчаниюОтправитьВБанк);
		УстановитьВидПоУмолчаниюОформлением(Элементы.ОплатитьПоБанку, КнопкаПоУмолчаниюУплата);
		
	КонецЕсли;
	
	УправлениеФормойМобильныйКлиент();
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить(
			ИмяФормы,
			"",
			ПользователиИнформационнойБазы.ТекущийПользователь().Имя);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПояснения()
	
	РасчетнаяСуммаКУплате = Всего - Уплачено;
	
	ПоясненияРасчета = Обработки.РасчетСтраховыхВзносовИП.ПоясненияРасчетаВзносаСДоходов(
		Организация,
		Период,
		ПараметрыРасчетаВзносаСДоходов,
		РасчетнаяСуммаКУплате - СуммаЗарезервированная,
		Уплачено);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПоясненияРасчета);
	
	Если ПрименяетсяУСНРасход Тогда
		СуммаДоходаУСН = СуммаДоходаУСН + СуммаРасходаУСН;
	КонецЕсли;
	
	// Динамические заголовки элементов
	Элементы.ДекорацияСуммаВзносаПояснение.Заголовок = ПоясненияРасчета.СуммаВзносаПояснение;
	Элементы.ДекорацияСуммаВзносаФормула.Заголовок = ПоясненияРасчета.СуммаВзносаФормула;
	
	Элементы.Всего.Заголовок = ПоясненияРасчета.СуммаВзносаЗаголовок;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтраховыеВзносыНаСервере()
	
	Периодичность = УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, Период);
	
	// Доходы по видам деятельности
	ДанныеРасчетаВзносов = УчетСтраховыхВзносовИП.ПараметрыРасчетаВзносовСДоходов(Организация, Период, Истина);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеРасчетаВзносов);
	
	ПараметрыРасчетаВзносаСДоходов = УчетСтраховыхВзносовИП.НовыеПараметрыРасчетаВзносаСДоходов();
	ПараметрыРасчетаВзносаСДоходов.ВсегоДоходов = ДанныеРасчетаВзносов.ВсегоДоходов;
	ПараметрыРасчетаВзносаСДоходов.УчитыватьРасходы = УчитыватьРасходы;
	
	ПредельнаяСуммаВзноса = ДанныеРасчетаВзносов.ПредельнаяСуммаВзноса;
	Всего                 = ДанныеРасчетаВзносов.ВзносыНачислено;
	Уплачено              = ДанныеРасчетаВзносов.СуммаОплаченнаяЗаСтраховойПериод;
	Сумма                 = Всего - Уплачено;
	ПроцентСДоходов       = ДанныеРасчетаВзносов.ПроцентСДоходов;
	СуммаДоходаУСН        = ДанныеРасчетаВзносов.СуммаДоходаУСН;
	
	ТаблицаПлатежей = ДанныеРасчетаВзносов.СтраховыеВзносыУплаченные;
	Если ТаблицаПлатежей <> Неопределено Тогда
		ТаблицаПлатежей.Колонки.ДокументОплаты.Имя = "Ссылка";
		ТаблицаПлатежей.Колонки.НомерДокументаОплаты.Имя = "Номер";
		ТаблицаПлатежей.Колонки.ДатаДокументаОплаты.Имя = "Дата";
		Платежи.Загрузить(ТаблицаПлатежей);
		Для Каждого Платеж Из Платежи Цикл
			Платеж.ПредставлениеДокумента = ПомощникиПоУплатеНалоговИВзносов.ПредставлениеДокументаОплаты(Платеж, Истина);
		КонецЦикла;
	КонецЕсли;
	
	НайтиПлатежиСвязанныеСЗадачей();
	
	ИспользоватьОстатокЕНС = (СуммаСписанияСЕНС > 0);
	
	ПоказыватьПодробныйРасчет = ПоказыватьПодробныйРасчет();
	
	ЗаполнитьПояснения();
	ЗаполнитьПодробныйРасчет();
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ИспользоватьОстатокЕНСПриИзмененииНаСервере()
	
	Сумма = СуммаЕНПСУчетомСовершенныхРанееПлатежей();
	
	ИзменитьСуммуЗачетаНаЕНСВЗадачах();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура СуммаСписанияСЕНСПриИзмененииНаСервере()
	
	Сумма = СуммаЕНПСУчетомСовершенныхРанееПлатежей();
	
	ИзменитьСуммуЗачетаНаЕНСВЗадачах();
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Функция СуммаЕНПСУчетомСовершенныхРанееПлатежей()
	 
	Возврат Макс(0, Всего - СуммаСписанияСЕНС - Платежи.Итог("Сумма"));
	
КонецФункции

&НаСервере
Процедура ИзменитьСуммуЗачетаНаЕНСВЗадачах()
	
	Если Не ЗначениеЗаполнено(Правило) Тогда
		Возврат;
	КонецЕсли;
	
	// Обновим статус задачи при установки суммы списания с ЕНС
	ПараметрыЗадачи = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадачСтруктурой();
	ПараметрыЗадачи.Организация = Организация;
	ПараметрыЗадачи.Правило = Правило;
	ПараметрыЗадачи.ПериодСобытия = Период;
	ПараметрыЗадачи.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога;
	ПараметрыЗадачи.Вставить("СуммаЗачетаЕНС", 0);
	ПараметрыЗадачи.Вставить("Статус", "");
	
	ПараметрыЗадачи.Правило = Правило;
	ПараметрыЗадачи.СуммаЗачетаЕНС = Мин(Всего, СуммаСписанияСЕНС);
	
	Если Сумма > 0 Тогда
		// Есть сумма к оплате - не можем считать задачу выполненной
		ПараметрыЗадачи.Статус = "";
	Иначе
		ПараметрыЗадачи.Статус = УчетСтраховыхВзносовИПКлиентСервер.СтатусОплатыВзноса();
	КонецЕсли;
	
	ЗаписатьСуммуЗачетаЕНС(ПараметрыЗадачи);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьСуммуЗачетаЕНС(ПараметрыЗадачи)
	
	МенеджерЗаписи = РегистрыСведений.ЗадачиБухгалтера.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыЗадачи);
	МенеджерЗаписи.Прочитать();
	
	Если Не МенеджерЗаписи.Выбран() Тогда
		// Нет задачи по данному правилу
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи.СуммаЗачетаЕНС = ПараметрыЗадачи.СуммаЗачетаЕНС;
	
	Если Не МенеджерЗаписи.СтатусУстановленВручную Тогда
		// Меняем статус только для задач, по которым не уставлено ручных корректировок
		МенеджерЗаписи.Статус = ПараметрыЗадачи.Статус;
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаСервере
Функция СоздатьПлатежныеДокументыНаСервере(СпособОплаты)
	
	Если СпособОплаты = Перечисления.СпособыУплатыНалогов.БанковскийПеревод Тогда
		
		СчетОрганизации = БанковскийСчетОрганизации(Организация);
		Если Не ЗначениеЗаполнено(СчетОрганизации) Тогда
			ТекстСообщения = НСтр("ru = 'Укажите банковский счет в реквизитах организации'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Организация);
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	СуммыКУплате = Обработки.РасчетСтраховыхВзносовИП.НовыеПоказателиРасчетаВзносовЗаСебя();
	СуммыКУплате.СуммаВзносаПФРсДоходов = Сумма;
	СуммыКУплате.СуммаЕНП = Сумма;
	
	ТаблицаПлатежей = Обработки.РасчетСтраховыхВзносовИП.ТаблицаПлатежейДляФормированияПлатежныхПоручений(
		Организация,
		Период,
		СуммыКУплате,
		Истина);
	
	СтруктураПараметров = Обработки.ФормированиеПлатежныхПорученийНаУплатуНалогов.НовыеПараметрыФормированияПлатежныхДокументов();
	СтруктураПараметров.Правило = Правило;
	СтруктураПараметров.ПериодСобытия = Период;
	СтруктураПараметров.Организация = Организация;
	СтруктураПараметров.НалоговыйПериод = НачалоГода(Период);
	СтруктураПараметров.Платежи = ПоместитьВоВременноеХранилище(ТаблицаПлатежей);
	СтруктураПараметров.СпособОплаты = СпособОплаты;
	СтруктураПараметров.СчетОрганизации = СчетОрганизации;
	
	ЗначенияЗаполнения = Обработки.ФормированиеПлатежныхПорученийНаУплатуНалогов.ДанныеЗаполненияПлатежныхДокументов(
		СтруктураПараметров);
	
	Если ЗначениеЗаполнено(ЗначенияЗаполнения) И ЗначенияЗаполнения.Количество() = 1 Тогда
		Возврат ЗначенияЗаполнения[0];
	Иначе
		СозданныеДокументы =
			Обработки.ФормированиеПлатежныхПорученийНаУплатуНалогов.СоздатьПлатежныеДокументыПоДаннымЗаполнения(
				СтруктураПараметров,
				ЗначенияЗаполнения);
	
		ЗаполнитьСтраховыеВзносыНаСервере();
		УправлениеФормой();
	
		Возврат СозданныеДокументы;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция БанковскийСчетОрганизации(Знач Организация)
	
	СчетОрганизации = Справочники.БанковскиеСчета.ПустаяСсылка();
	
	УчетДенежныхСредствБП.УстановитьБанковскийСчет(
		СчетОрганизации,
		Организация,
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	
	Возврат СчетОрганизации;
	
КонецФункции

&НаКлиенте
Процедура СоздатьДокументУплатыНалога(СпособУплатыНалога)
	
	РезультатОплаты = СоздатьПлатежныеДокументыНаСервере(СпособУплатыНалога);
	Если РезультатОплаты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(РезультатОплаты) = Тип("Структура") Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", РезультатОплаты);
		ПараметрыФормы.Вставить("ПериодСобытия", Период);
		ПараметрыФормы.Вставить("Правило", Правило);
		
		Если СпособУплатыНалога = ПредопределенноеЗначение("Перечисление.СпособыУплатыНалогов.БанковскийПеревод") Тогда
			ИмяФормыУплатыНалога = "Документ.ПлатежноеПоручение.Форма.ФормаДокументаНалоговая";
		Иначе
			ИмяФормыУплатыНалога = "Документ.РасходныйКассовыйОрдер.ФормаОбъекта";
		КонецЕсли;
		ОткрытьФорму(ИмяФормыУплатыНалога, ПараметрыФормы, , УникальныйИдентификатор);
		
	ИначеЕсли РезультатОплаты.Количество() > 0 Тогда
		ОповеститьОСозданныхДокументах(РезультатОплаты);
		Если СпособУплатыНалога = ПредопределенноеЗначение("Перечисление.СпособыУплатыНалогов.БанковскийПеревод") Тогда
			ЗапуститьОбновлениеБаннераСостоянияОтправки();
		КонецЕсли;
	КонецЕсли;
	
	Если СпособУплатыНалога = ПредопределенноеЗначение("Перечисление.СпособыУплатыНалогов.НаличнымиПоКвитанции") Тогда
		ПередНачаломДлительнойОперации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОСозданныхДокументах(СозданныеДокументы)
	
	Если СозданныеДокументы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПервыйДокумент = СозданныеДокументы[0];
	ТипСозданныхДокументов = ТипЗнч(ПервыйДокумент);
	
	ОповеститьОбИзменении(ТипСозданныхДокументов);
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ЗадачиБухгалтера"));
	
	Если ТипСозданныхДокументов = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		Оповестить("ПомощникРасчетСтраховыхВзносовИП_ВзносыУплачены", Новый Структура("Организация", Организация));
	КонецЕсли;
	
	Оповестить("ИзменилосьСостояниеДокументаИнтеграцииСБанком", Неопределено, СозданныеДокументы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПравилоУплатыВзноса(Организация, Период)
	
	ПорядокУплаты = ВыполнениеЗадачБухгалтера.ПорядокУплатыНалога(Организация,
		КонецГода(Период),
		Перечисления.ВидыНалогов.ФиксированныеВзносы_СвышеПредела);
	
	Возврат ПорядокУплаты.Правило;
	
КонецФункции

&НаСервере
Процедура НайтиПлатежиСвязанныеСЗадачей()
	
	Платежи.Очистить();
	
	Периодичность = Перечисления.Периодичность.Год;
	ВсеПлатежи = Обработки.РасчетСтраховыхВзносовИП.ПлатежныеДокументы(Организация, Период, Периодичность, Правило);
	
	ПлатежиПоВидамВзносов = Обработки.РасчетСтраховыхВзносовИП.РазделитьПлатежиПоВидамВзносов(ВсеПлатежи);
	Платежи.Загрузить(ПлатежиПоВидамВзносов.ПлатежиЕНП);
	
	Сумма = СуммаЕНПСУчетомСовершенныхРанееПлатежей();
	
	ВывестиПлатежиИБаннерСостоянияОтправкиНаФорму();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДокументыОплаты(Форма)
	
	Результат = Новый Массив;
	Для Каждого ТекущаяСтрока Из Форма.Платежи Цикл
		Результат.Добавить(ТекущаяСтрока.Ссылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЕстьДокументыОплатыПоКассе()
	
	Возврат ЕстьДокументОплаты(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	
КонецФункции

&НаСервере
Функция ЕстьДокументОплаты(ТипДокумента)
	
	Для Каждого СтрокаОплаты Из Платежи Цикл
		Если ТипЗнч(СтрокаОплаты.Ссылка) = ТипДокумента Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервереБезКонтекста
Функция СуммаЗачетаЕНС(Организация, ПериодСобытия, Правило)
	
	СписокПравил = Новый Массив;
	СписокПравил.Добавить(Правило);
	
	Возврат РегистрыСведений.ЗадачиБухгалтера.СуммаЗачетаЕНС(
		Организация,
		ПериодСобытия,
		Перечисления.ВидыДействийКалендаряБухгалтера.УплатаНалога,
		СписокПравил);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПлатежиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПлатежныхДокументов = ПомощникиПоУплатеНалоговИВзносовКлиент.ПараметрыОбработкиПлатежныхДокументов(
		Платежи,
		"Платежи",
		ОповещениеУдаленияПлатежногоДокумента());
	
	ПомощникиПоУплатеНалоговИВзносовКлиент.ОбработкаНавигационнойСсылкиПлатежногоДокумента(
		Элемент,
		НавигационнаяСсылка,
		ПараметрыПлатежныхДокументов);
	
КонецПроцедуры

&НаКлиенте
Функция ОповещениеУдаленияПлатежногоДокумента()
	Возврат Новый ОписаниеОповещения("УдалитьДокументУплатыНаКлиентеЗавершение", ЭтотОбъект);
КонецФункции

&НаКлиенте
Процедура УдалитьДокументУплатыНаКлиентеЗавершение(ДокументУплатыДляУдаления, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДокументУплатыДляУдаления) Тогда
		УдалитьДокументУплаты(ДокументУплатыДляУдаления);
		ЗапуститьОбновлениеБаннераСостоянияОтправки();
		ПередНачаломДлительнойОперации();
		Оповестить("Запись_ПлатежныйДокумент_УплатаНалогов", ДокументУплатыДляУдаления, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДокументУплаты(ДокументУплатыДляУдаления)
	
	ДокументУплатыОбъект = ДокументУплатыДляУдаления.ПолучитьОбъект();
	ДокументУплатыОбъект.УстановитьПометкуУдаления(Истина);
	
	ДанныеУчетаАктуальны = (ТипЗнч(ДокументУплатыДляУдаления) <> Тип("ДокументСсылка.РасходныйКассовыйОрдер")
		И ТипЗнч(ДокументУплатыДляУдаления) <> Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	
	ЗаполнитьСтраховыеВзносыНаСервере();
	
	ВыполнениеЗадачБухгалтера.ЗарегистрироватьИзменениеСтатусаЗадачиПодготовкиПлатежа(ДокументУплатыДляУдаления);
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстПоясненияДляУменьшенияНалога(Организация, ПериодСобытия, ПлательщикНДФЛ)
	
	Если ПлательщикНДФЛ Тогда
		
		СрокСПревышения = Формат(УчетСтраховыхВзносовИППовтИсп.СрокОплатыПоВидуПлатежа(
			ПериодСобытия,
			Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела), "ДЛФ=DD");
		
		СтрокиПояснения = Новый Массив;
		
		СтрокиПояснения.Добавить(СтрШаблон(
			НСтр("ru = 'Фактическое списание страховых взносов с доходов произойдет %1'"),
			СрокСПревышения));
		
		СтрокиПояснения.Добавить(Символы.ПС);
		СтрокиПояснения.Добавить(УчетСтраховыхВзносовИП.ТекстПоясненияУменьшенияНалогаРасходы(
			Организация,
			ПериодСобытия));
		
		ТекстПояснения = Новый ФорматированнаяСтрока(СтрокиПояснения);
		
	Иначе
		
		ТекстПояснения = "";
		
	КонецЕсли;
	
	Возврат ТекстПояснения;
	
КонецФункции

&НаСервере
Процедура ОбновитьПлатежныеДокументыНаФорме()
	
	НайтиПлатежиСвязанныеСЗадачей();
	УправлениеФормой();
	
КонецПроцедуры

#Область ОтправкаДокументовОплатыВБанк

&НаСервере
Функция ПлатежныеДокументыДляВыгрузкиВФайл()
	
	НеИсполненныеПлатежи = Новый Массив;
	
	Для Каждого СтрокаОплаты Из Платежи Цикл
		Если ТипЗнч(СтрокаОплаты.Ссылка) = Тип("ДокументСсылка.ПлатежноеПоручение")
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеОплачено(СтрокаОплаты.Состояние)
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеОшибки(СтрокаОплаты.Состояние) Тогда
			НеИсполненныеПлатежи.Добавить(СтрокаОплаты.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.ПлатежныеДокументыДляВыгрузкиВФайл(Организация, НеИсполненныеПлатежи);
	
КонецФункции

&НаСервере
Функция ПлатежныеДокументыДляОтправкиПоДиректБанку()
	
	НеОтправленныеПлатежи =  Новый Массив;
	
	Для Каждого СтрокаОплаты Из Платежи Цикл
		Если ТипЗнч(СтрокаОплаты.Ссылка) = Тип("ДокументСсылка.ПлатежноеПоручение")
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеУспешнойОтправки(СтрокаОплаты.Состояние)
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеОшибки(СтрокаОплаты.Состояние)
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеОплачено(СтрокаОплаты.Состояние) Тогда
			НеОтправленныеПлатежи.Добавить(СтрокаОплаты.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.ПлатежныеДокументыПоДиректБанку(Организация, НеОтправленныеПлатежи);
	
КонецФункции

&НаСервере
Функция ТребуетсяВыгрузитьДокументыОплаты()
	
	ДокументыОплатыВФайл = ПлатежныеДокументыДляВыгрузкиВФайл();
	
	Возврат ЗначениеЗаполнено(ДокументыОплатыВФайл);
	
КонецФункции

&НаСервере
Функция ТребуетсяОтправитьДокументыОплатыПоДиректБанку()
	
	ДокументыОплатыПоБанку = ПлатежныеДокументыДляОтправкиПоДиректБанку();
	
	Возврат ЗначениеЗаполнено(ДокументыОплатыПоБанку);
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьПлатежныеДокументыЗавершение(ПлатежныеПоручения, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ПлатежныеПоручения) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеУчетаАктуальны = Ложь;
	
	ОбработатьВыгрузкуПлатежейНаСервере(ПлатежныеПоручения);
	
	ЗапуститьОбновлениеБаннераСостоянияОтправки();
	ПередНачаломДлительнойОперации();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыгрузкуПлатежейНаСервере(ПлатежныеПоручения)
	
	Для Каждого ПлатежныйДокумент Из ПлатежныеПоручения Цикл
		РегистрыСведений.СостоянияБанковскихДокументов.УстановитьСостояниеДокумента(
			ПлатежныйДокумент, Перечисления.СостоянияБанковскихДокументов.Отправлено);
	КонецЦикла;
	
	ОбновитьПлатежныеДокументыНаФорме();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидПоУмолчаниюОформлением(Элемент, ЭтоКнопкаПоУмолчанию)
	
	ПолужирныйШрифт = ЭтоКнопкаПоУмолчанию;
	Шрифт = Новый Шрифт(Элемент.Шрифт, , , ПолужирныйШрифт);
	
	Элемент.Шрифт    = Шрифт;
	Элемент.ЦветФона = ?(ЭтоКнопкаПоУмолчанию, ЦветаСтиля.ВыборСтандартногоПериодаФонКнопки, Новый Цвет);
	
КонецПроцедуры

#КонецОбласти

#Область ПодробныйРасчет

&НаСервере
Процедура ЗаполнитьПодробныйРасчет()
	
	Если Не ПоказыватьПодробныйРасчет Тогда
		// Подробный расчет показывается для ИП на ОСНО
		Возврат;
	КонецЕсли;
	
	Пояснения = ПодробноеПоясненияРасчетаВзносаСДоходов(ПараметрыПодробногоПояснения());
	Для Каждого ТекущееПояснение Из Пояснения Цикл
		
		НомерЭтапаСтрокой = НомерЭтапаСтрокой(ТекущееПояснение.НомерЭтапа);
		
		ГруппаЭтапа = Элементы.Найти(СтрШаблон("ГруппаЭтап%1", НомерЭтапаСтрокой));
		Если ГруппаЭтапа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ГруппаЭтапа.Видимость = Истина;
		ЭлементЭтап = Элементы.Найти(СтрШаблон("Этап%1", НомерЭтапаСтрокой));
		Если ЭлементЭтап = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементЭтап.Заголовок = ТекущееПояснение.Пояснение;
		ЭлементЗначениеЭтапа = Элементы.Найти(СтрШаблон("Этап%1Значение", НомерЭтапаСтрокой));
		Если ЭлементЗначениеЭтапа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементЗначениеЭтапа.Заголовок = ТекущееПояснение.Значение;
		
	КонецЦикла;
	
	// Скроем лишние этапы
	ВсегоЭтаповНаФорме = Элементы.РасчетСуммыВзноса.ПодчиненныеЭлементы.Количество();
	НеиспользуемыйЭтап = Пояснения.Количество() + 1;
	Пока НеиспользуемыйЭтап <= ВсегоЭтаповНаФорме Цикл
		
		ГруппаЭтапа = Элементы.Найти(СтрШаблон("ГруппаЭтап%1", НомерЭтапаСтрокой(НеиспользуемыйЭтап)));
		Если ГруппаЭтапа <> Неопределено Тогда
			ГруппаЭтапа.Видимость = Ложь;
		КонецЕсли;
		
		НеиспользуемыйЭтап = НеиспользуемыйЭтап + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НомерЭтапаСтрокой(НомераЭтапа)
	
	Возврат Формат(НомераЭтапа, "ЧДЦ=0; ЧН=0; ЧГ=0; ЧО=0");
	
КонецФункции

&НаСервереБезКонтекста
Функция ПодробноеПоясненияРасчетаВзносаСДоходов(Показатели)
	
	Результат = НовыйРезультатПоясненияРасчета();
	ФорматЧисел = ФорматЧиселВПоясненииРасчета();
	
	// Для расчетных показателей нужно свести к минимуму погрешность округления на форме
	// Например, понижающий коэффициент участвует в расчете без округления,
	// но на форме мы не можем показать это с точностью до 2х знаков, т.к. при расчете пользователя может не сойтись расчет
	ФорматЧиселРасширенный = ФорматЧиселВПоясненииРасчета(7);
	ЕстьДоходПоУСН = Показатели.ДоходыУСН > 0;
	ЕстьРасходПоУСН = Показатели.ДоходыУСН > 0 И Показатели.Расходы > 0;
	ЕстьДоходПоОСНО = ЗначениеЗаполнено(Показатели.ДоходыОСНО);
	СовмещениеСПатентом = ЗначениеЗаполнено(Показатели.ПотенциальныйДоход);
	ИсключатьВзносыИзРасходовУСН = Показатели.ИсключатьВзносыИзРасходовУСН;
	
	// Расчет показателей взноса с доходов
	Доходы = Показатели.ДоходыУСН + Показатели.ДоходыОСНО + Показатели.ПотенциальныйДоход;
	Расходы = Показатели.Расходы - Показатели.СтраховыеВзносы;
	
	Если Показатели.КоличествоДнейЕдиныйТариф > 0 Тогда
		ПонижающийКоэффициент = Показатели.КоличествоДнейЕдиныйТариф / Показатели.КоличествоДнейОсуществленияДеятельности;
	Иначе
		ПонижающийКоэффициент = 1;
	КонецЕсли;
	
	НалоговаяБаза = Показатели.ДоходыУСН + Макс(Показатели.ДоходыОСНО - Расходы, 0) + Показатели.ПотенциальныйДоход;
	Если ЕстьРасходПоУСН Тогда
		НалоговаяБаза = НалоговаяБаза - Расходы;
	КонецЕсли;
	СуммаВзноса = Макс(0, Показатели.ПроцентВзноса / 100 * (НалоговаяБаза - Показатели.ПорогДоходов) * ПонижающийКоэффициент);
	ПревышенПредел = (Показатели.ПредельнаяСумма < СуммаВзноса);
	
	// Оформление показателей для вывода в интерфейс
	ДоходыУСН          = Формат(Показатели.ДоходыУСН, ФорматЧисел);
	ДоходОСНО          = Формат(Показатели.ДоходыОСНО, ФорматЧисел);
	ПотенциальныйДоход = Формат(Показатели.ПотенциальныйДоход, ФорматЧисел);
	РасходОСНО         = Формат(Показатели.Расходы, ФорматЧисел);
	РасходУСН          = Формат(Показатели.Расходы, ФорматЧисел);
	СтраховыеВзносы    = Формат(Показатели.СтраховыеВзносы, ФорматЧисел);
	ПорогДоходов       = Формат(Показатели.ПорогДоходов, ФорматЧисел);
	
	НомерЭтапа = 0;
	
	ЕстьДоходПоУСН = Показатели.ДоходыУСН > 0;
	ЕстьДоходПоОСНО = ЗначениеЗаполнено(Показатели.ДоходыОСНО);
	СовмещениеСПатентом = ЗначениеЗаполнено(Показатели.ПотенциальныйДоход);
	ПрименяетсяПонижающийКоэффициент = ПонижающийКоэффициент <> 1;
	
	Если СовмещениеСПатентом И ЕстьДоходПоОСНО Тогда
		ОтрицательнаяБазаПоОСНО = (Показатели.ДоходыОСНО - Расходы) < 0;
	Иначе
		ОтрицательнаяБазаПоОСНО = Ложь;
	КонецЕсли;
	
	// Этап - Доходы
	Если ЕстьДоходПоУСН И СовмещениеСПатентом И ЕстьДоходПоОСНО Тогда
		// В течение года перешли с одной системы на другую
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Доходы = Доходы по УСН + Доходы по ОСНО + Потенциальный доход'");
		ДополнительныйТекст = СтрШаблон("(%1 + %2 + %3)", ДоходыУСН, ДоходОСНО, ПотенциальныйДоход);
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(Доходы, ФорматЧисел);
		
	ИначеЕсли ЕстьДоходПоУСН И СовмещениеСПатентом Тогда
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Доходы = Доходы по УСН + Потенциальный доход'");
		ДополнительныйТекст = СтрШаблон("(%1 + %2)", ДоходыУСН, ПотенциальныйДоход);
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(Доходы, ФорматЧисел);
	ИначеЕсли ЕстьДоходПоОСНО И СовмещениеСПатентом И Не ОтрицательнаяБазаПоОСНО Тогда
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Доходы = Доходы по ОСНО + Потенциальный доход'");
		ДополнительныйТекст = СтрШаблон("(%1 + %2)", ДоходОСНО, ПотенциальныйДоход);
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(Доходы, ФорматЧисел);
	КонецЕсли;
	
	Если ЕстьДоходПоОСНО Тогда
		// Этап - Расходы: только для плательщиков НДФЛ (из расходов исключаются страховые взносы)
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Расходы = Профессиональный вычет - Страховые взносы ИП'");
		ДополнительныйТекст = СтрШаблон("(%1 - %2)", РасходОСНО, СтраховыеВзносы);
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(Расходы, ФорматЧисел);
	КонецЕсли;
	
	Если ЕстьРасходПоУСН И ИсключатьВзносыИзРасходовУСН Тогда
		// Этап - Расходы по УСН (из расходов исключаются страховые взносы)
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Расходы = Сумма расходов УСН - Страховые взносы ИП'");
		ДополнительныйТекст = СтрШаблон("(%1 - %2)", РасходУСН, СтраховыеВзносы);
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(Расходы, ФорматЧисел);
	КонецЕсли;
	
	Если ПрименяетсяПонижающийКоэффициент Тогда
		// Этап - Понижающий коэффициент: если ИП в течение периода переехал в(из) новый регион
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Понижающий коэффициент = Количество дней единого тарифа / Всего дней в периоде'");
		ДополнительныйТекст = СтрШаблон("(%1 / %2)",
			Формат(Показатели.КоличествоДнейЕдиныйТариф, "ЧЦ=3; ЧДЦ=0"),
			Формат(Показатели.КоличествоДнейОсуществленияДеятельности, "ЧЦ=3; ЧДЦ=0"));
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный);
	КонецЕсли;
	
	Если ОтрицательнаяБазаПоОСНО Тогда
		
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Налоговая база по ОСНО = Доходы по ОСНО - Расходы'");
		ДополнительныйТекст = СтрШаблон("(%1 - %2)", ДоходОСНО, Формат(Расходы, ФорматЧисел));
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = "0,00";
		
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		Если ЕстьДоходПоУСН И ПревышенПредел Тогда
			// Этап - Расчетная сумма взноса
			
			Если ПрименяетсяПонижающийКоэффициент Тогда
				ОсновнойТекст = СтрШаблон(
					НСтр("ru = 'Расчетная сумма взноса = %1%% * (Доходы по УСН + Налоговая база по ОСНО + Потенциальный доход - %2) * Понижающий коэффициент'"),
					Показатели.ПроцентВзноса,
					ПорогДоходов);
				ДополнительныйТекст = СтрШаблон("%1%% * (%2 + 0,00 + %3 - %4) * %5",
					Показатели.ПроцентВзноса,
					ДоходыУСН,
					ПотенциальныйДоход,
					ПорогДоходов,
					Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
			Иначе
				ОсновнойТекст = СтрШаблон(
					НСтр("ru = 'Расчетная сумма взноса = %1%% * (Доходы по УСН + Налоговая база по ОСНО + Потенциальный доход - %2)'"),
					Показатели.ПроцентВзноса,
					ПорогДоходов);
				ДополнительныйТекст = СтрШаблон("%1%% * (%2 + 0,00 + %3 - %4)",
					Показатели.ПроцентВзноса,
					ДоходыУСН,
					ПотенциальныйДоход,
					ПорогДоходов);
			КонецЕсли;
			
			Этап.НомерЭтапа = НомерЭтапа;
			Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
			Этап.Значение   = Формат(СуммаВзноса, ФорматЧисел);
			
			ДобавитьПояснениеПредельнойСуммыВзноса(Результат, НомерЭтапа, СуммаВзноса, Показатели.ПредельнаяСумма);
		ИначеЕсли ПревышенПредел Тогда
			// Этап - Расчетная сумма взноса
			Если ПрименяетсяПонижающийКоэффициент Тогда
				ОсновнойТекст = СтрШаблон(
					НСтр("ru = 'Расчетная сумма взноса = %1%% * (Налоговая база по ОСНО + Потенциальный доход - %2) * Понижающий коэффициент'"),
					Показатели.ПроцентВзноса,
					ПорогДоходов);
				ДополнительныйТекст = СтрШаблон("%1%% * (0,00 + %2 - %3) *%4",
					Показатели.ПроцентВзноса,
					ПотенциальныйДоход,
					ПорогДоходов,
					Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
			Иначе
				ОсновнойТекст = СтрШаблон(
					НСтр("ru = 'Расчетная сумма взноса = %1%% * (Налоговая база по ОСНО + Потенциальный доход - %2)'"),
					Показатели.ПроцентВзноса,
					ПорогДоходов);
				ДополнительныйТекст = СтрШаблон("%1%% * (0,00 + %2 - %3)",
					Показатели.ПроцентВзноса,
					ПотенциальныйДоход,
					ПорогДоходов);
			КонецЕсли;
			
			Этап.НомерЭтапа = НомерЭтапа;
			Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
			Этап.Значение   = Формат(СуммаВзноса, ФорматЧисел);
			
			ДобавитьПояснениеПредельнойСуммыВзноса(Результат, НомерЭтапа, СуммаВзноса, Показатели.ПредельнаяСумма);
		Иначе
			// Этап - Сумма взноса
			Если ПрименяетсяПонижающийКоэффициент Тогда
				ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
					НСтр("ru = '<b>Сумма взноса</b> = %1% * (Налоговая база по ОСНО + Потенциальный доход - %2) * Понижающий коэффициент'"),
					Показатели.ПроцентВзноса,
					ПорогДоходов);
				ДополнительныйТекст = СтрШаблон("%1%% * (0,00 + %2 - %3) * %4",
					Показатели.ПроцентВзноса,
					ПотенциальныйДоход,
					ПорогДоходов,
					Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
			Иначе
				ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
					НСтр("ru = '<b>Сумма взноса</b> = %1% * (Налоговая база по ОСНО + Потенциальный доход - %2)'"),
					Показатели.ПроцентВзноса,
					ПорогДоходов);
				ДополнительныйТекст = СтрШаблон("%1%% * (0,00 + %2 - %3)",
					Показатели.ПроцентВзноса,
					ПотенциальныйДоход,
					ПорогДоходов);
			КонецЕсли;
			
			Этап.НомерЭтапа = НомерЭтапа;
			Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
			Этап.Значение   = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>%1</b>'"),
				Формат(СуммаВзноса, ФорматЧисел));
		КонецЕсли;
		
	ИначеЕсли ПревышенПредел И ЕстьДоходПоОСНО Тогда
		// Этап - Расчетная сумма взноса, если ИП плательщик НДФЛ
		
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		Если ПрименяетсяПонижающийКоэффициент Тогда
			ОсновнойТекст = СтрШаблон(НСтр("ru = 'Расчетная сумма взноса = %1%% * (Доходы - Расходы - %2) * Понижающий коэффициент'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3 - %4) * %5",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				Формат(Расходы, ФорматЧисел),
				ПорогДоходов,
				Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
		Иначе
			ОсновнойТекст = СтрШаблон(НСтр("ru = 'Расчетная сумма взноса = %1%% * (Доходы - Расходы - %2)'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3 - %4)",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				Формат(Расходы, ФорматЧисел),
				ПорогДоходов);
		КонецЕсли;
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(СуммаВзноса, ФорматЧисел);
		
		ДобавитьПояснениеПредельнойСуммыВзноса(Результат, НомерЭтапа, СуммаВзноса, Показатели.ПредельнаяСумма);
	ИначеЕсли ПревышенПредел Тогда
		// Этап - Расчетная сумма взноса для УСН + ПСН
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		Если ПрименяетсяПонижающийКоэффициент И ИсключатьВзносыИзРасходовУСН Тогда
			ОсновнойТекст = СтрШаблон(НСтр("ru = 'Расчетная сумма взноса = %1%% * (Доходы - Расходы - %2) * Понижающий коэффициент'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3 - %4) * %5",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				Расходы,
				ПорогДоходов,
				Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
		ИначеЕсли ПрименяетсяПонижающийКоэффициент Тогда
			ОсновнойТекст = СтрШаблон(НСтр("ru = 'Расчетная сумма взноса = %1%% * (Доходы - %2) * Понижающий коэффициент'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3) * %4",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				ПорогДоходов,
				Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
		ИначеЕсли ИсключатьВзносыИзРасходовУСН Тогда
			ОсновнойТекст = СтрШаблон(НСтр("ru = 'Расчетная сумма взноса = %1%% * (Доходы - Расходы - %2)'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3 - %4)",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				Расходы,
				ПорогДоходов);
		Иначе
			ОсновнойТекст = СтрШаблон(НСтр("ru = 'Расчетная сумма взноса = %1%% * (Доходы - %2)'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3)",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				ПорогДоходов);
		КонецЕсли;
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(СуммаВзноса, ФорматЧисел);
		
		ДобавитьПояснениеПредельнойСуммыВзноса(Результат, НомерЭтапа, СуммаВзноса, Показатели.ПредельнаяСумма);
	ИначеЕсли ЕстьДоходПоОСНО Тогда
		// Этап - Сумма взноса, если ИП - плательщик НДФЛ
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		Если ПрименяетсяПонижающийКоэффициент Тогда
			ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Сумма взноса</b> = %1% * (Доходы - Расходы - %2) * Понижающий коэффициент'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3 - %4) * %5",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				Формат(Расходы, ФорматЧисел),
				ПорогДоходов,
				Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
		Иначе
			ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Сумма взноса</b> = %1% * (Доходы - Расходы - %2)'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3 - %4)",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				Формат(Расходы, ФорматЧисел),
				ПорогДоходов);
		КонецЕсли;
			
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<b>%1</b>'"),
			Формат(СуммаВзноса, ФорматЧисел));
	ИначеЕсли ЕстьРасходПоУСН Тогда
		// Этап - Сумма взноса для ИП на УСН Расход + ПСН
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		НаименованиеРасхода = ?(ИсключатьВзносыИзРасходовУСН, НСтр("ru = 'Расходы'"), НСтр("ru = 'Расходы УСН'"));
		
		Если ПрименяетсяПонижающийКоэффициент Тогда
			ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Сумма взноса</b> = %1% * (Доходы - Расходы - %2) * Понижающий коэффициент'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3 - %4) * %5",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				Формат(Расходы, ФорматЧисел),
				ПорогДоходов,
				Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
		Иначе
			ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Сумма взноса</b> = (Доходы - %3 - %1) * %2%'"),
				ПорогДоходов,
				Показатели.ПроцентВзноса,
				НаименованиеРасхода);
			ДополнительныйТекст = СтрШаблон("(%1 - %2 - %3) * %4%%",
				Формат(Доходы, ФорматЧисел),
				Формат(Расходы, ФорматЧисел),
				ПорогДоходов,
				Показатели.ПроцентВзноса);
		КонецЕсли;
			
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<b>%1</b>'"),
			Формат(СуммаВзноса, ФорматЧисел));
	Иначе
		// Этап - Сумма взноса дл ИП на УСН + ПСН
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		Если ПрименяетсяПонижающийКоэффициент Тогда
			ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Сумма взноса</b> = %1% * (Доходы - %2) * Понижающий коэффициент'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3) * %4",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				ПорогДоходов,
				Формат(ПонижающийКоэффициент, ФорматЧиселРасширенный));
		Иначе
			ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Сумма взноса</b> = %1% * (Доходы - %2)'"),
				Показатели.ПроцентВзноса,
				ПорогДоходов);
			ДополнительныйТекст = СтрШаблон("%1%% * (%2 - %3)",
				Показатели.ПроцентВзноса,
				Формат(Доходы, ФорматЧисел),
				ПорогДоходов);
		КонецЕсли;
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<b>%1</b>'"),
			Формат(СуммаВзноса, ФорматЧисел));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьПояснениеПредельнойСуммыВзноса(Результат, НомерЭтапа, СуммаВзноса, ПредельнаяСумма)
	
	// Этап - Сумма взноса больше предельной суммы согласно ст. 430 НК РФ
	НомерЭтапа = НомерЭтапа + 1;
	ФорматЧисел = ФорматЧиселВПоясненииРасчета();
	ПредельнаяСуммаСтрокой = Формат(ПредельнаяСумма, ФорматЧисел);
	СуммаВзносаСтрокой = Формат(СуммаВзноса, ФорматЧисел);
	
	Этап = Результат.Добавить();
	
	ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = '<b>Сумма взноса</b>: не может превышать предельный размер'"));
	ДополнительныйТекст = СтрШаблон("(%1 > %2)", СуммаВзносаСтрокой, ПредельнаяСуммаСтрокой);
	
	Этап.НомерЭтапа = НомерЭтапа;
	Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
	Этап.Значение   = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = '<b>%1</b>'"),
		Формат(ПредельнаяСумма, ФорматЧисел));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовыеПоказателиРасчетаВзносаСДоходов()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ДоходыУСН", 0);
	Результат.Вставить("ДоходыОСНО", 0);
	Результат.Вставить("Расходы", 0);
	Результат.Вставить("ПотенциальныйДоход", 0);
	Результат.Вставить("ПредельнаяСумма", 0);
	Результат.Вставить("СтраховыеВзносы", 0);
	Результат.Вставить("ПроцентВзноса", 0);
	Результат.Вставить("ПорогДоходов", 0);
	Результат.Вставить("КоличествоДнейОсуществленияДеятельности", 0);
	Результат.Вставить("КоличествоДнейЕдиныйТариф", 0);
	Результат.Вставить("ИсключатьВзносыИзРасходовУСН", Ложь);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ФорматЧиселВПоясненииРасчета(ТочностьОкругления = 2)
	
	Возврат СтрШаблон("Л=ru_RU; ЧДЦ=%1; ЧН=0,00; ЧГ=3,0", ТочностьОкругления);
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйРезультатПоясненияРасчета()
	
	ТипФорматированнаяСтрока = Новый ОписаниеТипов("ФорматированнаяСтрока, Строка");
	
	Пояснения = Новый ТаблицаЗначений;
	
	Пояснения.Колонки.Добавить("НомерЭтапа", ОбщегоНазначения.ОписаниеТипаЧисло(1, 0));
	Пояснения.Колонки.Добавить("Пояснение",  ТипФорматированнаяСтрока);
	Пояснения.Колонки.Добавить("Значение",   ТипФорматированнаяСтрока);
	
	Возврат Пояснения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПояснениеЭтапа(ОсновнойТекст, Дополнение)
	
	СтрокиПояснения = Новый Массив;
	СтрокиПояснения.Добавить(ОсновнойТекст);
	СтрокиПояснения.Добавить(Символы.ПС);
	СтрокиПояснения.Добавить(Дополнение);
	
	Возврат Новый ФорматированнаяСтрока(СтрокиПояснения);
	
КонецФункции

&НаСервере
Функция ПараметрыПодробногоПояснения()
	
	Результат = НовыеПоказателиРасчетаВзносаСДоходов();
	Результат.ДоходыУСН = СуммаДоходаУСН;
	Результат.ПотенциальныйДоход = ПотенциальноВозможныйДоход;
	Результат.ПредельнаяСумма = ПредельнаяСуммаВзноса;
	Результат.ПроцентВзноса = ПроцентСДоходов;
	Результат.ПорогДоходов = УчетСтраховыхВзносовИП.ПредельныйДоходНеОблагаемыйВзносамиВПФР();
	Результат.КоличествоДнейОсуществленияДеятельности = КоличествоДнейОсуществленияДеятельности;
	Результат.КоличествоДнейЕдиныйТариф = КоличествоДнейЕдиныйТариф;
	Результат.ИсключатьВзносыИзРасходовУСН = ИсключатьВзносыИзРасходовУСН;
	
	Если ПлательщикНДФЛ Тогда
		
		ДоходыПоКУДИР = 0;
		СтраховыеВзносыИП = 0;
		РасходыПоОСНО = 0;
		
		// При расчете взноса с доходов из доходов вычитывается профессиональный вычет за исключением страховых взносов
		// В форме расшифровке показываем в показателях доход как есть в КУДИР в таблице 6-1
		// Доходы вычисляем за периоды, когда уплачивается взнос с доходов
		
		// Взнос с доходов платится в случае, когда у ИП единый тариф - п.1.2 ст. 430 НК РФ
		// или ИП переехал в регион с пониженным тарифом (Письмо ФНС России от 5 марта 2024 г. № БС-4-11/2502@)
		Если ЗначениеЗаполнено(КоличествоДнейЕдиныйТариф) Тогда
			
			РасходыДляРасчета = УчетДоходовИРасходовПредпринимателя.РасходыДляРасчетаСтраховыхВзносов(
				Организация,
				НачалоГода(Период),
				КонецГода(Период));
			
			ДоходыПоКУДИР = СуммаДоходаИП;
			СтраховыеВзносыИП = СтраховыеВзносыИП + РасходыДляРасчета.УплаченныеВзносыИП;
			РасходыПоОСНО = РасходыПоОСНО + РасходыДляРасчета.ВсегоРасходов;
			
		Иначе
			
			ПериодыУплатыВзносаСДоходов = УчетСтраховыхВзносовИП.ПериодыУплатыВзносаСДоходов(Организация, КонецГода(Период));
			
			Для Каждого ТекущийПериод Из ПериодыУплатыВзносаСДоходов Цикл
				
				ДоходыПоКУДИР = ДоходыПоКУДИР + УчетДоходовИРасходовПредпринимателя.СуммаДоходаДляРасчетаСтраховыхВзносов(
					Организация,
					ТекущийПериод.ДатаНачала,
					ТекущийПериод.ДатаОкончания);
				
				РасходыДляРасчета = УчетДоходовИРасходовПредпринимателя.РасходыДляРасчетаСтраховыхВзносов(
					Организация,
					ТекущийПериод.ДатаНачала,
					ТекущийПериод.ДатаОкончания);
				
				СтраховыеВзносыИП = СтраховыеВзносыИП + РасходыДляРасчета.УплаченныеВзносыИП;
				РасходыПоОСНО = РасходыПоОСНО + РасходыДляРасчета.ВсегоРасходов;
				
			КонецЦикла;
			
		КонецЕсли;
		
		СуммаДоходаИП = ДоходыПоКУДИР + (РасходыПоОСНО - СтраховыеВзносыИП);
		Результат.ДоходыОСНО = СуммаДоходаИП;
		Результат.Расходы = РасходыПоОСНО;
		Результат.СтраховыеВзносы = СтраховыеВзносыИП;
	ИначеЕсли ПрименяетсяУСН И ПрименяетсяУСНРасход Тогда
		Если ИсключатьВзносыИзРасходовУСН Тогда
			СтраховыеВзносыИП = УчетУСН.ВзносыИспользованныеДляУменьшенияУСН(Организация, КонецКвартала(Период), Ложь, Ложь);
			СуммаРасходаУСН = СуммаРасходаУСН + СтраховыеВзносыИП;
			Результат.СтраховыеВзносы = СтраховыеВзносыИП;
		КонецЕсли;
		Результат.Расходы = СуммаРасходаУСН;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПоказыватьПодробныйРасчет()
	
	// Показываем расширенное пояснение в сложных случаях, когда в расчете участвует несколько показателей
	
	Если ПлательщикНДФЛ Тогда
		// По ОСНО нужно отдельно показать вычет признанных страховых взносов
		Возврат Истина;
	КонецЕсли;
	
	Если Всего >= ПредельнаяСуммаВзноса Тогда
		// При превышении предельной суммы по ст. 430 НК нужно отдельно вывести пояснение, что расчетная сумма превысила порог
		Возврат Истина;
	КонецЕсли;
	
	Если ПрименяетсяУСНРасход Тогда
		// Для УСН Доход-Расход показываем подробно
		Возврат Истина;
	КонецЕсли;
	
	Возврат ПрименяетсяУСНПатент И ПрименяетсяУСН Или ЗначениеЗаполнено(КоличествоДнейЕдиныйТариф);
	
КонецФункции

#КонецОбласти

#Область РасшифровкаПоказателей

&НаКлиенте
Процедура РасшифроватьДоходыРасходыИП()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РежимРасшифровки", Истина);
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("НачалоПериода", НачалоГода(Период));
	СтруктураПараметров.Вставить("КонецПериода", КонецКвартала(Период));
	
	ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходовПредпринимателя.Форма",
		СтруктураПараметров,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПотенциальныйДоход()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Отбор", Новый Структура("Организация", Организация));
	СтруктураПараметров.Вставить("Период", Период);
	
	ОткрытьФорму("Документ.ОперацияСПатентом.ФормаСписка", СтруктураПараметров, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьСтраховыеВзносыИП()
	
	ПользовательскиеНастройки  = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация",   Организация);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоГода(Период));
	
	Если ИсключатьВзносыИзРасходовУСН И ПрименяетсяУСНРасход Тогда
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода", КонецКвартала(Период));
		ВидОтчета = "ПодлежащиеВзносыУСН";
	Иначе
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода", КонецГода(Период));
		ВидОтчета = "СтраховыеВзносыИП";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидОтчета", ВидОтчета);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	
	ОткрытьФорму("Отчет.РасшифровкаПлатежейВБюджет.Форма",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьДоходыУСН()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	
	Если УчитыватьРасходы Тогда
		
		НачалоПериода = НачалоГода(Период);
		КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ПериодичностьНачисления, Период);
		
		ПараметрыФормы.Вставить("НачалоПериода", НачалоПериода);
		ПараметрыФормы.Вставить("КонецПериода", КонецПериода);
		ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
		ПараметрыФормы.Вставить("РежимРасшифровкиНалоговойБазы", Истина);
		
		ПолноеИмяФормыРасшифровки = "Отчет.КнигаУчетаДоходовИРасходов.Форма.ФормаОтчета";
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("НачалоПериода", НачалоГода(Период));
		ПараметрыФормы.Вставить("КонецПериода", КонецКвартала(Период));
		
		Если ИспользуетсяИнтеграцияСБанком Тогда
			ПолноеИмяФормыРасшифровки = "Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаДоходовИнтеграцияСБанком";
		Иначе
			ПолноеИмяФормыРасшифровки = "Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаДоходов";
		КонецЕсли;
		
	КонецЕсли;
	
	ОткрытьФорму(ПолноеИмяФормыРасшифровки, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтеграцияСБанком

&НаСервере
Процедура ВывестиПлатежиИБаннерСостоянияОтправкиНаФорму()
	
	СостоянияИнтеграцииДокументов = РегистрыСведений.ДокументыИнтеграцииСБанком.СостоянияИнтеграцииДокументов(
		ДокументыОплаты(ЭтотОбъект));
	
	ПлатежиДляОтображения = ПомощникиПоУплатеНалоговИВзносов.ПлатежиДляОтображения(
		Платежи,
		СостоянияИнтеграцииДокументов);
	ПомощникиПоУплатеНалоговИВзносов.ОтобразитьПлатежи(ЭтотОбъект, ПлатежиДляОтображения, "Платежи");
	
	ИнтеграцияСБанкамиФормы.ПолучитьДанныеИПоказатьБаннерСостоянияОтправки(ЭтотОбъект, СостоянияИнтеграцииДокументов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ХэшДанныхБаннера(Знач ДокументыОплаты)
	
	Возврат ИнтеграцияСБанкамиФормы.ХэшДанныхБаннера(ДокументыОплаты);
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьОбновлениеБаннераСостоянияОтправки()
	Если ИнтервалПроверкиСостоянияИнтеграцииСБанком > 0 Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЗапуститьОбновлениеБаннераСостояниеОтправки",
			ИнтервалПроверкиСостоянияИнтеграцииСБанком,
			Истина);
	Иначе
		ПередНачаломДлительнойОперации();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗапуститьОбновлениеБаннераСостояниеОтправки() Экспорт
	
	Если ХэшДанныхБаннера(ДокументыОплаты(ЭтотОбъект)) <> ХешДанныхБаннера Тогда
		ОбновитьПлатежныеДокументыНаФорме();
	КонецЕсли;
	ЗапуститьОбновлениеБаннераСостоянияОтправки();
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаАктуальностиДанных

&НаКлиенте
Процедура ПередНачаломДлительнойОперации()
	
	Если ТребуетсяАктуализироватьРасчетВзносов И Не ДанныеУчетаАктуальны Тогда
		
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальностьРасчетаВзносов", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальностьРасчетаВзносов()
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальностьРасчетаВзносов");
	ЗаданиеАктуализации = НайтиФоновоеЗаданиеАктуализацииНачисленияПоВзносам(Организация);
	
	Если Не ЗначениеЗаполнено(ЗаданиеАктуализации) Тогда
		ПодключитьОбработчикОжидания("Подключаемый_АктуализироватьРасчетВзносов", 0.1, Истина);
	Иначе
		ДанныеУчетаАктуальны = Ложь;
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальностьРасчетаВзносов", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиФоновоеЗаданиеАктуализацииНачисленияПоВзносам(Организация)
	
	ОтборЗаданий = Новый Структура;
	ОтборЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	ОтборЗаданий.Вставить("Наименование", "Обработки.РасчетСтраховыхВзносовИП.АктуализироватьРасчет");
	
	УстановитьПривилегированныйРежим(Истина);
	МассивФоновыхЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборЗаданий);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если МассивФоновыхЗаданий.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат МассивФоновыхЗаданий[0].УникальныйИдентификатор;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_АктуализироватьРасчетВзносов()
	
	ДлительнаяОперация = АктуализироватьРасчетВзносов();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатАктуализацииРасчетаВзносов", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения(
		"СообщитьПрогрессАктуализацииРасчетаВзносов",
		ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция АктуализироватьРасчетВзносов()
	
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("Организация", Организация);
	ПараметрыРасчета.Вставить("Период", Период);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"Обработки.РасчетСтраховыхВзносовИП.АктуализироватьРасчет",
		ПараметрыРасчета);
	
КонецФункции

&НаКлиенте
Процедура СообщитьПрогрессАктуализацииРасчетаВзносов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И ТипЗнч(Результат) = Тип("Структура")
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ПрогрессОбновлен", Ложь) Тогда
		
		ПроцентПрогресса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Процент", 0);
		ПроцентПредставление = Формат(ПроцентПрогресса, "ЧЦ=2; ЧДЦ=0; ЧГ=0; ЧФ=Ч%");
		
		СтатусФоновогоЗадания = СтрШаблон(НСтр("ru = 'Выполняется расчет взносов...%1'"), ПроцентПредставление);
		
	ИначеЕсли ТребуетсяАктуализироватьРасчетВзносов И Не ДанныеУчетаАктуальны Тогда
		СтатусФоновогоЗадания = НСтр("ru = 'Выполняется расчет взносов...'");
	Иначе
		СтатусФоновогоЗадания = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатАктуализацииРасчетаВзносов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Или Не ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	РезультатАктуализации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если РезультатАктуализации.Выполнено Тогда
		
		ДанныеУчетаАктуальны = Истина;
		ЗаполнитьСтраховыеВзносыНаСервере();
		Оповестить("АктуализацияЗавершенаУспешно", Новый Структура("Организация", Организация))
		
	Иначе
		
		ЗакрытиеМесяцаКлиент.ПоказатьОшибкиАктуализации(ЭтотОбъект,
			РезультатАктуализации,
			НСтр("ru='Расчет взносов не выполнен. Обнаружены ошибки.
					 |Попробуйте повторно выполнить расчет.'"));
		Оповестить("АктуализацияОтменена", Новый Структура("Организация", Организация));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	Элементы.ПоказателиРасчета.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаРасчетВзноса.РастягиватьПоГоризонтали = Истина;
	Элементы.ПоясненияРасчета.РастягиватьПоГоризонтали = Истина;
	Элементы.РасчетУпрощенный.РастягиватьПоГоризонтали = Истина;
	Элементы.СуммаДоходаИП.РастягиватьПоГоризонтали = Истина;
	Элементы.СуммаДоходаУСН.РастягиватьПоГоризонтали = Истина;
	Элементы.ПотенциальноВозможныйДоход.РастягиватьПоГоризонтали = Истина;
	Элементы.Всего.РастягиватьПоГоризонтали = Истина;
	
	Элементы.ГруппаОтправкаПлатежейВБанк.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаОтправкаПлатежейВБанк.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ГруппаОтправкаПлатежейВБанк.РастягиватьПоГоризонтали = Истина;
	
	Элементы.ГруппаКнопкиОплаты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаКнопкиОплаты.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ГруппаКнопкиОплаты.РастягиватьПоГоризонтали = Истина;
	
	Элементы.ГруппаФункцииОплаты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.ИспользоватьОстатокЕНС.ПоложениеЗаголовка=ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.СуммаСписанияСЕНС.ПоложениеЗаголовка=ПоложениеЗаголовкаЭлементаФормы.Авто;
	Элементы.СуммаСписанияСЕНС.Заголовок = НСтр("ru = 'Сумма списания'");
	Элементы.Сумма.ПоложениеЗаголовка=ПоложениеЗаголовкаЭлементаФормы.Авто;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойМобильныйКлиент()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ОплатитьПоБанку.Видимость Тогда
		МобильныйКлиентБПФормаОбъекта.ОформитьАкцентнуюКнопкуПанели(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.ОплатитьПоБанку));
		МобильныйКлиентБПФормаОбъекта.ОформитьНейтральнуюКнопкуПанели(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.ОплатитьНаличными));
	Иначе
		МобильныйКлиентБПФормаОбъекта.ОформитьАкцентнуюКнопкуПанели(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.ОплатитьНаличными));
	КонецЕсли;
	
	Если Элементы.ОтправитьПлатежиВБанк.Видимость Тогда
		МобильныйКлиентБПФормаОбъекта.ОформитьАкцентнуюКнопкуПанели(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.ОтправитьПлатежиВБанк));
		МобильныйКлиентБПФормаОбъекта.ОформитьНейтральнуюКнопкуПанели(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.ВыгрузитьПлатежи));
	Иначе
		МобильныйКлиентБПФормаОбъекта.ОформитьАкцентнуюКнопкуПанели(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.ВыгрузитьПлатежи));
	КонецЕсли;
	
	Элементы.ЗаголовокКОплате.Видимость = Ложь;
КонецПроцедуры

#КонецОбласти
