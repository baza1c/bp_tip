#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяВзноса = Параметры.ИмяВзноса;
	Всего     = Параметры.Всего;
	Уплачено  = Параметры.Уплачено;
	Сумма     = Параметры.Сумма;
	
	Период         = КонецКвартала(Параметры.Период);
	Организация    = Параметры.Организация;
	ПлательщикЕНП  = Параметры.ПлательщикЕНП;
	
	Если ПлательщикЕНП И Параметры.Свойство("ЗаявленияОЗачете") Тогда
		// В случае ЕНП сумма зарезервированная уже отображается в КУДИР:
		// если за период уже зарезервирован взнос, тогда показываем простую формулу 1% *(доходы - расходы),
		// иначе будет отбражена расчетная формула: (доходы - расходы)/101
		ВидНалогаСвышеПредела = УчетСтраховыхВзносовИП.ВидСтраховогоВзносаИП(Истина);
		ВидыНалогов = Новый Массив;
		ВидыНалогов.Добавить(Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоВиду(ВидНалогаСвышеПредела));
		ЗарезервированноПоДокументам = ЕдиныйНалоговыйСчет.ЗарезервированыеСуммыПоВидамНалогов(
			ВидыНалогов,
			Параметры.ЗаявленияОЗачете);
		СуммаЗарезервированная = ЗарезервированноПоДокументам.Итог("Сумма");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.СтруктураДоходов) Тогда
		СтруктураДоходов = Параметры.СтруктураДоходов;
	Иначе
		СтруктураДоходов = УчетСтраховыхВзносовИП.СтруктураДоходовПоВидамДеятельности(
			Организация,
			НачалоГода(Период),
			Период);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДоходов);
	
	Если ЗначениеЗаполнено(Параметры.ПараметрыРасчетаВзносаСДоходов) Тогда
		ПараметрыРасчетаВзносаСДоходов = Параметры.ПараметрыРасчетаВзносаСДоходов;
	Иначе
		ПараметрыРасчетаВзносаСДоходов = УчетСтраховыхВзносовИП.НовыеПараметрыРасчетаВзносаСДоходов();
		СтраховыеВзносыКУплате = Обработки.РасчетСтраховыхВзносовИП.СтраховыеВзносыКУплате(Организация,
			Период,
			УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, Период));
		ЗаполнитьЗначенияСвойств(ПараметрыРасчетаВзносаСДоходов, СтраховыеВзносыКУплате.ПараметрыРасчетаВзносаСДоходов);
		Платежи.Загрузить(СтраховыеВзносыКУплате.СтраховыеВзносыСДоходовУплаченные);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРасчетаВзносаСДоходов);
	ПредельнаяСуммаВзноса = Параметры.ПредельнаяСуммаВзноса;
	
	ЗаполнитьПояснения();
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресТаблицыПлатежей) Тогда
		ТаблицаПлатежей = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыПлатежей);
		Если ТипЗнч(ТаблицаПлатежей) = Тип("ТаблицаЗначений") Тогда
			Платежи.Загрузить(ТаблицаПлатежей);
			Для Каждого Платеж Из Платежи Цикл
				Платеж.ПредставлениеДокумента = ПомощникиПоУплатеНалоговИВзносов.ПредставлениеДокументаОплаты(Платеж, Истина);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ПомощникиПоУплатеНалоговИВзносов.ОтобразитьПлатежиССуммойВОтдельнойКолонке(ЭтотОбъект, Платежи, "Платеж");
	
	ИспользуетсяИнтеграцияСБанком = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
	Заголовок = Параметры.Заголовок;
	
	Элементы.ЗаголовокКОплате.Заголовок = СтрШаблон(НСтр("ru = 'К оплате за %1 квартал'"), Формат(Период, "ДФ=к"));
	
	УстановитьФункциональныеОпцииФормы();
	ЗаполнитьПодробныйРасчет();
	
	УправлениеФормой();
	
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененВидДеятельностиОрганизации" Тогда
		
		Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Организации") И Параметр = Организация Тогда
			ЗаполнитьСтраховыеВзносыНаСервере();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзменениеВыписки" 
		ИЛИ ИмяСобытия = "ОбновитьПоказателиРасчетаУСН" И Не ЗначениеЗаполнено(Параметр)
		ИЛИ ИмяСобытия = "ИзменениеЗаписиКУДиР" Тогда
		
		ЗаполнитьСтраховыеВзносыНаСервере();
		
	ИначеЕсли ИмяСобытия = "Запись_ПлатежныйДокумент_УплатаНалогов" Тогда
		
		Налог = Неопределено;
		
		Если ТипЗнч(Параметр) = Тип("Структура")
			И Параметр.Свойство("Организация") И Параметр.Организация = Организация
			И Параметр.Свойство("Налог", Налог) И ЗначениеЗаполнено(Налог)
			И Параметр.Свойство("Оплачено") И Параметр.Оплачено = Истина
			И ЭтоФиксированныеВзносы(Налог) Тогда
			
			ЗаполнитьСтраховыеВзносыНаСервере();
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "АктуализацияЗавершенаУспешно"
		И ТипЗнч(Параметр) = Тип("Структура")
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Организация", Неопределено) = Организация Тогда
		
		ЗаполнитьСтраховыеВзносыНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СуммаДоходаИПНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыРасходыИП();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходаУСННажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если УчитыватьРасходы Тогда
		
		НачалоПериода = НачалоГода(Период);
		КонецПериода  = ИнтерфейсыВзаимодействияБРОКлиентСервер.КонецПериода(ПериодичностьНачисления, Период);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация",                   Организация);
		ПараметрыФормы.Вставить("НачалоПериода",                 НачалоПериода);
		ПараметрыФормы.Вставить("КонецПериода",                  КонецПериода);
		ПараметрыФормы.Вставить("РежимРасшифровки",              Истина);
		ПараметрыФормы.Вставить("РежимРасшифровкиНалоговойБазы", Истина);
		
		ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходов.Форма.ФормаОтчета", ПараметрыФормы, ЭтотОбъект);
		
	Иначе
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Организация",   Организация);
		СтруктураПараметров.Вставить("НачалоПериода", НачалоГода(Период));
		СтруктураПараметров.Вставить("КонецПериода",  КонецКвартала(Период));
		
		ИмяФормыРасшифровки = ?(ИспользуетсяИнтеграцияСБанком, "РасшифровкаДоходовИнтеграцияСБанком", "РасшифровкаДоходов");
		ПолноеИмяФормыРасшифровки = СтрШаблон("Обработка.ПомощникРасчетаНалогаУСН.Форма.%1", ИмяФормыРасшифровки);
		
		ОткрытьФорму(ПолноеИмяФормыРасшифровки, СтруктураПараметров, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПотенциальноВозможныйДоходНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьПотенциальныйДоход();
	
КонецПроцедуры

&НаКлиенте
Процедура ВмененныйДоходНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",   Организация);
	СтруктураПараметров.Вставить("НачалоПериода", НачалоГода(Период));
	СтруктураПараметров.Вставить("КонецПериода",  КонецКвартала(Период));
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ВмененныйДоходПоВидамДеятельностиЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ВидыДеятельностиЕНВД.Форма.ВмененныйДоход",
		СтруктураПараметров,
		ЭтотОбъект,
		Истина,
		,
		,
		ОписаниеОповещенияОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область ПодробныйРасчет

&НаКлиенте
Процедура ДоходыПоОСНОНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыРасходыИП();
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыПоОСНОНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьДоходыРасходыИП();
	
КонецПроцедуры

&НаКлиенте
Процедура ПотенциальныйДоходНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьПотенциальныйДоход();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховыеВзносыИПНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьСтраховыеВзносыИП();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ОповеститьОВыбореИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОплатуЧерезБанк(Команда)
	
	Перем ВидНалога, ВидНалоговогоОбязательства, БанковскийСчет;
	
	Если ПлательщикЕНП Тогда
		ВидНалога                  = ПредопределенноеЗначение("Перечисление.ВидыНалогов.ЕдиныйНалоговыйПлатеж");
		ВидНалоговогоОбязательства = ПредопределенноеЗначение("Перечисление.ВидыПлатежейВГосБюджет.Налог");
	Иначе
		ВидНалога                  = ПредопределенноеЗначение("Перечисление.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть");
		ВидНалоговогоОбязательства = ПредопределенноеЗначение("Перечисление.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела");
	КонецЕсли;
	
	Если ИспользуетсяИнтеграцияСБанком Тогда
		БанковскийСчет = ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка");
	КонецЕсли;
	
	ПомощникиПоУплатеНалоговИВзносовКлиент.СоздатьОплатуЧерезБанк(
		Организация,
		Период,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидНалога),
		ВидНалоговогоОбязательства,
		БанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОплатуНаличными(Команда)
	
	Перем ВидНалога, ВидНалоговогоОбязательства;
	
	Если ПлательщикЕНП Тогда
		ВидНалога                  = ПредопределенноеЗначение("Перечисление.ВидыНалогов.ЕдиныйНалоговыйПлатеж");
		ВидНалоговогоОбязательства = ПредопределенноеЗначение("Перечисление.ВидыПлатежейВГосБюджет.Налог");
	Иначе
		ВидНалога                  = ПредопределенноеЗначение("Перечисление.ВидыНалогов.ФиксированныеВзносы_ПФР_СтраховаяЧасть");
		ВидНалоговогоОбязательства = ПредопределенноеЗначение("Перечисление.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела");
	КонецЕсли;
	
	ПомощникиПоУплатеНалоговИВзносовКлиент.СоздатьОплатуНаличными(
		Организация,
		Период,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидНалога),
		ВидНалоговогоОбязательства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	НачалоПериода = НачалоГода(Период);
	КонецПериода  = КонецГода(Период);
	
	Если УчетнаяПолитика.Существует(Организация, КонецПериода) Тогда
		ПлательщикНДФЛ       = УчетнаяПолитика.ПлательщикНДФЛЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПрименяетсяУСН       = УчетнаяПолитика.ПрименяетсяУСНЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Организация, НачалоПериода, КонецПериода);
		ПлательщикЕНВД       = УчетнаяПолитика.ПлательщикЕНВДЗаПериод(Организация, НачалоПериода, КонецПериода);
	Иначе
		День = 24 * 60 * 60;
		ПлательщикНДФЛ       = УчетнаяПолитика.ПлательщикНДФЛ(Организация, КонецПериода + День);
		ПрименяетсяУСН       = УчетнаяПолитика.ПрименяетсяУСН(Организация, КонецПериода + День);
		ПрименяетсяУСНПатент = УчетнаяПолитика.ПрименяетсяУСНПатент(Организация, КонецПериода + День);
		ПлательщикЕНВД       = УчетнаяПолитика.ПлательщикЕНВД(Организация, КонецПериода + День);
	КонецЕсли;
	
	Если ПлательщикНДФЛ Или УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходыЗаПериод(Организация, НачалоПериода, КонецПериода) Тогда
		УчитыватьРасходы = Истина;
	Иначе
		УчитыватьРасходы = Ложь;
	КонецЕсли;
	
	Если ПлательщикНДФЛ И УчетДоходовИРасходовПредпринимателя.ИсключатьВзносыИППриРасчетеВзносаСДоходов(Период) Тогда
		ПоказыватьПодробныйРасчет = Истина;
	Иначе
		ПоказыватьПодробныйРасчет = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	НесколькоВидовДеятельности =
		(ПлательщикНДФЛ + ПрименяетсяУСН + ПлательщикЕНВД + ПрименяетсяУСНПатент) > 1;
	
	Элементы.СуммаДоходаИП.Видимость = ПлательщикНДФЛ;
	Элементы.СуммаДоходаУСН.Видимость = ПрименяетсяУСН;
	Элементы.ВмененныйДоход.Видимость = ПлательщикЕНВД;
	Элементы.ПотенциальноВозможныйДоход.Видимость = ПрименяетсяУСНПатент;
	
	// Реквизиты подробного расчета взноса
	Элементы.ПотенциальныйДоход.Видимость = ПрименяетсяУСНПатент;
	Элементы.ДоходыПоОСНО.Видимость = ПлательщикНДФЛ;
	Элементы.РасходыПоОСНО.Видимость = ПлательщикНДФЛ;
	
	Элементы.ГруппаСуммаДоходовПоказатели.Отображение = ?(НесколькоВидовДеятельности,
		ОтображениеОбычнойГруппы.ОбычноеВыделение, ОтображениеОбычнойГруппы.Нет);
	Элементы.ВсегоДоходов.Видимость = НесколькоВидовДеятельности;
	
	// Отображение элементов сложного расчета
	
	Если УчитыватьРасходы Тогда
		Элементы.СуммаДоходаИП.Заголовок = НСтр("ru = 'Доход по ОСНО (за вычетом расходов)'");
		Элементы.СуммаДоходаУСН.Заголовок = НСтр("ru = 'Доход по УСН (за вычетом расходов)'");
	Иначе
		Элементы.СуммаДоходаИП.Заголовок = НСтр("ru = 'Доход по ОСНО'");
		Элементы.СуммаДоходаУСН.Заголовок = НСтр("ru = 'Доход по УСН'");
	КонецЕсли;
	
	ИспользуетсяРасчетныйМетод = Обработки.РасчетСтраховыхВзносовИП.ИспользуетсяРасчетныйМетодДляВзносаСДоходов(
		Период,
		УчитыватьРасходы,
		РасчетЗаТекущийПериод,
		ФиксированнаяЧастьВзносов,
		Всего - Уплачено - СуммаЗарезервированная);
	
	Элементы.ГруппаВсегоДоходовБезВзносов.Видимость = ИспользуетсяРасчетныйМетод И (Уплачено > 0);
	Элементы.ФиксированнаяЧастьВзносов.Видимость = ИспользуетсяРасчетныйМетод;
	Элементы.ГруппаОблагаемаяБаза.Видимость = ИспользуетсяРасчетныйМетод;
	
	// Отступы для подсказок при сложном расчете
	
	Элементы.ДекорацияСуммаВзносаОтступ.Видимость = ИспользуетсяРасчетныйМетод;
	Элементы.ДекорацияСуммаДоходовОтступ.Видимость = ИспользуетсяРасчетныйМетод;
	Элементы.ДекорацияОблагаемаяБазаОтступ.Видимость = ИспользуетсяРасчетныйМетод;
	Элементы.ДекорацияУплаченоОтступ.Видимость = ИспользуетсяРасчетныйМетод;
	
	Если УчетнаяПолитика.ПлательщикЕНП(Организация, Период) Тогда
		// Команды добавления ранее совершенных платежек недоступны,
		// С введением ЕНС недостаточно просто совершить платеж
		Элементы.ГруппаСумма.Видимость = Ложь;
		Элементы.КоманднаяПанель.Видимость = Ложь;
	Иначе
		Элементы.ГруппаСумма.Видимость = Истина;
		Элементы.КоманднаяПанель.Видимость = Истина;
	КонецЕсли;
	
	Если Период >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета() Тогда
		// Фактическая оплата взноса только после отражения на ЕНС и загрузки сведений об оплатах
		Элементы.ГруппаУплачено.Видимость = (Платежи.Количество() > 0);
		Элементы.ГруппаПлатежиКоманды.Видимость = Ложь;
		Элементы.Сумма.Вид = ВидПоляФормы.ПолеНадписи;
		Элементы.Сумма.Шрифт = Элементы.ЗаголовокКОплате.Шрифт;
		Элементы.ПояснениеСписанияСЕНС.Видимость = Истина;
		Элементы.ПояснениеСписанияСЕНС.Заголовок = Обработки.РасчетСтраховыхВзносовИП.ТекстПоясненияПоСписаниюВзносаСЕНС(
			Организация, Период, Истина);
	Иначе
		Элементы.ГруппаУплачено.Видимость = Истина;
		Элементы.ГруппаПлатежиКоманды.Видимость = Истина;
		Элементы.Сумма.Вид = ВидПоляФормы.ПолеВвода;
		Элементы.ПояснениеСписанияСЕНС.Видимость = Ложь;
	КонецЕсли;
	
	Если ПоказыватьПодробныйРасчет Тогда
		// Показываем подробный расчет с учетом расходов и предельной суммы взноса
		Элементы.РасчетОбщий.Видимость = Ложь;
		Элементы.ПодробныйРасчет.Видимость = Истина;
		Элементы.ПоясненияРасчета.ТекущаяСтраница = Элементы.ПодробныйРасчет;
		
		ФорматОтображенияЧисел = ФорматЧиселВПоясненииРасчета();
		Элементы.ДоходыПоОСНО.Формат = ФорматОтображенияЧисел;
		Элементы.РасходыПоОСНО.Формат = ФорматОтображенияЧисел;
		Элементы.ПотенциальныйДоход.Формат = ФорматОтображенияЧисел;
		Элементы.ПредельнаяСуммаВзноса.Формат = ФорматОтображенияЧисел;
		Элементы.СтраховыеВзносыИП.Формат = ФорматОтображенияЧисел;
		
		Если ЗначениеЗаполнено(ПотенциальноВозможныйДоход) Тогда
			Элементы.ДоходыПоОСНО.Заголовок = НСтр("ru = 'Доходы по ОСНО'");
		Иначе
			// Совмещения с ПСН нет, есть только доход по ОСНО - в заголовке нет смысла выделять систему налогообложения
			Элементы.ДоходыПоОСНО.Заголовок = НСтр("ru = 'Доходы'");
		КонецЕсли;
		
	Иначе
		Элементы.РасчетОбщий.Видимость = Истина;
		Элементы.ПодробныйРасчет.Видимость = Ложь;
		Элементы.ПоясненияРасчета.ТекущаяСтраница = Элементы.РасчетОбщий;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить(
			ИмяФормы,
			"",
			ПользователиИнформационнойБазы.ТекущийПользователь().Имя);
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПояснения()
	
	РасчетнаяСуммаКУплате = Всего - Уплачено;
	
	ПоясненияРасчета = Обработки.РасчетСтраховыхВзносовИП.ПоясненияРасчетаВзносаСДоходов(
		Организация,
		Период,
		ПараметрыРасчетаВзносаСДоходов,
		РасчетнаяСуммаКУплате - СуммаЗарезервированная,
		Уплачено);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПоясненияРасчета);
	
	// Динамические заголовки элементов
	
	Элементы.ДекорацияСуммаВзносаПояснение.Заголовок = ПоясненияРасчета.СуммаВзносаПояснение;
	Элементы.ДекорацияСуммаВзносаФормула.Заголовок = ПоясненияРасчета.СуммаВзносаФормула;
	
	Элементы.Всего.Заголовок = ПоясненияРасчета.СуммаВзносаЗаголовок;
	Элементы.ФиксированнаяЧастьВзносов.Заголовок = ПоясненияРасчета.ФиксированнаяЧастьВзносовЗаголовок;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтраховыеВзносыНаСервере()
	
	Периодичность = УчетСтраховыхВзносовИП.ПериодичностьУплатыФиксированныхСтраховыхВзносов(Организация, Период);
	
	// Доходы по видам деятельности
	СтруктураДоходов = УчетСтраховыхВзносовИП.СтруктураДоходовПоВидамДеятельности(
		Организация, НачалоГода(Период), КонецКвартала(Период));
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДоходов);
	
	// Страховые взносы, исчисленные с суммы доходов
	СтраховыеВзносыСДоходовКУплате = Обработки.РасчетСтраховыхВзносовИП.СтраховыеВзносыКУплате(
		Организация, Период, Периодичность);
	
	ПараметрыРасчетаВзносаСДоходов = СтраховыеВзносыСДоходовКУплате.ПараметрыРасчетаВзносаСДоходов;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРасчетаВзносаСДоходов);
	
	ЗаполнитьПояснения();
	
	ПредельнаяСуммаВзноса = СтраховыеВзносыСДоходовКУплате.ПредельнаяСуммаВзноса;
	Всего                 = СтраховыеВзносыСДоходовКУплате.СуммаВзносаПФРсДоходовВсего;
	Уплачено              = СтраховыеВзносыСДоходовКУплате.СуммаВзносаПФРсДоходовУплачено;
	Сумма                 = СтраховыеВзносыСДоходовКУплате.СуммаВзносаПФРсДоходов;
	
	ТаблицаПлатежей = СтраховыеВзносыСДоходовКУплате.СтраховыеВзносыСДоходовУплаченные;
	Если ТаблицаПлатежей <> Неопределено Тогда
		ТаблицаПлатежей.Колонки.ДокументОплаты.Имя = "Ссылка";
		ТаблицаПлатежей.Колонки.НомерДокументаОплаты.Имя = "Номер";
		ТаблицаПлатежей.Колонки.ДатаДокументаОплаты.Имя = "Дата";
		Платежи.Загрузить(ТаблицаПлатежей);
		Для Каждого Платеж Из Платежи Цикл
			Платеж.ПредставлениеДокумента = ПомощникиПоУплатеНалоговИВзносов.ПредставлениеДокументаОплаты(Платеж, Истина);
		КонецЦикла;
	КонецЕсли;
	
	ПомощникиПоУплатеНалоговИВзносов.ОтобразитьПлатежиССуммойВОтдельнойКолонке(ЭтотОбъект, Платежи, "Платеж");
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОВыбореИЗакрыть()
	
	ЗначениеВыбора = Новый Структура;
	ЗначениеВыбора.Вставить(ИмяВзноса, Сумма);
	ЗначениеВыбора.Вставить("СтруктураДоходов", СтруктураДоходов);
	ЗначениеВыбора.Вставить("ПараметрыРасчетаВзносаСДоходов", ПараметрыРасчетаВзносаСДоходов);
	
	Модифицированность = Ложь;
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ВмененныйДоходПоВидамДеятельностиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьСтраховыеВзносыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Функция ОповещениеУдаленияПлатежногоДокумента()
	Возврат Новый ОписаниеОповещения("УдалитьДокументУплатыНаКлиентеЗавершение", ЭтотОбъект);
КонецФункции

&НаКлиенте
Процедура УдалитьДокументУплатыНаКлиентеЗавершение(ДокументУплатыДляУдаления, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДокументУплатыДляУдаления) Тогда
		УдалитьДокументУплаты(ДокументУплатыДляУдаления);
		Оповестить("УдалитьДокументУплаты", ДокументУплатыДляУдаления);
		Оповестить("ОбновитьПоказателиРасчетаУСН", , ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДокументУплаты(ДокументУплатыДляУдаления)
	
	ДокументУплатыОбъект = ДокументУплатыДляУдаления.ПолучитьОбъект();
	ДокументУплатыОбъект.УстановитьПометкуУдаления(Истина);
	
	УдалитьДокументУплатыИзТаблицыПлатежей(ДокументУплатыДляУдаления);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДокументУплатыИзТаблицыПлатежей(ДокументУплатыДляУдаления)
	
	СтрокиПлатежа = Платежи.НайтиСтроки(Новый Структура("Ссылка", ДокументУплатыДляУдаления));
	Для Каждого СтрокаПлатежа Из СтрокиПлатежа Цикл
		Уплачено = Уплачено - СтрокаПлатежа.Сумма;
		Платежи.Удалить(СтрокаПлатежа);
		Сумма = Макс(Всего - Уплачено, 0);
	КонецЦикла;
	
	ПомощникиПоУплатеНалоговИВзносов.ОтобразитьПлатежиССуммойВОтдельнойКолонке(ЭтотОбъект, Платежи, "Платеж");
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОповеститьОВыбореИЗакрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПлатежОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПлатежныхДокументов = ПомощникиПоУплатеНалоговИВзносовКлиент.ПараметрыОбработкиПлатежныхДокументов(
		Платежи, "Платеж", ОповещениеУдаленияПлатежногоДокумента());
	
	ПомощникиПоУплатеНалоговИВзносовКлиент.ОбработкаНавигационнойСсылкиПлатежногоДокумента(Элемент, НавигационнаяСсылкаФорматированнойСтроки, ПараметрыПлатежныхДокументов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоФиксированныеВзносы(Знач Налог)
	
	Возврат Обработки.РасчетСтраховыхВзносовИП.ЭтоФиксированныеВзносы(Налог);
	
КонецФункции

#Область ПодробныйРасчет

&НаСервере
Процедура ЗаполнитьПодробныйРасчет()
	
	Если Не ПоказыватьПодробныйРасчет Тогда
		// Подробный расчет показывается для ИП на ОСНО
		Возврат;
	КонецЕсли;
	
	ДоходыПоКУДИР = 0;
	СтраховыеВзносыИП = 0;
	РасходыПоОСНО = 0;
	
	// При расчете взноса с доходов из доходов вычитывается профессиональный вычет за исключением страховых взносов
	// В форме расшифровке показываем в показателях доход как есть в КУДИР в таблице 6-1
	// Доходы вычисляем за периоды, когда уплачивается взнос с доходов
	
	// Взнос с доходов платится в случае, когда у ИП единый тариф - п.1.2 ст. 430 НК РФ
	ПериодыУплатыВзносаСДоходов = УчетСтраховыхВзносовИП.ПериодыУплатыВзносаСДоходов(Организация, КонецГода(Период));
	
	Для Каждого ТекущийПериод Из ПериодыУплатыВзносаСДоходов Цикл
		
		ДоходыПоКУДИР = ДоходыПоКУДИР + УчетДоходовИРасходовПредпринимателя.СуммаДоходаДляРасчетаСтраховыхВзносов(
			Организация,
			ТекущийПериод.ДатаНачала,
			ТекущийПериод.ДатаОкончания);
		
		РасходыДляРасчета = УчетДоходовИРасходовПредпринимателя.РасходыДляРасчетаСтраховыхВзносов(
			Организация,
			ТекущийПериод.ДатаНачала,
			ТекущийПериод.ДатаОкончания);
		
		СтраховыеВзносыИП = СтраховыеВзносыИП + РасходыДляРасчета.УплаченныеВзносыИП;
		РасходыПоОСНО = РасходыПоОСНО + РасходыДляРасчета.ВсегоРасходов;
		
	КонецЦикла;
	
	СуммаДоходаИП = ДоходыПоКУДИР + (РасходыПоОСНО - СтраховыеВзносыИП);
	
	ПараметрыПодробногоПояснения = НовыеПоказателиРасчетаВзносаСДоходов();
	ПараметрыПодробногоПояснения.Доходы = СуммаДоходаИП;
	ПараметрыПодробногоПояснения.Расходы = РасходыПоОСНО;
	ПараметрыПодробногоПояснения.ПотенциальныйДоход = ПотенциальноВозможныйДоход;
	ПараметрыПодробногоПояснения.ПредельнаяСумма = ПредельнаяСуммаВзноса;
	ПараметрыПодробногоПояснения.СтраховыеВзносы = СтраховыеВзносыИП;
	ПараметрыПодробногоПояснения.ПроцентВзноса = УчетСтраховыхВзносовИП.ПроцентСДоходовДляРасчетаВзносаСПревышения();
	ПараметрыПодробногоПояснения.ПорогДоходов = УчетСтраховыхВзносовИП.ПредельныйДоходНеОблагаемыйВзносамиВПФР();
	
	Пояснения = ПодробноеПоясненияРасчетаВзносаСДоходов(ПараметрыПодробногоПояснения);
	Для Каждого ТекущееПояснение Из Пояснения Цикл
		
		НомерЭтапаСтрокой = НомерЭтапаСтрокой(ТекущееПояснение.НомерЭтапа);
		
		ГруппаЭтапа = Элементы.Найти(СтрШаблон("ГруппаЭтап%1", НомерЭтапаСтрокой));
		Если ГруппаЭтапа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ГруппаЭтапа.Видимость = Истина;
		ЭлементЭтап = Элементы.Найти(СтрШаблон("Этап%1", НомерЭтапаСтрокой));
		Если ЭлементЭтап = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементЭтап.Заголовок = ТекущееПояснение.Пояснение;
		ЭлементЗначениеЭтапа = Элементы.Найти(СтрШаблон("Этап%1Значение", НомерЭтапаСтрокой));
		Если ЭлементЗначениеЭтапа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементЗначениеЭтапа.Заголовок = ТекущееПояснение.Значение;
		
	КонецЦикла;
	
	// Скроем лишние этапы
	ВсегоЭтаповНаФорме = Элементы.РасчетСуммыВзноса.ПодчиненныеЭлементы.Количество();
	НеиспользуемыйЭтап = Пояснения.Количество() + 1;
	Пока НеиспользуемыйЭтап <= ВсегоЭтаповНаФорме Цикл
		
		ГруппаЭтапа = Элементы.Найти(СтрШаблон("ГруппаЭтап%1", НомерЭтапаСтрокой(НеиспользуемыйЭтап)));
		Если ГруппаЭтапа <> Неопределено Тогда
			ГруппаЭтапа.Видимость = Ложь;
		КонецЕсли;
		
		НеиспользуемыйЭтап = НеиспользуемыйЭтап + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НомерЭтапаСтрокой(НомераЭтапа)
	
	Возврат Формат(НомераЭтапа, "ЧДЦ=0; ЧН=0; ЧГ=0; ЧО=0");
	
КонецФункции

&НаСервереБезКонтекста
Функция ПодробноеПоясненияРасчетаВзносаСДоходов(Показатели)
	
	Результат = НовыйРезультатПоясненияРасчета();
	ФорматЧисел = ФорматЧиселВПоясненииРасчета();
	
	// Расчет показателей взноса с доходов
	Доходы = Показатели.Доходы + Показатели.ПотенциальныйДоход;
	Расходы = Показатели.Расходы - Показатели.СтраховыеВзносы;
	
	НалоговаяБаза = Макс(Показатели.Доходы - Расходы, 0) + Показатели.ПотенциальныйДоход;
	СуммаВзноса = Макс(0, Показатели.ПроцентВзноса / 100 * (НалоговаяБаза - Показатели.ПорогДоходов));
	ПревышенПредел = (Показатели.ПредельнаяСумма < СуммаВзноса);
	
	// Оформление показателей для вывода в интерфейс
	ДоходОСНО          = Формат(Показатели.Доходы, ФорматЧисел);
	ПотенциальныйДоход = Формат(Показатели.ПотенциальныйДоход, ФорматЧисел);
	РасходОСНО         = Формат(Показатели.Расходы, ФорматЧисел);
	СтраховыеВзносы    = Формат(Показатели.СтраховыеВзносы, ФорматЧисел);
	ПорогДоходов       = Формат(Показатели.ПорогДоходов, ФорматЧисел);
	СуммаВзносаСтрокой = Формат(СуммаВзноса, ФорматЧисел);
	
	НомерЭтапа = 0;
	
	СовмещениеСПатентом = ЗначениеЗаполнено(Показатели.ПотенциальныйДоход);
	
	Если СовмещениеСПатентом Тогда
		ОтрицательнаяБазаПоОСНО = (Показатели.Доходы - Расходы) < 0;
	Иначе
		ОтрицательнаяБазаПоОСНО = Ложь;
	КонецЕсли;
	
	Если СовмещениеСПатентом И Не ОтрицательнаяБазаПоОСНО Тогда
		// Этап - Доходы
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Доходы = Доходы по ОСНО + Потенциальный доход'");
		ДополнительныйТекст = СтрШаблон("(%1 + %2)", ДоходОСНО, ПотенциальныйДоход);
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(Доходы, ФорматЧисел);
	КонецЕсли;
	
	// Этап - Расходы
	НомерЭтапа = НомерЭтапа + 1;
	Этап = Результат.Добавить();
	
	ОсновнойТекст = НСтр("ru = 'Расходы = Профессиональный вычет - Страховые взносы ИП'");
	ДополнительныйТекст = СтрШаблон("(%1 - %2)", РасходОСНО, СтраховыеВзносы);
	
	Этап.НомерЭтапа = НомерЭтапа;
	Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
	Этап.Значение   = Формат(Расходы, ФорматЧисел);
	
	Если ОтрицательнаяБазаПоОСНО Тогда
		
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = НСтр("ru = 'Налоговая база по ОСНО = Доходы по ОСНО - Расходы'");
		ДополнительныйТекст = СтрШаблон("(%1 - %2)", ДоходОСНО, Формат(Расходы, ФорматЧисел));
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = "0,00";
		
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		Если ПревышенПредел Тогда
			// Этап - Расчетная сумма взноса
			ОсновнойТекст = СтрШаблон(
				НСтр("ru = 'Расчетная сумма взноса = (Налоговая база по ОСНО + Потенциальный доход - %1) * %2%%'"),
				ПорогДоходов,
				Показатели.ПроцентВзноса);
			ДополнительныйТекст = СтрШаблон("(0,00 + %1 - %2) * %3%%",
				ПотенциальныйДоход,
				ПорогДоходов,
				Показатели.ПроцентВзноса);
			
			Этап.НомерЭтапа = НомерЭтапа;
			Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
			Этап.Значение   = Формат(СуммаВзноса, ФорматЧисел);
			
			ДобавитьПояснениеПредельнойСуммыВзноса(Результат, НомерЭтапа, СуммаВзноса, Показатели.ПредельнаяСумма);
		Иначе
			// Этап - Сумма взноса
			ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>Сумма взноса</b> = (Налоговая база по ОСНО + Потенциальный доход - %1) * %2%'"),
				ПорогДоходов,
				Показатели.ПроцентВзноса);
			ДополнительныйТекст = СтрШаблон("(0,00 + %1 - %2) * %3%%",
				ПотенциальныйДоход,
				ПорогДоходов,
				Показатели.ПроцентВзноса);
			
			Этап.НомерЭтапа = НомерЭтапа;
			Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
			Этап.Значение   = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '<b>%1</b>'"),
				Формат(СуммаВзноса, ФорматЧисел));
		КонецЕсли;
		
	ИначеЕсли ПревышенПредел Тогда
		// Этап - Расчетная сумма взноса
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = СтрШаблон(НСтр("ru = 'Расчетная сумма взноса = (Доходы - Расходы - %1) * %2%%'"),
			ПорогДоходов,
			Показатели.ПроцентВзноса);
		ДополнительныйТекст = СтрШаблон("(%1 - %2 - %3) * %4%%",
			Формат(Доходы, ФорматЧисел),
			Формат(Расходы, ФорматЧисел),
			ПорогДоходов,
			Показатели.ПроцентВзноса);
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = Формат(СуммаВзноса, ФорматЧисел);
		
		ДобавитьПояснениеПредельнойСуммыВзноса(Результат, НомерЭтапа, СуммаВзноса, Показатели.ПредельнаяСумма);
	Иначе
		// Этап - Сумма взноса
		НомерЭтапа = НомерЭтапа + 1;
		Этап = Результат.Добавить();
		
		ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<b>Сумма взноса</b> = (Доходы - Расходы - %1) * %2%'"),
			ПорогДоходов,
			Показатели.ПроцентВзноса);
		ДополнительныйТекст = СтрШаблон("(%1 - %2 - %3) * %4%%",
			Формат(Доходы, ФорматЧисел),
			Формат(Расходы, ФорматЧисел),
			ПорогДоходов,
			Показатели.ПроцентВзноса);
		
		Этап.НомерЭтапа = НомерЭтапа;
		Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
		Этап.Значение   = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = '<b>%1</b>'"),
			Формат(СуммаВзноса, ФорматЧисел));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьПояснениеПредельнойСуммыВзноса(Результат, НомерЭтапа, СуммаВзноса, ПредельнаяСумма)
	
	// Этап - Сумма взноса больше предельной суммы согласно ст. 430 НК РФ
	НомерЭтапа = НомерЭтапа + 1;
	ФорматЧисел = ФорматЧиселВПоясненииРасчета();
	ПредельнаяСуммаСтрокой = Формат(ПредельнаяСумма, ФорматЧисел);
	СуммаВзносаСтрокой = Формат(СуммаВзноса, ФорматЧисел);
	
	Этап = Результат.Добавить();
	
	ОсновнойТекст = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = '<b>Сумма взноса</b>: не может превышать предельный размер'"));
	ДополнительныйТекст = СтрШаблон("(%1 > %2)", СуммаВзносаСтрокой, ПредельнаяСуммаСтрокой);
	
	Этап.НомерЭтапа = НомерЭтапа;
	Этап.Пояснение  = ПояснениеЭтапа(ОсновнойТекст, ДополнительныйТекст);
	Этап.Значение   = СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = '<b>%1</b>'"),
		Формат(ПредельнаяСумма, ФорматЧисел));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовыеПоказателиРасчетаВзносаСДоходов()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Доходы", 0);
	Результат.Вставить("Расходы", 0);
	Результат.Вставить("ПотенциальныйДоход", 0);
	Результат.Вставить("ПредельнаяСумма", 0);
	Результат.Вставить("СтраховыеВзносы", 0);
	Результат.Вставить("ПроцентВзноса", 0);
	Результат.Вставить("ПорогДоходов", 0);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ФорматЧиселВПоясненииРасчета()
	
	Возврат "Л=ru_RU; ЧДЦ=2; ЧН=0,00; ЧГ=3,0";
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйРезультатПоясненияРасчета()
	
	ТипФорматированнаяСтрока = Новый ОписаниеТипов("ФорматированнаяСтрока, Строка");
	
	Пояснения = Новый ТаблицаЗначений;
	
	Пояснения.Колонки.Добавить("НомерЭтапа", ОбщегоНазначения.ОписаниеТипаЧисло(1, 0));
	Пояснения.Колонки.Добавить("Пояснение",  ТипФорматированнаяСтрока);
	Пояснения.Колонки.Добавить("Значение",   ТипФорматированнаяСтрока);
	
	Возврат Пояснения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПояснениеЭтапа(ОсновнойТекст, Дополнение)
	
	СтрокиПояснения = Новый Массив;
	СтрокиПояснения.Добавить(ОсновнойТекст);
	СтрокиПояснения.Добавить(Символы.ПС);
	СтрокиПояснения.Добавить(Дополнение);
	
	Возврат Новый ФорматированнаяСтрока(СтрокиПояснения);
	
КонецФункции

#КонецОбласти

#Область РасшифровкаПоказателей

&НаКлиенте
Процедура РасшифроватьДоходыРасходыИП()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РежимРасшифровки", Истина);
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("НачалоПериода", НачалоГода(Период));
	СтруктураПараметров.Вставить("КонецПериода", КонецКвартала(Период));
	
	ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходовПредпринимателя.Форма",
		СтруктураПараметров,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПотенциальныйДоход()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Отбор", Новый Структура("Организация", Организация));
	СтруктураПараметров.Вставить("Период", Период);
	
	ОткрытьФорму("Документ.ОперацияСПатентом.ФормаСписка", СтруктураПараметров, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьСтраховыеВзносыИП()
	
	ПользовательскиеНастройки  = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация",   Организация);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоГода(Период));
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода",  КонецГода(Период));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидОтчета", "СтраховыеВзносыИП");
	ПараметрыФормы.Вставить("ПользовательскиеНастройки",  ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	
	ОткрытьФорму("Отчет.РасшифровкаПлатежейВБюджет.Форма.ФормаОтчета",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
