
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Организация", Параметры.Организация);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыборЗаявления();

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)

	Отказ = Истина;
	
	ОбработатьОткрытиеЗаявления();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)

	Если Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КолонкиДоступны = Ложь;
	
	МассивЗаявлений = Новый Массив;
	Для Каждого Строка Из Строки Цикл
		ДанныеСтроки = Строка.Значение.Данные;
		
		МассивЗаявлений.Добавить(ДанныеСтроки["Заявление"]);
		
		КолонкиДоступны = ДанныеСтроки.Свойство("НаименованиеВидаДеятельности")
			И ДанныеСтроки.Свойство("ДатаНачала") И ДанныеСтроки.Свойство("ДатаОкончания");
	КонецЦикла;
	
	Если Не КолонкиДоступны Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЗаявлений = ДополнительныеДанныеЗаявлений(МассивЗаявлений);
	
	Для Каждого Строка Из Строки Цикл
		
		ДанныеСтроки = Строка.Значение.Данные;
		
		ДанныеЗаявления = ДанныеЗаявлений.Получить(ДанныеСтроки["Заявление"]);
		Если ДанныеЗаявления = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеСтроки["НаименованиеВидаДеятельности"] = ДанныеЗаявления.НаименованиеВидаДеятельности;
		ДанныеСтроки["ДатаНачала"] = ДанныеЗаявления.ДатаНачала;
		ДанныеСтроки["ДатаОкончания"] = ДанныеЗаявления.ДатаОкончания;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)

	ОбработатьВыборЗаявления();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗаявление(Команда)

	ОбработатьОткрытиеЗаявления();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборЗаявления()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВыбранноеЗаявление = ТекущиеДанные.Заявление;
	
	Закрыть(ВыбранноеЗаявление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОткрытиеЗаявления()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Заявление) Тогда
		ПоказатьЗначение(, ТекущиеДанные.Заявление);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДополнительныеДанныеЗаявлений(Заявления)
	
	ДанныеЗаявлений = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УведомлениеОСпецрежимахНалогообложения.Ссылка КАК Уведомление,
	|	УведомлениеОСпецрежимахНалогообложения.ДанныеУведомления КАК ДанныеУведомления,
	|	УведомлениеОСпецрежимахНалогообложения.ИмяФормы КАК ИмяФормы
	|ИЗ
	|	Документ.УведомлениеОСпецрежимахНалогообложения КАК УведомлениеОСпецрежимахНалогообложения
	|ГДЕ
	|	УведомлениеОСпецрежимахНалогообложения.Ссылка В (&Заявления)";
	
	Запрос.УстановитьПараметр("Заявления", Заявления);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеУведомления = Выборка.ДанныеУведомления.Получить();
		
		Если ТипЗнч(ДанныеУведомления) <> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяТитульногоЛиста = СтрШаблон("%1_Титульная", Выборка.ИмяФормы);
		ИмяЛиста2 = СтрШаблон("%1_Лист2", Выборка.ИмяФормы);
			
		ДанныеТитульногоЛиста = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДанныеУведомления.ДанныеУведомления, ИмяТитульногоЛиста);
		
		ДанныеЛиста2 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДанныеУведомления.ДанныеУведомления, ИмяЛиста2);
		
		СтруктураДанных = Новый Структура("НаименованиеВидаДеятельности, ДатаНачала, ДатаОкончания");
		СтруктураДанных.НаименованиеВидаДеятельности = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДанныеЛиста2, "НаимВД", "");
		СтруктураДанных.ДатаНачала = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДанныеТитульногоЛиста, "ДатаНачПат", '00010101');
		СтруктураДанных.ДатаОкончания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ДанныеТитульногоЛиста, "ДатаКонПат", '00010101');
		
		ДанныеЗаявлений.Вставить(Выборка.Уведомление, СтруктураДанных);
		
	КонецЦикла;
	
	Возврат ДанныеЗаявлений;
	
КонецФункции

#КонецОбласти
