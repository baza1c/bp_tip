#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПараметрыСпискаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПерезаполнитьНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеСредстваОтборПриИзменении(Элемент)
	
	#Если ВебКлиент Тогда
		ПерезаполнитьНаСервере()
	#КонецЕсли 

КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Объект.ПечатьИзДокумента = Ложь;
	ПараметрыФормы = Новый Структура("ОбъектыПечати, Организация", ПолучитьДанныеДляПечати(), Объект.Организация);
	ОткрытьФорму("Обработка.ПечатьИнвентарныхЭтикеток.Форма.ФормаНастройкиПечати", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОсновнойПрограммы

&НаСервере
Процедура УстановитьПараметрыСпискаНаСервере()
	
	ОсновныеСредства.Параметры.УстановитьЗначениеПараметра("Организация", Объект.Организация);
	ОсновныеСредства.Параметры.УстановитьЗначениеПараметра("Счета", ПолучитьСчетаУчетаОС());
	ОсновныеСредства.Параметры.УстановитьЗначениеПараметра("Дата", ТекущаяДата());
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьНаСервере()
	
	УстановитьПараметрыСпискаНаСервере();
	Элементы.ОсновныеСредства.Обновить();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДляПечати()
	
	ДанныеДляПечати = ДинамическийСписокВТаблицуЗначений("ОсновныеСредства");
	Возврат ТаблицаЗначенийВМассивСтруктур(ДанныеДляПечати);
	
КонецФункции

&НаСервере
Функция ДинамическийСписокВТаблицуЗначений(ИмяСписка = "Список")

    СКД = Элементы[ИмяСписка].ПолучитьИсполняемуюСхемуКомпоновкиДанных();

    Настройки = Элементы[ИмяСписка].ПолучитьИсполняемыеНастройкиКомпоновкиДанных();

    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
    МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, Настройки, , ,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

    ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);

    ПроцессорВыводаРезультата = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
    ТаблицаЗначений = ПроцессорВыводаРезультата.Вывести(ПроцессорКомпоновкиДанных);

    Возврат ТаблицаЗначений;

КонецФункции

&НаСервере
Функция ТаблицаЗначенийВМассивСтруктур(ИсходнаяТаблица)
	
	МассивСтрок = Новый Массив;
	
	Для Каждого Строка Из ИсходнаяТаблица Цикл
		СтруктураСтроки = Новый Структура;
		Для Каждого Колонка Из ИсходнаяТаблица.Колонки Цикл
			СтруктураСтроки.Вставить(Колонка.Имя);
			ЗаполнитьЗначенияСвойств(СтруктураСтроки, Строка);
		КонецЦикла;
		МассивСтрок.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	Возврат МассивСтрок;
	
КонецФункции

&НаСервере
Функция ПолучитьСчетаУчетаОС() Экспорт
	
	СчетаУчетаОС = Новый Массив;
	
	СчетаУчетаОС.Добавить(ПланыСчетов.Хозрасчетный.ОСвОрганизации);
	СчетаУчетаОС.Добавить(ПланыСчетов.Хозрасчетный.ГрупповыеОбъектыОС);
	СчетаУчетаОС.Добавить(ПланыСчетов.Хозрасчетный.АрендованныеОсновныеСредства);
	
	Возврат СчетаУчетаОС;
	
КонецФункции 

#КонецОбласти


