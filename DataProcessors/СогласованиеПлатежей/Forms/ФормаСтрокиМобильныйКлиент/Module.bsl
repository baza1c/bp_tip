
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСвойств = СписокСвойствДляЗаполнения(Истина);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы Тогда
		Если ЭтотОбъект.Модифицированность Тогда
			ПодключитьОбработчикОжидания("ПодключаемыйИзменитьПлатежноеПоручение", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отклонить(Команда)
	
	НачатьУстановкуСостоянияПлатежа(1, ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отклонено"));
	
КонецПроцедуры

&НаКлиенте
Процедура Согласовать(Команда)
	
	НачатьУстановкуСостоянияПлатежа(1, ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Согласовано"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПлатежноеПоручениеПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура("Ключ", ПлатежноеПоручение);
	
	ОповещениеЗакрытияФормыДокумента = Новый ОписаниеОповещения("ОбновитьПослеЗакрытияФормыДокумента", ЭтотОбъект);
	
	ОткрытьФорму("Документ.ПлатежноеПоручение.ФормаОбъекта", ПараметрыОткрытия ,,,,, ОповещениеЗакрытияФормыДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.Документы.ПлатежноеПоручение) Тогда
		Элементы.ГруппаКомандыСогласования.Доступность = Ложь;
		Элементы.РедактируемыеРеквизиты.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокСвойствДляЗаполнения(НачальноеЗаполнение = Ложь)
	
	СтрокаСвойств = "ВидОперации, ДатаПлатежа, Комментарий, КОплате,
			|ПлатежноеПоручениеПредставление, Получатель, БанковскийСчет, НомерПлатежа";
	
	Если НачальноеЗаполнение Тогда
		СтрокаСвойств = СтрокаСвойств + ", ПлатежноеПоручение";
	КонецЕсли;
	
	Возврат СтрокаСвойств;
	
КонецФункции

&НаКлиенте
Процедура НачатьУстановкуСостоянияПлатежа(Действие, НовоеСостояние, СогласоватьВсе = Ложь)   
	
	УстановитьСостояниеПлатежа(НовоеСостояние);
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеПлатежа(НовоеСостояние)
	
	ДокументОбъект = ПлатежноеПоручение.ПолучитьОбъект();
	
	СведенияОДатеЗапрета = ДатыЗапретаИзмененияСлужебный.ПроверитьДатыЗапретаИзмененияЗагрузкиДанных(
		ДокументОбъект, Ложь, Истина, Ложь);
	
	Если СведенияОДатеЗапрета.ИзменениеЗапрещено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ДокументОбъект.Заблокировать();
	Исключение
		ТекстСообщения = НСтр("ru = 'Документ уже редактируется!
		|Завершите редактирование документа.'");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	РегистрыСведений.СостоянияБанковскихДокументов.ИзменитьСостояниеСогласованияДокумента(ПлатежноеПоручение, НовоеСостояние);
	
	ПлатежноеПоручениеПредставление = Обработки.СогласованиеПлатежей.СформироватьПредставлениеСтрокиПлатежноеПоручение(
		НовоеСостояние,
		НомерПлатежа,
		ДатаПлатежа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПослеЗакрытияФормыДокумента(Результат, ДопПараметры) Экспорт
	
	СтруктураПараметров = Новый Структура("Организация, БанковскийСчет, Дата");
	
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("БанковскийСчет", БанковскийСчет);
	СтруктураПараметров.Вставить("Дата", ДатаПлатежа);
	
	РеквизитыПлатежноеПоручение = ПолучитьРеквизитыПлатежногоПоручения(ПлатежноеПоручение, СтруктураПараметров);
	
	Если РеквизитыПлатежноеПоручение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокСвойств = СписокСвойствДляЗаполнения();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыПлатежноеПоручение, СписокСвойств);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыПлатежногоПоручения(ПлатежноеПоручение, СтруктураПараметров)
	
	Результат = Обработки.СогласованиеПлатежей.ПолучитьРеквизитыПлатежногоПоручения(ПлатежноеПоручение, СтруктураПараметров);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ИзменитьПлатежноеПоручениеНаСервере(ПлатежноеПоручение, СтруктураСвойств) Экспорт
	
	ДокументОбъект = ПлатежноеПоручение.ПолучитьОбъект();
	
	Попытка
		ДокументОбъект.Заблокировать();
	Исключение
		ТекстСообщения = НСтр("ru = 'Документ уже редактируется!
		|Завершите редактирование документа.'");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтруктураСвойств);
	
	ПроцентНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ДокументОбъект.СтавкаНДС);
	СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(СтруктураСвойств.СуммаДокумента, Истина, ПроцентНДС);
	
	ДокументОбъект.СуммаНДС = СуммаНДС;
	
	ДокументОбъект.Записать();
	ДокументОбъект.Разблокировать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключаемыйИзменитьПлатежноеПоручение()
	
	СтруктураСвойств = Новый Структура("Комментарий, Дата, КОплате");
	
	СтруктураСвойств.Вставить("Комментарий", Комментарий);
	СтруктураСвойств.Вставить("Дата", ДатаПлатежа);
	СтруктураСвойств.Вставить("СуммаДокумента", КОплате);
	
	ИзменитьПлатежноеПоручениеНаСервере(ПлатежноеПоручение, СтруктураСвойств);
	
КонецПроцедуры

#КонецОбласти

