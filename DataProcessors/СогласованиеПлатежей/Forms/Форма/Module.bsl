
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьВалютныеСчета = Ложь;
	
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организация = Параметры.Организация;
	Иначе
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	Дата = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	УстановитьУсловноеОформление();
	
	ПроверитьНастройкиПриПервомЗапуске();
	
	УстановитьДоступностьЭлементов();
	
	АдаптироватьЭлементыФормыМК();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьНаСервереРассчитатьИтоги", 0.1, Истина);
	
	УстановитьВидимостьПодсказкиБанковскийСчет();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отклонить(Команда)
	
	НачатьУстановкуСостоянияПлатежей(0,
		ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отклонено"));
	
	ОпределитьСостояние();
	
КонецПроцедуры

&НаКлиенте
Процедура Согласовать(Команда)
	
	НачатьУстановкуСостоянияПлатежей(1,
		ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Согласовано"));
	
	ОпределитьСостояние();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомментарий(Команда)
	
	ВыбранныеСтроки = ПолучитьОбрабатываемыеСтроки();
	ВсегоВыбрано = ВыбранныеСтроки.Количество();
	Если ВсегоВыбрано = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВсегоВыбрано = 1 Тогда
		ВвестиКомментарийКПлатежу(ВыбранныеСтроки);
	Иначе
		НачатьУстановкуДействияСПлатежом(ВыбранныеСтроки, 3);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги",0.1,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РеестрПлатежей(Команда)
	
	ПараметрыОткрытия = Новый Структура;	
	
	ПараметрыОткрытия.Вставить("ТабличныйДокумент",ПодготовитьРеестрПлатежейНаСервере());
	
	ОткрытьФорму("Обработка.СогласованиеПлатежей.Форма.ПечатьРеестра",ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВБанк(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РежимПоУмолчанию", "ГруппаВыгрузка");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураПараметров.Вставить("Организация", Организация); 	
	КонецЕсли;
	
	СтруктураПараметров.Вставить("НачалоПериода", ДатаНачалаПлатежей());
	СтруктураПараметров.Вставить("КонецПериода", Дата);
	
	МассивСчетов = Новый Массив;
	Для каждого Строка из Объект.СписокПлатежей Цикл
		МассивСчетов.Добавить(Строка.БанковскийСчет);
	КонецЦикла;
	
	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивСчетов);
	
	ОповещениеЗакрытия = Новый ОписаниеОповещения("ЗавершениеВыбораСчета", ЭтотОбъект, СтруктураПараметров);
	 
	Если МассивСчетов.Количество() > 1 Тогда
		СтруктураПараметров.Вставить("ОтправкаВБанк", Истина);
		СтруктураПараметров.Вставить("СписокСчетов", МассивСчетов);
		ОткрытьФорму("Обработка.СогласованиеПлатежей.Форма.ФормаВыбораСчетовИзСпискаЗначений",
			СтруктураПараметров,,,,,ОповещениеЗакрытия,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли МассивСчетов.Количество() = 1 Тогда
		СтруктураПараметров.Вставить("БанковскийСчет", МассивСчетов[0]);
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			СтруктураПараметров.Вставить("Организация", ОрганизацияПоСчету(СтруктураПараметров.БанковскийСчет));	
		КонецЕсли;	
		ОткрытьФорму("Обработка.КлиентБанк.Форма", СтруктураПараметров);
	Иначе
		СтруктураПараметров.Вставить("БанковскийСчет", ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка"));
		ОткрытьФорму("Обработка.КлиентБанк.Форма", СтруктураПараметров);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьЗначенияФлажков(Истина);
	
КонецПроцедуры 

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьЗначенияФлажков(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйПлатеж(Команда)
	
	ОповещениеЗакрытияФормыДокумента = Новый ОписаниеОповещения("ОбновитьТабличнуюЧасть",ЭтотОбъект);
	ОткрытьФорму("Документ.ПлатежноеПоручение.Форма.ФормаДокумента",,,,,,ОповещениеЗакрытияФормыДокумента);
	
КонецПроцедуры 

&НаКлиенте
Процедура НаписатьРазработчикам(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры 

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги",0.1,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги",0.1,Истина);
	
	УстановитьВидимостьПодсказкиБанковскийСчет();
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ОписаниеОповещенияЗакрытия = Новый ОписаниеОповещения("ОбновитьОтборПоСчету", ЭтотОбъект);
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = СтруктураПараметровФормы();
	
	ОткрытьФорму("Обработка.СогласованиеПлатежей.Форма.ФормаВыбораСчетовИзСпискаЗначений",
		ПараметрыФормы,,,,,ОписаниеОповещенияЗакрытия, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	БанковскийСчет.Очистить();
	
	УстановитьВидимостьПодсказкиБанковскийСчет();
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги",0.1,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Организация Тогда
		БанковскийСчет.Очистить();
		УстановитьВидимостьПодсказкиБанковскийСчет();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги",0.1,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПодготовленоПриИзменении(Элемент)
	
	ОпределитьСостояние();
	
	ОпределитьСостояниеВсеПлатежи();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусСогласованоПриИзменении(Элемент)
	
	ОпределитьСостояние();
	
	ОпределитьСостояниеВсеПлатежи();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтклоненоПриИзменении(Элемент)
	
	ОпределитьСостояние();
	
	ОпределитьСостояниеВсеПлатежи();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтправленоПриИзменении(Элемент)
	
	ОпределитьСостояние();
	
	ОпределитьСостояниеВсеПлатежи();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусВсеПлатежиПриИзменении(Элемент)
	
	ИнициализироватьСтатусы();
	
	ОпределитьСостояние();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписокПлатежей

&НаКлиенте
Процедура СписокПлатежейПриИзменении(Элемент)
	
	РассчитатьИтоги();
	
КонецПроцедуры 

&НаКлиенте
Процедура СписокПлатежейКОплатеПриИзменении(Элемент)
	
	ИзменитьСуммуПлатежа();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПлатежейСуммаПлатежаУЕПриИзменении(Элемент)
	
	ИзменитьСуммуПлатежаУЕ();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПлатежейКомментарийПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокПлатежей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьКомментарийПлатежаНаСервере(ТекущиеДанные.ПлатежноеПоручение, ТекущиеДанные.Комментарий);    
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПлатежейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СписокПлатежей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоМобильныйКлиент Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОткрытия = НовыйПараметрыФормыСтроки();
		
		ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ТекущиеДанные);
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбновитьТабличнуюЧасть", ЭтотОбъект);
		
		ОткрытьФорму("Обработка.СогласованиеПлатежей.Форма.ФормаСтрокиМобильныйКлиент",
			ПараметрыОткрытия, ЭтотОбъект,,,, ОповещениеОЗакрытии,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		Если Поле.Имя = "СписокПлатежейПлатежноеПоручениеПредставление" Тогда	
			ОповещениеЗакрытияФормыДокумента = Новый ОписаниеОповещения("ОбновитьПослеЗакрытияФормыДокумента",
				ЭтотОбъект, ТекущиеДанные);
			ОткрытьФорму("Документ.ПлатежноеПоручение.ФормаОбъекта",Новый Структура("Ключ",
				ТекущиеДанные.ПлатежноеПоручение),,,,,ОповещениеЗакрытияФормыДокумента);
		ИначеЕсли Поле.Имя = "СписокПлатежейОснованиеПредставление" И ЗначениеЗаполнено(ТекущиеДанные.Основание) Тогда
			ОткрытьЗначениеАсинх(ТекущиеДанные.Основание);
		ИначеЕсли Поле.Имя = "СписокПлатежейПолучатель" И ЗначениеЗаполнено(ТекущиеДанные.Получатель) Тогда
			ОткрытьЗначениеАсинх(ТекущиеДанные.Получатель);
		ИначеЕсли Поле.Имя = "СписокПлатежейБанковскийСчетПредставление" И ЗначениеЗаполнено(ТекущиеДанные.БанковскийСчет) Тогда
			ОткрытьЗначениеАсинх(ТекущиеДанные.БанковскийСчет);
		ИначеЕсли Поле.Имя = "СписокПлатежейНазначениеПлатежа" И ЗначениеЗаполнено(ТекущиеДанные.НазначениеПлатежа) Тогда
			ПоказатьЗначение(,ТекущиеДанные.НазначениеПлатежа);
		ИначеЕсли Поле.Имя = "СписокПлатежейСтатьяДвиженияДенежныхСредств"
			И ЗначениеЗаполнено(ТекущиеДанные.СтатьяДвиженияДенежныхСредств) Тогда
			ПоказатьЗначение(,ТекущиеДанные.СтатьяДвиженияДенежныхСредств);
		ИначеЕсли Поле.Имя = "СписокПлатежейНомер" Тогда
			ОповещениеЗакрытияФормыДокумента = Новый ОписаниеОповещения("ОбновитьПослеЗакрытияФормыДокумента",
				ЭтотОбъект, ТекущиеДанные);
			ОткрытьФорму("Документ.ПлатежноеПоручение.ФормаОбъекта",Новый Структура("Ключ",
				ТекущиеДанные.ПлатежноеПоручение),,,,,ОповещениеЗакрытияФормыДокумента);
		ИначеЕсли Поле.Имя = "СписокПлатежейДата" Тогда
			ОповещениеЗакрытияФормыДокумента = Новый ОписаниеОповещения("ОбновитьПослеЗакрытияФормыДокумента",
				ЭтотОбъект, ТекущиеДанные);
			ОткрытьФорму("Документ.ПлатежноеПоручение.ФормаОбъекта",Новый Структура("Ключ",
				ТекущиеДанные.ПлатежноеПоручение),,,,,ОповещениеЗакрытияФормыДокумента);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПлатежейОбработкаЗапросаОбновления()
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги",0.1,Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовПодвалФормы

&НаКлиенте
Процедура ДоступноНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ГруппировкаВидДС = Новый Структура;
	ГруппировкаВидДС.Вставить("Использование",  Истина);
	ГруппировкаВидДС.Вставить("Поле",           "ВидДенежныхСредств");
	ГруппировкаВидДС.Вставить("Представление",  "Вид денежных средств");
	ГруппировкаВидДС.Вставить("ТипГруппировки", 0);
	
	ГруппировкаОрганизация = Новый Структура;
	ГруппировкаОрганизация.Вставить("Поле",           "Организация");
	ГруппировкаОрганизация.Вставить("Представление",  "Организация");
	ГруппировкаОрганизация.Вставить("Использование",  Истина);
	ГруппировкаОрганизация.Вставить("ТипГруппировки", 0);
	
	ГруппировкаРазмещение = Новый Структура;
	ГруппировкаРазмещение.Вставить("Поле",           "Размещение");
	ГруппировкаРазмещение.Вставить("Представление",  "Размещение");
	ГруппировкаРазмещение.Вставить("Использование",  Истина);
	ГруппировкаРазмещение.Вставить("ТипГруппировки", 0); 
	
	ГруппировкаОтчета = Новый Массив;
	ГруппировкаОтчета.Добавить(ГруппировкаВидДС);
	ГруппировкаОтчета.Добавить(ГруппировкаОрганизация);
	ГруппировкаОтчета.Добавить(ГруппировкаРазмещение);
	
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("РежимРасшифровки", Истина);
	ДополнительныеСвойства.Вставить("Организация",      Организация);
	ДополнительныеСвойства.Вставить("Период",           Дата);
	ДополнительныеСвойства.Вставить("Группировка", ГруппировкаОтчета);
	ДополнительныеСвойства.Вставить("ОчищатьТаблицуГруппировок", Истина);
	
	НастройкаОтбора = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	НастройкаОтбора.ИдентификаторПользовательскойНастройки = "Отбор";
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(НастройкаОтбора, "ВидДенежныхСредств", "Деньги на расчетных счетах", ВидСравненияКомпоновкиДанных.Равно);
	
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(НастройкаОтбора, "Размещение", БанковскийСчет, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура; 
	ПараметрыОтчета.Вставить("РежимРасшифровки",          Истина);
	ПараметрыОтчета.Вставить("ВидРасшифровки",            2);
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	
	ОткрытьФорму("Отчет.ОстаткиДенежныхСредств.Форма.ФормаОтчета", ПараметрыОтчета, ЭтотОбъект, Истина);
	
КонецПроцедуры 

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПересчитатьВалютуПоКурсу(Сумма, Курс, Кратность)
	
	БазовыйКурс = Курс/Кратность;
	Результат = Сумма/БазовыйКурс;
	
	Возврат Окр(Результат,2);
	
КонецФункции

&НаКлиенте
Функция ПересчитатьРублиВВалюту(Сумма, Курс, Кратность)
	
	БазовыйКурс = Курс/Кратность;
	Результат = Сумма*БазовыйКурс;
	
	Возврат Окр(Результат,2);
	
КонецФункции 

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	//доступность команд
	Если НЕ ПравоДоступа("Изменение", Метаданные.Документы.ПлатежноеПоручение) Тогда
		Элементы.СписокПлатежейСогласовать.Доступность = Ложь;
		Элементы.СписокПлатежейОтклонить.Доступность = Ложь;
		Элементы.СписокПлатежейУстановитьКомментарий.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//Согласованные платежи
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокПлатежейПлатежноеПоручениеПредставление");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.СписокПлатежей.Состояние",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.СостоянияБанковскихДокументов.Согласовано);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветПлатежныйДокументОплачен);
	
	//Отправленные платежи
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокПлатежейПлатежноеПоручениеПредставление");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.СписокПлатежей.Состояние",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.СостоянияБанковскихДокументов.Отправлено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаФормы);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина); 
	
	//Отправленные платежи
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокПлатежей");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.СписокПлатежей.Состояние",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.СостоянияБанковскихДокументов.Отправлено); 
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	
	//Отклоненные платежи
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокПлатежей");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.СписокПлатежей.Состояние",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.СостоянияБанковскихДокументов.Отклонено); 
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстНеутвержденныеПозиции);
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЗачеркнутыйШрифтБЭД);
	КонецЕсли;
	
	//УЕ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокПлатежейСуммаПлатежаУЕ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.СписокПлатежей.СуммаПлатежаУЕ",
		ВидСравненияКомпоновкиДанных.Равно,
		0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//Валюта
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокПлатежейГруппаВалюта");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.СписокПлатежей.СуммаПлатежаУЕ",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Истина);
	
	//Выделять отрицательные в подвале
	ИмяПоля = "Доступно";
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, ИмяПоля);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		ИмяПоля,
		ВидСравненияКомпоновкиДанных.Меньше,
		0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	
	ИмяПоля = "ПослеОплат";
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, ИмяПоля);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		ИмяПоля,
		ВидСравненияКомпоновкиДанных.Меньше,
		0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(
		ЭлементУО.Поля,
		"СписокПлатежейБанковскийСчетПредставление");
	
	Если ЭтоМобильныйКлиент Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"БанковскийСчет",
		ВидСравненияКомпоновкиДанных.Заполнено);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДатаНачалаПлатежей()
	
	Отбор = Новый Структура("Состояние",Перечисления.СостоянияБанковскихДокументов.Согласовано);
	ТаблицаДат = Объект.СписокПлатежей.Выгрузить(Отбор,"ДатаПлатежа");
	Если Не ТаблицаДат.Количество() Тогда
		Возврат Дата;
	КонецЕсли;
	
	ТаблицаДат.Сортировать("ДатаПлатежа Возр");
	
	Возврат ТаблицаДат[0].ДатаПлатежа;
	
КонецФункции 

&НаКлиенте
Процедура УстановитьЗначенияФлажков(ЗначениеФлажка)
	
	МассивСтрок = Объект.СписокПлатежей.НайтиСтроки(Новый Структура("Изменить", Не ЗначениеФлажка));
	СостояниеОтправлено = ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отправлено");
	Для Каждого ОбрабатываемаяСтрока Из МассивСтрок Цикл
		Если ОбрабатываемаяСтрока.Состояние = СостояниеОтправлено Тогда
			Продолжить;
		КонецЕсли;
		Если Элементы.СписокПлатежей.ПроверитьСтроку(ОбрабатываемаяСтрока.ПолучитьИдентификатор()) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ОбрабатываемаяСтрока.Изменить = ЗначениеФлажка;
	КонецЦикла;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбновитьПослеЗакрытияФормыДокумента(Результат, СтрокаСписка) Экспорт
	
	СтруктураПараметров = Новый Структура("Организация, БанковскийСчет, Дата");
	
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ЭтотОбъект);
	
	РеквизитыПлатежноеПоручение = ПолучитьРеквизитыПлатежногоПоручения(СтрокаСписка.ПлатежноеПоручение, СтруктураПараметров);
	
	Если РеквизитыПлатежноеПоручение <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(СтрокаСписка,РеквизитыПлатежноеПоручение);
		СтрокаСписка.Изменить = Ложь;
		
		РассчитатьИтоги();
	Иначе
		ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТабличнуюЧасть(Результат, ДопПараметры) Экспорт
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги",0.1,Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыПлатежногоПоручения(ПлатежноеПоручение, СтруктураПараметров)
	
	Результат = Обработки.СогласованиеПлатежей.ПолучитьРеквизитыПлатежногоПоручения(ПлатежноеПоручение,
		СтруктураПараметров);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура НачатьУстановкуСостоянияПлатежей(Действие, НовоеСостояние, СогласоватьВсе = Ложь)
	
	Если Объект.СписокПлатежей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныеСтроки = ПолучитьОбрабатываемыеСтроки(СогласоватьВсе, НовоеСостояние);
	Если ВыбранныеСтроки.Количество() = 0 Тогда
		ПоказатьОповещениеПользователя(,,НСтр("ru = 'Состояние уже изменено'"),БиблиотекаКартинок.Внимание48);
		Возврат;
	КонецЕсли;
	
	Если ВыбранныеСтроки.Количество() = 1 Тогда
		УстановитьСостояниеПлатежа(ВыбранныеСтроки, НовоеСостояние);
	ИначеЕсли ВыбранныеСтроки.Количество() > 1 Тогда
		НачатьУстановкуДействияСПлатежом(ВыбранныеСтроки, Действие, НовоеСостояние);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСуммуПлатежа()
	
	ТекущиеДанные = Элементы.СписокПлатежей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьСуммуПлатежаНаСервере(ТекущиеДанные.ПлатежноеПоручение, ТекущиеДанные.Коплате, ТекущиеДанные.НазначениеПлатежа);
	
	Если ТекущиеДанные.ВалютаОснования <> ВалютаРеглУчета Тогда
		ТекущиеДанные.СуммаПлатежаУЕ = ПересчитатьВалютуПоКурсу(ТекущиеДанные.КОплате, ТекущиеДанные.Курс, ТекущиеДанные.Кратность);
		ТекущиеДанные.НадписьВалютаОснования = ТекущиеДанные.ВалютаОснования;
	КонецЕсли;
	
	РассчитатьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСуммуПлатежаУЕ()
	
	ТекущиеДанные = Элементы.СписокПлатежей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Коплате = ПересчитатьРублиВВалюту(ТекущиеДанные.СуммаПлатежаУЕ, ТекущиеДанные.Курс, ТекущиеДанные.Кратность);
	
	НазначениеПлатежа = "";
	ИзменитьСуммуПлатежаНаСервере(ТекущиеДанные.ПлатежноеПоручение, ТекущиеДанные.Коплате, НазначениеПлатежа);
	
	ТекущиеДанные.НазначениеПлатежа = НазначениеПлатежа;
	
	РассчитатьИтоги();
	
КонецПроцедуры 

&НаСервере
Процедура ИзменитьКомментарииСпискаПлатежей(ВыбранныеСтроки, Комментарий)
	
	Для Каждого ВыбраннаяСтрока Из ВыбранныеСтроки Цикл
		СтрокаПлатеж = Объект.СписокПлатежей.Получить(ВыбраннаяСтрока);
		ИзменитьКомментарийПлатежаНаСервере(СтрокаПлатеж.ПлатежноеПоручение, Комментарий);
		СтрокаПлатеж.Комментарий = Комментарий;
	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьСостояниеПлатежа(ВыбранныеСтроки, НовоеСостояние)
	
	Для Каждого ВыбраннаяСтрока Из ВыбранныеСтроки Цикл
		СтрокаПлатеж = Объект.СписокПлатежей.Получить(ВыбраннаяСтрока);
		
		ДокументОбъект = СтрокаПлатеж.ПлатежноеПоручение.ПолучитьОбъект();
		
		СведенияОДатеЗапрета = ДатыЗапретаИзмененияСлужебный.ПроверитьДатыЗапретаИзмененияЗагрузкиДанных(
			ДокументОбъект, Ложь, Истина, Ложь);
		Если СведенияОДатеЗапрета.ИзменениеЗапрещено Тогда
			СтрокаПлатеж.Изменить = Ложь;
			Продолжить;
		КонецЕсли;
		
		РегистрыСведений.СостоянияБанковскихДокументов.ИзменитьСостояниеСогласованияДокумента(СтрокаПлатеж.ПлатежноеПоручение, НовоеСостояние);
		
		СтрокаПлатеж.Состояние = НовоеСостояние;
		ПлатежноеПоручениеПредставление = Обработки.СогласованиеПлатежей.СформироватьПредставлениеСтрокиПлатежноеПоручение(
			СтрокаПлатеж.Состояние,
			СтрокаПлатеж.НомерПлатежа,
			СтрокаПлатеж.ДатаПлатежа);
		
		СтрокаПлатеж.ПлатежноеПоручениеПредставление = ПлатежноеПоручениеПредставление
	КонецЦикла;
	
	РассчитатьИтоги();
	
КонецПроцедуры 

&НаКлиенте
Процедура НачатьУстановкуДействияСПлатежом(ОтмеченныеСтроки, Действие, СостояниеПлатежа = Неопределено)
	
	ВсегоОтмечено = ОтмеченныеСтроки.Количество();
	Если ВсегоОтмечено = 0 Тогда
		Возврат;
	ИначеЕсли ВсегоОтмечено = 1 Тогда
		Если Действие = 2 Тогда
			ШаблонСообщения = НСтр("ru = 'Перенести платеж №%1 от %2 г.?'");
		ИначеЕсли Действие = 3 Тогда 
			ШаблонСообщения = НСтр("ru = 'Записать комментарий в платеж №%1 от %2 г.?'");
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(ШаблонСообщения,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ОтмеченныеСтроки[0].НомерПлатежа),
		Формат(ОтмеченныеСтроки[0].ДатаПлатежа,"ДФ=dd.MM.yyyy"));
		
	ИначеЕсли ВсегоОтмечено > 1 Тогда
		Если Действие = 0 Тогда
			ШаблонСообщения =
			НСтр("ru=';Отклонить %1 платеж?;;Отклонить %1 платежа?;Отклонить %1 платежей?;';");
		ИначеЕсли Действие = 1 Тогда
			ШаблонСообщения =
			НСтр("ru=';Согласовать %1 платеж?;;Согласовать %1 платежа?;Согласовать %1 платежей?;';");
		ИначеЕсли Действие = 2 Тогда
			ШаблонСообщения =
			НСтр("ru=';Перенести %1 платеж?;;Перенести %1 платежа?;Перенести %1 платежей?;';");
		ИначеЕсли Действие = 3 Тогда
			ШаблонСообщения =
			НСтр("ru=';Установить комментарий для %1 платежа?;;Установить комментарий для %1 платежей?;Установить комментарий для %1 платежей?;';");
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонСообщения,ВсегоОтмечено);
	КонецЕсли;
	
	ПараметрыВопроса = Новый Структура;
	ПараметрыВопроса.Вставить("Действие", Действие);
	ПараметрыВопроса.Вставить("СостояниеПлатежа", СостояниеПлатежа);
	ПараметрыВопроса.Вставить("ОтмеченныеСтроки",ОтмеченныеСтроки);
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаДействиеСПлатежом", ЭтотОбъект, ПараметрыВопроса);
	ПоказатьВопрос(Оповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьОбрабатываемыеСтроки(СогласоватьВсе = Ложь, НовоеСостояние = Неопределено)
	
	ОбрабатываемыеСтроки = Новый Массив;
	СтатусыРедактирования = СтатусыРедактирования();
	
	ВыделенныеСтрокиСписка = Объект.СписокПлатежей.НайтиСтроки(Новый Структура("Изменить", Истина));
	ВыделенныеСтроки = Новый Массив;
	
	Для Каждого Строка Из ВыделенныеСтрокиСписка Цикл
		Если Строка.ВыборСтатуса Тогда
			ВыделенныеСтроки.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;

	Если Не ВыделенныеСтроки.Количество() Тогда
		ТекущиеДанные = Элементы.СписокПлатежей.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат Новый Массив;
		КонецЕсли;
		
		ДобавитьПлатежВОбрабатываемыеСтроки(ТекущиеДанные, ОбрабатываемыеСтроки, СтатусыРедактирования, НовоеСостояние);
		Возврат ОбрабатываемыеСтроки;
	КонецЕсли;
	
	Для Каждого СтрокаСписокПлатежей Из ВыделенныеСтроки Цикл
		ДобавитьПлатежВОбрабатываемыеСтроки(СтрокаСписокПлатежей, ОбрабатываемыеСтроки, СтатусыРедактирования, НовоеСостояние);
	КонецЦикла;
	
	Возврат ОбрабатываемыеСтроки;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьПлатежВОбрабатываемыеСтроки(СтрокаПлатеж, ОбрабатываемыеСтроки, СтатусыРедактирования, НовоеСостояние)
	
	Если СтрокаПлатеж = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтатусыРедактирования.Найти(СтрокаПлатеж.Состояние) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаПлатеж.Состояние = НовоеСостояние Тогда
		Возврат;
	КонецЕсли;
	
	ИндексСтроки = Объект.СписокПлатежей.Индекс(СтрокаПлатеж);
	ОбрабатываемыеСтроки.Добавить(ИндексСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаДействиеСПлатежом(Результат, ПараметрыВопроса) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныеСтроки = ПараметрыВопроса.ОтмеченныеСтроки;
	
	Если ПараметрыВопроса.Действие = 0 Или ПараметрыВопроса.Действие = 1 Тогда
		УстановитьСостояниеПлатежа(ВыбранныеСтроки, ПараметрыВопроса.СостояниеПлатежа);
	ИначеЕсли ПараметрыВопроса.Действие = 3 Тогда
		ВвестиКомментарийКПлатежу(ВыбранныеСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СтатусыРедактирования()
	
	СтатусыРедактирования = Новый Массив;
	СтатусыРедактирования.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.ПустаяСсылка"));
	СтатусыРедактирования.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отклонено"));
	СтатусыРедактирования.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Подготовлено"));
	СтатусыРедактирования.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Согласовано"));
	СтатусыРедактирования.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отправлено"));
	
	Возврат СтатусыРедактирования;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги() Экспорт
	
	ЗаполнитьНаСервереРассчитатьИтоги();
	
	ОпределитьСостояние();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервереРассчитатьИтоги()
	
	ЗаполнитьНаСервере();
	
	РассчитатьИтоги();
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Объект.СписокПлатежей.Очистить();
	
	ДанныеСвойствДляЗапроса = Новый Структура("Организация, БанковскийСчет, Дата");
	
	ДанныеСвойствДляЗапроса.Вставить("ВалютаРеглУчета", ВалютаРеглУчета);
	
	ЗаполнитьЗначенияСвойств(ДанныеСвойствДляЗапроса, ЭтотОбъект);
	
	РезультатЗапроса = Обработки.СогласованиеПлатежей.ЗапросДанныеПлатежей(ДанныеСвойствДляЗапроса);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	СоответствиеОснований = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		СтрокаСписка = Объект.СписокПлатежей.Добавить();
		
		СтрокаСписка.ПлатежноеПоручениеПредставление = Обработки.СогласованиеПлатежей.СформироватьПредставлениеСтрокиПлатежноеПоручение(
		Выборка.Состояние, Выборка.НомерПлатежа, Выборка.ДатаПлатежа);
		СтрокаСписка.БанковскийСчетПредставление =  Выборка.БанковскийСчет;
		
		НомерДокументаБезНулей = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(Выборка.НомерПлатежа);
		СтрокаСписка.Номер = ПрефиксацияОбъектовКлиентСервер.УдалитьПрефиксыИзНомераОбъекта(НомерДокументаБезНулей, Истина, Истина);
		СтрокаСписка.Дата = Выборка.ДатаПлатежа;
		
		Если ТипЗнч(Выборка.Основание) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			СтрокаСписка.ОснованиеПредставление = Обработки.СогласованиеПлатежей.ПредставлениеОснования(Выборка.Основание);
		КонецЕсли;
		
		СтрокаСписка.ВалютаОснования = Выборка.ВалютаВзаиморасчетов;
		СтрокаСписка.НадписьВалютаПлатежа = ВалютаРеглУчета;
		
		Если Выборка.ВалютаВзаиморасчетов <> ВалютаРеглУчета Тогда
			СтрокаСписка.СуммаПлатежаУЕ = ПересчитатьВалютуПоКурсу(Выборка.КОплате, Выборка.Курс, Выборка.Кратность);
			СтрокаСписка.НадписьВалютаОснования = Выборка.ВалютаВзаиморасчетов;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаСписка,Выборка);
		Если Не ЗначениеЗаполнено(Выборка.Состояние) Тогда
			СтрокаСписка.Состояние = Перечисления.СостоянияБанковскихДокументов.Подготовлено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ДокументОснование) Тогда
			ТипОснования = ТипЗнч(Выборка.ДокументОснование);
			Если СоответствиеОснований[ТипОснования] = Неопределено Тогда
				СоответствиеОснований.Вставить(ТипОснования,Новый Массив);
			КонецЕсли;
			
			СоответствиеОснований[ТипОснования].Добавить(Выборка.ДокументОснование);
		КонецЕсли;
	КонецЦикла; 
	
	Для Каждого ДокументыОснования ИЗ СоответствиеОснований Цикл
		ДоступныеРеквизиты = Обработки.СогласованиеПлатежей.ДоступныеРеквизитыОснования(ДокументыОснования.Ключ);
		РеквизитыОснований = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДокументыОснования.Значение, ДоступныеРеквизиты);
		
		ИменаРеквизитов = Обработки.СогласованиеПлатежей.ИменаРеквизитовОснования(ДокументыОснования.Ключ);
		
		Если ДоступныеРеквизиты <> Неопределено Тогда
			Для Каждого ДанныеОснований Из РеквизитыОснований Цикл
				ПлатежиПоОснованиям = Объект.СписокПлатежей.НайтиСтроки(Новый Структура("Основание", ДанныеОснований.Ключ));
				
				Для Каждого Платеж Из ПлатежиПоОснованиям Цикл
					Платеж.ВалютаОснования = ?(ИменаРеквизитов.Свойство("ВалютаДокумента"), ДанныеОснований.Значение[ИменаРеквизитов.ВалютаДокумента],ВалютаРеглУчета);
					Платеж.СуммаОснования = ДанныеОснований.Значение[ИменаРеквизитов.СуммаДокумента];
					
					Платеж.ОснованиеПредставление = Обработки.СогласованиеПлатежей.ПредставлениеОснования(
						Платеж.Основание,
						Платеж.НомерОснования,
						Платеж.ДатаОснования,
						Платеж.ВалютаОснования,
						Платеж.СуммаОснования);
				КонецЦикла;
				
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтоги()
	
	Элементы.СтатусПодготовлено.Заголовок 	= СтрШаблон(НСтр("ru = 'Подготовлено (%1)'"),0);
	Элементы.СтатусСогласовано.Заголовок 	= СтрШаблон(НСтр("ru = 'Согласовано (%1)'"),0);
	Элементы.СтатусОтправлено.Заголовок 	= СтрШаблон(НСтр("ru = 'Отправлено (%1)'"),0);
	Элементы.СтатусОтклонено.Заголовок 		= СтрШаблон(НСтр("ru = 'Отклонено (%1)'"),0);
	Элементы.СтатусВсеПлатежи.Заголовок 	= СтрШаблон(НСтр("ru = 'Все платежи (%1)'"),0);
	
	Подготовлено 	= 0;
	Согласовано 	= 0;
	Отправлено 		= 0;
	Отклонено 		= 0;
	
	ТЗПлатежи = Объект.СписокПлатежей;
	
	СостояниеПодготовлено 	= Перечисления.СостоянияБанковскихДокументов.Подготовлено;
	СостояниеСогласовано 	= Перечисления.СостоянияБанковскихДокументов.Согласовано;
	СостояниеОтправлено 	= Перечисления.СостоянияБанковскихДокументов.Отправлено;
	СостояниеОтклонено 		= Перечисления.СостоянияБанковскихДокументов.Отклонено;
	
	ВсегоПлатежей 			= 0;
	КоличествоПодготовлено 	= 0;
	КоличествоСогласовано 	= 0;
	КоличествоОтправлено 	= 0;
	КоличествоОтклонено 	= 0;
	
	// Подсчет показателей итого в подвале формы
	Для каждого Строка Из ТЗПлатежи Цикл
		Если Строка.Состояние = СостояниеПодготовлено Тогда
			Подготовлено = Подготовлено + Строка.КОплате;
			КоличествоПодготовлено = КоличествоПодготовлено + 1;
		ИначеЕсли Строка.Состояние = СостояниеСогласовано Тогда
			Согласовано = Согласовано + Строка.КОплате;
			КоличествоСогласовано = КоличествоСогласовано + 1;
		ИначеЕсли Строка.Состояние = СостояниеОтправлено Тогда
			Отправлено = Отправлено + Строка.КОплате;
			КоличествоОтправлено = КоличествоОтправлено + 1;
		ИначеЕсли Строка.Состояние = СостояниеОтклонено Тогда
			Отклонено = Отклонено + Строка.КОплате;
			КоличествоОтклонено = КоличествоОтклонено + 1;
		КонецЕсли;	
	КонецЦикла;
	
	ВсегоПлатежей = ТЗПлатежи.Количество();
	
	Элементы.СтатусПодготовлено.Заголовок 	= СтрШаблон(НСтр("ru = 'Подготовлено (%1)'"), КоличествоПодготовлено);
	Элементы.СтатусСогласовано.Заголовок 	= СтрШаблон(НСтр("ru = 'Согласовано (%1)'"), КоличествоСогласовано);
	Элементы.СтатусОтправлено.Заголовок 	= СтрШаблон(НСтр("ru = 'Отправлено (%1)'"), КоличествоОтправлено);
	Элементы.СтатусОтклонено.Заголовок 		= СтрШаблон(НСтр("ru = 'Отклонено (%1)'"), КоличествоОтклонено);
	Элементы.СтатусВсеПлатежи.Заголовок 	= СтрШаблон(НСтр("ru = 'Все платежи (%1)'"), ВсегоПлатежей);
	
	//Доступно
	БанковскийСчета = Новый Массив;
	
	Если БанковскийСчет.Количество() > 0 Тогда
		БанковскийСчета = БанковскийСчет;
	Иначе
		МассивОрганизаций = Новый Массив;
		Если ЗначениеЗаполнено(Организация) Тогда
			МассивОрганизаций.Добавить(Организация);
		Иначе
			МассивОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	БанковскиеСчета.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.Владелец В(&СписокОрганизаций)";
		
		Запрос.УстановитьПараметр("СписокОрганизаций", МассивОрганизаций);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			БанковскийСчета.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Доступно = 0;
	ПослеОплат = 0;
	
	ВидыСубконтоБанковскиеСчета = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.БанковскиеСчета;
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.ЦифровойРубль);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.СпециальныеСчета);
	УсловияОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловияОтбора.Валютный = Ложь;
	МассивСчетовССубсчетами = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(МассивСчетов, УсловияОтбора);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("БанковскийСчета", БанковскийСчета);
	Запрос.УстановитьПараметр("МассивСчетовССубсчетами", МассивСчетовССубсчетами);
	Запрос.УстановитьПараметр("ВидСубконто", ВидыСубконтоБанковскиеСчета);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК СчетОрганизации,
	|	ХозрасчетныйОстатки.СуммаОстатокДт КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет В (&МассивСчетовССубсчетами), &ВидСубконто, Субконто1 В (&БанковскийСчета)) КАК ХозрасчетныйОстатки";
	
	ТаблицаОстатковНаСчетах = Запрос.Выполнить().Выгрузить();
	
	Доступно = ТаблицаОстатковНаСчетах.Итог("Сумма");
	ПослеОплат  = Доступно - Согласовано - Отправлено;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьСуммуПлатежаНаСервере(ПлатежноеПоручение, СуммаДокумента, НазначениеПлатежа = "")
	
	ДокументОбъект = ПлатежноеПоручение.ПолучитьОбъект();
	Попытка
		ДокументОбъект.Заблокировать();
	Исключение
		ТекстСообщения = НСтр("ru = 'Нельзя отредактировать сумму, т.к. документ уже редактируется!
		|Завершите редактирование документа.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	
	ДокументОбъект.СуммаДокумента = СуммаДокумента;
	ПроцентНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ДокументОбъект.СтавкаНДС);
	ДокументОбъект.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(СуммаДокумента, Истина, ПроцентНДС);
	
	// Реквизиты плательщика и получателя
	АвтоЗначенияРеквизитов = УчетДенежныхСредствБП.СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя(
		ДокументОбъект.Организация,
		ДокументОбъект.СчетОрганизации,
		ДокументОбъект.Контрагент,
		ДокументОбъект.СчетКонтрагента,
		ДокументОбъект.ПеречислениеВБюджет,
		ДокументОбъект.Дата);
	ЗаполнитьЗначенияСвойств(ДокументОбъект, АвтоЗначенияРеквизитов,, ?(ЗначениеЗаполнено(ДокументОбъект.КПППлательщика), "КПППлательщика", ""));
	
	Документы.ПлатежноеПоручение.СформироватьНазначениеПлатежа(ДокументОбъект, АвтоЗначенияРеквизитов, Истина);
	
	НазначениеПлатежа = ДокументОбъект.НазначениеПлатежа;
	
	ДокументОбъект.Записать();
	ДокументОбъект.Разблокировать();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьКомментарийПлатежаНаСервере(ПлатежноеПоручение, Комментарий)
	
	ДокументОбъект = ПлатежноеПоручение.ПолучитьОбъект(); 
	Попытка
		ДокументОбъект.Заблокировать();
	Исключение     
		ТекстСообщения = НСтр("ru = 'Нельзя отредактировать комментарий, т.к. документ уже редактируется!
		|Завершите редактирование документа.'");		
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);   	
		Возврат;
	КонецПопытки;        
	
	ДокументОбъект.Комментарий = Комментарий; 
	ДокументОбъект.Записать();
	ДокументОбъект.Разблокировать();  
	
КонецПроцедуры   

&НаКлиенте
Асинх Процедура ВвестиКомментарийКПлатежу(ВыбранныеСтроки)
	
	ОбещаниеКомментария = ВвестиСтрокуАсинх(,"Комментарий");
	Результат = Ждать ОбещаниеКомментария;
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ПоказатьОповещениеПользователя(,,НСтр("ru = 'Нельзя установить пустой комментарий'"),БиблиотекаКартинок.Внимание48);
		Возврат;
	КонецЕсли;
	
	ИзменитьКомментарииСпискаПлатежей(ВыбранныеСтроки, Результат);
	
КонецПроцедуры 

&НаСервере
Функция ПодготовитьРеестрПлатежейНаСервере()
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("ПФ_MXL_РеестрПлатежей");
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ОбластьПечати = Макет.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	ОбластьПечати = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПечати.Параметры.Дата = Формат(Дата,"ДЛФ=DD");
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	Организация = ЭтотОбъект.Организация;
	ЗаполненаОрганизация = ЗначениеЗаполнено(Организация);
	Если ЗаполненаОрганизация Тогда
		НаименованияНаДату = Справочники.Организации.НаименованияНаДату(Организация, Дата);
		ОбластьПечати = Макет.ПолучитьОбласть("РеквизитыОрганизации");
		ОбластьПечати.Параметры.НадписьОрганизация = НаименованияНаДату.СокращенноеНаименование;
		ТабличныйДокумент.Вывести(ОбластьПечати);
	КонецЕсли; 
	
	Если БанковскийСчет.Количество() > 0 Тогда
		ОбластьПечати = Макет.ПолучитьОбласть("Отбор");
		ОтборСчетов = СтрСоединить(БанковскийСчет.ВыгрузитьЗначения(), "; ");
		ОбластьПечати.Параметры.Отбор = ОтборСчетов;
		ТабличныйДокумент.Вывести(ОбластьПечати);
	КонецЕсли;
	
	ОбластьПечати = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	СостояниеОтклонено = Перечисления.СостоянияБанковскихДокументов.Отклонено;
	
	Для Каждого СтрокаПлатеж Из Объект.СписокПлатежей Цикл  
		Если СтрокаПлатеж.ВыборСтатуса Тогда
			Если СтрокаПлатеж.Состояние = СостояниеОтклонено Тогда
				ОбластьПечати = Макет.ПолучитьОбласть("СтрокаТаблицыЗачеркнутыйТекст");
			Иначе
				ОбластьПечати = Макет.ПолучитьОбласть("СтрокаТаблицы");
			КонецЕсли;
			
			ОбластьПечати.Параметры.НомерСтроки = СтрокаПлатеж.НомерСтроки;
			
			ПредставлениеНомер = СтрокаПлатеж.Номер;
			ПредставлениеДата = Формат(СтрокаПлатеж.ДатаПлатежа, "ДФ=dd.MM.yyyy");
			
			ПлатежноеПоручениеПредставление = Обработки.СогласованиеПлатежей.СформироватьПредставлениеСтрокиПлатежноеПоручение(
			СтрокаПлатеж.Состояние, СтрокаПлатеж.НомерПлатежа, СтрокаПлатеж.ДатаПлатежа);
			
			ОбластьПечати.Параметры.ПредставлениеПлатежногоПоручения = ПлатежноеПоручениеПредставление;
			ОбластьПечати.Параметры.Основание = СтрокаПлатеж.ОснованиеПредставление;
			ОбластьПечати.Параметры.ПредставлениеДата = ПредставлениеДата;
			ОбластьПечати.Параметры.ПредставлениеНомер= ПредставлениеНомер;
			ОбластьПечати.Параметры.ПредставлениеПлатежногоПоручения = ПлатежноеПоручениеПредставление;
			ОбластьПечати.Параметры.Получатель = СтрокаПлатеж.Получатель;
			ОбластьПечати.Параметры.СуммаПлатежа = СтрокаПлатеж.КОплате;
			ОбластьПечати.Параметры.НазначениеПлатежа = СтрокаПлатеж.НазначениеПлатежа;
			ОбластьПечати.Параметры.Комментарий =  СтрокаПлатеж.Комментарий;
			ОбластьПечати.Параметры.БанковскийСчет =
			ПредставлениеБанковскогоСчета(СтрокаПлатеж.БанковскийСчет, Не ЗаполненаОрганизация);
			ТабличныйДокумент.Вывести(ОбластьПечати);
		КонецЕсли;
	КонецЦикла;
	
	ОбластьПечати = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	ОбластьПечати = Макет.ПолучитьОбласть("Итоги");
	ОбластьПечати.Параметры.НаСчетах = Доступно;
	ОбластьПечати.Параметры.Согласовано = Согласовано;
	ОбластьПечати.Параметры.Отклонено = Отклонено;
	ОбластьПечати.Параметры.Подготовлено = Подготовлено;
	ОбластьПечати.Параметры.Отправлено = Отправлено;
	ОбластьПечати.Параметры.Остаток = ПослеОплат;
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	ОбластьПечати = Макет.ПолучитьОбласть("Подвал");
	Если ЗначениеЗаполнено(Организация) Тогда
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		ОбластьПечати.Параметры.ДолжностьПредставление = ОтветственныеЛица.РуководительДолжностьПредставление;
		ОбластьПечати.Параметры.РуководительПредставление = ОтветственныеЛица.РуководительПредставление;
	КонецЕсли;
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	Возврат ТабличныйДокумент;
	
КонецФункции 

&НаСервере
Функция ПредставлениеБанковскогоСчета(БанковскийСчет, УказатьОрганизацию = Ложь)
	
	РеквизитыБанковскогоСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БанковскийСчет,"Владелец, Банк, НомерСчета");
	НаименованиеБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыБанковскогоСчета.Банк,"Наименование");
	ПредставлениеСчета = СтрШаблон("%1, *%2", НаименованиеБанка, Прав(РеквизитыБанковскогоСчета.НомерСчета,4));
	
	Если Не УказатьОрганизацию Тогда
		Возврат ПредставлениеСчета;
	Иначе 
		НаименованияНаДату = Справочники.Организации.НаименованияНаДату(Организация, Дата);
		Возврат СтрШаблон("%1; %2", ПредставлениеСчета, НаименованияНаДату.СокращенноеНаименование);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = НСтр("ru = 'Отзыв о функциональности ""Согласование платежей""'");
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьВидимостьПодсказкиБанковскийСчет()
	
	Если БанковскийСчет.Количество() = 0 Тогда
		Элементы.БанковскийСчет.ПодсказкаВвода = НСтр("ru = 'По всем банковским счетам'");
	Иначе
		Элементы.БанковскийСчет.ПодсказкаВвода = "";
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтборПоСчету(ВведенноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВведенноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	БанковскийСчет = ВведенноеЗначение.СписокОтмеченныхСчетов;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьТаблицуРассчитатьИтоги", 0.1, Истина);
	
	УстановитьВидимостьПодсказкиБанковскийСчет();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьНаСервереРассчитатьИтоги() Экспорт
	
	ЗаполнитьНаСервереРассчитатьИтоги();
	
	ОпределитьСостояние();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораСчета(ВведенноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВведенноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("БанковскийСчет", ВведенноеЗначение.БанковскийСчет);
	ДополнительныеПараметры.Вставить("Организация", ВведенноеЗначение.Организация);

	ОткрытьФорму("Обработка.КлиентБанк.Форма", ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Функция СтруктураПараметровФормы()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОтправкаВБанк", Ложь);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("БанковскийСчет", БанковскийСчет);
	ПараметрыФормы.Вставить("ВыбраннаяСтрока", Неопределено);

	Возврат ПараметрыФормы;
КонецФункции

&НаСервере
Процедура ПроверитьНастройкиПриПервомЗапуске()
	
	// Реквизит формы "ЕстьСохраненнаяНастройка" автоматически сохраняется в кэш после закрытия формы.
	// Поэтому при первом входе у реквизита по умолчанию значение ЛОЖЬ.
	Если Не ЕстьСохраненнаяНастройка Тогда
		СтатусПодготовлено 	= Истина;
		СтатусСогласовано 	= Истина;
		СтатусОтклонено 	= Истина;
		СтатусОтправлено 	= Истина;
		СтатусВсеПлатежи 	= Истина;
	КонецЕсли;
	
	ЕстьСохраненнаяНастройка = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьСтатусы()
	
	Элементы.СтатусВсеПлатежи.ТриСостояния = Ложь;
	
	Если СтатусВсеПлатежи = 1 Тогда
		СтатусПодготовлено 	= Истина;
		СтатусСогласовано 	= Истина;
		СтатусОтклонено 	= Истина;
		СтатусОтправлено	= Истина;
	ИначеЕсли СтатусВсеПлатежи = 0 Тогда
		СтатусПодготовлено 	= Ложь;
		СтатусСогласовано 	= Ложь;
		СтатусОтклонено 	= Ложь;
		СтатусОтправлено	= Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьСостояниеВсеПлатежи()
	
	Элементы.СтатусВсеПлатежи.ТриСостояния = Истина;
	
	Если НЕ СтатусПодготовлено И НЕ СтатусОтклонено И НЕ СтатусОтправлено И НЕ СтатусСогласовано Тогда
		СтатусВсеПлатежи = 0;
	ИначеЕсли СтатусПодготовлено И СтатусОтклонено И СтатусОтправлено И СтатусСогласовано Тогда
		СтатусВсеПлатежи = 1;
	Иначе
		СтатусВсеПлатежи = 2;
	КонецЕсли;
		
КонецПроцедуры  

&НаКлиенте
Процедура ОпределитьСостояние()
	
	ОтобратьСписокПлатежейПоСостоянию(СтатусПодготовлено, 	ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Подготовлено"));
	ОтобратьСписокПлатежейПоСостоянию(СтатусСогласовано, 	ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Согласовано"));
	ОтобратьСписокПлатежейПоСостоянию(СтатусОтклонено, 		ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отклонено"));
	ОтобратьСписокПлатежейПоСостоянию(СтатусОтправлено, 	ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Отправлено"));
	
	Элементы.СписокПлатежей.ОтборСтрок = Новый ФиксированнаяСтруктура("ВыборСтатуса", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьСписокПлатежейПоСостоянию(ЗначениеФлажка, Состояние);
	
	Для каждого Строка Из Объект.СписокПлатежей Цикл
		Если Строка.Состояние = Состояние Тогда
			Строка.ВыборСтатуса = ЗначениеФлажка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОрганизацияПоСчету(БанковскийСчет)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиентАдаптация

&НаСервере
Процедура АдаптироватьЭлементыФормыМК()
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьЭлементыФормыМК();
	
	НастроитьОтображениеЭлементовФормыМК();
	
	НастроитьПоложениеЭлементовМК();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыФормыМК()
	
	НовыйЭлемент = Элементы.Добавить("ГруппаОтбор", Тип("ГруппаФормы"));
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаСписок", Тип("ГруппаФормы"));
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаПодвалСписок", Тип("ГруппаФормы"), Элементы.ГруппаСписок);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	НовыйЭлемент.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	НовыйЭлемент.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Центр;
	НовыйЭлемент.СквозноеВыравнивание = СквозноеВыравнивание.Использовать;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	НовыйЭлемент.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаПодвалСписокЛево", Тип("ГруппаФормы"), Элементы.ГруппаПодвалСписок);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	НовыйЭлемент.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаПодвалСписокПраво", Тип("ГруппаФормы"), Элементы.ГруппаПодвалСписок);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	НовыйЭлемент.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеЭлементовФормыМК()
	
	//общие элементы формы
	Элементы.НаписатьРазработчикам.Заголовок = НСтр("ru = 'Написать разработчикам'");
	Элементы.СписокПлатежейУстановитьКомментарий.Заголовок = НСтр("ru = 'Комментарий'");
	
	//группа отбор
	Элементы.БанковскийСчет.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.Организация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	МассивЭлементовКСкрытию = Новый Массив;
	
	Для каждого Элемент Из Элементы.СписокПлатежейКоманднаяПанель.ПодчиненныеЭлементы Цикл
		Если Элемент <> Элементы.УправлениеСогласованием Тогда
			МассивЭлементовКСкрытию.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	
	МассивЭлементовКСкрытию.Добавить(Элементы.Шапка);
	МассивЭлементовКСкрытию.Добавить(Элементы.Подвал);
	МассивЭлементовКСкрытию.Добавить(Элементы.ОбратнаяСвязь);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейНомерСтроки);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейОснованиеПредставление);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейГруппаКОплате);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейГруппаВалюта);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейНазначениеПлатежа);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейСтатьяДвиженияДенежныхСредств);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейКонтекстноеМенюСоздатьДокумент);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейКонтекстноеМенюГруппаОбновить);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейКонтекстноеМенюГруппаПоиск);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейКонтекстноеМенюГруппаСортировка);
	МассивЭлементовКСкрытию.Добавить(Элементы.СписокПлатежейКонтекстноеМенюВывестиСписок);
	
	ОтключитьВидимостьМассиваЭлементовМК(МассивЭлементовКСкрытию);
	
	//список платежей настройка элементов
	Элементы.СписокПлатежей.ПоложениеСтрокиПоиска = ПоложениеСтрокиПоиска.Нет;
	Элементы.СписокПлатежей.ПоложениеСостоянияПросмотра = ПоложениеСостоянияПросмотра.Нет;
	Элементы.СписокПлатежей.ПоложениеУправленияПоиском = ПоложениеУправленияПоиском.Нет;
	Элементы.СписокПлатежей.ЗапросОбновления = СпособЗапросаОбновления.ПотянутьСверхуИлиСнизу;
	Элементы.СписокПлатежей.ВариантУправленияВысотой = ВариантУправленияВысотойТаблицы.ВСтрокахФормы;
	Элементы.СписокПлатежей.Высота = 20;
	
	Элементы.СписокПлатежей.Шапка = Ложь;
	
	Элементы.СписокПлатежейУстановитьФлажки.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Элементы.СписокПлатежейУстановитьФлажки.Заголовок = НСтр("ru = 'Выбрать все'");
	
	Элементы.СписокПлатежейСнятьФлажки.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Элементы.СписокПлатежейСнятьФлажки.Заголовок = НСтр("ru = 'Очистить выбор'");
	
	Элементы.СписокПлатежейКонтекстноеМенюУстановитьКомментарий.Заголовок = НСтр("ru = 'Комментарий'");
	
	Элементы.СписокПлатежейУстановитьКомментарий.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	
	Элементы.СписокПлатежейГруппаСостояние.Группировка = ГруппировкаКолонок.Горизонтальная;
	
	Элементы.СписокПлатежейВидОперации.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	
	//список платежей подвал настройка элементов
	Элементы.Доступно.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элементы.Доступно.Вид = ВидПоляФормы.ПолеВвода;
	Элементы.Доступно.ТолькоПросмотр = Истина;
	Элементы.Доступно.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.Доступно.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.Доступно.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	Элементы.Доступно.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
	
	Элементы.ПослеОплат.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элементы.ПослеОплат.ТолькоПросмотр = Истина;
	Элементы.ПослеОплат.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ПослеОплат.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.ПослеОплат.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	Элементы.ПослеОплат.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
	
	ШрифтКнопокСогласования = Новый Шрифт(,, Истина);
	
	Элементы.СписокПлатежейСогласовать.Отображение = ОтображениеКнопки.Текст;
	Элементы.СписокПлатежейСогласовать.РастягиватьПоГоризонтали = Истина;
	Элементы.СписокПлатежейСогласовать.РастягиватьПоВертикали = Истина;
	Элементы.СписокПлатежейСогласовать.ЦветТекста = ЦветаСтиля.ЦветТекстаКнопки;
	Элементы.СписокПлатежейСогласовать.ЦветРамки = ЦветаСтиля.ЦветРамкиКнопки;
	Элементы.СписокПлатежейСогласовать.Шрифт = ШрифтКнопокСогласования;
	Элементы.СписокПлатежейСогласовать.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.СписокПлатежейСогласовать.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.СписокПлатежейСогласовать.Высота = 2;	
	
	Элементы.СписокПлатежейОтклонить.Отображение = ОтображениеКнопки.Текст;
	Элементы.СписокПлатежейОтклонить.РастягиватьПоВертикали = Истина;
	Элементы.СписокПлатежейОтклонить.РастягиватьПоГоризонтали = Истина;
	Элементы.СписокПлатежейОтклонить.ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
	Элементы.СписокПлатежейОтклонить.ЦветРамки = ЦветаСтиля.ЦветРамкиКнопки;
	Элементы.СписокПлатежейОтклонить.Шрифт = ШрифтКнопокСогласования;
	Элементы.СписокПлатежейОтклонить.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.СписокПлатежейОтклонить.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;	
	Элементы.СписокПлатежейОтклонить.Высота = 2;
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьВидимостьМассиваЭлементовМК(МассивЭлементов)
	
	Для каждого Элемент Из МассивЭлементов Цикл
		Элемент.Видимость = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПоложениеЭлементовМК()
	
	Элементы.Переместить(Элементы.Дата, Элементы.ГруппаОтбор);
	Элементы.Переместить(Элементы.БанковскийСчет, Элементы.ГруппаОтбор);
	Элементы.Переместить(Элементы.Организация, Элементы.ГруппаОтбор, Элементы.БанковскийСчет);
	
	Элементы.Переместить(Элементы.ОбновитьСписок, ЭтотОбъект.КоманднаяПанель);
	Элементы.Переместить(Элементы.НаписатьРазработчикам, ЭтотОбъект.КоманднаяПанель);
	
	Элементы.Переместить(Элементы.СписокПлатежей, Элементы.ГруппаСписок, Элементы.ГруппаПодвалСписок);
	
	Элементы.Переместить(Элементы.Доступно, Элементы.ГруппаПодвалСписокЛево);
	Элементы.Переместить(Элементы.СписокПлатежейСогласовать, Элементы.ГруппаПодвалСписокЛево);
	
	Элементы.Переместить(Элементы.ПослеОплат, Элементы.ГруппаПодвалСписокПраво);
	Элементы.Переместить(Элементы.СписокПлатежейОтклонить, Элементы.ГруппаПодвалСписокПраво);
	
	Элементы.Переместить(Элементы.СписокПлатежейКОплате, Элементы.СписокПлатежейГруппаСостояние);
	
	Элементы.Переместить(Элементы.СписокПлатежейПолучатель, Элементы.СписокПлатежейГруппаПлатеж);
	
	Элементы.Переместить(Элементы.СписокПлатежейБанковскийСчетПредставление, Элементы.СписокПлатежей, Элементы.СписокПлатежейКомментарий);
	
КонецПроцедуры

&НаСервере
Функция НовыйПараметрыФормыСтроки()
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("ПлатежноеПоручение");
	ПараметрыОткрытия.Вставить("ПлатежноеПоручениеПредставление");
	ПараметрыОткрытия.Вставить("ВидОперации");
	ПараметрыОткрытия.Вставить("ДатаПлатежа");
	ПараметрыОткрытия.Вставить("Комментарий");
	ПараметрыОткрытия.Вставить("КОплате");
	ПараметрыОткрытия.Вставить("Получатель");
	ПараметрыОткрытия.Вставить("СостояниеПлатежа");
	ПараметрыОткрытия.Вставить("Согласовано");
	ПараметрыОткрытия.Вставить("Отклонено");
	ПараметрыОткрытия.Вставить("БанковскийСчет");
	ПараметрыОткрытия.Вставить("НомерПлатежа");
	ПараметрыОткрытия.Вставить("Организация");
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

#КонецОбласти

