
#Область ОбработчикиСобытийФормы  

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокСчетов = ВсеСчетаОрганизации(?(Параметры.Свойство("Организация"), Параметры.Организация, Неопределено));
	
	Если Параметры.ОтправкаВБанк Тогда
		
		Для каждого Строка Из Параметры.СписокСчетов Цикл
			СтрокаТаблицы = ТаблицаСчетов.Добавить();
			СтрокаТаблицы.Счет = Строка;
		КонецЦикла;
	Иначе
		БанковскийСчет = Параметры.БанковскийСчет;
		
		Для каждого Строка Из СписокСчетов Цикл
			СтрокаТаблицы = ТаблицаСчетов.Добавить();
			СтрокаТаблицы.Счет = Строка;
			СтрокаТаблицы.Банк = Строка.Банк;
			СтрокаТаблицы.Организация = Строка.Владелец; 
			НайденнаяСтрока = БанковскийСчет.НайтиПоЗначению(Строка);
			Если НайденнаяСтрока <> Неопределено Тогда
				СтрокаТаблицы.МеткаВыбора = НайденнаяСтрока.Пометка;
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаСчетов.Сортировать("Организация, Банк");
	КонецЕсли;
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();

	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗакрытиеФормы Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(,НСтр("ru = 'Выберите хотя бы один счет'"),,);
	КонецЕсли;
	
	Если Отказ Тогда
		ЗакрытиеФормы = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыполнитьПроцедуруВыбора();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсеФлажки(Команда)
	
	Для каждого Строка Из ТаблицаСчетов Цикл
		Строка.МеткаВыбора = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеФлажки(Команда)
	
	Для каждого Строка Из ТаблицаСчетов Цикл
		Строка.МеткаВыбора = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТаблицаСчетов

&НаКлиенте
Процедура ТаблицаСчетовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.ТаблицаСчетовМеткаВыбора.Видимость Тогда
		СтруктураПараметров = СтруктураПараметровФормы();
		СтруктураПараметров.БанковскийСчет = Элемент.ТекущиеДанные.Счет;
		ВыбратьБанковскийСчет(СтруктураПараметров);
	Иначе
		ВыполнитьПроцедуруВыбора()
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
		
	Если Параметры.ОтправкаВБанк Тогда
		Элементы.НадписьЗаголовок.Заголовок = НСтр("ru = 'Выберите только один счет для отправки в банк'");
		Элементы.НадписьЗаголовок.Видимость = Истина;
		Элементы.ТаблицаСчетовМеткаВыбора.Видимость = Ложь;
		Элементы.ТаблицаСчетовОрганизация.Видимость = Ложь;
		Элементы.ТаблицаСчетовКоманднаяПанель.Видимость = Ложь;
	Иначе
		ЭтотОбъект.Заголовок = НСтр("ru = 'Выбор счетов'");
		Элементы.НадписьЗаголовок.Видимость = Ложь;
		Элементы.ТаблицаСчетовМеткаВыбора.Видимость = Истина;
		Элементы.ТаблицаСчетовОрганизация.Видимость = Истина;
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			Элементы.ТаблицаСчетовОрганизация.Видимость = Ложь;
		Иначе
			Элементы.ТаблицаСчетовОрганизация.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СтруктураПараметровФормы()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("БанковскийСчет");
	СтруктураПараметров.Вставить("СписокОтмеченныхСчетов");
	СтруктураПараметров.Вставить("Организация");
	
	Возврат СтруктураПараметров;
КонецФункции

&НаСервере
Функция ВсеСчетаОрганизации(Организация)
	
	МассивСчетов 		= Новый Массив;
	МассивОрганизаций 	= Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	БанковскиеСчета.Ссылка КАК Счет
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1)
	|	И БанковскиеСчета.Владелец В(&СписокОрганизаций)";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		МассивОрганизаций.Добавить(Организация);
	Иначе
		МассивОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииРазрешенныеПользователю(Пользователи.ТекущийПользователь());
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СписокОрганизаций", МассивОрганизаций);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МассивСчетов.Добавить(Выборка.Счет);
	КонецЦикла;
	
	Возврат МассивСчетов;
КонецФункции

&НаСервереБезКонтекста
Функция ОрганизацияПоСчету(БанковскийСчет)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
	
КонецФункции

&НаКлиенте
Процедура ВыбратьБанковскийСчет(СтруктураПараметров)
	
	Если НЕ ЭтоМобильныйКлиент Тогда;
		ВыбраннаяСтрока = Элементы.ТаблицаСчетов.ТекущиеДанные;
	КонецЕсли;
	
	СписокВыбранныхСчетов = Новый СписокЗначений;
	СтруктураПараметров.СписокОтмеченныхСчетов = СписокВыбранныхСчетов;
	ЗакрытиеФормы = Ложь;
	
	Если Элементы.ТаблицаСчетовМеткаВыбора.Видимость Тогда
		Для каждого СтрокаВыбора Из ТаблицаСчетов Цикл
			Если СтрокаВыбора.МеткаВыбора Тогда
				СписокВыбранныхСчетов.Добавить(СтрокаВыбора.Счет,, СтрокаВыбора.МеткаВыбора);
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЭтоМобильныйКлиент Тогда
			Если СтруктураПараметров.БанковскийСчет <> Неопределено Тогда
				ВыбраннаяСтрока.МеткаВыбора = Истина;
				СписокВыбранныхСчетов.Добавить(СтруктураПараметров.БанковскийСчет,, ВыбраннаяСтрока.МеткаВыбора );
					ОповеститьОВыборе(СтруктураПараметров);
			КонецЕсли;
		КонецЕсли;
	
		Если СписокВыбранныхСчетов.Количество() = 0 Тогда
			ЗакрытиеФормы = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		Если СтруктураПараметров.БанковскийСчет = Неопределено Тогда
			СтруктураПараметров.БанковскийСчет = ВыбраннаяСтрока.Счет;
			СтруктураПараметров.Организация = ОрганизацияПоСчету(ВыбраннаяСтрока.Счет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроцедуруВыбора()
	
	Структура = СтруктураПараметровФормы();
	
	ВыбратьБанковскийСчет(Структура);
	
	ОповеститьОВыборе(Структура);
	
КонецПроцедуры

#КонецОбласти

