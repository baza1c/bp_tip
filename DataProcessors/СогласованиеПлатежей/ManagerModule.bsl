#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьРеквизитыПлатежногоПоручения(ПлатежноеПоручение, ДанныеСвойствДляЗапроса) Экспорт
	
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ДанныеСвойствДляЗапроса.Вставить("ВалютаРеглУчета", ВалютаРеглУчета);
	
	РезультатЗапроса = ЗапросДанныеПлатежей(ДанныеСвойствДляЗапроса, ПлатежноеПоручение);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РеквизитыПлатежноеПоручение = Новый Структура;
	
	РеквизитыПлатежноеПоручение.Вставить("Состояние");
	РеквизитыПлатежноеПоручение.Вставить("Получатель");
	РеквизитыПлатежноеПоручение.Вставить("НомерПлатежа");
	РеквизитыПлатежноеПоручение.Вставить("ДатаПлатежа");
	РеквизитыПлатежноеПоручение.Вставить("СчетОрганизации");
	РеквизитыПлатежноеПоручение.Вставить("ВидОперации");
	РеквизитыПлатежноеПоручение.Вставить("Контрагент");
	РеквизитыПлатежноеПоручение.Вставить("СуммаДокумента");
	РеквизитыПлатежноеПоручение.Вставить("СуммаУЕ");
	РеквизитыПлатежноеПоручение.Вставить("КОплате");
	РеквизитыПлатежноеПоручение.Вставить("Основание");
	РеквизитыПлатежноеПоручение.Вставить("НомерОснования");
	РеквизитыПлатежноеПоручение.Вставить("ДатаОснования");
	РеквизитыПлатежноеПоручение.Вставить("НазначениеПлатежа");
	РеквизитыПлатежноеПоручение.Вставить("Комментарий");
	РеквизитыПлатежноеПоручение.Вставить("БанковскийСчет", Выборка.СчетОрганизации);
	РеквизитыПлатежноеПоручение.Вставить("БанковскийСчетПредставление", Выборка.СчетОрганизации);
	РеквизитыПлатежноеПоручение.Вставить("Курс");
	РеквизитыПлатежноеПоручение.Вставить("Кратность");
	РеквизитыПлатежноеПоручение.Вставить("НадписьВалютаПлатежа", ВалютаРеглУчета);
	РеквизитыПлатежноеПоручение.Вставить("НадписьВалютаОснования");
	РеквизитыПлатежноеПоручение.Вставить("СуммаПлатежаУЕ", 0);
	
	Если Выборка.ВалютаВзаиморасчетов <> ВалютаРеглУчета Тогда
		ТекущаяДатаКурса = ТекущаяДатаСеанса();
		
		СуммаПлатежаУЕ = РаботаСКурсамиВалют.ПересчитатьВВалюту(Выборка.КОплате, ВалютаРеглУчета, Выборка.ВалютаВзаиморасчетов, ТекущаяДатаКурса);
		
		РеквизитыПлатежноеПоручение.Вставить("СуммаПлатежаУЕ", СуммаПлатежаУЕ);
		РеквизитыПлатежноеПоручение.Вставить("НадписьВалютаОснования", Выборка.ВалютаВзаиморасчетов);
	КонецЕсли;
	
	Если ТипЗнч(Выборка.Основание) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		ОснованиеПредставление = ПредставлениеОснования(Выборка.Основание);
	Иначе
		ТипОснования = ТипЗнч(Выборка.Основание);
		
		ДоступныеРеквизиты = ДоступныеРеквизитыОснования(ТипОснования);
		
		Если ДоступныеРеквизиты <> Неопределено Тогда
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Выборка.Основание, ДоступныеРеквизиты);
			
			ИменаРеквизитов = ИменаРеквизитовОснования(ТипОснования);
			
			ВалютаОснования = ?(ИменаРеквизитов.Свойство("ВалютаДокумента"), РеквизитыОснования[ИменаРеквизитов.ВалютаДокумента], ВалютаРеглУчета);
			СуммаОснования = РеквизитыОснования[ИменаРеквизитов.СуммаДокумента];
			
			РеквизитыПлатежноеПоручение.Вставить("ВалютаОснования", ВалютаОснования);
			РеквизитыПлатежноеПоручение.Вставить("СуммаОснования", СуммаОснования);

			ОснованиеПредставление = ПредставлениеОснования(
				Выборка.Основание,
				Выборка.НомерОснования,
				Выборка.ДатаОснования,
				ВалютаОснования,
				СуммаОснования);
		КонецЕсли;
	КонецЕсли;
	
	РеквизитыПлатежноеПоручение.Вставить("ОснованиеПредставление", ОснованиеПредставление);
	
	ПлатежноеПоручениеПредставление = СформироватьПредставлениеСтрокиПлатежноеПоручение(
		Выборка.Состояние, Выборка.НомерПлатежа, Выборка.ДатаПлатежа);
	
	РеквизитыПлатежноеПоручение.Вставить("ПлатежноеПоручениеПредставление",ПлатежноеПоручениеПредставление);
	
	ЗаполнитьЗначенияСвойств(РеквизитыПлатежноеПоручение,Выборка);
	
	Возврат РеквизитыПлатежноеПоручение;
	
КонецФункции

Функция ЗапросДанныеПлатежей(ДанныеСвойствДляЗапроса, ПлатежноеПоручение = Неопределено) Экспорт
	
	ВидыОперацийСписаниеДенежныхСредств = Перечисления.ВидыОперацийСписаниеДенежныхСредств;
	
	ПеречисленияПоИсполнительнымЛистам = Новый Массив;
	
	ПеречисленияПоИсполнительнымЛистам.Добавить(ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПоИсполнительномуЛистуРаботника);
	
	ПеречисленияЗарплаты = Новый Массив;
	
	ПеречисленияЗарплаты.Добавить(ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП);
	ПеречисленияЗарплаты.Добавить(ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику);
	ПеречисленияЗарплаты.Добавить(ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда);
	ПеречисленияЗарплаты.Добавить(ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда);
	
	ПеречисленияНалогов = Новый Массив;
	ПеречисленияНалогов.Добавить(ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога);
	
	ОперацииВыплатСамозанятым = Новый Массив;
	ОперацииВыплатСамозанятым.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым);
	
	МассивОрганизаций = Новый Массив;
	
	Если ЗначениеЗаполнено(ДанныеСвойствДляЗапроса.Организация) Тогда
		МассивОрганизаций.Добавить(ДанныеСвойствДляЗапроса.Организация);
	Иначе
		МассивОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) КАК Кратность,
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаКурса, ) КАК КурсыВалютСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлатежноеПоручение.СчетОрганизации КАК БанковскийСчет,
	|	ЕСТЬNULL(ПлатежноеПоручение.ДоговорКонтрагента.ВалютаВзаиморасчетов, &ВалютаРеглУчета) КАК ВалютаВзаиморасчетов,
	|	ПлатежноеПоручение.ВидОперации КАК ВидОперации,
	|	ПлатежноеПоручение.Дата КАК ДатаПлатежа,
	|	ПлатежноеПоручение.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручение.ВидОперации В (&ОперацииВыплатСамозанятым)
	|			ТОГДА ПлатежноеПоручение.РеестрВыплатСамозанятым
	|		ИНАЧЕ ПлатежноеПоручение.ДокументОснование
	|	КОНЕЦ КАК ДокументОснование,
	|	ПлатежноеПоручение.Комментарий КАК Комментарий,
	|	ПлатежноеПоручение.СуммаДокумента КАК КОплате,
	|	КурсыВалют.Кратность КАК Кратность,
	|	КурсыВалют.Курс КАК Курс,
	|	ПлатежноеПоручение.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ПлатежноеПоручение.Номер КАК НомерПлатежа,
	|	ПлатежноеПоручение.Ссылка КАК ПлатежноеПоручение,
	|	ПлатежноеПоручение.Контрагент КАК Получатель,
	|	ПлатежноеПоручение.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ПлатежноеПоручение.СчетОрганизации КАК СчетОрганизации,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручение.ВидОперации В (&ПеречисленияПоИсполнительнымЛистам)
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПлатежноеПоручение.ВидОперации В (&ПеречисленияЗарплаты)
	|					ТОГДА 2
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ПлатежноеПоручение.ВидОперации В (&ПеречисленияНалогов)
	|							ТОГДА 3
	|						ИНАЧЕ 4
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК Приоритет,
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) КАК Состояние,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Согласовано)
	|			ТОГДА 2
	|		КОГДА ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Отправлено)
	|			ТОГДА 3
	|		КОГДА ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Отклонено)
	|			ТОГДА 4
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПорядокСостояний,
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) В (ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено), ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.ПустаяСсылка)) КАК Изменить,
	|	СостоянияБанковскихДокументов.Ответственный КАК Ответственный,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА ПлатежноеПоручение.ВидОперации В (&ОперацииВыплатСамозанятым)
	|					ТОГДА ПлатежноеПоручение.РеестрВыплатСамозанятым
	|				ИНАЧЕ ПлатежноеПоручение.ДокументОснование
	|			КОНЕЦ <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ВЫБОР
	|					КОГДА ПлатежноеПоручение.ВидОперации В (&ОперацииВыплатСамозанятым)
	|						ТОГДА ПлатежноеПоручение.РеестрВыплатСамозанятым
	|					ИНАЧЕ ПлатежноеПоручение.ДокументОснование
	|				КОНЕЦ
	|		ИНАЧЕ ПлатежноеПоручение.ДоговорКонтрагента
	|	КОНЕЦ КАК Основание,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, НЕОПРЕДЕЛЕНО) КАК ДатаОснования,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.НомерРегистратора, НЕОПРЕДЕЛЕНО) КАК НомерОснования,
	|	&ВалютаРеглУчета КАК ВалютаРеглУчета
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|		ПО ПлатежноеПоручение.Организация = СостоянияБанковскихДокументов.Организация
	|			И ПлатежноеПоручение.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО ПлатежноеПоручение.Организация = ДанныеПервичныхДокументов.Организация
	|			И ПлатежноеПоручение.ДокументОснование = ДанныеПервичныхДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ПлатежноеПоручение.ДоговорКонтрагента.ВалютаВзаиморасчетов = КурсыВалют.Валюта
	|ГДЕ
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) <> ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Оплачено)
	|	И ПлатежноеПоручение.Дата <= &КонецПериода
	|	И ПлатежноеПоручение.Дата >= &НачалоПериода
	|	И ПлатежноеПоручение.Организация В(&МассивОрганизаций)
	|	И &СчетОрганизации
	|	И НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И &СсылкаНаПлатежноеПоручение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокСостояний,
	|	Приоритет,
	|	ДатаПлатежа УБЫВ,
	|	Получатель УБЫВ";
	
	Если ЗначениеЗаполнено(ДанныеСвойствДляЗапроса.БанковскийСчет) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&СчетОрганизации","ПлатежноеПоручение.СчетОрганизации В (&СчетОрганизации)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И &СчетОрганизации","");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПлатежноеПоручение) Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&СсылкаНаПлатежноеПоручение","ПлатежноеПоручение.Ссылка = &СсылкаНаПлатежноеПоручение");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И &СсылкаНаПлатежноеПоручение","");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ДанныеСвойствДляЗапроса.Дата)-3600*24*9);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДанныеСвойствДляЗапроса.Дата));
	Запрос.УстановитьПараметр("ДатаКурса", ОбщегоНазначения.ТекущаяДатаПользователя());
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ДанныеСвойствДляЗапроса.ВалютаРеглУчета);
	Запрос.УстановитьПараметр("ПеречисленияПоИсполнительнымЛистам", ПеречисленияПоИсполнительнымЛистам);
	Запрос.УстановитьПараметр("ПеречисленияЗарплаты", ПеречисленияЗарплаты);
	Запрос.УстановитьПараметр("ПеречисленияНалогов", ПеречисленияНалогов);
	Запрос.УстановитьПараметр("ОперацииВыплатСамозанятым", ОперацииВыплатСамозанятым);
	
	Если ЗначениеЗаполнено(ДанныеСвойствДляЗапроса.БанковскийСчет) Тогда
		Запрос.УстановитьПараметр("СчетОрганизации", ДанныеСвойствДляЗапроса.БанковскийСчет);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПлатежноеПоручение) Тогда
		Запрос.УстановитьПараметр("СсылкаНаПлатежноеПоручение", ПлатежноеПоручение);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция ПредставлениеОснования(Основание, НомерДокумента = Неопределено, ДатаДокумента = Неопределено, ВалютаОснования = Неопределено, Сумма = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Основание) Тогда
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		НаименованиеОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание,"Наименование");
		Если Не ПустаяСтрока(НаименованиеОснования) Тогда
			Возврат НаименованиеОснования;
		Иначе
			Возврат НСтр("ru = 'Договор'");
		КонецЕсли;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = '%1 №%2 от %3 на сумму %4 %5'");
	Синоним = Основание.Метаданные().Синоним;
	
	Если Не ЗначениеЗаполнено(НомерДокумента) Или Не ЗначениеЗаполнено(ДатаДокумента) Тогда
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание,"Номер, Дата");
		Представление = СтрШаблон(ТекстСообщения,
			Синоним,
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыДокумента.Номер),
			Формат(РеквизитыДокумента.Дата,"ДФ=dd.MM.yy"),
			Формат(Сумма,"ЧЦ=15; ЧДЦ=2"),
			ВалютаОснования);
	Иначе
		Представление = СтрШаблон(ТекстСообщения,
			Синоним,
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НомерДокумента),
			Формат(ДатаДокумента,"ДФ=dd.MM.yy"),
			Формат(Сумма,"ЧЦ=15; ЧДЦ=2"),
			ВалютаОснования);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция СформироватьПредставлениеСтрокиПлатежноеПоручение(Состояние, НомерДокумента, ДатаДокумента) Экспорт
	
	ТекстСообщения = НСтр("ru = '%1'");

	ПлатежноеПоручениеПредставление = СтрШаблон(ТекстСообщения,
		?(ЗначениеЗаполнено(Состояние), Состояние, НСтр("ru = 'Подготовлено'")));

	Возврат ПлатежноеПоручениеПредставление;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоступныеРеквизитыОснования(ТипОснования) Экспорт
	
	ДоступныеРеквизиты = Новый Соответствие;
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.СписаниеСРасчетногоСчета"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.АвансовыйОтчет"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ВыкупПредметовЛизинга"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.СчетНаОплатуПоставщика"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ПоступлениеНМА"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ПоступлениеТоваровУслуг"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ВозвратТоваровОтПокупателя"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ПоступлениеДопРасходов"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"), "СуммаДокумента, ВалютаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ОтчетКомитентуОПродажах"), "СуммаДокумента, ВалютаДокумента");
	
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты"), "СуммаДокумента");
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ОтчетОператораСистемыПлатон"), "СуммаДокумента");
	
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк"), "СуммаПоДокументу");
	
	ДоступныеРеквизиты.Вставить(Тип("ДокументСсылка.ВыплатыСамозанятым"), "СуммаДокумента");
	
	РеквизитыОснования = ДоступныеРеквизиты[ТипОснования];
	
	Возврат РеквизитыОснования;

КонецФункции

Функция ИменаРеквизитовОснования(ТипОснования) Экспорт
	
	СтруктураИмен = Новый Структура;
	
	Если ТипОснования = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВыкупПредметовЛизинга") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеНМА") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ПоступлениеДопРасходов") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
		СтруктураИмен.Вставить("ВалютаДокумента", "ВалютаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ОтчетОператораСистемыПлатон") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");	
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаПоДокументу");
	ИначеЕсли ТипОснования = Тип("ДокументСсылка.ВыплатыСамозанятым") Тогда
		СтруктураИмен.Вставить("СуммаДокумента", "СуммаДокумента");
	КонецЕсли;
	
	Возврат СтруктураИмен;
	
КонецФункции

#КонецОбласти

#КонецЕсли
