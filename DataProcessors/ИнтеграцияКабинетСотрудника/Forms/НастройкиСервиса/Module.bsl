
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрокаЗаголовок = НСтр("ru = '1С:Кабинет сотрудника. Настройки'");
	Заголовок = СтрокаЗаголовок;
	
	СтрокаЗаголовок = НСтр("ru = 'Настройки функциональности сервиса 1С:Кабинет сотрудника'");
	Элементы.ДекорацияИнформация.Заголовок = СтрокаЗаголовок;
	
	ПрочитатьНастройки();
	УстановитьДоступностьНастроек();
	ОбновитьЭУ();
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ЗаписатьНастройкиПриЗакрытииФормы", ЭтаФорма);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьБронированиеКомандировок" Тогда
		ПрочитатьДоступныеНастройки();
		УстановитьДоступностьИспользоватьБронированиеКомандировок(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкиСервисаЗаявлениеНаОтпускПриИзменении(Элемент)
	
	Если Не НастройкиСервиса.ЗаявлениеНаОтпуск Тогда
		НастройкиСервиса.МатериальнаяПомощьКОтпуску = Ложь;
		НастройкиСервиса.ЕдиновременнаяВыплатаКОтпуску = Ложь;
	КонецЕсли;
	УстановитьДоступностьДоплатКОтпуску(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервсаЗаявлениеОтпускБезОплатыПриИзменении(Элемент)
	
	Если Не НастройкиСервиса.ЗаявлениеОтпускБезОплаты Тогда
		ОтпускЗаСвойСчетОформлятьЗаднимЧислом = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтпускОформлятьЗаднимЧисломПриИзменении(Элемент)
	
	Если ОтпускОформлятьЗаднимЧислом И НастройкиОтпуска.КонтролироватьДатуНачалаОтпускаПриПодачеЗаявления Тогда
		НастройкиОтпуска.КонтролироватьДатуНачалаОтпускаПриПодачеЗаявления = Ложь;
		НастройкиОтпуска.КоличествоДнейДоНачалаОтпуска = 0;
		ОбновитьЭУКоличествоДнейДоНачалаОтпуска(ЭтаФорма);
		ОбновитьЭУОтпускОформлятьЗаднимЧислом(ЭтаФорма);
	Иначе
		ОбновитьЭУОтпускОформлятьЗаднимЧислом(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролироватьДатуНачалаОтпускаПриПодачеЗаявленияПриИзменении(Элемент)
	
	Если Не НастройкиОтпуска.КонтролироватьДатуНачалаОтпускаПриПодачеЗаявления Тогда
		НастройкиОтпуска.КоличествоДнейДоНачалаОтпуска = 0;
	Иначе
		НастройкиОтпуска.КоличествоДнейДоНачалаОтпуска = 1;
	КонецЕсли;
	ОбновитьЭУКоличествоДнейДоНачалаОтпуска(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаИспользоватьЗаявленияНаПереносОтпускаПриИзменении(Элемент)
	ОбновитьДоступностьЭУДнейДоНачалаОтпуска(ЭтаФорма, Истина);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаИспользоватьЗаявленияНаОтменуОтпускаПриИзменении(Элемент)
	ОбновитьДоступностьЭУДнейДоНачалаОтпуска(ЭтаФорма, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОграничиватьПредоставлениеОтпускаАвансомПриИзменении(Элемент)
	
	Если Не НастройкиОтпуска.ОграничиватьПредоставлениеОтпускаАвансом Тогда
		ОтпускАвансомНеПредоставляется 	= 0;
		ОтпускАвансомПредоставляется 	= 0;
	Иначе
		ОтпускАвансомПредоставляется 	= 0;
		ОтпускАвансомНеПредоставляется 	= 1;
	КонецЕсли;
	НастройкиОтпуска.МаксимальноеКоличествоДнейОтпускаАвансом = 0;
	ОбновитьЭУМаксимальноеКоличествоДнейОтпускаАвансом(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтпускАвансомНеПредоставляетсяПриИзменении(Элемент)
	
	ОтпускАвансомПредоставляется = 0;
	НастройкиОтпуска.МаксимальноеКоличествоДнейОтпускаАвансом = 0;
	ОбновитьЭУМаксимальноеКоличествоДнейОтпускаАвансом(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтпускАвансомПредоставляетсяПриИзменении(Элемент)
	ОтпускАвансомНеПредоставляется = 0;
	НастройкиОтпуска.МаксимальноеКоличествоДнейОтпускаАвансом = 1;
	ОбновитьЭУМаксимальноеКоличествоДнейОтпускаАвансом(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаЗапросНаИзменениеЛичнойИнформацииПриИзменении(Элемент)
	
	Если Не НастройкиСервиса.ЗапросНаИзменениеЛичнойИнформации Тогда
		НастройкиСервиса.ДоступноИзменениеФото = Ложь;
	КонецЕсли;
	УстановитьДоступностьДоступноИзменениеФото(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаЗаявленияНаДСВПриИзменении(Элемент)
	
	Если Не НастройкиСервиса.ЗаявленияНаДСВ Тогда
		НастройкиСервиса.СпособРасчетаДСВПроцентом = Ложь;
		НастройкиСервиса.СпособРасчетаДСВПроцентомНеБолееСуммы = Ложь;
		НастройкиСервиса.СпособРасчетаДСВСуммой = Ложь;
	КонецЕсли;
	
	ОбновитьЭУСпособРасчетаДСВ(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаПоказыватьОхрануТрудаПриИзменении(Элемент)
	
	Если ИспользуетсяВерсияDTO_3_1 Тогда
		Если НастройкиСервиса.ПоказыватьОхрануТруда Тогда
			НастройкиСервиса.ПоказыватьРабочиеМестаОхраныТруда = Истина;
		Иначе
			НастройкиСервиса.ПоказыватьРабочиеМестаОхраныТруда 				= Ложь;
			НастройкиСервиса.ПоказыватьМедицинскиеОсмотры 					= Ложь;
			НастройкиСервиса.ПоказыватьПсихиатрическиеОсвидетельствования 	= Ложь;
			НастройкиСервиса.ПоказыватьСредстваИЗ 							= Ложь;
		КонецЕсли;
	Иначе
		НастройкиСервиса.ПоказыватьРабочиеМестаОхраныТруда = НастройкиСервиса.ПоказыватьОхрануТруда;
	КонецЕсли;
	
	УстановитьДоступностьПодчиненныхНастроекОхраныТруда(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаУходЗаРебенкомИнвалидомПриИзменении(Элемент)
	ОбновитьДоступностьЭУДниУходаОтмена(ЭтаФорма, Истина);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаИсполнениеГособязанностейПриИзменении(Элемент)
	ОбновитьДоступностьЭУИсполнениеГособязанностей(ЭтаФорма, Истина);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСервисаКомандировкаПриИзменении(Элемент)
	ОбновитьДоступностьЭУКомандировки(ЭтаФорма, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КомандировкаОграничиватьОтменуПриИзменении(Элемент)
	
	ОбновитьДоступностьКомандировкаДнейДоНачалаОтмена(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УходЗаРебенкомИнвалидомОграничиватьОтменуПриИзменении(Элемент)
	
	ОбновитьДоступностьУходЗаРебенкомИнвалидомДнейДоНачалаОтмены(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнениеГособязанностейОграничиватьОтменуПриИзменении(Элемент)
	
	ОбновитьДоступностьИсполнениеГособязанностейДнейДоНачалаОтмены(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ЗаписатьНастройкиНаКлиенте() Тогда
		Модифицированность = Ложь;
		ОбновитьИнтерфейс();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	
	Если ЗаписатьНастройкиНаКлиенте() Тогда
		Модифицированность = Ложь;
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройки(Команда)
	
	Если Модифицированность Тогда
		
		ЗаголовокВопроса = НСтр("ru = 'Подтверждение'");
		ТекстВопроса = НСтр("ru = 'Для обновления настроек необходимо сохранить данные.
		|Сохранить и продолжить?'");
	
		Оповещение = Новый ОписаниеОповещения("ОбновитьНастройкиПродолжение", ЭтотОбъект, ЭтаФорма);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,ЗаголовокВопроса);
		
	Иначе
		ОбновитьНастройкиСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоУмолчанию(Команда)
	
	ЗаголовокВопроса = НСтр("ru = 'Подтверждение'");
	ТекстВопроса = НСтр("ru = 'Будут установлены настройки функциональности сервиса по умолчанию.'");
	
	Оповещение = Новый ОписаниеОповещения("УстановитьНастройкиПоУмолчаниюПродолжение", ЭтотОбъект, ЭтаФорма);
	ПоказатьВопрос(Оповещение, ТекстВопроса, ИнтеграцияУправлениеПерсоналомКлиента.ОписаниеКнопокВопроса(),,,ЗаголовокВопроса);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеНастроекСервиса

&НаКлиенте
Процедура ОбновитьНастройкиПродолжение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ЗаписатьНастройкиНаКлиенте() Тогда
			Модифицированность = Ложь;
			ОбновитьНастройкиСервиса();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиСервиса()

	Если СервисЗаблокирован() Тогда
		ТекстСообщения = ИнтеграцияКабинетСотрудникаКлиент.ТекстСообщенияОБлокировкеСервиса();
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ДлительнаяОперация = ДлительнаяОперацияОбновитьНастройкиСервиса();
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = НСтр("ru = 'Обновление настроек функциональности сервиса выполнено.'");
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьНастройкиСервисаЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНастройкиСервисаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = НСтр("ru = 'При выполнении фонового задания возникла ошибка:'") + Символы.ПС + Результат.КраткоеПредставлениеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Если РезультатВыполнения.БылиОшибки Тогда
		СообщениеОбОшибке = НСтр("ru = 'Не удалсь обновить настройки функциональности. Подробнее см в журнале регистрации.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ДлительнаяОперацияОбновитьНастройкиСервиса()

	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.Вставить("НаименованиеФоновогоЗадания", НСтр("ru = 'Обновление настроек функциональности сервиса'"));
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияКабинетСотрудника.ОбновитьНастройкиСервисаФоновоеЗадание",
		Неопределено,
		ПараметрыВыполненияВФоне);
		
	Возврат ДлительнаяОперация;

КонецФункции

#КонецОбласти

#Область УстановитьНастройкиПоУмолчанию

&НаКлиенте
Процедура УстановитьНастройкиПоУмолчаниюПродолжение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьНастройкиПоУмолчаниюНаСервере();
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиПоУмолчаниюНаСервере()

	Настройки = ИнтеграцияКабинетСотрудника.НастройкиСервисаПоУмолчанию();
	ЗаполнитьЗначенияСвойств(НастройкиСервиса, Настройки);
	
	НастройкиИнтеграции.ДнейСохраненияПубликации = 5;
	
	Настройки = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.СоздатьМенеджерЗаписи(),
							Метаданные.РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника);
	ЗаполнитьЗначенияСвойств(НастройкиОтпуска, Настройки);
	
	ОтпускОформлятьЗаднимЧислом 					= Ложь;
	ОтпускДнейДоНачалаОтсутствияОтмена 				= 0;
	ОтпускЗаСвойСчетОформлятьЗаднимЧислом 			= Ложь;
	ОтпускЗаСвойСчетДнейДоНачалаОтсутствияОтмена 	= 0;
	УчебныйОтпускОформлятьЗаднимЧислом 				= Ложь;
	КомандировкаОформлятьЗаднимЧислом 				= Ложь;
	ОтгулОформлятьЗаднимЧислом 						= Ложь;
	ЛичныеДелаОформлятьЗаднимЧислом 				= Ложь;
	ИсполнениеГособязанностейЗаднимЧислом 			= Ложь;
	
	ОбновитьЭУ();

КонецПроцедуры

#КонецОбласти

#Область ЗаписьНастроек

&НаКлиенте
Функция ЗаписатьНастройкиПриЗакрытииФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗаписатьНастройкиНаКлиенте() Тогда
		Модифицированность = Ложь;
		Закрыть();
	Иначе
		ВызватьИсключение НСтр("ru = 'Не удалось сохранить настройки.'");
	КонецЕсли;

КонецФункции

&НаКлиенте
Функция ЗаписатьНастройкиНаКлиенте()
	
	СообщениеОбОшибке = Неопределено;
	ЗаписатьНастройкиНаСервере(СообщениеОбОшибке);
	Если СообщениеОбОшибке <> Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке);
	КонецЕсли;
	
	Возврат (СообщениеОбОшибке = Неопределено);

КонецФункции

&НаСервере
Процедура ЗаписатьНастройкиНаСервере(СообщениеОбОшибке)
	
	// Проверка настроек.
	Если НастройкиСервиса.ЗаявленияНаДСВ
		И Не НастройкиСервиса.СпособРасчетаДСВПроцентом
		И Не НастройкиСервиса.СпособРасчетаДСВСуммой
		И Не НастройкиСервиса.СпособРасчетаДСВПроцентомНеБолееСуммы Тогда
		НастройкиСервиса.ЗаявленияНаДСВ = Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника");
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиИнтеграцииКабинетСотрудника");
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиОтсутствийКабинетСотрудника");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		ТекущиеНастройки = РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.НастройкиЗаявокНаОтпуск();
		НовыеНастройки = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(НастройкиОтпуска, Метаданные.РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника);
		Если Не ОбщегоНазначения.КоллекцииИдентичны(ТекущиеНастройки, НовыеНастройки,,"ОбновитьНастройкиВСервисе") Тогда
			НовыеНастройки.ОбновитьНастройкиВСервисе = Истина;
			РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.СохранитьНовыеНастройки(НовыеНастройки);
		КонецЕсли;
		
		ТекущиеНастройки = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
		Если ТекущиеНастройки.ДнейСохраненияПубликации <> НастройкиИнтеграции.ДнейСохраненияПубликации Тогда
			РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.СохранитьЗначениеДнейСохраненияПубликации(НастройкиИнтеграции.ДнейСохраненияПубликации);
		КонецЕсли;
		
		ТекущиеНастройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
		НовыеНастройки = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(НастройкиСервиса, Метаданные.РегистрыСведений.НастройкиКабинетСотрудника);
		Если Не ОбщегоНазначения.КоллекцииИдентичны(ТекущиеНастройки, НовыеНастройки) Тогда
			ИнтеграцияКабинетСотрудника.СохранитьНастройкиФункциональностиСервиса(НовыеНастройки, Истина);
		КонецЕсли;
		
		ТекущиеНастройки = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.Настройки(ПричиныОтсутствий);
		НовыеНастройки = ТекущиеНастройки.СкопироватьКолонки();
		Для каждого Настройка Из ТекущиеНастройки Цикл
			Если Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отпуск Тогда
				Если Настройка.ОформлятьЗаднимЧислом <> ОтпускОформлятьЗаднимЧислом
					Или Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки <> ОтпускДнейДоНачалаОтсутствияОтмена Тогда
					НоваяСтрока = НовыеНастройки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
					НоваяСтрока.ОформлятьЗаднимЧислом = ОтпускОформлятьЗаднимЧислом;
					НоваяСтрока.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки = ОтпускДнейДоНачалаОтсутствияОтмена;
					НоваяСтрока.ТребуетсяОбновление = Истина;
				КонецЕсли;
			ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет Тогда
				Если Настройка.ОформлятьЗаднимЧислом <> ОтпускЗаСвойСчетОформлятьЗаднимЧислом
					Или Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки <> ОтпускЗаСвойСчетДнейДоНачалаОтсутствияОтмена Тогда
					НоваяСтрока = НовыеНастройки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
					НоваяСтрока.ОформлятьЗаднимЧислом = ОтпускЗаСвойСчетОформлятьЗаднимЧислом;
					НоваяСтрока.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки = ОтпускЗаСвойСчетДнейДоНачалаОтсутствияОтмена;
					НоваяСтрока.ТребуетсяОбновление = Истина;
				КонецЕсли;
			ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.УчебныйОтпуск Тогда
				Если Настройка.ОформлятьЗаднимЧислом <> УчебныйОтпускОформлятьЗаднимЧислом Тогда
					НоваяСтрока = НовыеНастройки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
					НоваяСтрока.ОформлятьЗаднимЧислом = УчебныйОтпускОформлятьЗаднимЧислом;
					НоваяСтрока.ТребуетсяОбновление = Истина;
				КонецЕсли;
			ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Командировка Тогда
				Если Настройка.ОформлятьЗаднимЧислом <> КомандировкаОформлятьЗаднимЧислом
					Или Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки <> КомандировкаДнейДоНачалаОтсутствияОтмена Тогда
					НоваяСтрока = НовыеНастройки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
					НоваяСтрока.ОформлятьЗаднимЧислом = КомандировкаОформлятьЗаднимЧислом;
					НоваяСтрока.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки = КомандировкаДнейДоНачалаОтсутствияОтмена;
					НоваяСтрока.ТребуетсяОбновление = Истина;
				КонецЕсли;
			ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отгул Тогда
				Если Настройка.ОформлятьЗаднимЧислом <> ОтгулОформлятьЗаднимЧислом Тогда
					НоваяСтрока = НовыеНастройки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
					НоваяСтрока.ОформлятьЗаднимЧислом = ОтгулОформлятьЗаднимЧислом;
					НоваяСтрока.ТребуетсяОбновление = Истина;
				КонецЕсли;
			ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела Тогда
				Если Настройка.ОформлятьЗаднимЧислом <> ЛичныеДелаОформлятьЗаднимЧислом Тогда
					НоваяСтрока = НовыеНастройки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
					НоваяСтрока.ОформлятьЗаднимЧислом = ЛичныеДелаОформлятьЗаднимЧислом;
					НоваяСтрока.ТребуетсяОбновление = Истина;
				КонецЕсли;
			ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ИсполнениеГособязанностей Тогда
				Если Настройка.ОформлятьЗаднимЧислом <> ИсполнениеГособязанностейЗаднимЧислом
					Или Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки <> ИсполнениеГособязанностейДнейДоНачалаОтмены
					Или Настройка.ИспользоватьВнутрисменные <> ИсполнениеГособязанностейИспользоватьВнутрисменные Тогда
					НоваяСтрока = НовыеНастройки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
					НоваяСтрока.ОформлятьЗаднимЧислом = ИсполнениеГособязанностейЗаднимЧислом;
					НоваяСтрока.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки = ИсполнениеГособязанностейДнейДоНачалаОтмены;
					НоваяСтрока.ИспользоватьВнутрисменные = ИсполнениеГособязанностейИспользоватьВнутрисменные;
					НоваяСтрока.ТребуетсяОбновление = Истина;
				КонецЕсли;
			ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ДниУходаЗаДетьмиИнвалидами Тогда
				Если Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки <> УходЗаРебенкомИнвалидомДнейДоНачалаОтмены Тогда
					НоваяСтрока = НовыеНастройки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Настройка);
					НоваяСтрока.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки = УходЗаРебенкомИнвалидомДнейДоНачалаОтмены;
					НоваяСтрока.ТребуетсяОбновление = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(НовыеНастройки) Тогда
			Для каждого Настройка Из НовыеНастройки Цикл
				МенеджерЗаписи = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Настройка);
				МенеджерЗаписи.Записать();
			КонецЦикла;
		КонецЕсли;

		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		СообщениеОбОшибке = НСтр("ru = 'Не удалось сохранить настройки. Подробности см. в журнале регистрации.'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИнтеграцияКабинетСотрудника.ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
		
	КонецПопытки;
	

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПрочитатьНастройки()
	
	ЗаполнитьЗначенияСвойств(НастройкиИнтеграции, РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции());
	ЗаполнитьЗначенияСвойств(НастройкиСервиса, РегистрыСведений.НастройкиКабинетСотрудника.Настройки());
	
	ЗаполнитьЗначенияСвойств(НастройкиОтпуска, РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.НастройкиЗаявокНаОтпуск());
	Если НастройкиОтпуска.ОграничиватьПредоставлениеОтпускаАвансом Тогда
		Если НастройкиОтпуска.МаксимальноеКоличествоДнейОтпускаАвансом = 0 Тогда
			ОтпускАвансомНеПредоставляется = 1;
			ОтпускАвансомПредоставляется = 0;
		Иначе
			ОтпускАвансомНеПредоставляется = 0;
			ОтпускАвансомПредоставляется = 1;
		КонецЕсли;
	Иначе
		ОтпускАвансомНеПредоставляется = 0;
		ОтпускАвансомПредоставляется = 0;
	КонецЕсли;
	
	НастройкиОтсутствийПоУмолчанию = ОбщегоНазначения.СкопироватьРекурсивно(ИнтеграцияКабинетСотрудника.НастройкиОтсутствийПоУмолчанию(), Истина);
	ДоступныеПричиныОтсутствий = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.ДоступныеПричиныОтсутствий();
	ПричиныОтсутствий = Новый ФиксированныйМассив(ДоступныеПричиныОтсутствий);
	Настройки = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.Настройки(ДоступныеПричиныОтсутствий);
	Для каждого Настройка Из Настройки Цикл
		Если Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отпуск Тогда
			ОтпускОформлятьЗаднимЧислом 		= Настройка.ОформлятьЗаднимЧислом;
			ОтпускДнейДоНачалаОтсутствияОтмена 	= Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
		ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет Тогда
			ОтпускЗаСвойСчетОформлятьЗаднимЧислом 			= Настройка.ОформлятьЗаднимЧислом;
			ОтпускЗаСвойСчетДнейДоНачалаОтсутствияОтмена 	= Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
		ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.УчебныйОтпуск Тогда
			УчебныйОтпускОформлятьЗаднимЧислом = Настройка.ОформлятьЗаднимЧислом;
		ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Командировка Тогда
			КомандировкаОформлятьЗаднимЧислом 			= Настройка.ОформлятьЗаднимЧислом;
			КомандировкаДнейДоНачалаОтсутствияОтмена 	= Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
		ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отгул Тогда
			ОтгулОформлятьЗаднимЧислом = Настройка.ОформлятьЗаднимЧислом;
		ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела Тогда
			ЛичныеДелаОформлятьЗаднимЧислом = Настройка.ОформлятьЗаднимЧислом;
		ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ИсполнениеГособязанностей Тогда
			ИсполнениеГособязанностейДнейДоНачалаОтмены = Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
			Если ИсполнениеГособязанностейДнейДоНачалаОтмены И ИсполнениеГособязанностейДнейДоНачалаОтмены = 0 Тогда
				ИсполнениеГособязанностейДнейДоНачалаОтмены = НастройкиОтсутствийПоУмолчанию[Настройка.ПричинаОтсутствия].КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
			КонецЕсли;
			ИсполнениеГособязанностейЗаднимЧислом 				= Настройка.ОформлятьЗаднимЧислом;
			ИсполнениеГособязанностейИспользоватьВнутрисменные 	= Настройка.ИспользоватьВнутрисменные;
		ИначеЕсли Настройка.ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ДниУходаЗаДетьмиИнвалидами Тогда
			УходЗаРебенкомИнвалидомДнейДоНачалаОтмены = Настройка.КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
			Если УходЗаРебенкомИнвалидомДнейДоНачалаОтмены И УходЗаРебенкомИнвалидомДнейДоНачалаОтмены = 0 Тогда
				УходЗаРебенкомИнвалидомДнейДоНачалаОтмены = НастройкиОтсутствийПоУмолчанию[Настройка.ПричинаОтсутствия].КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	КомандировкаОграничиватьОтмену = (КомандировкаДнейДоНачалаОтсутствияОтмена > 0);
	УходЗаРебенкомИнвалидомОграничиватьОтмену = (УходЗаРебенкомИнвалидомДнейДоНачалаОтмены > 0);
	ИсполнениеГособязанностейОграничиватьОтмену = (ИсполнениеГособязанностейДнейДоНачалаОтмены > 0);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьНастроек()
	
	ИспользуетсяВерсияDTO_2_4 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("2.4");
	УстановитьВидимостьЭУ("ГруппаДнейУдаленияКабинета", ИспользуетсяВерсияDTO_2_4);
	
	ПрочитатьДоступныеНастройки();
	ДоступныеНастройкиОтсутствий = ИнтеграцияКабинетСотрудника.ДоступныеНастройкиОтсутствий();
	ДоступенРасширенныйФункционал = ИнтеграцияУправлениеПерсоналом.ДоступенРасширенныйФункционал();
	ДоступенРасчетЗарплатыДляНебольшихОрганизаций = ИнтеграцияУправлениеПерсоналом.ДоступенРасчетЗарплатыДляНебольшихОрганизаций();
	ИспользуетсяВерсияDTO_2_0 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("2.0");
	ИспользуетсяВерсияDTO_3_1 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("3.1");
	ИспользуетсяВерсияDTO_3_2 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("3.2");
	
	// Разделы.
	ПоказыватьГруппуРазделы =  ДоступныеНастройки.РазделДокументы
		Или ДоступныеНастройки.РазделОтпуск
		Или ДоступныеНастройки.РазделКомпания
		Или ДоступныеНастройки.РазделЗарплата
		Или ДоступныеНастройки.РазделОтсутствия
		Или ДоступныеНастройки.РазделСправки
		Или ДоступныеНастройки.РазделКомандировки;
	
	УстановитьВидимостьЭУ("ГруппаРазделы", ПоказыватьГруппуРазделы);
	УстановитьВидимостьЭУ("НастройкиСервисаРазделДокументы", 	ДоступныеНастройки.РазделДокументы);
	УстановитьВидимостьЭУ("НастройкиСервисаРазделОтпуск", 		ДоступныеНастройки.РазделОтпуск);
	УстановитьВидимостьЭУ("НастройкиСервисаРазделКомпания", 	ДоступныеНастройки.РазделКомпания);
	УстановитьВидимостьЭУ("НастройкиСервисаРазделЗарплата", 	ДоступныеНастройки.РазделЗарплата);
	УстановитьВидимостьЭУ("НастройкиСервисаРазделОтсутствия", 	ДоступныеНастройки.РазделОтсутствия);
	УстановитьВидимостьЭУ("НастройкиСервисаРазделСправки", 		ДоступныеНастройки.РазделСправки);
	УстановитьВидимостьЭУ("НастройкиСервисаРазделКомандировки", ДоступныеНастройки.РазделКомандировки);
	
	// Справки.
	УстановитьВидимостьЭУ("НастройкиСервисаСправки2НДФЛ", 			ДоступныеНастройки.Справки2НДФЛ);
	УстановитьВидимостьЭУ("НастройкиСервисаСправкиСМестаРаботы", 	ДоступныеНастройки.СправкиСМестаРаботы);
	УстановитьВидимостьЭУ("НастройкиСервисаСправкиОстаткиОтпусков", ДоступныеНастройки.СправкиОстаткиОтпусков);
	УстановитьВидимостьЭУ("НастройкиСервисаДоступноПолучениеСканаОригинала", 			ДоступныеНастройки.ДоступноПолучениеСканаОригинала);
	УстановитьВидимостьЭУ("НастройкиСервисаДоступноПолучениеДокументаВБумажномВиде", 	ДоступныеНастройки.ДоступноПолучениеДокументаВБумажномВиде);
	УстановитьВидимостьЭУ("НастройкиСервисаДоступноПолучениеДокументаСЭП", 				ДоступныеНастройки.ДоступноПолучениеДокументаСЭП);
	УстановитьВидимостьЭУ("НастройкиСервисаДоступноПолучениеКопииНаЭлектроннуюПочту", 	ДоступныеНастройки.ДоступноПолучениеКопииНаЭлектроннуюПочту);
	
	// Отпуска.
	ПоказыватьГруппуОтпуска = ДоступенРасширенныйФункционал Или ДоступенРасчетЗарплатыДляНебольшихОрганизаций;
	УстановитьВидимостьЭУ("ГруппаОтпуска", ПоказыватьГруппуОтпуска);
	Если ПоказыватьГруппуОтпуска Тогда
		
		// Расширенные настройки заявлений на отпуск.
		УстановитьВидимостьЭУ("ГруппаНастройкиОтпускаРасширенные", ДоступенРасширенныйФункционал);
		
		УстановитьВидимостьЭУ("ГруппаДоступныеЗаявленияНаОтпуск", ИспользуетсяВерсияDTO_2_0);
		
		УстановитьВидимостьЭУ("НастройкиСервисаПоказыватьГрафикОтпусков", 	ДоступныеНастройки.ПоказыватьГрафикОтпусков);
		УстановитьВидимостьЭУ("ДекорацияОтступОтпуска", 					ДоступныеНастройки.ПоказыватьГрафикОтпусков);
		УстановитьВидимостьЭУ("НастройкиСервисаПоказыватьОстаткиОтпусков", 	ДоступныеНастройки.ПоказыватьОстаткиОтпусков);
		УстановитьВидимостьЭУ("НастройкиСервисаИзменятьГрафикиОтпусков", 	ДоступныеНастройки.ИзменятьГрафикиОтпусков);
		
		УстановитьВидимостьЭУ("НастройкиСервисаЗаявлениеНаОтпуск", ДоступныеНастройки.ЗаявлениеНаОтпуск);
		УстановитьВидимостьЭУ("ГруппаДоплатыКОтпускуГлавная", ДоступныеНастройки.МатериальнаяПомощьКОтпуску Или ДоступныеНастройки.ЕдиновременнаяВыплатаКОтпуску);
		УстановитьВидимостьЭУ("НастройкиСервисаМатериальнаяПомощьКОтпуску", 	ДоступныеНастройки.МатериальнаяПомощьКОтпуску);
		УстановитьВидимостьЭУ("НастройкиСервисаЕдиновременнаяВыплатаКОтпуску", 	ДоступныеНастройки.ЕдиновременнаяВыплатаКОтпуску);
		
		УстановитьВидимостьЭУ("НастройкиСервисаКомпенсацияОтпуска", 		ДоступныеНастройки.КомпенсацияОтпуска);
		УстановитьВидимостьЭУ("НастройкиСервисаЗаявлениеУчебныйОтпуск", 	ДоступныеНастройки.ЗаявлениеУчебныйОтпуск);
		УстановитьВидимостьЭУ("НастройкиСервисаЗаявлениеОтпускБезОплаты", 	ДоступныеНастройки.ЗаявлениеОтпускБезОплаты);
		
		// Настройки отсутствия задним числом.
		УстановитьВидимостьЭУ("ОтпускОформлятьЗаднимЧислом", ИспользуетсяВерсияDTO_2_0 И ДоступныеНастройки.ЗаявлениеНаОтпуск);
		УстановитьВидимостьЭУ("ГруппаОтпускаЗаднимЧислом", ИспользуетсяВерсияDTO_2_0 И (ДоступныеНастройки.ЗаявлениеУчебныйОтпуск Или ДоступныеНастройки.ЗаявлениеОтпускБезОплаты));
		УстановитьВидимостьЭУ("УчебныйОтпускОформлятьЗаднимЧислом", 	ДоступныеНастройки.ЗаявлениеУчебныйОтпуск);
		УстановитьВидимостьЭУ("ОтпускЗаСвойСчетОформлятьЗаднимЧислом", 	ДоступныеНастройки.ЗаявлениеОтпускБезОплаты);
		
		// Переносы отпусков.
		УстановитьВидимостьЭУ("ГруппаНастройкаИсправленияЗаявокОтпуск", ДоступныеНастройки.ИспользоватьЗаявленияНаПереносОтпуска Или  ДоступныеНастройки.ИспользоватьЗаявленияНаОтменуОтпуска);
		УстановитьВидимостьЭУ("НастройкиСервисаИспользоватьЗаявленияНаПереносОтпуска", 	ДоступныеНастройки.ИспользоватьЗаявленияНаПереносОтпуска);
		УстановитьВидимостьЭУ("НастройкиСервисаИспользоватьЗаявленияНаОтменуОтпуска", 	ДоступныеНастройки.ИспользоватьЗаявленияНаОтменуОтпуска);
	
	КонецЕсли;
	
	// Заявления.
	УстановитьВидимостьЭУ("НастройкиСервисаЗаявленияНаВычетыНДФЛ", 					ДоступныеНастройки.ЗаявленияНаВычетыНДФЛ);
	УстановитьВидимостьЭУ("НастройкиСервисаИспользоватьПособияПриРожденииРебенка", 	ДоступныеНастройки.ИспользоватьПособияПриРожденииРебенка);
	УстановитьВидимостьЭУ("НастройкиСервисаИспользоватьПособияПоУходуЗаРебенком", 	ДоступныеНастройки.ИспользоватьПособияПоУходуЗаРебенком);
	УстановитьВидимостьЭУ("НастройкиСервисаЗапросНаИзменениеЛичнойИнформации", 		ДоступныеНастройки.ЗапросНаИзменениеЛичнойИнформации);
	УстановитьВидимостьЭУ("ГруппаИзменениеФото", 	ДоступныеНастройки.ДоступноИзменениеФото);
	УстановитьВидимостьЭУ("ГруппаЗаявленияДСВ", 	ДоступныеНастройки.ЗаявленияНаДСВ);
	
	// Отсутствия.
	ПоказыватьГруппуОтсутствия = ИспользуетсяВерсияDTO_2_0;
	УстановитьВидимостьЭУ("ГруппаОтсутствия", ПоказыватьГруппуОтсутствия);
	Если ПоказыватьГруппуОтсутствия Тогда
		
		УстановитьВидимостьЭУ("НастройкиСервисаБолезнь", 					ДоступныеНастройки.Болезнь);
		УстановитьВидимостьЭУ("НастройкиСервисаОтпускПоБеременностиИРодам", ДоступныеНастройки.ОтпускПоБеременностиИРодам);
		УстановитьВидимостьЭУ("НастройкиСервисаУходЗаРебенкомИнвалидом", 	ДоступныеНастройки.УходЗаРебенкомИнвалидом);
		УстановитьВидимостьЭУ("НастройкиСервисаОтпускПоУходуЗаРебенком", 	ДоступныеНастройки.ОтпускПоУходуЗаРебенком);
		УстановитьВидимостьЭУ("НастройкиСервисаКомандировка", 				ДоступныеНастройки.Командировка);
		УстановитьВидимостьЭУ("НастройкиСервисаОтгул", 						ДоступныеНастройки.Отгул);
		УстановитьВидимостьЭУ("НастройкиСервисаОпоздание", 					ДоступныеНастройки.Опоздание);
		УстановитьВидимостьЭУ("НастройкиСервисаОтсутствиеПоЛичнымОбстоятельствам", 	ДоступныеНастройки.ОтсутствиеПоЛичнымОбстоятельствам);
		УстановитьВидимостьЭУ("НастройкиСервисаВозвратИзОтпускаПоУходуЗаРебенком", 	ДоступныеНастройки.ВозвратИзОтпускаПоУходуЗаРебенком);
		
		// // Настройки отсутствия задним числом.
		ПоказыватьГруппуОтсутствияЗаднимЧислом = ДоступныеНастройки.Отгул Или ДоступныеНастройки.ОтсутствиеПоЛичнымОбстоятельствам
			Или ДоступныеНастройки.Командировка Или ДоступныеНастройки.ИсполнениеГособязанностей;
		УстановитьВидимостьЭУ("ГруппаОтсутствияЗаднимЧислом", 			ПоказыватьГруппуОтсутствияЗаднимЧислом);
		УстановитьВидимостьЭУ("ОтгулОформлятьЗаднимЧислом", 			ДоступныеНастройки.Отгул);
		УстановитьВидимостьЭУ("ЛичныеДелаОформлятьЗаднимЧислом", 		ДоступныеНастройки.ОтсутствиеПоЛичнымОбстоятельствам);
		УстановитьВидимостьЭУ("КомандировкаОформлятьЗаднимЧислом", 		ДоступныеНастройки.Командировка);
		УстановитьВидимостьЭУ("ИсполнениеГособязанностейЗаднимЧислом", 	ДоступныеНастройки.ИсполнениеГособязанностей);
		
		УстановитьВидимостьЭУ("ГруппаГособязанности", ДоступныеНастройки.ИсполнениеГособязанностей);
		Если ДоступныеНастройки.ИсполнениеГособязанностей Тогда
			ИспользоватьВнутрисменные = Ложь;
			НастройкиОтсутствийИсполнениеГособязанностей = ДоступныеНастройкиОтсутствий[Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ИсполнениеГособязанностей];
			Если НастройкиОтсутствийИсполнениеГособязанностей <> Неопределено Тогда
				ИспользоватьВнутрисменные = НастройкиОтсутствийИсполнениеГособязанностей.ИспользоватьВнутрисменные;
			КонецЕсли;
			УстановитьВидимостьЭУ("ИсполнениеГособязанностейИспользоватьВнутрисменные", ИспользоватьВнутрисменные);
		КонецЕсли;
			
		УстановитьВидимостьЭУ("ГруппаДниУходаОтмена", ДоступныеНастройки.УходЗаРебенкомИнвалидом И ИспользуетсяВерсияDTO_3_2);
		
		ДоступныРасширенныеНастройкиКомандировки = ДоступныеНастройки.Командировка И ИспользуетсяВерсияDTO_3_2;
		УстановитьВидимостьЭУ("ГруппаКомандировкиДополнительная", ДоступныРасширенныеНастройкиКомандировки);
		УстановитьДоступностьИспользоватьБронированиеКомандировок(ЭтаФорма);
		
	КонецЕсли;
	
	// Охрана труда.
	ПоказыватьГруппуОхранаТруда = ДоступныеНастройки.ПоказыватьОхрануТруда;
	УстановитьВидимостьЭУ("ГруппаОхранаТруда", ПоказыватьГруппуОхранаТруда);
	Если ПоказыватьГруппуОхранаТруда Тогда
		
		УстановитьВидимостьЭУ("НастройкиСервисаПоказыватьРабочиеМестаОхраныТруда", 	ДоступныеНастройки.ПоказыватьРабочиеМестаОхраныТруда);
		УстановитьВидимостьЭУ("НастройкиСервисаПоказыватьМедицинскиеОсмотры", 		ДоступныеНастройки.ПоказыватьМедицинскиеОсмотры);
		УстановитьВидимостьЭУ("НастройкиСервисаПоказыватьПсихиатрическиеОсвидетельствования", ДоступныеНастройки.ПоказыватьПсихиатрическиеОсвидетельствования);
		УстановитьВидимостьЭУ("НастройкиСервисаПоказыватьСредстваИЗ", ДоступныеНастройки.ПоказыватьСредстваИЗ);
		
		Если Не ИспользуетсяВерсияDTO_3_1 Тогда
			ЗаголовокПоказыватьОхрануТруда = НСтр("ru = 'Показывать сведения о рабочих местах в кабинете сотрудника'");
			УстановитьВидимостьЭУ("ГруппаРазделыОхраныТруда", Ложь);
		Иначе
			ЗаголовокПоказыватьОхрануТруда = НСтр("ru = 'Показывать данные охраны труда в кабинете сотрудника'");
			УстановитьДоступностьПодчиненныхНастроекОхраныТруда(ЭтаФорма);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НастройкиСервисаПоказыватьОхрануТруда", "Заголовок", ЗаголовокПоказыватьОхрануТруда);
		
	КонецЕсли;
	
	// Прочие.
	УстановитьВидимостьЭУ("ГруппаУровниДоступаКИ", ДоступныеНастройки.УровеньДоступаКИ);
	УстановитьВидимостьЭУ("НастройкиСервисаДоступенОтказОтПодписания", ДоступныеНастройки.ДоступенОтказОтПодписания);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДоступныеНастройки()

	ДоступныеНастройки = Новый ФиксированнаяСтруктура(ИнтеграцияКабинетСотрудника.ДоступныеНастройкиСервиса());

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьИспользоватьБронированиеКомандировок(Форма)
	
	Если Форма.ДоступныРасширенныеНастройкиКомандировки Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"НастройкиСервисаИспользоватьБронированиеБилетовПроживания", "Доступность", Форма.ДоступныеНастройки.ИспользоватьБронированиеБилетовПроживания);
		ТекстПодсказки = "";
		Если Не Форма.ДоступныеНастройки.ИспользоватьБронированиеБилетовПроживания Тогда
			ТекстПодсказки = НСтр("ru = 'Необходимо включить использование интеграции с сервисами бронирования в настройках кадрового учета'");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"НастройкиСервисаИспользоватьБронированиеБилетовПроживания", "Подсказка", ТекстПодсказки);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьЭУ()

	УстановитьДоступностьДоплатКОтпуску(ЭтаФорма);
	ОбновитьЭУКоличествоДнейДоНачалаОтпуска(ЭтаФорма);
	ОбновитьЭУОтпускОформлятьЗаднимЧислом(ЭтаФорма);
	УстановитьДоступностьПодчиненныхНастроекОхраныТруда(ЭтаФорма);
	ОбновитьДоступностьЭУДнейДоНачалаОтпуска(ЭтаФорма, Ложь);
	ОбновитьЭУМаксимальноеКоличествоДнейОтпускаАвансом(ЭтаФорма);
	УстановитьДоступностьДоступноИзменениеФото(ЭтаФорма);
	ОбновитьЭУСпособРасчетаДСВ(ЭтаФорма);
	ОбновитьДоступностьЭУДниУходаОтмена(ЭтаФорма, Ложь);
	ОбновитьДоступностьЭУИсполнениеГособязанностей(ЭтаФорма, Ложь);
	ОбновитьДоступностьЭУКомандировки(ЭтаФорма, Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭУКоличествоДнейДоНачалаОтпуска(Форма)
	
	Элементы = Форма.Элементы;
	КонтролироватьДатуНачалаОтпуска = Форма.НастройкиОтпуска.КонтролироватьДатуНачалаОтпускаПриПодачеЗаявления;
	Элементы.КоличествоДнейДоНачалаОтпуска.Доступность = КонтролироватьДатуНачалаОтпуска;
	Элементы.КоличествоДнейДоНачалаОтпуска.АвтоОтметкаНезаполненного = КонтролироватьДатуНачалаОтпуска;
	Элементы.КоличествоДнейДоНачалаОтпуска.ОтметкаНезаполненного = Не КонтролироватьДатуНачалаОтпуска Или КонтролироватьДатуНачалаОтпуска И Не ЗначениеЗаполнено(Форма.НастройкиОтпуска.КоличествоДнейДоНачалаОтпуска);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭУМаксимальноеКоличествоДнейОтпускаАвансом(Форма)
	
	Элементы = Форма.Элементы;
	Если Форма.НастройкиОтпуска.ОграничиватьПредоставлениеОтпускаАвансом Тогда
		
		Элементы.ОтпускАвансомНеПредоставляется.Доступность = Истина;
		Элементы.ОтпускАвансомПредоставляется.Доступность 	= Истина;
		Элементы.МаксимальноеКоличествоДнейОтпускаАвансом.Доступность = (Форма.ОтпускАвансомПредоставляется = 1);
		
		Элементы.МаксимальноеКоличествоДнейОтпускаАвансом.АвтоОтметкаНезаполненного = (Форма.ОтпускАвансомПредоставляется = 1);
		Элементы.МаксимальноеКоличествоДнейОтпускаАвансом.ОтметкаНезаполненного = (Форма.ОтпускАвансомПредоставляется = 1)
				И Форма.НастройкиОтпуска.МаксимальноеКоличествоДнейОтпускаАвансом = 0;
		
	Иначе
		
		Элементы.ОтпускАвансомНеПредоставляется.Доступность = Ложь;
		Элементы.ОтпускАвансомПредоставляется.Доступность 	= Ложь;
		Элементы.МаксимальноеКоличествоДнейОтпускаАвансом.Доступность = Ложь;
		Элементы.МаксимальноеКоличествоДнейОтпускаАвансом.АвтоОтметкаНезаполненного = Ложь;
		Элементы.МаксимальноеКоличествоДнейОтпускаАвансом.ОтметкаНезаполненного = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭУ(ИмяЭлементоа, ВидимостьЭлемента)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлементоа, "Видимость", ВидимостьЭлемента);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьДоплатКОтпуску(Форма)

	Форма.Элементы.НастройкиСервисаМатериальнаяПомощьКОтпуску.Доступность 		= Форма.НастройкиСервиса.ЗаявлениеНаОтпуск;
	Форма.Элементы.НастройкиСервисаЕдиновременнаяВыплатаКОтпуску.Доступность 	= Форма.НастройкиСервиса.ЗаявлениеНаОтпуск;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭУОтпускОформлятьЗаднимЧислом(Форма)
	
	Форма.Элементы.КонтролироватьДатуНачалаОтпускаПриПодачеЗаявления.Доступность = Не Форма.ОтпускОформлятьЗаднимЧислом;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПодчиненныхНастроекОхраныТруда(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
		"НастройкиСервисаПоказыватьРабочиеМестаОхраныТруда", "Доступность", Форма.НастройкиСервиса.ПоказыватьОхрануТруда);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
		"НастройкиСервисаПоказыватьМедицинскиеОсмотры", "Доступность", Форма.НастройкиСервиса.ПоказыватьОхрануТруда);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
		"НастройкиСервисаПоказыватьПсихиатрическиеОсвидетельствования", "Доступность", Форма.НастройкиСервиса.ПоказыватьОхрануТруда);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
		"НастройкиСервисаПоказыватьСредстваИЗ", "Доступность", Форма.НастройкиСервиса.ПоказыватьОхрануТруда);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭУДнейДоНачалаОтпуска(Форма, УстановитьЗначение)

	НастройкаДоступна = Форма.НастройкиСервиса.ИспользоватьЗаявленияНаПереносОтпуска
		Или Форма.НастройкиСервиса.ИспользоватьЗаявленияНаОтменуОтпуска;
		
	Форма.Элементы.ГруппаНастройкиОтпускаДни.Доступность = НастройкаДоступна;
	
	Если УстановитьЗначение Тогда
		Если НастройкаДоступна Тогда
			Если Форма.ОтпускДнейДоНачалаОтсутствияОтмена = 0 Тогда
				ПричинаОтсутствия = ПредопределенноеЗначение("Перечисление.ПричиныОтсутствийЗаявокКабинетСотрудника.Отпуск");
				Форма.ОтпускДнейДоНачалаОтсутствияОтмена = Форма.НастройкиОтсутствийПоУмолчанию[ПричинаОтсутствия].КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
			КонецЕсли;
			Если Форма.ОтпускЗаСвойСчетДнейДоНачалаОтсутствияОтмена = 0 Тогда
				ПричинаОтсутствия = ПредопределенноеЗначение("Перечисление.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет");
				Форма.ОтпускЗаСвойСчетДнейДоНачалаОтсутствияОтмена = Форма.НастройкиОтсутствийПоУмолчанию[ПричинаОтсутствия].КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
			КонецЕсли;
		Иначе
			Форма.ОтпускДнейДоНачалаОтсутствияОтмена = 0;
			Форма.ОтпускЗаСвойСчетДнейДоНачалаОтсутствияОтмена = 0;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьДоступноИзменениеФото(Форма)

	Форма.Элементы.НастройкиСервисаДоступноИзменениеФото.Доступность = Форма.НастройкиСервиса.ЗапросНаИзменениеЛичнойИнформации;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭУСпособРасчетаДСВ(Форма)

	Элементы = Форма.Элементы;
	Элементы.НастройкиСервисаСпособРасчетаДСВПроцентом.Доступность = Форма.НастройкиСервиса.ЗаявленияНаДСВ;
	Элементы.НастройкиСервисаСпособРасчетаДСВПроцентомНеБолееСуммы.Доступность = Форма.НастройкиСервиса.ЗаявленияНаДСВ;
	Элементы.НастройкиСервисаСпособРасчетаДСВСуммой.Доступность = Форма.НастройкиСервиса.ЗаявленияНаДСВ;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СервисЗаблокирован()

	Возврат ИнтеграцияКабинетСотрудника.ПриложениеЗаблокировано();

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭУДниУходаОтмена(Форма, УстановитьЗначение)

	НастройкаДоступна = Форма.НастройкиСервиса.УходЗаРебенкомИнвалидом;
	Форма.Элементы.ГруппаДниУходаОтмена.Доступность = НастройкаДоступна;
	
	Если УстановитьЗначение Тогда
		Если НастройкаДоступна Тогда
			Форма.УходЗаРебенкомИнвалидомОграничиватьОтмену = (Форма.УходЗаРебенкомИнвалидомДнейДоНачалаОтмены > 0);
		Иначе
			Форма.УходЗаРебенкомИнвалидомДнейДоНачалаОтмены = 0;
			Форма.УходЗаРебенкомИнвалидомОграничиватьОтмену = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьДоступностьУходЗаРебенкомИнвалидомДнейДоНачалаОтмены(Форма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьУходЗаРебенкомИнвалидомДнейДоНачалаОтмены(Форма)

	Форма.Элементы.УходЗаРебенкомИнвалидомДнейДоНачалаОтмены.Доступность = Форма.УходЗаРебенкомИнвалидомОграничиватьОтмену;
	Если Форма.УходЗаРебенкомИнвалидомОграничиватьОтмену Тогда
		Если Форма.УходЗаРебенкомИнвалидомДнейДоНачалаОтмены = 0 Тогда
			ПричинаОтсутствия = ПредопределенноеЗначение("Перечисление.ПричиныОтсутствийЗаявокКабинетСотрудника.ДниУходаЗаДетьмиИнвалидами");
			Форма.УходЗаРебенкомИнвалидомДнейДоНачалаОтмены = Форма.НастройкиОтсутствийПоУмолчанию[ПричинаОтсутствия].КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
		КонецЕсли;
	Иначе
		Форма.УходЗаРебенкомИнвалидомДнейДоНачалаОтмены = 0;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭУИсполнениеГособязанностей(Форма, УстановитьЗначение)

	НастройкаДоступна = Форма.НастройкиСервиса.ИсполнениеГособязанностей;
	Форма.Элементы.ГруппаГособязанностиОтмена.Доступность = НастройкаДоступна;
	Форма.Элементы.ИсполнениеГособязанностейИспользоватьВнутрисменные.Доступность = НастройкаДоступна;
	
	Если УстановитьЗначение Тогда
		Если НастройкаДоступна Тогда
			Если Форма.ИсполнениеГособязанностейДнейДоНачалаОтмены = 0 Тогда
				ПричинаОтсутствия = ПредопределенноеЗначение("Перечисление.ПричиныОтсутствийЗаявокКабинетСотрудника.ИсполнениеГособязанностей");
				Форма.ИсполнениеГособязанностейДнейДоНачалаОтмены = Форма.НастройкиОтсутствийПоУмолчанию[ПричинаОтсутствия].КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
			КонецЕсли;
		Иначе
			Форма.ИсполнениеГособязанностейДнейДоНачалаОтмены = 0;
			Форма.ИсполнениеГособязанностейОграничиватьОтмену = Ложь;
			Форма.ИсполнениеГособязанностейИспользоватьВнутрисменные = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьДоступностьИсполнениеГособязанностейДнейДоНачалаОтмены(Форма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьИсполнениеГособязанностейДнейДоНачалаОтмены(Форма)

	Форма.Элементы.ИсполнениеГособязанностейДнейДоНачалаОтмены.Доступность = Форма.ИсполнениеГособязанностейОграничиватьОтмену;
	Если Форма.ИсполнениеГособязанностейОграничиватьОтмену Тогда
		Если Форма.ИсполнениеГособязанностейДнейДоНачалаОтмены = 0 Тогда
			ПричинаОтсутствия = ПредопределенноеЗначение("Перечисление.ПричиныОтсутствийЗаявокКабинетСотрудника.ИсполнениеГособязанностей");
			Форма.ИсполнениеГособязанностейДнейДоНачалаОтмены = Форма.НастройкиОтсутствийПоУмолчанию[ПричинаОтсутствия].КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
		КонецЕсли;
	Иначе
		Форма.ИсполнениеГособязанностейДнейДоНачалаОтмены = 0;
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭУКомандировки(Форма, УстановитьЗначение)

	НастройкаДоступна = Форма.НастройкиСервиса.Командировка;
	Форма.Элементы.ГруппаКомандировкиДополнительная.Доступность = НастройкаДоступна;
	
	Если УстановитьЗначение Тогда
		Если НастройкаДоступна Тогда
			Форма.КомандировкаОграничиватьОтмену = (Форма.КомандировкаДнейДоНачалаОтсутствияОтмена > 0);
		Иначе
			Форма.НастройкиСервиса.ОтчетыОбИтогахКомандировки 					= Ложь;
			Форма.НастройкиСервиса.ОтчетыОЗатратахВКомандировках 				= Ложь;
			Форма.НастройкиСервиса.ИспользоватьБронированиеБилетовПроживания 	= Ложь;
			Форма.КомандировкаДнейДоНачалаОтсутствияОтмена 						= 0;
			Форма.КомандировкаОграничиватьОтмену 								= Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьДоступностьКомандировкаДнейДоНачалаОтмена(Форма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьКомандировкаДнейДоНачалаОтмена(Форма)

	Форма.Элементы.КомандировкаДнейДоНачалаОтсутствияОтмена.Доступность = Форма.КомандировкаОграничиватьОтмену;
	Если Форма.КомандировкаОграничиватьОтмену Тогда
		Если Форма.КомандировкаДнейДоНачалаОтсутствияОтмена = 0 Тогда
			ПричинаОтсутствия = ПредопределенноеЗначение("Перечисление.ПричиныОтсутствийЗаявокКабинетСотрудника.Командировка");
			Форма.КомандировкаДнейДоНачалаОтсутствияОтмена = Форма.НастройкиОтсутствийПоУмолчанию[ПричинаОтсутствия].КоличествоДнейДоНачалаОтсутствияДляИзмененияЗаявки;
		КонецЕсли;
	Иначе
		Форма.КомандировкаДнейДоНачалаОтсутствияОтмена = 0;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти