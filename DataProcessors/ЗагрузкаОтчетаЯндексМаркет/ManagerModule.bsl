#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ДобавитьКолонкуИД(ТаблицаТоваров) Экспорт
	
	КолонкиТаблицыТоваров = ТаблицаТоваров.Колонки;
	
	Если КолонкиТаблицыТоваров.Найти("ИД") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("ИД");
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("ИсторияИдентификаторов") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("ИсторияИдентификаторов", Новый ОписаниеТипов("Массив"));
	КонецЕсли;
		
	Для Каждого СтрокаТоваров Из ТаблицаТоваров Цикл
		
		НатуральныйИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(ВРег(СтрЗаменить(СтрокаТоваров.НаимТов, " ", ""))
				+ "#" + ВРег(СтрЗаменить(СтрокаТоваров.КодМагазина, " ", "") + "#"));
				
		Если ЗначениеЗаполнено(СтрокаТоваров.КодМагазина) Тогда
			СтрокаТоваров.ИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(СтрокаТоваров.КодМагазина);
			СтрокаТоваров.ИсторияИдентификаторов.Добавить(НатуральныйИД);
		Иначе
			СтрокаТоваров.ИД = НатуральныйИД;						
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СписокНеСопоставленнойНоменклатурыНаСервере(МассивДокументов, Контрагент) Экспорт
	
	МассивНеСопоставленнойНоменклатуры = Новый Массив;
	
	ИменаКолонок = "НаимТов, Артикул, СтавкаНДС, КодМагазина";
	ТаблицаТоваров = Неопределено;
	
	Если ТипЗнч(МассивДокументов) = Тип("Массив") Тогда
		Для Каждого СтраницаОтчета Из МассивДокументов Цикл
			ДанныеДокумента = СтраницаОтчета.ДанныеДокумента;
			
			Если ТаблицаТоваров = Неопределено Тогда
				ТаблицаТоваров = ДанныеДокумента.ТаблицаТоваров.Скопировать(,ИменаКолонок);
			Иначе
				Для Каждого СтрокаТаблицы Из ДанныеДокумента.ТаблицаТоваров Цикл
					НоваяСтрока = ТаблицаТоваров.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				КонецЦикла;
			КонецЕсли;
		
		КонецЦикла;
		Если ТаблицаТоваров = Неопределено Тогда
			Возврат МассивНеСопоставленнойНоменклатуры;
		КонецЕсли;
		ТаблицаТоваров.Свернуть(ИменаКолонок);
	КонецЕсли;
	
	Владелец = СопоставлениеНоменклатурыКонтрагентовСлужебный.ВладелецНоменклатурыКонтрагента(Контрагент);
		
	МассивПоискаНоменклатуры  = Новый Массив;
	СопоставлениеНоменклатуры = Новый Соответствие;
	НаборНоменклатурыКонтрагентов = Новый Массив;
	
	КолонкиТаблицыТоваров = ТаблицаТоваров.Колонки;
	
	КолонкаНаимТов = КолонкиТаблицыТоваров.Найти("НаимТов");
	КолонкаНаименование = КолонкиТаблицыТоваров.Найти("Наименование");
	Если КолонкаНаимТов <> Неопределено
		И КолонкаНаименование = Неопределено Тогда
		КолонкаНаимТов.Имя = "Наименование";
	КонецЕсли;
	
	Если КолонкиТаблицыТоваров.Найти("КодМагазина") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("КодМагазина", ОбщегоНазначения.ОписаниеТипаСтрока(300)); 	
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("ИсторияИдентификаторов") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("ИсторияИдентификаторов", Новый ОписаниеТипов("Массив"));
	КонецЕсли;
	Если КолонкиТаблицыТоваров.Найти("ИД") = Неопределено Тогда
		КолонкиТаблицыТоваров.Добавить("ИД", ОбщегоНазначения.ОписаниеТипаСтрока(300));
	КонецЕсли;
	
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(Владелец,);
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, СтрокаТовара);
		
		НатуральныйИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(ВРег(СтрЗаменить(СтрокаТовара.Наименование, " ", ""))
				+ "#" + ВРег(СтрЗаменить(СтрокаТовара.КодМагазина, " ", "") + "#"));
				
		Если ЗначениеЗаполнено(СтрокаТовара.КодМагазина) Тогда
			НоменклатураКонтрагента.Идентификатор = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(СтрокаТовара.КодМагазина);
			НоменклатураКонтрагента.ИсторияИдентификаторов.Добавить(НатуральныйИД);
			СтрокаТовара.ИсторияИдентификаторов.Добавить(НатуральныйИД);
		Иначе
			НоменклатураКонтрагента.Идентификатор = НатуральныйИД;						
		КонецЕсли;
		
		СтрокаТовара.ИД = НоменклатураКонтрагента.Идентификатор;
		
		НоменклатураКонтрагента.ИдентификаторНоменклатуры = НоменклатураКонтрагента.Идентификатор;
		
		НаборНоменклатурыКонтрагентов.Добавить(НоменклатураКонтрагента);
		МассивПоискаНоменклатуры.Добавить(НоменклатураКонтрагента);
		
	КонецЦикла;
	
	МассивНайденнойНоменклатуры   = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Новый Структура("НоменклатураКонтрагента", МассивПоискаНоменклатуры), Ложь);
	
	Для Каждого СтрокаНоменклатуры Из МассивНайденнойНоменклатуры Цикл
		Идентификатор = СтрокаНоменклатуры.НоменклатураКонтрагента.Идентификатор;
		Номенклатура  = СтрокаНоменклатуры.НоменклатураИБ.Номенклатура;
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			СопоставлениеНоменклатуры.Вставить(Идентификатор, Номенклатура);
		КонецЕсли;
	КонецЦикла;	
	
	Для Каждого СтрокаНоменклатуры Из НаборНоменклатурыКонтрагентов Цикл
		Если НЕ ЗначениеЗаполнено(СопоставлениеНоменклатуры.Получить(СтрокаНоменклатуры.Идентификатор)) Тогда
			МассивНеСопоставленнойНоменклатуры.Добавить(СтрокаНоменклатуры);				
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивНеСопоставленнойНоменклатуры;
	
КонецФункции

Процедура СоздатьДокументы(ПараметрыДокумента, ДанныеФайла, Дерево, ПометитьНаУдаление = Истина) Экспорт
	
	Если ПометитьНаУдаление Тогда
		ПометитьНаУдалениеДокументы(ПараметрыДокумента.ОтчетМаркетплейса);
	КонецЕсли;
	
	СоздатьКонтрагентовИДоговоры(ДанныеФайла, ПараметрыДокумента);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ДанныеФайла);
	
	СоздатьДокументыПоСтрокамДерева(Дерево.Строки, ПараметрыДокумента, АдресХранилища);
	
КонецПроцедуры

Процедура СоздатьДокументыПоСтрокамДерева(Строки, Знач ПараметрыДокумента, АдресХранилища) Экспорт
	
	Для Каждого СтрокаДерева Из Строки Цикл
		
		Если СтрокаДерева.Создать Тогда
									
			Если СтрокаДерева.ТипДокумента = "ВозвратТоваровОтПокупателя" И СтрокаДерева.Уровень() > 0 Тогда 
				ПараметрыДокумента.Вставить("Сделка", СтрокаДерева.Родитель.Документ);
			ИначеЕсли СтрокаДерева.ТипДокумента = "РеализацияОтгруженныхТоваров" И СтрокаДерева.Уровень() > 0 Тогда 
				ПараметрыДокумента.Вставить("ДокументОтгрузки", СтрокаДерева.Родитель.Документ);
			КонецЕсли;
			
			ПараметрыДокумента.Вставить("НомерДокумента", СтрокаДерева.НомерДокумента);
			
			НовыйДокумент = Документы[СтрокаДерева.ТипДокумента].СоздатьДокументПоОтчетуЯндекса(
				АдресХранилища, ПараметрыДокумента, НачалоДня(ПараметрыДокумента.ПериодОтчета) + СтрокаДерева.Уровень());
				
			Если ЗначениеЗаполнено(НовыйДокумент) Тогда
				СтрокаДерева.Документ = НовыйДокумент;
			Конецесли;
		КонецЕсли;
		
		СоздатьДокументыПоСтрокамДерева(СтрокаДерева.Строки, ПараметрыДокумента, АдресХранилища);
		
	КонецЦикла;
		
КонецПроцедуры

Процедура ПометитьНаУдалениеДокументы(ОтчетМаркетплейса)
	
	Если НЕ ЗначениеЗаполнено(ОтчетМаркетплейса) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтчетМаркетплейса", ОтчетМаркетплейса);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ОтчетМаркетплейса = &ОтчетМаркетплейса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияОтгруженныхТоваров.Ссылка
	|ИЗ
	|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
	|ГДЕ
	|	РеализацияОтгруженныхТоваров.ОтчетМаркетплейса = &ОтчетМаркетплейса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.ОтчетМаркетплейса = &ОтчетМаркетплейса";
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(Результат.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ДокОбъект = Результат.Ссылка.ПолучитьОбъект();
		ДокОбъект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьКонтрагентовИДоговоры(ДанныеФайла, ПараметрыДокумента)

	ОписаниеОшибки = "";
	Для Каждого Данные Из ДанныеФайла Цикл
		Если Данные.ЭтоПродажаБизнесу Тогда
			ИнтеграцияЯндексМаркет.СоздатьКонтрагентаИДоговорПоПродажеБизнесу(
				Данные.ДанныеДокумента, ПараметрыДокумента, ОписаниеОшибки);
		Иначе // заполняем обобщенным покупателем
			ЗаполнитьЗначенияСвойств(Данные.ДанныеДокумента, ПараметрыДокумента, "Контрагент, ДоговорКонтрагента");
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьПериоды(ОбработкаОбъект, МассивДокументов, ОрганизацияИзОтчета) Экспорт
	
	ПродажиБизнесу = Новый Массив;
	Для Каждого СтраницаОтчета Из МассивДокументов Цикл
		Если ЭлектронноеВзаимодействиеБП.ЭтоОтчетЯндекс(СтраницаОтчета.ВидДокумента) Тогда
			ИнтеграцияЯндексМаркет.ВыделитьПродажиБизнесу(СтраницаОтчета, ПродажиБизнесу);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ПродажаБизнесу Из ПродажиБизнесу Цикл
		МассивДокументов.Добавить(ПродажаБизнесу);
	КонецЦикла;
	
	Для Каждого СтраницаОтчета Из МассивДокументов Цикл
		ДанныеДокумента = СтраницаОтчета.ДанныеДокумента;
		
		Если Не ЗначениеЗаполнено(ОбработкаОбъект.Период) И ДанныеДокумента.Свойство("Дата") Тогда
			ОбработкаОбъект.Период = ДанныеДокумента.Дата;
		КонецЕсли;
		Если ДанныеДокумента.Свойство("Заказчик") Тогда
			Если НЕ ЗначениеЗаполнено(ОрганизацияИзОтчета) Тогда
				ОрганизацияИзОтчета = ДанныеДокумента.Заказчик.НаименованиеПолное;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОбработкаОбъект.Организация) Тогда
				ОбработкаОбъект.Организация = Справочники.Организации.НайтиПоРеквизиту("НаименованиеСокращенное", ДанныеДокумента.Заказчик.НаименованиеПолное);
			КонецЕсли;
		КонецЕсли;
		
		Если СтраницаОтчета.ВидДокумента = "ОтчетЯндекс_Доставлено" Тогда
			ИмяТаблицы = "Реализации";
			ИменаКолонок = "ДатаПередачи, ДатаДокумента";
			ИменаКолонокСорт = "ДатаПередачи Убыв, ДатаДокумента Убыв";
		ИначеЕсли СтраницаОтчета.ВидДокумента = "ОтчетЯндекс_Невыкуплено" Тогда
			ИмяТаблицы = "Невыкупы";
			ИменаКолонок = "ДатаПередачи, ДатаДокумента";
			ИменаКолонокСорт = "ДатаПередачи Убыв, ДатаДокумента Убыв";
		ИначеЕсли СтраницаОтчета.ВидДокумента = "ОтчетЯндекс_Возвращено" Тогда
			ИмяТаблицы = "Возвраты";
			ИменаКолонок = "ДатаПередачи, ДатаДоставки, ДатаДокумента";
			ИменаКолонокСорт = "ДатаПередачи Убыв, ДатаДоставки Убыв, ДатаДокумента Убыв";
		ИначеЕсли СтраницаОтчета.ВидДокумента = "ОтчетЯндекс_Отгружено" Тогда
			ИмяТаблицы = "Отгрузки";
			ИменаКолонок = "ДатаПередачи, ДатаДокумента";
			ИменаКолонокСорт = "ДатаПередачи Убыв, ДатаДокумента Убыв";
		Иначе
			Продолжить;
		Конецесли;
		
		ТаблицаДат = ДанныеДокумента.ТаблицаТоваров.Скопировать(,ИменаКолонок);
		Если Не СтраницаОтчета.ЭтоПродажаБизнесу Тогда
			Для Каждого СтрокаТаблицы Из ТаблицаДат Цикл
				Для Каждого КолонкаТаблицы Из ТаблицаДат.Колонки Цикл
					СтрокаТаблицы[КолонкаТаблицы.Имя] = НачалоМесяца(СтрокаТаблицы[КолонкаТаблицы.Имя]);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		ТаблицаДат.Свернуть(ИменаКолонок);
		ТаблицаДат.Сортировать(ИменаКолонокСорт);
		
		ТаблицаДат.Колонки["ДатаПередачи"].Имя = "Период";
		Если ИмяТаблицы = "Возвраты" Тогда
			ТаблицаДат.Колонки["ДатаДоставки"].Имя = "ПериодДоставки";
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из ТаблицаДат Цикл
			НоваяСтрока = ОбработкаОбъект[ИмяТаблицы].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			Если СтраницаОтчета.ЭтоПродажаБизнесу Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДокумента,
					"КраткоеНаименованиеПокупателя, ДатаДокумента, НомерДокумента");
				Если ИмяТаблицы = "Невыкупы" Тогда
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДокумента,
						"ДатаКоррСФ, НомерКоррСФ");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЗаполнитьСвязанныеДокументы(ОбработкаОбъект) Экспорт
	
	ДатыОтгрузок = Новый Массив;
	ДатыОтгрузок.Добавить(НачалоМесяца(ОбработкаОбъект.Период));
	
	НомераДокументов = Новый Массив;
	НомераДокументов.Добавить("");
	
	Для Каждого СтрокаТаблицы Из ОбработкаОбъект.Реализации Цикл
		ДатыОтгрузок.Добавить(НачалоМесяца(СтрокаТаблицы.Период));
		Если ЗначениеЗаполнено(СтрокаТаблицы.НомерДокумента) Тогда
			НомераДокументов.Добавить(СтрокаТаблицы.НомерДокумента);
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаТаблицы Из ОбработкаОбъект.Невыкупы Цикл
		ДатыОтгрузок.Добавить(НачалоМесяца(СтрокаТаблицы.Период));
		Если ЗначениеЗаполнено(СтрокаТаблицы.НомерДокумента) Тогда
			НомераДокументов.Добавить(СтрокаТаблицы.НомерДокумента);
		КонецЕсли;
	КонецЦикла;
	Для Каждого СтрокаТаблицы Из ОбработкаОбъект.Возвраты Цикл
		ДатыОтгрузок.Добавить(НачалоМесяца(СтрокаТаблицы.Период));
		Если ЗначениеЗаполнено(СтрокаТаблицы.НомерДокумента) Тогда
			НомераДокументов.Добавить(СтрокаТаблицы.НомерДокумента);
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(ДатыОтгрузок);
	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(НомераДокументов);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатыОтгрузок", ДатыОтгрузок);
	Запрос.УстановитьПараметр("Организация",  ОбработкаОбъект.Организация);
	Запрос.УстановитьПараметр("НомераДокументов", НомераДокументов);
	Запрос.УстановитьПараметр("Склад",        ОбработкаОбъект.Склад);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Отгрузка,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.НомерВходящегоДокумента = """"
	|			ТОГДА НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ)
	|		ИНАЧЕ РеализацияТоваровУслуг.Дата
	|	КОНЕЦ КАК Период,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.НомерВходящегоДокумента КАК НомерДокумента
	|ПОМЕСТИТЬ Отгрузки
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности)
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|	И РеализацияТоваровУслуг.Склад = &Склад
	|	И НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, МЕСЯЦ) В (&ДатыОтгрузок)
	|	И РеализацияТоваровУслуг.НомерВходящегоДокумента В(&НомераДокументов)
	|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Отгрузки.Отгрузка КАК Отгрузка,
	|	Отгрузки.Период КАК Период,
	|	Отгрузки.Дата КАК ДатаОтгрузки,
	|	Отгрузки.НомерДокумента КАК НомерДокумента
	|ИЗ
	|	Отгрузки КАК Отгрузки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ,
	|	ДатаОтгрузки УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияОтгруженныхТоваров.Ссылка КАК Доставка,
	|	РеализацияОтгруженныхТоваров.Дата КАК ДатаДоставки,
	|	НАЧАЛОПЕРИОДА(РеализацияОтгруженныхТоваров.Дата, МЕСЯЦ) КАК ПериодДоставки,
	|	Отгрузки.Отгрузка КАК Отгрузка,
	|	Отгрузки.Период КАК Период,
	|	Отгрузки.Дата КАК ДатаОтгрузки,
	|	Отгрузки.НомерДокумента КАК НомерДокумента
	|ИЗ
	|	Отгрузки КАК Отгрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
	|		ПО (РеализацияОтгруженныхТоваров.ДокументОтгрузки = Отгрузки.Отгрузка)
	|			И (НЕ РеализацияОтгруженныхТоваров.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ,
	|	ДатаОтгрузки УБЫВ,
	|	ПериодДоставки УБЫВ,
	|	ДатаДоставки УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Отгрузки.Отгрузка КАК Отгрузка,
	|	Отгрузки.Период КАК Период,
	|	Отгрузки.Дата КАК ДатаОтгрузки,
	|	Отгрузки.НомерДокумента КАК НомерДокумента,
	|	ВозвратТоваровОтПокупателя.Ссылка КАК Невыкуп,
	|	НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, МЕСЯЦ) КАК ПериодНевыкупа,
	|	ВозвратТоваровОтПокупателя.Дата КАК ДатаНевыкупа
	|ИЗ
	|	Отгрузки КАК Отгрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО Отгрузки.Отгрузка = ВозвратТоваровОтПокупателя.Сделка
	|			И (НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ,
	|	ДатаОтгрузки УБЫВ,
	|	ПериодНевыкупа УБЫВ,
	|	ДатаНевыкупа УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Отгрузки.Отгрузка КАК Отгрузка,
	|	Отгрузки.Период КАК Период,
	|	Отгрузки.Дата КАК ДатаОтгрузки,
	|	Отгрузки.НомерДокумента КАК НомерДокумента,
	|	НАЧАЛОПЕРИОДА(ВозвратТоваровОтПокупателя.Дата, МЕСЯЦ) КАК ПериодВозврата,
	|	НАЧАЛОПЕРИОДА(РеализацияОтгруженныхТоваров.Дата, МЕСЯЦ) КАК ПериодДоставки,
	|	ВозвратТоваровОтПокупателя.Ссылка КАК Возврат,
	|	ВозвратТоваровОтПокупателя.Дата КАК ДатаВозврата,
	|	РеализацияОтгруженныхТоваров.Ссылка КАК Доставка,
	|	РеализацияОтгруженныхТоваров.Дата КАК ДатаДоставки
	|ИЗ
	|	Отгрузки КАК Отгрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|			ПО РеализацияОтгруженныхТоваров.Ссылка = ВозвратТоваровОтПокупателя.Сделка
	|				И (НЕ ВозвратТоваровОтПокупателя.ПометкаУдаления)
	|		ПО Отгрузки.Отгрузка = РеализацияОтгруженныхТоваров.ДокументОтгрузки
	|			И (НЕ РеализацияОтгруженныхТоваров.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	ДатаОтгрузки УБЫВ,
	|	ПериодДоставки УБЫВ,
	|	ДатаДоставки УБЫВ,
	|	ПериодВозврата УБЫВ,
	|	ДатаВозврата УБЫВ";
	
	Результаты = Запрос.ВыполнитьПакет();
	Отгрузки = Результаты[1].Выгрузить();
	Доставки = Результаты[2].Выгрузить();
	Невыкупы = Результаты[3].Выгрузить();
	Возвраты = Результаты[4].Выгрузить();
	
	//ДеревоДокументов
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	Дерево.Колонки.Добавить("КраткоеНаименованиеПокупателя", Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("НомерДокумента", Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("ТипДокумента", Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Дерево.Колонки.Добавить("Создать", Новый ОписаниеТипов("Булево"));
	Дерево.Колонки.Добавить("ТекущийПериод", Новый ОписаниеТипов("Булево"));
	Дерево.Колонки.Добавить("Блокировка", Новый ОписаниеТипов("Булево"));
	Дерево.Колонки.Добавить("ИндексКартинки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0)));
	Дерево.Колонки.Добавить("Документ");
	
	Для Каждого СтрокаТаблицы Из ОбработкаОбъект.Отгрузки Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		НоваяСтрока.Создать       = Истина;
		НоваяСтрока.ТекущийПериод = Истина;
		НоваяСтрока.ТипДокумента  = "РеализацияТоваровУслуг";
		Если ЗначениеЗаполнено(СтрокаТаблицы.НомерДокумента) Тогда
			НоваяСтрока.Представление = Формат(СтрокаТаблицы.ДатаДокумента, "ДЛФ='D'") + " Отгрузка без перехода права собственности";
			НоваяСтрока.Период        = СтрокаТаблицы.ДатаДокумента;
		Иначе
			НоваяСтрока.Представление = Формат(КонецМесяца(ОбработкаОбъект.Период), "ДЛФ='D'") + " Отгрузка без перехода права собственности";
			НоваяСтрока.Период        = НачалоМесяца(ОбработкаОбъект.Период);
		КонецЕсли;
		НоваяСтрока.ИндексКартинки = ПолучитьИндексКартинкиДерева(НоваяСтрока);
		НоваяСтрока.КраткоеНаименованиеПокупателя = СтрокаТаблицы.КраткоеНаименованиеПокупателя;
		НоваяСтрока.НомерДокумента = СтрокаТаблицы.НомерДокумента;
		
	КонецЦикла;
	
	//Доставки
	Для Каждого СтрокаДоставки Из ОбработкаОбъект.Реализации Цикл
		Если ЗначениеЗаполнено(СтрокаДоставки.НомерДокумента) Тогда
			СтруктураПоиска = Новый Структура("Период, НомерДокумента", СтрокаДоставки.ДатаДокумента, СтрокаДоставки.НомерДокумента);
		Иначе
			СтруктураПоиска = Новый Структура("Период, НомерДокумента", НачалоМесяца(СтрокаДоставки.Период), "");
		КонецЕсли;
		НайденныеОтгрузки = Доставки.НайтиСтроки(СтруктураПоиска);
		Для Каждого Отгрузка Из НайденныеОтгрузки Цикл
			СтрокаДоставки.ДокументОтгрузки = Отгрузка.Отгрузка;
			Если Отгрузка.ПериодДоставки = НачалоМесяца(ОбработкаОбъект.Период)
				ИЛИ Не ЗначениеЗаполнено(Отгрузка.ПериодДоставки) Тогда
				Если ЗначениеЗаполнено(Отгрузка.Отгрузка) Тогда
					СтрокаДоставки.ДокументРеализации = Отгрузка.Доставка;
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаДоставки.ДокументРеализации) Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		СтрокаДоставки.Загрузить = ЗначениеЗаполнено(СтрокаДоставки.ДокументОтгрузки);
		
		//ДеревоДокументов
		НайденныеСтроки = Дерево.Строки.НайтиСтроки(СтруктураПоиска, Ложь);
		СтрокаОтгрузка = ?(НайденныеСтроки.Количество() = 0, Неопределено, НайденныеСтроки[0]);
		Если СтрокаОтгрузка = Неопределено Тогда
			СтрокаОтгрузка = Дерево.Строки.Добавить();
			СтрокаОтгрузка.ТипДокумента = "РеализацияТоваровУслуг";
			Если ЗначениеЗаполнено(СтрокаДоставки.НомерДокумента) Тогда
				СтрокаОтгрузка.Представление = Формат(СтрокаДоставки.ДатаДокумента, "ДЛФ='D'")
					+" Отгрузка без перехода права собственности";
				СтрокаОтгрузка.Период = СтрокаДоставки.ДатаДокумента;
			Иначе
				СтрокаОтгрузка.Представление = Формат(КонецМесяца(СтрокаДоставки.Период), "ДЛФ='D'")
					+ " Отгрузка без перехода права собственности";
				СтрокаОтгрузка.Период = НачалоМесяца(СтрокаДоставки.Период);
			КонецЕсли;
			СтрокаОтгрузка.Документ = СтрокаДоставки.ДокументОтгрузки;
			СтрокаОтгрузка.ТекущийПериод = (НачалоМесяца(СтрокаОтгрузка.Период) = НачалоМесяца(ОбработкаОбъект.Период));
			СтрокаОтгрузка.ИндексКартинки = ПолучитьИндексКартинкиДерева(СтрокаОтгрузка);
			СтрокаОтгрузка.КраткоеНаименованиеПокупателя = СтрокаДоставки.КраткоеНаименованиеПокупателя;
			СтрокаОтгрузка.НомерДокумента = СтрокаДоставки.НомерДокумента;
		КонецЕсли;
		СтрокаРеализации = СтрокаОтгрузка.Строки.Добавить();
		СтрокаРеализации.Создать = ЗначениеЗаполнено(СтрокаОтгрузка.Документ) ИЛИ СтрокаОтгрузка.ТекущийПериод;
		СтрокаРеализации.Блокировка = НЕ СтрокаРеализации.Создать;
		СтрокаРеализации.ТипДокумента = "РеализацияОтгруженныхТоваров";
		Если ЗначениеЗаполнено(СтрокаДоставки.НомерДокумента) Тогда
			СтрокаРеализации.Представление = Формат(СтрокаДоставки.ДатаДокумента, "ДЛФ='D'")
				+ " Реализация товаров, отгруженных " + Формат(СтрокаОтгрузка.Период, "ДЛФ='D'");
			СтрокаРеализации.Период = СтрокаДоставки.ДатаДокумента;
		Иначе
			СтрокаРеализации.Представление = Формат(КонецМесяца(ОбработкаОбъект.Период), "ДЛФ='D'") + 
				" Реализация товаров, отгруженных в " + НРег(ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(СтрокаДоставки.Период, 6));
			СтрокаРеализации.Период = НачалоМесяца(ОбработкаОбъект.Период);
		КонецЕсли;
		СтрокаРеализации.ТекущийПериод = (НачалоМесяца(СтрокаРеализации.Период) = НачалоМесяца(ОбработкаОбъект.Период));
		СтрокаРеализации.ИндексКартинки = ПолучитьИндексКартинкиДерева(СтрокаРеализации);
		СтрокаРеализации.КраткоеНаименованиеПокупателя = СтрокаДоставки.КраткоеНаименованиеПокупателя;
		СтрокаРеализации.НомерДокумента = СтрокаДоставки.НомерДокумента;
		
	КонецЦикла;
	
	//Невыкупы
	Для Каждого СтрокаДоставки Из ОбработкаОбъект.Невыкупы Цикл
		Если ЗначениеЗаполнено(СтрокаДоставки.НомерДокумента) Тогда
			СтруктураПоиска = Новый Структура("Период, НомерДокумента", СтрокаДоставки.ДатаДокумента, СтрокаДоставки.НомерДокумента);
		Иначе
			СтруктураПоиска = Новый Структура("Период, НомерДокумента", НачалоМесяца(СтрокаДоставки.Период), "");
		КонецЕсли;
		НайденныеОтгрузки = Невыкупы.НайтиСтроки(СтруктураПоиска);
		Для Каждого Отгрузка Из НайденныеОтгрузки Цикл
			СтрокаДоставки.ДокументОтгрузки = Отгрузка.Отгрузка;
			Если Отгрузка.ПериодНевыкупа = НачалоМесяца(ОбработкаОбъект.Период)
				ИЛИ Не ЗначениеЗаполнено(Отгрузка.ПериодНевыкупа) Тогда
				Если ЗначениеЗаполнено(Отгрузка.Отгрузка) Тогда
					СтрокаДоставки.ДокументВозврата = Отгрузка.Невыкуп;
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаДоставки.ДокументВозврата) Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		СтрокаДоставки.Загрузить = ЗначениеЗаполнено(СтрокаДоставки.ДокументОтгрузки);
		
		//ДеревоДокументов
		НайденныеСтроки = Дерево.Строки.НайтиСтроки(СтруктураПоиска, Ложь);
		СтрокаОтгрузка = ?(НайденныеСтроки.Количество() = 0, Неопределено, НайденныеСтроки[0]);
		Если СтрокаОтгрузка = Неопределено Тогда
			СтрокаОтгрузка = Дерево.Строки.Добавить();
			СтрокаОтгрузка.ТипДокумента = "РеализацияТоваровУслуг";
			Если ЗначениеЗаполнено(СтрокаДоставки.НомерДокумента) Тогда
				СтрокаОтгрузка.Представление = Формат(СтрокаДоставки.ДатаДокумента, "ДЛФ='D'")
					+ " Отгрузка без перехода права собственности";
				СтрокаОтгрузка.Период = СтрокаДоставки.ДатаДокумента;
			Иначе
				СтрокаОтгрузка.Представление = Формат(КонецМесяца(СтрокаДоставки.Период), "ДЛФ='D'")
					+ " Отгрузка без перехода права собственности";
				СтрокаОтгрузка.Период = НачалоМесяца(СтрокаДоставки.Период);
			КонецЕсли;
			СтрокаОтгрузка.Документ = СтрокаДоставки.ДокументОтгрузки;
			СтрокаОтгрузка.ТекущийПериод = (НачалоМесяца(СтрокаОтгрузка.Период) = НачалоМесяца(ОбработкаОбъект.Период));
			СтрокаОтгрузка.ИндексКартинки = ПолучитьИндексКартинкиДерева(СтрокаОтгрузка);
			СтрокаОтгрузка.КраткоеНаименованиеПокупателя = СтрокаДоставки.КраткоеНаименованиеПокупателя;
			СтрокаОтгрузка.НомерДокумента = СтрокаДоставки.НомерДокумента;
		КонецЕсли;
		СтрокаНевыкуп = СтрокаОтгрузка.Строки.Добавить();
		СтрокаНевыкуп.Создать = ЗначениеЗаполнено(СтрокаОтгрузка.Документ) ИЛИ СтрокаОтгрузка.ТекущийПериод;
		СтрокаНевыкуп.Блокировка = НЕ СтрокаНевыкуп.Создать;
		СтрокаНевыкуп.ТипДокумента = "ВозвратТоваровОтПокупателя";
		Если ЗначениеЗаполнено(СтрокаДоставки.НомерКоррСФ) Тогда
			СтрокаНевыкуп.Представление = Формат(СтрокаДоставки.ДатаКоррСФ, "ДЛФ='D'")
				+ " Возврат (невыкуп) товаров, отгруженных " + Формат(СтрокаДоставки.ДатаДокумента, "ДЛФ='D'");
			СтрокаНевыкуп.Период = СтрокаДоставки.ДатаКоррСФ;
			СтрокаНевыкуп.НомерДокумента = СтрокаДоставки.НомерКоррСФ;
		ИначеЕсли ЗначениеЗаполнено(СтрокаДоставки.НомерДокумента) Тогда
			СтрокаНевыкуп.Представление = Формат(СтрокаДоставки.ДатаДокумента, "ДЛФ='D'")
				+ " Возврат (невыкуп) товаров, отгруженных " + Формат(СтрокаДоставки.ДатаДокумента, "ДЛФ='D'");
			СтрокаНевыкуп.Период = СтрокаДоставки.ДатаДокумента;
			СтрокаНевыкуп.НомерДокумента = СтрокаДоставки.НомерДокумента;
		Иначе
			СтрокаНевыкуп.Представление = Формат(КонецМесяца(ОбработкаОбъект.Период), "ДЛФ='D'")
				+ " Возврат (невыкуп) товаров, отгруженных в " + НРег(ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(СтрокаДоставки.Период, 6));
			СтрокаНевыкуп.Период = НачалоМесяца(ОбработкаОбъект.Период);
		КонецЕсли;
		СтрокаНевыкуп.ТекущийПериод = (НачалоМесяца(СтрокаНевыкуп.Период) = НачалоМесяца(ОбработкаОбъект.Период));
		СтрокаНевыкуп.ИндексКартинки = ПолучитьИндексКартинкиДерева(СтрокаНевыкуп);
		СтрокаНевыкуп.КраткоеНаименованиеПокупателя = СтрокаДоставки.КраткоеНаименованиеПокупателя;
	КонецЦикла;
	
	//Возвраты
	Для Каждого СтрокаДоставки Из ОбработкаОбъект.Возвраты Цикл
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Период", НачалоМесяца(СтрокаДоставки.Период));
		СтруктураПоиска.Вставить("ПериодДоставки", НачалоМесяца(СтрокаДоставки.ПериодДоставки));
		СтруктураПоиска.Вставить("НомерДокумента", СтрокаДоставки.НомерДокумента);
		НайденныеОтгрузки = Возвраты.НайтиСтроки(СтруктураПоиска);
		Для Каждого Отгрузка Из НайденныеОтгрузки Цикл
			СтрокаДоставки.ДокументРеализации = Отгрузка.Доставка;
			СтрокаДоставки.ДокументОтгрузки = Отгрузка.Отгрузка;
			Если Отгрузка.ПериодВозврата = НачалоМесяца(ОбработкаОбъект.Период)
				ИЛИ Не ЗначениеЗаполнено(Отгрузка.ПериодВозврата) Тогда
				Если ЗначениеЗаполнено(Отгрузка.Доставка) Тогда
					СтрокаДоставки.ДокументВозврата = Отгрузка.Возврат;
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаДоставки.ДокументВозврата) Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		СтрокаДоставки.Загрузить = ЗначениеЗаполнено(СтрокаДоставки.ДокументРеализации);
		
		//ДеревоДокументов
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Период", НачалоМесяца(СтрокаДоставки.Период));
		Если ЗначениеЗаполнено(СтрокаДоставки.НомерДокумента) Тогда
			СтруктураПоиска.Вставить("НомерДокумента", СтрокаДоставки.НомерДокумента);
		КонецЕсли;
		НайденныеСтроки = Дерево.Строки.НайтиСтроки(СтруктураПоиска, Ложь);
		СтрокаОтгрузка = ?(НайденныеСтроки.Количество() = 0, Неопределено, НайденныеСтроки[0]);
		Если СтрокаОтгрузка = Неопределено Тогда
			СтрокаОтгрузка = Дерево.Строки.Добавить();
			СтрокаОтгрузка.ТипДокумента = "РеализацияТоваровУслуг";
			СтрокаОтгрузка.Представление = Формат(КонецМесяца(СтрокаДоставки.Период), "ДЛФ='D'") + 
				" Отгрузка без перехода права собственности";
			СтрокаОтгрузка.Период = НачалоМесяца(СтрокаДоставки.Период);
			СтрокаОтгрузка.Документ = СтрокаДоставки.ДокументОтгрузки;
			СтрокаОтгрузка.ТекущийПериод = (НачалоМесяца(СтрокаОтгрузка.Период) = НачалоМесяца(ОбработкаОбъект.Период));
			СтрокаОтгрузка.ИндексКартинки = ПолучитьИндексКартинкиДерева(СтрокаОтгрузка);
			СтрокаОтгрузка.КраткоеНаименованиеПокупателя = СтрокаДоставки.КраткоеНаименованиеПокупателя;
			СтрокаОтгрузка.НомерДокумента = СтрокаДоставки.НомерДокумента;
		КонецЕсли;
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Период", СтрокаДоставки.ПериодДоставки);
		СтруктураОтбора.Вставить("ТипДокумента", "РеализацияОтгруженныхТоваров");
		Если ЗначениеЗаполнено(СтрокаДоставки.НомерДокумента) Тогда
			СтруктураОтбора.Вставить("НомерДокумента", СтрокаДоставки.НомерДокумента);
		КонецЕсли;
		НайденныеСтроки = СтрокаОтгрузка.Строки.НайтиСтроки(СтруктураОтбора, Ложь);
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаРеализации = СтрокаОтгрузка.Строки.Добавить();
			СтрокаРеализации.ТипДокумента = "РеализацияОтгруженныхТоваров";
			СтрокаРеализации.Представление = Формат(КонецМесяца(СтрокаДоставки.ПериодДоставки), "ДЛФ='D'") + 
				" Реализация товаров, отгруженных в " + Нрег(ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(СтрокаДоставки.ПериодДоставки, 6));
			СтрокаРеализации.Период = НачалоМесяца(СтрокаДоставки.ПериодДоставки);
			СтрокаРеализации.Документ = СтрокаДоставки.ДокументРеализации;
			СтрокаРеализации.ТекущийПериод = (НачалоМесяца(СтрокаРеализации.Период) = НачалоМесяца(ОбработкаОбъект.Период));
			СтрокаРеализации.ИндексКартинки = ПолучитьИндексКартинкиДерева(СтрокаРеализации);
		Иначе
			СтрокаРеализации = НайденныеСтроки[0];
		КонецЕсли;
		СтрокаРеализации.КраткоеНаименованиеПокупателя = СтрокаДоставки.КраткоеНаименованиеПокупателя;
		СтрокаРеализации.НомерДокумента = СтрокаДоставки.НомерДокумента;
		
		СтрокаВозврат = СтрокаРеализации.Строки.Добавить();
		СтрокаВозврат.ТипДокумента = "ВозвратТоваровОтПокупателя";
		Если СтрокаДоставки.Период = СтрокаДоставки.ПериодДоставки Тогда
			СтрокаВозврат.Представление = Формат(КонецМесяца(ОбработкаОбъект.Период), "ДЛФ='D'") + 
				" Возврат товаров, отгруженных и проданных в " + НРег(ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(СтрокаДоставки.ПериодДоставки, 6));
		Иначе
			СтрокаВозврат.Представление = Формат(КонецМесяца(ОбработкаОбъект.Период), "ДЛФ='D'") + 
				" Возврат товаров, отгруженных в " + НРег(ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(СтрокаДоставки.Период, 6)) + 
				" и проданных в " + НРег(ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(СтрокаДоставки.ПериодДоставки, 6));
		КонецЕсли;
		СтрокаВозврат.Период = НачалоМесяца(ОбработкаОбъект.Период);
		СтрокаВозврат.ТекущийПериод = (НачалоМесяца(СтрокаВозврат.Период) = НачалоМесяца(ОбработкаОбъект.Период));
		СтрокаВозврат.Создать = (ЗначениеЗаполнено(СтрокаРеализации.Документ) И НЕ СтрокаВозврат.Блокировка) ИЛИ (СтрокаРеализации.ТекущийПериод И СтрокаРеализации.Создать);
		СтрокаВозврат.Блокировка = НЕ СтрокаВозврат.Создать;
		СтрокаВозврат.ИндексКартинки = ПолучитьИндексКартинкиДерева(СтрокаВозврат);
		СтрокаВозврат.КраткоеНаименованиеПокупателя = СтрокаРеализации.КраткоеНаименованиеПокупателя;
		СтрокаВозврат.НомерДокумента = СтрокаРеализации.НомерДокумента;
	КонецЦикла;
	
	КоличествоДокументов = ОбработкаОбъект.Отгрузки.Количество()
		+ ОбработкаОбъект.Реализации.Количество()
		+ ОбработкаОбъект.Невыкупы.Количество()
		+ ОбработкаОбъект.Возвраты.Количество();
	КоличествоДокументовСоздать = Дерево.Строки.НайтиСтроки(Новый Структура("Создать", Истина), Истина).Количество();
	
	ОшибкаОснованияРеализации = Дерево.Строки.НайтиСтроки(Новый Структура("Блокировка, ТекущийПериод, ТипДокумента", Истина, Истина, "РеализацияОтгруженныхТоваров"), Истина).Количество() > 0;
	ОшибкаОснованияВозврата = Дерево.Строки.НайтиСтроки(Новый Структура("Блокировка, ТекущийПериод, ТипДокумента", Истина, Истина, "ВозвратТоваровОтПокупателя"), Истина).Количество() > 0;
	
	Результат = Новый Структура;
	Результат.Вставить("КоличествоДокументов", КоличествоДокументов);
	Результат.Вставить("КоличествоДокументовСоздать", КоличествоДокументовСоздать);
	Результат.Вставить("ОшибкаОснованияРеализации", ОшибкаОснованияРеализации);
	Результат.Вставить("ОшибкаОснованияВозврата", ОшибкаОснованияВозврата);
	Результат.Вставить("Дерево", Дерево);
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИндексКартинкиДерева(СтрокаДерева)
	
	Если СтрокаДерева.ТекущийПериод Тогда
		Возврат ?(СтрокаДерева.Создать, 1, 0);
	ИначеЕсли ЗначениеЗаполнено(СтрокаДерева.Документ) Тогда
		Возврат 2;
	КонецЕсли;
	
	Возврат 3;
		
КонецФункции

Процедура ДокументыДерева(СписокДокументов, СтрокиДерева) Экспорт
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.Создать Тогда
			Если ЗначениеЗаполнено(СтрокаДерева.Документ) Тогда
				СписокДокументов.Добавить(СтрокаДерева.Документ);
			Конецесли;
			ДокументыДерева(СписокДокументов, СтрокаДерева.Строки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ДеревоНеСозданныхДокументов(ДеревоДокументов) Экспорт
	
	ДеревоНеСозданныхДокументов = ДеревоДокументов.Скопировать();
	
	ПоискСтрокРеализации = Новый Структура("Блокировка, ТекущийПериод, ТипДокумента", Истина, Истина, "РеализацияОтгруженныхТоваров");
	ПоискСтрокВозвраты = Новый Структура("Блокировка, ТекущийПериод, ТипДокумента", Истина, Истина, "ВозвратТоваровОтПокупателя");
	
	КоличествоЭлементов = ДеревоНеСозданныхДокументов.Строки.Количество();
	Для ОбратныйИндекс = 1 По КоличествоЭлементов Цикл
		Индекс = КоличествоЭлементов - ОбратныйИндекс;
		СтрокаДерева = ДеревоНеСозданныхДокументов.Строки[Индекс];
		
		УсловиеУдаления = Не(СтрокаДерева.Блокировка И СтрокаДерева.ТекущийПериод
			И (СтрокаДерева.ТипДокумента = "РеализацияОтгруженныхТоваров"
			Или СтрокаДерева.ТипДокумента = "ВозвратТоваровОтПокупателя"));
		УсловиеУдаления = УсловиеУдаления
			И Не ЗначениеЗаполнено(СтрокаДерева.Строки.НайтиСтроки(ПоискСтрокРеализации, Истина))
			И Не ЗначениеЗаполнено(СтрокаДерева.Строки.НайтиСтроки(ПоискСтрокВозвраты, Истина));
		Если УсловиеУдаления Тогда
			ДеревоНеСозданныхДокументов.Строки.Удалить(Индекс);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДеревоНеСозданныхДокументов;
	
КонецФункции

Функция ПараметрыСозданияДокументов(ОбработкаОбъект, ОрганизацияИзОтчета, ГруппаДляНовыхКонтрагентов) Экспорт
	
	ОтчетМаркетплейса = НайтиСоздатьОтчетЯндекса(
		ОбработкаОбъект.Организация, ОбработкаОбъект.Период, ОрганизацияИзОтчета);
	
	Параметры  = Новый Структура;
	Параметры.Вставить("Организация",        ОбработкаОбъект.Организация);
	Параметры.Вставить("Контрагент",         ОбработкаОбъект.Контрагент);
	Параметры.Вставить("ДоговорКонтрагента", ОбработкаОбъект.ДоговорКонтрагента);
	Параметры.Вставить("Склад",              ОбработкаОбъект.Склад);
	Параметры.Вставить("ПериодОтчета",       ОбработкаОбъект.Период);
	Параметры.Вставить("ОтчетМаркетплейса",  ОтчетМаркетплейса);
	Параметры.Вставить("ПлатежныйАгент",     ОбработкаОбъект.ПлатежныйАгент);
	Параметры.Вставить("ДоговорПлатежногоАгента", ОбработкаОбъект.ДоговорПлатежногоАгента);
	Параметры.Вставить("ГруппаДляНовыхКонтрагентов", ГруппаДляНовыхКонтрагентов);
	
	Возврат Параметры;
	
КонецФункции

Функция НайтиСоздатьОтчетЯндекса(Организация, Период, Магазин = "")
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", КонецДня(Период));
	Запрос.УстановитьПараметр("Маркетплейс", Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	Запрос.УстановитьПараметр("Магазин", СокрЛП(Магазин));
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетМаркетплейса.Ссылка КАК Ссылка,
	|	ОтчетМаркетплейса.Магазин КАК Магазин
	|ИЗ
	|	Документ.ОтчетМаркетплейса КАК ОтчетМаркетплейса
	|ГДЕ
	|	НЕ ОтчетМаркетплейса.ПометкаУдаления
	|	И ОтчетМаркетплейса.Организация = &Организация
	|	И ОтчетМаркетплейса.Маркетплейс = &Маркетплейс
	|	И ОтчетМаркетплейса.Магазин = &Магазин
	|	И ОтчетМаркетплейса.Дата = &Дата";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		
		Если НЕ ЗначениеЗаполнено(Результат.Магазин)
			И ЗначениеЗаполнено(Магазин) Тогда
			ДокументОбъект = Результат.Ссылка.ПолучитьОбъект();
			ДокументОбъект.Магазин = Магазин;
			Попытка
				ДокументОбъект.Записать();
			Исключение
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр
				("ru = 'Не удалось записать отчет Яндекс.Маркет от %1 по организации ""%2""'"), Формат(Период, "ДЛФ=ДД"), Организация);
			КонецПопытки;
		КонецЕсли;
		
		Возврат Результат.Ссылка;
	КонецЕсли;
	
	НовыйОтчетЯндекса = Документы.ОтчетМаркетплейса.СоздатьДокумент();
	НовыйОтчетЯндекса.Дата = КонецДня(Период);
	НовыйОтчетЯндекса.Организация = Организация;
	НовыйОтчетЯндекса.Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет;
	НовыйОтчетЯндекса.Магазин     = Магазин;
	
	Попытка
		НовыйОтчетЯндекса.Записать();
	Исключение
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр
			("ru = 'Не удалось записать отчет Яндекс.Маркет от %1 по организации ""%2""'"), Формат(Период, "ДЛФ=ДД"), Организация);
	КонецПопытки;
	
	Возврат НовыйОтчетЯндекса.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли
