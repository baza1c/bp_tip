
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтотОбъект.Параметры.Свойство("ДеревоДокументов") Тогда
		АдресХранилища = Параметры.ДеревоДокументов;
		Дерево = ПолучитьИзВременногоХранилища(АдресХранилища);
		ЗначениеВРеквизитФормы(Дерево, "ДеревоДокументов");
		
		ЗаполнитьЗначенияСвойств(Объект, ЭтотОбъект.Параметры);
		
		Если ЭтотОбъект.Параметры.Свойство("Период") Тогда
			ТекущийПериод = НачалоМесяца(Параметры.Период);
		КонецЕсли;
		
		Параметры.Свойство("ОтложенноеСвязываниеДокументов", ОтложенноеСвязываниеДокументов);
		
		УстановитьУсловноеОформлениеДерево();
		УправлениеВидимостью(ЭтотОбъект);
		
	Иначе
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ПриЗакрытииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСоздать(Команда)
	
	ПриЗакрытииНаСервере();
	Закрыть(АдресХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДеревоДокументовСоздатьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоДокументов.ТекущиеДанные;
	ТекущиеДанные.ИндексКартинки = ?(ТекущиеДанные.Создать, 1, 0); 
			
	ПодчиненныеСтроки = ТекущиеДанные.ПолучитьЭлементы();
	Если ПодчиненныеСтроки.Количество() > 0 Тогда
		ОбработатьБлокировкуПодчиненныхСтрок(ПодчиненныеСтроки, НЕ ТекущиеДанные.Создать);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДокументовДокументПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоДокументов.ТекущиеДанные;
	ТекущиеДанные.ИндексКартинки = ?(ЗначениеЗаполнено(ТекущиеДанные.Документ), 2, 3);
	ТекущиеДанные.Блокировка = Не ЗначениеЗаполнено(ТекущиеДанные.Документ);
	
	ПодчиненныеСтроки = ТекущиеДанные.ПолучитьЭлементы();
	Если ПодчиненныеСтроки.Количество() > 0 Тогда
		ОбработатьБлокировкуПодчиненныхСтрок(ПодчиненныеСтроки, ТекущиеДанные.Блокировка);
	КонецЕсли;
	
	ЕстьДокументыДляСоздания = Ложь;
	ЕстьДокументыДляСоздания(ДеревоДокументов.ПолучитьЭлементы(), ЕстьДокументыДляСоздания);
	УправлениеВидимостью(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеДерево()
	
	//Цвет строк прошлого месяца
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовСоздать");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовПредставление");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовНомерДокумента");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовКраткоеНаименованиеПокупателя");
	//КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.ТекущийПериод", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	//Недоступность флажка прошлого месяца
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовСоздать");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.ТекущийПериод", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//Недоступность флажка блокировка
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовСоздать");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Блокировка", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Создать", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//оформление поля "Документ" текущего месяца
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.ТекущийПериод", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ВажноеСобытие);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Документ не найден>'"));
		
	//Недоступность поля "Документ" текущего месяца
	
	//оформление поля "Документ" текущего месяца
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.ТекущийПериод", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Создать", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Будет создан автоматически>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//оформление поля "Документ" текущего месяца
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.ТекущийПериод", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Создать", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Операция будет пропущена>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьБлокировкуПодчиненныхСтрок(ПодчиненныеСтроки, Знач Блокировка)
	
	Для Каждого СтрокаДерева Из ПодчиненныеСтроки Цикл
		Если СтрокаДерева.ТекущийПериод Тогда
			СтрокаДерева.Создать = Не Блокировка;
			СтрокаДерева.ИндексКартинки = ?(СтрокаДерева.Создать, 1, 0);
			СтрокаДерева.Блокировка = Блокировка;
		Иначе
			СтрокаДерева.Блокировка = НЕ ЗначениеЗаполнено(СтрокаДерева.Документ);
		КонецЕсли;
		ПодчиненныеСтроки = СтрокаДерева.ПолучитьЭлементы();
		Если ПодчиненныеСтроки.Количество() > 0 
			И СтрокаДерева.Блокировка = Блокировка Тогда
			ОбработатьБлокировкуПодчиненныхСтрок(ПодчиненныеСтроки, Блокировка);
		КонецЕсли;
	КонецЦикла
		
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	Дерево = РеквизитФормыВЗначение("ДеревоДокументов");
	ПоместитьВоВременноеХранилище(Дерево, АдресХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьДокументыДляСоздания(СтрокиДерева, ЕстьДокументыКСозданию)
	
	Для Каждого ПодчиненнаяСтрока Из СтрокиДерева Цикл
		Если Не ПодчиненнаяСтрока.Блокировка Тогда
			ЕстьДокументыДляСоздания = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостью(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ФормаКомандаСоздать.Видимость = Форма.ОтложенноеСвязываниеДокументов
		И Форма.ЕстьДокументыДляСоздания;
	Элементы.ФормаКомандаЗакрыть.Видимость = Форма.ОтложенноеСвязываниеДокументов;
	
	Элементы.ГруппаПредупрежденияОтложенноеСвязываниеДокументов.Видимость = Форма.ОтложенноеСвязываниеДокументов;
	
КонецПроцедуры

#КонецОбласти

