
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Объект.Организация                    = Параметры.Организация;
	Объект.ДатаВводаОстатков              = Параметры.ДатаВводаОстатков;
	Объект.ВалютаРегламентированногоУчета = Параметры.ВалютаРегламентированногоУчета;
	
	ТекстЗаголовок = НСтр("ru = 'Начальные остатки: Запасы (%1)'");
	ТекстЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовок, Объект.Организация);
	ЭтаФорма.Заголовок = ТекстЗаголовок;
	
	ВедетсяСкладскойУчет = Обработки.ВводНачальныхОстатков.ВедетсяСкладскойУчет();
	
	Элементы.ЗапасыСклад.Видимость = ВедетсяСкладскойУчет;
	
	МенеджерОбработки = Обработки.ВводНачальныхОстатков;
	МенеджерОбработки.СобратьСтруктуруТаблиц(Объект.Запасы, "Запасы", СтруктураТаблиц);
	МенеджерОбработки.ОбновитьОстатки(Объект.Запасы, "Запасы", 
		Новый Структура("Организация,ДатаВводаОстатков",
					Объект.Организация,Объект.ДатаВводаОстатков),
		Объект.СуществующиеДокументы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзмененениеДатыВводаОстатков" И Источник = "ВводНачальныхОстатков" И Параметр = Объект.Организация Тогда
		Объект.ДатаВводаОстатков = ПеречитатьДатуНачалаУчета(Объект.Организация);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПередЗакрытиемЗавершение",
		ЭтотОбъект);
	
	ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗапасы

&НаКлиенте
Процедура ЗапасыНоменклатураПриИзменении(Элемент)
	
	СтрокаТаблицы   = Элементы.Запасы.ТекущиеДанные;
	СтрокаТаблицы.СчетУчета = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыСкладПриИзменении(Элемент)
	
	СтрокаТаблицы   = Элементы.Запасы.ТекущиеДанные;
	СтрокаТаблицы.СчетУчета = "";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	Перем ОтборТекущейСтроки;
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы.Запасы.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		
		ОтборТекущейСтроки = Новый Структура;
		ОтборТекущейСтроки.Вставить("Номенклатура", ТекДанные.Номенклатура);
		ОтборТекущейСтроки.Вставить("Количество",   ТекДанные.Количество);
		ОтборТекущейСтроки.Вставить("Сумма",        ТекДанные.Сумма);
		Если ЗначениеЗаполнено(ТекДанные.Склад) Тогда
			ОтборТекущейСтроки.Вставить("Склад", ТекДанные.Склад);
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекДанные.СчетУчета) Тогда
			ОтборТекущейСтроки.Вставить("СчетУчета", ТекДанные.СчетУчета);
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекДанные.Партия) Тогда
			ОтборТекущейСтроки.Вставить("Партия", ТекДанные.Партия);
		КонецЕсли;
		
	КонецЕсли;
		
	Отказ = Ложь;
	ЗаписатьНаСервере(Истина, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтборТекущейСтроки <> Неопределено Тогда
		НайденныеСтроки = Объект.Запасы.НайтиСтроки(ОтборТекущейСтроки);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Элементы.Запасы.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	Оповестить("ОбновитьФормуПомощникаВводаОстатков", Объект.Организация, "ВводНачальныхОстатков");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Отказ = Ложь;
	Если Модифицированность Тогда
		ЗаписатьНаСервере(Ложь, Отказ);
		Если НЕ Отказ Тогда
			Оповестить("ОбновитьФормуПомощникаВводаОстатков", Объект.Организация, "ВводНачальныхОстатков");
		КонецЕсли;
	КонецЕсли;
	Если НЕ Отказ Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзВнешнихФайловКлиент.НовыйПараметрыЗагрузкиВТабЧасть();
	
	ПараметрыЗагрузки.ИмяТабличнойЧасти = "Объект.Запасы";
	ПараметрыЗагрузки.Заголовок = НСтр("ru = 'Загрузка запасов из файла'");
	ПараметрыЗагрузки.ПрикладнаяЗагрузка = Истина;
	ПараметрыЗагрузки.ПараметрыПрикладнойЗагрузки.Период = Объект.ДатаВводаОстатков;
	ПараметрыЗагрузки.Оповещение = Новый ОписаниеОповещения("ЗавершитьЗагрузкуИзФайла", ЭтотОбъект);
	
	ЗагрузкаДанныхИзВнешнихФайловКлиент.ВыбратьФайлДляЗагрузки(
		"Обработка.ЗагрузкаНоменклатурыИзФайла.Форма",
		УникальныйИдентификатор,
		ПараметрыЗагрузки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПеречитатьДатуНачалаУчета(Организация)
	
	Возврат Обработки.ВводНачальныхОстатков.ПеречитатьДатуНачалаУчета(Организация);
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Отказ = Ложь;
		ЗаписатьНаСервере(, Отказ);
		Если НЕ Отказ Тогда
			Закрыть();
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОпределитьРазделУчетаЗапасов(Счет, СписокРазделов)

	КодСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет).Код;
	ПозицияТочки = Найти(КодСчета, ".");
	КодРодителя = ?(ПозицияТочки = 0, КодСчета, Лев(КодСчета, ПозицияТочки - 1));

	Для Каждого СтрокаРаздела Из СписокРазделов Цикл

		Если Найти(СтрокаРаздела.КодыГрупп, "," + КодРодителя + ",") > 0 Тогда
			Возврат СтрокаРаздела.РазделУчета;
		КонецЕсли;

	КонецЦикла;

	Возврат ПредопределенноеЗначение("Перечисление.РазделыУчетаДляВводаОстатков.Товары");

КонецФункции

&НаСервере
Процедура ЗаписатьНаСервере(ОбновитьОстатки = Истина, Отказ = Ложь)
	
	Отказ = НЕ ПроверитьЗаполнение();
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерОбработки = Обработки.ВводНачальныхОстатков;
	МенеджерОбработки.СинхронизироватьСостояниеДокументов(Объект.Запасы, Объект.СуществующиеДокументы);
	
	СтруктураПараметровДокументов = Новый Структура("Организация, Дата, РазделУчета", 
		Объект.Организация, Объект.ДатаВводаОстатков, "");

	Отбор = Новый Структура("НеЗаполненныеРеквизиты, ТабличнаяЧасть", Истина, "Запасы");
	СчетаУчетаВДокументах.ЗаполнитьТаблицу(Обработки.ВводНачальныхОстатков, СтруктураПараметровДокументов, Объект.Запасы, Отбор);
	
	ТаблицыДанных = ПодготовитьТабличнуюЧастьКЗаписи(Объект.Запасы, ВедетсяСкладскойУчет, СтруктураПараметровДокументов);
	Для Каждого ТаблицаДанных ИЗ ТаблицыДанных Цикл
		СтруктураПараметровДокументов.Вставить("РазделУчета", ТаблицаДанных[0].РазделУчета);
		МенеджерОбработки.ЗаписатьНаСервереДокументы(СтруктураПараметровДокументов, ТаблицаДанных, "НоменклатураНаСкладе");
	КонецЦикла;
	МенеджерОбработки.ОбновитьФинансовыйРезультат(СтруктураПараметровДокументов, Объект.ФинансовыйРезультат);
	
	Если ОбновитьОстатки Тогда
		
		МенеджерОбработки.ОбновитьОстатки(Объект.Запасы, "Запасы", 
			Новый Структура("Организация,ДатаВводаОстатков",
				Объект.Организация,Объект.ДатаВводаОстатков),
			Объект.СуществующиеДокументы);
		
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодготовитьТабличнуюЧастьКЗаписи(Таблица, ВедетсяСкладскойУчет, СтруктураПараметровДокументов);
	
	СписокРазделов = Новый ТаблицаЗначений;
	СписокРазделов.Колонки.Добавить("РазделУчета");
	СписокРазделов.Колонки.Добавить("КодыГрупп");
	
	СтрокаРаздела = СписокРазделов.Добавить();
	СтрокаРаздела.РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.ГотоваяПродукцияИПолуфабрикаты;
	СтрокаРаздела.КодыГрупп   = ",43,21,";

	СтрокаРаздела = СписокРазделов.Добавить();
	СтрокаРаздела.РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.КапитальныеВложения;
	СтрокаРаздела.КодыГрупп   = ",07,08,";

	СтрокаРаздела = СписокРазделов.Добавить();
	СтрокаРаздела.РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.Материалы;
	СтрокаРаздела.КодыГрупп   = ",10,";

	СтрокаРаздела = СписокРазделов.Добавить();
	СтрокаРаздела.РазделУчета    = Перечисления.РазделыУчетаДляВводаОстатков.Товары;
	СтрокаРаздела.КодыГрупп      = ",41,42,";

	СтрокаРаздела = СписокРазделов.Добавить();
	СтрокаРаздела.РазделУчета    = Перечисления.РазделыУчетаДляВводаОстатков.ТоварыОтгруженные;
	СтрокаРаздела.КодыГрупп      = ",45,";
	
	СтрокаРаздела = СписокРазделов.Добавить();
	СтрокаРаздела.РазделУчета    = Перечисления.РазделыУчетаДляВводаОстатков.ПрочиеСчетаБухгалтерскогоУчета;
	СтрокаРаздела.КодыГрупп      = ",004,";
	
	ТаблицаСуществующихДанных = Таблица.Выгрузить();
	ТаблицаСуществующихДанных.Очистить();
	ТаблицаСуществующихДанных.Колонки.Добавить("РазделУчета");
	ТаблицаСуществующихДанных.Колонки.Добавить("СуммаНУ");
	ТаблицаНовыхДанных = ТаблицаСуществующихДанных.СкопироватьКолонки();
	
	Для Каждого СтрокаТаблицы ИЗ Таблица Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицы.Ссылка) Тогда
			НоваяСтрока = ТаблицаСуществующихДанных.Добавить();
			НоваяСтрока.РазделУчета = СтрокаТаблицы.Ссылка.РазделУчета;
		Иначе
			НоваяСтрока = ТаблицаНовыхДанных.Добавить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		Если НоваяСтрока.РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.КапитальныеВложения Тогда
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Партия) Тогда
				НоваяСтрока.Партия = Обработки.ВводНачальныхОстатков.ПолучитьДокумент(Справочники.Контрагенты.ПустаяСсылка(),Справочники.ДоговорыКонтрагентов.ПустаяСсылка(), СтруктураПараметровДокументов);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураТаблиц = Новый Структура();
	Если ТаблицаСуществующихДанных.Количество()<> 0 Тогда
		СтруктураТаблиц.Вставить("ОсновнойРазделУчета", ТаблицаСуществующихДанных);
	КонецЕсли;
	
	МасивТаблиц = Новый Массив;
	Если ТаблицаСуществующихДанных.Количество()<> 0 Тогда
		МасивТаблиц.Добавить(ТаблицаСуществующихДанных);
	КонецЕсли;
	
	ТаблицаНовыхДанных.Сортировать("СчетУчета");
	РазделУчета = "";
	КоличествоСтрокНовыхДанных = ТаблицаНовыхДанных.Количество();
	Для Каждого СтрокаТаблицы ИЗ ТаблицаНовыхДанных Цикл
		Если РазделУчета = "" Тогда
			РазделУчета = ОпределитьРазделУчетаЗапасов(СтрокаТаблицы.СчетУчета, СписокРазделов);
			ТекущийРазделУчета = ОпределитьРазделУчетаЗапасов(СтрокаТаблицы.СчетУчета, СписокРазделов);
			ТекущаяТаблица = ТаблицаНовыхДанных.СкопироватьКолонки();
		КонецЕсли;
		ТекущийРазделУчета = ОпределитьРазделУчетаЗапасов(СтрокаТаблицы.СчетУчета, СписокРазделов);
		Если ТекущийРазделУчета <> РазделУчета Тогда
			МасивТаблиц.Добавить(ТекущаяТаблица);
			РазделУчета = ОпределитьРазделУчетаЗапасов(СтрокаТаблицы.СчетУчета, СписокРазделов);
			ТекущаяТаблица = ТаблицаНовыхДанных.СкопироватьКолонки();
		КонецЕсли;
		НоваяСтрока = ТекущаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.РазделУчета = РазделУчета;
		НоваяСтрока.СуммаНУ     = НоваяСтрока.Сумма;
		Если НоваяСтрока.РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.КапитальныеВложения Тогда
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Партия) Тогда
				НоваяСтрока.Партия = Обработки.ВводНачальныхОстатков.ПолучитьДокумент(Справочники.Контрагенты.ПустаяСсылка(),Справочники.ДоговорыКонтрагентов.ПустаяСсылка(), СтруктураПараметровДокументов);
			КонецЕсли;
		КонецЕсли;
		Если ТаблицаНовыхДанных.Индекс(СтрокаТаблицы) = КоличествоСтрокНовыхДанных - 1 Тогда
			МасивТаблиц.Добавить(ТекущаяТаблица);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МасивТаблиц;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьЗагрузкуИзФайла(ЗагруженныеДанные, НеиспользуемыйПараметр) Экспорт
	
	Если ТипЗнч(ЗагруженныеДанные) <> Тип("Массив")
		Или ЗагруженныеДанные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЗагруженнаяСтрока Из ЗагруженныеДанные Цикл
		НоваяСтрока = Объект.Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗагруженнаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
