
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Объект.Организация = Параметры.Организация;
	Объект.ДатаВводаОстатков = Параметры.ДатаВводаОстатков;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Начальные остатки: Платежные карты (%1)'"), Объект.Организация);
	
	ОбновитьОстаткиНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзмененениеДатыВводаОстатков"
		И Источник = "ВводНачальныхОстатков"
		И Параметр = Объект.Организация Тогда
		
		Объект.ДатаВводаОстатков = ПеречитатьДатуНачалаУчета(Объект.Организация);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	
	ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПлатежныеКарты

&НаКлиенте
Процедура ПлатежныеКартыВидОплатыСоздание(Элемент, СтандартнаяОбработка)

	ТекущиеДанные = Элементы.ПлатежныеКарты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ВведенноеЗначение = ?(Элемент.ТекстРедактирования = Строка(ТекущиеДанные.ВидОплаты), "", Элемент.ТекстРедактирования);
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Организация", Объект.Организация);
	ЗначенияЗаполнения.Вставить("Наименование", ВведенноеЗначение);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);

	ОткрытьФорму("Справочник.ВидыОплатОрганизаций.Форма.ФормаНовогоЭлемента", ПараметрыФормы, Элемент);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)

	Если Модифицированность Тогда
		
		Отказ = Ложь;
		
		ЗаписатьНаСервере(Истина, Отказ);
		Если Не Отказ Тогда
			Оповестить("ОбновитьФормуПомощникаВводаОстатков", Объект.Организация, "ВводНачальныхОстатков");
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)

	Отказ = Ложь;
	Если Модифицированность Тогда
		
		ЗаписатьНаСервере(Ложь, Отказ);
		Если Не Отказ Тогда
			Оповестить("ОбновитьФормуПомощникаВводаОстатков", Объект.Организация, "ВводНачальныхОстатков");
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Отказ Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьОстаткиНаСервере()
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Организация", Объект.Организация);
	ПараметрыВыполнения.Вставить("ДатаВводаОстатков", Объект.ДатаВводаОстатков);
	
	Обработки.ВводНачальныхОстатков.ОбновитьОстатки(Объект.ПлатежныеКарты,
		"ПлатежныеКарты",
		ПараметрыВыполнения,
		Объект.СуществующиеДокументы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПеречитатьДатуНачалаУчета(Организация)
	
	Возврат Обработки.ВводНачальныхОстатков.ПеречитатьДатуНачалаУчета(Организация);
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Отказ = Ложь;
		ЗаписатьНаСервере(, Отказ);
		
		Если Не Отказ Тогда
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере(ОбновитьОстатки = Истина, Отказ = Ложь)
	
	Если Не ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МенеджерОбработки = Обработки.ВводНачальныхОстатков;
	МенеджерОбработки.СинхронизироватьСостояниеДокументов(Объект.ПлатежныеКарты, Объект.СуществующиеДокументы);
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("Организация", Объект.Организация);
	ПараметрыДокумента.Вставить("Дата",        Объект.ДатаВводаОстатков);
	ПараметрыДокумента.Вставить("РазделУчета", Перечисления.РазделыУчетаДляВводаОстатков.ДенежныеСредства);
	
	ДанныеПоПлатежнымКартам = Объект.ПлатежныеКарты.Выгрузить();
	ТаблицаДанных = ПодготовленныеДанныеКЗаписи(ДанныеПоПлатежнымКартам, ПараметрыДокумента);
	
	МенеджерОбработки.ЗаписатьНаСервереДокументы(ПараметрыДокумента, ТаблицаДанных, "БухСправка");
	МенеджерОбработки.ОбновитьФинансовыйРезультат(ПараметрыДокумента, Объект.ФинансовыйРезультат);
	
	Если ОбновитьОстатки Тогда
		ОбновитьОстаткиНаСервере();
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодготовленныеДанныеКЗаписи(ТаблицаДанных, ПараметрыДокумента)
	
	Возврат Обработки.ВводНачальныхОстатков.ПодготовитьТабличнуюЧасть_ПлатежныеКарты(ТаблицаДанных, ПараметрыДокумента);
	
КонецФункции

#КонецОбласти