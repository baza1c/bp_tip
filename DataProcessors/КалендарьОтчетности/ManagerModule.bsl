
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Формирует отчет по ранее полученным данным приложений.
//
// Параметры:
//  ДанныеПриложений - Структура - см. НовыйДанныеДляОтчета
//  Организация - Справочник.Организации, Справочник.ОрганизацииОблачныхПриложений, Неопределено - отбор по организации
//
// Возвращаемое значение:
//   ТабличныйДокумент - сформированный отчет.
//
Функция СформироватьОтчет(ДанныеПриложений, Организация = Неопределено) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПараметрыВывода = Новый Структура;
	ПараметрыВывода.Вставить("НаДату", ДанныеПриложений.НаДату);
	
	// В общем случае строка с представлением налога оптимально умещается в 2 строки.
	ПараметрыВывода.Вставить("ВысотаСтрокиШапкиНалоги", ВысотаСтрокиШапкиНалоги() * 2);
	
	Данные = СводнаяТаблицаДанныхПриложений(
		ДанныеПриложений.ДанныеЭтогоПриложения,
		ДанныеПриложений.ДанныеПодключенныхПриложений,
		Организация);
	
	Если Данные.Расписание.Количество() = 0 Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;
	
	ОтборПоОрганизации = Организация <> Неопределено;
	
	// Отчеты и уведомления есть по всем организациям, по которым ведется учет. Но требования могут быть только у одной из них.
	// Некрасиво перестраивать ориентацию отчета (горизонтальная <-> вертикальная), если меняется отбор.
	ЧислоОрганизаций = Макс(Данные.Организации.Количество(), ДанныеПриложений.Параметры.Организации.Количество());
	ПараметрыВывода.Вставить("ПоказатьОрганизации", ЧислоОрганизаций > 1 И Не ОтборПоОрганизации);
	
	ЧислоОрганизаций = Данные.Организации.Количество();
	ПараметрыВывода.Вставить("МногоОрганизаций", ЧислоОрганизаций > ПорогВместимостиЭкрана() И Не ОтборПоОрганизации);
	ПараметрыВывода.Вставить("Области", ВсеОбластиМакета(ПараметрыВывода.МногоОрганизаций));
	
	ПараметрыВывода.Вставить("ОжидаетсяЗагрузкаДанных", ДанныеПриложений.ОжидающиеЗапросы.Количество() > 0);
	ПараметрыВывода.Вставить("ЕстьОшибкиЗапросов", ДанныеПриложений.ОшибкиПриложений.Количество() > 0);
	ПараметрыВывода.Вставить("ПравоНаВключениеОтчетов", Ложь);
	КалендарьОтчетностиПереопределяемый.УстановитьПараметрыВыводаКалендаряОтчетности(ПараметрыВывода);
	
	// Добавляем данные о делении сроков сдачи на группы.
	Данные.Сроки = СрокиПоЗадачам(Данные, ПараметрыВывода.НаДату);
	
	ВывестиЗаголовок(ТабличныйДокумент, Данные, ПараметрыВывода);
	
	ВывестиСтрокуСроков(ТабличныйДокумент, Данные, ПараметрыВывода);
	
	Если ПараметрыВывода.ПоказатьОрганизации Тогда
		ТабличныйДокумент.Вывести(ПараметрыВывода.Области.ДатыОрганизация);
	КонецЕсли;
	
	ВысотаШапки = ТабличныйДокумент.ВысотаТаблицы;
	
	МакетДанных = ВывестиТаблицуДанных(
		Данные,
		ДанныеПриложений.ОжидающиеЗапросы,
		ДанныеПриложений.ОшибкиПриложений,
		ПараметрыВывода);
		
	Если ПараметрыВывода.ПоказатьОрганизации Тогда
		ВывестиСтрокуНалогов(ТабличныйДокумент, Данные, ПараметрыВывода);
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(МакетДанных);
	
	ОформитьСтрокиОтчета(ТабличныйДокумент, ВысотаШапки);
	
	ЗафиксироватьШапку(ТабличныйДокумент, ПараметрыВывода, ВысотаШапки);
	
	Если Не ПараметрыВывода.ПоказатьОрганизации Тогда
		ОформитьОбластиОрганизаций(ТабличныйДокумент, Данные, ПараметрыВывода, ВысотаШапки);
		ВывестиПодвал(ТабличныйДокумент, Данные, ПараметрыВывода);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Возвращает параметры формирования отчета.
// 
// Возвращаемое значение:
//   - Структура:
//     * ДатаНачала - Дата - начало периода отчета.
//     * ДатаКонца - Дата - конец периода отчета.
//     * НаДату - Дата - дата, относительно которой период отчета делится на прошлое и будущее.
//     * Пользователь - СправочникСсылка.Пользователи - пользователь, формирующий отчет.
//     * Организации - Массив из СправочникСсылка.Организации - список организаций, по которым формировать отчет.
//
Функция НовыйПараметрыОтчета() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ДатаНачала", '0001-01-01');
	Параметры.Вставить("ДатаКонца", '0001-01-01');
	Параметры.Вставить("НаДату", '0001-01-01');
	Параметры.Вставить("ПоНалоговымПлатежам", Ложь);
	Параметры.Вставить("ПоДокументамСЭДО", Ложь);
	Параметры.Вставить("ПоОтчетам", Ложь);
	Параметры.Вставить("ПоТребованиямФНС", Ложь);
	Параметры.Вставить("ПоУведомлениямЕНС", Ложь);
	Параметры.Вставить("ПоЗарплате", Ложь);
	Параметры.Вставить("Пользователь", Справочники.Пользователи.ПустаяСсылка());
	Параметры.Вставить("Организации", Новый Массив);
	
	Возврат Параметры;
	
КонецФункции

Функция НовыйДанныеОтчетности() Экспорт
	
	ТипСтрока = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(250));
	ТипДата = Новый ОписаниеТипов("Дата");
	ТипБулево = Новый ОписаниеТипов("Булево");
	КвалификаторЧисла = Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный);
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить(Тип("СправочникСсылка.Организации"));
	КалендарьОтчетностиПереопределяемый.ПриОпределенииСпискаТиповОрганизации(СписокТипов);
	ТипОрганизация = Новый ОписаниеТипов(СписокТипов);
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить(Тип("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов"));
	СписокТипов.Добавить(Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ТранспортноеСообщение"));
	КалендарьОтчетностиПереопределяемый.ПриОпределенииСпискаТиповПравила(СписокТипов);
	
	ТипПравило = Новый ОписаниеТипов(СписокТипов);
	
	СписокТипов = Новый Массив;
	КалендарьОтчетностиПереопределяемый.ПриОпределенииСпискаТиповПриложения(СписокТипов);
	ТипПриложение = Новый ОписаниеТипов(СписокТипов);
	
	Данные = Новый ТаблицаЗначений();
	Данные.Колонки.Добавить("Приложение", ТипПриложение);
	Данные.Колонки.Добавить("Организация", ТипОрганизация);
	Данные.Колонки.Добавить("НаименованиеОрганизации", ТипСтрока);
	Данные.Колонки.Добавить("ИдентификаторОрганизации", ТипСтрока);
	Данные.Колонки.Добавить("РегистрацияФНС", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
	Данные.Колонки.Добавить("КодФНС", ТипСтрока);
	Данные.Колонки.Добавить("КодНалога", ТипСтрока);
	Данные.Колонки.Добавить("Правило", ТипПравило);
	Данные.Колонки.Добавить("ИдентификаторПравила", ТипСтрока);
	Данные.Колонки.Добавить("КраткоеОписаниеПравила", ТипСтрока);
	Данные.Колонки.Добавить("НаименованиеЗадачи", ТипСтрока);
	Данные.Колонки.Добавить("НаименованиеНалога", ТипСтрока);
	Данные.Колонки.Добавить("ИдентификаторТребования", ТипСтрока);
	Данные.Колонки.Добавить("ЭтоТребованиеОтвет", ТипБулево);
	Данные.Колонки.Добавить("Срок", ТипДата);
	Данные.Колонки.Добавить("НачалоВыполнения", ТипДата);
	Данные.Колонки.Добавить("ПредставлениеПериодичности", ТипСтрока);
	Данные.Колонки.Добавить("КонецПериода", ТипДата);
	Данные.Колонки.Добавить("Статус", ТипСтрока);
	Данные.Колонки.Добавить("СтатусУстановленВручную", ТипБулево);
	Данные.Колонки.Добавить("ВАрхиве", ТипБулево);
	Данные.Колонки.Добавить("ЕстьЗадача", ТипБулево);
	Данные.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число", КвалификаторЧисла));
	Данные.Колонки.Добавить("Действие", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДействийКалендаряБухгалтера"));
	Данные.Колонки.Добавить("ПредставлениеДействия", ТипСтрока);
	Данные.Колонки.Добавить("Комментарий", ТипСтрока);
	Данные.Колонки.Добавить("ЭтоПлатеж", ТипБулево);
	Данные.Колонки.Добавить("СуммаПлатежа", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	Данные.Колонки.Добавить("ЕстьЗадолженность", ТипБулево);
	
	Возврат Данные;
	
КонецФункции

// Собирает данные приложений для формирования отчета.
//
// Параметры:
//  Параметры - Структура - см. НовыйПараметрыОтчета
//
// Возвращаемое значение:
//   Структура - см. НовыйДанныеДляОтчета.
//
Функция ДанныеДляОтчета(Параметры) Экспорт
	
	// Чтобы с максимальной вероятностью получить данные подключенных приложений с первого раза:
	// 1. Отправим запросы в подключенные приложения на формирование данных
	// 2. Получим данные этого приложения
	// 3. Опросим подключенные приложения
	
	ПодключенныеОрганизации = Новый ТаблицаЗначений;
	КалендарьОтчетностиПереопределяемый.ЗаполнитьПодключенныеОрганизации(ПодключенныеОрганизации);
	
	ОпрашиватьПриложения = ПодключенныеОрганизации.Количество() > 0;
	ДанныеПодключенныхПриложений = НовыйДанныеПодключенныхПриложений();
	
	Если Параметры.Организации.Количество() = 0 Тогда
		Параметры.Организации = ОрганизацииЭтогоПриложенияДляКалендаря();
	КонецЕсли;
	
	Если ОпрашиватьПриложения Тогда
		Запросы = ЗапроситьДанныеПодключенныхПриложений(ПодключенныеОрганизации, Параметры);
		ДанныеПодключенныхПриложений.ОшибкиПриложений = Запросы.Ошибки;
	КонецЕсли;
	
	ДанныеЭтогоПриложения = ДанныеПриложения(Параметры);
	
	Если ОпрашиватьПриложения Тогда
		Ответы = ОпроситьПодключенныеПриложения(Запросы.Выполнено);
		ДанныеПодключенныхПриложений.Данные = Ответы.Данные;
		ДанныеПодключенныхПриложений.ОжидающиеЗапросы = Ответы.ОжидающиеЗапросы;
		ДанныеПодключенныхПриложений.ОшибкиПриложений = Ответы.ОшибкиПриложений;
		ОтметитьДанныеПолучены(
			Запросы.Выполнено,
			ДанныеПодключенныхПриложений.ОжидающиеЗапросы,
			ДанныеПодключенныхПриложений.ОшибкиПриложений);
	КонецЕсли;
	
	КалендарьОтчетностиПереопределяемый.ДобавитьОрганизацииПодключенныхПриложений(
		ДанныеПодключенныхПриложений, ПодключенныеОрганизации);
	
	КоличествоСрочныхТребованийФНС = КоличествоСрочныхТребованийФНС(
		ДанныеЭтогоПриложения, ДанныеПодключенныхПриложений.Данные, Параметры.НаДату);
	КоличествоДокументовСЭДО = КоличествоДокументовСЭДО(
		ДанныеЭтогоПриложения, ДанныеПодключенныхПриложений.Данные, Параметры.НаДату);
	
	Результат = НовыйДанныеДляОтчета();
	
	Результат.ДанныеЭтогоПриложения = ДанныеЭтогоПриложения;
	Результат.ПодключенныеОрганизации = ПодключенныеОрганизации;
	Результат.ДанныеПодключенныхПриложений = ДанныеПодключенныхПриложений.Данные;
	Результат.КоличествоСрочныхТребованийФНС = КоличествоСрочныхТребованийФНС;
	Результат.КоличествоДокументовСЭДО = КоличествоДокументовСЭДО;
	Результат.ОжидающиеЗапросы = ДанныеПодключенныхПриложений.ОжидающиеЗапросы;
	Результат.ОшибкиПриложений = ДанныеПодключенныхПриложений.ОшибкиПриложений;
	
	Для Каждого Параметр Из Параметры Цикл
		Результат.Параметры.Вставить(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Опрашивает облачные приложения, которым был ранее отправлен запрос
// на получение данных. Формирует таблицу данных из ответов приложений.
//
// Параметры:
//  Запросы - ТаблицаЗначений - отправленные ранее запросы другим облачным приложениям,
//            см. СообщенияОтчетностиОблачныхПриложений.НовыйЗапросыОблачнымПриложениям().
//
// Возвращаемое значение:
//   Структура - 
//      *Ответы - ТаблицаЗначений - см. НовыйДанныеОтчетности()
//      *ОжидающиеЗапросы - ТаблицаЗначений - запросы, по которым не удалось получить ответ,
//                          см. СообщенияОтчетностиОблачныхПриложений.НовыйЗапросыОблачнымПриложениям()
//      *ОшибкиПриложений - Массив - список приложений, получение данных от которых более невозможно,
//                        опрашивать повторно их не следует.
//
Функция ОпроситьПодключенныеПриложения(Запросы) Экспорт
	
	ДанныеПриложений = Новый Структура("Данные,ОжидающиеЗапросы,ОшибкиПриложений");
	
	КалендарьОтчетностиПереопределяемый.ОпроситьПодключенныеПриложения(Запросы, ДанныеПриложений);
	
	Возврат ДанныеПриложений;
	
КонецФункции

// Подтверждает получение данных. Удаляет ответы в приложениях, от которых он был получен.
// 
// Параметры:
//   Запросы - ТаблицаЗначений - см. СообщенияОтчетностиОблачныхПриложений.НовыйЗапросыОблачнымПриложениям()
//   ОжидающиеЗапросы - ТаблицаЗначений - запросы, по которым не удалось получить ответ,
//                      см. СообщенияОтчетностиОблачныхПриложений.НовыйЗапросыОблачнымПриложениям()
//   ОшибкиПриложений - Массив - список приложений, получение данных от которых более невозможно,
//                    опрашивать повторно их не следует.
//
Процедура ОтметитьДанныеПолучены(Запросы, ОжидающиеЗапросы, ОшибкиПриложений) Экспорт
	
	КалендарьОтчетностиПереопределяемый.ОтметитьДанныеПолучены(Запросы, ОжидающиеЗапросы, ОшибкиПриложений);
	
КонецПроцедуры

// Возвращает состояние отчетной кампании этого приложения
//
// Параметры:
//  Параметры - Структура - см. НовыйПараметрыОтчета
//
// Возвращаемое значение:
//   Структура - см. НовыйДанныеОтчетности
//
Функция ДанныеПриложения(Параметры) Экспорт
	
	ДатаНачала = Параметры.ДатаНачала;
	ДатаКонца = Параметры.ДатаКонца;
	НаДату = Параметры.НаДату;
	Организации = Параметры.Организации;
	
	СобытияКалендаря = Новый Массив;
	Если Параметры.ПоОтчетам Тогда
		СобытияКалендаря.Добавить(Перечисления.ВидыДействийКалендаряБухгалтера.Отчет);
		СобытияКалендаря.Добавить(Перечисления.ВидыДействийКалендаряБухгалтера.ФинОтчетность);
	КонецЕсли;
	Если Параметры.ПоТребованиямФНС Тогда
		СобытияКалендаря.Добавить(Перечисления.ВидыДействийКалендаряБухгалтера.ТребованиеФНС);
	КонецЕсли;
	Если Параметры.ПоУведомлениямЕНС Тогда
		СобытияКалендаря.Добавить(Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление);
	КонецЕсли;
	Если Параметры.ПоДокументамСЭДО Тогда
		СобытияКалендаря.Добавить(Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО);
	КонецЕсли;
	Если Параметры.ПоЗарплате Тогда
		СобытияКалендаря.Добавить(Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата);
	КонецЕсли;
	
	// Расписание Списка задач во временном интервале формируется по дате возникновения события.
	// Формирование Календаря отчетности выполняется иначе - по сроку сдачи.
	// Например, у задачи по сдаче декларации по налогу на прибыль срок 25.03.2025, но период
	// события, к которому она относится, - 31.12.2024. Таким образом, период формирования расписания
	// следует брать с запасом. А только потом уже выполнять отбор по сроку сдачи.
	ПоследнийДеньПрошлогоГода = НачалоДня(НачалоГода(ДатаНачала) - 1);
	ПредпоследнийДеньПоследнегоМесяца = НачалоДня(НачалоДня(КонецМесяца(ДатаКонца)) - 1);
	
	Отбор = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ОтборПравилЗаполнения();
	Отбор.НачалоИнтервала = ПоследнийДеньПрошлогоГода;
	Отбор.КонецИнтервала = ПредпоследнийДеньПоследнегоМесяца;
	
	Расписание = НовыйРасписание();
	РасписаниеОрганизации = Неопределено;
	ФормироватьОтчетыУведомления = СобытияКалендаря.Найти(Перечисления.ВидыДействийКалендаряБухгалтера.Отчет) <> Неопределено
		Или СобытияКалендаря.Найти(Перечисления.ВидыДействийКалендаряБухгалтера.ФинОтчетность) <> Неопределено
		Или СобытияКалендаря.Найти(Перечисления.ВидыДействийКалендаряБухгалтера.Уведомление) <> Неопределено
		Или СобытияКалендаря.Найти(Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата) <> Неопределено;
	ФормироватьТребования = СобытияКалендаря.Найти(Перечисления.ВидыДействийКалендаряБухгалтера.ТребованиеФНС) <> Неопределено;
	ФормироватьДокументыСЭДО = СобытияКалендаря.Найти(Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО) <> Неопределено;
	
	Для каждого Организация Из Организации Цикл
		Отбор.Организация = Организация;
		
		Если ФормироватьОтчетыУведомления Тогда
			РасписаниеОрганизации = РегистрыСведений.ЗадачиБухгалтера.РасписаниеПоНалогамОтчетамЗаПериод(Отбор);
		КонецЕсли;
		
		Если РасписаниеОрганизации = Неопределено Или РасписаниеОрганизации.Количество() = 0 Тогда
			РасписаниеОрганизации = НовыйРасписание();
		КонецЕсли;
		
		Если ФормироватьТребования Тогда
			РегистрыСведений.ЗадачиБухгалтера.ЗаполнитьРасписаниеТребованияФНС(
				Организация,
				РасписаниеОрганизации,
				Отбор.НачалоИнтервала,
				Отбор.КонецИнтервала,
				Ложь);
		КонецЕсли;
		
		Если ФормироватьДокументыСЭДО Тогда
			РегистрыСведений.ЗадачиБухгалтера.ЗаполнитьРасписаниеДокументыСЭДО(
				Организация,
				РасписаниеОрганизации,
				Отбор.НачалоИнтервала,
				Отбор.КонецИнтервала);
		КонецЕсли;
		
		Для Каждого СтрокаРасписание Из РасписаниеОрганизации Цикл
			Если СобытияКалендаря.Найти(СтрокаРасписание.Действие) <> Неопределено
				И СтрокаРасписание.Срок <= ДатаКонца Тогда
				
				НоваяСтрока = Расписание.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасписание);
			КонецЕсли;
		КонецЦикла;
		РасписаниеОрганизации = Неопределено;
	КонецЦикла;
	
	// Расписание является плановым, т.е. задачи в регистре сведений "Список задач" могут отсутствовать.
	// Получим текущее состояние по задачам.
	ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач(Расписание);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации",            Организации);
	Запрос.УстановитьПараметр("Расписание",             Расписание);
	Запрос.УстановитьПараметр("ДатаНачала",             ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца",              ДатаКонца);
	Запрос.УстановитьПараметр("СобытияКалендаря",       СобытияКалендаря);
	Запрос.УстановитьПараметр("КодНалогаТребованиеФНС", ЗадачиБухгалтераКлиентСервер.КодНалогаТребованиеФНС());
	Запрос.УстановитьПараметр("КодЗадачиДокументСЭДО",  ЗадачиБухгалтераКлиентСервер.КодЗадачиДокументСЭДО());
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	Статусы = Новый Массив;
	Статусы.Добавить(СтатусыОтчета.ОтправьтеОтвет);
	Статусы.Добавить(СтатусыОтчета.ОтветОтправлен);
	Запрос.УстановитьПараметр("СтатусыОтвета", Статусы);
	Запрос.УстановитьПараметр("ДанныеПлатежей", ДанныеПлатежей(Параметры));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Расписание.Организация КАК Организация,
	|	Расписание.РегистрацияВНалоговомОргане КАК РегистрацияФНС,
	|	Расписание.Правило КАК Правило,
	|	Расписание.Наименование КАК НаименованиеЗадачи,
	|	Расписание.НаименованиеСокращенное КАК НаименованиеНалога,
	|	Расписание.Срок КАК Срок,
	|	Расписание.НачалоВыполнения КАК НачалоВыполнения,
	|	Расписание.ПериодСобытия КАК КонецПериода,
	|	Расписание.Периодичность КАК Периодичность,
	|	Расписание.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление) КАК ЭтоУведомление,
	|	Расписание.Действие КАК Действие,
	|	Расписание.Статус КАК Статус,
	|	Расписание.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Расписание.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ТаблицаРасписание
	|ИЗ
	|	&Расписание КАК Расписание
	|ГДЕ
	|	Расписание.Срок МЕЖДУ &ДатаНачала И &ДатаКонца
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПлатежей.Организация КАК Организация,
	|	ДанныеПлатежей.Правило КАК Правило,
	|	ДанныеПлатежей.Срок КАК Срок,
	|	ДанныеПлатежей.СуммаПлатежа КАК СуммаПлатежа,
	|	ДанныеПлатежей.Статус КАК Статус,
	|	ДанныеПлатежей.ЕстьЗадолженность КАК ЕстьЗадолженность
	|ПОМЕСТИТЬ ДанныеПлатежей
	|ИЗ
	|	&ДанныеПлатежей КАК ДанныеПлатежей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Расписание.Организация КАК Организация,
	|	NULL КАК Приложение,
	|	Расписание.Организация.Наименование КАК НаименованиеОрганизации,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(36)) КАК ИдентификаторОрганизации,
	|	Расписание.РегистрацияФНС КАК РегистрацияФНС,
	|	ЕСТЬNULL(Расписание.РегистрацияФНС.Код, """") КАК КодФНС,
	|	Расписание.Правило КАК Правило,
	|	Расписание.Правило.Код КАК ИдентификаторПравила,
	|	ЕСТЬNULL(Расписание.Правило.Владелец.РеквизитДопУпорядочивания, 0) КАК Порядок,
	|	ЕСТЬNULL(Расписание.Правило.Владелец.Код, """") КАК КодНалога,
	|	Расписание.НаименованиеЗадачи КАК НаименованиеЗадачи,
	|	Расписание.НаименованиеНалога КАК НаименованиеНалога,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(36)) КАК ИдентификаторТребования,
	|	ВЫБОР
	|		КОГДА Расписание.Правило ССЫЛКА Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов
	|				И Расписание.Статус В (&СтатусыОтвета)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоТребованиеОтвет,
	|	Расписание.Срок КАК Срок,
	|	Расписание.НачалоВыполнения КАК НачалоВыполнения,
	|	ПРЕДСТАВЛЕНИЕ(Расписание.Периодичность) КАК ПредставлениеПериодичности,
	|	Расписание.КонецПериода КАК КонецПериода,
	|	Расписание.ЭтоУведомление КАК ЭтоУведомление,
	|	Расписание.КраткоеОписаниеПравила КАК КраткоеОписаниеПравила,
	|	Расписание.Статус КАК Статус,
	|	Расписание.ВАрхиве КАК ВАрхиве,
	|	Расписание.ЕстьЗадача КАК ЕстьЗадача,
	|	Расписание.Действие КАК Действие,
	|	Расписание.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|	Расписание.Комментарий КАК Комментарий,
	|	Расписание.ЭтоПлатеж КАК ЭтоПлатеж,
	|	Расписание.СуммаПлатежа КАК СуммаПлатежа,
	|	Расписание.ЕстьЗадолженность КАК ЕстьЗадолженность
	|ИЗ
	|	(ВЫБРАТЬ
	|		Расписание.Организация КАК Организация,
	|		Расписание.Организация.Наименование КАК НаименованиеОрганизации,
	|		Расписание.РегистрацияФНС КАК РегистрацияФНС,
	|		Расписание.Правило КАК Правило,
	|		Расписание.НаименованиеЗадачи КАК НаименованиеЗадачи,
	|		Расписание.НаименованиеНалога КАК НаименованиеНалога,
	|		Расписание.Срок КАК Срок,
	|		Расписание.НачалоВыполнения КАК НачалоВыполнения,
	|		Расписание.Периодичность КАК Периодичность,
	|		Расписание.КонецПериода КАК КонецПериода,
	|		Расписание.ЭтоУведомление КАК ЭтоУведомление,
	|		Расписание.ЭтоУведомление
	|			И ЕСТЬNULL(Регламент.ВыполняетсяЕдинымПомощником, ИСТИНА) КАК ПодчиненнаяЗадача,
	|		ЕСТЬNULL(Регламент.Описание, """") КАК КраткоеОписаниеПравила,
	|		Расписание.Действие КАК Действие,
	|		Расписание.СтатусУстановленВручную КАК СтатусУстановленВручную,
	|		Расписание.Комментарий КАК Комментарий,
	|		Расписание.Статус КАК Статус,
	|		ЕстьNULL(ЗадачиБухгалтера.ВАрхиве, ЛОЖЬ) КАК ВАрхиве,
	|		НЕ ЗадачиБухгалтера.Организация ЕСТЬ NULL КАК ЕстьЗадача,
	|		ЛОЖЬ КАК ЭтоПлатеж,
	|		0 КАК СуммаПлатежа,
	|		ЛОЖЬ КАК ЕстьЗадолженность
	|	ИЗ
	|		ТаблицаРасписание КАК Расписание
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Регламент
	|			ПО Расписание.Правило = Регламент.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов КАК ДокументыРеализацииПолномочийНалоговыхОрганов
	|			ПО Расписание.Правило = ДокументыРеализацииПолномочийНалоговыхОрганов.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТранспортноеСообщение КАК ТранспортноеСообщение
	|			ПО Расписание.Правило = ТранспортноеСообщение.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|			ПО Расписание.Организация = ЗадачиБухгалтера.Организация
	|				И Расписание.Правило = ЗадачиБухгалтера.Правило
	|				И Расписание.КонецПериода = ЗадачиБухгалтера.ПериодСобытия
	|				И Расписание.РегистрацияФНС = ЗадачиБухгалтера.РегистрацияВНалоговомОргане
	|				И Расписание.Действие = ЗадачиБухгалтера.Действие
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗадачиБухгалтера.Организация,
	|		ЗадачиБухгалтера.Организация.Наименование,
	|		ЗадачиБухгалтера.РегистрацияВНалоговомОргане,
	|		ЗадачиБухгалтера.Правило,
	|		ЗадачиБухгалтера.Наименование,
	|		ЗадачиБухгалтера.НаименованиеСокращенное,
	|		ЗадачиБухгалтера.Срок,
	|		ЗадачиБухгалтера.НачалоВыполнения,
	|		ЗадачиБухгалтера.Периодичность,
	|		ЗадачиБухгалтера.ПериодСобытия,
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		"""",
	|		ЗадачиБухгалтера.Действие,
	|		ЗадачиБухгалтера.СтатусУстановленВручную,
	|		ЗадачиБухгалтера.Комментарий,
	|		ВЫБОР КОГДА ЗадачиБухгалтера.Статус = """" ТОГДА
	|			ЕСТЬNULL(ДанныеПлатежей.Статус, """")
	|		ИНАЧЕ
	|			ЗадачиБухгалтера.Статус
	|		КОНЕЦ,
	|		ЗадачиБухгалтера.ВАрхиве,
	|		ИСТИНА,
	|		ИСТИНА,
	|		ЕСТЬNULL(ДанныеПлатежей.СуммаПлатежа, 0),
	|		ЕСТЬNULL(ДанныеПлатежей.ЕстьЗадолженность, ЛОЖЬ)
	|	ИЗ
	|		РегистрСведений.ЗадачиБухгалтера КАК ЗадачиБухгалтера
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаПредставленияОтчетовУплатыНалогов КАК Регламент
	|			ПО ЗадачиБухгалтера.Правило = Регламент.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПлатежей КАК ДанныеПлатежей
	|			ПО ЗадачиБухгалтера.Организация = ДанныеПлатежей.Организация
	|				И ЗадачиБухгалтера.Правило = ДанныеПлатежей.Правило
	|				И ЗадачиБухгалтера.Срок = ДанныеПлатежей.Срок
	|	ГДЕ
	|		ЗадачиБухгалтера.Организация В(&Организации)
	|		И ЗадачиБухгалтера.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога)
	|		И ЗадачиБухгалтера.Срок МЕЖДУ &ДатаНачала И &ДатаКонца
	|		И НЕ ЕСТЬNULL(Регламент.ВыполняетсяЕдинымПомощником, ЛОЖЬ)) КАК Расписание
	|ГДЕ
	|	НЕ Расписание.ПодчиненнаяЗадача";
	
	КалендарьОтчетностиПереопределяемый.ПриУстановкеТекстаЗапросаДанныеПриложения(Запрос.Текст);
	
	Расписание = Запрос.Выполнить().Выгрузить();
	
	СместитьСрокиВыполненияЗадач(Расписание, НаДату);
	
	Для Каждого Строка Из Расписание Цикл
		
		Строка.ИдентификаторОрганизации = XMLСтрока(Строка.Организация.УникальныйИдентификатор());
		Если ТипЗнч(Строка.Правило) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов")
			Или ВыполнениеЗадачБухгалтераКлиентСервер.ЭтоДокументСЭДО(Строка.Правило) Тогда
			Строка.ИдентификаторТребования = XMLСтрока(Строка.Правило.УникальныйИдентификатор());
		КонецЕсли;
	КонецЦикла;
	
	Возврат Расписание;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДанныеОтчета

Функция ЗапроситьДанныеПодключенныхПриложений(Организации, Параметры)
	
	Результат = Новый Структура("Выполнено,Ошибки", Новый Массив, Новый Массив);
	
	КалендарьОтчетностиПереопределяемый.ЗапроситьДанныеПодключенныхПриложений(Организации, Параметры, Результат);
	
	Возврат Результат;
	
КонецФункции

Функция СводнаяТаблицаДанныхПриложений(ДанныеЭтогоПриложения, ДанныеПодключенныхПриложений, Организация)
	
	// Основное расписание задач.
	Расписание = НовыйДанныеОтчетности();
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеЭтогоПриложения, Расписание);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеПодключенныхПриложений, Расписание);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Расписание = Расписание.Скопировать(Новый Структура("Организация", Организация));
	КонецЕсли;
	
	Расписание.Сортировать("Приложение,Организация,Срок,РегистрацияФНС,Порядок,КодНалога");
	
	// Налоги организаций. Используется для формирования отчета с налогами "по горизонтали".
	НалогиОрганизаций = Расписание.Скопировать();
	НалогиОрганизаций.Свернуть(
		"Организация,Приложение,Правило,ИдентификаторПравила,КраткоеОписаниеПравила,ЭтоТребованиеОтвет,КодНалога,РегистрацияФНС,КодФНС,НаименованиеНалога,Порядок,
		|ЭтоПлатеж,СуммаПлатежа,ЕстьЗадолженность");
	НалогиОрганизаций.Сортировать("Организация,РегистрацияФНС,Порядок,КодНалога,НаименованиеНалога");
	
	// Налоги по срокам. Используется для формирования отчета с налогами "по вертикали".
	НалогиПоСрокам = Расписание.Скопировать();
	НалогиПоСрокам.Свернуть(
		"РегистрацияФНС,КодФНС,КодНалога,НаименованиеНалога,Срок,ИдентификаторПравила,КраткоеОписаниеПравила,ЭтоТребованиеОтвет,Порядок,ЭтоПлатеж");
	НалогиПоСрокам.Сортировать("Срок,РегистрацияФНС,Порядок,КодНалога,НаименованиеНалога");
	
	// Налоги по правилу.
	// Если отчет формируется по одной организации, то сразу несколько налогов с одинаковым кодом могут
	// быть в одном периоде(cроке). В таком случае записи нужно детализировать по "правилу".
	КопияРасписания = Расписание.Скопировать();
	КопияРасписания.Колонки.Добавить("ЧислоСтрок");
	Для Каждого СтрокаДанных Из КопияРасписания Цикл
		СтрокаДанных.ЧислоСтрок = 1;
	КонецЦикла;
	КопияРасписания.Свернуть("КодНалога,РегистрацияФНС,Срок", "ЧислоСтрок");
	НалогиПоПравилу = КопияРасписания.Скопировать();
	НалогиПоПравилу.Очистить();
	Для Каждого СтрокаДанных Из КопияРасписания Цикл
		Если СтрокаДанных.ЧислоСтрок > 1 Или ДетализироватьНалогПоПравилу(СтрокаДанных.КодНалога) Тогда
			СтрокаНалоги = НалогиПоПравилу.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНалоги, СтрокаДанных);
		КонецЕсли;
	КонецЦикла;
	
	// Список организаций с приложениями.
	Организации = Расписание.Скопировать();
	Организации.Свернуть("Приложение,Организация");
	Организации.Сортировать("Приложение,Организация");
	
	Данные = НовыйРасписаниеПоНалогам();
	Данные.Расписание = Расписание;
	Данные.Расписание.Индексы.Добавить("Организация,Срок,РегистрацияФНС,КодНалога");
	
	Данные.НалогиОрганизаций = НалогиОрганизаций;
	Данные.НалогиОрганизаций.Индексы.Добавить("Организация,КодНалога");
	
	Данные.НалогиПоСрокам = НалогиПоСрокам;
	Данные.НалогиПоСрокам.Индексы.Добавить("Срок,КодНалога");
	
	Данные.НалогиПоПравилу = НалогиПоПравилу;
	
	Данные.Организации = Организации;
	
	Возврат Данные;
	
КонецФункции

Процедура СместитьСрокиВыполненияЗадач(Расписание, НаДату)
	
	Завтра = НаДату + 86400;
	// Не ограничиваем срок выполнения для задач, у которых по расписанию списка задач
	// всего один день на подготовку(например, отчет о сотруднике, принятом на работу).
	// Помощник подготовки отчета должен быть доступен уже сегодня.
	Для Каждого СтрокаРасписания Из Расписание Цикл
		НачалоВыполнения = СтрокаРасписания.НачалоВыполнения;
		Если НачалоВыполнения = СтрокаРасписания.Срок И НачалоВыполнения = Завтра Тогда
			СтрокаРасписания.НачалоВыполнения = НаДату;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйРасписаниеПоНалогам()
	
	Данные = Новый Структура;
	Данные.Вставить("Расписание");
	Данные.Вставить("НалогиОрганизаций");
	Данные.Вставить("НалогиПоСрокам");
	Данные.Вставить("Сроки");
	Данные.Вставить("Организации");
	Данные.Вставить("НалогиПоПравилу");

	Возврат Данные;
	
КонецФункции

Функция НовыйДанныеДляОтчета()
	
	Данные = Новый Структура;
	Данные.Вставить("ДанныеЭтогоПриложения"); //см. НовыйДанныеОтчетности()
	Данные.Вставить("ПодключенныеОрганизации"); //см. РегистрыСведений.ОрганизацииОтчетностиОблачныхПриложений.Организации()
	Данные.Вставить("ДанныеПодключенныхПриложений"); //см. НовыйДанныеОтчетности()
	Данные.Вставить("КоличествоСрочныхТребованийФНС"); // требующие скорого подтверждения и скорого ответа требования ФНС
	Данные.Вставить("КоличествоДокументовСЭДО"); // требующие скорого подтверждения и скорого ответа документы СЭДО
	Данные.Вставить("ОжидающиеЗапросы"); //см. СообщенияОтчетностиОблачныхПриложений.НовыйЗапросыОблачнымПриложениям()
	Данные.Вставить("ОшибкиПриложений"); // массив из СправочникСсылка.ОблачныеПриложения
	Данные.Вставить("Параметры", Новый Структура); // параметры отбора, по которым были получены данные
	
	Возврат Данные;
	
КонецФункции

Функция НовыйДанныеПодключенныхПриложений()
	
	// Данные - итоговая таблица данных подключенных приложений, по которой будет формироваться отчет.
	// При её заполнении записи не разделяются на "c ошибкой" и "ожидает результата"
	// Т.е. здесь "ошибки" - есть то же, что и "безрезультатные". Как для запросов с ошибкой, так и для
	// ожидающих запросов добавляются пустые строки с организациями приложений.
	//
	// Ошибки приложений - это список приложений, запросы к которым не удалось выполнить.
	// Т.е. приложение по какой-то причине недоступно пользователю. Например, владелец абонента его удалил.
	//
	// Ожидающие запросы - запросы, что были успешно отправлены приложениям, но в рамках текущей
	// итерации опроса приложений на них ещё не удалось получить ответы. Но ответы будут получены позже.
	
	Данные = Новый Структура;
	Данные.Вставить("Данные", НовыйДанныеОтчетности());
	Данные.Вставить("ОшибкиПриложений", Новый Массив);
	Данные.Вставить("ОжидающиеЗапросы", Новый ТаблицаЗначений);
	
	КалендарьОтчетностиПереопределяемый.ИнициализироватьДанныеПодключенныхПриложений(Данные);
	
	Возврат Данные;
	
КонецФункции

// Сроки по задачам, разделенные на несколько групп.
// Все сроки в группах упорядочены по дате.
//
// Параметры:
//  Расписание - ТаблицаЗначений - см. РасписаниеЗадач
//  НаДату - Дата - дата, относительно которой сроки делятся по группам.
// 
// Возвращаемое значение:
//   - Структура
//     * Все - Массив из Дата - все сроки из расписания.
//     * Прошлое - Массив из Дата - прошедшие сроки.
//     * СовсемСкоро - Массив из Дата - ближайшие сроки, по которым можно выполнять все задачи.
//          Может быть 1 или несколько таких сроков. Например, если сегодня 1 июня, это могут быть 25 и 29 июня.
//          Если сегодня 25 июня, то это могут быть 25 и 29 июня.
//     * Скоро - Массив из Дата -ближайший срок, по которому есть задачи, которые нельзя выполнять.
//          Заполняется, только если нет сроков в списке СовсемСкоро.
//          Может быть только 1 такой срок. Например, если сегодня 30 мая, это будет 25 июня.
//     * Будущее - Массив из Дата - будущие сроки, которые не вошли в группы СовсемСкоро или Скоро. Может содержать
//          задачи, которые можно выполнять.
//
Функция СрокиПоЗадачам(Данные, НаДату)
	
	Сроки = НовыйСрокиПоЗадачам();
	Расписание = Данные.Расписание;
	НалогиПоСрокам = Данные.НалогиПоСрокам;
	
	ВсеСроки = ОбщегоНазначения.ВыгрузитьКолонку(НалогиПоСрокам, "Срок", Истина);
	Для Каждого Срок Из ВсеСроки Цикл
		Если ЗначениеЗаполнено(Срок) Тогда
			Сроки.Все.Добавить(Срок);
		КонецЕсли;
	КонецЦикла;
	
	ПроверятьВыполнение = Истина;
	Для каждого Срок Из Сроки.Все Цикл
		Если Срок < НаДату Тогда
			Сроки.Прошлое.Добавить(Срок);
		ИначеЕсли ПроверятьВыполнение Тогда
			МожноВыполнять = Истина;
			ОтборПоСроку = Новый Структура("Срок", Срок);
			ЗадачиПоСроку = Расписание.НайтиСтроки(ОтборПоСроку);
			Для каждого Задача Из ЗадачиПоСроку Цикл
				Если Задача.НачалоВыполнения > НаДату Тогда
					// Задачу нельзя выполнять.
					МожноВыполнять = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если МожноВыполнять Тогда
				Сроки.СовсемСкоро.Добавить(Срок);
			Иначе
				Если Не ЗначениеЗаполнено(Сроки.СовсемСкоро) Тогда
					Сроки.Скоро.Добавить(Срок);
				Иначе
					Сроки.Будущее.Добавить(Срок);
				КонецЕсли;
				ПроверятьВыполнение = Ложь;
			КонецЕсли;
		Иначе
			Сроки.Будущее.Добавить(Срок);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Сроки;
	
КонецФункции

Функция НовыйСрокиПоЗадачам()
	
	Сроки = Новый Структура;
	Сроки.Вставить("Все", Новый Массив);
	Сроки.Вставить("Прошлое", Новый Массив);
	Сроки.Вставить("СовсемСкоро", Новый Массив);
	Сроки.Вставить("Скоро", Новый Массив);
	Сроки.Вставить("Будущее", Новый Массив);
	
	Возврат Сроки;
	
КонецФункции

Функция ОрганизацииЭтогоПриложенияДляКалендаря()
	
	ВсеОрганизации = Новый Массив;
	КалендарьОтчетностиПереопределяемый.ЗаполнитьДоступныеОрганизацииПоRLS(ВсеОрганизации);
	
	УстановитьПривилегированныйРежим(Истина);
	ОтключенныеОрганизацииЭтогоПриложения = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		Пользователи.ТекущийПользователь(),
		КалендарьОтчетностиКлиентСервер.КлючХраненияСпискаОтключенныхОрганизаций());
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТипЗнч(ОтключенныеОрганизацииЭтогоПриложения) <> Тип("Массив") Тогда
		Возврат ВсеОрганизации;
	КонецЕсли;
	
	Возврат ОбщегоНазначенияКлиентСервер.РазностьМассивов(ВсеОрганизации, ОтключенныеОрганизацииЭтогоПриложения);
	
КонецФункции

Функция КоличествоСрочныхТребованийФНС(ДанныеЭтогоПриложения, ДанныеПодключенныхПриложений, НаДату) Экспорт
	
	Расписания = Новый Массив;
	Расписания.Добавить(ДанныеЭтогоПриложения);
	Расписания.Добавить(ДанныеПодключенныхПриложений);
	
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	Статусы = Новый Массив;
	Статусы.Добавить(СтатусыОтчета.ПодтвердитеПрием);
	Статусы.Добавить(СтатусыОтчета.ОтправьтеОтвет);
	
	Подтвердить = 0;
	БезОтвета = 0;
	
	Для Каждого Расписание Из Расписания Цикл
		Для Каждого СтрокаРасписание Из Расписание Цикл
			Если СтрокаРасписание.КодНалога <> ЗадачиБухгалтераКлиентСервер.КодНалогаТребованиеФНС()
				Или Статусы.Найти(СтрокаРасписание.Статус) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// Отбираем требования, по которым до срока подтверждения или ответа осталось 2 дня или меньше, совсем просроченные (более 60 дней) не рассматриваем.
			// см. ТребованияФНС.ТребующиеСкорогоПодтверждения, ТребованияФНС.ТребующиеСкорогоОтвета
			РазностьДней = ТребованияФНС.РазностьДатПоКалендарю(НаДату, СтрокаРасписание.Срок).Дельта;
			Если РазностьДней <= 2 И РазностьДней >-60 Тогда
				Если СтрокаРасписание.Статус = СтатусыОтчета.ПодтвердитеПрием Тогда
					Подтвердить = Подтвердить + 1;
				КонецЕсли;
				Если СтрокаРасписание.Статус = СтатусыОтчета.ОтправьтеОтвет Тогда
					БезОтвета = БезОтвета + 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Подтвердить = 0 И БезОтвета = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КоличествоСрочныхТребованийФНС = Новый Структура();
	КоличествоСрочныхТребованийФНС.Вставить("Подтвердить", Подтвердить);
	КоличествоСрочныхТребованийФНС.Вставить("БезОтвета",   БезОтвета);
	Возврат КоличествоСрочныхТребованийФНС;
	
КонецФункции

Функция КоличествоДокументовСЭДО(ДанныеЭтогоПриложения, ДанныеПодключенныхПриложений, НаДату) Экспорт
	
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	Статусы = Новый Массив;
	Статусы.Добавить(СтатусыОтчета.ПодтвердитеПрием);
	Статусы.Добавить(СтатусыОтчета.ОтправьтеОтвет);
	
	Расписания = Новый Массив;
	Расписания.Добавить(ДанныеЭтогоПриложения);
	Расписания.Добавить(ДанныеПодключенныхПриложений);
	
	Подтвердить = 0;
	БезОтвета = 0;
	
	Для Каждого Расписание Из Расписания Цикл
		Для Каждого СтрокаРасписание Из Расписание Цикл
			Если СтрокаРасписание.КодНалога <> ЗадачиБухгалтераКлиентСервер.КодЗадачиДокументСЭДО()
				Или Статусы.Найти(СтрокаРасписание.Статус) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			РазностьДней = ТребованияФНС.РазностьДатПоКалендарю(НаДату, СтрокаРасписание.Срок).Дельта;
			Если РазностьДней >-60 Тогда
				Если СтрокаРасписание.Статус = СтатусыОтчета.ПодтвердитеПрием Тогда
					Подтвердить = Подтвердить + 1;
				КонецЕсли;
				Если СтрокаРасписание.Статус = СтатусыОтчета.ОтправьтеОтвет Тогда
					БезОтвета = БезОтвета + 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Подтвердить = 0 И БезОтвета = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КоличествоДокументовСЭДО = Новый Структура();
	КоличествоДокументовСЭДО.Вставить("Подтвердить", Подтвердить);
	КоличествоДокументовСЭДО.Вставить("БезОтвета",   БезОтвета);
	
	Возврат КоличествоДокументовСЭДО;
	
КонецФункции

Функция НовыйДанныеПлатежей()
	
	ОписаниеТиповСчетУчета = Неопределено;
	КалендарьОтчетностиПереопределяемый.ПриОпределенииОписанияТиповСчетУчета(ОписаниеТиповСчетУчета);
	
	ДанныеПлатежей = Новый ТаблицаЗначений;
	ДанныеПлатежей.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДанныеПлатежей.Колонки.Добавить("СчетУчета", ОписаниеТиповСчетУчета);
	ДанныеПлатежей.Колонки.Добавить("Правило", Новый ОписаниеТипов("СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов"));
	ДанныеПлатежей.Колонки.Добавить("Срок", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ДанныеПлатежей.Колонки.Добавить("СуммаПлатежа", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ДанныеПлатежей.Колонки.Добавить("ЕстьЗадолженность", Новый ОписаниеТипов("Булево"));
	ДанныеПлатежей.Колонки.Добавить("Статус", ОбщегоНазначения.ОписаниеТипаСтрока(20));
	
	Возврат ДанныеПлатежей;
	
КонецФункции

Функция ДанныеПлатежей(Параметры)
	
	ДанныеПлатежей = НовыйДанныеПлатежей();
	
	КалендарьОтчетностиПереопределяемый.ЗаполнитьДанныеПлатежей(ДанныеПлатежей, Параметры);
	
	Возврат ДанныеПлатежей;
	
КонецФункции

#КонецОбласти

#Область ФормированиеОтчета

Процедура ВывестиЗаголовок(Результат, Данные, ПараметрыВывода)
	
	Сроки = Данные.Сроки;
	НалогиПоСрокам = Данные.НалогиПоСрокам;
	Области = ПараметрыВывода.Области;
	НаДату = ПараметрыВывода.НаДату;
	ПоказатьОрганизации = ПараметрыВывода.ПоказатьОрганизации;
	
	// Организация и налог
	Если ПоказатьОрганизации Тогда
		Результат.Вывести(Области.ЗаголовокОрганизация);
	Иначе
		Результат.Вывести(Области.ЗаголовокНалог);
	КонецЕсли;
	
	Если ПараметрыВывода.ОжидаетсяЗагрузкаДанных Или ПараметрыВывода.ЕстьОшибкиЗапросов Тогда
		Результат.Присоединить(Области.ЗаголовокПолучениеДанных);
	КонецЕсли;
	
	// Прошлое
	Если ЗначениеЗаполнено(Сроки.Прошлое) Тогда
		ЧислоКолонок = ?(ПоказатьОрганизации,
			ЧислоКолонокНалоговПоВертикали(НалогиПоСрокам, Сроки.Прошлое),
			Сроки.Прошлое.Количество());
			
		ТекстыПрошлое = Новый Массив(ЧислоКолонок);
		ТекстыПрошлое[0] = НСтр("ru='Прошлые сроки сдачи'");
		ВывестиПериодЗаголовка(Результат, ТекстыПрошлое, Области.ЗаголовокСрок, ЦветаСтиля.СерыйФонШапкиОтчета, Истина);
	КонецЕсли;
	
	// "Совсем скоро"
	Для каждого Срок Из Сроки.СовсемСкоро Цикл
		ТекстыСовсемСкоро = Новый Массив;
		Если ПоказатьОрганизации Тогда
			СрокиСовсемСкоро = Новый Массив();
			СрокиСовсемСкоро.Добавить(Срок);
			ЧислоКолонок = ЧислоКолонокНалоговПоВертикали(НалогиПоСрокам, СрокиСовсемСкоро);
			ТекстыСовсемСкоро = Новый Массив(ЧислоКолонок);
			ТекстыСовсемСкоро[0] = ТекстСкоро(Срок, НаДату);
			ВывестиПериодЗаголовка(Результат, ТекстыСовсемСкоро, Области.ЗаголовокСрок, ЦветаСтиля.ЦветВажного, Истина);
		Иначе
			ТекстыСовсемСкоро.Добавить(ТекстСкоро(Срок, НаДату));
			ВывестиПериодЗаголовка(Результат, ТекстыСовсемСкоро, Области.ЗаголовокСрок, ЦветаСтиля.ЦветВажного, Ложь);
		КонецЕсли;
	КонецЦикла;
	
	// "Скоро"
	Для каждого Срок Из Сроки.Скоро Цикл
		ТекстыСкоро = Новый Массив;
		Если ПоказатьОрганизации Тогда
			СрокиСкоро = Новый Массив();
			СрокиСкоро.Добавить(Срок);
			ЧислоКолонок = ЧислоКолонокНалоговПоВертикали(НалогиПоСрокам, СрокиСкоро);
			ТекстыСкоро = Новый Массив(ЧислоКолонок);
			ТекстыСкоро[0] = ТекстСкоро(Срок, НаДату);
			ВывестиПериодЗаголовка(Результат, ТекстыСкоро, Области.ЗаголовокБудущее, ЦветаСтиля.ЦветВажного, Истина);
		Иначе
			ТекстыСкоро.Добавить(ТекстСкоро(Срок, НаДату));
			ВывестиПериодЗаголовка(Результат, ТекстыСкоро, Области.ЗаголовокБудущее, ЦветаСтиля.ЦветВажного, Ложь);
		КонецЕсли;
	КонецЦикла;
	
	// Будущее
	Если ЗначениеЗаполнено(Сроки.Будущее) Тогда
		ЧислоКолонок = ?(ПоказатьОрганизации,
			ЧислоКолонокНалоговПоВертикали(НалогиПоСрокам, Сроки.Будущее),
			Сроки.Будущее.Количество());
			
		ТекстыБудущее = Новый Массив(ЧислоКолонок);
		ТекстыБудущее[0] = НСтр("ru='Будущие сроки сдачи'");
		ВывестиПериодЗаголовка(Результат, ТекстыБудущее, Области.ЗаголовокБудущее, ЦветаСтиля.СерыйФонШапкиОтчета, Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ЧислоКолонокНалоговПоВертикали(НалогиПоСрокам, СрокиСдачи)
	
	ЧислоКолонок = 0;
	Для Каждого СтрокаНалогиПоСрокам Из НалогиПоСрокам Цикл
		Если СрокиСдачи.Найти(СтрокаНалогиПоСрокам.Срок) <> Неопределено Тогда
			ЧислоКолонок = ЧислоКолонок + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЧислоКолонок;
КонецФункции

Процедура ВывестиПериодЗаголовка(Результат, ТекстыПериода, Область, ЦветФона, Объединить)
	
	Если Не ЗначениеЗаполнено(ТекстыПериода) Тогда
		Возврат;
	КонецЕсли;
	
	Область.Область().ЦветФона = ЦветФона;
	Для каждого ТекстПериода Из ТекстыПериода Цикл
		ПараметрыОбласти = Новый Структура("ТекстПериода", ТекстПериода);
		Область.Параметры.Заполнить(ПараметрыОбласти);
		Результат.Присоединить(Область);
	КонецЦикла;
	
	Если Не Объединить Тогда
		Возврат;
	КонецЕсли;
	
	ПоследняяСтрока = Результат.ПолучитьОбласть(Результат.ВысотаТаблицы, , Результат.ВысотаТаблицы, );
	Конец = ПоследняяСтрока.ШиринаТаблицы;
	Ширина = Область.ШиринаТаблицы * ТекстыПериода.Количество();
	Начало = Конец - Ширина + 1;
	ВысотаОбласти = Область.ВысотаТаблицы;
	Для Номер = 1 По ВысотаОбласти Цикл
		НомерСтроки = Результат.ВысотаТаблицы - Номер + 1;
		ОбластьПериода = Результат.Область(НомерСтроки, Начало, НомерСтроки, Конец);
		ОбластьПериода.Объединить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСрокиНалоги(Результат, Ячейки, Область, ИмяПараметра)
	
	Если Не ЗначениеЗаполнено(Ячейки) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Ячейка Из Ячейки Цикл
		ПараметрыОбласти = Новый Структура(ИмяПараметра, Ячейка);
		Область.Параметры.Заполнить(ПараметрыОбласти);
		Результат.Присоединить(Область);
	КонецЦикла;
	
	ПоследняяСтрока = Результат.ПолучитьОбласть(Результат.ВысотаТаблицы, , Результат.ВысотаТаблицы, );
	Конец = ПоследняяСтрока.ШиринаТаблицы;
	Ширина = Область.ШиринаТаблицы * Ячейки.Количество();
	Начало = Конец - Ширина + 1;
	ВысотаОбласти = Область.ВысотаТаблицы;
	Для Номер = 1 По ВысотаОбласти Цикл
		НомерСтроки = Результат.ВысотаТаблицы - Номер + 1;
		ОбластьПериода = Результат.Область(НомерСтроки, Начало, НомерСтроки, Конец);
		ОбластьПериода.Объединить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокуСроков(Результат, Данные, ПараметрыВывода)
	
	Сроки = Данные.Сроки;
	Области = ПараметрыВывода.Области;
	ПоказатьОрганизации = ПараметрыВывода.ПоказатьОрганизации;
	Расписание = Неопределено;
	
	Если ПоказатьОрганизации Тогда
		Расписание = Данные.Расписание.Скопировать();
		Расписание.Свернуть("ИдентификаторПравила, КодНалога, РегистрацияФНС, ЭтоТребованиеОтвет, Срок");
		Результат.Вывести(Области.ДатыОрганизация);
		Если ПараметрыВывода.ОжидаетсяЗагрузкаДанных Или ПараметрыВывода.ЕстьОшибкиЗапросов Тогда
			Результат.Присоединить(Области.ДатыПолучениеДанных);
		КонецЕсли;
	Иначе
		Результат.Вывести(Области.ДатыНалог);
	КонецЕсли;

	Для каждого Срок Из Сроки.Все Цикл
		ПараметрыОбласти = Новый Структура("Срок", Срок);
		Если Сроки.Прошлое.Найти(Срок) <> Неопределено 
			Или Сроки.СовсемСкоро.Найти(Срок) <> Неопределено Тогда
			ТекОбласть = Области.ДатыСрок;
		Иначе
			ТекОбласть = Области.ДатыБудущее;
		КонецЕсли;
		
		Если ПоказатьОрганизации Тогда
			ЧислоКолонок = Расписание.НайтиСтроки(ПараметрыОбласти).Количество();
			ЯчейкиСроков = Новый Массив(ЧислоКолонок);
			ЯчейкиСроков[0] = Срок;
			ВывестиСрокиНалоги(Результат, ЯчейкиСроков, ТекОбласть, "Срок");
		Иначе
			ТекОбласть.Параметры.Заполнить(ПараметрыОбласти);
			Результат.Присоединить(ТекОбласть);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСтрокуНалогов(Результат, Данные, ПараметрыВывода)
	
	Сроки = Данные.Сроки;
	НалогиПоСрокам = Данные.НалогиПоСрокам;
	Области = ПараметрыВывода.Области;
	МногоОрганизаций = ПараметрыВывода.МногоОрганизаций;

	ЧислоСекторовСтатуса = 8;
	Если ПараметрыВывода.ОжидаетсяЗагрузкаДанных Или ПараметрыВывода.ЕстьОшибкиЗапросов Тогда
		Результат.Присоединить(Области.НалогПолучениеДанных);
	КонецЕсли;
	МаксимальнаяДлинаСтроки = 24; // Число символов, которые могут уместиться в ячейке высотой по умолчанию.
	
	Для каждого Срок Из Сроки.Все Цикл
		
		СрокПрошлое = Истина;
		
		Если Сроки.Прошлое.Найти(Срок) <> Неопределено Тогда
			ОбластьМакетаСтатус = "ШарКрасный%1";
			ТекОбласть = Области.НалогСрок;
		ИначеЕсли Сроки.СовсемСкоро.Найти(Срок) <> Неопределено Тогда
			ОбластьМакетаСтатус = "ШарЗолотой%1";
			ТекОбласть = Области.НалогСрок;
		Иначе
			ТекОбласть = Области.НалогБудущее;
			СрокПрошлое = Ложь;
		КонецЕсли;
		
		Для Каждого Строка из НалогиПоСрокам Цикл
			Если Строка.Срок <> Срок Тогда
				Продолжить;
			КонецЕсли;
			
			НаименованиеНалога = Строка.НаименованиеНалога;
			// Представление требования ФНС иногда следует разместить в 3 строки, в зависимости от длины названия налога.
			// Например, Требование ФНС(Налог на прибыль).
			Если Строка.КодНалога = ЗадачиБухгалтераКлиентСервер.КодНалогаТребованиеФНС()
				И СтрДлина(НаименованиеНалога) > МаксимальнаяДлинаСтроки Тогда
				
				ПараметрыВывода.ВысотаСтрокиШапкиНалоги = ВысотаСтрокиШапкиНалоги() * 3;
			КонецЕсли;
			ОтборПоНалогу = Новый Структура("ИдентификаторПравила, КодНалога, Срок",
				Строка.ИдентификаторПравила, Строка.КодНалога, Строка.Срок);
				
			ВыводитьКодФНС = НалогиПоСрокам.НайтиСтроки(ОтборПоНалогу).Количество() > 1;
			Если ВыводитьКодФНС И Строка.КодНалога <> ЗадачиБухгалтераКлиентСервер.КодНалогаТребованиеФНС() Тогда
				НаименованиеНалога = СтрШаблон(НСтр("ru='%1%2%3'"), 
					НаименованиеНалога,
					Символы.НПП,
					РегистрыСведений.ЗадачиБухгалтера.ПредставлениеНалоговогоОргана(Строка.КодФНС));
					
				// Если необходимо выводить код ФНС, то представления налога следует разместить в 3 строки.
				ПараметрыВывода.ВысотаСтрокиШапкиНалоги = ВысотаСтрокиШапкиНалоги() * 3;
			КонецЕсли;
			
			ПараметрыОбласти = Новый Структура("Налог",
				ВыполнениеЗадачБухгалтера.СокращенноеПредставлениеНалога(
					Строка.КодНалога, Строка.ИдентификаторПравила, Строка.КраткоеОписаниеПравила, НаименованиеНалога));
				
			Если МногоОрганизаций И СрокПрошлое И Строка.АктивныхСтрок > 0 Тогда
				ИндексСтатуса = Окр(Строка.Сдано / Строка.АктивныхСтрок * ЧислоСекторовСтатуса);
				ИндексСтатуса = Макс(0,(Мин(ЧислоСекторовСтатуса, ИндексСтатуса)));
				ПараметрыОбласти.Вставить("КартинкаСтатуса",
					Области.СтатусыНалогов.Рисунки[СтрШаблон(ОбластьМакетаСтатус, ИндексСтатуса)].Картинка);
			КонецЕсли;
				
			Если Строка.ЭтоПлатеж Тогда
				ПараметрыОбласти.Налог = СтрШаблон(НСтр("ru = '%1, платеж'"), Строка.НаименованиеНалога);
				ТекОбласть = Области.НалогСтрокаПлатеж;
				Если СтрДлина(ПараметрыОбласти.Налог) > МаксимальнаяДлинаСтроки Тогда
					ПараметрыВывода.ВысотаСтрокиШапкиНалоги = ВысотаСтрокиШапкиНалоги() * 3;
				КонецЕсли;
			КонецЕсли;
			
			ТекОбласть.Параметры.Заполнить(ПараметрыОбласти);
			Результат.Присоединить(ТекОбласть);
		КонецЦикла;
	КонецЦикла;
	
	// Последовательность вывода в отчет строк шапки, если налоги выводятся вертикально:
	// первая строка - Сроки(Прошлое, Скоро, Совсем скоро, Будущее). Вторая - даты. Третья - Налоги.
	// Увеличим высоту строки "Налоги", чтобы все представления налогов уместились.
	ВысотаШапки = Результат.ВысотаТаблицы;
	СтрокаШапкиНалоги = Результат.Область(ВысотаШапки, 1, ВысотаШапки, Результат.ШиринаТаблицы);
	СтрокаШапкиНалоги.ВысотаСтроки = ПараметрыВывода.ВысотаСтрокиШапкиНалоги;
	
КонецПроцедуры

Функция ВывестиДанныеПоОднойОрганизации(Данные, ПараметрыВывода)
	
	Результат = Новый ТабличныйДокумент;
	
	Сроки = Данные.Сроки;
	ВсеРасписание = Данные.Расписание;
	НалогиОрганизаций = Данные.НалогиОрганизаций;
	НалогиПоПравилу = Данные.НалогиПоПравилу;
	Области = ПараметрыВывода.Области;
	
	ТекущаяОрганизация = Неопределено;
	ТекущийНалог = Неопределено;
	КешТолькоПросмотр = Новый Соответствие;
	ОбработанныеТребования = Новый Массив;
	ОбработанныеПлатежи = Новый Массив;
	ОбработанныеДокументыСЭДО = Новый Массив;
	
	ДокументыСЭДОНесколькоЗадачВОдинДень = Ложь;
	
	Для каждого НалогОрганизации Из НалогиОрганизаций Цикл
		
		ОтборПоНалогу = Новый Структура("Организация,Правило", НалогОрганизации.Организация, НалогОрганизации.Правило);
		
		ОтборДетализацияПоПравилу = Новый Структура("КодНалога", НалогОрганизации.КодНалога);
		НаименованиеНалога = НалогОрганизации.НаименованиеНалога;
		ВыводитьКодФНС = НалогиОрганизаций.НайтиСтроки(ОтборПоНалогу).Количество() > 1;
		Если ВыводитьКодФНС И Не НалогОрганизации.КодНалога = ЗадачиБухгалтераКлиентСервер.КодНалогаТребованиеФНС() Тогда
			ОтборДетализацияПоПравилу.Вставить("РегистрацияФНС", НалогОрганизации.РегистрацияФНС);
			НаименованиеНалога = СтрШаблон(НСтр("ru='%1%2%3'"),
				НаименованиеНалога,
				Символы.НПП,
				РегистрыСведений.ЗадачиБухгалтера.ПредставлениеНалоговогоОргана(НалогОрганизации.КодФНС));
		КонецЕсли;
		
		ЭтоПлатеж = НалогОрганизации.ЭтоПлатеж;
		ЭтоДокументСЭДО = НалогОрганизации.КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиДокументСЭДО();
		
		ДетализироватьПоПравилу = НалогиПоПравилу.НайтиСтроки(ОтборДетализацияПоПравилу).Количество() > 0;
		// Все строки с одинаковым кодом налога требуется выводить только если требуется детализация по правилу.
		Если ТекущийНалог = НалогОрганизации.КодНалога
			И Не ДетализироватьПоПравилу
			И Не ЭтоПлатеж
			И Не ЭтоДокументСЭДО Тогда
			Продолжить;
		КонецЕсли;
		ТекущийНалог = НалогОрганизации.КодНалога;
		
		Если НалогОрганизации.КодНалога = ЗадачиБухгалтераКлиентСервер.КодНалогаТребованиеФНС() Тогда
			// На одно требование как правило две задачи, лишние строки не выводим
			Если ОбработанныеТребования.Найти(НалогОрганизации.Правило) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ОбработанныеТребования.Добавить(НалогОрганизации.Правило);
		КонецЕсли;
		
		Если ЭтоПлатеж Тогда
			Если ОбработанныеПлатежи.Найти(НалогОрганизации.Правило) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ОбработанныеПлатежи.Добавить(НалогОрганизации.Правило);
		КонецЕсли;
		
		Если ЭтоДокументСЭДО Тогда
			ДетализироватьПоПравилу = Ложь;
			// может быть несколько документов СЭДО одного вида за один день
			Если ОбработанныеДокументыСЭДО.Найти(ТипЗнч(НалогОрганизации.Правило)) <> Неопределено Тогда
				ДокументыСЭДОНесколькоЗадачВОдинДень = Истина;
				Продолжить;
			КонецЕсли;
			ОбработанныеДокументыСЭДО.Добавить(ТипЗнч(НалогОрганизации.Правило));
		КонецЕсли;
		
		ПредставлениеНалога = ВыполнениеЗадачБухгалтера.СокращенноеПредставлениеНалога(
			НалогОрганизации.КодНалога,
			НалогОрганизации.ИдентификаторПравила,
			НалогОрганизации.КраткоеОписаниеПравила,
			НаименованиеНалога);
			
		Если ЭтоПлатеж Тогда
			ПредставлениеНалога = СтрШаблон(НСтр("ru = '%1, платеж'"), НалогОрганизации.НаименованиеНалога)
		КонецЕсли;
		ПараметрыОбласти = Новый Структура("Налог", ПредставлениеНалога);
		Области.СтрокаНалог.Параметры.Заполнить(ПараметрыОбласти);
		
		Результат.Вывести(Области.СтрокаНалог);
		СкорректироватьВысотуСтроки(Области.СтрокаНалог, Результат);
		
		Для каждого Срок Из Сроки.Все Цикл
			
			ОтборПоСроку = Новый Структура("Организация,КодНалога,РегистрацияФНС,Срок,ЭтоПлатеж",
				НалогОрганизации.Организация, НалогОрганизации.КодНалога, НалогОрганизации.РегистрацияФНС, Срок, ЭтоПлатеж);
				
			Если ДетализироватьПоПравилу Или ЭтоПлатеж Или ЭтоДокументСЭДО Тогда
				ОтборПоСроку.Вставить("ИдентификаторПравила", НалогОрганизации.ИдентификаторПравила);
			КонецЕсли;
			
			РасписаниеПоСроку = ВсеРасписание.НайтиСтроки(ОтборПоСроку);
			ТребуетсяОтчет = ЗначениеЗаполнено(РасписаниеПоСроку);
			
			Расписание = Неопределено;
			ЗадачиПоСроку = Неопределено;
			
			Если ТребуетсяОтчет Тогда
				ОтборПоСроку.Вставить("ЕстьЗадача", Истина);
				Расписание = РасписаниеПоСроку[0];
				ЗадачиПоСроку = ВсеРасписание.Скопировать(ОтборПоСроку);
			КонецЕсли;
			
			МожноВыполнять = Ложь;
			Если Расписание <> Неопределено Тогда
				МожноВыполнять = Расписание.НачалоВыполнения <= ПараметрыВывода.НаДату;
			КонецЕсли;
			
			ЭтоПрошлое = Сроки.Прошлое.Найти(Срок) <> Неопределено;
			ЭтоБудущее = Сроки.Будущее.Найти(Срок) <> Неопределено;
			
			Если ЭтоПлатеж И ЭтоБудущее И Расписание <> Неопределено И Не Расписание.ЕстьЗадолженность Тогда
				ТекущаяОбласть = Области.СтрокаБудущее;
				МожноВыполнять = Ложь;
			ИначеЕсли ЭтоПлатеж Тогда
				ТекущаяОбласть = Области.СтрокаПлатеж;
				МожноВыполнять = Истина;
			ИначеЕсли МожноВыполнять И ТребуетсяОтчет Тогда
				ТекущаяОбласть = Области.СтрокаСрок;
			ИначеЕсли МожноВыполнять Тогда
				ТекущаяОбласть = Области.НеТребуетсяСрок;
			ИначеЕсли ТребуетсяОтчет Тогда
				ТекущаяОбласть = Области.СтрокаБудущее;
			Иначе
				ТекущаяОбласть = Области.НеТребуетсяБудущее;
			КонецЕсли;
			
			ДанныеЗаполнения = НовыйДанныеЗаполненияОбласти();
			ДанныеЗаполнения.Организация = НалогОрганизации.Организация;
			ДанныеЗаполнения.Налог = НалогОрганизации;
			ДанныеЗаполнения.Расписание = Расписание;
			ДанныеЗаполнения.ЗадачиПоСроку = ЗадачиПоСроку;
			ДанныеЗаполнения.ЭтоПрошлое = ЭтоПрошлое;
			ДанныеЗаполнения.ЭтоПлатеж = ЭтоПлатеж;
			ДанныеЗаполнения.МожноВыполнять = МожноВыполнять;
			ДанныеЗаполнения.КешТолькоПросмотр = КешТолькоПросмотр;
			ДанныеЗаполнения.Приложение = НалогОрганизации.Приложение;
			ДанныеЗаполнения.ПравоНаВключениеОтчетов = ПараметрыВывода.ПравоНаВключениеОтчетов;
			
			ПараметрыОбласти = ПараметрыОбласти(ДанныеЗаполнения);
			ТекущаяОбласть.Параметры.Заполнить(ПараметрыОбласти);
			
			Область = ТекущаяОбласть.Область(1, 1, 1, 1);
			Область.Примечание.Текст = ?(ТипЗнч(ПараметрыОбласти.Расшифровка) = Тип("Структура"),
				ПараметрыОбласти.Расшифровка.ПараметрыДействияСОтчетом.Комментарий, "");
			
			Результат.Присоединить(ТекущаяОбласть);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ДокументыСЭДОНесколькоЗадачВОдинДень Тогда
		КалендарьОтчетностиПереопределяемый.СтатистикаПоПоказателямДобавитьСобытие(
			"КалендарьОтчетности.ДокументыСЭДО.НесколькоЗадачВОдинДень");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВывестиДанныеПоНесколькимОрганизациям(Данные, ОжидающиеЗапросы, ОшибкиПриложений, ПараметрыВывода)
	
	Результат = Новый ТабличныйДокумент;
	
	Сроки = Данные.Сроки;
	ВсеРасписание = Данные.Расписание;
	ДанныеОрганизаций = Данные.Организации;
	НалогиОрганизаций = Данные.НалогиОрганизаций;
	Области = ПараметрыВывода.Области;
	
	НалогиПоСрокам = Данные.НалогиПоСрокам;
	КвалификаторЧисла = Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный);
	ТипЧисло = Новый ОписаниеТипов("Число", КвалификаторЧисла);
	НалогиПоСрокам.Колонки.Добавить("Сдано", ТипЧисло);
	НалогиПоСрокам.Колонки.Добавить("АктивныхСтрок", ТипЧисло);
	
	КешТолькоПросмотр = Новый Соответствие;
	
	ДокументыСЭДОНесколькоЗадачВОдинДень = Ложь;
	
	Для каждого СтрокаДанных Из ДанныеОрганизаций Цикл
		
		ПерваяЗаписьРасписания = Истина;
		Для каждого СтрокаРасписания Из НалогиПоСрокам Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаРасписания.Срок) Тогда
				// Срок не будет указан для организации, по которой не удалось получить данные.
				Продолжить;
			КонецЕсли;
			
			Если ПерваяЗаписьРасписания Тогда
				Наименование = "";
				КалендарьОтчетностиПереопределяемый.ПриЗаполненииНаименованияОрганизацииОблачногоПриложения(
					СтрокаДанных.Организация, Наименование);
				ПараметрыОбласти = Новый Структура("Организация", Наименование);
				Области.СтрокаОрганизация.Параметры.Заполнить(ПараметрыОбласти);
				Результат.Вывести(Области.СтрокаОрганизация);
				СкорректироватьВысотуСтроки(Области.СтрокаОрганизация, Результат);
				ВывестиОбластьПолучениеДанных(Результат, СтрокаДанных.Приложение, ОжидающиеЗапросы, ОшибкиПриложений, ПараметрыВывода);
				ПерваяЗаписьРасписания = Ложь;
			Иначе
				ПараметрыОбласти = Новый Структура;
			КонецЕсли;
				
			ОтборЯчейки = Новый Структура("Организация,КодНалога,РегистрацияФНС,ИдентификаторПравила,ЭтоТребованиеОтвет,Срок",
				СтрокаДанных.Организация, СтрокаРасписания.КодНалога, СтрокаРасписания.РегистрацияФНС,
				СтрокаРасписания.ИдентификаторПравила, СтрокаРасписания.ЭтоТребованиеОтвет, СтрокаРасписания.Срок);
				
			РасписаниеПоСроку = ВсеРасписание.НайтиСтроки(ОтборЯчейки);
			ТребуетсяОтчет = ЗначениеЗаполнено(РасписаниеПоСроку);
			
			Расписание = Неопределено;
			ЗадачиПоСроку = Неопределено;
			
			Если ТребуетсяОтчет Тогда
				ОтборЯчейки.Вставить("ЕстьЗадача", Истина);
				Расписание = РасписаниеПоСроку[0];
				ЗадачиПоСроку = ВсеРасписание.Скопировать(ОтборЯчейки);
			КонецЕсли;
			
			МожноВыполнять = Ложь;
			Если Расписание <> Неопределено Тогда
				МожноВыполнять = Расписание.НачалоВыполнения <= ПараметрыВывода.НаДату;
			КонецЕсли;
			
			ЭтоПлатеж = СтрокаРасписания.ЭтоПлатеж;
			ЭтоПрошлое = Сроки.Прошлое.Найти(СтрокаРасписания.Срок) <> Неопределено;
			ЭтоБудущее = Сроки.Будущее.Найти(СтрокаРасписания.Срок) <> Неопределено;
			
			Если ЭтоПлатеж И ЭтоБудущее И Расписание <> Неопределено И Не Расписание.ЕстьЗадолженность Тогда
				ТекущаяОбласть = Области.СтрокаБудущее;
				МожноВыполнять = Ложь;
			ИначеЕсли ЭтоПлатеж Тогда
				ТекущаяОбласть = Области.СтрокаПлатеж;
				МожноВыполнять = Истина;
			ИначеЕсли МожноВыполнять И ТребуетсяОтчет Тогда
				ТекущаяОбласть = Области.СтрокаСрок;
			ИначеЕсли ТребуетсяОтчет Тогда
				ТекущаяОбласть = Области.СтрокаБудущее;;
			Иначе
				ТекущаяОбласть = Области.НеТребуетсяСрок;
			КонецЕсли;
			
			ДанныеЗаполнения = НовыйДанныеЗаполненияОбласти();
			ДанныеЗаполнения.Организация = СтрокаДанных.Организация;
			ДанныеЗаполнения.Налог = СтрокаРасписания;
			ДанныеЗаполнения.Расписание = Расписание;
			ДанныеЗаполнения.ЗадачиПоСроку = ЗадачиПоСроку;
			ДанныеЗаполнения.ЭтоПрошлое = ЭтоПрошлое;
			ДанныеЗаполнения.ЭтоПлатеж = ЭтоПлатеж;
			ДанныеЗаполнения.МожноВыполнять = МожноВыполнять;
			ДанныеЗаполнения.КешТолькоПросмотр = КешТолькоПросмотр;
			ДанныеЗаполнения.Приложение = СтрокаДанных.Приложение;
			ДанныеЗаполнения.ПравоНаВключениеОтчетов = ПараметрыВывода.ПравоНаВключениеОтчетов;
			
			ПараметрыОбласти = ПараметрыОбласти(ДанныеЗаполнения);
			
			ТекущаяОбласть.Параметры.Заполнить(ПараметрыОбласти);
			
			Область = ТекущаяОбласть.Область(1, 1, 1, 1);
			Область.Примечание.Текст = ?(ТипЗнч(ПараметрыОбласти.Расшифровка) = Тип("Структура"),
				ПараметрыОбласти.Расшифровка.ПараметрыДействияСОтчетом.Комментарий, "");
			
			Результат.Присоединить(ТекущаяОбласть);
			
			// Статус итого налога
			ОтборЯчейки = Новый Структура("КодНалога,РегистрацияФНС,ИдентификаторПравила,Срок",
				СтрокаРасписания.КодНалога, СтрокаРасписания.РегистрацияФНС,
				СтрокаРасписания.ИдентификаторПравила, СтрокаРасписания.Срок);
				
			СтрокаСрок = НалогиПоСрокам.НайтиСтроки(ОтборЯчейки)[0];
			Если ТребуетсяОтчет Тогда
				СтрокаСрок.АктивныхСтрок = СтрокаСрок.АктивныхСтрок + 1;
				Если ПараметрыОбласти.Статус = КалендарьОтчетностиКлиентСервер.СтатусыОтчета().Сдано Тогда
					СтрокаСрок.Сдано = СтрокаСрок.Сдано + 1;
				КонецЕсли;
			КонецЕсли;
			
			ДокументыСЭДОНесколькоЗадачВОдинДень = ДокументыСЭДОНесколькоЗадачВОдинДень
				Или (СтрокаРасписания.КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиДокументСЭДО()
				И ЗадачиПоСроку <> Неопределено И ЗадачиПоСроку.Количество() > 1);
			
		КонецЦикла;
	КонецЦикла;
	
	Если ДокументыСЭДОНесколькоЗадачВОдинДень Тогда
		КалендарьОтчетностиПереопределяемый.СтатистикаПоПоказателямДобавитьСобытие(
			"КалендарьОтчетности.ДокументыСЭДО.НесколькоЗадачВОдинДень");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВывестиТаблицуДанных(Данные, ОжидающиеЗапросы, ОшибкиПриложений, ПараметрыВывода);
	
	Если ПараметрыВывода.ПоказатьОрганизации Тогда
		Возврат ВывестиДанныеПоНесколькимОрганизациям(Данные, ОжидающиеЗапросы, ОшибкиПриложений, ПараметрыВывода);
	КонецЕсли;
	
	Возврат ВывестиДанныеПоОднойОрганизации(Данные, ПараметрыВывода);
КонецФункции

Процедура ВывестиОбластьПолучениеДанных(Результат, Приложение, ОжидающиеЗапросы, ОшибкиПриложений, ПараметрыВывода)
	
	Если Не ПараметрыВывода.ОжидаетсяЗагрузкаДанных И Не ПараметрыВывода.ЕстьОшибкиЗапросов Тогда
		Возврат;
	КонецЕсли;
	
	Области = ПараметрыВывода.Области;
	
	Если ОшибкиПриложений.Найти(Приложение) <> Неопределено Тогда
		Результат.Присоединить(Области.ПолучениеДанныхНеВыполнено);
		Возврат;
	КонецЕсли;
	
	Для Каждого Запрос Из ОжидающиеЗапросы Цикл
		Если Запрос.Приложение = Приложение Тогда
			Результат.Присоединить(Области.ПолучениеДанныхВыполняется);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	Результат.Присоединить(Области.ПолучениеДанныхВыполнено);
	
КонецПроцедуры

Процедура ОформитьСтрокиОтчета(Результат, ВысотаШапки)
	
	// Нечетные строки отчета выделяются цветом.
	НомерПоПорядку = 0;
	Для НомерСтроки = ВысотаШапки + 1 По Результат.ВысотаТаблицы Цикл
		
		НомерПоПорядку = НомерПоПорядку + 1;
		Если НомерПоПорядку % 2 = 0 Тогда
			ЦветФонаСтроки = Новый Цвет;
		Иначе
			ЦветФонаСтроки = ЦветаСтиля.СерыйФонСтрокиОтчета;
		КонецЕсли;
		Результат.Область(НомерСтроки, 1, НомерСтроки, Результат.ШиринаТаблицы).ЦветФона = ЦветФонаСтроки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОформитьОбластиОрганизаций(Результат, Данные, ПараметрыВывода, ВысотаШапки)
	
	ПоказатьОрганизации = ПараметрыВывода.ПоказатьОрганизации;
	
	Если Не ПоказатьОрганизации Тогда
		Возврат;
	КонецЕсли;
	
	НалогиОрганизаций = Данные.НалогиОрганизаций;
	Области = ПараметрыВывода.Области;
	
	// Области ячеек по организациям объединяются, нечетные области выделяются цветом.
	
	ВысотаОбласти = Области.СтрокаОрганизация.ВысотаТаблицы;
	ШиринаОбласти = Области.СтрокаОрганизация.ШиринаТаблицы;
	НомерОрганизации = 0;
	ПерваяСтрока = ВысотаШапки + 1;
	
	ВсеОрганизации = ОбщегоНазначения.ВыгрузитьКолонку(НалогиОрганизаций, "Организация", Истина);
	Для каждого ТекущаяОрганизация Из ВсеОрганизации Цикл
		
		НомерОрганизации = НомерОрганизации + 1;
		ОтборПоОрганизации = Новый Структура("Организация", ТекущаяОрганизация);
		СтрокОрганизации = НалогиОрганизаций.НайтиСтроки(ОтборПоОрганизации).Количество();
		ПоследняяСтрока = ПерваяСтрока + СтрокОрганизации * ВысотаОбласти - 1;
		ОбластьОрганизации = Результат.Область(ПерваяСтрока, 1, ПоследняяСтрока, ШиринаОбласти);
		ОбластьОрганизации.Объединить();
		
		Если НомерОрганизации % 2 = 0 Тогда
			ЦветФонаТекущейОрганизации = Новый Цвет;
		Иначе
			ЦветФонаТекущейОрганизации = ЦветаСтиля.СерыйФонСтрокиОтчета;
		КонецЕсли;
		ОбластьОрганизации.ЦветФона = ЦветФонаТекущейОрганизации;
		
		ПерваяСтрока = ПоследняяСтрока + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗафиксироватьШапку(Результат, ПараметрыВывода, ВысотаШапки)
	
	Области = ПараметрыВывода.Области;
	ПоказатьОрганизации = ПараметрыВывода.ПоказатьОрганизации;
	ОжидаетсяЗагрузкаДанных = ПараметрыВывода.ОжидаетсяЗагрузкаДанных;
	ЕстьОшибкиЗапросов = ПараметрыВывода.ЕстьОшибкиЗапросов;
	
	ОбластьШапки = ?(ПоказатьОрганизации, Области.ЗаголовокОрганизация, Области.ЗаголовокНалог);
	ШиринаШапки = ОбластьШапки.ШиринаТаблицы;
	
	Если ОжидаетсяЗагрузкаДанных Или ЕстьОшибкиЗапросов Тогда
		ШиринаШапки = ШиринаШапки + Области.ЗаголовокПолучениеДанных.ШиринаТаблицы;
	КонецЕсли;
	
	Результат.ФиксацияСверху = ВысотаШапки;
	Результат.ФиксацияСлева = ШиринаШапки;
	
КонецПроцедуры

Процедура ВывестиПодвал(Результат, Данные, ПараметрыВывода)
	
	Сроки = Данные.Сроки;
	Области = ПараметрыВывода.Области;
	ПоказатьОрганизации = ПараметрыВывода.ПоказатьОрганизации;
	
	Если ПоказатьОрганизации Тогда
		Результат.Вывести(Области.ПодвалОрганизация);
		Результат.Присоединить(Области.ПодвалНалог);
	Иначе
		Результат.Вывести(Области.ПодвалНалог);
	КонецЕсли;
	
	Для каждого Срок Из Сроки.Все Цикл
		МожноВыполнять = Сроки.Прошлое.Найти(Срок) <> Неопределено 
			Или Сроки.СовсемСкоро.Найти(Срок) <> Неопределено;
		Если МожноВыполнять Тогда
			ТекОбласть = Области.ПодвалСрок;
		Иначе
			ТекОбласть = Области.ПодвалБудущее;
		КонецЕсли;
		Результат.Присоединить(ТекОбласть);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстСкоро(Знач Дата, Знач НаДату)
	
	Если Дата = НаДату Тогда
		Возврат НСтр("ru='Сегодня'");
	КонецЕсли;
	
	ТекстСкоро = "";
	РазницаДней = Окр((Дата - НаДату) / 60 / 60 / 24);
	Если РазницаДней = 1 Тогда
		ТекстСкоро = НСтр("ru='Завтра'");
	ИначеЕсли РазницаДней = 7 Тогда
		ТекстСкоро = НСтр("ru='Через неделю'");
	ИначеЕсли РазницаДней = 14 Тогда
		ТекстСкоро = НСтр("ru='Через 2 недели'");
	ИначеЕсли РазницаДней = 21 Тогда
		ТекстСкоро = НСтр("ru='Через 3 недели'");
	ИначеЕсли РазницаДней >= 30 Тогда
		ЧислоМесяцев = Окр(РазницаДней / 30);
		ТекстСкоро = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';Через %1 месяц;;Через %1 месяца;Через %1 месяцев;Через %1 месяца'"),
			ЧислоМесяцев);
	Иначе
		ТекстСкоро = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';Через %1 день;;Через %1 дня;Через %1 дней;Через %1 дня'"),
			РазницаДней);
	КонецЕсли;
	
	Возврат ТекстСкоро;
	
КонецФункции

Функция ПараметрыОбласти(ДанныеЗаполнения)
	
	Расписание = ДанныеЗаполнения.Расписание;
	Приложение = ДанныеЗаполнения.Приложение;
	
	ЭтоПриложение = Не ЗначениеЗаполнено(Приложение);
	ПараметрыОбласти = НовыйПараметрыОбласти();
	
	Если Не ЗначениеЗаполнено(Расписание) Тогда
		ДоступноУправлениеОтчетом = КалендарьОтчетностиКлиентСервер.ДоступноУправлениеОтчетом(
			ДанныеЗаполнения.Налог.КодНалога,
			ДанныеЗаполнения.Налог.ИдентификаторПравила);
			
		Если ДоступноУправлениеОтчетом И ЭтоПриложение И ДанныеЗаполнения.ПравоНаВключениеОтчетов Тогда
			Возврат ПараметрыОбластиКраткаяРасшифровка(ДанныеЗаполнения);
		КонецЕсли;
		
		Возврат ПараметрыОбласти;
	КонецЕсли;
	
	ЗадачиПоСроку = ДанныеЗаполнения.ЗадачиПоСроку;
	ЭтоПрошлое = ДанныеЗаполнения.ЭтоПрошлое;
	ЭтоПлатеж = ДанныеЗаполнения.ЭтоПлатеж;
	МожноВыполнять = ДанныеЗаполнения.МожноВыполнять;
	КешТолькоПросмотр = ДанныеЗаполнения.КешТолькоПросмотр;
	
	ТолькоПросмотр = КешТолькоПросмотр[Расписание.Правило];
	Если ТолькоПросмотр = Неопределено Тогда
		ТолькоПросмотр = Не КалендарьБухгалтера.ПравоВыполненияЗадачи(Расписание.Правило);
		КешТолькоПросмотр.Вставить(Расписание.Правило, ТолькоПросмотр);
	КонецЕсли;
	
	Если Не ТолькоПросмотр Тогда
		ПараметрыОтчета = КалендарьОтчетностиКлиентСервер.НовыйПараметрыОтчета();
		ЗаполнитьЗначенияСвойств(ПараметрыОтчета, Расписание);
		ПараметрыОтчета.РегистрацияВНалоговомОргане = Расписание.РегистрацияФНС;
		ПараметрыОтчета.ПериодСобытия = Расписание.КонецПериода;
		ПараметрыОтчета.Наименование = Расписание.НаименованиеЗадачи;
		ПараметрыОтчета.ИдентификаторЗадачи = Расписание.КодНалога;
		Если ЗначениеЗаполнено(Расписание.ПредставлениеПериодичности) Тогда
			ПараметрыОтчета.Периодичность = Перечисления.Периодичность[Расписание.ПредставлениеПериодичности];
		КонецЕсли;
		Если Расписание.КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиДокументСЭДО() Тогда
			ИмяКолонкиПравила = ИмяКолонкиПравилаДокументаСЭДО(Расписание.Правило);
			ПараметрыОтчета.Правило = Расписание[ИмяКолонкиПравила];
		КонецЕсли;
		
		ПараметрыОбласти.Расшифровка = НовыйРасшифровка();
		ПараметрыОбласти.Расшифровка.Вставить("ПараметрыОтчета", ПараметрыОтчета);
	КонецЕсли;
	
	ПараметрыДействия = КалендарьОтчетностиКлиентСервер.НовыйПараметрыДействияСОтчетом();
	ПараметрыДействия.Организация = Расписание.Организация;
	ПараметрыДействия.НаименованиеОрганизации = Расписание.НаименованиеОрганизации;
	ПараметрыДействия.НаименованиеОтчета = Расписание.НаименованиеЗадачи;
	ПараметрыДействия.Срок = Расписание.Срок;
	ПараметрыДействия.СтатусУстановленВручную = Расписание.СтатусУстановленВручную;
	ПараметрыДействия.Комментарий = Расписание.Комментарий;
	ПараметрыДействия.ЕстьЗадача = Расписание.ЕстьЗадача;
	ПараметрыДействия.Приложение = Приложение;
	ПараметрыДействия.КоличествоЗадач = ЗадачиПоСроку.Количество();
	
	// Режим совместимости работы с другим приложением в режиме сервиса.
	// Необновленное приложение не передает Действие.
	ПараметрыДействия.РежимСовместимости = Не ЗначениеЗаполнено(Расписание.Действие);
	
	Если Не МожноВыполнять Тогда
		
		ПараметрыОбласти.КартинкаСтатуса = БиблиотекаКартинок.СерыйШар;
		Если Не ТолькоПросмотр Тогда
			ПараметрыДействия.СледующееДействие = "ПоказатьСообщение";
			ПараметрыДействия.ТекстСообщения = СтрШаблон(НСтр("ru='Доступно для подготовки после %1'"),
				Формат(Расписание.НачалоВыполнения, "ДФ='d MMMM'"));
			ПараметрыДействия.КартинкаСтатуса = ПараметрыОбласти.КартинкаСтатуса;
			Если Не ЭтоПриложение Тогда
				КалендарьОтчетностиПереопределяемый.ЗаполнитьПараметрыДействияПараметрыЗапросаИзмененияОтчета(
					ПараметрыДействия.ПараметрыЗапросаИзмененияОтчета,
					Расписание,
					ЗадачиПоСроку);
			КонецЕсли;
			ПараметрыОбласти.Расшифровка.Вставить("ПараметрыДействияСОтчетом", ПараметрыДействия);
		КонецЕсли;
		
		Возврат ПараметрыОбласти;
		
	КонецЕсли;
	
	Если Не ТолькоПросмотр Тогда
		Если ЭтоПриложение Тогда
			ПараметрыДействия.СледующееДействие = "ПоказатьПомощник";
		Иначе
			ПараметрыДействия.СледующееДействие = "ПерейтиПоСсылке";
			КалендарьОтчетностиПереопределяемый.ЗаполнитьПараметрыДействияНавигационнаяСсылка(ПараметрыДействия, Расписание);
			КалендарьОтчетностиПереопределяемый.ЗаполнитьПараметрыДействияПараметрыЗапросаИзмененияОтчета(
				ПараметрыДействия.ПараметрыЗапросаИзмененияОтчета,
				Расписание,
				ЗадачиПоСроку);
		КонецЕсли;
	КонецЕсли;
	
	Если Расписание.СтатусУстановленВручную Тогда
		ПараметрыОбласти.Статус = Расписание.Статус;
		ПараметрыОбласти.СуммаПлатежа = Расписание.Статус;
	Иначе
		ЗаполнитьПараметрыОбластиСтатусОтчета(ПараметрыОбласти, ДанныеЗаполнения);
	КонецЕсли;
	
	ЗаполнитьПараметрыОбластиКартинкаСтатуса(ПараметрыОбласти, ЭтоПрошлое, ЭтоПлатеж, Расписание.СтатусУстановленВручную);
	
	Если Не ТолькоПросмотр Тогда
		ПараметрыДействия.Статус = ПараметрыОбласти.Статус;
		ПараметрыДействия.КартинкаСтатуса = ПараметрыОбласти.КартинкаСтатуса;
		ПараметрыОбласти.Расшифровка.Вставить("ПараметрыДействияСОтчетом", ПараметрыДействия);
	КонецЕсли;
	
	Возврат ПараметрыОбласти;
	
КонецФункции

Функция ПараметрыОбластиКраткаяРасшифровка(ДанныеЗаполнения)
	
	ПараметрыОбласти = НовыйПараметрыОбласти();
	Налог = ДанныеЗаполнения.Налог;
	
	ПараметрыОбласти.Расшифровка = НовыйРасшифровка();
	ПараметрыОтчета = КалендарьОтчетностиКлиентСервер.НовыйПараметрыОтчета();
	ПараметрыОтчета.Организация = ДанныеЗаполнения.Организация;
	ПараметрыОтчета.ИдентификаторЗадачи = Налог.КодНалога;
	ПараметрыОбласти.Расшифровка.Вставить("ПараметрыОтчета", ПараметрыОтчета);
	
	ПараметрыДействия = КалендарьОтчетностиКлиентСервер.НовыйПараметрыДействияСОтчетом();
	ПараметрыДействия.Организация = ДанныеЗаполнения.Организация;
	ПараметрыДействия.НаименованиеОрганизации = Строка(ДанныеЗаполнения.Организация);
	ПараметрыДействия.КраткоеНаименованиеОтчета = ВыполнениеЗадачБухгалтера.СокращенноеПредставлениеНалога(
		Налог.КодНалога, Налог.ИдентификаторПравила, Налог.КраткоеОписаниеПравила, Налог.НаименованиеНалога);
	ПараметрыОбласти.Расшифровка.Вставить("ПараметрыДействияСОтчетом", ПараметрыДействия);
	
	Возврат ПараметрыОбласти;
	
КонецФункции

Процедура ЗаполнитьПараметрыОбластиСтатусОтчета(ПараметрыОбласти, ДанныеЗаполнения)
	
	Расписание = ДанныеЗаполнения.Расписание;
	ЗадачиПоСроку = ДанныеЗаполнения.ЗадачиПоСроку;
	ЭтоПрошлое = ДанныеЗаполнения.ЭтоПрошлое;
	
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	СтатусНеСдано = ?(Расписание.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.Зарплата,
		СтатусыОтчета.НеОплачено, СтатусыОтчета.НеСдано);
		
	Если ДанныеЗаполнения.ЭтоПлатеж Тогда
		ПараметрыОбласти.ЕстьЗадолженность = Расписание.ЕстьЗадолженность;
		// Статус из Расписание является приоритетным. Если статус не заполнен, тогда рассчитываем.
		Если ЗначениеЗаполнено(Расписание.Статус) Тогда
			ПараметрыОбласти.Статус = Расписание.Статус;
		ИначеЕсли ПараметрыОбласти.ЕстьЗадолженность И ЭтоПрошлое Тогда
			ПараметрыОбласти.Статус = СтатусыОтчета.Просрочено;
		ИначеЕсли ПараметрыОбласти.ЕстьЗадолженность И Не ЭтоПрошлое Тогда
			ПараметрыОбласти.Статус = СтатусыОтчета.НеОплачено;
		Иначе
			ПараметрыОбласти.Статус = СтатусыОтчета.Оплачено;
		КонецЕсли;
		ПараметрыОбласти.СуммаПлатежа =
			?(ЭтоПрошлое Или Расписание.СуммаПлатежа = 0, ПараметрыОбласти.Статус, Формат(Расписание.СуммаПлатежа, "ЧДЦ=0"));
	Иначе
		// Обычно задача одна, но если их несколько, выбираем минимальный статус.
		ЧислоЗадачПоСроку = ЗадачиПоСроку.Количество();
		Если ЗадачиПоСроку.Количество() = 1 Тогда 
			Задача = ЗадачиПоСроку[0];
			Если ЗначениеЗаполнено(Задача.Статус) Тогда
				ПараметрыОбласти.Статус = Задача.Статус;
			ИначеЕсли Не Задача.ВАрхиве Тогда
				// Есть активная задача с пустым статусом.
				ПараметрыОбласти.Статус = ?(ЭтоПрошлое, СтатусыОтчета.Просрочено, СтатусНеСдано);
			Иначе
				// Есть задача, отправленная пользователем в архив, с пустым статусом.
				ПараметрыОбласти.Статус = СтатусНеСдано;
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		// для прав Только просмотр расшифровки нет
		Если ПараметрыОбласти.Расшифровка <> Неопределено Тогда
			ПараметрыОбласти.Расшифровка.ПараметрыОтчета.КоличествоЗадач = ЧислоЗадачПоСроку;
		КонецЕсли;
		
		ПорядокСтатусов = Новый Массив;
		ПорядокСтатусов.Добавить(СтатусыОтчета.ВРаботе);
		ПорядокСтатусов.Добавить(СтатусыОтчета.Подготовлено);
		ПорядокСтатусов.Добавить(СтатусыОтчета.Сдано);
		ПорядокСтатусов.Добавить(СтатусыОтчета.Расшифровать);
		ПорядокСтатусов.Добавить(СтатусыОтчета.ПодтвердитеПрием);
		ПорядокСтатусов.Добавить(СтатусыОтчета.ОтправьтеОтвет);
		ПорядокСтатусов.Добавить(СтатусыОтчета.ПриемПодтвержден);
		ПорядокСтатусов.Добавить(СтатусыОтчета.ОтветОтправлен);
		
		СтатусНайден = Ложь;
		Для Каждого Статус Из ПорядокСтатусов Цикл
			НайденнаяЗадача = ЗадачиПоСроку.Найти(Статус, "Статус");
			Если НайденнаяЗадача <> Неопределено Тогда
				ПараметрыОбласти.Статус = Статус;
				СтатусНайден = Истина;
				// если в одной ячейке несколько задач, то будет открывать задачу с минимальным статусом
				Если НайденнаяЗадача.КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиДокументСЭДО()
					И ПараметрыОбласти.Расшифровка <> Неопределено Тогда
					ИмяКолонкиПравила = ИмяКолонкиПравилаДокументаСЭДО(НайденнаяЗадача.Правило);
					ПараметрыОбласти.Расшифровка.ПараметрыОтчета.Правило = НайденнаяЗадача[ИмяКолонкиПравила];
					// правила, которые не показываем сохраним в параметрах расшифровки
					ПодчиненныеПравила = ЗадачиПоСроку.ВыгрузитьКолонку(ИмяКолонкиПравила);
					ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПодчиненныеПравила, НайденнаяЗадача[ИмяКолонкиПравила]);
					ПараметрыОбласти.Расшифровка.ПараметрыОтчета.ПодчиненныеПравила = ПодчиненныеПравила;
				КонецЕсли;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не СтатусНайден Тогда
			Если ЗадачиПоСроку.Найти(Ложь, "ВАрхиве") <> Неопределено Тогда
				// Есть активная задача с пустым статусом.
				ПараметрыОбласти.Статус = ?(ЭтоПрошлое, СтатусыОтчета.Просрочено, СтатусНеСдано);
			Иначе
				// Есть задача, отправленная пользователем в архив, с пустым статусом.
				ПараметрыОбласти.Статус = СтатусНеСдано;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыОбластиКартинкаСтатуса(ПараметрыОбласти, ЭтоПрошлое, ЭтоПлатеж, СтатусУстановленВручную)
	
	Если СтатусУстановленВручную Тогда
		ПараметрыОбласти.КартинкаСтатуса = БиблиотекаКартинок.РедактироватьАдресСайта;
		Возврат;
	КонецЕсли;
	
	Если ЭтоПлатеж Тогда
		ПараметрыОбласти.КартинкаСтатуса =
			?(ПараметрыОбласти.ЕстьЗадолженность, БиблиотекаКартинок.КрасныйШар, БиблиотекаКартинок.ЗеленыйШар);
	Иначе
		СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
		Если ПараметрыОбласти.Статус = СтатусыОтчета.Сдано Или ПараметрыОбласти.Статус = СтатусыОтчета.ПриемПодтвержден
			Или ПараметрыОбласти.Статус = СтатусыОтчета.ОтветОтправлен Тогда
			ПараметрыОбласти.КартинкаСтатуса = БиблиотекаКартинок.ЗеленыйШар;
		ИначеЕсли (ПараметрыОбласти.Статус = СтатусыОтчета.НеСдано Или ПараметрыОбласти.Статус = СтатусыОтчета.ОтказаноВПриеме)
			И ЭтоПрошлое Тогда
			ПараметрыОбласти.КартинкаСтатуса = БиблиотекаКартинок.СерыйШар;
		ИначеЕсли ЭтоПрошлое Или ПараметрыОбласти.Статус = СтатусыОтчета.Расшифровать Тогда
			ПараметрыОбласти.КартинкаСтатуса = БиблиотекаКартинок.КрасныйШар;
		Иначе
			ПараметрыОбласти.КартинкаСтатуса = БиблиотекаКартинок.ЗолотойШар;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция НовыйПараметрыОбласти()
	
	Параметры = Новый Структура;
	Параметры.Вставить("Статус");
	Параметры.Вставить("КартинкаСтатуса", Новый Картинка);
	Параметры.Вставить("Расшифровка");
	Параметры.Вставить("СуммаПлатежа");
	Параметры.Вставить("ЕстьЗадолженность");
	Возврат Параметры;
	
КонецФункции

Функция НовыйРасшифровка()

	Расшифровка = Новый Структура;
	Расшифровка.Вставить("ПараметрыОтчета", Новый Структура);
	Расшифровка.Вставить("ПараметрыДействияСОтчетом", Новый Структура);
	Возврат Расшифровка;
	
КонецФункции

Процедура СкорректироватьВысотуСтроки(ОбластьСтроки, Результат)
	
	Если Не ОбластьВмещаетВыведенныйТекст(ОбластьСтроки) Тогда
		// Если наименование не умещается в ячейке, включим автоматическую высоту строки.
		Область = Результат.Область(Результат.ВысотаТаблицы, 1, Результат.ВысотаТаблицы, 1);
		Область.АвтоВысотаСтроки = Истина;
		Область.ВысотаСтроки = 0;
	КонецЕсли;
	
КонецПроцедуры

Функция ОбластьВмещаетВыведенныйТекст(Область)
	// В переданной Области выведен какой-то текст.
	// Чтобы понять, вмещается ли текст по высоте, нужно
	// 1. В области расположить рисунок, замерить высоту рисунка.
	// 2. Выставить ВысотаСтроки в 0 и АвтоВысотаСтроки в Истина.
	// 3. Если рисунок не стал больше, значит, содержимое умещается в строке.
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Вывести(Область);
	ОбластьЯчейка = ТабличныйДокумент.Область(1, 1, 1, 1);
	Рисунок = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Текст);
	Рисунок.Расположить(ОбластьЯчейка);
	
	ИсходнаяВысота = Рисунок.Высота;
	ОбластьЯчейка.ВысотаСтроки = 0;
	ОбластьЯчейка.АвтоВысотаСтроки = Истина;
	
	Возврат Рисунок.Высота <= ИсходнаяВысота;
	
КонецФункции

Функция НовыйРасписание()
	
	Расписание = РегистрыСведений.ЗадачиБухгалтера.НовыйРасписание();
	Расписание.Колонки.Добавить("Статус", Новый ОписаниеТипов(РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса()));
	Расписание.Колонки.Добавить("СтатусУстановленВручную", Новый ОписаниеТипов("Булево"));
	Расписание.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка"));
	
	Возврат Расписание;
КонецФункции

Функция НовыйДанныеЗаполненияОбласти()
	
	ДанныеЗаполнения = Новый Структура;
	
	ДанныеЗаполнения.Вставить("Организация");
	ДанныеЗаполнения.Вставить("Налог");
	ДанныеЗаполнения.Вставить("Расписание");
	ДанныеЗаполнения.Вставить("ЗадачиПоСроку");
	ДанныеЗаполнения.Вставить("ЭтоПрошлое", Ложь);
	ДанныеЗаполнения.Вставить("ЭтоПлатеж", Ложь);
	ДанныеЗаполнения.Вставить("МожноВыполнять", Ложь);
	ДанныеЗаполнения.Вставить("КешТолькоПросмотр", Новый Соответствие());
	ДанныеЗаполнения.Вставить("Приложение");
	ДанныеЗаполнения.Вставить("ПравоНаВключениеОтчетов", Ложь);
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Функция ДетализироватьНалогПоПравилу(КодНалога)
	
	Возврат КодНалога = ЗадачиБухгалтераКлиентСервер.КодНалогаТребованиеФНС()
		Или КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиНДФЛПоОтчету()
		Или КодНалога = ЗадачиБухгалтераКлиентСервер.КодЗадачиСтатистика()
	
КонецФункции

#КонецОбласти

Функция ПорогВместимостиЭкрана()
	// Если организаций достаточно много, и список по вертикали не умещается на экран, то для большей
	// информативности следует выводить статусы сдачи отчетности в целом по налогу. Условно будем считать,
	// что при стандартном разрешении 1920x1080 на экране помещается 20 организаций.
	Возврат 20;
КонецФункции

Функция ВысотаСтрокиШапкиНалоги()
	// Высота вывода наименования в 1 строку.
	Возврат 14;
	
КонецФункции

Функция ВсеОбластиМакета(МногоОрганизаций = Ложь)
	
	Области = Новый Структура;
	
	// Если календарь включает много организаций, то в шапке добавляются итоговые статусы по каждому
	// из налогов в виде "пирога". В связи с этим верстка макета изменяется, чтобы уместить
	// все элементы в ячейках. С целью упрощения кода сделано 2 разных макета.
	// Эти макеты отличаются:
	// 1. Шириной колонок
	// 2. Наличием в макете "КалендарьМногоОрганизаций" области с картинками итоговых статусов налогов ("пирогов").
	ИмяМакета = ?(МногоОрганизаций, "КалендарьМногоОрганизаций", "Календарь");
	Макет = ПолучитьМакет(ИмяМакета);
	
	Области.Вставить("ЗаголовокОрганизация", Макет.ПолучитьОбласть("Заголовок|Организация"));
	Области.Вставить("ЗаголовокНалог", Макет.ПолучитьОбласть("Заголовок|Налог"));
	Области.Вставить("ЗаголовокСрок", Макет.ПолучитьОбласть("Заголовок|Срок"));
	Области.Вставить("ЗаголовокБудущее", Макет.ПолучитьОбласть("Заголовок|Будущее"));
	Области.Вставить("ДатыОрганизация", Макет.ПолучитьОбласть("Даты|Организация"));
	Области.Вставить("ДатыНалог", Макет.ПолучитьОбласть("Даты|Налог"));
	Области.Вставить("ДатыСрок", Макет.ПолучитьОбласть("Даты|Срок"));
	Области.Вставить("ДатыБудущее", Макет.ПолучитьОбласть("Даты|Будущее"));
	Области.Вставить("СтрокаОрганизация", Макет.ПолучитьОбласть("Строка|Организация"));
	Области.Вставить("СтрокаНалог", Макет.ПолучитьОбласть("Строка|Налог"));
	Области.Вставить("СтрокаСрок", Макет.ПолучитьОбласть("Строка|Срок"));
	Области.Вставить("СтрокаБудущее", Макет.ПолучитьОбласть("Строка|Будущее"));
	Области.Вставить("НеТребуетсяСрок", Макет.ПолучитьОбласть("НеТребуется|Срок"));
	Области.Вставить("НеТребуетсяБудущее", Макет.ПолучитьОбласть("НеТребуется|Будущее"));
	Области.Вставить("ПодвалОрганизация", Макет.ПолучитьОбласть("Подвал|Организация"));
	Области.Вставить("ПодвалНалог", Макет.ПолучитьОбласть("Подвал|Налог"));
	Области.Вставить("ПодвалСрок", Макет.ПолучитьОбласть("Подвал|Срок"));
	Области.Вставить("ПодвалБудущее", Макет.ПолучитьОбласть("Подвал|Будущее"));
	Области.Вставить("НалогОрганизация", Макет.ПолучитьОбласть("НалогСтрока|Организация"));
	Области.Вставить("НалогСрок", Макет.ПолучитьОбласть("НалогСтрока|Срок"));
	Области.Вставить("НалогБудущее", Макет.ПолучитьОбласть("НалогСтрока|Будущее"));
	Области.Вставить("СтатусыНалогов", Макет.ПолучитьОбласть("СтатусыНалогов"));
	Области.Вставить("ЗаголовокПолучениеДанных", Макет.ПолучитьОбласть("Заголовок|ПолучениеДанныхВыполняется"));
	Области.Вставить("ДатыПолучениеДанных", Макет.ПолучитьОбласть("Даты|ПолучениеДанныхВыполняется"));
	Области.Вставить("НалогПолучениеДанных", Макет.ПолучитьОбласть("НалогСтрока|ПолучениеДанныхВыполняется"));
	Области.Вставить("ПолучениеДанныхВыполняется", Макет.ПолучитьОбласть("Строка|ПолучениеДанныхВыполняется"));
	Области.Вставить("ПолучениеДанныхВыполнено", Макет.ПолучитьОбласть("Строка|ПолучениеДанныхВыполнено"));
	Области.Вставить("ПолучениеДанныхНеВыполнено", Макет.ПолучитьОбласть("Строка|ПолучениеДанныхНеВыполнено"));
	Области.Вставить("НалогСтрокаПлатеж", Макет.ПолучитьОбласть("НалогСтрока|Платеж"));
	Области.Вставить("СтрокаПлатеж", Макет.ПолучитьОбласть("Строка|Платеж"));
	
	Возврат Области;
	
КонецФункции

Функция ИмяКолонкиПравилаДокументаСЭДО(Правило)
	
	Возврат ?(ЗначениеЗаполнено(Правило), "Правило", "ИдентификаторТребования");
	
КонецФункции

#КонецОбласти

#КонецЕсли
