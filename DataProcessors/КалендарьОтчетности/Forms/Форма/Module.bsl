
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ДатаНачала,ДатаКонца");
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	СтатусыОтчета = КалендарьОтчетностиКлиентСервер.СтатусыОтчета();
	СохранятьНастрокиОтчета = Истина;
	
	Если Параметры.Свойство("СобытияКалендаря") И Параметры.СобытияКалендаря = "Уведомления" Тогда
		ПоОтчетам = Ложь;
		ПоНалоговымПлатежам = Ложь;
		ПоТребованиямФНС = Ложь;
		ПоДокументамСЭДО = Ложь;
		ПоУведомлениямЕНС = Истина;
		ПоЗарплате = Ложь;
		СохранятьНастрокиОтчета = Ложь;
	Иначе
		КалендарьОтчетностиНастройки = ХранилищеОбщихНастроек.Загрузить("КалендарьОтчетностиНастройки");
		Если ТипЗнч(КалендарьОтчетностиНастройки) <> Тип("Структура") Тогда
			ПоОтчетам = Истина;
			ПоНалоговымПлатежам = Истина;
			ПоТребованиямФНС = Истина;
			ПоДокументамСЭДО = Истина;
			ПоУведомлениямЕНС = Истина;
			ПоЗарплате = Истина;
		Иначе
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, КалендарьОтчетностиНастройки);
		КонецЕсли;
	КонецЕсли;

	НаДату = ТекущаяДатаПользователя();
	Если Не ЗначениеЗаполнено(ДатаНачала) Тогда
		ДатаНачала = ДобавитьМесяц(НачалоМесяца(НаДату), -1);
		ДатаКонца = КонецМесяца(ДобавитьМесяц(НаДату, 1));
	КонецЕсли;
	
	КалендарьОтчетностиПереопределяемый.КалендарьОтчетностиПриСозданииНаСервере(ЭтотОбъект, Параметры, Отказ, СтандартнаяОбработка);
	ПоДокументамСЭДО = ПоДокументамСЭДО И УчетЗарплатыВЭтойПрограмме;
	
	СформироватьНадписьПериод(ЭтотОбъект);
	ЗаполнитьДоступныеОрганизации();
	УстановитьВидимостьЭлементов(ЭтотОбъект);
	
	КалендарьОтчетностиПереопределяемый.СобратьСтатистикуИспользования();
	КалендарьОтчетностиПереопределяемый.ПодключитьКонтекстныеНовости(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьЗаголовокОтборыПоЗадачам();
	СформироватьОтчет();
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ИменаСобытий = Новый Массив();
	ИменаСобытий.Добавить("Запись_УведомлениеОбИсчисленныхСуммахНалогов");
	ИменаСобытий.Добавить("Запись_СведенияОЗастрахованныхЛицахСЗВ_М");
	ИменаСобытий.Добавить("Запись_СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ");
	ИменаСобытий.Добавить("Запись_СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД");
	ИменаСобытий.Добавить("Запись_ЗаявлениеОЗачетеВСчетПредстоящейОбязанности");
	ИменаСобытий.Добавить("Обновить дерево отчетов"); // оповещение при записи изменений настроек налогов и отчетов
	ИменаСобытий.Добавить("Позиционирование в списке отчетов");
	ИменаСобытий.Добавить("Получены новые сообщения 1С-Отчетности");
	ИменаСобытий.Добавить(РегламентированнаяОтчетностьКлиент.ИмяСобытияУспешнаяОтправка());
	ИменаСобытий.Добавить("Завершение обновления");
	ИменаСобытий.Добавить("УспешноеПодтверждение");
	ИменаСобытий.Добавить("Запись_ДокументыРеализацииПолномочийНалоговыхОрганов");
	ИменаСобытий.Добавить(КалендарьОтчетностиКлиентСервер.ИмяСобытияОповещенияСпискаЗадач());
	
	Если ИменаСобытий.Найти(ИмяСобытия) <> Неопределено
		// При расшифровке транспортного сообщения игнорируем событие Запись_РегламентированныйОтчет,
		// т.к. часто происходят избыточные вызовы этого события, РегламентированныйОтчет может при этом не измениться.
		// Изменение его состояния отлавливается в других событиях.
		Или (ИмяСобытия = "Запись_РегламентированныйОтчет" И НЕ (Параметр.Свойство("МетодВызова") И Параметр.МетодВызова = "РасшифроватьТранспортноеСообщение")) 
		Или (ИмяСобытия = РегламентированнаяОтчетностьКлиент.ИмяСобытияЗавершениеОтправки() И ТипЗнч(Параметр.Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов")) Тогда
		
		СформироватьОтчетПриОповещенииФормы(Параметр, Источник)
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	
	Если ПараметрыДействияСОтчетом.СледующееДействие = "ПоказатьПомощник" Тогда
		
		Если ТипЗнч(ПараметрыОтчета.Правило) = Тип("ДокументСсылка.ТранспортноеСообщение") Тогда
			// Расшифровка сообщений выполняется синхронно.
			// После расшифровки будет открыта форма со списком полученных сообщений.
			ВыполнениеЗадачБухгалтераКлиент.РасшифроватьСообщения(ПараметрыОтчета.Организация);
			Возврат;
		КонецЕсли;
		ПредупреждениеОткрытияПомощника = КалендарьОтчетностиКлиент.ПоказатьПомощник(ПараметрыОтчета);
		Если ЗначениеЗаполнено(ПредупреждениеОткрытияПомощника) Тогда
			ПоказатьПредупреждение(, ПредупреждениеОткрытияПомощника);
			Возврат;
		КонецЕсли;
		// При открытии отчета из прошлого, задачи ещё может не быть, создадим её.
		Если Не ПараметрыДействияСОтчетом.ЕстьЗадача Тогда
			СоздатьЗадачуПрошлого();
		КонецЕсли;
	ИначеЕсли ПараметрыДействияСОтчетом.СледующееДействие = "ПерейтиПоСсылке" Тогда
		ПерейтиПоНавигационнойСсылке(ПараметрыДействияСОтчетом.НавигационнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриАктивизации(Элемент)
	
	Если Элемент.ТекущаяОбласть = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Расшифровка = Элемент.ТекущаяОбласть.Расшифровка;
	Если ТипЗнч(Расшифровка) <> Тип("Структура") Тогда
		ПараметрыОтчета = Неопределено;
		ПараметрыДействияСОтчетом = Неопределено;
		Элементы.ГруппаПанель.Видимость = Ложь;
		ЭтотОбъект.ТекущийЭлемент = Элементы.Результат;
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета = Расшифровка.ПараметрыОтчета;
	ПараметрыДействияСОтчетом = Расшифровка.ПараметрыДействияСОтчетом;
	
	Если ЗначениеЗаполнено(ПараметрыДействияСОтчетом.СледующееДействие) Тогда
		ЗаполнитьБоковуюПанель();
	Иначе
		ЗаполнитьБоковуюПанельДоступноВключениеОтчета();
	КонецЕсли;
	
	Если Не БоковаяПанельСкрытаПользователем И Не Элементы.ГруппаПанель.Видимость Тогда
		ПоказатьБоковуюПанель();
	КонецЕсли;
	
	// Вернём фокус обратно на Табличный документ.
	ЭтотОбъект.ТекущийЭлемент = Элементы.Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПоЗадачамПриИзменении(Элемент)
	
	СформироватьЗаголовокОтборыПоЗадачам();
	
	ОтключитьОбработчикОжидания("Подключаемый_ОтборыПоЗадачамПриИзменении");
	ПодключитьОбработчикОжидания("Подключаемый_ОтборыПоЗадачамПриИзменении", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		// Не меняем отбор, если отчет ещё формируется.
		СтандартнаяОбработка = Ложь;
		Возврат
	КонецЕсли;
	
	ПереформироватьКалендарь = Истина;
	КалендарьОтчетностиКлиентПереопределяемый.ОрганизацияОбработкаВыбора(
		ВыбранноеЗначение, ПереформироватьКалендарь, СтандартнаяОбработка);
	
	Если ПереформироватьКалендарь Тогда
		Элементы.ГруппаПанель.Видимость = Ложь;
		ПереформироватьКалендарьОтборПоОрганизации(ВыбранноеЗначение);
	Иначе
		ОткрытьМоиПриложения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	Элементы.ГруппаПанель.Видимость = Ложь;
	ПереформироватьКалендарьОтборПоОрганизации(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаНалоговОтчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИсточникПерехода = ?(ПоказыватьМоиПриложения, "ИзОтборов", "");
	КалендарьОтчетностиКлиентПереопределяемый.ОткрытьНастройкуНалоговОтчетов(Организация, ИсточникПерехода);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПанельНастройкаНалоговОтчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИсточникПерехода = ?(ПоказыватьМоиПриложения, "ИзСайдбараОтчетНеТребуется", "");
	ОткрытьФормуНалогиИОтчетыНаТекущейЗакладке(ИсточникПерехода);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПанельВключениеОтчетаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИсточникПерехода = ?(ПоказыватьМоиПриложения, "ИзСайдбараВключениеОтчета", "");
	ОткрытьФормуНалогиИОтчетыНаТекущейЗакладке(ИсточникПерехода);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПериодНазад(Команда)
	
	ДатаНачала = ДобавитьМесяц(ДатаНачала, -1);
	ДатаКонца = ДобавитьМесяц(ДатаКонца, -1);
	
	СформироватьНадписьПериод(ЭтотОбъект);
	СформироватьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодВперед(Команда)
	
	ДатаНачала = ДобавитьМесяц(ДатаНачала, 1);
	ДатаКонца = ДобавитьМесяц(ДатаКонца, 1);
	
	СформироватьНадписьПериод(ЭтотОбъект);
	СформироватьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьТекстБаннераОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КалендарьОтчетностиКлиентПереопределяемый.ЗаписатьСобытиеСтатистикиПоПоказателям(
		"КалендарьОтчетности.ОткрытыМоиПриложения.ИзБаннера");
	ОткрытьМоиПриложения();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьБаннерНажатие(Элемент)
	
	Элементы.ГруппаБаннер.Видимость = Ложь;
	УстановитьНеПоказыватьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	СформироватьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура МоиПриложения(Команда)
	
	ОткрытьМоиПриложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельТребованийНажатие(Элемент)
	
	КоличествоСрочныхТребованийФНСПредставление = "";
	КоличествоСрочныхДокументовСЭДОПредставление = "";
	ОтобразитьБаннерПоСрочнымТребованиям(
		КоличествоСрочныхТребованийФНСПредставление,
		КоличествоСрочныхДокументовСЭДОПредставление,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаПанельСкрытьНажатие(Элемент)
	
	СкрытьБоковуюПанель();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаПанельПоказатьНажатие(Элемент)
	
	ПоказатьБоковуюПанель();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьСдано(Команда)
	
	ПараметрыДействияСОтчетом.СтатусУстановленВручную = Истина;
	НовыйСтатус = ФинальныйСтатусОтчета(ПараметрыОтчета);
	ОбновитьСтатусОтчета(НовыйСтатус);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьНеТребуется(Команда)
	
	ПараметрыДействияСОтчетом.СтатусУстановленВручную = Истина;
	ОбновитьСтатусОтчета(СтатусыОтчета.НеТребуется);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРучнойСтатус(Команда)
	
	ПараметрыДействияСОтчетом.СтатусУстановленВручную = Ложь;
	ОбновитьСтатусОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПанельКомментарийПриИзменении(Элемент)
	
	ПараметрыДействияСОтчетом.Комментарий = Комментарий;
	
	Если ЗначениеЗаполнено(ПараметрыДействияСОтчетом.Приложение) Тогда
		// Другое подключенное приложение в режиме сервиса.
		ПередатьКомментарийОтчета();
	Иначе
		ЗаписатьКомментарийОтчета();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбратнаяСвязьНажатие(Элемент)

	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьОтзывПоЭлектроннойПочте", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодОдинМесяц(Команда)
	
	ВыборПериода("ТекущийМесяц");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодТриМесяца(Команда)
	
	ВыборПериода("ТриМесяца");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодДругой(Команда)
	
	ПараметрыОткрытия = Новый Структура("ДатаНачала,ДатаОкончания", ДатаНачала, ДатаКонца);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьПослеВыбораДругогоПериода", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаНесколькоМесяцев", ПараметрыОткрытия,,,,,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодПрошлыйГод(Команда)
	
	ВыборПериода("ПрошлыйГод");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодТекущийГод(Команда)
	
	ВыборПериода("ТекущийГод");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтборыПоЗадачамПриИзменении() Экспорт
	
	СформироватьОтчет();
	
	Если СохранятьНастрокиОтчета Тогда
		СохранитьНастройкиОтчета(ПоНалоговымПлатежам, ПоОтчетам, ПоТребованиямФНС, ПоУведомлениямЕНС, ПоДокументамСЭДО, ПоЗарплате, ТекущийПользователь);
	КонецЕсли;
	
	КалендарьОтчетностиКлиентПереопределяемый.СобратьСтатистикуИспользованияОтборов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет()
	
	Элементы.ГруппаПанель.Видимость = Ложь;
	
	Если Не ПоДокументамСЭДО И Не ПоНалоговымПлатежам И Не ПоОтчетам И Не ПоТребованиямФНС И Не ПоУведомлениямЕНС И Не ПоЗарплате Тогда
		Элементы.ГруппаТребований.Видимость = Ложь;
		ОчиститьТабличныйДокумент(Результат);
		Возврат;
	КонецЕсли;
	ПараметрыОтчета = ПолучитьПараметрыОтчета();
	
	Если ПараметрыОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// При повторном формировании отчета предыдущее формирование может быть ещё не завершено,
	// т.е. обработчик ожидания может быть подключен.
	// На такой случай следует принудительно отключить обработчик ожидания.
	ОтключитьОбработчикОжидания("Подключаемый_ОпроситьПодключенныеПриложения");
	
	ДлительнаяОперация = НачатьФормированиеОтчета(ПараметрыОтчета);
	ОжидатьФормированиеОтчета(ДлительнаяОперация);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыОтчета()
	
	// Проверим, выполняется ли сейчас формирование отчета.
	Если ЗначениеЗаполнено(ИдентификаторЗадания)
		И ФоновыеЗадания.НайтиПоУникальномуИдентификатору(
			Новый УникальныйИдентификатор(ИдентификаторЗадания)) <> Неопределено Тогда
			
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыОтчета = Обработки.КалендарьОтчетности.НовыйПараметрыОтчета();
	ЗаполнитьЗначенияСвойств(ПараметрыОтчета, ЭтотОбъект);
	ПараметрыОтчета.Пользователь = ТекущийПользователь;
	
	Возврат ПараметрыОтчета;
КонецФункции

&НаКлиенте
Процедура ОжидатьФормированиеОтчета(ДлительнаяОперация)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатПолученияДанныхДляОтчета", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция НачатьФормированиеОтчета(ПараметрыОтчета)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	СрокОжиданияДанных = ТекущаяУниверсальнаяДата() + ВремяОжиданияПолученияДанных();
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, 
		"Обработки.КалендарьОтчетности.ДанныеДляОтчета", 
		ПараметрыОтчета);
		
	ИдентификаторЗадания = XMLСтрока(ДлительнаяОперация.ИдентификаторЗадания);
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатПолученияДанныхДляОтчета(РезультатФормирования, ДополнительныеПараметры) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	Если РезультатФормирования = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru='Формирование отчета отменено'"));
	ИначеЕсли РезультатФормирования.Статус = "Ошибка" Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru='При формировании отчета возникла ошибка:
			|%1'"), РезультатФормирования.КраткоеПредставлениеОшибки);
		ПоказатьПредупреждение(, ТекстСообщения);
	Иначе //Статус = "Выполнено"
		РезультатВывода = ВывестиПолныйОтчет(РезультатФормирования.АдресРезультата);
		Если РезультатВывода = Неопределено Тогда
			// Подготовленные данные уже неактуальны. Пользователь уже изменил настройки
			// календаря. Следует переформировать отчет.
			СформироватьОтчет();
		ИначеЕсли РезультатВывода = Ложь Тогда
			// Не от всех приложений получены данные. Запланируем повторный опрос.
			ПодключитьОбработчикОжидания("Подключаемый_ОпроситьПодключенныеПриложения", 2, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ОпроситьПодключенныеПриложения()
	
	Если Не ОпроситьПодключенныеПриложения() Тогда
		// Не от всех приложений получены данные. Запланируем повторный опрос.
		ПодключитьОбработчикОжидания("Подключаемый_ОпроситьПодключенныеПриложения", 2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОпроситьПодключенныеПриложения()
	
	ТекущийСчетчик = СчетчикИзмененийДанныхОтчета;
	ДанныеДляОтчета = ПолучитьИзВременногоХранилища(АдресДанныхОтчета);
	Если ДанныеДляОтчета = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запросы = ДанныеДляОтчета.ОжидающиеЗапросы.Скопировать();
	Ответы = Обработки.КалендарьОтчетности.ОпроситьПодключенныеПриложения(Запросы);
	
	ДанныеДляОтчета.ОжидающиеЗапросы = Ответы.ОжидающиеЗапросы.Скопировать();
	
	// За время получения ожидающих запросов мог быть вручную изменен статус какого-либо отчета или его комментарий.
	// Следует перечитать данные из хранилища, если это произошло.
	Если ТекущийСчетчик <> СчетчикИзмененийДанныхОтчета Тогда
		ДанныеДляОтчета = ПолучитьИзВременногоХранилища(АдресДанныхОтчета);
		Если ДанныеДляОтчета = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Ответы.Данные.Количество() Тогда
		// Соберем вместе новые данные и полученные ранее.
		// Перекладывание данных из ДанныеДляОтчета.ДанныеПодключенныхПриложений в Ответы.Данные
		// позволяет избавиться от пустых строк по организациям, по которым ранее не удалось получить данные,
		// а сейчас данные были получены.
		ОрганизацииОтвета = Ответы.Данные.ВыгрузитьКолонку("Организация");
		Для Каждого СтрокаДанных Из ДанныеДляОтчета.ДанныеПодключенныхПриложений Цикл
			Если ОрганизацииОтвета.Найти(СтрокаДанных.Организация) = Неопределено Тогда
				НоваяСтрока = Ответы.Данные.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
			КонецЕсли;
		КонецЦикла;
		// Запишем обратно в ДанныеПодключенныхПриложений полученные ранее и новые данные.
		ДанныеДляОтчета.ДанныеПодключенныхПриложений = Ответы.Данные.Скопировать();
		ДанныеДляОтчета.КоличествоСрочныхТребованийФНС = Обработки.КалендарьОтчетности.КоличествоСрочныхТребованийФНС(
			ДанныеДляОтчета.ДанныеЭтогоПриложения, ДанныеДляОтчета.ДанныеПодключенныхПриложений, НаДату);
		Результат = Обработки.КалендарьОтчетности.СформироватьОтчет(ДанныеДляОтчета, ОрганизацияСсылка(Организация));
		
	КонецЕсли;
	
	Для Каждого Запрос Из Ответы.ОшибкиПриложений Цикл
		ДанныеДляОтчета.ОшибкиПриложений.Добавить(Запрос);
	КонецЦикла;
	АдресДанныхОтчета = ПоместитьВоВременноеХранилище(ДанныеДляОтчета, УникальныйИдентификатор);
	
	Обработки.КалендарьОтчетности.ОтметитьДанныеПолучены(
		Запросы,
		ДанныеДляОтчета.ОжидающиеЗапросы,
		ДанныеДляОтчета.ОшибкиПриложений);
		
	Если ДанныеДляОтчета.ОжидающиеЗапросы.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ТекущаяУниверсальнаяДата() <= СрокОжиданияДанных Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Повторно запрашивать данные не будем. Выведем индикацию ошибки запроса.
	Для Каждого Запрос Из ДанныеДляОтчета.ОжидающиеЗапросы Цикл
		ДанныеДляОтчета.ОшибкиПриложений.Добавить(Запрос.Приложение);
	КонецЦикла;
	ДанныеДляОтчета.ОжидающиеЗапросы.Очистить();
	Результат = Обработки.КалендарьОтчетности.СформироватьОтчет(ДанныеДляОтчета, ОрганизацияСсылка(Организация));
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция ВывестиПолныйОтчет(АдресРезультата)
	
	ДанныеДляОтчета = ПолучитьИзВременногоХранилища(АдресРезультата);
	ДанныеДляОтчета.Вставить("НаДату", НаДату);
	Результат = Обработки.КалендарьОтчетности.СформироватьОтчет(ДанныеДляОтчета, ОрганизацияСсылка(Организация));
	ИдентификаторЗадания = "";
	
	КоличествоСрочныхТребованийФНСПредставление = КоличествоСрочныхТребованийФНСПредставление(ДанныеДляОтчета.КоличествоСрочныхТребованийФНС);
	КоличествоСрочныхДокументовСЭДОПредставление = КоличествоДокументовСЭДОПредставление(ДанныеДляОтчета.КоличествоДокументовСЭДО);
	ОтобразитьБаннерПоСрочнымТребованиям(
		КоличествоСрочныхТребованийФНСПредставление,
		КоличествоСрочныхДокументовСЭДОПредставление,
		ЭтотОбъект);
	
	// Проверим, не изменились ли настройки календаря пока фоновое задание готовило данные.
	Если ДанныеДляОтчета.Параметры.ДатаНачала <> ДатаНачала Или
		ДанныеДляОтчета.Параметры.ПоОтчетам <> ПоОтчетам Или
		ДанныеДляОтчета.Параметры.ПоНалоговымПлатежам <> ПоНалоговымПлатежам Или
		ДанныеДляОтчета.Параметры.ПоУведомлениямЕНС <> ПоУведомлениямЕНС Или
		ДанныеДляОтчета.Параметры.ПоДокументамСЭДО <> ПоДокументамСЭДО Или
		ДанныеДляОтчета.Параметры.ПоТребованиямФНС <> ПоТребованиямФНС Или
		ДанныеДляОтчета.Параметры.ПоЗарплате <> ПоЗарплате Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	АдресДанныхОтчета = ПоместитьВоВременноеХранилище(ДанныеДляОтчета, УникальныйИдентификатор);
	
	// Отчет является полностью сформированным, если получены данные от всех подключенных приложений.
	Возврат ДанныеДляОтчета.ОжидающиеЗапросы.Количество() = 0;
	
КонецФункции

&НаСервере
Функция КоличествоСрочныхТребованийФНСПредставление(Состояние)
	
	Если ТипЗнч(Состояние) <> Тип("Структура") Тогда
		Возврат "";
	КонецЕсли;
	ШаблонПодтверждений = НСтр("ru=';%1 подтверждение о приеме;;%1 подтверждения о приеме;%1 подтверждений о приеме;%1 подтверждений о приеме'");
	ШаблонОтвет = НСтр("ru=';%1 ответ;;%1 ответа;%1 ответов;%1 ответов'");
	
	ПредставлениеПодтверждений = ДокументооборотСКОКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонПодтверждений, Состояние.Подтвердить);
	ПредставлениеОтветов = ДокументооборотСКОКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонОтвет, Состояние.БезОтвета);
	
	Если Состояние.Подтвердить > 0 И Состояние.БезОтвета > 0 Тогда
		Тело = ПредставлениеПодтверждений + НСтр("ru = ' и '") + ПредставлениеОтветов;
	ИначеЕсли Состояние.Подтвердить > 0 И Состояние.БезОтвета = 0 Тогда
		Тело = ПредставлениеПодтверждений;
	ИначеЕсли Состояние.Подтвердить = 0  И Состояние.БезОтвета > 0 Тогда
		Тело = ПредставлениеОтветов;
	Иначе
		Возврат "";
	КонецЕсли;
	
	Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Требования ФНС: Отправьте '"),
			Тело,
			НСтр("ru = ' в ближайшее время. '"));
			
	Возврат Текст;

КонецФункции

&НаСервере
Функция КоличествоДокументовСЭДОПредставление(Состояние)
	
	Если ТипЗнч(Состояние) <> Тип("Структура") Тогда
		Возврат "";
	КонецЕсли;
	ШаблонПодтверждений = НСтр("ru=';%1 подтверждение о приеме;;%1 подтверждения о приеме;%1 подтверждений о приеме;%1 подтверждений о приеме'");
	ШаблонОтвет = НСтр("ru=';%1 ответ;;%1 ответа;%1 ответов;%1 ответов'");
	
	ПредставлениеПодтверждений = ДокументооборотСКОКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонПодтверждений, Состояние.Подтвердить);
	ПредставлениеОтветов = ДокументооборотСКОКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(ШаблонОтвет, Состояние.БезОтвета);
	
	Если Состояние.Подтвердить > 0 И Состояние.БезОтвета > 0 Тогда
		Тело = ПредставлениеПодтверждений + НСтр("ru = ' и '") + ПредставлениеОтветов;
	ИначеЕсли Состояние.Подтвердить > 0 И Состояние.БезОтвета = 0 Тогда
		Тело = ПредставлениеПодтверждений;
	ИначеЕсли Состояние.Подтвердить = 0  И Состояние.БезОтвета > 0 Тогда
		Тело = ПредставлениеОтветов;
	Иначе
		Возврат "";
	КонецЕсли;
	
	Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Сообщения СЭДО: Отправьте '"),
			Тело,
			НСтр("ru = ' в ближайшее время. '"));
			
	Возврат Текст;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВремяОжиданияПолученияДанных()
	
	ВремяОжидания = 10; // время ожидания получения данных от других приложений по умолчанию(в секундах).
	
	КалендарьОтчетностиПереопределяемый.ЗаполнитьВремяОжиданияПолученияДанных(ВремяОжидания);
	
	Возврат ВремяОжидания;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьНадписьПериод(Форма)
	
	ДатаНачала = Форма.ДатаНачала;
	ДатаКонца = Форма.ДатаКонца;
	Если ДатаНачала = НачалоМесяца(ДатаКонца) Тогда
		ТекстНадписи = Формат(ДатаНачала, "ДФ=""ММММ гггг 'г.'""");
	ИначеЕсли Год(ДатаНачала) = Год(ДатаКонца) Тогда
		ТекстНадписи = СтрШаблон("%1 - %2", 
			Формат(ДатаНачала, "ДФ=ММММ"), Формат(ДатаКонца, "ДФ=""ММММ гггг 'г.'"""));
	Иначе
		ТекстНадписи = СтрШаблон("%1 - %2", 
			Формат(ДатаНачала, "ДФ=""ММММ гггг 'г.'"""), Формат(ДатаКонца, "ДФ=""ММММ гггг 'г.'"""));
	КонецЕсли;
	Форма.Элементы.ГруппаВыбратьПериод.Заголовок = ТекстНадписи;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьЭлементов(Форма)
	
	Форма.Элементы.КнопкаМоиПриложения.Видимость = Форма.ПоказыватьМоиПриложения;
	Если Не Форма.ПоказыватьМоиПриложения Или Не ПоказыватьБаннер() Тогда
		Форма.Элементы.ГруппаБаннер.Видимость = Ложь;
	КонецЕсли;
	Форма.Элементы.ГруппаТребований.Видимость = Ложь;
	Форма.Элементы.ГруппаПанель.Видимость = Ложь;
	Форма.Элементы.КартинкаПанельПоказать.Видимость = Ложь;
	Организация = Форма.Элементы.Организация;
	Организация.Видимость = Организация.СписокВыбора.Количество() > 1 Или Форма.ПоказыватьМоиПриложения;
	Форма.Элементы.ДекорацияНастройкаНалоговОтчетов.Видимость = Форма.ПравоНастройкиНалоговОтчетов;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекущаяДатаПользователя()
	
	Возврат ОбщегоНазначения.ТекущаяДатаПользователя();
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьНеПоказыватьБаннер()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("КалендарьОтчетности", "ПоказыватьБаннер", Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоказыватьБаннер()
	
	ПоказыватьБаннер = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("КалендарьОтчетности", "ПоказыватьБаннер");
	Возврат ?(ПоказыватьБаннер = Неопределено, Истина, ПоказыватьБаннер);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПослеЗакрытияМоиПриложения(РезультатЗакрытия, ПараметрыОповещения) Экспорт
	
	Если Элементы.ГруппаБаннер.Видимость Тогда
		Элементы.ГруппаБаннер.Видимость = Ложь;
		УстановитьНеПоказыватьБаннер();
	КонецЕсли;
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		МоиПриложенияКонтрольнаяСумма = РезультатЗакрытия.МоиПриложенияКонтрольнаяСумма;
		ЗаполнитьДоступныеОрганизации();
		СформироватьОтчет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМоиПриложения()
	
	КалендарьОтчетностиКлиентПереопределяемый.ОткрытьФормуМоиПриложения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьБаннерПоСрочнымТребованиям(КоличествоСрочныхТребованийФНСПредставление, КоличествоСрочныхДокументовСЭДОПредставление, Форма)
	
	Элементы = Форма.Элементы;
	
	ПанельТребованийЗаголовок = "";
	Если ЗначениеЗаполнено(КоличествоСрочныхТребованийФНСПредставление)
		И ЗначениеЗаполнено(КоличествоСрочныхДокументовСЭДОПредставление) Тогда
		ЧастиБанера = Новый Массив;
		ЧастиБанера.Добавить(КоличествоСрочныхТребованийФНСПредставление);
		ЧастиБанера.Добавить(" ");
		ЧастиБанера.Добавить(КоличествоСрочныхДокументовСЭДОПредставление);
		ПанельТребованийЗаголовок = Новый ФорматированнаяСтрока(ЧастиБанера);
	ИначеЕсли ЗначениеЗаполнено(КоличествоСрочныхТребованийФНСПредставление) Тогда
		ПанельТребованийЗаголовок = КоличествоСрочныхТребованийФНСПредставление;
	ИначеЕсли ЗначениеЗаполнено(КоличествоСрочныхДокументовСЭДОПредставление) Тогда
		ПанельТребованийЗаголовок = КоличествоСрочныхДокументовСЭДОПредставление;
	КонецЕсли;
	
	Элементы.ПанельТребований.Заголовок = ПанельТребованийЗаголовок;
	Элементы.ГруппаТребований.Видимость = (ЗначениеЗаполнено(КоличествоСрочныхТребованийФНСПредставление) И Форма.ПоТребованиямФНС)
		Или (ЗначениеЗаполнено(КоличествоСрочныхДокументовСЭДОПредставление) И Форма.ПоДокументамСЭДО);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиОтчета(ПоНалоговымПлатежам, ПоОтчетам, ПоТребованиямФНС, ПоУведомлениямЕНС, ПоДокументамСЭДО, ПоЗарплате, ТекущийПользователь)
	
	КалендарьОтчетностиНастройки = Новый Структура;
	КалендарьОтчетностиНастройки.Вставить("ПоНалоговымПлатежам", ПоНалоговымПлатежам);
	КалендарьОтчетностиНастройки.Вставить("ПоОтчетам",           ПоОтчетам);
	КалендарьОтчетностиНастройки.Вставить("ПоТребованиямФНС",    ПоТребованиямФНС);
	КалендарьОтчетностиНастройки.Вставить("ПоУведомлениямЕНС",   ПоУведомлениямЕНС);
	КалендарьОтчетностиНастройки.Вставить("ПоДокументамСЭДО",    ПоДокументамСЭДО);
	КалендарьОтчетностиНастройки.Вставить("ПоЗарплате",          ПоЗарплате);
	ХранилищеОбщихНастроек.Сохранить("КалендарьОтчетностиНастройки",, КалендарьОтчетностиНастройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьТабличныйДокумент(Результат)
	
	Результат.Очистить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФинальныйСтатусОтчета(ПараметрыОтчета)
	
	Возврат ВыполнениеЗадачБухгалтера.ФинальныйСтатусЗадачи(ПараметрыОтчета);
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСтатусОтчета(НовыйСтатус = "")
	
	ПараметрыДействияСОтчетом.Статус = НовыйСтатус;
	
	Если ЗначениеЗаполнено(ПараметрыДействияСОтчетом.Приложение) Тогда
		// Другое подключенное приложение в режиме сервиса.
		ПередатьРучнойСтатусОтчета();
		Возврат;
	КонецЕсли;
	
	// Это прикладное приложение.
	ЗаписатьРучнойСтатусОтчета();
	ПеречитатьРасшифровкуЯчейкиЗаполнитьБоковуюПанель();
	
	Оповестить(
		КалендарьОтчетностиКлиентСервер.ИмяСобытияОповещенияКалендаряОтчетности(),
		Новый Структура);
КонецПроцедуры

#Область ОтображениеБоковойПанели

&НаКлиенте
Процедура ПеречитатьРасшифровкуЯчейкиЗаполнитьБоковуюПанель()
	
	Расшифровка = Элементы.Результат.ТекущаяОбласть.Расшифровка;
	Если ЗначениеЗаполнено(Расшифровка) Тогда
		ПараметрыОтчета = Расшифровка.ПараметрыОтчета;
		ПараметрыДействияСОтчетом = Расшифровка.ПараметрыДействияСОтчетом;
	КонецЕсли;
	
	ЗаполнитьБоковуюПанель();
	ЭтотОбъект.ТекущийЭлемент = Элементы.Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПояснениеДляДокументаСЭДОНаБоковойПанели()
	
	КалендарьОтчетностиКлиентПереопределяемый.ЗаполнитьПояснениеДляДокументаСЭДОНаБоковойПанели(ЭтотОбъект, ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьБоковуюПанель()
	
	Если ТипЗнч(ПараметрыДействияСОтчетом) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоЗадач = ПараметрыОтчета.КоличествоЗадач;
	
	Элементы.ДекорацияПанельНаименованиеОтчета.Заголовок = ПараметрыДействияСОтчетом.Наименованиеотчета;
	Элементы.ДекорацияПанельОрганизация.Заголовок = ПараметрыДействияСОтчетом.НаименованиеОрганизации;
	СрокСдачи = Формат(ПараметрыДействияСОтчетом.Срок, "ДФ='dd MMMM'");
	Элементы.ДекорацияПанельСрокСдачи.Заголовок = СтрШаблон(НСтр("ru='Срок сдачи до %1'"), СрокСдачи);
	Комментарий = ПараметрыДействияСОтчетом.Комментарий;
	
	ВыводитьСообщение = Ложь;
	ВыводитьСтатус = Истина;
	ВыводитьУстановкуСтатусаВручную = Истина;
	ВыводитьОтменуСтатусаВручную = Ложь;
	ВыводитьКнопкуОткрытия = Истина;
	ВыводитьПолеКомментария = Истина;
	ВыводитьПереходВДругоеПриложение = Ложь;
	ВыводитьПереходВНастройки = Ложь; // переход в форму Настройка налогов и отчетов
	
	ДействиеПлатеж = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.УплатаНалога");
	ДействиеЗарплата = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.Зарплата");
	ЭтоПлатеж = (ПараметрыОтчета.Действие = ДействиеПлатеж Или ПараметрыОтчета.Действие = ДействиеЗарплата);
	
	Если ПараметрыДействияСОтчетом.СледующееДействие = "ПоказатьПомощник" Тогда
		
		Элементы.ДекорацияТекущийСтатус.Заголовок = ПараметрыДействияСОтчетом.Статус;
		Элементы.КартинкаПанельТекущийСтатус.Картинка = ПараметрыДействияСОтчетом.КартинкаСтатуса;
		ВыводитьОтменуСтатусаВручную = ПараметрыДействияСОтчетом.СтатусУстановленВручную;
		ДоступноУправлениеОтчетом = КалендарьОтчетностиКлиентСервер.ДоступноУправлениеОтчетом(
			ПараметрыОтчета.ИдентификаторЗадачи,
			ПараметрыОтчета.ИдентификаторПравила);
		ВыводитьПереходВНастройки = ДоступноУправлениеОтчетом И
			ПараметрыДействияСОтчетом.Статус = СтатусыОтчета.НеТребуется;
		
	ИначеЕсли ПараметрыДействияСОтчетом.СледующееДействие = "ПоказатьСообщение" Тогда
		
		Элементы.ДекорацияПанельСообщение.Заголовок = ПараметрыДействияСОтчетом.ТекстСообщения;
		ВыводитьСообщение = Истина;
		ВыводитьСтатус = Ложь;
		ВыводитьКнопкуОткрытия = Ложь;
		
	ИначеЕсли ПараметрыДействияСОтчетом.СледующееДействие = "ПерейтиПоСсылке" Тогда
		
		Элементы.ДекорацияТекущийСтатус.Заголовок = ПараметрыДействияСОтчетом.Статус;
		Элементы.КартинкаПанельТекущийСтатус.Картинка = ПараметрыДействияСОтчетом.КартинкаСтатуса;
		ВыводитьОтменуСтатусаВручную = ПараметрыДействияСОтчетом.СтатусУстановленВручную;
		ВыводитьПереходВДругоеПриложение = Истина;
		
	КонецЕсли;
	
	Если ЭтоТребованиеФНС(ПараметрыОтчета.Правило) Тогда
		ВыводитьУстановкуСтатусаВручную = Ложь;
	КонецЕсли;
	
	Если ПараметрыОтчета.ИдентификаторЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиДокументСЭДО() Тогда
		ВыводитьУстановкуСтатусаВручную = Ложь;
		Элементы.ДекорацияПанельСрокСдачи.Заголовок = СтрШаблон(НСтр("ru='Срок выполнения до %1'"), СрокСдачи);
	КонецЕсли;
	
	Если ПараметрыОтчета.Действие = ДействиеЗарплата Тогда
		Элементы.ДекорацияПанельСрокСдачи.Заголовок = СтрШаблон(НСтр("ru='Срок выплаты до %1'"), СрокСдачи);
	КонецЕсли;
	
	Если ПараметрыДействияСОтчетом.РежимСовместимости Тогда
		ВыводитьУстановкуСтатусаВручную = Ложь;
		ВыводитьПолеКомментария = Ложь;
	КонецЕсли;
	
	СтатусОтчета = ПараметрыДействияСОтчетом.Статус;
	
	Если ВыводитьКнопкуОткрытия Тогда
		Если ВыводитьПереходВДругоеПриложение Тогда
			Элементы.КнопкаПанельОткрытьОтчет.Заголовок = НСтр("ru='Открыть приложение'");
		ИначеЕсли СтатусОтчета = СтатусыОтчета.Просрочено Или СтатусОтчета = СтатусыОтчета.НеСдано Тогда
			ШаблонЗаголовка = НСтр("ru='Подготовить %1'");
		Иначе
			ШаблонЗаголовка = НСтр("ru='Открыть %1'");
		КонецЕсли;
		
		Если Не ПустаяСтрока(ШаблонЗаголовка) Тогда
			ДействиеУведомление = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.Уведомление");
			ДействиеТребованиеФНС = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.ТребованиеФНС");
			ДействиеДокументСЭДО = ПредопределенноеЗначение("Перечисление.ВидыДействийКалендаряБухгалтера.ДокументСЭДО");
			
			Если ПараметрыОтчета.Действие = ДействиеУведомление Тогда
				Элементы.КнопкаПанельОткрытьОтчет.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru='уведомление'"));
			ИначеЕсли ПараметрыОтчета.Действие = ДействиеТребованиеФНС Тогда
				Если ТипЗнч(ПараметрыОтчета.Правило) = Тип("ДокументСсылка.ТранспортноеСообщение") Тогда
					Элементы.КнопкаПанельОткрытьОтчет.Заголовок = НСтр("ru='Расшифровать'");
				Иначе
					Элементы.КнопкаПанельОткрытьОтчет.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru='требование'"));
				КонецЕсли;
			ИначеЕсли ПараметрыОтчета.Действие = ДействиеДокументСЭДО Тогда
				Если КоличествоЗадач = 1 Тогда
					Элементы.КнопкаПанельОткрытьОтчет.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru='документ'"));
				Иначе
					Элементы.КнопкаПанельОткрытьОтчет.Заголовок = СтрШаблон(ШаблонЗаголовка,
						СтрШаблон(НСтр("ru='список документов (%1 шт.)'"), КоличествоЗадач));
				КонецЕсли;
			ИначеЕсли ЭтоПлатеж Тогда
				Элементы.КнопкаПанельОткрытьОтчет.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru='платеж'"));
			Иначе
				Элементы.КнопкаПанельОткрытьОтчет.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru='отчет'"));
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтатусОтчета = СтатусыОтчета.Сдано Или СтатусОтчета = СтатусыОтчета.Оплачено Тогда
		ВыводитьУстановкуСтатусаВручную = Ложь;
	КонецЕсли;
	
	Элементы.КнопкаПанельОтметитьСдано.Заголовок =
		?(ЭтоПлатеж, НСтр("ru='Отметить оплачено'"), НСтр("ru='Отметить сдано'"));
		
	Элементы.КнопкаПанельОтметитьНеТребуется.Заголовок =
		?(ЭтоПлатеж, НСтр("ru='Не требуется оплачивать'"), НСтр("ru='Не требуется сдавать'"));
	
	Элементы.ДекорацияПанельСообщение.Видимость = ВыводитьСообщение;
	Элементы.ДекорацияПанельСтатусЗаголовок.Видимость = ВыводитьСтатус;
	Элементы.ГруппаПанельСтатус1.Видимость = ВыводитьСтатус;
	Элементы.КнопкаПанельОтметитьСдано.Видимость = ВыводитьУстановкуСтатусаВручную И Не ВыводитьОтменуСтатусаВручную;
	Элементы.КнопкаПанельОтметитьНеТребуется.Видимость = ВыводитьУстановкуСтатусаВручную И Не ВыводитьОтменуСтатусаВручную;
	Элементы.КнопкаПанельОтменитьСтатусВручную.Видимость = ВыводитьОтменуСтатусаВручную;
	Элементы.ПолеПанельКомментарий.Видимость = ВыводитьПолеКомментария;
	Элементы.КнопкаПанельОткрытьОтчет.Видимость = ВыводитьКнопкуОткрытия;
	Элементы.ДекорацияПанельПереходВПриложение.Видимость = ВыводитьПереходВДругоеПриложение;
	Элементы.ДекорацияПанельНастройкаНалоговОтчетов.Видимость = ВыводитьПереходВНастройки И ПравоНастройкиНалоговОтчетов;
	Элементы.ДекорацияПанельВключениеОтчета.Видимость = Ложь;
	Элементы.ДекорацияПанельСтатусЗаголовок.Видимость = Истина;
	Элементы.ДекорацияПанельСрокСдачи.Видимость = Истина;
	
	ЗаполнитьПояснениеДляДокументаСЭДОНаБоковойПанели();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьБоковуюПанельДоступноВключениеОтчета()
	
	Элементы.ДекорацияТекущийСтатус.Заголовок = ПараметрыДействияСОтчетом.Статус;
	Элементы.КартинкаПанельТекущийСтатус.Картинка = ПараметрыДействияСОтчетом.КартинкаСтатуса;
	Элементы.ДекорацияПанельНаименованиеОтчета.Заголовок = ПараметрыДействияСОтчетом.КраткоеНаименованиеотчета;
	Элементы.ДекорацияПанельОрганизация.Заголовок = ПараметрыДействияСОтчетом.НаименованиеОрганизации;
	
	Элементы.ГруппаПанельСтатус1.Видимость = Ложь;
	Элементы.ПолеПанельКомментарий.Видимость = Ложь;
	Элементы.ДекорацияПанельПереходВПриложение.Видимость = Ложь;
	Элементы.КнопкаПанельОткрытьОтчет.Видимость = Ложь;
	Элементы.ДекорацияПанельВключениеОтчета.Видимость = Истина;
	Элементы.ДекорацияПанельНастройкаНалоговОтчетов.Видимость = Ложь;
	Элементы.ДекорацияПанельСтатусЗаголовок.Видимость = Ложь;
	Элементы.ДекорацияПанельСрокСдачи.Видимость = Ложь;
	Элементы.ДекорацияПанельОтчетИнформация.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьБоковуюПанель()
	
	БоковаяПанельСкрытаПользователем = Истина;
	Элементы.ГруппаПанель.Видимость = Ложь;
	Элементы.КартинкаПанельПоказать.Видимость = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБоковуюПанель()
	
	БоковаяПанельСкрытаПользователем = Ложь;
	Элементы.ГруппаПанель.Видимость = Истина;
	Элементы.КартинкаПанельПоказать.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаписатьРучнойСтатусОтчета()
	
	Если ПараметрыДействияСОтчетом.СтатусУстановленВручную Тогда
		ВыполнениеЗадачБухгалтера.ЗаписатьРучнойСтатусЗадачи(
			ПараметрыОтчета,
			ПараметрыДействияСОтчетом.Статус);
	Иначе
		ВыполнениеЗадачБухгалтера.ВернутьАвтоматическийСтатусЗадачи(ПараметрыОтчета);
	КонецЕсли;
	
	ПереформироватьКалендарьРучнойСтатус(Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьРучнойСтатусОтчета()
	
	ДлительнаяОперация = ПередатьРучнойСтатусОтчетаСервер();
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ИзмененСтатус", Истина);
	ПараметрыОповещения.Вставить("ОтменаРучногоСтатуса", Не ПараметрыДействияСОтчетом.СтатусУстановленВручную);
	ПараметрыОповещения.Вставить("ИзмененКомментарий", Ложь);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбработатьРезультатПередачиСтатусаКомментария",
		ЭтотОбъект,
		ПараметрыОповещения);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ПередатьРучнойСтатусОтчетаСервер()
	
	ДлительнаяОперация = Неопределено;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	КалендарьОтчетностиПереопределяемый.ПередатьСтатусОтчета(
		ПараметрыДействияСОтчетом, ДлительнаяОперация, ПараметрыВыполнения);
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервере
Процедура ЗаписатьКомментарийОтчета()
	
	ВыполнениеЗадачБухгалтера.ЗаписатьКомментарийЗадачи(ПараметрыОтчета, Комментарий);
	Для Каждого ПодчиненноеПравило Из ПараметрыОтчета.ПодчиненныеПравила Цикл
		ПараметрыПравила = КалендарьОтчетностиКлиентСервер.НовыйПараметрыОтчета();
		ЗаполнитьЗначенияСвойств(ПараметрыПравила, ПараметрыОтчета);
		ПараметрыПравила.Правило = ПодчиненноеПравило;
		ВыполнениеЗадачБухгалтера.ЗаписатьКомментарийЗадачи(ПараметрыПравила, Комментарий);
	КонецЦикла;
	
	ПереформироватьКалендарьРучнойСтатус(Ложь, Истина);
	КалендарьОтчетностиПереопределяемый.ПриЗаписиКомментарияОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьКомментарийОтчета()
	
	ДлительнаяОперация = ПередатьКомментарийОтчетаСервер();
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ИзмененСтатус", Ложь);
	ПараметрыОповещения.Вставить("ОтменаРучногоСтатуса", Ложь);
	ПараметрыОповещения.Вставить("ИзмененКомментарий", Истина);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбработатьРезультатПередачиСтатусаКомментария",
		ЭтотОбъект,
		ПараметрыОповещения);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
	ЗаписатьСобытиеДобавленКомментарий();
	
КонецПроцедуры

&НаСервере
Функция ПередатьКомментарийОтчетаСервер()
	
	ДлительнаяОперация = Неопределено;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	КалендарьОтчетностиПереопределяемый.ПередатьКомментарийОтчета(
		ПараметрыДействияСОтчетом, ДлительнаяОперация, ПараметрыВыполнения);
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатПередачиСтатусаКомментария(РезультатФормирования, ДополнительныеПараметры) Экспорт
	
	Если РезультатФормирования = Неопределено Или РезультатФормирования.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПередачи = ПолучитьИзВременногоХранилища(РезультатФормирования.АдресРезультата);
	
	Успешно = Ложь;
	Если ТипЗнч(РезультатПередачи) = Тип("Структура") И РезультатПередачи.Свойство("Ответ") Тогда
		Если РезультатПередачи.Ответ.Записан Тогда
			Успешно = Истина;
			
			// В случае отмены ручного статуса в ответе другого приложения будет статус, который был
			// назначен отчету автоматически.
			Если ДополнительныеПараметры.ИзмененСтатус И ДополнительныеПараметры.ОтменаРучногоСтатуса Тогда
				ПараметрыДействияСОтчетом.Статус = РезультатПередачи.Ответ.СтатусОтчета;
				ПараметрыДействияСОтчетом.СтатусУстановленВручную = Ложь;
			КонецЕсли;
			ПереформироватьКалендарьРучнойСтатус(
				ДополнительныеПараметры.ИзмененСтатус,
				ДополнительныеПараметры.ИзмененКомментарий);
			
			ПеречитатьРасшифровкуЯчейкиЗаполнитьБоковуюПанель();
		Иначе
			ИмяСобытия = "КалендарьОтчетностиРучнойСтатус";
			ТекстОшибки = СтрШаблон(
				НСтр("ru='Не удалось передать данные приложению %1 по причине: %2'"),
				ПараметрыДействияСОтчетом.АдресПриложения,
				РезультатПередачи.Ответ.Причина);
			
			ЗаписатьОшибку(ИмяСобытия, ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если Не Успешно Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось передать данные. Ошибка записана в Журнал регистрации.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьОшибку(ИмяСобытия, ТекстОшибки)
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Ошибка,
		,
		,
		ТекстОшибки);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоТребованиеФНС(ПравилоПодготовкиОтчета)
	
	ТипПравила = ТипЗнч(ПравилоПодготовкиОтчета);
	Возврат ТипПравила = Тип("ДокументСсылка.ТранспортноеСообщение")
		Или ТипПравила = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов");
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтчетПриОповещенииФормы(ПараметрыОповещения, ИсточникОповещения)
	
	Если ТипЗнч(ПараметрыОповещения) <> Тип("Структура") Тогда
		СформироватьОтчет();
		Возврат;
	КонецЕсли;
	
	ОтчетПереформирован = Ложь;
	Если ПараметрыОповещения.Свойство("ПараметрыПоискаОтчета") Тогда
		// Оповещение пришло из Списка задач бухгалтера.
		ОтчетПереформирован = ПереформироватьКалендарьИзСпискаЗадач(ПараметрыОповещения);
	Иначе
		// Оповещение при записи регламентированного отчета. Ссылка на документ может быть передана как
		// в ПараметрыОповещения, так и ИсточникОповещения.
		ДокументОтчет = ?(ПараметрыОповещения.Свойство("Ссылка"), ПараметрыОповещения.Ссылка, ИсточникОповещения);
		Если ЗначениеЗаполнено(ДокументОтчет) Тогда
			ОтчетПереформирован = ПереформироватьКалендарьПриЗаписиОтчета(ДокументОтчет);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОтчетПереформирован Тогда
		ПеречитатьРасшифровкуЯчейкиЗаполнитьБоковуюПанель();
	Иначе
		СформироватьОтчет();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПереформироватьКалендарьРучнойСтатус(ИзмененСтатус = Ложь, ИзмененКомментарий = Ложь)
	
	// Переформировывает Календарть отчетности, если в сайдбаре был изменен статус или введен комментарий.
	
	Если Не ИзмененСтатус И Не ИзмененКомментарий Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляОтчета = ПолучитьИзВременногоХранилища(АдресДанныхОтчета);
	Если ДанныеДляОтчета = Неопределено Или ПараметрыОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = КалендарьОтчетностиКлиентСервер.НовыйПараметрыПоискаОтчетаВСервисе();
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ПараметрыОтчета);
	ПараметрыПоиска.РегистрацияФНС = ПараметрыОтчета.РегистрацияВНалоговомОргане;
	ПараметрыПоиска.КодНалога = ПараметрыОтчета.ИдентификаторЗадачи;
	// Для документов СЭДО может быть несколько задач в одной ячейке
	Если ПараметрыОтчета.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО Тогда
		ПараметрыПоиска = КалендарьОтчетностиКлиентСервер.ПараметрыПоискаДокументовСЭДО(ПараметрыОтчета, Истина);
	КонецЕсли;
	
	СтрокиДанных = ДанныеДляОтчета.ДанныеЭтогоПриложения.НайтиСтроки(ПараметрыПоиска);
	Если Не СтрокиДанных.Количество() Тогда
		// Для документов СЭДО может быть несколько задач в одной ячейке
		Если ПараметрыОтчета.Действие = Перечисления.ВидыДействийКалендаряБухгалтера.ДокументСЭДО Тогда
			ПараметрыПоиска = КалендарьОтчетностиКлиентСервер.ПараметрыПоискаДокументовСЭДО(ПараметрыОтчета, Ложь);
		КонецЕсли;
		СтрокиДанных = ДанныеДляОтчета.ДанныеПодключенныхПриложений.НайтиСтроки(ПараметрыПоиска);
	КонецЕсли;
	
	Если СтрокиДанных.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	СчетчикИзмененийДанныхОтчета = СчетчикИзмененийДанныхОтчета + 1;
	
	СтрокаДанных = СтрокиДанных[0];
	Если ИзмененСтатус Тогда
		СтрокаДанных.Статус = ПараметрыДействияСОтчетом.Статус;
		СтрокаДанных.СтатусУстановленВручную = ПараметрыДействияСОтчетом.СтатусУстановленВручную;
	КонецЕсли;
	
	Если ИзмененКомментарий Тогда
		СтрокаДанных.Комментарий = ПараметрыДействияСОтчетом.Комментарий;
	КонецЕсли;
	
	АдресДанныхОтчета = ПоместитьВоВременноеХранилище(ДанныеДляОтчета, УникальныйИдентификатор);
	Результат = Обработки.КалендарьОтчетности.СформироватьОтчет(ДанныеДляОтчета, ОрганизацияСсылка(Организация));
	
КонецПроцедуры

&НаСервере
Процедура ПереформироватьКалендарьОтборПоОрганизации(ОрганизацияОтбор)
	
	Если Не ЗначениеЗаполнено(АдресДанныхОтчета) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляОтчета = ПолучитьИзВременногоХранилища(АдресДанныхОтчета);
	Если ДанныеДляОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Обработки.КалендарьОтчетности.СформироватьОтчет(ДанныеДляОтчета, ОрганизацияСсылка(ОрганизацияОтбор));
	
КонецПроцедуры

&НаСервере
Функция ПереформироватьКалендарьПриЗаписиОтчета(ДокументОтчет)
	
	// Переформировывает Календарть отчетности, если был записан регламентированный отчет и
	// Календарь отчетности получил оповещение о записи.
	
	// Попробуем получить задачу по регламентированному отчету, чтобы обновить её статус.
	ТаблицаЗадач = ВыполнениеЗадачБухгалтера.ЗадачиСвязанныеСОтчетом(ДокументОтчет);
	Если ТипЗнч(ТаблицаЗадач) <> Тип("ТаблицаЗначений") Или ТаблицаЗадач.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	ВыполнениеЗадачБухгалтера.ДобавитьСтатусыЗадач(ТаблицаЗадач);
	
	ДанныеДляОтчета = ПолучитьИзВременногоХранилища(АдресДанныхОтчета);
	Если ДанныеДляОтчета = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Задача = ТаблицаЗадач[0];
	ПараметрыПоиска = КалендарьОтчетностиКлиентСервер.НовыйПараметрыПоискаОтчета();
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, Задача);
	ПараметрыПоиска.РегистрацияФНС = Задача.РегистрацияВНалоговомОргане;
	
	СтрокиДанных = ДанныеДляОтчета.ДанныеЭтогоПриложения.НайтиСтроки(ПараметрыПоиска);
	
	Если СтрокиДанных.Количество() <> 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СчетчикИзмененийДанныхОтчета = СчетчикИзмененийДанныхОтчета + 1;
	
	СтрокаДанных = СтрокиДанных[0];
	СтрокаДанных.Статус = Задача.Статус;
	СтрокаДанных.СтатусУстановленВручную = Задача.СтатусУстановленВручную;
	
	АдресДанныхОтчета = ПоместитьВоВременноеХранилище(ДанныеДляОтчета, УникальныйИдентификатор);
	Результат = Обработки.КалендарьОтчетности.СформироватьОтчет(ДанныеДляОтчета, ОрганизацияСсылка(Организация));
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция ПереформироватьКалендарьИзСпискаЗадач(ПараметрыОповещения)
	
	// Переформировывает Календарть отчетности, если статус отчета был изменен в Списке задач.
	
	ДанныеДляОтчета = ПолучитьИзВременногоХранилища(АдресДанныхОтчета);
	Если ДанныеДляОтчета = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтрокиДанных = ДанныеДляОтчета.ДанныеЭтогоПриложения.НайтиСтроки(ПараметрыОповещения.ПараметрыПоискаОтчета);
	
	Если СтрокиДанных.Количество() <> 1 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СчетчикИзмененийДанныхОтчета = СчетчикИзмененийДанныхОтчета + 1;
	
	СтрокаДанных = СтрокиДанных[0];
	СтрокаДанных.Статус = ПараметрыОповещения.Статус;
	СтрокаДанных.СтатусУстановленВручную = ПараметрыОповещения.СтатусУстановленВручную;
	СтрокаДанных.Комментарий = ПараметрыОповещения.Комментарий;
	
	АдресДанныхОтчета = ПоместитьВоВременноеХранилище(ДанныеДляОтчета, УникальныйИдентификатор);
	Результат = Обработки.КалендарьОтчетности.СформироватьОтчет(ДанныеДляОтчета, ОрганизацияСсылка(Организация));
	
	Возврат Истина;
КонецФункции

&НаСервере
Процедура СоздатьЗадачуПрошлого()
	
	ДанныеДляОтчета = ПолучитьИзВременногоХранилища(АдресДанныхОтчета);
	Если ДанныеДляОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоиска = КалендарьОтчетностиКлиентСервер.НовыйПараметрыПоискаОтчета();
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ПараметрыОтчета);
	ПараметрыПоиска.РегистрацияФНС = ПараметрыОтчета.РегистрацияВНалоговомОргане;
	
	СтрокиДанных = ДанныеДляОтчета.ДанныеЭтогоПриложения.НайтиСтроки(ПараметрыПоиска);
	Если СтрокиДанных.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	СтрокиДанных[0].ЕстьЗадача = Истина;
	
	СчетчикИзмененийДанныхОтчета = СчетчикИзмененийДанныхОтчета + 1;
	
	ТаблицаЗадач = РегистрыСведений.ЗадачиБухгалтера.НовыйРасписание();
	ЗаполнитьЗначенияСвойств(ТаблицаЗадач.Добавить(), ПараметрыОтчета);
	ВыполнениеЗадачБухгалтера.ЗаписатьНовоеСостояниеЗадачи(ТаблицаЗадач[0]);
	ПараметрыДействияСОтчетом.ЕстьЗадача = Истина;
	
	АдресДанныхОтчета = ПоместитьВоВременноеХранилище(ДанныеДляОтчета, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьСобытиеДобавленКомментарий()
	
	КалендарьОтчетностиПереопределяемый.ПриЗаписиКомментарияОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтзывПоЭлектроннойПочте(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	КалендарьОтчетностиКлиент.ОтправитьОтзывПоЭлектроннойПочте();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаголовокОтборыПоЗадачам()
	
	МаксимальнаяДлинаОтборыПоЗадачам = 60;
	МаксимальноеКоличествоВидовОтбора = 5;
	КалендарьОтчетностиКлиентПереопределяемый.ПриОпределенииМаксимальногоКоличестваВидовОтбора(МаксимальноеКоличествоВидовОтбора);
	
	// Добавится отбор по документам СЭДО
	Если УчетЗарплатыВЭтойПрограмме Тогда
		МаксимальноеКоличествоВидовОтбора = МаксимальноеКоличествоВидовОтбора + 1;
	КонецЕсли;
	
	ТекстОтбора = "";
	
	ЧастиСтроки = Новый Массив;
	Если ПоОтчетам Тогда
		ЧастиСтроки.Добавить(НСтр("ru = 'Отчеты'"));
	КонецЕсли;
	
	Если ПоТребованиямФНС Тогда
		ЧастиСтроки.Добавить(НСтр("ru = 'Требования ФНС'"));
	КонецЕсли;
	
	Если ПоДокументамСЭДО Тогда
		ЧастиСтроки.Добавить(НСтр("ru = 'Сообщения СЭДО'"));
	КонецЕсли;
	
	Если ПоУведомлениямЕНС Тогда
		ЧастиСтроки.Добавить(НСтр("ru = 'Уведомления ЕНС'"));
	КонецЕсли;
	
	Если ПоНалоговымПлатежам Тогда
		ЧастиСтроки.Добавить(НСтр("ru = 'Налоговые платежи'"));
	КонецЕсли;
	
	Если ПоЗарплате Тогда
		ЧастиСтроки.Добавить(НСтр("ru = 'Зарплата'"));
	КонецЕсли;
	
	Если ЧастиСтроки.Количество() = МаксимальноеКоличествоВидовОтбора Тогда
		ТекстОтбора = НСтр("ru = 'По всем задачам'");
		Элементы.ГруппаОтборыПоЗадачам.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСлева;
	ИначеЕсли ЧастиСтроки.Количество() = 0 Тогда
		ТекстОтбора = НСтр("ru = 'Настроить отбор по задачам'");
		Элементы.ГруппаОтборыПоЗадачам.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	Иначе
		ТекстОтбора = СтрСоединить(ЧастиСтроки, ", ");
		Элементы.ГруппаОтборыПоЗадачам.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСлева;
	КонецЕсли;
	
	Если СтрДлина(ТекстОтбора) > МаксимальнаяДлинаОтборыПоЗадачам Тогда
		ТекстОтбора = Лев(ТекстОтбора, МаксимальнаяДлинаОтборыПоЗадачам-3) + "...";
	КонецЕсли;
	
	ОтборыПоЗадачам = ТекстОтбора;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНалогиИОтчетыНаТекущейЗакладке(ИсточникПерехода)
	
	Если ПараметрыОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КалендарьОтчетностиКлиентПереопределяемый.ОткрытьНастройкуНалоговОтчетов(
		ПараметрыОтчета.Организация,
		ИсточникПерехода,
		ПараметрыОтчета.ИдентификаторЗадачи);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОрганизацияСсылка(Организация)
	
	Результат = Организация;
	КалендарьОтчетностиПереопределяемый.ЗаполнитьСсылкуНаОрганизацию(Результат);
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ВыборПериода(ПредставлениеПериода)
	
	Если ПредставлениеПериода = "ТекущийМесяц" Тогда
		ДатаНачала = НачалоМесяца(НаДату);
		ДатаКонца = КонецМесяца(НаДату);
	ИначеЕсли ПредставлениеПериода = "ТриМесяца" Тогда
		ДатаНачала = НачалоМесяца(ДобавитьМесяц(НаДату, -1));
		ДатаКонца = КонецМесяца(ДобавитьМесяц(ДатаНачала, 2));
	ИначеЕсли ПредставлениеПериода = "ПрошлыйГод" Тогда
		ДатаНачала = НачалоГода(ДобавитьМесяц(НаДату, -12));
		ДатаКонца = КонецГода(ДатаНачала);
	ИначеЕсли ПредставлениеПериода = "ТекущийГод" Тогда
		ДатаНачала = НачалоГода(НаДату);
		ДатаКонца = КонецГода(НаДату);
	Иначе
		Возврат;
	КонецЕсли;
		
	СформироватьНадписьПериод(ЭтотОбъект);
	СформироватьОтчет();
	КалендарьОтчетностиКлиентПереопределяемый.ЗаписатьСтатистикуВыбораПериода(ЭтаФорма, ПредставлениеПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеВыбораДругогоПериода(РезультатЗакрытия, ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) <> Тип("СтандартныйПериод") Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачала = РезультатЗакрытия.ДатаНачала;
	ДатаКонца = КонецМесяца(РезультатЗакрытия.ДатаОкончания);
		
	СформироватьНадписьПериод(ЭтотОбъект);
	СформироватьОтчет();
	
	Если ПоказыватьМоиПриложения Тогда
		КалендарьОтчетностиКлиентПереопределяемый.ЗаписатьСобытиеСтатистикиПоПоказателям(
			СтрШаблон("КалендарьОтчетности.ВыбранПроизвольныйПериод.%1", ПредставлениеПериодаОтчета()));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПредставлениеПериодаОтчета()
	
	НачалоГода = НачалоГода(НаДату);
	
	Если ДатаНачала = ДобавитьМесяц(НачалоМесяца(НаДату), -1) И ДатаКонца = ДобавитьМесяц(КонецМесяца(НаДату), -1) Тогда
		Возврат "ПрошлыйМесяц";
	ИначеЕсли ДатаНачала = НачалоМесяца(НаДату) И ДатаКонца = КонецМесяца(НаДату) Тогда
		Возврат "ТекущийМесяц";
	ИначеЕсли ДатаНачала = НачалоМесяца(ДобавитьМесяц(НаДату, -1)) И ДатаКонца = КонецМесяца(ДобавитьМесяц(ДатаНачала, 1)) Тогда
		Возврат "ТриМесяца";
	ИначеЕсли ДатаНачала = НачалоМесяца(НаДату) И ДатаКонца = КонецМесяца(ДобавитьМесяц(ДатаНачала, 1)) Тогда
		Возврат "Ближайшие2Месяца";
	ИначеЕсли ДатаНачала = НачалоМесяца(НаДату) И ДатаКонца = КонецМесяца(ДобавитьМесяц(ДатаНачала, 2)) Тогда
		Возврат "Ближайшие3Месяца";
	ИначеЕсли ДатаНачала = НачалоГода И ДатаКонца = КонецГода(НаДату) Тогда
		Возврат "ТекущийГод";
	ИначеЕсли ДатаНачала = НачалоГода(ДобавитьМесяц(НаДату, -12)) И ДатаКонца = КонецГода(ДатаНачала) Тогда
		Возврат "ПрошлыйГод";
	ИначеЕсли ДатаНачала = НачалоГода И ДатаКонца = КонецМесяца(ДобавитьМесяц(НачалоГода, 2)) Тогда
		Возврат "ПервыйКварталТекущегоГода";
	ИначеЕсли ДатаНачала = НачалоГода И ДатаКонца = КонецМесяца(ДобавитьМесяц(НачалоГода, 5)) Тогда
		Возврат "ПолугодиеТекущегоГода";
	ИначеЕсли ДатаНачала = НачалоГода И ДатаКонца = КонецМесяца(ДобавитьМесяц(НачалоГода, 9)) Тогда
		Возврат "9МесяцевТекущегоГода";
	КонецЕсли;
	
	Возврат СтрШаблон("%1-%2", Формат(ДатаНачала, "ДФ=""ММММ гггг"""), Формат(ДатаКонца, "ДФ=""ММММ гггг"""));
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДоступныеОрганизации()
	
	Элементы.Организация.СписокВыбора.Очистить();
	
	КалендарьОтчетностиПереопределяемый.ЗаполнитьДоступныеОрганизации(
		ЭтотОбъект,
		Элементы.Организация.СписокВыбора);
	
	Организация = Неопределено;
	
КонецПроцедуры

#КонецОбласти
