#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
Функция УстановитьПараметрыУстройства(ДатаЗагрузки, ИдентификаторУстройства) Экспорт 
	
	Результат = Ложь;
	УстановитьПривилегированныйРежим(Истина);
	ДанныеУстройства = МенеджерОфлайнОборудованияВызовСервера.ДанныеУстройства(ИдентификаторУстройства);
	Если Не ДанныеУстройства.Параметры.Свойство("ДатаНачала") ИЛИ Не ДанныеУстройства.Параметры.Свойство("ДатаОкончания") Тогда
		Возврат Результат;
	КонецЕсли;
	
	ПараметрыУстройства = ДанныеУстройства.Параметры; // Структура
	ПараметрыУстройства.ДатаНачала           = ДатаЗагрузки;
	ПараметрыУстройства.ДатаОкончания        = Макс(ТекущаяДата(), ДатаЗагрузки);
	ПараметрыУстройства.ЭтоПерваяЗагрузка    = Истина;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДатаЗагрузкиОтчетовКассЭвотор");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("КассаЭвотор", ИдентификаторУстройства);
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ДатаЗагрузкиОтчетовКассЭвотор.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КассаЭвотор.Значение = ИдентификаторУстройства;
		НаборЗаписей.Отбор.КассаЭвотор.Использование = Истина;
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			Запись             = НаборЗаписей.Добавить();
			Запись.КассаЭвотор = ИдентификаторУстройства;
			Запись.ДатаПоследнейЗакрытойКассовойСмены = ДатаЗагрузки;
			НаборЗаписей.Записать();
		КонецЕсли;
		
		ДанныеУстройства.Параметры.ПериодИзмененВручную = Истина;
		РезультатЗавершения = МенеджерОфлайнОборудованияВызовСервера.СохранитьПараметрыУстройства(ИдентификаторУстройства, ДанныеУстройства.Параметры);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗавершения Тогда
		Результат = РезультатЗавершения;
		ОбновитьПовторноИспользуемыеЗначения();
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив настроек касс ОФД 
//
// Параметры:
// ОтборОрганизация - СправочникСсылка.Организация - организация по которой надо получить настройки, если не заполнена, то по всем организация
//
//
// Возвращаемое значение:
//   <Массив>   - массив описаний настроек (см. НовыйНастройка())
//
Функция СписокНастроекКассОФД(ОтборОрганизация) Экспорт
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОфлайнОборудование.Ссылка КАК ИдентификаторУстройства,
	|	ОборудованиеПоОрганизациям.Организация КАК Организация,
	|	ОборудованиеПоОрганизациям.Склад КАК Склад,
	|	НастройкиКассОФД.Магазин КАК Магазин,
	|	НастройкиКассОФД.Касса КАК Касса,
	|	НастройкиКассОФД.РегистрационныйНомерККТ КАК РегистрационныйНомерККТ,
	|	НастройкиКассОФД.ЗаводскойНомерФН КАК ЗаводскойНомерФН,
	|	НастройкиКассОФД.НастройкаПодключения КАК НастройкаПодключения,
	|	НЕ ОфлайнОборудование.ПометкаУдаления КАК ЗагрузитьДанные
	|ИЗ
	|	РегистрСведений.НастройкиКассОФД КАК НастройкиКассОФД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОфлайнОборудование КАК ОфлайнОборудование
	|		ПО НастройкиКассОФД.Касса = ОфлайнОборудование.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|		ПО НастройкиКассОФД.Касса = ОборудованиеПоОрганизациям.Оборудование
	|ГДЕ
	|	ОфлайнОборудование.Организация В(&СписокОрганизаций)
	|	И ОфлайнОборудование.ТипОфлайнОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыОфлайнОборудования.ОФД)");
	
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		СписокОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОтборОрганизация);
	Иначе
		СписокОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	КонецЕсли;
		
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	
	СписокНастроек = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если НЕ ОФД.ПодключениеНастроено(Выборка.ИдентификаторУстройства) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыУстройства = МенеджерОфлайнОборудованияВызовСервера.ПараметрыУстройства(Выборка.ИдентификаторУстройства);
		
		Настройка = НовыйНастройка();
		ЗаполнитьЗначенияСвойств(Настройка, Выборка);
		
		Настройка.ПериодИзмененВручную  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыУстройства, "ПериодИзмененВручную", Ложь);
		Настройка.ДатаПоследнейЗагрузки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыУстройства, "ДатаОкончания");
		
		СписокНастроек.Добавить(Настройка);
	КонецЦикла;
	
	Возврат СписокНастроек;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	Если НЕ ЗагрузкаИзОФД.ЕстьНастройкиКассОФД() Тогда
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "НастройкаПодключения";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовыйНастройка()
	Настройка = Новый Структура;
	
	Настройка.Вставить("ИдентификаторУстройства", Справочники.ОфлайнОборудование.ПустаяСсылка());
	Настройка.Вставить("Организация",             Справочники.Организации.ПустаяСсылка());
	Настройка.Вставить("Склад",                   Справочники.Склады.ПустаяСсылка());
	Настройка.Вставить("Магазин",                 "");
	Настройка.Вставить("Касса",                   "");
	Настройка.Вставить("РегистрационныйНомерККТ", "");
	Настройка.Вставить("ЗаводскойНомерФН",        "");
	Настройка.Вставить("НастройкаПодключения",    Справочники.НастройкиПодключенияКОФД.ПустаяСсылка());
	Настройка.Вставить("ДатаПоследнейЗагрузки",   "");
	Настройка.Вставить("ПериодИзмененВручную",    Ложь);
	Настройка.Вставить("ЗагрузитьДанные",         Истина);
	
	Возврат Настройка;
КонецФункции 


#КонецОбласти

#КонецЕсли

