#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.КассыОФДНоваяНастройка.Видимость = ОФДСлужебный.НастройкаПодключенияДоступна();
	
	ОбновитьСписокКасс();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если НЕ СтрНачинаетсяС(ИсточникВыбора.ИмяФормы, "Обработка.ЗагрузкаИзОФД.Форма.НастройкаПодключения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранноеЗначение[0].ЗагрузитьДанные Тогда
		ОповеститьОВыборе(ВыбранноеЗначение);
	Иначе
		ОбновитьСписокКасс();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Загрузить(Команда)
	СписокКасс = Новый Массив;
	Для каждого НастройкаКассыОФД Из СписокНастроек Цикл
		Если НастройкаКассыОФД.Загружать ИЛИ СписокНастроек.Количество() = 1 Тогда
			СписокКасс.Добавить(Новый Структура("ИдентификаторУстройства, ЗагрузитьДанные, ПериодИзмененВручную", НастройкаКассыОФД.ИдентификаторУстройства, Истина, НастройкаКассыОФД.ПериодИзмененВручную));
		КонецЕсли;
	КонецЦикла;
	
	Если СписокКасс.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбраны кассы для загрузки'"));
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(СписокКасс);
КонецПроцедуры 

&НаКлиенте
Процедура НоваяНастройка(Команда)
	ОткрытьФормуНастройкаПодключения();
КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)
	ТекущиеДанные = Элементы.СписокКассыОФД.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрана касса для отключения'"));
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("НастройкаПодключения, РегистрационныйНомерККТ, ИдентификаторУстройства");
	
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ТекущиеДанные);
	
	Оповещение = Новый ОписаниеОповещения("ОтключитьКассуПослеЗакрытияВопроса", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(
		Оповещение,
		НСтр("ru = 'Отключить получение данных от ОФД по кассе?'"),
		РежимДиалогаВопрос.ДаНет);
	Возврат;
КонецПроцедуры 

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Для каждого Настройка Из СписокНастроек Цикл
		Настройка.Загружать = ИСтина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметку(Команда)
	Для каждого Настройка Из СписокНастроек Цикл
		Настройка.Загружать = Ложь;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ОбновитьСписокКасс();
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	ОбновитьСписокКасс();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыСписокКассыОФД

&НаКлиенте
Процедура СписокКассыОФДПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ОткрытьФормуНастройкаПодключения();
КонецПроцедуры

&НаКлиенте
Процедура СписокКассыОФДПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуНастройкаПодключения()
	ПараметрыФормы = Новый Структура;
	Если ОтборОрганизацияИспользование Тогда
		ПараметрыФормы.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	ОткрытьФорму("Обработка.ЗагрузкаИзОФД.Форма.НастройкаПодключения",,
		ЭтотОбъект,ПараметрыФормы,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаСервере
Функция ОбновитьСписокКасс()
	Если ОтборОрганизацияИспользование Тогда
		СписокКассПоОрганизации = Обработки.ЗагрузкаИзОФД.СписокНастроекКассОФД(ОтборОрганизация);
	Иначе
		СписокКассПоОрганизации = Обработки.ЗагрузкаИзОФД.СписокНастроекКассОФД(Неопределено);
	КонецЕсли;
	
	СписокНастроек.Очистить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СписокКассПоОрганизации, СписокНастроек);
	
	Элементы.КассыОФДЗагружать.Видимость = СписокНастроек.Количество() > 1;
	Элементы.КассыОФДКомандаВыделения.Видимость = СписокНастроек.Количество() > 1;
КонецФункции

&НаКлиенте
Процедура ОтключитьКассуПослеЗакрытияВопроса(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ОтключитьКассуЗапускЗадания(
		ДополнительныеПараметры.РегистрационныйНомерККТ,
		ДополнительныеПараметры.НастройкаПодключения,
		УникальныйИдентификатор);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		Новый ОписаниеОповещения("ОтключитьКассуЗавершение", ЭтотОбъект, ДополнительныеПараметры),
		ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтключитьКассуЗапускЗадания(
	Знач РегистрационныйНомерККТ,
	Знач НастройкаПодключения,
	Знач УникальныйИдентификатор)
	
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("РегистрационныйНомерККТ", РегистрационныйНомерККТ);
	ПараметрыФункции.Вставить("НастройкаПодключения",    НастройкаПодключения);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отключение кассы на портале.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОФДСервис.ОтключитьКассу",
		ПараметрыФункции);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ОтключитьКассуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	СтатусЗадания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Статус");
	Если СтатусЗадания = "Выполнено" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			ПоказатьПредупреждение(
				Неопределено,
				РезультатОперации.СообщениеОбОшибке);
		Иначе
			
			УдалитьНастройкиКассы(ДополнительныеПараметры.ИдентификаторУстройства);
			
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Отключение кассы завершено успешно.'"));
				
		КонецЕсли;
	ИначеЕсли СтатусЗадания = "Ошибка" Тогда
		ПоказатьПредупреждение(
			Неопределено,
			Результат.КраткоеПредставлениеОшибки);
	Иначе
		Возврат;
	КонецЕсли;
		
	ОбновитьСписокКасс();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНастройкиКассы(Знач КассаОФД)
	
	РегистрыСведений.НастройкиКассОФД.УдалитьНастройкиКассы(КассаОФД);
	
КонецПроцедуры

#КонецОбласти


