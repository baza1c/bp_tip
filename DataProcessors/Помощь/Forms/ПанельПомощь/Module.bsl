#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	УправлениеФормой(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоискНаИТСОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://its.1c.ru/#accnt");
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиНаИТС(Команда)
	
	ПоискОтветаНаВопрос();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорумБУХНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://buh.ru/forum/");
	
КонецПроцедуры

&НаКлиенте
Процедура МониторингЗаконодательстваНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://v8.1c.ru/lawmonitor/");
	
КонецПроцедуры

&НаКлиенте
Процедура ЛекторийНажатие(Элемент)
	
	 ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://its.1c.ru/lector?utm_source=bp30&utm_medium=prog&utm_content=PoleznayaInformatsiyaGlavnoe");
	
КонецПроцедуры

&НаКлиенте
Процедура ВидеоурокиНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://buh.ru/video/tag-1s-bukhgalteriya-8/");
	
КонецПроцедуры

&НаКлиенте
Процедура ЦентрИдейНажатие(Элемент)
	
	ИнформационныйЦентрКлиент.ОткрытьЦентрИдей();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорумФрешНажатие(Элемент)
	
	ИнформационныйЦентрКлиент.ОткрытьОбсужденияНаФоруме();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбратитьсяВСлужбуПоддержкиНажатие(Элемент)
	
	Если ИспользуетсяРазделениеДанных Тогда
		ИнформационныйЦентрКлиент.ОткрытьОбращенияВСлужбуПоддержки();
	Иначе
		ДанныеСообщения = СообщенияВСлужбуТехническойПоддержкиКлиентСервер.ДанныеСообщения();
		ДанныеСообщения.Получатель = СообщенияВСлужбуТехническойПоддержкиБПКлиентСервер.ЛинияПоддержки();
		ДанныеСообщения.ИспользоватьСтандартныйШаблон = Ложь;
		ДанныеСообщения.ИНН = ИННОрганизации;
		СообщенияВСлужбуТехническойПоддержкиКлиент.ОтправитьСообщение(ДанныеСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаписатьВЧатНажатие(Элемент)
	
	ИнтеграцияСКоннектКлиент.СвязатьсяСоСпециалистом();
	
КонецПроцедуры

&НаКлиенте
Процедура БагбоардНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://bugboard.v8.1c.ru/project/bp3");
	
КонецПроцедуры

&НаКлиенте
Процедура ДругиеВидеороликиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура КакВключитьОтключитьУчетПоДоговорамОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущаяСтраница", "ГруппаУчетПоДоговорам");
	ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КакПоказатьСкрытьСчетаУчетаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ОбщаяФорма.ПерсональныеНастройки");
	
КонецПроцедуры

&НаКлиенте
Процедура КакВключитьПолныйПростойИнтерфейсОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ПанельАдминистрированияБП.Форма.Интерфейс");
	
КонецПроцедуры

&НаКлиенте
Процедура КакПосмотретьИзменитьСистемуНалогообложенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Организация = ОбщегоНазначенияБПВызовСервера.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
	ПараметрыФормыНалогиИОтчеты = Новый Структура;
	ПараметрыФормыНалогиИОтчеты.Вставить("Организация", Организация);
	
	ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты", ПараметрыФормыНалогиИОтчеты, , Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура НовоеВПрограммеНажатие(Элемент)
	
	ОбщегоНазначенияБПКлиент.ПоказатьОбновления(НСтр("ru='Что нового в программе'"));
	
КонецПроцедуры

&НаКлиенте
Процедура НовостиНажатие(Элемент)
	
	ОткрытьФорму("Справочник.Новости.Форма.ФормаПросмотраНовостей",,, "");
	
КонецПроцедуры

&НаКлиенте
Процедура БухобслуживаниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://1cbo.ru/");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДанныхИзДругойСистемыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ЗагрузкаДанныхИзДругойУчетнойСистемы.Форма");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ЭтоПростойИнтерфейс = ОбщегоНазначенияБП.ЭтоПростойИнтерфейс();
	ИспользуетсяРазделениеДанных = РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных()
		И РаботаВМоделиСервиса.РазделениеВключено();
	ЭтоБизнесСтарт = ОбщегоНазначенияБП.ЭтоБизнесСтарт();
	ЭтоПолныйИнтерфейс = ОбщегоНазначенияБП.ЭтоПолныйИнтерфейс();
	ЭтоWindowsКлиент = ОбщегоНазначения.ЭтоWindowsКлиент();
	ПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	
	Если ЭтоБизнесСтарт Тогда
		Элементы.ДругиеВидеоролики.Заголовок = СсылкиКаналаБизнесСтарт();
	КонецЕсли;
	
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	Если ЗначениеЗаполнено(Организация) Тогда
		ИННОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИНН");
	КонецЕсли;
	
	РазрешенаРаботаСНовостями = ПолучитьФункциональнуюОпцию("РазрешенаРаботаСНовостями");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ЗагрузкаДанныхВидимость = Форма.ЭтоПростойИнтерфейс И Форма.ИспользуетсяРазделениеДанных И Форма.ПолноправныйПользователь;
	
	Элементы.ГруппаСредняяСторона.Видимость         = Форма.ЭтоПростойИнтерфейс;
	Элементы.Видеоуроки.Видимость                   = Форма.ЭтоПолныйИнтерфейс;
	Элементы.ЦентрИдей.Видимость                    = Форма.ИспользуетсяРазделениеДанных;
	Элементы.ФорумФреш.Видимость                    = Форма.ИспользуетсяРазделениеДанных;
	Элементы.ГруппаВидеоролики.Видимость            = Форма.ЭтоБизнесСтарт;
	Элементы.ГруппаЧастоЗадаваемыеВопросы.Видимость = Форма.ЭтоПростойИнтерфейс;
	Элементы.ГруппаНеХватаетВремени.Видимость       = Форма.ЭтоПростойИнтерфейс;
	Элементы.ГруппаЗагрузкаДанных.Видимость         = ЗагрузкаДанныхВидимость;
	Элементы.ГруппаКот.Видимость                    = Не Форма.ЭтоБизнесСтарт;
	Элементы.ГруппаЧтоНового.Видимость              = Форма.РазрешенаРаботаСНовостями;
	Элементы.НаписатьВЧат.Видимость                 = Форма.ЭтоWindowsКлиент;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СсылкиКаналаБизнесСтарт()
	
	Возврат Обработки.Помощь.СсылкиКаналаБизнесСтарт();
	
КонецФункции

&НаКлиенте
Процедура ПоискОтветаНаВопрос()
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбработкаОжиданияПоискаОтветаНаВопрос", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаОжиданияПоискаОтветаНаВопрос()
	
	Если ПустаяСтрока(СтрокаПоискаНаИТС) Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаПоискаИнформации = "http://its.1c.ru/db/alldb#search:";
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СсылкаПоискаИнформации + СтрокаПоискаНаИТС);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаБазыЗнаний);
	МобильныйКлиентБПФормаОбъекта.СкрытьПрочиеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти