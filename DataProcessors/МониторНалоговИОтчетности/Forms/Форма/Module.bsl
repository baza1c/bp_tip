#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияАктуализации Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОгобработка)

	Монитор = Обработки.МониторНалоговИОтчетности;

	// Разрешим выбор только тех организаций, данные которых доступны на чтение.
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	УстановитьПараметрыВыбораОрганизации(ЭтотОбъект, ДоступныеОрганизации);
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организация = Параметры.Организация;
	Иначе
		Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	Если ДоступныеОрганизации.Найти(Организация) = Неопределено Тогда
		Организация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	ОрганизацияПредставление = Организация;
	
	Элементы.ШапкаНалоги.Заголовок = Монитор.ЗаголовокОплатаНалогов();
	Элементы.ШапкаОтчетность.Заголовок = Монитор.ЗаголовокСдачаОтчетности();
	
	ЕстьПравоНаАктуализацию   = ПравоДоступа("Изменение", Метаданные.Документы.РегламентнаяОперация);
	ЕстьПравоВыполненияЗадачи = КалендарьБухгалтера.ПравоВыполненияЗадачи(Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПустаяСсылка());
	
	АктуализацияВозможна = Обработки.ЗакрытиеМесяца.АктуализацияВозможна();
	
	РазрешенУчетРегулярнойДеятельности = ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности();
	
	ПростойУчетЕНС = Обработки.МониторНалоговИОтчетности.ТекущаяДатаМонитора() >= ЕдиныйНалоговыйСчет.НачалоПростогоУчета();
	
	ИспользуетсяКомандаНалоги = ИспользуетсяКомандаНалоги();
	
	УстановитьПараметрыЗависимыеОтОрганизации();
	
	УстановитьОформлениеМонитора();
	
	ПереключитьВРежимОжидания(Истина);
	
	Элементы.РекомендацияПоОбновлению.Видимость = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ВРег("МониторНалоговИОтчетности"), 
		ВРег("РекомендацияПоОбновлению"), 
		Истина);
		
	Если УпрощеннаяНулеваяОтчетность Тогда
		Элементы.РекомендацияПоОбновлению.Ширина = 82;
		Элементы.РекомендацияПоОбновлениюМонитора.Шрифт = Новый Шрифт(Элементы.РекомендацияПоОбновлениюМонитора.Шрифт,,11);
		
		Элементы.Баннер.Ширина = 82;
		Элементы.Актуализация.Ширина = 82;
		Элементы.ТребуетсяАктуализация.Ширина = 82;
		Элементы.ПодсказкаПоЗадолженности.Ширина = 82;
	КонецЕсли;
		
	УстановитьФункциональныеОпцииФормы();
	
	Элементы.ПодсказкаПоЗадолженности.Доступность = Ложь;
	Элементы.ПодсказкаПоЗадолженности.Видимость   = Ложь;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Обработка.МониторНалоговИОтчетности",
		"Форма",
		НСтр("ru='Новости: Монитор налогов и отчетности'"),
		ИдентификаторыСобытийПриОткрытии);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗапуститьОбновление(Истина);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ПоказатьБаннерПерсонализированногоПредложения();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "АктуализацияЗавершенаУспешно"
		И Параметр.Свойство("Организация")
		И Организация = Параметр.Организация
		И Параметр.Свойство("ВызовИзМонитора")
		И Параметр.ВызовИзМонитора) Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
		Элементы.Актуализация.Видимость = Ложь;
		ДатаАктуальности = МаксДата;
		
	ИначеЕсли ИмяСобытия = "Запись_ЗаявлениеОЗачетеВСчетПредстоящейОбязанности"
		И Параметр.Свойство("Организация")
		И Организация = Параметр.Организация Тогда
		
		ЗапуститьОбновление();
		
	ИначеЕсли (ИмяСобытия = "ТребуетсяАктуализация" Или ИмяСобытия = "АктуализацияОтменена")
		И (Параметр.Свойство("Организация")
		И Организация = Параметр.Организация 
		И Параметр.Свойство("ДатаАктуальности")) Тогда
		
		ДатаАктуальности = Параметр.ДатаАктуальности;
		
		Элементы.Актуализация.Видимость     = Истина;
		Элементы.ИдетАктуализация.Видимость = Ложь;
		Элементы.ИдетПроверкаАктуальности.Видимость = Ложь;
		Элементы.ТребуетсяАктуализация.Видимость    = Истина;
		
		МассивПодстрок = Новый Массив();
		ТекстДанныеУчетаНеАктуальны = НСтр("ru='Данные учета неактуальны с '");
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(ТекстДанныеУчетаНеАктуальны));
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(Формат(ДатаАктуальности, "ДЛФ=D"), Новый Шрифт(,, Истина)));
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока("."));
		
		Если АктуализацияВозможна Тогда
			Элементы.Актуализировать.Видимость = ЕстьПравоНаАктуализацию;
		Иначе
			МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(" " + НСтр("ru='Рекомендуется выполнить '")));
			МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='закрытие месяца'"),,,, "e1cib/app/Обработка.ЗакрытиеМесяца"));
			МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ' и сформировать монитор повторно.'")));
			Элементы.Актуализировать.Видимость = Ложь;
		КонецЕсли;
		
		Элементы.ДекорацияАктуальность.Заголовок = Новый ФорматированнаяСтрока(МассивПодстрок);
		
	ИначеЕсли ИмяСобытия = "АктуализацияОтменена" Тогда
		
		Элементы.Актуализация.Видимость     = Истина;
		Элементы.ИдетАктуализация.Видимость = Ложь;
		Элементы.ИдетПроверкаАктуальности.Видимость = Ложь;
		Элементы.ТребуетсяАктуализация.Видимость    = Истина;
		
	ИначеЕсли ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		
		Если Параметр = Организация Тогда
			ЗапуститьОбновление();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_Организации" Тогда
		
		Если Источник = Организация Тогда
			ОрганизацияПредставление = Организация;
			УстановитьПараметрыЗависимыеОтОрганизации();
			ЗапуститьОбновление();
		КонецЕсли;
	
	ИначеЕсли ИмяСобытия = "ОтчетыПрошлыхПериодов_ПроверкаЗавершена" Тогда
		
		Если Источник = Организация Тогда
			ОрганизацияПредставление = Организация;
			УстановитьПараметрыЗависимыеОтОрганизации();
			ЗапуститьОбновление();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "НалогиПрошлыхПериодов_ИзмененыОстатки" Тогда
		
		Если Источник = Организация Тогда
			ОрганизацияПредставление = Организация;
			УстановитьПараметрыЗависимыеОтОрганизации();
			ЗапуститьОбновление();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ПомощникОплатыВзносовИП_ИзменениеСостояния"
		Или ИмяСобытия = "ИзменениеВыписки" Тогда
		
		ЗапуститьОбновление();
		
	ИначеЕсли ИмяСобытия = "Позиционирование в списке отчетов"
		Или ИмяСобытия = "Изменение пометки удаления объекта"
		Или ИмяСобытия = "Запись_РегламентированныйОтчет" Тогда
		
		// Записан регламентированный отчет или изменен его статус.
		
		Если ТипЗнч(Параметр) = Тип("Структура")
			И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Организация") = Организация Тогда
			
			ЗапуститьОбновление();
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_ПлатежныйДокумент_УплатаНалогов" Тогда
		
		// Записан документ ПлатежноеПоручение или РасходныйКассовыйОрдер с видом операции "Уплата налога"
		Налог = Неопределено;
		Если ТипЗнч(Параметр) = Тип("Структура")
			И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Организация") = Организация
			И ПоддерживаетсяОбновлениеМонитораПриЗаписиПлатежки(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Налог")) Тогда
			
			ЗапуститьОбновление();
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ПомощникРасчетаНалогаУСН_ИзмененаЗадача" Тогда
		
		ЗапуститьОбновление();
		
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовИКомандФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПредставление = Организация;
	
	УстановитьПараметрыЗависимыеОтОрганизации();
	
	ЗапуститьОбновление();
	
	ПоказатьБаннерПерсонализированногоПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СсылкаНалогОтчетНажатие(Элемент)
	
	ОбработатьСсылкуНаНалогОтчет(Элемент, АдресСсылокНадписей);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НавигационнаяСсылкаНажатие(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьСсылкуНаНалогОтчет(Элемент, АдресНавигационныхСсылок);
	
КонецПроцедуры

&НаКлиенте
Процедура Актуализировать(Команда)
	
	АктуализироватьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМонитор(Команда)
	
	УстановитьПараметрыЗависимыеОтОрганизации();
	ЗапуститьОбновление(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияАктуальностьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбработатьСсылкуНаПереходВЗакрытиеМесяца(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьРекомендациюПоОбновлениюНажатие(Элемент)
	
	Элементы.РекомендацияПоОбновлению.Видимость = Ложь;
	СохранитьОтключениеРекомендацииПоОбновлению();
	
	ПоказатьБаннерПерсонализированногоПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаПоЗадолженностиЗакрытьНажатие(Элемент)
	
	Элементы.ПодсказкаПоЗадолженности.Видимость = Ложь;
	СохранитьОтключениеПодсказкаПоЗадолженности();
	
	ПоказатьБаннерПерсонализированногоПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаНалоговИОтчетов(Команда)
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = НСтр("ru = 'Настройки налогов и отчетов'");
	ПараметрыОткрытия.ИмяФормы = "ОбщаяФорма.НалогиИОтчеты";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.ЗакрытьБаннер(ЭтотОбъект, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьПредыдущийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийБаннерНажатие(Элемент)
	
	ОтключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер");
	
	ПерсонализированныеПредложенияСервисовКлиент.УстановитьРежимОжиданияНаБаннере(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокЕНСНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	Параметрыформы.Вставить("НачалоПериода",           НачалоГода(ТекущийДень));
	Параметрыформы.Вставить("КонецПериода",            КонецДня(ТекущийДень));
	ПараметрыФормы.Вставить("Организация",             Организация);
	ПараметрыФормы.Вставить("РежимРасшифровки",        Истина);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.РасчетыПоЕНС.Форма.ФормаОтчета", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область Представление

&НаСервере
Процедура РазместитьВИнтерфейсе()
	
	Монитор = Обработки.МониторНалоговИОтчетности;
	
	Если УпрощеннаяНулеваяОтчетность Тогда
		ШиринаТаблицыМонитора = 82;
	Иначе
		ШиринаТаблицыМонитора = 104;
	КонецЕсли;
	
	Элементы.Шапка.Ширина = ШиринаТаблицыМонитора;
	Элементы.Разделитель.Ширина = ШиринаТаблицыМонитора;
	
	ИндикаторыМонитора = ПолучитьИзВременногоХранилища(АдресРезультата);
	ИндикаторыМонитора = ИндикаторыМонитора.Получить(); // получение из хранилища значения.
	
	ВключенныеЗадачи    = ИндикаторыМонитора.ВключенныеЗадачи;
	НалоговыеИндикаторы = ИндикаторыМонитора.НалоговыеИндикаторы;
	ОтчетныеИндикаторы  = ИндикаторыМонитора.ОтчетныеИндикаторы;
	ДанныеЕНС           = ИндикаторыМонитора.ЕдиныйНалоговыйСчет;
	НалоговыеИндикаторы.Индексы.Добавить("Владелец");
	ОтчетныеИндикаторы.Индексы.Добавить("Владелец");
	
	ПериодЕНП = Обработки.МониторНалоговИОтчетности.ПредставлениеПериодаЕНП(НалоговыеИндикаторы);
	
	// Очистка.
	ПараметрыСсылокНадписей = Новый Соответствие;
	ПараметрыНавигационныхСсылок = Новый Соответствие;
	ЛимитСсылок = Монитор.ЛимитСсылокИндикатора();
	Столбцы    = Новый Массив(3);
	Столбцы[0] = 1; // первый столбец монитора с максимум 1-ой декорацией.
	Столбцы[1] = ЛимитСсылок + 3; // второй столбец монитора с максимум декораций согласно лимиту плюс запас
	Столбцы[2] = ЛимитСсылок + 3; // третий столбец монитора с максимум декораций согласно лимиту плюс запас.
	
	Для СчРяд = 1 По ТекущееЧислоРядов Цикл
		Элементы[ИмяРяд() + Строка(СчРяд)].Видимость = Ложь;
		Для СчСтолбец = 0 По Столбцы.ВГраница() Цикл 
			Для СчСтрока = 0 По Столбцы[СчСтолбец] - 1 Цикл
				ЭлементФормы = Элементы.Найти(ИмяСтрока() + Строка(СчРяд) + Строка(СчСтолбец) + Строка(СчСтрока));
				Если ЭлементФормы <> Неопределено Тогда
					ЭлементФормы.Видимость = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Формирование.
	РядыБлоков = Монитор.СформироватьРядыБлоков(ВключенныеЗадачи, НалоговыеИндикаторы, ОтчетныеИндикаторы);
	
	ШиринаЗаголовка = 20;
	
	Если ЭтоМобильныйКлиент Тогда
		
		ШиринаЗаголовка = 0;
		Элементы.Ряд0.Видимость = Ложь;
		Элементы.ДанныеЕНС.Видимость = Истина;
		Элементы.ГруппаОтчетЕНС.Видимость = Истина;
		Элементы.СтрокаЕНС.Заголовок = НСтр("ru = 'Единый налоговый счет'");
		Элементы.СтрокаЕНС.РастягиватьПоГоризонтали = Истина;
		Элементы.СтрокаЕНС.ЦветФона = Новый Цвет;
		Элементы.КонтейнерНаименованиеЕНС.ЦветФона = Новый Цвет;
		
	ИначеЕсли УпрощеннаяНулеваяОтчетность Тогда
		Элементы.Ряд0.Видимость = Ложь;
		Элементы.ДанныеЕНС.Видимость = Истина;
		Элементы.ГруппаОтчетЕНС.Видимость = Ложь;
		Элементы.КонтейнерНаименованиеЕНС.Ширина = ШиринаЗаголовка * 2;
	Иначе
		Элементы.Ряд0.Видимость = Истина;
		Элементы.ДанныеЕНС.Видимость = Истина;
		Элементы.ГруппаОтчетЕНС.Видимость = Истина;
		Элементы.СтрокаЕНС.Заголовок = НСтр("ru = 'Единый
			|налоговый
			|счет'");
		Элементы.КонтейнерНаименованиеЕНС.Ширина = ШиринаЗаголовка;
	КонецЕсли;
	
	СчРяд = 1;
	Для Каждого Ряд Из РядыБлоков Цикл
		
		Если Монитор.ЭтоЗадачаЕНС(Ряд.СтрокаЗадачи.КодЗадачи) И Не УпрощеннаяНулеваяОтчетность Тогда
			ЗаполнитьРядЕНС(Ряд, ДанныеЕНС, ПараметрыСсылокНадписей, ПараметрыНавигационныхСсылок);
			Продолжить;
		ИначеЕсли ЭтоИПНулевка И Ряд.СтрокаЗадачи.КодЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносыИП() Тогда
			// Разместим для ИП итоговую строку по единому налоговому счету с остатком ЕНС
			ЗаполнитьРядЕНСНулевка(ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Ряд),
				ДанныеЕНС,
				ПараметрыСсылокНадписей,
				ПараметрыНавигационныхСсылок);
		КонецЕсли;
		
		РазместитьНовыйРяд(СчРяд);
		
		Если УпрощеннаяНулеваяОтчетность Тогда 
			Элементы[ИмяКонтейнер() + Строка(СчРяд) + "0"].Ширина = ШиринаЗаголовка*2;
		Иначе
			Элементы[ИмяКонтейнер() + Строка(СчРяд) + "0"].Ширина = ШиринаЗаголовка;
		КонецЕсли;
		
		Элементы[ИмяРяд() + Строка(СчРяд)].Видимость = Истина;
		ЭлементПоказателя = Элементы[ИмяСтрока() + Строка(СчРяд) + "00"];
		ЭлементПоказателя.Видимость = Истина;
		ЭлементПоказателя.Заголовок = Строка(СчРяд) + ". " + Ряд.СтрокаЗадачи.ИмяЗадачи;
		
		Если ЭтоМобильныйКлиент Тогда
			Элементы[ИмяКонтейнер() + Строка(СчРяд) + "0"].РастягиватьПоВертикали = Ложь;
			Элементы[ИмяКонтейнер() + Строка(СчРяд) + "0"].РастягиватьПоГоризонтали = Истина;
			Элементы[ИмяКонтейнер() + Строка(СчРяд) + "0"].ЦветФона = Новый Цвет;
			ЭлементПоказателя.ЦветФона = Новый Цвет;
		КонецЕсли;
		
		Если УпрощеннаяНулеваяОтчетность
			И Ряд.СтрокаЗадачи.КодЗадачи = ЗадачиБухгалтераКлиентСервер.КодЗадачиСтраховыеВзносыИП() Тогда
			// В Нулевке монитор имеет одну колонку и взнос выводится в раздел отчетов
			ЭтоНалог = Истина;
		Иначе
			ЭтоНалог = Ложь;
		КонецЕсли;
		
		БлокОтчетов = Монитор.СоздатьЭкземплярБлока(Ряд, ЭтоНалог, ЭтотОбъект);
		ЗаполнитьЭлементБлока(БлокОтчетов, СчРяд, ЭтоНалог, ПараметрыСсылокНадписей, ПараметрыНавигационныхСсылок);
		
		Если Не УпрощеннаяНулеваяОтчетность Тогда
			БлокНалога = Монитор.СоздатьЭкземплярБлока(Ряд, Истина, ЭтотОбъект);
			ЗаполнитьЭлементБлока(БлокНалога, СчРяд, Истина, ПараметрыСсылокНадписей, ПараметрыНавигационныхСсылок);
		КонецЕсли;
		
		СчРяд = СчРяд + 1;
	
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ПараметрыСсылокНадписей, АдресСсылокНадписей);
	ПоместитьВоВременноеХранилище(ПараметрыНавигационныхСсылок, АдресНавигационныхСсылок);
	
	ВсеЗадачиВыполнены = ИндикаторыМонитора.ВсеЗадачиВыполнены;
	
	ПереключитьВРежимОжидания(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура РазместитьНовыйРяд(СчРяд)
	
	Монитор = Обработки.МониторНалоговИОтчетности;
	
	Если СчРяд <= ТекущееЧислоРядов Тогда
		// Уже создан ряд при предыдущем выполнении отчета или есть предопределенный.
		Возврат
	КонецЕсли;
	
	ИсклСвойстваГруппы = "ПутьКДаннымЗаголовка";

	Ряд = Строка(СчРяд);
	
	НовыйРяд = Элементы.Добавить(ИмяРяд() + Ряд, Тип("ГруппаФормы"), Элементы.ДанныеМонитора);
	ЗаполнитьЗначенияСвойств(НовыйРяд, Элементы.Ряд1,, ИсклСвойстваГруппы);
	
	Столбцы	= Новый Массив(3);
	Столбцы[0] = 1; // первый столбец монитора с минимум 1-ой декорацией.
	Столбцы[1] = 3; // второй столбец монитора с минимум 3-я декорациями.
	Столбцы[2] = 3; // третий столбец монитора с минимум 3-я декорациями.

	Для Сч = 0 По Столбцы.Количество() - 1 Цикл
		Столб = Строка(Сч);
		НовыйГруппаКонтейнер = Элементы.Добавить(ИмяГруппаКонтейнер() + Ряд + Столб, Тип("ГруппаФормы"), НовыйРяд);
		ЗаполнитьЗначенияСвойств(НовыйГруппаКонтейнер, Элементы[ИмяГруппаКонтейнер() + "1" + Столб],, ИсклСвойстваГруппы);
		
		НовыйКонтейнер = Элементы.Добавить(ИмяКонтейнер() + Ряд + Столб, Тип("ГруппаФормы"), НовыйГруппаКонтейнер);
		ЗаполнитьЗначенияСвойств(НовыйКонтейнер, Элементы[ИмяКонтейнер() + "1" + Столб],, ИсклСвойстваГруппы);
		
		Для СчСтрока = 0 По Столбцы[Сч] - 1 Цикл
			НомерСтроки = Строка(СчСтрока);
			НоваяСтрокаИмя = ИмяСтрока() + Ряд + Столб + НомерСтроки;
			НовыйСтрока = Элементы.Добавить(НоваяСтрокаИмя, Тип("ДекорацияФормы"), НовыйКонтейнер);
			ЗаполнитьЗначенияСвойств(НовыйСтрока, Элементы[ИмяСтрока() + "1" + Столб + НомерСтроки]);
			НовыйСтрока.Заголовок = "";
			НовыйСтрока.Видимость = Ложь;
		КонецЦикла;
	КонецЦикла;
	
	ТекущееЧислоРядов = СчРяд;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементБлока(БлокИндикатора, СчРяд, ЭтоНалог, ПараметрыСсылокНадписей = Неопределено,
	ПараметрыНавигационныхСсылок = Неопределено, БлокЕНС = Ложь)
	
	Монитор = Обработки.МониторНалоговИОтчетности;
	Раздел = ?(ЭтоНалог, "2", "1");
	
	Если Не БлокЕНС Тогда
		ГруппаКонтейнер = Элементы[ИмяГруппаКонтейнер() + Строка(СчРяд) + Раздел];
		ЗаполнитьЗначенияСвойств(ГруппаКонтейнер, БлокИндикатора);
	КонецЕсли;
	
	Для СчСтрока = 0 По БлокИндикатора.КолвоСтрок - 1 Цикл
		
		// Заполняем элементы формы.
		Если БлокЕНС Тогда
			Если ЭтоНалог Тогда
				ЭлементСтроки = Элементы.СтрокаПомощникОплатаЕНС;
			ИначеЕсли СчСтрока > 0 Тогда
				ЭлементСтроки = Элементы.СтрокаПросроченныеОтчеты;
			Иначе
				ЭлементСтроки = Элементы.СтрокаПомощникУведомлениеЕНС;
			КонецЕсли;
			ИмяЭлемента = ЭлементСтроки.Имя;
		Иначе
			ИмяЭлемента = ИмяСтрока() + Строка(СчРяд) + Раздел + Строка(СчСтрока);
			ЭлементСтроки = Элементы.Найти(ИмяЭлемента);
			Если ЭлементСтроки = Неопределено Тогда
				ИмяРодителя =  ИмяКонтейнер() + Строка(СчРяд) + Раздел;
				Родитель = Элементы.Найти(ИмяРодителя);
				ЭлементСтроки = Элементы.Добавить(ИмяЭлемента, Тип("ДекорацияФормы"), Родитель);
				ЗаполнитьЗначенияСвойств(ЭлементСтроки, Элементы.Строка112);
			КонецЕсли;
		КонецЕсли;
		
		ЭлементСтроки.Заголовок = БлокИндикатора[ИмяСтрока() + СчСтрока];
		ЭлементСтроки.Видимость = БлокИндикатора[ИмяСтрока() + "Видимость" + СчСтрока];
		
		// Заполняем параметры ссылок.
		ПараметрыСсылки = Неопределено;
		Если БлокИндикатора.Свойство("СтрокаПараметрыСсылки" + СчСтрока, ПараметрыСсылки)
			И ПараметрыСсылокНадписей <> Неопределено
			Или БлокЕНС Тогда
			ПараметрыСсылокНадписей.Вставить(ИмяЭлемента, ПараметрыСсылки);
			ЭлементСтроки.УстановитьДействие("Нажатие", "Подключаемый_СсылкаНалогОтчетНажатие");
			ЭлементСтроки.Доступность = ЕстьПравоВыполненияЗадачи;
			ЭлементСтроки.Гиперссылка = Истина;
			ЭлементСтроки.Высота = 1;
		Иначе
			ЭлементСтроки.Гиперссылка = Ложь;
			ЭлементСтроки.Высота = 0;
		КонецЕсли;
		
		// Заполняем навигационные ссылки.
		Если ПараметрыНавигационныхСсылок <> Неопределено И БлокИндикатора.Свойство("ПараметрыНавигационныхСсылок") Тогда
			ПараметрыНавигационныхСсылок.Вставить(ИмяЭлемента, БлокИндикатора.ПараметрыНавигационныхСсылок);
			ЭлементСтроки.УстановитьДействие("Нажатие", "Подключаемый_СсылкаНалогОтчетНажатие");
			ЭлементСтроки.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_НавигационнаяСсылкаНажатие");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРядЕНС(Ряд, ДанныеЕНС, ПараметрыСсылокНадписей, ПараметрыНавигационныхСсылок)
	
	Монитор            = Обработки.МониторНалоговИОтчетности;
	ОстатокЕНС         = ДанныеЕНС.ОстатокЕНС;
	РассчитаноНалогов  = ДанныеЕНС.РассчитаноНалогов;
	НужноОплатить      = ДанныеЕНС.НужноОплатить;
	ВходящийОстатокЕНС = ДанныеЕНС.ВходящийОстатокЕНС;
	
	ЦветаИндикаторов = Монитор.ЦветаИндикаторов();
	СтрокаНалога = Ряд.СтрокиНалога[0];
	СтрокаНалога.КУплатеТекущий = НужноОплатить;
	
	Если ДанныеЕНС.ОстатокЕНС < 0 Тогда
		Элементы.ГруппаОплатаЕНС.ЦветФона = ЦветКрасныйБлок;
		СтрокаНалога.Индикатор = ЦветаИндикаторов.Красный;
	ИначеЕсли ДанныеЕНС.НужноОплатить = 0 Тогда
		Элементы.ГруппаОплатаЕНС.ЦветФона = ЦветЗеленыйБлок;
		СтрокаНалога.Индикатор = ЦветаИндикаторов.Зеленый;
	Иначе
		Элементы.ГруппаОплатаЕНС.ЦветФона = ЦветЖелтыйБлок;
		СтрокаНалога.Индикатор = ЦветаИндикаторов.Желтый;
	КонецЕсли;
	
	БлокОтчетаЕНС = Монитор.СоздатьЭкземплярБлока(Ряд, Ложь, ЭтотОбъект);
	Элементы.СтрокаПомощникУведомлениеЕНС.Заголовок = "";
	Элементы.СтрокаПросроченныеОтчеты.Заголовок =     "";
	Элементы.ГруппаОтчетЕНС.ЦветФона = БлокОтчетаЕНС.ЦветФона;
	Если ЭтоМобильныйКлиент И БлокОтчетаЕНС.Свойство("Видимость") Тогда
		Элементы.ГруппаОтчетЕНС.Видимость = БлокОтчетаЕНС.Видимость;
	КонецЕсли;
	ЗаполнитьЭлементБлока(БлокОтчетаЕНС, 0, Ложь, ПараметрыСсылокНадписей, ПараметрыНавигационныхСсылок, Истина);
	
	БлокНалогаЕНС = Монитор.СоздатьЭкземплярБлока(Ряд, Истина, ЭтотОбъект);
	ЗаполнитьЭлементБлока(БлокНалогаЕНС, 0, Истина, ПараметрыСсылокНадписей, ПараметрыНавигационныхСсылок, Истина);
	Элементы.РядЕНС.Высота = ВысотаБлокаЕНСПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область ФоновоеФормирование

&НаКлиенте
Процедура ЗапуститьОбновление(ВыполнитьОбменССервисомИнтеграции = Ложь)
	
	Если Не ФоновыеЗаданияАктивны(ИдентификаторЗадания, ИдентификаторЗаданияАктуализации) Тогда
		ПодключитьОбработчикОжидания("Подключаемый_НачатьФормированиеМонитора",
			БухгалтерскиеОтчетыКлиент.ИнтервалЗапускаФормированияОтчетаПриОткрытии(), Истина);
	КонецЕсли;
	
	Если ВыполнитьОбменССервисомИнтеграции И НастроенОбменССервисомАУСН Тогда
		ПодключитьОбработчикОжидания("Подключаемый_АУСН", 0.1, Истина);
	КонецЕсли;
	
	Если НастроенОбменССервисомЛКАУСН Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЛКАУСН", 0.1, Истина);
	КонецЕсли;
	
КонецПРоцедуры

&НаКлиенте
Процедура Подключаемый_НачатьФормированиеМонитора()
	НачатьФормированиеМонитора();
КонецПроцедуры

&НаКлиенте
Процедура НачатьФормированиеМонитора(ПроверитьАктуальность = Истина)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Актуализация.Видимость = Ложь;
	
	Результат = ВыполнитьВФоне();

	ПараметрыОжидания     = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьФормированиеМонитора", ЭтотОбъект, ПроверитьАктуальность);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Процедура ПереключитьВРежимОжидания(ВключитьОжидание)
	
	Элементы.Ожидание.Видимость = ВключитьОжидание;
	Элементы.ДанныеМонитора.Видимость = Не ВключитьОжидание;
	Элементы.ДанныеЕНС.Видимость = Не ВключитьОжидание;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Элементы.ЖдатьТекст.Заголовок = НСтр("ru = 'Пожалуйста, подождите…'");
	Иначе
		Элементы.ЖдатьТекст.Заголовок = НСтр("ru = 'Укажите организацию для формирования монитора.'");
	КонецЕсли;
	
КонецПРоцедуры

&НаСервереБезКонтекста
Функция ФоновыеЗаданияАктивны(знач ИдентификаторЗадания, знач ИдентификаторЗаданияАктуализации)
	
	ФоновоеЗаданиеМонитора = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	ФоновоеЗаданиеАктуализации = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗаданияАктуализации);
	
	Если (ФоновоеЗаданиеМонитора <> Неопределено И ФоновоеЗаданиеМонитора.Состояние = СостояниеФоновогоЗадания.Активно) 
		Или (ФоновоеЗаданиеАктуализации <> Неопределено И ФоновоеЗаданиеАктуализации.Состояние = СостояниеФоновогоЗадания.Активно)  Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ВыполнитьВФоне()
	
	ПереключитьВРежимОжидания(Истина);

	НаименованиеЗадания = НСтр("ru = 'Обновление монитора'");
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Организация",                     Организация);
	ПараметрыЗадания.Вставить("ТекущийДень",                     Обработки.МониторНалоговИОтчетности.ТекущаяДатаМонитора());
	ПараметрыЗадания.Вставить("ОтсутствуютПатенты",              ОтсутствуютПатенты);
	ПараметрыЗадания.Вставить("УпрощеннаяНулеваяОтчетность",     УпрощеннаяНулеваяОтчетность);
	ПараметрыЗадания.Вставить("ПростойУчетЕНС",                  ПростойУчетЕНС);
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("Обработки.МониторНалоговИОтчетности.СФормироватьПоказателиМонитораВФоне", 
		ПараметрыЗадания, ПараметрыВыполненияВФоне);
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьФормированиеМонитора(Результат, ПроверитьАктуальность) Экспорт
	
	Если Результат = Неопределено Тогда // Задание было отменено.
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	АдресРезультата = Результат.АдресРезультата;
	РазместитьВИнтерфейсе();
	АдресРезультата = Неопределено;
	
	Если ПроверитьАктуальность Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальность", 0.1, Истина);
	КонецЕсли;
	
	Если НЕ РазрешенУчетРегулярнойДеятельности И ВсеЗадачиВыполнены Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПоказатьПриглашениеПознакомитьсяСПриложением", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаАктуальности

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	Элементы.Актуализация.Видимость = Истина;
	
	ЗаданиеАктуализации = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(
		Организация, ИдентификаторЗаданияАктуализации); 
	
	Если ЗаданиеАктуализации = Неопределено Или Не ЗначениеЗаполнено(ЗаданиеАктуализации.УникальныйИдентификатор) Тогда
		ПроверитьАктуальностьДанных();
	Иначе
		
		Если ИдентификаторЗаданияАктуализации <> ЗаданиеАктуализации.УникальныйИдентификатор Тогда
			// Задание запущено в другой форме
			ЗаданиеЗапущеноВДругойФорме = Истина;
			ИдентификаторЗаданияАктуализации = ЗаданиеАктуализации.УникальныйИдентификатор;
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеАктуализации",
				ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал,
				Истина);
			Элементы.ИдетАктуализация.Видимость = Истина;
		Иначе
			ЗаданиеЗапущеноВДругойФорме = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьАктуальностьДанных()
	
	Элементы.ИдетАктуализация.Видимость = Ложь;
	Элементы.ИдетПроверкаАктуальности.Видимость = Истина;
	Элементы.ТребуетсяАктуализация.Видимость    = Ложь;

	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	ИдентификаторЗаданияАктуализации = Неопределено;
	
	УИДЗамера = ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Ложь, "АктуализацияДанныхТребуетсяАктуализацияЗаПериод");
	
	ПараметрыПроверки = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыПроверкиАктуальности();
	ПараметрыПроверки.Организация                  = Организация;
	ПараметрыПроверки.Период                       = ТекущийДень;
	ПараметрыПроверки.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыПроверки.УИДЗамера                    = УИДЗамера;
	Если УпрощеннаяНулеваяОтчетность Тогда
		ПараметрыПроверки.АктуализироватьВесьПериод    = Истина;
		ПараметрыПроверки.АктуализацияДляРасчетаНалога = Истина;
	КонецЕсли;
	
	РезультатВыполнения = ЗакрытиеМесяцаВызовСервера.ПроверитьАктуальность(ПараметрыПроверки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиАктуальности", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеАктуализации()

	СтатусЗадания = ЗакрытиеМесяцаВызовСервера.СтатусФоновогоЗадания(ИдентификаторЗаданияАктуализации);
	
	Если СтатусЗадания.Статус = "Выполнено" Тогда
		Если ЗаданиеЗапущеноВДругойФорме Тогда
			// Если запущено в другой форме и выполнено, то проверяем актуальность еще раз.
			ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
			ЗаданиеЗапущеноВДругойФорме = Ложь;
			ИдентификаторЗаданияАктуализации = Неопределено;
			АктуализироватьДанные();
		Иначе
			ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
			ОбработатьРезультатАктуализации();
			ИдентификаторЗаданияАктуализации = Неопределено;
		КонецЕсли;
	ИначеЕсли СтатусЗадания.Статус = "Выполняется" Тогда
		ОбновитьПроцентПрогресса();
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации",
			ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);
	ИначеЕсли СтатусЗадания.Статус = "Отменено" Или СтатусЗадания.Статус = "Ошибка" Тогда
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
		ЗаданиеЗапущеноВДругойФорме = Ложь;
		ИдентификаторЗаданияАктуализации = Неопределено;
		АктуализироватьДанные();
		Если СтатусЗадания.Статус = "Ошибка" И НЕ ПустаяСтрока(СтатусЗадания.ТекстОшибки) Тогда
			ПоказатьПредупреждение(, СтатусЗадания.ТекстОшибки);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПроверкиАктуальности(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если РезультатПроверки.УИДЗамера <> Неопределено Тогда
		ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(РезультатПроверки.УИДЗамера);
	КонецЕсли;
	
	Если Не РезультатПроверки.ТребуетсяАктуализация
		И АктуализироватьПриНулевке(Организация, ЭтоИПНулевка, ТекущийДень) Тогда
		// Закрываем декабрь предыдущего года для ИП - нужно для начисления взносов ИП
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Организация", Организация);
		ПараметрыОповещения.Вставить("ДатаАктуальности", НачалоГода(ТекущийДень) - 1);
		Оповестить("ТребуетсяАктуализация", ПараметрыОповещения);
		ТребуетсяАктуализация = Истина;
	ИначеЕсли Не РезультатПроверки.ТребуетсяАктуализация Тогда
		ТребуетсяАктуализация = Ложь;
		// Монитор вызывает различные помощники, в которых используется собственная проверка актуальности, период  которой 
		// может не совпадать с монитором, поэтому нужно обрабатывать оповещения только из самого монитора. 
		// Для этого добавляем параметр ВызовИзМонитора.
		Оповестить("АктуализацияЗавершенаУспешно", Новый Структура("Организация, ВызовИзМонитора", Организация, Истина));
	Иначе
		ПараметрыОповещения = Новый Структура("Организация, ДатаАктуальности", 
			Организация, РезультатПроверки.ДатаАктуальности);
		Оповестить("ТребуетсяАктуализация", ПараметрыОповещения);
		ТребуетсяАктуализация = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатАктуализации()
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	Если ЗаданиеЗапущеноВДругойФорме Тогда
		ПроверитьАктуальностьДанных();
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоАдресВременногоХранилища(АдресХранилищаАктуализации) Тогда
		Оповестить("АктуализацияОтменена", Новый Структура("Организация", Организация));
		Возврат;
	КонецЕсли;
	
	РезультатАктуализации = ПолучитьИзВременногоХранилища(АдресХранилищаАктуализации);
	УдалитьИзВременногоХранилища(АдресХранилищаАктуализации);
	АдресХранилищаАктуализации = "";
	
	Если РезультатАктуализации.Выполнено Тогда
		ПараметрыОповещения = Новый Структура("Организация", Организация);
		Оповестить("АктуализацияЗавершенаУспешно", ПараметрыОповещения);
		НачатьФормированиеМонитора(Ложь);
	Иначе
		ЗакрытиеМесяцаКлиент.ПоказатьОшибкиАктуализации(ЭтотОбъект, РезультатАктуализации);
		Оповестить("АктуализацияОтменена", Новый Структура("Организация", Организация));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктуализироватьДанные()
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	Элементы.ИдетАктуализация.Видимость = Истина;
	Элементы.ИдетПроверкаАктуальности.Видимость = Ложь;
	Элементы.ТребуетсяАктуализация.Видимость = Ложь;
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыАктуализации();
	ПараметрыАктуализации.Организация                  = Организация;
	ПараметрыАктуализации.Период                       = ТекущийДень;
	ПараметрыАктуализации.ИдентификаторЗадания         = ИдентификаторЗаданияАктуализации;
	ПараметрыАктуализации.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	
	Если ЭтоИПНулевка Тогда
		ПараметрыАктуализации.Период = КонецДня(ДатаАктуальности);
		ПараметрыАктуализации.АктуализацияДляРасчетаНалога = Истина;
		ПараметрыАктуализации.АктуализироватьВесьПериод = Истина;
	КонецЕсли;
	
	ЗаданиеАктуализации = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(Организация, УникальныйИдентификатор);
	ПодключитьПроверку = Ложь;
	
	Если ЗаданиеАктуализации = Неопределено Тогда
		РезультатВыполнения = ЗакрытиеМесяцаВызовСервера.АктуализироватьДанные(ПараметрыАктуализации);

		АдресХранилищаАктуализации = РезультатВыполнения.АдресРезультата;

		Если РезультатВыполнения.ЗаданиеВыполнено Тогда
			ОбработатьРезультатАктуализации();
			ИдентификаторЗаданияАктуализации = Неопределено;
		Иначе
			ИдентификаторЗаданияАктуализации = РезультатВыполнения.ИдентификаторЗадания;
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
			ПодключитьПроверку = Истина;
		КонецЕсли;
		
	Иначе
		
		ДанныеУчетаАктуальны = Ложь;
		ЗакрытиеМесяцаКлиент.УстановитьТекстПриОжиданииАктуализации(ЗаданиеАктуализации, ТекстПриАктуализации, НСтр("ru = 'для расчета'"));
		Если ИдентификаторЗаданияАктуализации <> ЗаданиеАктуализации.УникальныйИдентификатор Тогда
			// Задание запущено в другой форме
			ЗаданиеЗапущеноВДругойФорме = Истина;
			ИдентификаторЗаданияАктуализации = ЗаданиеАктуализации.УникальныйИдентификатор;
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
			ПодключитьПроверку = Истина;
		Иначе
			ЗаданиеЗапущеноВДругойФорме = Ложь;
		КонецЕсли;
		
	КонецЕсли;

	Если ПодключитьПроверку Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации",
			ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьПроцентПрогресса()
	
	Прогресс = ЗакрытиеМесяцаВызовСервера.ПрочитатьПрогресс(ИдентификаторЗаданияАктуализации);
	
	Если ТипЗнч(Прогресс) = Тип("Структура") И Прогресс.Свойство("Процент") Тогда
		Процент = Мин(Прогресс.Процент, 99);
		ПрогрессорАктуализации = Строка(Процент) + "%.";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьСсылкуНаПереходВЗакрытиеМесяца(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыйПараметрыАктуализацииОтчета();
	ПараметрыАктуализации.Организация = Организация;
	ПараметрыАктуализации.ДатаАктуальности = ДатаАктуальности;
	ПараметрыАктуализации.ДатаОкончанияАктуализации = ТекущийДень;
	
	ЗакрытиеМесяцаКлиент.ТекстПриНеобходимостиАктуализацииОбработкаНавигационнойСсылки(
		НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, ЭтотОбъект, ПараметрыАктуализации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеМесяцаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "СформироватьОтчет" Тогда
		
		БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
		Активизировать();
		ЗапуститьОбновление();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыЗависимыеОтОрганизации()
	
	ТекущийДень = Обработки.МониторНалоговИОтчетности.ТекущаяДатаМонитора();
	
	ЗависимыеОтОрганизации = Обработки.МониторНалоговИОтчетности.ПараметрыЗависимыеОтОрганизации(Организация, ТекущийДень);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗависимыеОтОрганизации);
	ВсеЗадачиВыполнены = Ложь;
	
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьСсылкуНаНалогОтчет(Элемент, АдресХранилища)
	
	СтрокиСсылок = ПолучитьИзВременногоХранилища(АдресХранилища);
	ОписаниеДействия = СтрокиСсылок[Элемент.Имя];
	
	Если ОписаниеДействия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОписаниеДействия) = Тип("Структура") Тогда
		Если ЭтоМобильныйКлиент
			И Не ФормаДоступнаВМобильном(ОписаниеДействия) Тогда
			ПоказатьНедоступнуюФункциональность(ОписаниеДействия);
		Иначе
			ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
		КонецЕсли;
	ИначеЕсли ТипЗнч(ОписаниеДействия) = Тип("Массив") Тогда
		ОткрытьФорму("Обработка.МониторНалоговИОтчетности.Форма.ФормаНесданныхОтчетов", 
			Новый Структура("ПараметрыСсылок", ОписаниеДействия), ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНедоступнуюФункциональность(ОписаниеДействия)
	ОписаниеНедоступнойЗадачи = НСтр("ru = 'Сформировать отчет'");
	МобильныйКлиентБПКлиент.ПоказатьФормуНедоступнаяФункциональность(ОписаниеНедоступнойЗадачи);
КонецПроцедуры

&НаКлиенте
Функция ФормаДоступнаВМобильном(ОписаниеДействия)
	
	Возврат ОписаниеДействия.ИмяФормы = "ОбщаяФорма.СписокЗадач"
		Или ОписаниеДействия.ИмяФормы = "Обработка.ПомощникУплатыНалога.Форма.ФормаЕдиная"
		Или ОписаниеДействия.ИмяФормы = "Обработка.ПомощникРасчетаНалогаУСН.Форма.Форма"
		Или ОписаниеДействия.ИмяФормы = "ОбщаяФорма.НапоминаниеОплатитьАУСН"
		Или ОписаниеДействия.ИмяФормы = "Документ.ПлатежноеПоручение.Форма.ФормаДокумента"
		Или ОписаниеДействия.ИмяФормы = "Документ.ПлатежноеПоручение.Форма.ФормаДокументаНалоговая"
		Или ОписаниеДействия.ИмяФормы = "Отчет.БанковскиеОперацииАУСН.ФормаОбъекта"
		Или ОписаниеДействия.ИмяФормы = "Документ.ОперацияСПатентом.ФормаСписка";
		
КонецФункции

&НаСервере
Процедура УстановитьОформлениеМонитора()
	
	Монитор = Обработки.МониторНалоговИОтчетности;
	Если ИспользуетсяКомандаНалоги Тогда
		Заголовок = НСтр("ru='Налоги'");
	КонецЕсли;
	
	АдресСсылокНадписей = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	АдресНавигационныхСсылок = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	КорректныйПериод = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов();
	
	МаксДата = ДобавитьМесяц(КорректныйПериод.КонецКорректногоПериода, 24);
	МинДата = ДобавитьМесяц(КорректныйПериод.НачалоКорректногоПериода, - 24);
	ШрифтТекста     = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста,, 11);
	ШрифтСсылок     = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста,, 11);
	ШрифтЗаголовков = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста,, 14);
	
	ЦветСерыйБлок   = ЦветаСтиля.ЦветМонитораЗадачаНеПоддерживается;
	ЦветЗеленыйБлок = ЦветаСтиля.ЦветМонитораВсеВыполнено;
	ЦветЖелтыйБлок  = ЦветаСтиля.ЦветМонитораПораВыполнятьЗадачу;
	ЦветКрасныйБлок = ЦветаСтиля.ЦветМонитораЗадачаПросрочена;
	ЦветПустойБлок  = ЦветаСтиля.ЦветФонаБлоковИнформационнойПанели;
	
	РадостныйСмайлик = БиблиотекаКартинок.РадостныйСмайлик;
	
	ТекущееЧислоРядов = 6;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуетсяКомандаНалоги()
	
	Возврат ОбщегоНазначенияБП.ЭтоПростойИнтерфейс()
		Или ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяРяд()
	Возврат "Ряд";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСтрока()
	Возврат "Строка";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяГруппаКонтейнер()
	Возврат "ГруппаКонтейнер";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяКонтейнер()
	Возврат "Контейнер";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораОрганизации(Форма, ДоступныеОрганизации)
	
	Элементы = Форма.Элементы;

	ПараметрВыбораОтборПоОрганизации     = Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеОрганизации);
	ПараметрыВыбораОрганизации           = Новый Массив();
	ПараметрыВыбораОрганизации.Добавить(ПараметрВыбораОтборПоОрганизации);
	Элементы.Организация.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораОрганизации);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьПриглашениеПознакомитьсяСПриложением()
	
	ОписаниеСистемыНавигации = ?(ИспользуетсяКомандаНалоги,
		НСтр("ru='Воспользуйтесь меню в верхней части экрана.'"),
		НСтр("ru='Воспользуйтесь меню в левой части экрана.'"));
	
	ТекстПредупреждения = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(НСтр("ru='Поздравляем!'"), 
			Новый Шрифт(, 12, Истина)),
		НСтр("ru='
		|На сегодняшний день вы сдали всю отчетность и уплатили все налоги.
		|
		|Познакомьтесь в демо-режиме с другими удобными функциями приложения, например:
		| • Выпишите счет на оплату вашему покупателю;
		| • Создайте платежное поручение для вашего поставщика;
		| • Оформите накладную на товары.
		|'"),
		ОписаниеСистемыНавигации,
		Символы.ПС);
	
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьОтключениеРекомендацииПоОбновлению()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ВРег("МониторНалоговИОтчетности"), ВРег("РекомендацияПоОбновлению"), Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьОтключениеПодсказкаПоЗадолженности()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ВРег("МониторНалоговИОтчетности"), ВРег("ПодсказкаПоЗадолженности"), Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПараметрыОпций = Новый Структура;
	ПараметрыОпций.Вставить("Организация", Организация);
	ПараметрыОпций.Вставить("Период", ТекущаяДатаСеанса());
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыОпций);
	
КонецПроцедуры

#Область Баннер

&НаКлиенте
Процедура Подключаемый_УстановитьБаннер()
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьСледующийБаннер()
	
	// Поскольку обработчик может вызываться не только интерактивно пользователем,
	// но и автоматически по таймеру, меняем баннер при условии, что форма находится в фокусе.
	Если НЕ ВводДоступен() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
		Возврат;
	КонецЕсли;
	
	УстановитьБаннер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьПредыдущийБаннер()
	
	УстановитьБаннер(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБаннер(ПоказатьПредыдущий = Ложь)
	
	ДлительнаяОперация = ПолучитьБаннерНаСервере(ПоказатьПредыдущий);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияБаннераВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияБаннераВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УстановитьБаннерНаФорме(ДлительнаяОперация.АдресРезультата);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьСледующийБаннер",
			ПерсонализированныеПредложенияСервисовКлиент.ИнтервалПереключенияБаннеров(), Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьБаннерНаСервере(ПоказатьПредыдущий)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение баннера в фоне'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", Организация);
	СтруктураПараметров.Вставить("Размещение", ПерсонализированныеПредложенияСервисов.ИмяРазмещенияМониторНалогов());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", ПоказатьПредыдущий);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ПерсонализированныеПредложенияСервисов.ПолучитьБаннер",
		СтруктураПараметров,
		НастройкиЗапуска);
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	ПерсонализированныеПредложенияСервисов.УстановитьБаннерНаФорме(ЭтотОбъект, АдресРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБаннерПерсонализированногоПредложения()
	
	Если Элементы.ИдетАктуализация.Видимость
		ИЛИ Элементы.ТребуетсяАктуализация.Видимость Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьБаннер", 1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АУСН()

	ИнтеграцияАУСНКлиент.ВыполнитьОбменССервисом(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЛКАУСН()
	
	ИнтеграцияАУСНКлиент.ВыполнитьОбменССервисомЛКАУСН(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция ПоддерживаетсяОбновлениеМонитораПриЗаписиПлатежки(Налог)
	
	Если Не ЗначениеЗаполнено(Налог) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Налог, "ВидНалога");
	ЭтоЕНС = ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоЕдиныйНалоговыйПлатеж(ВидНалога);
	Возврат ЭтоЕНС Или ВидНалога = Перечисления.ВидыНалогов.ФиксированныеВзносы_ФСС;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРядЕНСНулевка(Знач Ряд, ДанныеЕНС, ПараметрыСсылокНадписей, ПараметрыНавигационныхСсылок)
	
	Если Ряд.СтрокиНалога.Количество() = 0 Тогда
		Элементы.ДанныеЕНС.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекущийРяд = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Ряд);
	ТекущийРяд.СтрокаЗадачи = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Ряд.СтрокаЗадачи);
	
	ОстатокЕНС = ДанныеЕНС.ОстатокЕНС;
	РассчитаноНалогов  = ДанныеЕНС.РассчитаноНалогов;
	НужноОплатить = ДанныеЕНС.НужноОплатить;
	ВходящийОстатокЕНС = ДанныеЕНС.ВходящийОстатокЕНС;
	
	Элементы.СтрокаЕНС.Заголовок = НСтр("ru = 'Единый налоговый счет'");
	
	ЦветаИндикаторов = Обработки.МониторНалоговИОтчетности.ЦветаИндикаторов();
	СтрокаНалога = ТекущийРяд.СтрокиНалога[0];
	
	Событие = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаНалога);
	ОписаниеДействия = Обработки.МониторНалоговИОтчетности.ОписаниеДействияВзносыЗаСебяИПНулевка(
		Организация,
		Событие);
	
	ЗаполнитьЗначенияСвойств(СтрокаНалога, Событие);
	ЗаполнитьЗначенияСвойств(СтрокаНалога, ОписаниеДействия.ПараметрыФормы);
	
	РеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ОписаниеДействия.ПараметрыФормы.Правило,
		"Код, Владелец");
	
	ЕдиныйТарифВключенВЕНП = УчетСтраховыхВзносовИПКлиентСервер.ЕдиныйТарифВключенВЕдиныйНалоговыйПлатеж(
		СтрокаНалога.НачалоПериодаСобытия);
	
	Если ЕдиныйТарифВключенВЕНП Тогда
		СтрокаНалога.Наименование = ОписаниеДействия.ПараметрыФормы.Описание;
		СтрокаНалога.НаименованиеЗадачи = ОписаниеДействия.ПараметрыФормы.Описание;
		СтрокаНалога.КодЗадачи = ОписаниеДействия.ПараметрыФормы.ИдентификаторЗадачи;
	КонецЕсли;
	
	СтрокаНалога.КодПравила = РеквизитыПравила.Код;
	СтрокаНалога.Владелец = РеквизитыПравила.Владелец;
	СтрокаНалога.КУплатеТекущий = НужноОплатить;
	
	ЗаполнитьЗначенияСвойств(ТекущийРяд.СтрокаЗадачи, СтрокаНалога);
	ТекущийРяд.СтрокаЗадачи.ИмяЗадачи = СтрокаНалога.Наименование;
	
	Если ДанныеЕНС.ОстатокЕНС < 0 Тогда
		Элементы.ГруппаОплатаЕНС.ЦветФона = ЦветКрасныйБлок;
		СтрокаНалога.Индикатор = ЦветаИндикаторов.Красный;
	ИначеЕсли ДанныеЕНС.НужноОплатить = 0 Тогда
		Элементы.ГруппаОплатаЕНС.ЦветФона = ЦветЗеленыйБлок;
		СтрокаНалога.Индикатор = ЦветаИндикаторов.Зеленый;
	Иначе
		Элементы.ГруппаОплатаЕНС.ЦветФона = ЦветЖелтыйБлок;
		СтрокаНалога.Индикатор = ЦветаИндикаторов.Желтый;
	КонецЕсли;
	
	БлокНалогаЕНС = Обработки.МониторНалоговИОтчетности.СоздатьЭкземплярБлока(ТекущийРяд, Истина, ЭтотОбъект);
	ЗаполнитьЭлементБлока(БлокНалогаЕНС, 0, Истина, ПараметрыСсылокНадписей, ПараметрыНавигационныхСсылок, Истина);
	
	Если ЕдиныйТарифВключенВЕНП Тогда
		Элементы.СтрокаПомощникОплатаЕНС.Видимость = Истина;
		Элементы.РядЕНС.Высота = ВысотаБлокаЕНСПоУмолчанию();
	Иначе
		Элементы.СтрокаПомощникОплатаЕНС.Видимость = Ложь;
		Элементы.РядЕНС.Высота = ВысотаБлокаЕНСПоУмолчанию() - 2;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВысотаБлокаЕНСПоУмолчанию()
	
	Возврат 7;
	
КонецФункции

&НаСервереБезКонтекста
Функция АктуализироватьПриНулевке(Организация, ЭтоНулевка, Период)
	
	Если Не ЭтоНулевка Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// В нулевке нужно актуализировать начисление взносов ИП
	// Даже если ИП заходит в базу только для подачи нулевой декларации и не закрывает месяц,
	// нужно показывать, сколько следует заплатить взносов, в т.ч. за прошлый год
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(РегламентнаяОперация.Дата, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))) КАК ДатаПроведения
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Организация = &Организация
	|	И РегламентнаяОперация.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийРегламентныхОпераций.Выполнено)
	|	И РегламентнаяОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыРегламентныхОпераций.НачислениеСтраховыхВзносовИП)");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(РезультатЗапроса) Или Не ЗначениеЗаполнено(РезультатЗапроса[0].ДатаПроведения) Тогда
		// В базе нет ни одного документа, ИП только сдает нулевую отчетность
		// По учетной политике можно понять, с какого года предприниматель числится в базе
		ПериодСобытия = КонецГода(УчетнаяПолитика.ДатаПостановкиНаУчетНалогоплательщика(Организация, Период));
	Иначе
		ПериодСобытия = РезультатЗапроса[0].ДатаПроведения;
	КонецЕсли;
	
	Возврат НачалоГода(Период) - 1 > ПериодСобытия;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(
		ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

// Адаптирует форму под мобильный клиент
//
&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);

	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Шапка);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Актуализация);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Ожидание);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.ДанныеМонитора);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.ДанныеЕНС);

	АдаптироватьМКШапкаФормы();
	АдаптироватьМККоманднаяПанельФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	КомандыПоля.РазрешеноИзменение = Ложь;
	КомандыПоля.СворачиватьПрочиеКолонки = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКПерваяКомандаПанели(ЭтотОбъект, Элементы.ОбновитьМонитор);
	МобильныйКлиентБПФормаСписка.ПеренестиЭлементВМККомандыФормы(ЭтотОбъект, Элементы.НастройкаНалоговИОтчетов);
	МобильныйКлиентБПФормаСписка.ПеренестиКомандыВМККомандыФормы(ЭтотОбъект, Элементы.ДействияМонитора.ПодчиненныеЭлементы);

КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	Элементы.Шапка.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.Шапка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.Актуализировать.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ТребуетсяАктуализация.РастягиватьПоГоризонтали = Истина;
	
	Элементы.Ожидание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	Элементы.ОрганизацияПредставление.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Элементы.Организация.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Элементы.ОрганизацияПредставление.АвтоМаксимальнаяШирина = Ложь;
	Элементы.Организация.АвтоМаксимальнаяШирина = Ложь;
	Элементы.ОрганизацияПредставление.МаксимальнаяШирина = 0;
	Элементы.Организация.МаксимальнаяШирина = 0;
	Элементы.ОрганизацияПредставление.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.Организация.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ОрганизацияПредставление.РастягиватьПоГоризонтали = Истина;
	Элементы.Организация.РастягиватьПоГоризонтали = Истина;
	
	Элементы.РядЕНС.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.КонтейнерНаименованиеЕНС.Ширина = 0;
	Элементы.СтрокаЕНС.РастягиватьПоГоризонтали = Истина;
	
	//Новые динамические ряды создаются по образу Ряд1, статические - переопределим
	Для Сч = 1 по 6 Цикл
		Элементы[ИмяРяд()+Сч].Объединенная = Ложь;
		Элементы[ИмяРяд()+Сч].Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
