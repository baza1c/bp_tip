
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ПараметрыСсылок") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.ПараметрыСсылок[0].ЭтоТекущаяЗадача Тогда
		Заголовок = СтрШаблон(НСтр("ru = 'Текущие отчеты: %1'"), Параметры.ПараметрыСсылок[0].ИмяЗадачи);
	Иначе
		Заголовок = СтрШаблон(НСтр("ru = 'Просроченные отчеты: %1'"), Параметры.ПараметрыСсылок[0].ИмяЗадачи);
	КонецЕсли;
	
	Для каждого ТекСтрока Из Параметры.ПараметрыСсылок Цикл
		НоваяСтрока = Список.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		НоваяСтрока.НомерСтроки = Список.Индекс(НоваяСтрока) + 1;
	КонецЦикла;
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовИКомандФормы

&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	ОткрытьФормуОтчета();
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьФормуОтчета();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуОтчета()
	
	ВыбранныйОтчет = Элементы.Список.ТекущиеДанные;
	
	Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
		ОписаниеНедоступнойЗадачи = НСтр("ru = 'Сформировать отчет'");
		МобильныйКлиентБПКлиент.ПоказатьФормуНедоступнаяФункциональность(ОписаниеНедоступнойЗадачи);
	Иначе
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ВыбранныйОтчет);
	КонецЕсли;
	
КонецПроцедуры

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	//В мобильном открытие через выбор
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаДействия",
		"Видимость",
		Ложь);
	
КонецПроцедуры

#КонецОбласти
#КонецОбласти