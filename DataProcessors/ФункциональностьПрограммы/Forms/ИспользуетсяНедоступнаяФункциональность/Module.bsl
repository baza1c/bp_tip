
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РазделЛиды = Параметры.РазделЛиды;
	
	Если РазделЛиды Тогда
		Заголовок = НСтр("ru = 'Взаимодействие с лидами и покупателями отключено'");
		ЛицензияЗахвачена = Обработки.ФункциональностьПрограммы.ЗахватитьЛицензииДляИспользуемойФункциональности(
			НедоступнаяФункциональностьЛиды());
	Иначе
		Заголовок = НСтр("ru = 'Возможность проведения документов отключена'");
		ЛицензияЗахвачена = Обработки.ФункциональностьПрограммы.ЗахватитьЛицензииДляИспользуемойФункциональности();
	КонецЕсли;
	
	Если ЛицензияЗахвачена Тогда
		// Для используемой функциональности нашлись свободные лицензии
		// и их удалось удачно захватить.
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьТекстРекомендаций();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеЧтоСделатьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ФункциональностьПрограммы" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма.ФормаФункциональностьПрограммы", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТекстРекомендаций()
	
	ЭтоБизнесСтарт = ОбщегоНазначенияБП.ЭтоБизнесСтарт();
	
	Элементы.ОписаниеТариф.Видимость = Не ЭтоБизнесСтарт И Не РазделЛиды;
	Элементы.ОписаниеБлокирующаяФункциональность.Видимость = ЭтоБизнесСтарт И Не РазделЛиды;
	Элементы.ГруппаБлокирующаяФункциональность.Видимость = ЭтоБизнесСтарт;
	
	Если РазделЛиды Тогда
		
		Элементы.ОписаниеЗаголовок.Заголовок = ТекстЗаголовкаНедоступнойФункциональности();
		НедоступнаяФункциональность = НедоступнаяФункциональностьЛиды();
		ШаблонСтроки = НСтр("ru = 'Чтобы открыть доступ к ранее введенным данным, подключите опцию'");
		
		НедоступныеОпции = ОписаниеНедоступныхОпций(
			Обработки.ФункциональностьПрограммы.ОписаниеФункциональности(),
			НедоступнаяФункциональность);
		
		Элементы.ОписаниеФункциональность.Заголовок = Новый ФорматированнаяСтрока(ШаблонСтроки,
			" ",
			НедоступныеОпции.Представление,
			НСтр("ru = '. Для этого обратитесь к обслуживающей организации.'"));
	
	ИначеЕсли ЭтоБизнесСтарт Тогда
		
		Элементы.ОписаниеЗаголовок.Заголовок = ТекстЗаголовкаНедоступнойФункциональности();
		
		ОписаниеФункциональности = Обработки.ФункциональностьПрограммы.ОписаниеФункциональности();
		
		НедоступнаяФункциональность = Обработки.ФункциональностьПрограммы.ВключеннаяНедоступнаяФункциональность(
			ОписаниеФункциональности);
		
		НедоступныеОпции = ОписаниеНедоступныхОпций(ОписаниеФункциональности, НедоступнаяФункциональность);
		
		Если НедоступныеОпции.КоличествоОпций > 1 Тогда
			ШаблонСтроки = НСтр("ru = 'Чтобы включить проведение документов, подключите тариф с опциями'");
		Иначе
			ШаблонСтроки = НСтр("ru = 'Чтобы включить проведение документов, подключите тариф с опцией'");
		КонецЕсли;
		
		Элементы.ОписаниеФункциональность.Заголовок = Новый ФорматированнаяСтрока(ШаблонСтроки,
			" ",
			НедоступныеОпции.Представление,
			НСтр("ru = '. Для этого обратитесь к обслуживающей организации.'"));
		
		РодительскаяГруппаДляНадписей =
			?(НедоступнаяФункциональность.Количество() > 1,
			Элементы.ГруппаБлокирующаяФункциональность,
			Элементы.ГруппаОписание);
		
		Для Каждого ИмяФункциональности Из НедоступнаяФункциональность Цикл
			СвойстваФункциональности = ОписаниеФункциональности.Найти(ИмяФункциональности.Ключ);
			Декорация = Элементы.Добавить(
				СтрШаблон("НедоступнаяФункциональность%1", ИмяФункциональности.Ключ),
				Тип("ДекорацияФормы"),
				РодительскаяГруппаДляНадписей);
			Декорация.Гиперссылка = Истина;
			Декорация.УстановитьДействие("Нажатие", "Подключаемый_Нажатие");
			Декорация.Заголовок = СвойстваФункциональности.Заголовок;
		КонецЦикла;
		
	Иначе
		ТекстРекомендаций = Новый Массив;
		ТекстРекомендаций.Добавить(НСтр("ru = 'Восстановить возможность проведения документов можно, отказавшись от этой функциональности.'"));
		ТекстРекомендаций.Добавить(" ");
		ТекстРекомендаций.Добавить(НСтр("ru = 'Для этого снимите флажки с пометкой'"));
		ТекстРекомендаций.Добавить(" ");
		ТекстРекомендаций.Добавить(Новый ФорматированнаяСтрока(
			ФункциональностьПрограммы.ТекстНедоступенТариф(), , , ФункциональностьПрограммы.ЦветБаннераНедоступенТариф()));
		ТекстРекомендаций.Добавить(" ");
		ТекстРекомендаций.Добавить(НСтр("ru = 'и'"));
		ТекстРекомендаций.Добавить(" ");
		ТекстРекомендаций.Добавить(Новый ФорматированнаяСтрока(
			ФункциональностьПрограммы.ТекстОграничениеТарифа(), , , ФункциональностьПрограммы.ЦветБаннераОграниченияТарифа()));
		ТекстРекомендаций.Добавить(" ");
		ТекстРекомендаций.Добавить(НСтр("ru = 'в настройках'"));
		ТекстРекомендаций.Добавить(" ");
		ТекстРекомендаций.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Функциональности'"), , , , "ФункциональностьПрограммы"));
		ТекстРекомендаций.Добавить(".");
		
		Элементы.ОписаниеФункциональность.Заголовок = Новый ФорматированнаяСтрока(ТекстРекомендаций);
		
		ТекстРекомендаций = Новый Массив;
		ТекстРекомендаций.Добавить(НСтр("ru = 'Если же эта функциональность вам действительно нужна, перейдите на другой тариф.'"));
		ТекстРекомендаций.Добавить(" ");
		ТекстРекомендаций.Добавить(НСтр("ru = 'Для изменения тарифа обратитесь к обслуживающей организации.'"));
		
		Элементы.ОписаниеТариф.Заголовок = Новый ФорматированнаяСтрока(ТекстРекомендаций);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Нажатие(Элемент)
	
	ОповещениеЗакрытия = Новый ОписаниеОповещения("ЗакрытиеФормыСпискаОбъектовПрепятствующихОтключениюОпции", ЭтотОбъект);
	ФункциональностьПрограммыКлиент.ПоказатьОбъектыПрепятствующиеОтключениюОпции(
		Элемент, "НедоступнаяФункциональность", ЭтотОбъект, ОповещениеЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФормыСпискаОбъектовПрепятствующихОтключениюОпции(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ВсеНедоступныеОпцииОтключены() Тогда
		ОбработкаОтветаПерезапускПрограммы = Новый ОписаниеОповещения("ОбработкаОтветаПерезапускПрограммы", ЭтотОбъект);
		ПоказатьВопрос(
				ОбработкаОтветаПерезапускПрограммы,
				НСтр("ru = 'Для включения возможности проведения документов необходимо перезапустить программу. Перезапустить сейчас?'"),
				РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВсеНедоступныеОпцииОтключены()
	
	РезультатОтключенияОпцийЗарплатаКадры =
		Обработки.ФункциональностьПрограммы.ОтключитьНедоступнуюФункциональностьЗарплатаКадры();
	
	НедоступнаяФункциональностьЗарплатаКадрыОтключена =
		Не РезультатОтключенияОпцийЗарплатаКадры.ПотребовалосьОтключениефункциональности
		Или РезультатОтключенияОпцийЗарплатаКадры.ОтключениеВыполненоУспешно;
	
	Возврат Обработки.ФункциональностьПрограммы.УстановитьФункциональностьПоОпцииРазрешенПолныйИнтерфейс()
		И НедоступнаяФункциональностьЗарплатаКадрыОтключена;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОтветаПерезапускПрограммы(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗавершитьРаботуСистемы(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеНедоступныхОпций(ОписаниеФункциональности, НедоступнаяФункциональность)
	
	Результат = Новый Структура;
	Результат.Вставить("Представление", "");
	Результат.Вставить("Количество", 0);
	
	РазрешенПолныйИнтерфейс = ТарификацияБП.РазрешенПолныйИнтерфейс();
	РазрешенУчетЗарплаты = ТарификацияБП.РазрешенУчетЗарплаты();
	РазрешеноВзаимодействиеСЛидами = ТарификацияБПВызовСервераПовтИсп.РазрешеноВзаимодействиеСЛидами();
	
	ЦветНедоступенТариф = ФункциональностьПрограммы.ЦветБаннераНедоступенТариф();
	
	ОпцияСотрудникиДобавлена = Ложь;
	ОпцияПолныйИнтерфейсДобавлена = Ложь;
	ОпцияЛидыДобавлена = Ложь;
	
	НедоступныеОпции = Новый Массив;
	
	// Проверяем каких именно опций не хватает на текущем тарифе
	Для Каждого Функциональность Из НедоступнаяФункциональность Цикл
		
		СвойстваФункциональности = ОписаниеФункциональности.Найти(Функциональность.Ключ, "Имя");
		
		Если Не ОпцияСотрудникиДобавлена И СвойстваФункциональности.Раздел = "ЗарплатаКадры" И Не РазрешенУчетЗарплаты Тогда
			НедоступныеОпции.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'С сотрудниками'"), , , ЦветНедоступенТариф));
			ОпцияСотрудникиДобавлена = Истина;
		КонецЕсли;
		
		Если Не ОпцияПолныйИнтерфейсДобавлена И Не СвойстваФункциональности.ДоступнаВПростомИнтерфейсе И Не РазрешенПолныйИнтерфейс Тогда
			НедоступныеОпции.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Полный интерфейс'"), , , ЦветНедоступенТариф));
			ОпцияПолныйИнтерфейсДобавлена = Истина;
		КонецЕсли;
		
		Если СвойстваФункциональности.Раздел = ФункциональностьПрограммыКлиентСервер.РазделЛиды()
			И Не РазрешеноВзаимодействиеСЛидами
			И Не ОпцияЛидыДобавлена Тогда
				
			НедоступныеОпции.Добавить(Новый ФорматированнаяСтрока(
				ФункциональностьПрограммыКлиентСервер.ПредставлениеРазделаЛиды(), , ,
				ЦветНедоступенТариф));
			ОпцияЛидыДобавлена = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	КоличествоОпций = НедоступныеОпции.Количество();
	
	РазделительОпций = СтрШаблон(НСтр("ru = '%1и%1'"), Символы.НПП);
	ИндексОпции = 0;
	ТекстОпции = "";
	
	Пока ИндексОпции < КоличествоОпций Цикл
		Если ИндексОпции = 0 Тогда
			ТекстОпции = Новый ФорматированнаяСтрока(НедоступныеОпции[ИндексОпции]);
		Иначе
			ТекстОпции = Новый ФорматированнаяСтрока(ТекстОпции, РазделительОпций, НедоступныеОпции[ИндексОпции]);
		КонецЕсли;
		
		ИндексОпции = ИндексОпции + 1;
	КонецЦикла;
	
	Результат.Представление = ТекстОпции;
	Результат.Количество = КоличествоОпций;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция НедоступнаяФункциональностьЛиды()
	
	Результат = Новый Структура;
	Результат.Вставить(ФункциональностьПрограммыВызовСервера.НаименованиеОпцииЛиды(), 
		ТарификацияБП.ИдентификаторУслугиВзаимодействиеСЛидами());
	
	Возврат Результат;
		
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗаголовкаНедоступнойФункциональности()
	
	Возврат НСтр("ru = 'В программе используется функциональность, которая не входит в ваш текущий тариф.'");
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаКартинкаОписание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
КонецПроцедуры

#КонецОбласти
