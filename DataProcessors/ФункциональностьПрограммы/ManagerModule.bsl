#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеФункциональности

Функция ОписаниеФункциональности() Экспорт
	
	// При добавлении новой функциональности следует внести изменения в макет Перечисления.ВидыБизнеса.Макеты.Функциональность.
	// В макете указывается связь вида бизнеса и используемой функциональности.
	
	ОписаниеФункциональности = НовыйОписаниеФункциональности();
	
	ЗаполнитьОписаниеФункциональности(ОписаниеФункциональности);
	
	ОчиститьНедоступнуюРодительскуюФункциональность(ОписаниеФункциональности);
	
	ЗаполнитьДочернююФункциональность(ОписаниеФункциональности);
	
	Для Каждого Функциональность Из ОписаниеФункциональности Цикл
		Если Функциональность.ПроверяютсяУсловияТарификации = Неопределено Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Для функциональности %1 не добавлена проверка на соответствие условиям тарификации'"),
				Функциональность.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОписаниеФункциональности;
	
КонецФункции

Функция НовыйОписаниеФункциональности()
	
	ОписаниеФункциональности = Новый ТаблицаЗначений;
	
	// Имя функциональности - должно совпадать с именем константы,
	// которая связана с функциональной опцией.
	ОписаниеФункциональности.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	
	// Заголовок для пользовательского интерфейса
	ОписаниеФункциональности.Колонки.Добавить("Заголовок", Новый ОписаниеТипов("Строка"));
	
	// Подсказка к элементу пользовательского интерфейса
	ОписаниеФункциональности.Колонки.Добавить("Подсказка", Новый ОписаниеТипов("Строка"));
	
	// Имя раздела, к которому относится функциональность.
	// Должен совпадать с именем одной из закладок на форме ФункциональностьПрограммы
	ОписаниеФункциональности.Колонки.Добавить("Раздел", Новый ОписаниеТипов("Строка"));
	
	// Признак, что функциональность изменяется в длительной операции.
	ОписаниеФункциональности.Колонки.Добавить("ТребуетсяДлительнаяОперация", Новый ОписаниеТипов("Булево"));
	
	// Признак, что функциональность доступна в простом интерфейсе.
	// Если не доступна - то в простом интерфейсе такую функциональность можно будет включить только
	// после перехода на полный интерфейс.
	ОписаниеФункциональности.Колонки.Добавить("ДоступнаВПростомИнтерфейсе", Новый ОписаниеТипов("Булево"));
	
	// Признак, что функциональность нужно автоматически включить при переходе на полный интерфейс.
	// Нужно установить Истина в данном параметре, если он описывает функциональность, которая обязательно
	// должна быть включена в полном интерфейсе и ее выключение в нем заблокировано.
	ОписаниеФункциональности.Колонки.Добавить("ДолжнаБытьОбязательноВключенаВПолномИнтерфейсе", Новый ОписаниеТипов("Булево"));
	
	// Родительская функциональность - список функциональности, которую необходимо включить,
	// чтобы можно было включить данную функциональность.
	// Для добавления родительской функциональности нужно использовать функцию ДобавитьРодителя()
	ОписаниеФункциональности.Колонки.Добавить("РодительскаяФункциональность", Новый ОписаниеТипов("Массив"));
	
	// Дочерняя функциональность заполняется автоматически.
	ОписаниеФункциональности.Колонки.Добавить("ДочерняяФункциональность", Новый ОписаниеТипов("Массив"));
	
	// Признак, что функциональность в данной версии приложения доступна.
	// Если Ложь - то функциональность не будет отображаться на форме.
	ОписаниеФункциональности.Колонки.Добавить("Доступна", Новый ОписаниеТипов("Булево"));
	
	// Признак, что функциональность отображается на форме.
	// Функциональность может не отображаться, например, в результате поиска по подстроке.
	ОписаниеФункциональности.Колонки.Добавить("Видимость", Новый ОписаниеТипов("Булево"));
	
	// Признак, что при включении "Синхронизации данных" нужно включить функциональность.
	// Также влияет на отключение - если синхронизация данных включена, то отключить функциональность нельзя.
	ОписаниеФункциональности.Колонки.Добавить("ТребуетсяДляОбменов", Новый ОписаниеТипов("Булево"));
	
	// Тарифная опция, которые должны быть доступна к захвату, чтобы функциональность можно было включить.
	ОписаниеФункциональности.Колонки.Добавить("ИмяТарифнойОпции", Новый ОписаниеТипов("Строка"));
	
	// Для константы, отвечающей за функциональность должна быть добавлена проверка
	// ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью модуля менеджера значения.
	// После добавления указанной проверки необходимо установить признак ПроверяютсяУсловияТарификации в значение Истина
 	ОписаниеФункциональности.Колонки.Добавить("ПроверяютсяУсловияТарификации");
	
	ОписаниеФункциональности.Индексы.Добавить("Имя");
	
	Возврат ОписаниеФункциональности;
	
КонецФункции

Процедура ЗаполнитьОписаниеФункциональности(ОписаниеФункциональности)
	
	ЗаполнитьОписаниеРазделаБанкКасса(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаЗапасы(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаРасчеты(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаУчетПоДоговорам(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаПланированиеПлатежей(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаТорговля(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаРозничнаяТорговля(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаКомиссионнаяТорговля(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаВнешняяТорговля(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаОбязательнаяМаркировка(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаЛиды(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаПроизводство(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаОсновныеСредства(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаНематериальныеАктивы(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаЗарплатаКадры(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаОрганизация(ОписаниеФункциональности);
	
	ЗаполнитьОписаниеРазделаДополнительно(ОписаниеФункциональности);
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаБанкКасса(ОписаниеФункциональности)
	
	Раздел = "БанкКасса"; 
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьСогласованиеПлатежей",
		НСтр("ru = 'Согласование платежей'"),
		НСтр("ru = 'Согласование платежных поручений перед отправкой в банк'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
  
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьСтатьиДвиженияДенежныхСредств",
		НСтр("ru = 'Статьи движения денег'"),
		НСтр("ru = 'Учет поступлений и расходов денег по статьям.
			|Выбор статей движения в кассовых и банковских документах'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетДенежныхДокументов",
		НСтр("ru = 'Денежные документы'"),
		НСтр("ru = 'Учет документов, оформленных на бланках строгой отчетности (билеты, путевки и т.д.)'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьОплатуПоПлатежнымКартам",
		НСтр("ru = 'Платежные карты'"),
		НСтр("ru = 'Возможность оплаты товаров и услуг покупателями при помощи платежных карт и банковских кредитов'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьОплатуСертификатамиНСПК",
		НСтр("ru = 'Электронные сертификаты на социально значимые товары (491-ФЗ)'"),
		НСтр("ru = 'Прием электронных сертификатов на карте МИР, для оплаты отдельных групп социально значимых товаров за счет бюджетных средств'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ИспользоватьОплатуПоПлатежнымКартам");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПлатежиПоРеестрам", НСтр("ru = 'Платежи по реестрам'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПлатежныеТребования",
		НСтр("ru = 'Платежные требования'"),
		НСтр("ru = 'Выставление покупателям платежных требований'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьФискальныйРегистратор",
		НСтр("ru = 'Онлайн-касса'"),
		НСтр("ru = 'Возможность подключения онлайн-кассы (ККТ с передачей данных для печати чеков контрагентам)'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьИнкассацию",
		НСтр("ru = 'Инкассация'"),
		НСтр("ru = 'Наличные деньги из кассы в банк доставляют инкассаторы'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "УчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров",
		НСтр("ru = 'Валютный контроль'"),
		НСтр("ru = 'Указание кодов валютных операций в документах поступления и списания безналичных денежных средств, а также номеров валютных контрактов в договорах'")); 
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаЗапасы(ОписаниеФункциональности)
	
	Раздел = "Запасы";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетСпецодеждыСпецоснастки",
		НСтр("ru = 'Ведомости выдачи спецодежды'"),
		НСтр("ru = 'Выдача спецодежды по ведомости нескольким сотрудникам, передача спецоснастки в подразделение, назначение использования'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВедетсяУчетМалоценки");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетМалоценки",
		НСтр("ru = 'Малоценное оборудование и запасы'"),
		НСтр("ru = 'Учет малоценного оборудования, специальной одежды, инструмента, приспособлений и хозяйственного инвентаря'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Ложь;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетВозвратнойТары",
		НСтр("ru = 'Возвратная тара'"),
		НСтр("ru = 'Учет многооборотной возвратной тары'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьКомплектациюНоменклатуры",
		НСтр("ru = 'Комплектация номенклатуры'"),
		НСтр("ru = 'Возможность формировать номенклатурные позиции из составных частей, а также изменять их состав'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетПоПутевымЛистам",
		НСтр("ru = 'Путевые листы'"),
		НСтр("ru = 'Учет топлива по путевым листам'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	//Поступление на несколько складов
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ПоступлениеНаНесколькоСкладов",
		НСтр("ru = 'Поступление на несколько складов'"),
		НСтр("ru = 'Возможность оформлять поступления товаров на разные склады одним документом'"));
	Функциональность.ДоступнаВПростомИнтерфейсе 	= Истина;
	Функциональность.ТребуетсяДляОбменов 			= Ложь;
	Функциональность.ПроверяютсяУсловияТарификации 	= Ложь;
	Функциональность.Видимость 						= Справочники.Склады.КоличествоСкладов() > 1 
														Или ОтказСнятьПоступлениеНаНесколькоСкладов() 
	                                                    Или ПолучитьФункциональнуюОпцию("ПоступлениеНаНесколькоСкладов");
КонецПроцедуры                                          

Процедура ЗаполнитьОписаниеРазделаОсновныеСредства(ОписаниеФункциональности)
	
	Раздел = "ОсновныеСредства";
	
	ЭтоВерсияКОРП = ВариантыПриложений.ЭтоВерсияКОРП();
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетОсновныхСредств",
		НСтр("ru = 'Основные средства'"),
		НСтр("ru = 'Учет основных средств (зданий, оборудования, транспортных средств и т.д.)'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	// От этой функциональности зависит константа РасширенныйФункционалОсновныеСредства - см. НастроитьАгрегирующуюКонстанту
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетЛизинговогоИмущества",
		НСтр("ru = 'Лизинг'"),
		НСтр("ru = 'Учет имущества и обязательств  по договорам лизинга'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Ложь;
	ДобавитьРодителя(Функциональность, "ВедетсяУчетОсновныхСредств");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетПредметовАренды",
		НСтр("ru = 'Права пользования предметами аренды'"),
		НСтр("ru = 'Бухгалтерский учет прав пользования предметами аренды
			|и обязательств по аренде'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Ложь;
	ДобавитьРодителя(Функциональность, "ВедетсяУчетОсновныхСредств");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетСдачиИмуществаВАренду",
		НСтр("ru = 'Сдача в аренду'"),
		НСтр("ru = 'Учет имущества, передаваемого в аренду'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВедетсяУчетОсновныхСредств");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетГрупповыхОС",
		НСтр("ru = 'Групповые объекты основных средств'"),
		НСтр("ru = 'Учет в составе основных средств большого количества однородных объектов (инвентаря, инструментов, мебели, компьютеров и т.д.)'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВедетсяУчетОсновныхСредств");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "КомпонентыАмортизацииОС",
		НСтр("ru = 'Компоненты амортизации'"),
		НСтр("ru = 'Обособленный расчет амортизации существенных по величине затрат на проведение ремонта, технического осмотра, технического обслуживания основных средств'"));
	Функциональность.Доступна = ЭтоВерсияКОРП;
	Функциональность.ДоступнаВПростомИнтерфейсе = Ложь;
	ДобавитьРодителя(Функциональность, "ВедетсяУчетОсновныхСредств");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаНематериальныеАктивы(ОписаниеФункциональности)
	
	Раздел = "НематериальныеАктивы";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетНМА",
		НСтр("ru = 'Нематериальные активы'"),
		НСтр("ru = 'Учет нематериальных активов (товарных знаков, исключительных прав и т.д.)'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	// От этой функциональности зависит константа РасширенныйФункционалНематериальныеАктивы - см. НастроитьАгрегирующуюКонстанту
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаТорговля(ОписаниеФункциональности)
	
	Раздел = "Торговля";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяОтгрузкаБезПереходаПраваСобственности",
		НСтр("ru = 'Отгрузка без перехода права собственности, продажи на Яндекс.Маркет'"),
		НСтр("ru = 'Реализация товаров с особыми условиями перехода прав собственности'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПакетноеВыставлениеАктов",
		НСтр("ru = 'Пакетное выставление актов и счетов-фактур'"),
		НСтр("ru = 'Формирование актов и счетов-фактур по одной услуге одновременно нескольким контрагентам (услуги связи, аренды и т.д.)'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьНачислениеНДССМежценовойРазницы",
		НСтр("ru = 'НДС с межценовой разницы'"),
		НСтр("ru = 'При реализации некоторых групп товаров, в том числе приобретенных у физических лиц, требуется начислять НДС на разницу между ценой реализации и приобретения товаров'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьКорректировочныеДокументы",
		НСтр("ru = 'Исправительные и корректировочные документы'"),
		НСтр("ru = 'Исправления и корректировки в поступлении и реализации товаров (работ, услуг) отражаются специальными документами'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПередачуПрав",
		НСтр("ru = 'Использовать передачу прав'"),
		НСтр("ru = 'Печатная форма ""Акт на передачу прав"" в документах реализации (акты, накладные)'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьДоставкуАвтотранспортом",
		НСтр("ru = 'Доставка товара автотранспортом'"),
		НСтр("ru = 'Осуществляется доставка товаров автомобильным транспортом с оформлением транспортных и товарно-транспортных накладных'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьДоставкуТранспортнойКомпанией",
		НСтр("ru = 'Доставка транспортной компанией'"),
		НСтр("ru = 'Осуществляется доставка товара транспортными компаниями с оказанием сопутствующих услуг'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПродажиЧерезМаркетплейс",
		НСтр("ru = 'Продажи на Ozon, Wildberries'"),
		НСтр("ru = 'Маркетплейс обеспечивает расчеты с покупателем и доставку заказов'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьОбменСИнтернетМагазином",
		НСтр("ru = 'Интеграция с интернет-магазином'"),
		НСтр("ru = 'Счета покупателям и сведения об онлайн-оплатах создаются по заказам интернет-магазина'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ЗагрузкаДанныхИзТиС", НСтр("ru = 'Загрузка данных из конфигурации Торговля и Склад 7.7'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаРозничнаяТорговля(ОписаниеФункциональности)
	
	Раздел = "РозничнаяТорговля";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяРозничнаяТорговля",
		НСтр("ru = 'Учет товаров в розничной торговле'"),
		НСтр("ru = 'Печать фискальных и товарных чеков, формирование отчетов о розничных продажах'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользуютсяПодарочныеСертификаты",
		НСтр("ru = 'Подарочные сертификаты'"),
		НСтр("ru = 'Продаются и принимаются к оплате подарочные сертификаты'"));
	ДобавитьРодителя(Функциональность, "ВедетсяРозничнаяТорговля");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПредоплатыВРознице",
		НСтр("ru = 'Предоплата'"),
		НСтр("ru = 'Получение и зачет предоплат в розничной торговле'"));
	ДобавитьРодителя(Функциональность, "ВедетсяРозничнаяТорговля");
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		НСтр("ru = 'Алкогольная продукция'"),
		НСтр("ru = 'Розничная продажа пива и алкогольной продукции'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВедетсяРозничнаяТорговля");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаКомиссионнаяТорговля(ОписаниеФункциональности)
	
	Раздел = "КомиссионнаяТорговля";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ОсуществляетсяРеализацияТоваровУслугКомитентов", НСтр("ru = 'Продажа товаров или услуг комитентов (принципалов)'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетПоДоговорам");
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров", НСтр("ru = 'Продажа товаров или услуг через комиссионеров (агентов)'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетПоДоговорам");
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ОсуществляетсяЗакупкаТоваровУслугДляКомитентов", НСтр("ru = 'Приобретение товаров или услуг для комитентов (принципалов)'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетПоДоговорам");
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров", НСтр("ru = 'Приобретение товаров или услуг через комиссионеров (агентов)'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетПоДоговорам");
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаВнешняяТорговля(ОписаниеФункциональности)
	
	Раздел = "ВнешняяТорговля";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетИмпортныхТоваров",
		НСтр("ru = 'Приобретение и реализация импортных товаров'"),
		НСтр("ru = 'Учет товаров по номерам таможенных деклараций'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВестиУчетПрослеживаемыхТоваров",
		НСтр("ru = 'Приобретение и реализация прослеживаемых товаров'"),
		НСтр("ru = 'Учет товаров по РНПТ'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВедетсяУчетИмпортныхТоваров");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИмпортПодакцизныхТоваров",
		НСтр("ru = 'Приобретение подакцизных товаров'"),
		НСтр("ru = 'Учет акцизов при импорте подакцизных товаров'"));
	ДобавитьРодителя(Функциональность, "ВедетсяУчетИмпортаТоваров");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетИмпортаТоваров",
		НСтр("ru = 'Импорт товаров, в том числе из ЕАЭС'"),
		НСтр("ru = 'Учет таможенных деклараций по операциям импорта, 
			|а также заявлений о ввозе товаров из Евразийского Экономического Союза'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетТаможенныхДекларацийЭкспорт",
		НСтр("ru = 'Экспорт товаров, в том числе в ЕАЭС'"),
		НСтр("ru = 'Учет таможенных деклараций по операциям экспорта,
			|а также заявлений о ввозе товаров, полученных от покупателей из Евразийского Экономического Союза'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаОбязательнаяМаркировка(ОписаниеФункциональности)
	
	Раздел = "ОбязательнаяМаркировка";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПодконтрольныеТоварыККТ",
		НСтр("ru = 'Продажа средств индивидуальной защиты'"),
		НСтр("ru = 'Продажа средств индивидуальной защиты (медицинские маски, перчатки, и т.д.) с использованием ККТ'"));
	Функциональность.ДоступнаВПростомИнтерфейсе 	= Истина;
	ДобавитьРодителя(Функциональность, "ИспользоватьФискальныйРегистратор");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации 	= Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВестиУчетМаркировкиПродукцииВГИСМ", НСтр("ru = 'Маркировка меховых изделий'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	//маркировка в простом интерфейсе
	Функциональность = ДобавитьОписаниеФункциональности(ОписаниеФункциональности, Раздел, "ИспользоватьМаркировку",
						НСтр("ru = 'Торговля маркированными товарами'"), 
						НСтр("ru = 'Торговля товарами имеющими индентификационные коды государственной информационной системы ""Честный Знак""'"));
	Функциональность.ДоступнаВПростомИнтерфейсе 	= Истина;
	Функциональность.ПроверяютсяУсловияТарификации 	= Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаРасчеты(ОписаниеФункциональности)
	
	Раздел = "Расчеты";
	
	ЭтоВерсияКОРП = ВариантыПриложений.ЭтоВерсияКОРП();
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВестиУчетРасчетовСКонтрагентами", 
		НСтр("ru = 'Расчеты с покупателями и поставщиками'"),
		НСтр("ru = 'Учет задолженности покупателей и поставщиков, полученных и выданных авансов. Счета, акты и накладные от поставщиков, авансовые отчеты'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ДолжнаБытьОбязательноВключенаВПолномИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьФакторинг",
		НСтр("ru = 'Факторинговые операции'"),
		НСтр("ru = 'Возможность отражать расчеты через факторинговую компанию'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "УправлениеЗачетомАвансовПогашениемЗадолженности",
		НСтр("ru = 'Управление зачетом авансов и погашением задолженности'"),
		НСтр("ru = 'Возможность выбора конкретных документов при зачете авансов и погашении задолженности, отражение взаимозачетов документом ""Корректировка долга""'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользуетсяГособоронзаказ",
		НСтр("ru = 'Контракты государственного заказа'"),
		НСтр("ru = 'Расчеты по контрактам для выполнения государственного заказа'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.Доступна = ДоступнаФункциональностьГособоронзаказ();
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	Функциональность.ИмяТарифнойОпции = ТарификацияБП.ИдентификаторУслугиГособоронзаказ();
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьУведомленияОКонтролируемыхСделках",
		НСтр("ru = 'Контролируемые сделки'"),
		НСтр("ru = 'Представление Уведомления о контролируемых сделках в ФНС'"));
	Функциональность.Доступна = ЭтоВерсияКОРП;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	Функциональность.ИмяТарифнойОпции = ТарификацияБП.ИдентификаторУслугиУведомленияОКонтролируемыхСделках();
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьВыплатыСамозанятым",
		НСтр("ru = 'Реестры выплат самозанятым'"),
		НСтр("ru = 'Оформление реестра для передачи в банк при выплате самозанятым'"));
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаУчетПоДоговорам(ОписаниеФункциональности)
	
	Раздел = "УчетПоДоговорам";
	
	ЭтоВерсияКОРП = ВариантыПриложений.ЭтоВерсияКОРП();
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВестиУчетПоДоговорам",
		НСтр("ru = 'Учет по договорам'"),
		НСтр("ru = 'Возможность выбора конкретных договоров для расчетов с контрагентами'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИсполняютсяОбязанностиНалоговогоАгентаПоНДС",
		НСтр("ru = 'Организация - налоговый агент по НДС'"),
		НСтр("ru = 'Организация выступает в качестве налогового агента по НДС'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетПоДоговорам");
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяУчетУслугИностранныхИнтернетКомпаний",
		НСтр("ru = 'Услуги иностранных интернет-компаний'"),
		НСтр("ru = 'Договоры на приобретение электронных услуг у иностранных компаний'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетПоДоговорам");
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ОбратноеНачислениеНДС",
		НСтр("ru = 'Обратное начисление НДС'"),
		НСтр("ru = 'Договоры на покупку и реализацию металлолома,
			|вторичного алюминия и сырых шкур животных'"));
	Функциональность.ТребуетсяДляОбменов = Истина;
	Функциональность.Доступна = ЭтоВерсияКОРП;
	ДобавитьРодителя(Функциональность, "ВестиУчетПоДоговорам");
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	Функциональность.ИмяТарифнойОпции = ТарификацияБП.ИдентификаторУслугиОбратноеНачислениеНДС();
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "УчетОбеспеченийОбязательствИПлатежей",
		НСтр("ru = 'Учет обеспечений обязательств и платежей'"),
		НСтр("ru = 'Обеспечения по договорам (залоги, поручительства, гарантии)'"));
	Функциональность.ТребуетсяДляОбменов        = Ложь;
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетПоДоговорам");
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаПланированиеПлатежей(ОписаниеФункциональности)
	
	Раздел = "ПланированиеПлатежей";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПланированиеПлатежейПоставщикам",
		НСтр("ru = 'Планирование платежей поставщикам'"),
		НСтр("ru = 'Возможность указывать сроки оплаты для документов ""Поступление (акты, накладные)"", ""Поступление доп. расходов"", ""Поступление НМА"" и ""Счет от поставщика""'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьПланированиеПлатежейОтПокупателей",
		НСтр("ru = 'Планирование платежей от покупателей'"),
		НСтр("ru = 'Возможность указывать сроки оплаты для документов ""Реализация (акты, накладные)"", ""Оказание производственных услуг"", ""Передача ОС"", ""Передача НМА"" и ""Счет покупателю""'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаПроизводство(ОписаниеФункциональности)
	
	Раздел = "Производство";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользоватьВыпускПродукции",
		НСтр("ru = 'Выпуск продукции'"),
		НСтр("ru = 'Учет готовой продукции и списание материалов'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ТребуетсяДляОбменов        = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВедетсяПроизводственнаяДеятельность",
		НСтр("ru = 'Комплексный учет производства'"),
		НСтр("ru = '- Учет готовой продукции, полуфабрикатов и возвратных отходов
			|- Учет переработки давальческого сырья
			|- Оказание производственных услуг
			|- Учет незавершенного производства
			|- Настройка правил расчета себестоимости продукции'"));
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	ДобавитьРодителя(Функциональность, "ИспользоватьВыпускПродукции");
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ИспользуетсяКалькуляцияРасходов",
		НСтр("ru = 'Калькуляция расходов в счете покупателю'"),
		НСтр("ru = 'Расчет расходов на материалы для производства продукции по спецификации'"));
	ДобавитьРодителя(Функциональность, "ИспользоватьВыпускПродукции");
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ПроизводствоПодакцизныхТоваров",
		НСтр("ru = 'Производство подакцизных товаров'"),
		НСтр("ru = 'Продажа произведенных подакцизных товаров'"));
	ДобавитьРодителя(Функциональность, "ИспользоватьВыпускПродукции");
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВыполняютсяСтроительныеРаботы",
		НСтр("ru = 'Выполнение строительных работ'"),
		НСтр("ru = 'Отражение в учете выполнения строительных работ, формирование ""Акта о приемке выполненных строительных работ""'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Ложь;
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаОрганизация(ОписаниеФункциональности)
	
	Раздел = "Организация";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВестиУчетПоОрганизациям",
		НСтр("ru = 'Учет по нескольким организациям'"),
		НСтр("ru = 'Возможность ведения учета по нескольким организациям в одной информационной базе'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.Доступна = ДоступнаФункциональностьУчетПоНесколькимОрганизациям();
	Функциональность.ИмяТарифнойОпции = ТарификацияБП.ИдентификаторУслугиВестиУчетПоОрганизациям();
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "ВестиУчетПоПодразделениям",
		НСтр("ru = 'Обособленные подразделения'"),
		НСтр("ru = 'Учет операций обособленных подразделений,
			|в том числе и выделенных на отдельный баланс'"));
	Функциональность.ТребуетсяДлительнаяОперация = Истина;
	Функциональность.Доступна = ВариантыПриложений.ЭтоВерсияКОРП();
	ДобавитьРодителя(Функциональность, "ВестиУчетРасчетовСКонтрагентами");
	Функциональность.ИмяТарифнойОпции = ТарификацияБП.ИдентификаторУслугиОбособленныеПодразделения();
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаДополнительно(ОписаниеФункциональности)
	
	Раздел = "Дополнительно";
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности, Раздел, "РасширенныйФункционал",
		НСтр("ru = 'Расширенный функционал'"),
		НСтр("ru = '- Учет расходов по элементам затрат
			|- Расширенная аналитика счетов затрат
			|- Оценка готовой продукции в сумме плановых (нормативных) затрат
			|- Амортизация основных средств с дня принятия к учету
			|- Обесценение запасов
			|- Обесценение внеоборотных активов
			|- Закрытие счетов 28 ""Брак в производстве"", 29 ""Обслуживающие производства и хозяйства""
			|- Дисконтирование обязательств по аренде и лизингу
			|- Дисконтирование обязательств по рассрочке оплаты поступления оборудования, товаров
			|- Настройка контроля операций закрытия месяца
			|- Счета-фактуры экспедиторов
			|- Консолидация отчетности по НДС
			|- Уведомления о контролируемых иностранных компаниях
			|- Отчетность некредитных финансовых организаций
			|- Отчетность крупнейших налогоплательщиков'"));
	Функциональность.Доступна = ВариантыПриложений.ЭтоВерсияКОРП();
	Функциональность.ИмяТарифнойОпции = ТарификацияБП.ИдентификаторУслугиРасширенныйФункционал();
	// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
	// От этой функциональности зависят константы РасширенныйФункционалОсновныеСредства и РасширенныйФункционалНематериальныеАктивы -
	// см. НастроитьАгрегирующуюКонстанту
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеРазделаЗарплатаКадры(ОписаниеФункциональности, ИмяРаздела = Неопределено)
	
	Раздел = "ЗарплатаКадры";
	
	Если ЗначениеЗаполнено(ИмяРаздела) И ИмяРаздела <> Раздел Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатЗапросаЮридическиеЛица().Пустой() Тогда
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ВедетсяУчетЗарплатыКадров",
			НСтр("ru = 'Сотрудники'"),
			НСтр("ru = 'Выплата зарплаты сотрудникам и кадровый учет'"));
		Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ИспользоватьКадровыйУчет",
			НСтр("ru = 'Кадровые документы'"),
			НСтр("ru = 'Оформление приема, перевода и увольнения отдельными документами (приказами)'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		Функциональность.ТребуетсяДлительнаяОперация = ТребуетсяДлительнаяОперацияИспользоватьКадровыйУчет();
		ДобавитьРодителя(Функциональность, "ВедетсяУчетЗарплатыКадров");
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ИспользоватьВоинскийУчет",
			НСтр("ru = 'Воинский учет'"),
			НСтр("ru = 'Учет сотрудников, подлежащих воинскому учету и находящихся в запасе, подготовка данных для военных комиссариатов'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		ДобавитьРодителя(Функциональность, "ИспользоватьКадровыйУчет");
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ИспользоватьРаботуНаНеполнуюСтавку",
			НСтр("ru = 'Неполная ставка'"),
			НСтр("ru = 'Оформление сотрудников на неполный рабочий день'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		ДобавитьРодителя(Функциональность, "ВедетсяУчетЗарплатыКадров");
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ПоддержкаСовместительства",
			НСтр("ru = 'Совместительство'"),
			НСтр("ru = 'Работа сотрудников на условиях внутреннего или внешнего совместительства'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		ДобавитьРодителя(Функциональность, "ВедетсяУчетЗарплатыКадров");
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ИспользоватьНачисленияПоДоговорам",
			НСтр("ru = 'Договоры ГПХ'"),
			НСтр("ru = 'Расчеты за работы и услуги, оказанные в рамках договоров гражданско-правового характера'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		ДобавитьРодителя(Функциональность, "ВедетсяУчетЗарплатыКадров");
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
	Иначе
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ВедетсяУчетЗарплатыКадров",
			НСтр("ru = 'Сотрудники'"),
			НСтр("ru = 'Выплата зарплаты сотрудникам и кадровый учет'"));
		Функциональность.Доступна = Ложь;
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ИспользоватьКадровыйУчет",
			НСтр("ru = 'Кадровые документы'"),
			НСтр("ru = 'Оформление приема, перевода и увольнения отдельными документами (приказами)'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		Функциональность.ТребуетсяДлительнаяОперация = ТребуетсяДлительнаяОперацияИспользоватьКадровыйУчет();
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ИспользоватьВоинскийУчет",
			НСтр("ru = 'Воинский учет'"),
			НСтр("ru = 'Учет сотрудников, подлежащих воинскому учету и находящихся в запасе, подготовка данных для военных комиссариатов'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		ДобавитьРодителя(Функциональность, "ИспользоватьКадровыйУчет");
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ИспользоватьРаботуНаНеполнуюСтавку",
			НСтр("ru = 'Неполная ставка'"),
			НСтр("ru = 'Оформление сотрудников на неполный рабочий день'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		ДобавитьРодителя(Функциональность, "ВедетсяУчетЗарплатыКадров");
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ПоддержкаСовместительства",
			НСтр("ru = 'Совместительство'"),
			НСтр("ru = 'Работа сотрудников на условиях внутреннего или внешнего совместительства'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
		Функциональность = ДобавитьОписаниеФункциональности(
			ОписаниеФункциональности, Раздел, "ИспользоватьНачисленияПоДоговорам",
			НСтр("ru = 'Договоры ГПХ'"),
			НСтр("ru = 'Расчеты за работы и услуги, оказанные в рамках договоров гражданско-правового характера'"));
		Функциональность.Доступна = Не ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме");
		Функциональность.ДоступнаВПростомИнтерфейсе  = Истина;
		// добавить проверку ТарификацияБП.КонстантаФункциональностиПередЗаписью в событие ПередЗаписью константы
		Функциональность.ПроверяютсяУсловияТарификации = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДобавитьОписаниеФункциональности(ОписаниеФункциональности, Раздел, ИмяФункциональности, Заголовок = "", Подсказка = "")
	
	Функциональность = ОписаниеФункциональности.Добавить();
	Функциональность.Имя = ИмяФункциональности;
	Функциональность.Раздел = Раздел;
	Функциональность.Заголовок = Заголовок;
	Функциональность.Подсказка = Подсказка;
	Функциональность.ТребуетсяДлительнаяОперация                    = Ложь;
	Функциональность.ДоступнаВПростомИнтерфейсе                     = Ложь;
	Функциональность.ДолжнаБытьОбязательноВключенаВПолномИнтерфейсе = Ложь;
	Функциональность.Доступна = Истина;
	Функциональность.Видимость = Истина;
	
	Возврат Функциональность;
	
КонецФункции

Процедура ОчиститьНедоступнуюРодительскуюФункциональность(ОписаниеФункциональности)
	
	НедоступнаяФункциональность = Новый Соответствие;
	Для Каждого Функциональность Из ОписаниеФункциональности Цикл
		Если Не Функциональность.Доступна Тогда
			НедоступнаяФункциональность.Вставить(Функциональность.Имя, Истина);
		КонецЕсли;
	КонецЦикла;
	
	ОчиститьРодительскуюФункциональность(ОписаниеФункциональности, Неопределено, НедоступнаяФункциональность);
	
КонецПроцедуры

Процедура ЗаполнитьДочернююФункциональность(ОписаниеФункциональности)
	
	Для Каждого Функциональность Из ОписаниеФункциональности Цикл
		Если Не Функциональность.Доступна Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого РодительскаяФукциональность Из Функциональность.РодительскаяФункциональность Цикл
			СтрокиРодительскойФункциональности = ОписаниеФункциональности.НайтиСтроки(Новый Структура("Имя", РодительскаяФукциональность));
			Для Каждого СтрокаРодительскойФункциональности Из СтрокиРодительскойФункциональности Цикл
				СтрокаРодительскойФункциональности.ДочерняяФункциональность.Добавить(Функциональность.Имя);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьРодителя(Функциональность, ИмяРодителя)
	
	Если ЗначениеЗаполнено(ИмяРодителя) Тогда
		Функциональность.РодительскаяФункциональность.Добавить(ИмяРодителя);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьРодительскуюФункциональность(ОписаниеФункциональности, Отбор, ОчищаемаяФункциональность)
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		СтрокиФункциональности = ОписаниеФункциональности.НайтиСтроки(Отбор);
	Иначе
		СтрокиФункциональности = ОписаниеФункциональности;
	КонецЕсли;
	
	Для Каждого Функциональность Из СтрокиФункциональности Цикл
		
		НомерРодительскойФункциональности = Функциональность.РодительскаяФункциональность.Количество() - 1;
		Пока НомерРодительскойФункциональности >= 0 Цикл
			ИмяРодительскойФункциональности = Функциональность.РодительскаяФункциональность[НомерРодительскойФункциональности];
			Если ОчищаемаяФункциональность.Получить(ИмяРодительскойФункциональности) <> Неопределено Тогда
				Функциональность.РодительскаяФункциональность.Удалить(НомерРодительскойФункциональности);
			КонецЕсли;
			НомерРодительскойФункциональности = НомерРодительскойФункциональности - 1;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкиНаОтключениеФлага

Процедура ИзменитьДляПроверкиТекстЗапроса(ТекстЗапроса, ТолькоПроверка = Истина)
	
	Если Не ТолькоПроверка Тогда
		Возврат;
	КонецЕсли;
	
	// Если запрос выполняется для проверки, то выбирается 1 запись
	КоличествоПолучаемыхЗаписей = 1;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	Для Каждого ПакетЗапроса Из СхемаЗапроса.ПакетЗапросов Цикл
		Для Каждого Оператор Из ПакетЗапроса.Операторы Цикл
			Оператор.КоличествоПолучаемыхЗаписей = КоличествоПолучаемыхЗаписей;
		КонецЦикла;
	КонецЦикла;	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецПроцедуры

Функция ТекстЗапросаИспользуетсяСправочник(ИмяСправочника)
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИскомыйСправочник.Ссылка
	|ИЗ
	|	Справочник.%1 КАК ИскомыйСправочник";
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ИмяСправочника);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаИспользуетсяДокумент(ИмяДокумента, ОтборПоВидуОперации = Ложь, ТолькоПроверка = Истина)
	
	Если ТолькоПроверка Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИскомыйДокумент.Ссылка
		|ИЗ
		|	Документ.%1 КАК ИскомыйДокумент
		|ГДЕ
		|	ИскомыйДокумент.ПометкаУдаления = Ложь";
		Если ОтборПоВидуОперации Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|	
			|	И ИскомыйДокумент.ВидОперации В (&ВидыОпераций)";
		КонецЕсли;
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИскомыйДокумент.Ссылка КАК Ссылка,
		|	ИскомыйДокумент.Дата КАК Дата,
		|	ИскомыйДокумент.Ссылка КАК Представление
		|ИЗ
		|	Документ.%1 КАК ИскомыйДокумент
		|ГДЕ
		|	ИскомыйДокумент.ПометкаУдаления = ЛОЖЬ";
		Если ОтборПоВидуОперации Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|	
			|	И ИскомыйДокумент.ВидОперации В (&ВидыОпераций)";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + "
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	КонецЕсли;
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ИмяДокумента);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция РезультатЗапросаИсполняютсяОбязанностиНалоговогоАгентаПоНДС(ТолькоПроверка = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.УчетАгентскогоНДС
	|	И ДоговорыКонтрагентов.ВидАгентскогоДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров)";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивДоговоров = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивДоговоров;
	
КонецФункции

Функция ТекстЗапросаВедетсяДеятельностьПоДоговорамКомиссии(УчетПоДоговорам = Ложь)
	
	Если УчетПоДоговорам Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
		|	ДоговорыКонтрагентов.Владелец КАК СсылкаКонтрагент
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеДоговорыКонтрагента КАК ОсновныеДоговорыКонтрагента
		|		ПО ОсновныеДоговорыКонтрагента.Договор = ДоговорыКонтрагентов.Ссылка
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора В(&ВидыДоговоров)
		|	И ОсновныеДоговорыКонтрагента.Договор ЕСТЬ NULL";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
		|	ДоговорыКонтрагентов.Владелец КАК СсылкаКонтрагент
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора В(&ВидыДоговоров)";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаУправлениеЗачетомАвансовРеализацияОтгруженныхТоваров(ТолькоПроверка)
	
	// В реализации отгруженных товаров для расчетов в условных единицах 
	// по документам отгрузки "Реализация товаров и услуг" зачет авансов не выполняется.

	Если ТолькоПроверка Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РеализацияОтгруженныхТоваров.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
		|ГДЕ
		|	РеализацияОтгруженныхТоваров.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически)
		|	И РеализацияОтгруженныхТоваров.ПометкаУдаления = ЛОЖЬ
		|	И НЕ(ТИПЗНАЧЕНИЯ(РеализацияОтгруженныхТоваров.ДокументОтгрузки) = ТИП(Документ.РеализацияТоваровУслуг)
		|				И РеализацияОтгруженныхТоваров.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах)";

	Иначе
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	РеализацияОтгруженныхТоваров.Ссылка КАК Ссылка,
		|	РеализацияОтгруженныхТоваров.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(РеализацияОтгруженныхТоваров.Ссылка) КАК Представление
		|ИЗ
		|	Документ.РеализацияОтгруженныхТоваров КАК РеализацияОтгруженныхТоваров
		|ГДЕ
		|	РеализацияОтгруженныхТоваров.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически)
		|	И РеализацияОтгруженныхТоваров.ПометкаУдаления = ЛОЖЬ
		|	И НЕ(ТИПЗНАЧЕНИЯ(РеализацияОтгруженныхТоваров.ДокументОтгрузки) = ТИП(Документ.РеализацияТоваровУслуг)
		|				И РеализацияОтгруженныхТоваров.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ИспользуетсяСинхронизацияДанных() Экспорт
	
	Возврат Константы.ИспользоватьСинхронизациюДанных.Получить() И НЕ ОбщегоНазначения.РазделениеВключено();
	
КонецФункции 

Функция ОтказСнятьИспользоватьДенежныеДокументы()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииДенежныеДокументы(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьОплатуПоПлатежнымКартам()
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ОплатаПлатежнойКартой", Истина, Истина);

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаЕстьОплатаКартойВДокументе("ОтчетОРозничныхПродажах", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаЕстьОплатаКартойВДокументе("РозничнаяПродажа", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИнтеграцияНСПК();
	
	Запрос = Новый Запрос;
	
	СписокОпераций = Новый Массив;
	СписокОпераций.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ВозвратПокупателю);
	СписокОпераций.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.РозничнаяВыручка);
	
	Запрос.УстановитьПараметр("ВидыОпераций", СписокОпераций);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтказСнять = Ложь;
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ОтказСнятьИспользоватьПлатежныеТребования()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПлатежныеТребования(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьВедетсяУчетВозвратнойТары()
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("АвансовыйОтчет", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ВозвратТоваровОтПокупателя", Истина);

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ВозвратТоваровПоставщику", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПередачаТоваров", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПеремещениеТоваров", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПоступлениеИзПереработки", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПоступлениеТоваровУслуг", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("РеализацияТоваровУслуг", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("СписаниеТоваров", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("СчетНаОплатуПокупателю", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("СчетНаОплатуПоставщика", Истина);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПередачаМатериаловВЭксплуатацию", Истина);
		
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтказСнять = Ложь;
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;

КонецФункции

Функция ОтказСнятьВедетсяУчетИмпортныхТоваров()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИспользуетсяСправочник("НомераГТД");
	
	УстановитьПривилегированныйРежим(Истина);

	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ОтказСнятьВедетсяУчетИмпортаТоваров()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииИмпортТоваров(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьКомплектациюНоменклатуры()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИспользуетсяДокумент("КомплектацияНоменклатуры");
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ОтказСнятьВедетсяУчетОсновныхСредств()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИспользуетсяСправочник("ОсновныеСредства");
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ОтказСнятьВедетсяУчетНМА()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииНематериальныеАктивы(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьВедетсяОтгрузкаБезПереходаПраваСобственности()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииОтгрузкаБезПереходаПраваСобственности(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьПакетноеВыставлениеАктов()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПакетноеВыставлениеАктов(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьНачислениеНДССМежценовойРазницы()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииНачислениеНДССМежценовойРазницы(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьВедетсяПроизводственнаяДеятельность()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииКомплексныйУчетПроизводства(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьВыпускПродукции()
	
	ТолькоПроверка = Истина;
	ТаблицаЗапросов = ТаблицаЗапросовИспользуетсяВыпускПродукции(ТолькоПроверка);
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтказСнять = Ложь;
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Запрос.УстановитьПараметр("ВидыОпераций", СтрокаТЗ.ВидыОпераций);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ОтказСнятьВедетсяУчетСпецодеждыСпецоснастки()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииМалоценноеОборудование(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьОсуществляетсяЗакупкаТоваровУслугДляКомитентов()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПриобретениеТоваровУслугДляКомитентов(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИсполняютсяОбязанностиНалоговогоАгентаПоНДС()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииНалоговыйАгентПоНДС(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПриобретениеТоваровУслугЧерезКомиссионеров(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьОсуществляетсяРеализацияТоваровУслугКомитентов()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПродажаТоваровУслугКомитентов(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПродажаТоваровУслугЧерезКомиссионеров(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	ОтказСнять = УсловияДоступностиОбъектов.Количество() > 0;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ОтказСнятьВедетсяРозничнаяТорговля()
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ВидыОпераций");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ОтчетОРозничныхПродажах");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("РозничнаяПродажа");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользоватьДокументыРозничнойТорговли("ПриходныйКассовыйОрдер", Истина);
	НоваяСтрока.ВидыОпераций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		Перечисления.ВидыОперацийПКО.РозничнаяВыручка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользоватьДокументыРозничнойТорговли("ПоступлениеНаРасчетныйСчет", Истина);
	НоваяСтрока.ВидыОпераций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПоступленияОтПродажПоПлатежнымКартамИБанковскимКредитам);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаЕстьОплатаКартойРозницаСЗакрывающимиДокументами("ОплатаПлатежнойКартой", Истина);
	НоваяСтрока.ВидыОпераций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		Перечисления.ВидыОперацийОплатаПлатежнойКартой.РозничнаяВыручка);
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Запрос.УстановитьПараметр("ВидыОпераций", СтрокаТЗ.ВидыОпераций);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтказСнять = Ложь;
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ТекстЗапросаИспользоватьДокументыРозничнойТорговли(ИмяДокумента, ТолькоПроверка = Истина)
	
	Если ТолькоПроверка Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Док.Ссылка КАК Ссылка
		|ИЗ
		|	Документ." + ИмяДокумента + " КАК Док
		|ГДЕ
		|	НЕ Док.ПометкаУдаления
		|	И Док.ВидОперации В (&ВидыОпераций)
		|	И НЕ Док.БезЗакрывающихДокументов";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Док.Ссылка КАК Ссылка,
		|	Док.Дата КАК Дата,
		|	Док.Ссылка КАК Представление
		|ИЗ
		|	Документ." + ИмяДокумента + " КАК Док
		|ГДЕ
		|	НЕ Док.ПометкаУдаления
		|	И Док.ВидОперации В (&ВидыОпераций)
		|	И НЕ Док.БезЗакрывающихДокументов
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ОтказСнятьИспользоватьУведомленияОКонтролируемыхСделках()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииКонтролируемыеСделки(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииАлкогольнаяПродукция(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользуютсяПодарочныеСертификаты()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПодарочныеСертификаты(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьКорректировочныеДокументы()

	Запрос = Новый Запрос;
	
	ДокументыДляПроверки = Новый Массив;
	ДокументыДляПроверки.Добавить("КорректировкаПоступления");
	ДокументыДляПроверки.Добавить("КорректировкаРеализации");
	ДокументыДляПроверки.Добавить("АктОРасхождениях");
	ДокументыДляПроверки.Добавить("АктОРасхожденияхПолученный");
	
	ОтказСнять = Ложь;
	
	Для Каждого ДокументДляПроверки Из ДокументыДляПроверки Цикл
	
		Запрос.Текст = ТекстЗапросаИспользуетсяДокумент(ДокументДляПроверки);
		
		ОтказСнять = НЕ Запрос.Выполнить().Пустой();
		
		Если ОтказСнять Тогда
			Прервать;
		КонецЕсли; 
		
	КонецЦикла; 
	
	Возврат ОтказСнять;

КонецФункции 

Функция ОтказСнятьИспользоватьФискальныйРегистратор()
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПодключаемоеОборудование.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|	(ПодключаемоеОборудование.ТипОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыПодключаемогоОборудования.ФискальныйРегистратор)
	|			ИЛИ ПодключаемоеОборудование.ТипОборудования = ЗНАЧЕНИЕ(Перечисление.ТипыПодключаемогоОборудования.ККТ))";
	
	ОтказСнять = НЕ Запрос.Выполнить().Пустой();
	
	Возврат ОтказСнять;
КонецФункции

Функция ОтказСнятьИспользоватьВыплатыСамозанятым()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииВыплатыСамозанятым(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьИнкассацию()
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ВидыОпераций");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РасходныйКассовыйОрдер";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("РасходныйКассовыйОрдер", Истина, Ложь);
	НоваяСтрока.ВидыОпераций = Новый Массив;
	НоваяСтрока.ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.Инкассация);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеНаРасчетныйСчет";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПоступлениеНаРасчетныйСчет", Истина, Ложь);
	НоваяСтрока.ВидыОпераций = Новый Массив;
	НоваяСтрока.ВидыОпераций.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.Инкассация);
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Запрос.УстановитьПараметр("ВидыОпераций", СтрокаТЗ.ВидыОпераций);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтказСнять = Ложь;
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ОтказСнятьИспользоватьДоставкуАвтотранспортом()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеализацияТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	(РеализацияТоваровУслуг.Перевозчик <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИЛИ РеализацияТоваровУслуг.МаркаАвтомобиля <> """"
	|			ИЛИ РеализацияТоваровУслуг.РегистрационныйЗнакАвтомобиля <> """"
	|			ИЛИ РеализацияТоваровУслуг.Водитель <> """"
	|			ИЛИ РеализацияТоваровУслуг.КраткоеНаименованиеГруза <> """"
	|			ИЛИ РеализацияТоваровУслуг.СопроводительныеДокументы <> """")";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;

КонецФункции 

Функция ОтказСнятьИспользоватьПодконтрольныеТоварыККТ()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.СредствоИндивидуальнойЗащиты";
	
	УстановитьПривилегированныйРежим(Истина);

	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;

КонецФункции

Функция ОтказСнятьВедетсяУчетТаможенныхДекларацийЭкспорт()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииЭкспортТоваров(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьВестиУчетМаркировкиПродукцииВГИСМ()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииМаркировкаМеховыхИзделий(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

#Область ВестиУчетПоПодразделениям

Функция ОтказСнятьВестиУчетПоПодразделениям()
	
	РезультатыЗапросовИспользуютсяОбособленныеПодразделения = РезультатыЗапросовИспользуютсяОбособленныеПодразделения();
	ОтказСнять = Не (РезультатыЗапросовИспользуютсяОбособленныеПодразделения[0].Пустой() // филиалы (обособленные организации)
		И РезультатыЗапросовИспользуютсяОбособленныеПодразделения[1].Пустой()); // обособленные подразделения
	
	Если ОтказСнять Тогда
		Возврат ОтказСнять;
	КонецЕсли;
	
	РезультатыЗапросовИспользуютсяПодразделенияВПроводках = РезультатыЗапросовИспользуютсяПодразделенияВПроводках();
	ОтказСнять = Не РезультатыЗапросовИспользуютсяПодразделенияВПроводках.Пустой();
	Возврат ОтказСнять;
	
КонецФункции

Функция РезультатыЗапросовИспользуютсяОбособленныеПодразделения(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.Представление КАК Представление
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ОбособленноеПодразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Ссылка КАК Ссылка,
	|	ПодразделенияОрганизаций.Представление КАК Представление
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.ОбособленноеПодразделение";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция РезультатыЗапросовИспользуютсяПодразделенияВПроводках(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Хозрасчетный.Регистратор КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(Хозрасчетный.Регистратор) КАК Представление
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	(Хозрасчетный.СчетДт В (&СчетаДляПроверки)
	|				И НЕ Хозрасчетный.ПодразделениеДт В (ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|			ИЛИ Хозрасчетный.СчетКт В (&СчетаДляПроверки)
	|				И НЕ Хозрасчетный.ПодразделениеКт В (ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка), НЕОПРЕДЕЛЕНО))";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	// Проверяем счета, где признак УчетПоПодразделеиям в КОРП отличается от ПРОФ.
	СчетаСОтключаемымУчетомПоПодразделениям = ПланыСчетов.Хозрасчетный.СчетаСРазличиямиПризнакаУчетПоПодразделениям();
	
	// Исключаем счета, где подразделение заполняется программно при включении обособленных подразделений.
	СчетаДляПроверки = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		СчетаСОтключаемымУчетомПоПодразделениям,
		ПланыСчетов.Хозрасчетный.СчетаОСДляЗаполненияПодразделенияПоДаннымМестонахождения());
		
	Запрос.УстановитьПараметр("СчетаДляПроверки", СчетаДляПроверки);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура ДействияПриИзмененииВестиУчетПоПодразделениям(НовоеЗначение)
	
	НачатьТранзакцию();
	
	Константы.ВестиУчетПоПодразделениям.Установить(НовоеЗначение);
	
	УстановитьПризнакОбособленныхПодразделенийВОрганизациях(НовоеЗначение);
	ПланыСчетов.Хозрасчетный.НастроитьПланСчетовДляОбособленныхПодразделений(НовоеЗначение);
	
	ЗафиксироватьТранзакцию();
	
	// Обработка движений плана счетов выполняется вне транзакции,
	// т.к. она может содержать большой объем изменений - транзакция может переполниться.
	// При этом эти изменения не влияют на целостность и могут быть выполнены позже.
	Если НовоеЗначение Тогда
		УчетОбособленныхПодразделений.ПриВключенииВестиУчетПоПодразделениям();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПризнакОбособленныхПодразделенийВОрганизациях(НовоеЗначение)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ЕстьОбособленныеПодразделения", НовоеЗначение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ЕстьОбособленныеПодразделения <> &ЕстьОбособленныеПодразделения";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОрганизацияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ОрганизацияОбъект.ЕстьОбособленныеПодразделения = НовоеЗначение;
		ОрганизацияОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОперацииФакторинга

Процедура НастроитьФункциональностьИспользоватьФакторинг(ВыполняетсяОбработчикОбновления = Ложь) Экспорт
	
	КлассификаторыДоходовРасходов.ОбеспечитьФункциональность(Справочники.ПрочиеДоходыИРасходы, "ИспользоватьФакторинг");
	
	Справочники.СтатьиДвиженияДенежныхСредств.СоздатьУстановитьПредопределенныеЭлементыФакторинг(ВыполняетсяОбработчикОбновления);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеЗачетомАвансовПогашениемЗадолженности

Функция ОтказСнятьУправлениеЗачетомАвансовПогашениемЗадолженности()
	
	ТаблицаЗапросов = ТаблицаЗапросовУправлениеЗачетомАвансовПогашениемЗадолженности(Истина);
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Запрос.УстановитьПараметр("ВидыОперацийОплатаИВозврат", СтрокаТЗ.ВидыОперацийОплатаИВозврат);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтказСнять = Ложь;
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ТаблицаЗапросовУправлениеЗачетомАвансовПогашениемЗадолженности(ТолькоПроверка)

	// Начинаем поиск с документов, в которых наиболее вероятно 
	// используется зачет авансов (погашение задолженности) по документам.
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ВидыОперацийОплатаИВозврат");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеНаРасчетныйСчет";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеПогашениемЗадолженностиКассаБанк("ПоступлениеНаРасчетныйСчет", ТолькоПроверка);
	НоваяСтрока.ВидыОперацийОплатаИВозврат = Новый Массив;
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя);
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратОтПоставщика);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "СписаниеСРасчетногоСчета";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеПогашениемЗадолженностиКассаБанк("СписаниеСРасчетногоСчета", ТолькоПроверка);
	НоваяСтрока.ВидыОперацийОплатаИВозврат = Новый Массив;
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику);
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПриходныйКассовыйОрдер";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеПогашениемЗадолженностиКассаБанк("ПриходныйКассовыйОрдер", ТолькоПроверка);
	НоваяСтрока.ВидыОперацийОплатаИВозврат = Новый Массив;
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийПКО.ОплатаПокупателя);
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РасходныйКассовыйОрдер";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеПогашениемЗадолженностиКассаБанк("РасходныйКассовыйОрдер", ТолькоПроверка);
	НоваяСтрока.ВидыОперацийОплатаИВозврат = Новый Массив;
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийРКО.ОплатаПоставщику);
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийРКО.ВозвратПокупателю);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ОплатаПлатежнойКартой";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеПогашениемЗадолженностиКассаБанк("ОплатаПлатежнойКартой", ТолькоПроверка);
	НоваяСтрока.ВидыОперацийОплатаИВозврат = Новый Массив;
	НоваяСтрока.ВидыОперацийОплатаИВозврат.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РеализацияТоваровУслуг";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовРеализацияТоваровУслуг(ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РеализацияОтгруженныхТоваров";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовРеализацияОтгруженныхТоваров(ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеТоваровУслуг";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеТоваровУслуг(ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеДопРасходов";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("ПоступлениеДопРасходов", ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "АвансовыйОтчет";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеПогашениемЗадолженностиАвансовыйОтчет(ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "АктОбОказанииПроизводственныхУслуг";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("АктОбОказанииПроизводственныхУслуг", ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ОтчетКомиссионераОПродажах";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("ОтчетКомиссионераОПродажах", ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПередачаОС";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("ПередачаОС", ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПередачаНМА";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("ПередачаНМА", ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеНМА";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("ПоступлениеНМА", ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеИзПереработки";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("ПоступлениеИзПереработки", ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РеализацияУслугПоПереработке";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("РеализацияУслугПоПереработке", ТолькоПроверка);

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "КорректировкаДолга";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеКорректировкаДолга(ТолькоПроверка);

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ГТДИмпорт";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация("ГТДИмпорт", ТолькоПроверка);
	
	Возврат ТаблицаЗапросов;

КонецФункции

Функция ДокументыПрепятствующиеОтключениюУправлениеЗачетомАвансовПогашениемЗадолженности()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();

	ТаблицаЗапросов = ТаблицаЗапросовУправлениеЗачетомАвансовПогашениемЗадолженности(Ложь);

	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Запрос.УстановитьПараметр("ВидыОперацийОплатаИВозврат", СтрокаТЗ.ВидыОперацийОплатаИВозврат);
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.ИмяДокумента = СтрокаТЗ.ИмяДокумента;
		МетаданныеДокумента = Метаданные.Документы.Найти(СтрокаТЗ.ИмяДокумента);
		СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции

Функция ТекстЗапросаУправлениеЗачетомАвансовРеализацияТоваровУслуг(ТолькоПроверка)
	
	// В реализации товаров и услуг для вида операции "Отгрузка без перехода права собственности"
	// и при передачи товаров комиссионеру на реализацию зачет авансов не выполняется.

	Если ТолькоПроверка Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументРТУ.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК ДокументРТУ
		|ГДЕ
		|	ДокументРТУ.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности)
		|	И ДокументРТУ.ДоговорКонтрагента.ВидДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером)
		|	И ДокументРТУ.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически)
		|	И ДокументРТУ.ПометкаУдаления = ЛОЖЬ";

	Иначе
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументРТУ.Ссылка КАК Ссылка,
		|	ДокументРТУ.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ДокументРТУ.Ссылка) КАК Представление
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК ДокументРТУ
		|ГДЕ
		|	ДокументРТУ.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности)
		|	И ДокументРТУ.ДоговорКонтрагента.ВидДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером)
		|	И ДокументРТУ.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически)
		|	И ДокументРТУ.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаУправлениеЗачетомАвансовПоступлениеТоваровУслуг(ТолькоПроверка)
	
	// В поступлении товаров и услуг для вида операции "В переработку"
	// и при получения товаров от комитента на реализацию зачет авансов не выполняется.
	
	Если ТолькоПроверка Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументПТУ.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ДокументПТУ
		|ГДЕ
		|	ДокументПТУ.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку)
		|	И ДокументПТУ.ДоговорКонтрагента.ВидДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом)
		|	И ДокументПТУ.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически)
		|	И ДокументПТУ.ПометкаУдаления = ЛОЖЬ";
	
	Иначе

		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументПТУ.Ссылка КАК Ссылка,
		|	ДокументПТУ.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ДокументПТУ.Ссылка) КАК Представление
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ДокументПТУ
		|ГДЕ
		|	ДокументПТУ.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку)
		|	И ДокументПТУ.ДоговорКонтрагента.ВидДоговора <> ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентом)
		|	И ДокументПТУ.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически)
		|	И ДокументПТУ.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	
	КонецЕсли;
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаУправлениеЗачетомАвансовПоступлениеРеализация(ИмяДокумента, ТолькоПроверка)
	
	Если ТолькоПроверка Тогда

		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументПоступленияРеализации.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.АктОбОказанииПроизводственныхУслуг КАК ДокументПоступленияРеализации
		|ГДЕ
		|	ДокументПоступленияРеализации.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически)
		|	И ДокументПоступленияРеализации.ПометкаУдаления = ЛОЖЬ";
		
	Иначе

		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументПоступленияРеализации.Ссылка КАК Ссылка,
		|	ДокументПоступленияРеализации.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ДокументПоступленияРеализации.Ссылка) КАК Представление
		|ИЗ
		|	Документ.АктОбОказанииПроизводственныхУслуг КАК ДокументПоступленияРеализации
		|ГДЕ
		|	ДокументПоступленияРеализации.СпособЗачетаАвансов <> ЗНАЧЕНИЕ(Перечисление.СпособыЗачетаАвансов.Автоматически)
		|	И ДокументПоступленияРеализации.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "АктОбОказанииПроизводственныхУслуг", ИмяДокумента);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаУправлениеПогашениемЗадолженностиКассаБанк(ИмяДокумента, ТолькоПроверка)
	
	Если ТолькоПроверка Тогда

		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументКассаБанк.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет.РасшифровкаПлатежа КАК ДокументКассаБанк
		|ГДЕ
		|	ДокументКассаБанк.СпособПогашенияЗадолженности <> ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияЗадолженности.Автоматически)
		|	И ДокументКассаБанк.Ссылка.ВидОперации В (&ВидыОперацийОплатаИВозврат)
		|	И ДокументКассаБанк.Ссылка.ПометкаУдаления = ЛОЖЬ";
		
	Иначе

		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументКассаБанк.Ссылка КАК Ссылка,
		|	ДокументКассаБанк.Ссылка.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ДокументКассаБанк.Ссылка) КАК Представление
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет.РасшифровкаПлатежа КАК ДокументКассаБанк
		|ГДЕ
		|	ДокументКассаБанк.СпособПогашенияЗадолженности <> ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияЗадолженности.Автоматически)
		|	И ДокументКассаБанк.Ссылка.ВидОперации В (&ВидыОперацийОплатаИВозврат)
		|	И ДокументКассаБанк.Ссылка.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПоступлениеНаРасчетныйСчет", ИмяДокумента);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаУправлениеПогашениемЗадолженностиАвансовыйОтчет(ТолькоПроверка)
	
	Если ТолькоПроверка Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	АвансовыйОтчет.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК АвансовыйОтчет
		|ГДЕ
		|	АвансовыйОтчет.СпособПогашенияЗадолженности <> ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияЗадолженности.Автоматически)
		|	И АвансовыйОтчет.Ссылка.ПометкаУдаления = ЛОЖЬ";
		
	Иначе

		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АвансовыйОтчет.Ссылка КАК Ссылка,
		|	АвансовыйОтчет.Ссылка.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(АвансовыйОтчет.Ссылка) КАК Представление
		|ИЗ
		|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК АвансовыйОтчет
		|ГДЕ
		|	АвансовыйОтчет.СпособПогашенияЗадолженности <> ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияЗадолженности.Автоматически)
		|	И АвансовыйОтчет.Ссылка.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаУправлениеКорректировкаДолга(ТолькоПроверка)
	
	Если ТолькоПроверка Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КорректировкаДолга.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.КорректировкаДолга КАК КорректировкаДолга
		|ГДЕ
		|	КорректировкаДолга.ПометкаУдаления = ЛОЖЬ";
		
	Иначе

		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	КорректировкаДолга.Ссылка КАК Ссылка,
		|	КорректировкаДолга.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(КорректировкаДолга.Ссылка) КАК Представление
		|ИЗ
		|	Документ.КорректировкаДолга КАК КорректировкаДолга
		|ГДЕ
		|	КорректировкаДолга.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ИспользованиеВозвратнойТары

Функция ТекстЗапросаВедетсяУчетВозвратнойТары(ИмяДокумента, ТолькоПроверка)
	
	Если ТолькоПроверка Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ Первые 1
		|	ДокументСВозвратнойТарой.Ссылка
		|ИЗ
		|	Документ.%1.ВозвратнаяТара КАК ДокументСВозвратнойТарой
		|ГДЕ 
		|	ДокументСВозвратнойТарой.Ссылка.ПометкаУдаления = Ложь";
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументСВозвратнойТарой.Ссылка КАК Ссылка,
		|	ДокументСВозвратнойТарой.Ссылка.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ДокументСВозвратнойТарой.Ссылка) КАК Представление
		|ИЗ
		|	Документ.%1.ВозвратнаяТара КАК ДокументСВозвратнойТарой
		|ГДЕ
		|	ДокументСВозвратнойТарой.Ссылка.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
		
	КонецЕсли;
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса, ИмяДокумента);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТаблицаЗапросовВедетсяУчетВозвратнойТары()

	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "АвансовыйОтчет";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("АвансовыйОтчет", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ВозвратТоваровОтПокупателя";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ВозвратТоваровОтПокупателя", Ложь);

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ВозвратТоваровПоставщику";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ВозвратТоваровПоставщику", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПередачаТоваров";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПередачаТоваров", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПеремещениеТоваров";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПеремещениеТоваров", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеИзПереработки";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПоступлениеИзПереработки", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеТоваровУслуг";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПоступлениеТоваровУслуг", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РеализацияТоваровУслуг";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("РеализацияТоваровУслуг", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "СписаниеТоваров";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("СписаниеТоваров", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "СчетНаОплатуПокупателю";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("СчетНаОплатуПокупателю", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "СчетНаОплатуПоставщика";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("СчетНаОплатуПоставщика", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПередачаМатериаловВЭксплуатацию";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетВозвратнойТары("ПередачаМатериаловВЭксплуатацию", Ложь);
		
	Возврат ТаблицаЗапросов;
	
КонецФункции 

Функция ДокументыПрепятствующиеОтключениюВозвратойТары()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();

	ТаблицаЗапросов = ТаблицаЗапросовВедетсяУчетВозвратнойТары();

	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.ИмяДокумента = СтрокаТЗ.ИмяДокумента;
		МетаданныеДокумента = Метаданные.Документы.Найти(СтрокаТЗ.ИмяДокумента);
		СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции

#КонецОбласти

#Область ВедетсяРозничнаяТорговля

Функция ТекстЗапросаЕстьОплатаКартойРозницаСЗакрывающимиДокументами(ИмяДокумента, ТолькоПроверка = Истина)
	
	ТекстЗапроса =
	"ВЫБРАТЬ %1
	|	ОплатаПлатежнойКартойРозница.Ссылка КАК Ссылка,
	|	ОплатаПлатежнойКартойРозница.Дата КАК Дата,
	|	ОплатаПлатежнойКартойРозница.Представление КАК Представление
	|ИЗ
	|	Документ.%2 КАК ОплатаПлатежнойКартойРозница
	|ГДЕ
	|	НЕ ОплатаПлатежнойКартойРозница.ПометкаУдаления
	|	И НЕ ОплатаПлатежнойКартойРозница.БезЗакрывающихДокументов
	|	И ОплатаПлатежнойКартойРозница.ВидОперации В(&ВидыОпераций)";
	
	УсловиеВыбора = ?(ТолькоПроверка, "ПЕРВЫЕ 1", "РАЗЛИЧНЫЕ");
	
	Возврат СтрШаблон(ТекстЗапроса, УсловиеВыбора, ИмяДокумента);
	
КонецФункции

#КонецОбласти

#Область ИспользуетсяВыпускПродукции

Функция ТаблицаЗапросовИспользуетсяВыпускПродукции(ТолькоПроверка)

	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ВидыОпераций");

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ОтчетПроизводстваЗаСмену";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ОтчетПроизводстваЗаСмену", , ТолькоПроверка);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РегламентнаяОперация";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("РегламентнаяОперация", Истина, ТолькоПроверка);
	НоваяСтрока.ВидыОпераций = Новый Массив;
	НоваяСтрока.ВидыОпераций.Добавить(Перечисления.ВидыРегламентныхОпераций.РасходМатериаловДляВыпускаПродукции);
	
	Возврат ТаблицаЗапросов;
	
КонецФункции

Функция ДокументыПрепятствующиеОтключениюИспользуетсяВыпускПродукции()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();

	ТолькоПроверка = Ложь;
	ТаблицаЗапросов = ТаблицаЗапросовИспользуетсяВыпускПродукции(ТолькоПроверка);

	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Запрос.УстановитьПараметр("ВидыОпераций", СтрокаТЗ.ВидыОпераций);
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.ИмяДокумента = СтрокаТЗ.ИмяДокумента;
		МетаданныеДокумента = Метаданные.Документы.Найти(СтрокаТЗ.ИмяДокумента);
		СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции 

#КонецОбласти

#Область ИспользуетсяОплатаПоПлатежнымКартам

Функция ТекстЗапросаЕстьОплатаКартойВДокументе(ИмяДокумента, ТолькоПроверка = Истина)
	
	Если ТолькоПроверка Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОплатаВОтчетеОРозничныхПродажах.Ссылка
		|ИЗ
		|	Документ.%1.Оплата КАК ОплатаВОтчетеОРозничныхПродажах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыОплатОрганизаций КАК ВидыОплатОрганизаций
		|		ПО ОплатаВОтчетеОРозничныхПродажах.ВидОплаты = ВидыОплатОрганизаций.Ссылка
		|ГДЕ
		|	ОплатаВОтчетеОРозничныхПродажах.Ссылка.ПометкаУдаления = ЛОЖЬ
		|	И ВидыОплатОрганизаций.ТипОплаты В (ЗНАЧЕНИЕ(Перечисление.ТипыОплат.ПлатежнаяКарта), ЗНАЧЕНИЕ(Перечисление.ТипыОплат.БанковскийКредит))";
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОплатаВОтчетеОРозничныхПродажах.Ссылка КАК Ссылка,
		|	ОплатаВОтчетеОРозничныхПродажах.Ссылка.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ОплатаВОтчетеОРозничныхПродажах.Ссылка) КАК Представление
		|ИЗ
		|	Документ.%1.Оплата КАК ОплатаВОтчетеОРозничныхПродажах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыОплатОрганизаций КАК ВидыОплатОрганизаций
		|		ПО ОплатаВОтчетеОРозничныхПродажах.ВидОплаты = ВидыОплатОрганизаций.Ссылка
		|ГДЕ
		|	ОплатаВОтчетеОРозничныхПродажах.Ссылка.ПометкаУдаления = ЛОЖЬ
		|	И ВидыОплатОрганизаций.ТипОплаты В (ЗНАЧЕНИЕ(Перечисление.ТипыОплат.ПлатежнаяКарта), ЗНАЧЕНИЕ(Перечисление.ТипыОплат.БанковскийКредит))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
		
	КонецЕсли;
	
	Возврат СтрШаблон(ТекстЗапроса, ИмяДокумента);
	
КонецФункции

Функция ТаблицаЗапросовИспользуетсяОплатаПоПлатежнымКартам()

	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ОплатаПлатежнойКартой";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ОплатаПлатежнойКартой",Истина, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ОтчетОРозничныхПродажах";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаЕстьОплатаКартойВДокументе("ОтчетОРозничныхПродажах", Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РозничнаяПродажа";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаЕстьОплатаКартойВДокументе("РозничнаяПродажа", Ложь);
	
	Возврат ТаблицаЗапросов;
	
КонецФункции 

Функция ДокументыПрепятствующиеОтключениюИспользуетсяОплатаПоПлатежнымКартам()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();

	ТаблицаЗапросов = ТаблицаЗапросовИспользуетсяОплатаПоПлатежнымКартам();
	
	ВидыОперацийОплатаПлатежнойКартой = Новый Массив;
	ВидыОперацийОплатаПлатежнойКартой.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя);
	ВидыОперацийОплатаПлатежнойКартой.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ВозвратПокупателю);
	ВидыОперацийОплатаПлатежнойКартой.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.РозничнаяВыручка);

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидыОпераций", ВидыОперацийОплатаПлатежнойКартой);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.ИмяДокумента = СтрокаТЗ.ИмяДокумента;
		МетаданныеДокумента = Метаданные.Документы.Найти(СтрокаТЗ.ИмяДокумента);
		СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции

Функция ТаблицаЗапросовИспользуютсяКорректировочныеДокументы()

	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "КорректировкаПоступления";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("КорректировкаПоступления",, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "КорректировкаРеализации";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("КорректировкаРеализации",, Ложь);
	
	Возврат ТаблицаЗапросов;

КонецФункции

Функция ДокументыПрепятствующиеОтключениюИспользуютсяКорректировочныеДокументы()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();

	ТаблицаЗапросов = ТаблицаЗапросовИспользуютсяКорректировочныеДокументы();

	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.ИмяДокумента = СтрокаТЗ.ИмяДокумента;
		МетаданныеДокумента = Метаданные.Документы.Найти(СтрокаТЗ.ИмяДокумента);
		СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;

КонецФункции

Функция ТекстЗапросаИнтеграцияНСПК()

	Возврат 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОборудованиеПоОрганизациям.Оборудование КАК Оборудование
	|ИЗ
	|	РегистрСведений.ОборудованиеПоОрганизациям КАК ОборудованиеПоОрганизациям
	|ГДЕ
	|	ОборудованиеПоОрганизациям.ЭлектронныеСертификатыНСПК = ИСТИНА";

КонецФункции

Функция ДокументыПрепятствующиеОтключениюИспользуютсяПлатежиПоРеестрам()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();

	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.Текст =  ТекстЗапросаИспользуетсяДокумент("ОплатаПлатежнойКартой",Истина, Ложь);
	Запрос.УстановитьПараметр("ВидыОпераций", 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ПлатежПоРеестру));
	
	МетаданныеДокумента = Метаданные.Документы.ОплатаПлатежнойКартой;
	
	СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
	
	СтрокаТаблицыДокументов.ИмяДокумента     = МетаданныеДокумента.Имя;
	СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
	СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;
КонецФункции

Функция ТекстЗапросаПредоплатыОтчетОРозничныхПродажах()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетОРозничныхПродажахОплатаСчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.ОплатаСчетов КАК ОтчетОРозничныхПродажахОплатаСчетов
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетОРозничныхПродажахПредоплаты.Ссылка
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Предоплаты КАК ОтчетОРозничныхПродажахПредоплаты
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетОРозничныхПродажахЗачетАвансов.Ссылка
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.ЗачетАвансов КАК ОтчетОРозничныхПродажахЗачетАвансов";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#Область ВестиУчетРасчетовСКонтрагентами

Функция ОтказСнятьВестиУчетРасчетовСКонтрагентами()
	
	Если Не ПростойИнтерфейсФункциональности() Тогда
		Возврат Истина;
	КонецЕсли;
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ВидОперации");
	
	// Расчеты с покупателями
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ПоступлениеНаРасчетныйСчет", Истина);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ПриходныйКассовыйОрдер", Истина);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("СписаниеСРасчетногоСчета", Истина);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("РасходныйКассовыйОрдер", Истина);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ОплатаПлатежнойКартой", Истина);
	НоваяСтрока.ВидОперации  = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ОплатаПлатежнойКартой", Истина);
	НоваяСтрока.ВидОперации  = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ВозвратПокупателю;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("НачислениеПеней");
	
	// Расчеты с поставщиками
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("СписаниеСРасчетногоСчета", Истина);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("РасходныйКассовыйОрдер", Истина);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийРКО.ОплатаПоставщику;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ПоступлениеНаРасчетныйСчет", Истина);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратОтПоставщика;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ПриходныйКассовыйОрдер", Истина);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПоступлениеТоваровУслуг");

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ИнвентаризацияТоваровНаСкладе");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ВозвратТоваровПоставщику");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ОприходованиеТоваров");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПеремещениеТоваров");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПоступлениеДопРасходов");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("СписаниеТоваров");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("СчетНаОплатуПоставщика");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ТребованиеНакладная");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("АвансовыйОтчет");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентамиНалогообложение(Истина);
	
	Запрос = Новый Запрос;

	УстановитьПривилегированныйРежим(Истина);
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Если ЗначениеЗаполнено(СтрокаТЗ.ВидОперации) Тогда
			Запрос.УстановитьПараметр("ВидОперации", СтрокаТЗ.ВидОперации);
		КонецЕсли;
		
		РезультатЗапроса = Запрос.Выполнить();
		ОтказСнять = Ложь;
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ОтказСнять = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ДокументыПрепятствующиеОтключениюВестиУчетРасчетовСКонтрагентами()
	
	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();
	
	ТаблицаЗапросов = ТаблицаЗапросовВестиУчетРасчетовСКонтрагентами();
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Если ЗначениеЗаполнено(СтрокаТЗ.ВидОперации) Тогда
			Запрос.УстановитьПараметр("ВидОперации", СтрокаТЗ.ВидОперации);
		КонецЕсли;
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.ИмяДокумента = СтрокаТЗ.ИмяДокумента;
		МетаданныеДокумента = Метаданные.Документы.Найти(СтрокаТЗ.ИмяДокумента);
		СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции 

Функция ТаблицаЗапросовВестиУчетРасчетовСКонтрагентами()

	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ВидОперации");
	
	// Расчеты с покупателями
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеНаРасчетныйСчет";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ПоступлениеНаРасчетныйСчет", Ложь);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПриходныйКассовыйОрдер";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ПриходныйКассовыйОрдер", Ложь);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "СписаниеСРасчетногоСчета";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("СписаниеСРасчетногоСчета", Ложь);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РасходныйКассовыйОрдер";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("РасходныйКассовыйОрдер", Ложь);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ОплатаПлатежнойКартой";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ОплатаПлатежнойКартой", Ложь);
	НоваяСтрока.ВидОперации  = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ОплатаПлатежнойКартой";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ОплатаПлатежнойКартой", Ложь);
	НоваяСтрока.ВидОперации  = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ВозвратПокупателю;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "НачислениеПеней";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("НачислениеПеней", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ВозвратТоваровОтПокупателя";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ВозвратТоваровОтПокупателя", Ложь, Ложь);
	
	// Расчеты с поставщиками
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "СписаниеСРасчетногоСчета";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("СписаниеСРасчетногоСчета", Ложь);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РасходныйКассовыйОрдер";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("РасходныйКассовыйОрдер", Ложь);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийРКО.ОплатаПоставщику;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеНаРасчетныйСчет";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ПоступлениеНаРасчетныйСчет", Ложь);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратОтПоставщика;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПриходныйКассовыйОрдер";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВестиУчетРасчетовСКонтрагентами("ПриходныйКассовыйОрдер", Ложь);
	НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика;
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеТоваровУслуг";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПоступлениеТоваровУслуг", Ложь, Ложь);

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РеализацияОтгруженныхТоваров";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ИнвентаризацияТоваровНаСкладе", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ВозвратТоваровПоставщику";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ВозвратТоваровПоставщику", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ОприходованиеТоваров";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ОприходованиеТоваров", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПеремещениеТоваров";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПеремещениеТоваров", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеДопРасходов";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПоступлениеДопРасходов", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "СписаниеТоваров";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("СписаниеТоваров", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "СчетНаОплатуПоставщика";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("СчетНаОплатуПоставщика", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ТребованиеНакладная";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ТребованиеНакладная", Ложь, Ложь);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "АвансовыйОтчет";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("АвансовыйОтчет", Ложь, Ложь);
	
	Возврат ТаблицаЗапросов;
	
КонецФункции 

Функция ТекстЗапросаВестиУчетРасчетовСКонтрагентами(ИмяДокумента, ТолькоПроверка = Истина)
	
	Если ТолькоПроверка Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Док.Ссылка КАК Ссылка
		|ИЗ
		|	Документ." + ИмяДокумента + " КАК Док
		|ГДЕ
		|	НЕ Док.ПометкаУдаления
		|	И Док.ВидОперации = &ВидОперации
		|	И НЕ Док.БезЗакрывающихДокументов";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Док.Ссылка КАК Ссылка,
		|	Док.Дата КАК Дата,
		|	Док.Ссылка КАК Представление
		|ИЗ
		|	Документ." + ИмяДокумента + " КАК Док
		|ГДЕ
		|	НЕ Док.ПометкаУдаления
		|	И Док.ВидОперации = &ВидОперации
		|	И НЕ Док.БезЗакрывающихДокументов
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВестиУчетРасчетовСКонтрагентамиНалогообложение(ТолькоПроверка = Истина)
	
	Если ТолькоПроверка Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Настройки.СистемаНалогообложения КАК СистемаНалогообложения
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения КАК Настройки
		|ГДЕ
		|	НЕ(Настройки.ПрименяетсяУСНДоходы
		|	)
		|	И НЕ Настройки.ПрименяетсяАУСН
		|	И НЕ Настройки.ПрименяетсяОсобыйПорядокНалогообложения
		|	И НЕ Настройки.ПрименяетсяНалогНаПрофессиональныйДоход";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Настройки.Период КАК Период,
		|	Настройки.СистемаНалогообложения КАК СистемаНалогообложения,
		|	Настройки.ПрименяетсяУСН КАК ПрименяетсяУСН,
		|	Настройки.ПрименяетсяАУСН КАК ПрименяетсяАУСН,
		|	Настройки.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
		|	Настройки.ПлательщикНДФЛ КАК ПлательщикНДФЛ,
		|	Настройки.ПлательщикЕНВД КАК ПлательщикЕНВД,
		|	Настройки.ПрименяетсяУСНПатент КАК ПрименяетсяУСНПатент,
		|	Настройки.ПрименяетсяОсобыйПорядокНалогообложения КАК ПрименяетсяОсобыйПорядокНалогообложения,
		|	Настройки.ПрименяетсяУСНДоходы КАК ПрименяетсяУСНДоходы,
		|	Настройки.ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
		|	Настройки.ПлательщикНДС КАК ПлательщикНДС,
		|	Настройки.ПлательщикТорговогоСбора КАК ПлательщикТорговогоСбора,
		|	Настройки.ПлательщикНДСПрименяющийУСН КАК ПлательщикНДСПрименяющийУСН
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения КАК Настройки
		|ГДЕ
		|	НЕ(Настройки.ПрименяетсяУСНДоходы
		|	)
		//|				И НЕ Настройки.ПлательщикНДСПрименяющийУСН)
		|	И НЕ Настройки.ПрименяетсяАУСН
		|	И НЕ Настройки.ПрименяетсяОсобыйПорядокНалогообложения
		|	И НЕ Настройки.ПрименяетсяНалогНаПрофессиональныйДоход
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция НалогообложениеПрепятствующееОтключениюВестиУчетРасчетовСКонтрагентами()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВестиУчетРасчетовСКонтрагентамиНалогообложение(Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса;
	
КонецФункции 

#КонецОбласти

#Область ВестиУчетПоДоговорам

Функция РезультатыЗапросовВестиУчетПоДоговорам(ТолькоПроверка = Истина)
	
	ЗапросВестиУчетПоДоговорам = Новый Запрос;
	ЗапросВестиУчетПоДоговорам.Текст = ТекстЗапросаВестиУчетПоДоговорам();
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	ЗапросВестиУчетПоДоговорам.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	
	ЗапросИспользуютсяДоговораКомиссии = Новый Запрос;
	ЗапросИспользуютсяДоговораКомиссии.Текст = ТекстЗапросаВедетсяДеятельностьПоДоговорамКомиссии(Истина);
	ИзменитьДляПроверкиТекстЗапроса(ЗапросИспользуютсяДоговораКомиссии.Текст, ТолькоПроверка);
	МассивВидовДоговоровКомиссии = Новый Массив;
	МассивВидовДоговоровКомиссии.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
	МассивВидовДоговоровКомиссии.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	МассивВидовДоговоровКомиссии.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	МассивВидовДоговоровКомиссии.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);
	ЗапросИспользуютсяДоговораКомиссии.УстановитьПараметр("ВидыДоговоров", МассивВидовДоговоровКомиссии);
	
	ЗапросИспользуетсяРазделениеПоВидамАктивов = Новый Запрос;
	ЗапросИспользуетсяРазделениеПоВидамАктивов.Текст = ТекстЗапросаРазделениеПоВидамАктивов();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗапросыВестиУчетПоДоговорам = Новый Структура;
	ЗапросыВестиУчетПоДоговорам.Вставить("УчетПоДоговорам",  ЗапросВестиУчетПоДоговорам.Выполнить());
	ЗапросыВестиУчетПоДоговорам.Вставить("ДоговораКомиссии", ЗапросИспользуютсяДоговораКомиссии.Выполнить());
	ЗапросыВестиУчетПоДоговорам.Вставить("РазделениеАвансов", ЗапросИспользуетсяРазделениеПоВидамАктивов.Выполнить());
	
	Возврат ЗапросыВестиУчетПоДоговорам;

КонецФункции

Функция ОтказСнятьВестиУчетПоДоговорам()
	
	РезультатыЗапросовВестиУчетПоДоговорам = РезультатыЗапросовВестиУчетПоДоговорам();
	ОтказСнять = (НЕ РезультатыЗапросовВестиУчетПоДоговорам.УчетПоДоговорам.Пустой() 
		ИЛИ НЕ РезультатыЗапросовВестиУчетПоДоговорам.ДоговораКомиссии.Пустой() 
		ИЛИ НЕ РезультатыЗапросовВестиУчетПоДоговорам.РазделениеАвансов.Пустой());
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ТекстЗапросаВестиУчетПоДоговорам()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОсновныеДоговорыКонтрагента.Договор,
	|	ОсновныеДоговорыКонтрагента.Контрагент,
	|	ОсновныеДоговорыКонтрагента.Организация
	|ПОМЕСТИТЬ ОсновныеДоговора
	|ИЗ
	|	РегистрСведений.ОсновныеДоговорыКонтрагента КАК ОсновныеДоговорыКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОстатки.Субконто1 КАК СсылкаКонтрагент,
	|	ХозрасчетныйОстатки.Субконто2 КАК Договор,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СуммаОстаток < 0
	|			ТОГДА -ХозрасчетныйОстатки.СуммаОстаток
	|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстаток
	|	КОНЕЦ КАК Остаток
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			,
	|			,
	|			&ВидыСубконто,
	|			НЕ Субконто2 В
	|					(ВЫБРАТЬ
	|						ОсновныеДоговора.Договор
	|					ИЗ
	|						ОсновныеДоговора)) КАК ХозрасчетныйОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРазделениеПоВидамАктивов()

	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УчетнаяПолитика.Период КАК Период,
	|	УчетнаяПолитика.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.СпособОтраженияАвансовВыданных = ЗНАЧЕНИЕ(Перечисление.СпособОтраженияАвансовВыданных.РазделятьПоВидамАктивов)";
	

	Возврат ТекстЗапроса;
КонецФункции 
 
#КонецОбласти

#Область ИспользуетсяГособоронзаказ

Функция ОтказСнятьИспользуетсяГособоронзаказ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИспользуетсяСправочник("ГосКонтракты");
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;
	
КонецФункции

#КонецОбласти

#Область ВедетсяУчетСдачиИмуществаВАренду

Функция ОтказСнятьВедетсяУчетСдачиИмуществаВАренду()
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПередачаОСВАренду");

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ВозвратОСОтАрендатора");
	
	Запрос = Новый Запрос;

	УстановитьПривилегированныйРежим(Истина);

	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтказСнять = Ложь;
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
				
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ДокументыПрепятствующиеОтключениюСдачиИмуществаВАренду()
	
	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();
	
	ТаблицаЗапросов = ТаблицаЗапросовВедетсяУчетСдачиИмуществаВАренду();
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.ИмяДокумента = СтрокаТЗ.ИмяДокумента;
		МетаданныеДокумента = Метаданные.Документы.Найти(СтрокаТЗ.ИмяДокумента);
		СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции 

Функция ТаблицаЗапросовВедетсяУчетСдачиИмуществаВАренду()

	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПередачаОСВАренду";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПередачаОСВАренду", Ложь, Ложь);

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ВозвратОСОтАрендатора";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ВозвратОСОтАрендатора", Ложь, Ложь);
	
	Возврат ТаблицаЗапросов;
	
КонецФункции 

#КонецОбласти

#Область ВедетсяУчетГрупповыхОсновныхСредств

Функция ОтказСнятьВедетсяУчетГрупповыхОС()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИспользуютсяГрупповыеОС();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

Функция ТекстЗапросаИспользуютсяГрупповыеОС(ТолькоПроверка = Истина)
	
	Если ТолькоПроверка Тогда
		Возврат "ВЫБРАТЬ ПЕРВЫЕ 1
		        |	ОсновныеСредства.Ссылка КАК Ссылка
		        |ИЗ
		        |	Справочник.ОсновныеСредства КАК ОсновныеСредства
		        |ГДЕ
		        |	ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)";
	Иначе
		Возврат "ВЫБРАТЬ
		        |	ОсновныеСредства.Ссылка КАК Ссылка,
		        |	ОсновныеСредства.Наименование КАК Представление
		        |ИЗ
		        |	Справочник.ОсновныеСредства КАК ОсновныеСредства
		        |ГДЕ
		        |	ОсновныеСредства.ЕдиницаУчета = ЗНАЧЕНИЕ(Перечисление.ЕдиницыУчетаОС.ГрупповойОбъект)
		        |
		        |УПОРЯДОЧИТЬ ПО
		        |	Представление,
		        |	Ссылка";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ИспользоватьСтатьиДвиженияДенежныхСредств

Функция ТекстЗапросаИспользоватьСтатьиДвиженияДенежныхСредств(ТолькоПроверка = Истина)
	
	Если ТолькоПроверка Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Субконто.Регистратор КАК Ссылка
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Субконто КАК Субконто
		|ГДЕ
		|	Субконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств)
		|	И Субконто.Значение <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)";
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Субконто.Период КАК Дата,
		|	Субконто.Регистратор КАК Ссылка
		|ПОМЕСТИТЬ ДокументыДДС
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Субконто КАК Субконто
		|ГДЕ
		|	Субконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств)
		|	И Субконто.Значение <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыДДС.Дата КАК Дата,
		|	ДокументыДДС.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(ДокументыДДС.Ссылка) КАК Представление
		|ИЗ
		|	ДокументыДДС КАК ДокументыДДС
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ОтказСнятьИспользоватьСтатьиДвиженияДенежныхСредств()
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользоватьСтатьиДвиженияДенежныхСредств();

	Запрос = Новый Запрос;

	УстановитьПривилегированныйРежим(Истина);

	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтказСнять = Ложь;
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
				
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ТаблицаЗапросовИспользоватьСтатьиДвиженияДенежныхСредств()

	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));

	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользоватьСтатьиДвиженияДенежныхСредств(Ложь);

	Возврат ТаблицаЗапросов;
	
КонецФункции 

Функция ДокументыПрепятствующиеОтключениюИспользоватьСтатьиДвиженияДенежныхСредств()
	
	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();
	
	ТаблицаЗапросов = ТаблицаЗапросовИспользоватьСтатьиДвиженияДенежныхСредств();
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции 

Процедура ДействияПриИзмененииИспользоватьСтатьиДвиженияДенежныхСредств(НовоеЗначение)
	
	НачатьТранзакцию();
	
	Константы.ИспользоватьСтатьиДвиженияДенежныхСредств.Установить(НовоеЗначение);
	
	// Изменим состав субконто на счетах учета денежных средств.
	ПараметрыУчета = ОбщегоНазначенияБП.ОпределитьПараметрыУчета();
	ПараметрыУчета.ВестиУчетПоСтатьямДДС = НовоеЗначение;
	ОбщегоНазначенияБП.ПрименитьПараметрыУчета(ПараметрыУчета, Истина, Ложь);
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#Область ИспользуетсяИнкассация

Функция ТаблицаЗапросовИспользоватьИнкассацию()
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ИмяДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаЗапросов.Колонки.Добавить("ВидыОпераций");
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "РасходныйКассовыйОрдер";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("РасходныйКассовыйОрдер", Истина, Ложь);
	НоваяСтрока.ВидыОпераций = Новый Массив;
	НоваяСтрока.ВидыОпераций.Добавить(Перечисления.ВидыОперацийРКО.Инкассация);
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ИмяДокумента = "ПоступлениеНаРасчетныйСчет";
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаИспользуетсяДокумент("ПоступлениеНаРасчетныйСчет", Истина, Ложь);
	НоваяСтрока.ВидыОпераций = Новый Массив;
	НоваяСтрока.ВидыОпераций.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.Инкассация);
	
	Возврат ТаблицаЗапросов;
	
КонецФункции

Функция ДокументыПрепятствующиеОтключениюИспользоватьИнкассацию()
	
	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();
	
	ТаблицаЗапросов = ТаблицаЗапросовИспользоватьИнкассацию();
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		Запрос.УстановитьПараметр("ВидыОпераций", СтрокаТЗ.ВидыОпераций);
		
		СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
		СтрокаТаблицыДокументов.ИмяДокумента = СтрокаТЗ.ИмяДокумента;
		МетаданныеДокумента = Метаданные.Документы.Найти(СтрокаТЗ.ИмяДокумента);
		СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
		СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
		
	КонецЦикла;
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции

#КонецОбласти

#Область ИспользуетсяДоставкаАвтотранспортом

Функция ДокументыПрепятствующиеОтключениюИспользуетсяДоставкаАвтотранспортом()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Ссылка КАК Представление
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	(РеализацияТоваровУслуг.Перевозчик <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИЛИ РеализацияТоваровУслуг.МаркаАвтомобиля <> """"
	|			ИЛИ РеализацияТоваровУслуг.РегистрационныйЗнакАвтомобиля <> """"
	|			ИЛИ РеализацияТоваровУслуг.Водитель <> """"
	|			ИЛИ РеализацияТоваровУслуг.КраткоеНаименованиеГруза <> """"
	|			ИЛИ РеализацияТоваровУслуг.СопроводительныеДокументы <> """")
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
	СтрокаТаблицыДокументов.ИмяДокумента = Метаданные.Документы.РеализацияТоваровУслуг.Имя;
	СтрокаТаблицыДокументов.СинонимДокумента = Метаданные.Документы.РеализацияТоваровУслуг.Синоним;
	СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;

КонецФункции

#КонецОбласти

#Область ОбратноеНачислениеНДС

Функция РезультатЗапросаОбратноеНачислениеНДС(ТолькоПроверка = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	(ДоговорыКонтрагентов.УчетАгентскогоНДСПокупателем
	|			ИЛИ ДоговорыКонтрагентов.УчетАгентскогоНДС
	|				И ДоговорыКонтрагентов.ВидАгентскогоДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.РеализацияТоваров))";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивДоговоров = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивДоговоров;
	
КонецФункции

#КонецОбласти

#Область ОбеспеченияОбязательствИПлатежей

Функция РезультатЗапросаОбеспеченияОбязательствИПлатежей(ТолькоПроверка = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидОбеспеченияОбязательствИПлатежей <> ЗНАЧЕНИЕ(Перечисление.ВидыОбеспеченийОбязательствИПлатежей.ПустаяСсылка)";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивДоговоров = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МассивДоговоров;
	
КонецФункции

Функция ОтказСнятьУчетОбеспеченийОбязательствИПлатежей()
	
	ОтказСнять = РезультатЗапросаОбеспеченияОбязательствИПлатежей().Количество() > 0;
	
	Возврат ОтказСнять;
	
КонецФункции

#КонецОбласти

#Область ВыполняютсяСтроительныеРаботы

Функция ОтказСнятьВыполняютсяСтроительныеРаботы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВыполняютсяСтроительныеРаботы();
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

Функция ДокументыПрепятствующиеОтключениюВыполняютсяСтроительныеРаботы()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВыполняютсяСтроительныеРаботы(Ложь);
	
	СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
	МетаданныеДокумента = Метаданные.Документы.РеализацияТоваровУслуг;
	СтрокаТаблицыДокументов.ИмяДокумента = МетаданныеДокумента.Имя;
	СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
	СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции

Функция ТекстЗапросаВыполняютсяСтроительныеРаботы(ТолькоПроверка = Истина)
	
	Если ТолькоПроверка Тогда
		Возврат "ВЫБРАТЬ ПЕРВЫЕ 1
		        |	РеализацияТоваровУслуг.Ссылка КАК Ссылка
		        |ИЗ
		        |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		        |ГДЕ
		        |	РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.СтроительныеРаботы)
		        |	И НЕ РеализацияТоваровУслуг.ПометкаУдаления";
	Иначе
		Возврат "ВЫБРАТЬ
		        |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		        |	РеализацияТоваровУслуг.Дата КАК Дата,
		        |	ПРЕДСТАВЛЕНИЕ(РеализацияТоваровУслуг.Ссылка) КАК Представление
		        |ИЗ
		        |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		        |ГДЕ
		        |	РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.СтроительныеРаботы)
		        |	И НЕ РеализацияТоваровУслуг.ПометкаУдаления";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

Функция ОтказСнятьВедетсяУчетПоПутевымЛистам()
	
	ТаблицаЗапросов = Новый ТаблицаЗначений;
	ТаблицаЗапросов.Колонки.Добавить("ТекстЗапроса", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	НоваяСтрока = ТаблицаЗапросов.Добавить();
	НоваяСтрока.ТекстЗапроса = ТекстЗапросаВедетсяУчетПоПутевымЛистам(Истина);
	
	Запрос = Новый Запрос;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого СтрокаТЗ Из ТаблицаЗапросов Цикл
		
		Запрос.Текст = СтрокаТЗ.ТекстЗапроса;
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ОтказСнять = Ложь;
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			ОтказСнять = Истина;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОтказСнять;

КонецФункции

Функция ОтказСнятьИспользоватьДоставкуТранспортнойКомпанией()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.СпособДоставки <> ЗНАЧЕНИЕ(Справочник.СпособыДоставки.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетНаОплатуПокупателю.Ссылка
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.СпособДоставки <> ЗНАЧЕНИЕ(Справочник.СпособыДоставки.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;

КонецФункции 

Функция ОтказСнятьВестиУчетПрослеживаемыхТоваров()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ПрослеживаемыйТовар";
	
	УстановитьПривилегированныйРежим(Истина);

	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;

КонецФункции

Функция ОтказСнятьИспользоватьОплатуСертификатамиНСПК()
	Запрос = Новый Запрос;
	
	Запрос.Текст = ТекстЗапросаИнтеграцияНСПК();

	
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

Функция ОтказСнятьИспользоватьПродажиЧерезМаркетплейс()
	
	УстановитьПривилегированныйРежим(Истина);
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтчетКомиссионераОПродажах.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах)
	|	ИЛИ ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОСписании)
	|	ИЛИ ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОбОприходовании)";
		
	РезультатЗапроса = Запрос.Выполнить();
		
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
			
	Возврат ОтказСнять;

КонецФункции 

Функция ОтказСнятьИспользоватьПлатежиПоРеестрам()
	
	Запрос = Новый Запрос;
	
	СписокОпераций = Новый Массив;
	СписокОпераций.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ПлатежПоРеестру);
	
	Запрос.УстановитьПараметр("ВидыОпераций", СписокОпераций);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.Текст = ТекстЗапросаИспользуетсяДокумент("ОплатаПлатежнойКартой", Истина, Истина);
	
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

Функция ОтказСнятьОбратноеНачислениеНДС()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииОбратноеНачислениеНДС(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьОсуществляетсяДеятельностьПоОперациямФакторинга()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииФакторинговыеОперации(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьКомпонентыАмортизацииОС()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииКомпонентыАмортизации(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьВедетсяУчетПредметовАренды()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПредметыАренды(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьВедетсяУчетЛизинговогоИмущества()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииЛизинг(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьПредоплатыВРознице()
	Запрос = Новый Запрос;
	
	СписокОпераций = Новый Массив;
	СписокОпераций.Добавить(Перечисления.ВидыОперацийРозничнаяПродажа.Предоплата);
	
	Запрос.УстановитьПараметр("ВидыОпераций", СписокОпераций);
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивЗапросов = Новый Массив;
	МассивЗапросов.Добавить(ТекстЗапросаИспользуетсяДокумент("РозничнаяПродажа", Истина));
	МассивЗапросов.Добавить(ТекстЗапросаПредоплатыОтчетОРозничныхПродажах());
	
	Для каждого ТекстЗапроса Из МассивЗапросов Цикл
		Запрос.Текст = ТекстЗапроса;
		Если НЕ Запрос.Выполнить().Пустой() Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Функция ОтказСнятьИмпортПодакцизныхТоваров()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПорядокУплатыАкцизов.Организация КАК Организация,
	|	ПорядокУплатыАкцизов.Импорт КАК Импорт
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Импорт = ИСТИНА";
	
	УстановитьПривилегированныйРежим(Истина);

	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;

КонецФункции

Функция ОтказСнятьПроизводствоПодакцизныхТоваров()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПорядокУплатыАкцизов.Организация КАК Организация,
	|	ПорядокУплатыАкцизов.Производство КАК Производство
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Производство = ИСТИНА";
	
	УстановитьПривилегированныйРежим(Истина);

	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;

КонецФункции

Функция ТекстЗапросаИспользуютсяПоступленияНаНесколькоСкладов()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.Дата КАК Дата,
	|	ПРЕДСТАВЛЕНИЕ(ПоступлениеТоваровУслуг.Ссылка) КАК Представление
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.СкладВСписке = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ОтказСнятьПоступлениеНаНесколькоСкладов()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Проведен = ИСТИНА
	|	И ПоступлениеТоваровУслуг.СкладВСписке = ИСТИНА");
	
	Результат = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НЕ Результат.Пустой();
	
	
КонецФункции

#КонецОбласти

#Область СписокДокументовПрепятствующихОтключению

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюУчетПоДоговорам(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
			
	РезультатыЗапросовВестиУчетПоДоговорам = РезультатыЗапросовВестиУчетПоДоговорам(Ложь);
	
	УчетПоДоговорам = РезультатыЗапросовВестиУчетПоДоговорам.УчетПоДоговорам.Выбрать();
	
	Если УчетПоДоговорам.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок",
			НСтр("ru = 'Остатки по договорам'"));
		ПарамерыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета по договорам перенесите задолженность на один договор 
			|и установите его ""Основным"" (по кнопке ""Использовать как основной"")'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"СтрокаОбъекты",
			УчетПоДоговорам,
			,
			"ШапкаОбъекты",
			ПарамерыШапки);
		
	КонецЕсли;
	
	ДоговораКомиссии = РезультатыЗапросовВестиУчетПоДоговорам.ДоговораКомиссии.Выбрать();
	
	Если ДоговораКомиссии.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок", НСтр("ru = 'Договора комиссии'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"СтрокаКомиссия",
			ДоговораКомиссии,
			,
			"ШапкаКомиссия",
			ПарамерыШапки);
		
	КонецЕсли;
		
	УчетнаяПолитикаРазделениеАвансов = РезультатыЗапросовВестиУчетПоДоговорам.РазделениеАвансов.Выбрать();
	
	Если УчетнаяПолитикаРазделениеАвансов.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		Заголовок = НСтр("ru = 'Отражение авансов в балансе отдельными строками'");
		Пояснение = НСтр("ru = 'Авансы выданные отражаются в балансе отдельными строками, по видам активов указанных в договоре.
		                        |Для отключения учета по договорам измените значение переключателя в учетной политике
		                        |""Авансы, выданные поставщикам, отражаются в балансе"" на ""Одной строкой в дебиторской задолженности""'");
		
		ЗаполнитьПечатнуюФормуПоУчетнойПолитике(
			УчетнаяПолитикаРазделениеАвансов, 
			Заголовок, 
			Пояснение, 
			СтруктураРезультата);
		
	КонецЕсли;	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокДокументовПрепятствующихОтключениюОпции(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);

	// Шапка своя для каждой опции.
	// Поэтому заполняем шапку табличного документа вне основной процедуры.
	// Далее см. ЗаполнитьТабличныйДокументПоДаннымВыборки()
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.СписокДокументов");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		
	Если Параметры.Свойство("УправлениеЗачетомАвансовПогашениемЗадолженности") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюУправлениеЗачетомАвансовПогашениемЗадолженности();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Управление зачетом авансов и погашением задолженности'");
	ИначеЕсли Параметры.Свойство("ВедетсяУчетВозвратнойТары") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюВозвратойТары();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Возвратная тара'");
	ИначеЕсли Параметры.Свойство("ВедетсяУчетПоПутевымЛистам") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюПутевыхЛистов();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Путевые листы'");
	ИначеЕсли Параметры.Свойство("ИспользоватьДоставкуТранспортнойКомпанией") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюДоставкаТранспортнойКомпанией();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Доставка транспортной компанией'");
	ИначеЕсли Параметры.Свойство("ИспользоватьВыпускПродукции") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюИспользуетсяВыпускПродукции();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Выпуск продукции'");
	ИначеЕсли Параметры.Свойство("ИспользоватьОплатуПоПлатежнымКартам") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюИспользуетсяОплатаПоПлатежнымКартам();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Платежные карты'");
	ИначеЕсли Параметры.Свойство("ИспользоватьПлатежиПоРеестрам") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюИспользуютсяПлатежиПоРеестрам();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Платежи по реестрам'");
	ИначеЕсли Параметры.Свойство("ИспользоватьКорректировочныеДокументы") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюИспользуютсяКорректировочныеДокументы();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Исправительные и корректировочные документы'");
	ИначеЕсли Параметры.Свойство("ИспользоватьДоставкуАвтотранспортом") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюИспользуетсяДоставкаАвтотранспортом();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Доставка товаров автотранспортом'");
	ИначеЕсли Параметры.Свойство("ИспользоватьИнкассацию") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюИспользоватьИнкассацию();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Документы по инкассации'");
	ИначеЕсли Параметры.Свойство("ВедетсяУчетСдачиИмуществаВАренду") Тогда 
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюСдачиИмуществаВАренду();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Сдача имущества в аренду'");
	ИначеЕсли Параметры.Свойство("ИспользоватьСтатьиДвиженияДенежныхСредств") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюИспользоватьСтатьиДвиженияДенежныхСредств();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Статьи движения денег'");
	ИначеЕсли Параметры.Свойство("ВыполняютсяСтроительныеРаботы") Тогда
		ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюВыполняютсяСтроительныеРаботы();
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Выполнение строительных работ'");
	КонецЕсли;

	СтруктураРезультата.Результат.Вывести(ОбластьШапка);
	
	НомерПП = 0;
	
	Для Каждого СтрокаТаблицыДокументов Из ТаблицаРезультатовЗапросов Цикл
		
		Выборка = СтрокаТаблицыДокументов.РезультатЗапроса.Выбрать();
		
		Если Выборка.Количество() > 0 Тогда
			ЗаполнитьТабличныйДокументПоДаннымВыборки(
				СтруктураРезультата.Результат,
				"Строка",
				Выборка,
				НомерПП);
		КонецЕсли;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокНастроекПрепятствующихОтключениюОпции(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);

	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.СписокДокументов");
	
	Если Параметры.Свойство("ВестиУчетРасчетовСКонтрагентами") Тогда
		
		Если Не ПростойИнтерфейсФункциональности() Тогда
			
			// Отдельно выведем настройки интерфейса, мешающие отключению опции.
			ОбластьШапкаИПояснение = Макет.ПолучитьОбласть("ШапкаТолькоЗаголовокИПояснение");
			ОбластьШапкаИПояснение.Параметры.Заголовок = НСтр("ru = 'Полный интерфейс'");
			ОбластьШапкаИПояснение.Параметры.Пояснение = НСтр("ru = 'Для отключения опции перейдите на простой интерфейс
				|в форме ""Интерфейс"" раздела ""Администрирование""'");
			
			СтруктураРезультата.Результат.Вывести(ОбластьШапкаИПояснение);
			
		КонецЕсли;
		
		РезультатЗапроса = НалогообложениеПрепятствующееОтключениюВестиУчетРасчетовСКонтрагентами();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			
			// Отдельно выведем настройки системы налогообложения, мешающие отключению ФО
			ОбластьШапкаНалогообложение = Макет.ПолучитьОбласть("ШапкаНалогообложение");
			ОбластьШапкаНалогообложение.Параметры.Заголовок = НСтр("ru = 'Система налогообложения'");
			
			СтруктураРезультата.Результат.Вывести(ОбластьШапкаНалогообложение);
			
			ОбластьСтрокаНалогообложение = Макет.ПолучитьОбласть("СтрокаНалогообложение");
			СтруктураДанных = НастройкиУчета.ОписаниеСтруктурыУчетнойПолитики();
			
			Пока Выборка.Следующий() Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);
				ПредставлениеЗаписи = ОрганизацииФормыКлиентСервер.ПредставлениеСистемыНалогообложения(СтруктураДанных);
				ОбластьСтрокаНалогообложение.Параметры.Период = Формат(Выборка.Период, "ДЛФ=D");
				ОбластьСтрокаНалогообложение.Параметры.Представление = ПредставлениеЗаписи;
				
				СтруктураРезультата.Результат.Вывести(ОбластьСтрокаНалогообложение);
				
			КонецЦикла;
		
		КонецЕсли; 
		
		// Далее выводим список документов, мешающих отключению ФО 
		ПараметрыШапки = Новый Структура("Заголовок");
		
		Если Параметры.Свойство("ВестиУчетРасчетовСКонтрагентами") Тогда
			ТаблицаРезультатовЗапросов = ДокументыПрепятствующиеОтключениюВестиУчетРасчетовСКонтрагентами();
			ПараметрыШапки.Заголовок = НСтр("ru = 'Документы расчетов с покупателями и поставщиками'");
		КонецЕсли;
				
		НомерПП = 0;
		
		Для Каждого СтрокаТаблицыДокументов Из ТаблицаРезультатовЗапросов Цикл
			
			Выборка = СтрокаТаблицыДокументов.РезультатЗапроса.Выбрать();
			
			Если Выборка.Количество() > 0 Тогда
				ЗаполнитьТабличныйДокументПоДаннымВыборки(
					СтруктураРезультата.Результат,
					"Строка",
					Выборка,
					НомерПП,
					"Шапка",
					ПараметрыШапки);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюУчетПоПодразделениям(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
		
	РезультатыЗапросовИспользуютсяОбособленныеПодразделения = РезультатыЗапросовИспользуютсяОбособленныеПодразделения(Ложь);
	
	// Филиалы
	Выборка = РезультатыЗапросовИспользуютсяОбособленныеПодразделения[0].Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Организации с видом ""Обособленное подразделение""'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета по подразделениям необходимо удалить организации 
				|с видом ""Обособленное подразделение'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаОбособленноеПодразделение",
			ПараметрыШапки);
			
	КонецЕсли;
	
	// Обособленные подразделения
	Выборка = РезультатыЗапросовИспользуютсяОбособленныеПодразделения[1].Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Обособленные подразделения'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета по подразделениям снимите флажок ""Обособленное подразделение""'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаОбособленноеПодразделение",
			ПараметрыШапки);
			
	КонецЕсли;
	
	// Документы с заполненными подразделениями
	РезультатыЗапросовИспользуютсяПодразделенияВПроводках = РезультатыЗапросовИспользуютсяПодразделенияВПроводках(Ложь);
	Выборка = РезультатыЗапросовИспользуютсяПодразделенияВПроводках.Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Документы с движениями по подразделениям'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета по подразделениям очистите ""Подразделение"" в движениях документов'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСПояснением",
			ПараметрыШапки);
			
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюУчетГрупповыхОС(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИспользуютсяГрупповыеОС(Ложь);
	
	// ОС с единицей учета "Групповой объект"
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Групповые объекты основные средства'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета групповых объектов ОС установите единицу учета ""Инвентарный объект""'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаГрупповыеОС",
			ПараметрыШапки);
			
	КонецЕсли;
	
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

// Процедура заполняет табличный документ, состоящий из шапки и строк таблицы
//
// Параметры:
//  ТабличныйДокумент	 - ТабличныйДокумент - заполняемый табличный документ. 
//  ИмяОбластиСтроки	 - Строка - наименование области макета СписокДокументов для вывода строки таблицы.
//  Выборка				 - ВыборкаИзРезультатаЗапроса - выборка, по данным которой заполняются строки таблицы.
//  НомерСтроки			 - Число - начальное значение счетчика строк, который выводится в строках таблицы.
//  ИмяОбластиШапки		 - Строка - наименование области макета СписокДокументов для вывода шапки отчета.
//						   Если параметр не передан, то шапка отчета не выводится.
//  ПараметрыШапки		 - Структура - структура, из которой заполняются параметры области шапки.
//
Процедура ЗаполнитьТабличныйДокументПоДаннымВыборки(ТабличныйДокумент, ИмяОбластиСтроки, Выборка, НомерСтроки = 0, ИмяОбластиШапки = "", ПараметрыШапки = Неопределено) Экспорт 
		
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.СписокДокументов");
	
	// Выводим шапку
	Если ИмяОбластиШапки <> "" Тогда
		
		ОбластьШапка = Макет.ПолучитьОбласть(ИмяОбластиШапки);
		
		Если ТипЗнч(ПараметрыШапки) = Тип("Структура") Тогда
			ОбластьШапка.Параметры.Заполнить(ПараметрыШапки);
		КонецЕсли;	
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
	КонецЕсли;	
		
	ОбластьСтрока = Макет.ПолучитьОбласть(ИмяОбластиСтроки);
	
	Пока Выборка.Следующий() Цикл
	
		НомерСтроки = НомерСтроки + 1;
	
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		ОбластьСтрока.Параметры.НомерПП = НомерСтроки;
		
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	
	КонецЦикла;	
	
КонецПроцедуры

Функция ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции()

	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("ИмяДокумента",     ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаДокументов.Колонки.Добавить("СинонимДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаДокументов.Колонки.Добавить("РезультатЗапроса");
	
	Возврат ТаблицаДокументов;

КонецФункции

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюРасширенногоФункционала(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Результаты = РезультатЗапросаРасширенныйФункционал(Ложь);
	
	// Учет расходов по элементам затрат.
	Выборка = Результаты.ЭлементыЗатрат.Выбрать();
	Заголовок = НСтр("ru = 'Учет расходов по элементам затрат'");
	Пояснение = НСтр("ru = 'Для отключения расширенного функционала в учетной политике организации снимите флажки 
		|""Учет расходов ведется по элементам затрат"" и ""Учет расходов ведется по статьям затрат""'");
	ЗаполнитьПечатнуюФормуПоУчетнойПолитике(Выборка, Заголовок, Пояснение, СтруктураРезультата);
	
	// Закрытие счета 28 "Брак в производстве"
	Выборка = Результаты.БракВПроизводстве.Выбрать();
	Заголовок = НСтр("ru = 'Закрытие счета 28 ""Брак в производстве""'");
	Пояснение = НСтр("ru = 'Для отключения расширенного функционала в учетной политике откажитесь от автоматического закрытия счета 28 ""Брак в производстве"" (раздел ""Учет затрат"")'");
	ЗаполнитьПечатнуюФормуПоУчетнойПолитике(Выборка, Заголовок, Пояснение, СтруктураРезультата);
	
	// Закрытие счета 29 "Обслуживающие производства и хозяйства"
	Выборка = Результаты.ОбслуживающиеПроизводства.Выбрать();
	Заголовок = НСтр("ru = 'Закрытие счета 29 ""Обслуживающие производства и хозяйства""'");
	Пояснение = НСтр("ru = 'Для отключения расширенного функционала в учетной политике откажитесь от автоматического закрытия счета 29 ""Обслуживающие производства и хозяйства"" (раздел ""Учет затрат"")'");
	ЗаполнитьПечатнуюФормуПоУчетнойПолитике(Выборка, Заголовок, Пояснение, СтруктураРезультата);
	
	// Оценка продукции по плановой себестоимости
	Выборка = Результаты.ОценкаПродукцииПоПлановойСтоимости.Выбрать();
	Заголовок = НСтр("ru = 'Оценка готовой продукции в сумме плановых (нормативных) затрат'");
	Пояснение = НСтр("ru = 'Для отключения расширенного функционала в учетной политике организации установите оценку готовой продукции по фактической себестоимости (раздел ""Учет затрат"")'");
	ЗаполнитьПечатнуюФормуПоУчетнойПолитике(Выборка, Заголовок, Пояснение, СтруктураРезультата);
	
	// Обесценение запасов
	Выборка = Результаты.ОбесценениеЗапасов.Выбрать();
	Заголовок = НСтр("ru = 'Обесценение запасов'");
	Пояснение = НСтр("ru = 'Для отключения расширенного функционала в учетной политике организации снимите флажок ""Обесценение запасов""'");
	ЗаполнитьПечатнуюФормуПоУчетнойПолитике(Выборка, Заголовок, Пояснение, СтруктураРезультата);
	
	// Обесценение внеоборотных активов
	Выборка = Результаты.ОбесценениеВнеоборотныхАктивов.Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		СинонимДокумента = Метаданные.Документы.ОбесценениеВнеоборотныхАктивов.Синоним;
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Обесценение внеоборотных активов'"));
		ПараметрыШапки.Вставить("Пояснение",
			СтрШаблон(НСтр("ru = 'Для отключения расширенного функционала удалите документы ""%1""'"), СинонимДокумента));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаОрганизация",
			ПараметрыШапки);
			
	КонецЕсли;
	
	// Амортизация с дня принятия к учету
	Выборка = Результаты.АмортизацияСДатыПризнания.Выбрать();
	Заголовок = НСтр("ru = 'Амортизация с дня принятия к учету'");
	Пояснение = НСтр("ru = 'Для отключения расширенного функционала укажите в учетной политике организации, что начисление амортизации начинается с месяца, следующего за месяцем принятия к учету'");
	ЗаполнитьПечатнуюФормуПоУчетнойПолитике(Выборка, Заголовок, Пояснение, СтруктураРезультата);
	
	// Договоры (счета-фактуры экспедиторов).
	Выборка = Результаты.СчетаФактурыЭкспедиторов.Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Счета-фактуры экспедиторов'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения расширенного функционала снимите в договорах флажок
				|""Счета-фактуры перевыставляются от имени организации""'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаДоговор",
			ПараметрыШапки);
			
	КонецЕсли;
	
	// Отчетность некредитных финансовых организаций
	Выборка = Результаты.НекредитныеФинансовыеОрганизации.Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Отчетность некредитных финансовых организаций'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения расширенного функционала снимите в организациях флажок
				|""Организация является некредитной финансовой организацией""'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаОрганизация",
			ПараметрыШапки);
			
	КонецЕсли;
	
	// Рассрочка платежа по документам поступления.
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИспользуетсяДокумент("УсловияОплатыПоступления", , Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Количество() > 0 Тогда
		// см. ЗаполнитьТабличныйДокументПоДаннымВыборки()
		Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.СписокДокументов");
		ОбластьШапка = Макет.ПолучитьОбласть("ШапкаСПояснением");
		ОбластьШапка.Параметры.Заголовок = НСтр("ru = 'Дисконтирование обязательств по рассрочке оплаты поступления'");
		ОбластьШапка.Параметры.Пояснение =
			НСтр("ru = 'Для отключения расширенного функционала удалите документы ""Условия оплаты поступлений""'");
		
		СтруктураРезультата.Результат.Вывести(ОбластьШапка);
		
		НомерПП = 0;
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			НомерПП);
	КонецЕсли;
		
	// Отчетность крупнейших налогоплательщиков
	Выборка = Результаты.КрупнейшийНалогоплательщик.Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Отчетность крупнейших налогоплательщиков'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения расширенного функционала снимите в организациях флажок ""Организация является крупнейшим налогоплательщиком""'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаОрганизация",
			ПараметрыШапки);
			
	КонецЕсли;
		
	Выборка = Результаты.КрупнейшийНалогоплательщикИстория.Выбрать();
	Заголовок = НСтр("ru = 'Отчетность крупнейших налогоплательщиков'");
	Пояснение = НСтр("ru = 'Для отключения расширенного функционала удалите все записи регистра 
		|""История регистраций в налоговом органе крупнейшего налогоплательщика""'");
	ЗаполнитьПечатнуюФормуПериодическогоРегистра(Выборка, Заголовок, Пояснение, СтруктураРезультата, "КрупнейшийНалогоплательщикИсторияРегистрацийВНалоговомОргане", "СтруктурнаяЕдиница");
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура ЗаполнитьПечатнуюФормуПоУчетнойПолитике(Выборка, Заголовок, Пояснение, СтруктураРезультата)
	
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыШапки = Новый Структура;
	
	ПараметрыШапки.Вставить("Заголовок", Заголовок);
	ПараметрыШапки.Вставить("Пояснение", Пояснение);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.СписокДокументов");
	
	ОбластьШапка = Макет.ПолучитьОбласть("ШапкаУчетнаяПолитика");
	ОбластьШапка.Параметры.Заполнить(ПараметрыШапки);
	СтруктураРезультата.Результат.Вывести(ОбластьШапка);
	
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаУчетнаяПолитика");
	ПараметрыСтроки = Новый Структура;
	НомерПП = 0;
	Пока Выборка.Следующий() Цикл
		НомерПП = НомерПП + 1;
		ПараметрыСтроки.Вставить("НомерПП", НомерПП);
		ПараметрыСтроки.Вставить("Представление", Формат(Выборка.Период, "ДФ='MMMM yyyy'") + ", " + Выборка.Организация);
		ПараметрыСтроки.Вставить("КлючЗаписи",
			РегистрыСведений.УчетнаяПолитика.СоздатьКлючЗаписи(Новый Структура("Период, Организация", Выборка.Период, Выборка.Организация)));
		ОбластьСтрока.Параметры.Заполнить(ПараметрыСтроки);
		СтруктураРезультата.Результат.Вывести(ОбластьСтрока);
	КонецЦикла;
		
КонецПроцедуры

Процедура ЗаполнитьПечатнуюФормуПериодическогоРегистра(Выборка, Заголовок, Пояснение, СтруктураРезультата, ИмяРегистра, ИмяВедущегоИзмерения = "Организация")
	
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыШапки = Новый Структура;
	
	ПараметрыШапки.Вставить("Заголовок", Заголовок);
	ПараметрыШапки.Вставить("Пояснение", Пояснение);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.СписокДокументов");
	
	ОбластьШапка = Макет.ПолучитьОбласть("ШапкаЗаписьПериодическогоРегистра");
	ОбластьШапка.Параметры.Заполнить(ПараметрыШапки);
	СтруктураРезультата.Результат.Вывести(ОбластьШапка);
	
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаЗаписьПериодическогоРегистра");
	ПараметрыСтроки = Новый Структура;
	НомерПП = 0;
	Пока Выборка.Следующий() Цикл
		НомерПП = НомерПП + 1;
		ПараметрыСтроки.Вставить("НомерПП", НомерПП);
		ПараметрыСтроки.Вставить("Представление", Формат(Выборка.Период, "ДФ='MMMM yyyy'") + ", " + Выборка[ИмяВедущегоИзмерения]);
		ПараметрыСтроки.Вставить("КлючЗаписи",
			РегистрыСведений[ИмяРегистра].СоздатьКлючЗаписи(Новый Структура("Период," + ИмяВедущегоИзмерения, Выборка.Период, Выборка[ИмяВедущегоИзмерения])));
		ОбластьСтрока.Параметры.Заполнить(ПараметрыСтроки);
		СтруктураРезультата.Результат.Вывести(ОбластьСтрока);
	КонецЦикла;
		
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюВестиУчетПоОрганизациям(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.Представление КАК Представление
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	НЕ Организации.Предопределенный";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Организации'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета по нескольким организациям необходимо, 
				|чтобы в информационной базе была только одна организация.
				|И по ней не установлен флаг ""Скрывать в списках, все операции по организации завершены"".'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаОрганизация",
			ПараметрыШапки);
			
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюИспользоватьКадровыйУчет(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Выборка = РезультатЗапросаКадровыеДокументы(Ложь).Выбрать();	
	Если Выборка.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок",
			НСтр("ru = 'Кадровые документы'"));
		ПарамерыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения кадрового учета удалите кадровые документы.
			|После отключения потребуется заполнить кадровые данные сотрудников.'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСПояснением",
			ПарамерыШапки);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюРаботыНаНеполнуюСтавку(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Выборка = РезультатЗапросаНеполныйРабочийДень(Ложь).Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок",
			НСтр("ru = 'Работа на неполную ставку'"));
		ПарамерыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета работников на неполной ставке удалите данные о сотрудниках'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСотрудник",
			ПарамерыШапки);
		
	КонецЕсли;
	
	Выборка = РезультатЗапросаКадровыеДокументыНеполныйРабочийДень(Ложь).Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок",
			НСтр("ru = 'Кадровые документы сотрудников на неполной ставке'"));
		ПарамерыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета работников на неполной ставке удалите кадровые документы сотрудников'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСПояснением",
			ПарамерыШапки);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюИспользоватьНачисленияПоДоговорам(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Выборка = РезультатЗапросаДоговораГПХ(Ложь).Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок",
			НСтр("ru = 'Договоры ГПХ'"));
		ПарамерыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета договоров ГПХ удалите договоры сотрудников'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСПояснением",
			ПарамерыШапки);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПоддержкаСовместительства(Параметры, АдресХранилища) Экспорт

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Выборка = РезультатЗапросаСовместительство(Ложь).Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок",
			НСтр("ru = 'Совместительство'"));
		ПарамерыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения учета совместительства удалите данные о сотрудниках'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСотрудник",
			ПарамерыШапки);
		
	КонецЕсли;
	
	Выборка = РезультатЗапросаКадровыеДокументыСовместителей(Ложь).Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок",
			НСтр("ru = 'Кадровые документы совместителей'"));
		ПарамерыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения совместительства удалите кадровые документы совместителей'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСПояснением",
			ПарамерыШапки);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюДенежныеДокументы(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииДенежныеДокументы(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПлатежныеТребования(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПлатежныеТребования(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюМалоценноеОборудование(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииМалоценноеОборудование(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюФакторинговыеОперации(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииФакторинговыеОперации(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюВыплатыСамозанятым(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииВыплатыСамозанятым(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюКонтролируемыеСделки(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииКонтролируемыеСделки(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюНалоговыйАгентПоНДС(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииНалоговыйАгентПоНДС(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюОбратноеНачислениеНДС(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииОбратноеНачислениеНДС(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюОтгрузкаБезПереходаПраваСобственности(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииОтгрузкаБезПереходаПраваСобственности(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПакетноеВыставлениеАктов(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПакетноеВыставлениеАктов(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюНачислениеНДССМежценовойРазницы(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииНачислениеНДССМежценовойРазницы(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПодарочныеСертификаты(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПодарочныеСертификаты(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюАлкогольнаяПродукция(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииАлкогольнаяПродукция(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПродажаТоваровУслугКомитентов(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПродажаТоваровУслугКомитентов(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПродажаТоваровУслугЧерезКомиссионеров(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПродажаТоваровУслугЧерезКомиссионеров(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПриобретениеТоваровУслугДляКомитентов(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПриобретениеТоваровУслугДляКомитентов(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПриобретениеТоваровУслугЧерезКомиссионеров(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПриобретениеТоваровУслугЧерезКомиссионеров(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюИмпортТоваров(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииИмпортТоваров(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюЭкспортТоваров(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииЭкспортТоваров(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюМаркировкаМеховыхИзделий(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииМаркировкаМеховыхИзделий(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюИспользоватьМаркировку(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииИспользоватьМаркировку(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюКомплексныйУчетПроизводства(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииКомплексныйУчетПроизводства(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюЛизинг(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииЛизинг(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПредметыАренды(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПредметыАренды(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюКомпонентыАмортизации(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииКомпонентыАмортизации(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюВедетсяУчетНМА(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииНематериальныеАктивы(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюСотрудники(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииСотрудники(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюВалютныйКонтроль(Параметры, АдресХранилища) Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаУчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров(Ложь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок",
			НСтр("ru = 'Объекты валютного контроля'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения валютного контроля очистите поля: ""Код валютной операции"" и ""Уникальный номер контракта""'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаОбъектыВалютногоКонтроля",
			ПараметрыШапки);
			
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюИспользоватьВоинскийУчет(Параметры, АдресХранилища) Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Выборка = РезультатЗапросаДокументыВоинскогоУчета(Ложь).Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок",
			НСтр("ru = 'Воинский учет'"));
		ПарамерыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения воинского учета удалите документы.'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСПояснением",
			ПарамерыШапки);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюРозничнаяТорговля(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииРозничнаяТорговля(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры 

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюИспользоватьПродажиЧерезМаркетплейс(Параметры, АдресХранилища) Экспорт
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииИспользоватьПродажиЧерезМаркетплейс(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);
	
КонецПроцедуры 

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПредоплатаВРознице(Параметры, АдресХранилища) Экспорт

	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииПредоплатыВРознице(УсловияДоступностиОбъектов);
	ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища);

КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюИспользоватьЛиды(Параметры, АдресХранилища) Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Выборка = РезультатЗапросаОбъектыИспользоватьЛиды(Ложь).Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПарамерыШапки = Новый Структура;
		
		ПарамерыШапки.Вставить("Заголовок", ФункциональностьПрограммыКлиентСервер.ПредставлениеРазделаЛиды());
		ПарамерыШапки.Вставить("Пояснение", НСтр("ru = 'Для отключения опции удалите объекты:'"));
		
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСсылкаНаОбъекты",
			ПарамерыШапки);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Процедура СформироватьСписокОбъектовПрепятствующихОтключениюПоступлениеНаНесколькоСкладов(Параметры, АдресХранилища) Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаИспользуютсяПоступленияНаНесколькоСкладов();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		
		ПараметрыШапки = Новый Структура;
		
		ПараметрыШапки.Вставить("Заголовок", НСтр("ru = 'Документы поступления на несколько складов'"));
		ПараметрыШапки.Вставить("Пояснение",
			НСтр("ru = 'Для отключения возможности поступления на несколько складов необходимо указать склад в шапке и перепровести документы поступления'"));
			
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"ШапкаСсылкаНаОбъекты",
			ПараметрыШапки);
			
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикОбновленияИнформационнойБазыБП

// Включает полную функциональность после начала интерактивной работы пользователя в программе,
// например, при обновлении с другой программы с сохранением накопленных данных.
//
Процедура УстановитьПолнуюФункциональность() Экспорт
	
	СписокФункциональности = СписокВсейФункциональности();
	
	ИзменитьСписокФункциональности(СписокФункциональности, Истина);
	
КонецПроцедуры

// Включает полную функциональность до начала интерактивной работы пользователя в программе
//
Процедура УстановитьПолнуюФункциональностьНачалоРаботы() Экспорт
	
	УстановитьПолнуюФункциональность();
	
	// Поскольку пользователь не включает функциональность явно, то сложившийся после включения всей функциональности
	// порядок элементов в классификаторах не имеет для него оправдания.
	// Поэтому выстроим элементы в порядке, заданном классификаторами.
	КлассификаторыДоходовРасходов.УстановитьПорядокЭлементовПриНачалеРаботы(Справочники.СтатьиЗатрат);
	КлассификаторыДоходовРасходов.УстановитьПорядокЭлементовПриНачалеРаботы(Справочники.ПрочиеДоходыИРасходы);
	
КонецПроцедуры

Процедура ЗаполнитьКонстантуАлкогольнаяПродукция() Экспорт
	
	ВедетсяРозничнаяТорговля = Константы.ВедетсяРозничнаяТорговля.Получить();
	
	МенеджерКонстанты = Константы.ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива.СоздатьМенеджерЗначения();
	МенеджерКонстанты.Значение = ВедетсяРозничнаяТорговля;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
	
КонецПроцедуры

Процедура УстановитьКонстантуИспользоватьПланированиеПлатежейОтПокупателей() Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерКонстанты = Константы.ИспользоватьПланированиеПлатежейОтПокупателей.СоздатьМенеджерЗначения();
	
	Если ОбщегоНазначенияБП.ЭтоВерсияСОсновнойФункциональностью() Тогда
		
		МенеджерКонстанты.Значение = Ложь;
		
	Иначе
		
		МенеджерКонстанты.Значение = Истина;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
	
КонецПроцедуры

Процедура ЗаполнитьКонстантуИнкассация() Экспорт
	
	ИнкассацияИспользуется = НЕ ОтказСнятьИспользоватьИнкассацию();
	ЭтоБазоваяВерсияКонфигурации = СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации();
	
	ИспользоватьИнкассацию = Константы.ИспользоватьИнкассацию.СоздатьМенеджерЗначения();
	ИспользоватьИнкассацию.Значение = ИнкассацияИспользуется ИЛИ НЕ ЭтоБазоваяВерсияКонфигурации;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(ИспользоватьИнкассацию);
	
КонецПроцедуры

Процедура УстановитьКонстантуФакторинг() Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФакторингИспользуется = ОтказСнятьОсуществляетсяДеятельностьПоОперациямФакторинга();
	ЭтоВерсияСОновнойФункциональностью = ОбщегоНазначенияБП.ЭтоВерсияСОсновнойФункциональностью();
	
	ИспользоватьФакторинг = Константы.ИспользоватьФакторинг.СоздатьМенеджерЗначения();
	ИспользоватьФакторинг.Значение = ФакторингИспользуется ИЛИ НЕ ЭтоВерсияСОновнойФункциональностью;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(ИспользоватьФакторинг, Истина, Истина);
	
	Если ИспользоватьФакторинг.Значение Тогда
		НастроитьФункциональностьИспользоватьФакторинг(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКонстантуРасширенныйФункционал() Экспорт
	
	Если Не ОбщегоНазначения.РазделениеВключено() И ВариантыПриложений.ЭтоВерсияКОРП() Тогда
		Константы.РасширенныйФункционал.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКонстантуВедетсяУчетУслугИностранныхИнтернетКомпаний() Экспорт
	
	ЭтоВерсияСОновнойФункциональностью = ОбщегоНазначенияБП.ЭтоВерсияСОсновнойФункциональностью();
	Если НЕ ЭтоВерсияСОновнойФункциональностью Тогда
		МенеджерКонстанты = Константы.ВедетсяУчетУслугИностранныхИнтернетКомпаний.СоздатьМенеджерЗначения();
		МенеджерКонстанты.Значение = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.УчетАгентскогоНДС
	|	И ДоговорыКонтрагентов.ВидАгентскогоДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.НерезидентЭлектронныеУслуги)";
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		МенеджерКонстанты = Константы.ВедетсяУчетУслугИностранныхИнтернетКомпаний.СоздатьМенеджерЗначения();
		МенеджерКонстанты.Значение = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКонстантуВедетсяУчетМалоценки() Экспорт
	
	ВедетсяУчетСпецодеждыСпецоснастки = Константы.ВедетсяУчетСпецодеждыСпецоснастки.Получить();
	
	МенеджерКонстанты = Константы.ВедетсяУчетМалоценки.СоздатьМенеджерЗначения();
	МенеджерКонстанты.Значение = ВедетсяУчетСпецодеждыСпецоснастки;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
	
КонецПроцедуры

// Обработчик обновления информационной базы.
// Устанавливает константы ВестиУчетПоОднойОрганизацииПростойИнтерфейс, ВестиУчетПоНесколькимОрганизациямПростойИнтерфейс
// ВестиУчетПоОднойОрганизацииПолныйИнтерфейс, ВестиУчетПоНесколькимОрганизациямПолныйИнтерфейс
//
Процедура ЗаполнитьКонстантыВестиУчетПоОрганизациям() Экспорт
	
	ВестиУчетПоОрганизациям = Константы.ВестиУчетПоОрганизациям.Получить();
	Константы.ВестиУчетПоОрганизациям.УстановитьЗначенияСвязанныхКонстант(ВестиУчетПоОрганизациям, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ВернутьМассивИменКонстант

// Формирует массив имен констант используемых для управления функциональностью
//
Функция СписокВсейФункциональности(ОписаниеФункциональности = Неопределено) Экспорт
	
	МассивИменКонстант = Новый Массив;
	
	Если ОписаниеФункциональности = Неопределено Тогда
		ОписаниеФункциональности = ОписаниеФункциональности();
	КонецЕсли;
	
	Отбор = Новый Структура("Доступна", Истина);
	Для Каждого Функциональность Из ОписаниеФункциональности.НайтиСтроки(Отбор) Цикл
		МассивИменКонстант.Добавить(Функциональность.Имя);
	КонецЦикла;
	
	Возврат МассивИменКонстант;
	
КонецФункции

Функция ФункциональностьНедоступнаяВПростомИнтерфейсе(ОписаниеФункциональности)
	
	ФункциональностьНедоступнаяВПростомИнтерфейсе = Новый Массив;
	
	Отбор = Новый Структура("ДоступнаВПростомИнтерфейсе,Доступна", Ложь, Истина);
	Для Каждого Функциональность Из ОписаниеФункциональности.НайтиСтроки(Отбор) Цикл
		ФункциональностьНедоступнаяВПростомИнтерфейсе.Добавить(Функциональность.Имя);
	КонецЦикла;
	
	Возврат ФункциональностьНедоступнаяВПростомИнтерфейсе;
	
КонецФункции

#КонецОбласти

#Область ПодпискаНаСобытиеИзменениеКонстантыСинхронизацияДанных

// При установке флага Синхронизация Данных, включаем зависимые функциональные возможности
Процедура УстановитьКонстантыЗависимыеОтОбменов() Экспорт
	
	Если Константы.ИспользоватьСинхронизациюДанных.Получить() Тогда
		
		ОписаниеФункциональности = ОписаниеФункциональности();
		
		ФункциональностьДляОбмена = Новый Массив;
		
		Отбор = Новый Структура("ТребуетсяДляОбменов,Доступна", Истина, Истина);
		Для Каждого СтрокаФункциональности Из ОписаниеФункциональности.НайтиСтроки(Отбор) Цикл
			ФункциональностьДляОбмена.Добавить(СтрокаФункциональности.Имя);
		КонецЦикла;
		
		ИзменитьСписокФункциональности(ФункциональностьДляОбмена, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыЗапускаПроверокКонстантНаВозможностьОтключения

Функция ОтказСнятьИспользованиеКонстанты(ИмяКонстанты, ОписаниеФункциональности)
	
	Если ФункциональностьВлияетНаСинхронизациюДанных(ИмяКонстанты, ОписаниеФункциональности)
		И ИспользуетсяСинхронизацияДанных() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат ОтказСнятьИспользованиеКонстантыПоДаннымБазы(ИмяКонстанты);
	
КонецФункции

Функция ОтказСнятьИспользованиеКонстантыПоДаннымБазы(ИмяКонстанты) Экспорт

	ОтказСнять = Истина;
	
	Если ИмяКонстанты = "ВедетсяУчетНМА" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетНМА();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетОсновныхСредств" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетОсновныхСредств();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетСдачиИмуществаВАренду" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетСдачиИмуществаВАренду();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетПредметовАренды" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетПредметовАренды();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетЛизинговогоИмущества" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетЛизинговогоИмущества();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетСпецодеждыСпецоснастки" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетСпецодеждыСпецоснастки();
	ИначеЕсли ИмяКонстанты = "ВедетсяОтгрузкаБезПереходаПраваСобственности" Тогда
		ОтказСнять = ОтказСнятьВедетсяОтгрузкаБезПереходаПраваСобственности();
	ИначеЕсли ИмяКонстанты = "ИспользоватьКомплектациюНоменклатуры" Тогда
		ОтказСнять = ОтказСнятьИспользоватьКомплектациюНоменклатуры();
	ИначеЕсли ИмяКонстанты = "ИспользоватьОплатуПоПлатежнымКартам" Тогда
		ОтказСнять = ОтказСнятьИспользоватьОплатуПоПлатежнымКартам();
	ИначеЕсли ИмяКонстанты = "ИспользоватьОплатуСертификатамиНСПК" Тогда
		ОтказСнять = ОтказСнятьИспользоватьОплатуСертификатамиНСПК();
	ИначеЕсли ИмяКонстанты = "ИспользоватьПлатежиПоРеестрам" Тогда
		ОтказСнять = ОтказСнятьИспользоватьПлатежиПоРеестрам();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетДенежныхДокументов" Тогда
		ОтказСнять = ОтказСнятьИспользоватьДенежныеДокументы();
	ИначеЕсли ИмяКонстанты = "ВедетсяПроизводственнаяДеятельность" Тогда
		ОтказСнять = ОтказСнятьВедетсяПроизводственнаяДеятельность();
	ИначеЕсли ИмяКонстанты = "ИспользуетсяКалькуляцияРасходов" Тогда
		ОтказСнять = Ложь;
	ИначеЕсли ИмяКонстанты = "ИспользоватьВыпускПродукции" Тогда
		ОтказСнять = ОтказСнятьИспользоватьВыпускПродукции();
	ИначеЕсли ИмяКонстанты = "ОсуществляетсяРеализацияТоваровУслугКомитентов" Тогда
		ОтказСнять = ОтказСнятьОсуществляетсяРеализацияТоваровУслугКомитентов();
	ИначеЕсли ИмяКонстанты = "ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров" Тогда
		ОтказСнять = ОтказСнятьОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров();
	ИначеЕсли ИмяКонстанты = "ОсуществляетсяЗакупкаТоваровУслугДляКомитентов" Тогда
		ОтказСнять = ОтказСнятьОсуществляетсяЗакупкаТоваровУслугДляКомитентов();
	ИначеЕсли ИмяКонстанты = "ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров" Тогда
		ОтказСнять = ОтказСнятьОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров();
	ИначеЕсли ИмяКонстанты = "ВедетсяРозничнаяТорговля" Тогда
		ОтказСнять = ОтказСнятьВедетсяРозничнаяТорговля();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетВозвратнойТары" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетВозвратнойТары();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетПоПутевымЛистам" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетПоПутевымЛистам();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетИмпортныхТоваров" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетИмпортныхТоваров();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетИмпортаТоваров" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетИмпортаТоваров();
	ИначеЕсли ИмяКонстанты = "ВестиУчетПрослеживаемыхТоваров" Тогда
		ОтказСнять = ОтказСнятьВестиУчетПрослеживаемыхТоваров();
	ИначеЕсли ИмяКонстанты = "ИспользоватьПакетноеВыставлениеАктов" Тогда
		ОтказСнять = ОтказСнятьИспользоватьПакетноеВыставлениеАктов();
	ИначеЕсли ИмяКонстанты = "ИспользоватьНачислениеНДССМежценовойРазницы" Тогда
		ОтказСнять = ОтказСнятьИспользоватьНачислениеНДССМежценовойРазницы();
	ИначеЕсли ИмяКонстанты = "ИспользоватьПлатежныеТребования" Тогда
		ОтказСнять = ОтказСнятьИспользоватьПлатежныеТребования();
	ИначеЕсли ИмяКонстанты = "УправлениеЗачетомАвансовПогашениемЗадолженности" Тогда
		ОтказСнять = ОтказСнятьУправлениеЗачетомАвансовПогашениемЗадолженности();
	ИначеЕсли ИмяКонстанты = "ИспользоватьУведомленияОКонтролируемыхСделках" Тогда
		ОтказСнять = ОтказСнятьИспользоватьУведомленияОКонтролируемыхСделках();
	ИначеЕсли ИмяКонстанты = "ИспользуетсяГособоронзаказ" Тогда
		ОтказСнять = ОтказСнятьИспользуетсяГособоронзаказ();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива();
	ИначеЕсли ИмяКонстанты = "ИспользоватьКорректировочныеДокументы" Тогда
		ОтказСнять = ОтказСнятьИспользоватьКорректировочныеДокументы();
	ИначеЕсли ИмяКонстанты = "ИспользоватьФискальныйРегистратор" Тогда
		ОтказСнять = ОтказСнятьИспользоватьФискальныйРегистратор();
	ИначеЕсли ИмяКонстанты = "ИспользоватьПередачуПрав" Тогда
		ОтказСнять = Ложь;
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетМалоценки" Тогда
		ОтказСнять = Ложь;
	ИначеЕсли ИмяКонстанты = "ИспользоватьВыплатыСамозанятым" Тогда
		ОтказСнять = ОтказСнятьИспользоватьВыплатыСамозанятым();
	ИначеЕсли ИмяКонстанты = "ИспользуютсяПодарочныеСертификаты" Тогда
		ОтказСнять = ОтказСнятьИспользуютсяПодарочныеСертификаты();
	ИначеЕсли ИмяКонстанты = "ИспользоватьИнкассацию" Тогда
		ОтказСнять = ОтказСнятьИспользоватьИнкассацию();
	ИначеЕсли ИмяКонстанты = "ИспользоватьДоставкуАвтотранспортом" Тогда
		ОтказСнять = ОтказСнятьИспользоватьДоставкуАвтотранспортом();
	ИначеЕсли ИмяКонстанты = "ИспользоватьДоставкуТранспортнойКомпанией" Тогда
		ОтказСнять = ОтказСнятьИспользоватьДоставкуТранспортнойКомпанией();
	ИначеЕсли ИмяКонстанты = "ИспользоватьПродажиЧерезМаркетплейс" Тогда
		ОтказСнять = ОтказСнятьИспользоватьПродажиЧерезМаркетплейс();
	ИначеЕсли ИмяКонстанты = "ИспользоватьОбменСИнтернетМагазином" Тогда
		ОтказСнять = Ложь;  
	ИначеЕсли ИмяКонстанты = "ИспользоватьСогласованиеПлатежей" Тогда
		ОтказСнять = Ложь;
	ИначеЕсли ИмяКонстанты = "ИспользоватьПланированиеПлатежейПоставщикам" Тогда
		ОтказСнять = Ложь;
	ИначеЕсли ИмяКонстанты = "ИспользоватьПланированиеПлатежейОтПокупателей" Тогда
		ОтказСнять = Ложь;
	ИначеЕсли ИмяКонстанты = "ИспользоватьПредоплатыВРознице" Тогда
		ОтказСнять = ОтказСнятьИспользоватьПредоплатыВРознице();
	ИначеЕсли ИмяКонстанты = "ВестиУчетПоДоговорам" Тогда
		ОтказСнять = ОтказСнятьВестиУчетПоДоговорам();
	ИначеЕсли ИмяКонстанты = "ИсполняютсяОбязанностиНалоговогоАгентаПоНДС" Тогда
		ОтказСнять = ОтказСнятьИсполняютсяОбязанностиНалоговогоАгентаПоНДС();
	ИначеЕсли ИмяКонстанты = "ОбратноеНачислениеНДС" Тогда
		ОтказСнять = ОтказСнятьОбратноеНачислениеНДС();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетУслугИностранныхИнтернетКомпаний" Тогда
		ОтказСнять = ОтказСнятьЭлектронныеУслугиИностранцев();
	ИначеЕсли ИмяКонстанты = "УчетОбеспеченийОбязательствИПлатежей" Тогда
		ОтказСнять = ОтказСнятьУчетОбеспеченийОбязательствИПлатежей();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетТаможенныхДекларацийЭкспорт" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетТаможенныхДекларацийЭкспорт();
	ИначеЕсли ИмяКонстанты = "ВестиУчетМаркировкиПродукцииВГИСМ" Тогда
		ОтказСнять = ОтказСнятьВестиУчетМаркировкиПродукцииВГИСМ();
	ИначеЕсли ИмяКонстанты = "ЗагрузкаДанныхИзТиС" Тогда
		ОтказСнять = Ложь;
	ИначеЕсли ИмяКонстанты = "ИспользоватьФакторинг" Тогда
		ОтказСнять = ОтказСнятьОсуществляетсяДеятельностьПоОперациямФакторинга();
	ИначеЕсли ИмяКонстанты = "ИспользоватьСтатьиДвиженияДенежныхСредств" Тогда
		ОтказСнять = ОтказСнятьИспользоватьСтатьиДвиженияДенежныхСредств();
	ИначеЕсли ИмяКонстанты = "ВестиУчетРасчетовСКонтрагентами" Тогда
		ОтказСнять = ОтказСнятьВестиУчетРасчетовСКонтрагентами();
	ИначеЕсли ИмяКонстанты = "ВестиУчетПоПодразделениям" Тогда
		ОтказСнять = ОтказСнятьВестиУчетПоПодразделениям();
	ИначеЕсли ИмяКонстанты = "РасширенныйФункционал" Тогда
		ОтказСнять = ОтказСнятьРасширенныйФункционал();
	ИначеЕсли ИмяКонстанты = "ВестиУчетПоОрганизациям" Тогда
		ОтказСнять = ОтказСнятьВестиУчетПоОрганизациям();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетЗарплатыКадров" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетЗарплатыКадров();
	ИначеЕсли ИмяКонстанты = "ИспользоватьКадровыйУчет" Тогда
		ОтказСнять = ОтказСнятьИспользоватьКадровыйУчет();
	ИначеЕсли ИмяКонстанты = "ИспользоватьВоинскийУчет" Тогда
		ОтказСнять = ОтказСнятьИспользоватьВоинскийУчет();
	ИначеЕсли ИмяКонстанты = "ИспользоватьРаботуНаНеполнуюСтавку" Тогда
		ОтказСнять = ОтказСнятьИспользоватьРаботуНаНеполнуюСтавку();
	ИначеЕсли ИмяКонстанты = "ПоддержкаСовместительства" Тогда
		ОтказСнять = ОтказСнятьПоддержкаСовместительства();
	ИначеЕсли ИмяКонстанты = "ИспользоватьНачисленияПоДоговорам" Тогда
		ОтказСнять = ОтказСнятьИспользоватьНачисленияПоДоговорам();
	ИначеЕсли ИмяКонстанты = "ИспользоватьПодконтрольныеТоварыККТ" Тогда
		ОтказСнять = ОтказСнятьИспользоватьПодконтрольныеТоварыККТ();
	ИначеЕсли ИмяКонстанты = "КомпонентыАмортизацииОС" Тогда
		ОтказСнять = ОтказСнятьКомпонентыАмортизацииОС();
	ИначеЕсли ИмяКонстанты = "ВедетсяУчетГрупповыхОС" Тогда
		ОтказСнять = ОтказСнятьВедетсяУчетГрупповыхОС();
	ИначеЕсли ИмяКонстанты = "ВыполняютсяСтроительныеРаботы" Тогда
		ОтказСнять = ОтказСнятьВыполняютсяСтроительныеРаботы();
	ИначеЕсли ИмяКонстанты = "УчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров" Тогда
		ОтказСнять = ОтказСнятьУчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров();
	ИначеЕсли ИмяКонстанты = "ИспользоватьМаркировку" Тогда
		ОтказСнять = Ложь;
	ИначеЕсли ИмяКонстанты = "ИмпортПодакцизныхТоваров" Тогда
		ОтказСнять = ОтказСнятьИмпортПодакцизныхТоваров();
	ИначеЕсли ИмяКонстанты = "ПроизводствоПодакцизныхТоваров" Тогда
		ОтказСнять = ОтказСнятьПроизводствоПодакцизныхТоваров();
	ИначеЕсли ИмяКонстанты = ФункциональностьПрограммыВызовСервера.НаименованиеОпцииЛиды() Тогда
		ОтказСнять = ОтказСнятьИспользоватьЛиды();
	ИначеЕсли ИмяКонстанты = "ПоступлениеНаНесколькоСкладов" Тогда
		ОтказСнять = ОтказСнятьПоступлениеНаНесколькоСкладов();
	КонецЕсли;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ФункциональностьВлияетНаСинхронизациюДанных(ИмяКонстанты, ОписаниеФункциональности)
	
	Отбор = Новый Структура();
	Отбор.Вставить("Доступна", Истина);
	Отбор.Вставить("ТребуетсяДляОбменов", Истина);
	Отбор.Вставить("Имя", ИмяКонстанты);
	
	СтрокиФункциональности = ОписаниеФункциональности.НайтиСтроки(Отбор);
	
	Возврат СтрокиФункциональности.Количество() > 0;
	
КонецФункции

Функция ИспользуемаяФункциональность(СписокФункциональности, ОписаниеФункциональности)
	
	ИспользуемаяФункциональность = Новый Массив;
	
	Для Каждого ИмяФункциональности Из ВключеннаяФункциональность(СписокФункциональности) Цикл
		
		Если ОтказСнятьИспользованиеКонстанты(ИмяФункциональности, ОписаниеФункциональности) Тогда
			ИспользуемаяФункциональность.Добавить(ИмяФункциональности);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИспользуемаяФункциональность;
	
КонецФункции

#КонецОбласти

#Область ТарификацияФункциональности

Функция ЕстьИспользуемаяФункциональностьБезТарифа() Экспорт
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	
	ВключеннаяНедоступнаяФункциональность = ВключеннаяНедоступнаяФункциональность(ОписаниеФункциональности);
	
	Возврат ВключеннаяНедоступнаяФункциональность.Количество() > 0;
	
КонецФункции

Функция ОграниченияФункциональностиПоТарифу(ОписаниеФункциональности) Экспорт
	
	Ограничения = Новый Структура;
	Ограничения.Вставить("ФункциональностьБезСвободныхЛицензий", НовыйОписаниеФункциональностиБезСвободныхЛицензий());
	Ограничения.Вставить("ФункциональностьНедоступнаяПоТарифу", Новый Массив);
	
	РазрешенДополнительныйФункционал = ТарификацияБП.РазрешенДополнительныйФункционал();
	
	ФункциональностьДоступнаяСНО = ТарификацияБП.ФункциональностьДоступнаяСНО();
	
	Если Не РазрешенДополнительныйФункционал Тогда
		Для Каждого СтрокаФункциональности Из ОписаниеФункциональности Цикл
			Если ФункциональностьДоступнаяСНО.Найти(СтрокаФункциональности.Имя) = Неопределено Тогда
				Ограничения.ФункциональностьНедоступнаяПоТарифу.Добавить(СтрокаФункциональности.Имя);
			КонецЕсли;
		КонецЦикла;
		Возврат Ограничения;
	КонецЕсли;
	
	РазрешенПолныйИнтерфейс  = ТарификацияБП.РазрешенПолныйИнтерфейс();
	
	Если Не РазрешенПолныйИнтерфейс Тогда
		Для Каждого СтрокаФункциональности Из ОписаниеФункциональности Цикл
			Если Не СтрокаФункциональности.ДоступнаВПростомИнтерфейсе
				И ФункциональностьДоступнаяСНО.Найти(СтрокаФункциональности.Имя) = Неопределено Тогда
				Ограничения.ФункциональностьНедоступнаяПоТарифу.Добавить(СтрокаФункциональности.Имя);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ТарификацияБП.РазрешенУчетЗарплаты() Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Ограничения.ФункциональностьНедоступнаяПоТарифу,
			ВсяФункциональностьПоРазделу("ЗарплатаКадры", ОписаниеФункциональности),
			Истина);
	КонецЕсли;
	
	Если Не ТарификацияБПВызовСервераПовтИсп.РазрешеноВзаимодействиеСЛидами() Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Ограничения.ФункциональностьНедоступнаяПоТарифу,
			ВсяФункциональностьПоРазделу(ФункциональностьПрограммыКлиентСервер.РазделЛиды(), ОписаниеФункциональности),
			Истина);
	КонецЕсли;
	
	Если Не ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		Возврат Ограничения;
	КонецЕсли;
	
	ТарифныеОпцииФункциональности = ТарифныеОпцииФункциональности(ОписаниеФункциональности);
	
	ДоступныеЛицензииФункциональности = ДоступныеЛицензииТарифныхОпцийФункциональности(ТарифныеОпцииФункциональности);
	
	ЗаполнитьОграниченияФункциональностиПоТарифу(ОписаниеФункциональности, Ограничения, ДоступныеЛицензииФункциональности);
	
	Возврат Ограничения;
	
КонецФункции

Процедура ДополнитьКэшЛицензийНаВозможностьИспользованияФункциональности(СписокФункциональности, ОписаниеФункциональности, КэшЛицензий)
	
	ФункциональностьДляПроверки = Новый Массив;
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		Если КэшЛицензий.Получить(ИмяФункциональности) = Неопределено Тогда
			ФункциональностьДляПроверки.Добавить(ИмяФункциональности);
		КонецЕсли;
	КонецЦикла;
	
	Если ФункциональностьДляПроверки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТарификацияБП.РазрешенДополнительныйФункционал() Тогда
		Для Каждого ИмяФункциональности Из ФункциональностьДляПроверки Цикл
			КэшЛицензий.Вставить(ИмяФункциональности, НовыйДоступныеЛицензииТарифныхОпцийФункциональности());
		КонецЦикла;
	ИначеЕсли Не ТарификацияБП.РазрешенПолныйИнтерфейс() Тогда
		Для Каждого ИмяФункциональности Из ФункциональностьДляПроверки Цикл
			СтрокаОписания = ОписаниеФункциональности.Найти(ИмяФункциональности, "Имя");
			Если СтрокаОписания <> Неопределено И Не СтрокаОписания.ДоступнаВПростомИнтерфейсе Тогда
				КэшЛицензий.Вставить(ИмяФункциональности, НовыйДоступныеЛицензииТарифныхОпцийФункциональности());
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ТарификацияБП.РазрешенУчетЗарплаты() Тогда
		Для Каждого ИмяФункциональности Из ФункциональностьДляПроверки Цикл
			СтрокаОписания = ОписаниеФункциональности.Найти(ИмяФункциональности, "Имя");
			Если СтрокаОписания <> Неопределено И СтрокаОписания.Раздел = "ЗарплатаКадры" Тогда
				КэшЛицензий.Вставить(ИмяФункциональности, НовыйДоступныеЛицензииТарифныхОпцийФункциональности());
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		Возврат;
	КонецЕсли;
	
	ТарифныеОпцииФункциональности = ТарифныеОпцииФункциональности(ОписаниеФункциональности, ФункциональностьДляПроверки);
	
	ДоступныеЛицензииФункциональности = ДоступныеЛицензииТарифныхОпцийФункциональности(ТарифныеОпцииФункциональности);
	
	Для Каждого ИмяФункциональности Из ФункциональностьДляПроверки Цикл
		
		Если КэшЛицензий.Получить(ИмяФункциональности) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяТарифнойОпции = ТарифныеОпцииФункциональности.Получить(ИмяФункциональности);
		Если Не ЗначениеЗаполнено(ИмяТарифнойОпции) Тогда
			// Функциональность не тарифицируется, поэтому включать ее можно безусловно, она входит в любой тариф.
			ДоступныеЛицензии = НовыйДоступныеЛицензииТарифныхОпцийФункциональности();
			ДоступныеЛицензии.ВходитВТариф = Истина;
			ДоступныеЛицензии.ЕстьСвободныеЛицензии = Истина;
			КэшЛицензий.Вставить(ИмяФункциональности, ДоступныеЛицензии);
			Продолжить;
		КонецЕсли;
		
		КэшЛицензий.Вставить(ИмяФункциональности, 
			ДоступныеЛицензииФункциональности.Получить(ИмяТарифнойОпции));
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТарифныеОпцииФункциональности(ОписаниеФункциональности, СписокФункциональности = Неопределено)
	
	ТарифныеОпцииФункциональности = Новый Соответствие;
	
	Отбор = Новый Структура("Доступна", Истина);
	Для Каждого Функциональность Из ОписаниеФункциональности.НайтиСтроки(Отбор) Цикл
		
		Если СписокФункциональности <> Неопределено
			И СписокФункциональности.Найти(Функциональность.Имя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Функциональность.ИмяТарифнойОпции) Тогда
			ТарифныеОпцииФункциональности.Вставить(Функциональность.Имя, Функциональность.ИмяТарифнойОпции);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТарифныеОпцииФункциональности;
	
КонецФункции

Функция ДоступныеЛицензииТарифныхОпцийФункциональности(ТарифныеОпцииФункциональности)
	
	ДоступныеЛицензииТарифныхОпцийФункциональности = Новый Соответствие();
	
	// Проверим, что каждая тарифная опция доступна для пользователя.
	// Результат поместим в структуру:
	//   - ВходитВТариф - Истина, если опция входит в тариф.
	//   - ЕстьСвободныеЛицензии - Если истина, есть свободная лицензия для захвата (либо лицензия уже захвачена).
	//   - ДоступноЛицензий - Заполняется только в том случае, если ЕстьСвободныеЛицензии = Истина.
	//                        Содержит общее количество доступных лицензий без учета используемых.
	
	Для Каждого ТарифнаяОпцияФункциональности Из ТарифныеОпцииФункциональности Цикл
		
		ПроверяемаяТарифнаяОпция = ТарифнаяОпцияФункциональности.Значение;
		
		Если ТарификацияБП.ЗарегистрированаЛицензияУслугиФункциональности(ПроверяемаяТарифнаяОпция) Тогда
			ДоступныеЛицензии = НовыйДоступныеЛицензииТарифныхОпцийФункциональности();
			ДоступныеЛицензии.ВходитВТариф = Истина;
			ДоступныеЛицензии.ЕстьСвободныеЛицензии = Истина;
			ДоступныеЛицензииТарифныхОпцийФункциональности.Вставить(ПроверяемаяТарифнаяОпция,
				ДоступныеЛицензии);
			Продолжить;
		КонецЕсли;
		
		// Проверим запросом к менеджеру сервиса, что есть свободные лицензии по тарифной опции.
		
		КоличествоЛицензий = ТарификацияБП.КоличествоЛицензийУслугиФункциональности(
			ПроверяемаяТарифнаяОпция);
		
		Если КоличествоЛицензий <> Неопределено
			И КоличествоЛицензий.ДоступноЛицензий <> 0 Тогда
			
			ДоступныеЛицензии = НовыйДоступныеЛицензииТарифныхОпцийФункциональности();
			ДоступныеЛицензии.ВходитВТариф = Истина;
			ДоступныеЛицензии.ЕстьСвободныеЛицензии = КоличествоЛицензий.ДоступноЛицензий = -1
				ИЛИ КоличествоЛицензий.СвободноЛицензий > 0;
			
			Если Не ДоступныеЛицензии.ЕстьСвободныеЛицензии Тогда
				ДоступныеЛицензии.ДоступноЛицензий = КоличествоЛицензий.ДоступноЛицензий;
			КонецЕсли;
			
			ДоступныеЛицензииТарифныхОпцийФункциональности.Вставить(ПроверяемаяТарифнаяОпция,
				ДоступныеЛицензии);
			
		Иначе
			
			// В тарифе нет такой опции.
			ДоступныеЛицензииТарифныхОпцийФункциональности.Вставить(ПроверяемаяТарифнаяОпция,
				НовыйДоступныеЛицензииТарифныхОпцийФункциональности());
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДоступныеЛицензииТарифныхОпцийФункциональности;
	
КонецФункции

Функция НовыйДоступныеЛицензииТарифныхОпцийФункциональности()
	
	ДоступныеЛицензии = Новый Структура;
	ДоступныеЛицензии.Вставить("ВходитВТариф", Ложь);
	ДоступныеЛицензии.Вставить("ЕстьСвободныеЛицензии", Ложь);
	ДоступныеЛицензии.Вставить("ДоступноЛицензий", 0);
	Возврат ДоступныеЛицензии;
	
КонецФункции

Процедура ЗаполнитьОграниченияФункциональностиПоТарифу(ОписаниеФункциональности, Ограничения, ДоступныеЛицензииФункциональности)
	
	Для Каждого Функциональность Из ОписаниеФункциональности Цикл
		
		Если Не ЗначениеЗаполнено(Функциональность.ИмяТарифнойОпции) Тогда
			Продолжить;
		КонецЕсли;
		
		ЛицензииТарифнойОпции = ДоступныеЛицензииФункциональности.Получить(Функциональность.ИмяТарифнойОпции);
		Если ЛицензииТарифнойОпции <> Неопределено
			И ЛицензииТарифнойОпции.ВходитВТариф Тогда
			// Функциональность входит в тариф, но ее использование невозможно, т.к. использовано
			// максимальное количество доступных лицензий.
			Если Не ЛицензииТарифнойОпции.ЕстьСвободныеЛицензии Тогда
				ФункциональностьБезСвободныхЛицензий = Ограничения.ФункциональностьБезСвободныхЛицензий.Добавить();
				ФункциональностьБезСвободныхЛицензий.Имя = Функциональность.Имя;
				ФункциональностьБезСвободныхЛицензий.ДоступноЛицензий = ЛицензииТарифнойОпции.ДоступноЛицензий;
			КонецЕсли;
		Иначе
			// Функциональность не входит в тариф.
			Ограничения.ФункциональностьНедоступнаяПоТарифу.Добавить(Функциональность.Имя);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВключеннаяНедоступнаяФункциональность(ОписаниеФункциональности) Экспорт
	
	СписокВсейФункциональности = СписокВсейФункциональности(ОписаниеФункциональности);
	
	ВключеннаяНедоступнаяФункциональность = Новый Соответствие;
	
	РазрешенДополнительныйФункционал = ТарификацияБП.РазрешенДополнительныйФункционал();
	РазрешенПолныйИнтерфейс = ТарификацияБП.РазрешенПолныйИнтерфейс();
	ФункциональностьОграничиваетсяТарифом = ТарификацияБП.ФункциональностьОграничиваетсяТарифом();
	ФункциональностьДоступнаяСНО = ТарификацияБП.ФункциональностьДоступнаяСНО();
	РазрешенУчетЗарплаты = ТарификацияБП.РазрешенУчетЗарплаты();
	
	Для Каждого ИмяФункциональности Из ВключеннаяФункциональность(СписокВсейФункциональности)  Цикл
		
		Если ФункциональностьДоступнаяСНО.Найти(ИмяФункциональности) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Функциональность = СвойстваФункциональности(ИмяФункциональности, ОписаниеФункциональности);
		Если (Не РазрешенДополнительныйФункционал)
			Или (Не РазрешенПолныйИнтерфейс И Не Функциональность.ДоступнаВПростомИнтерфейсе) Тогда
			ВключеннаяНедоступнаяФункциональность.Вставить(ИмяФункциональности, "");
		КонецЕсли;
		
		Если Не РазрешенУчетЗарплаты И Функциональность.Раздел = "ЗарплатаКадры" Тогда
			ВключеннаяНедоступнаяФункциональность.Вставить(ИмяФункциональности, "");
		КонецЕсли;
		
		Если Не ФункциональностьОграничиваетсяТарифом Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Функциональность.ИмяТарифнойОпции) Тогда
			ЛицензияЗарегистрирована = ТарификацияБП.ЗарегистрированаЛицензияУслугиФункциональности(Функциональность.ИмяТарифнойОпции);
			
			Если Не ЛицензияЗарегистрирована Тогда
				ВключеннаяНедоступнаяФункциональность.Вставить(Функциональность.Имя, Функциональность.ИмяТарифнойОпции);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВключеннаяНедоступнаяФункциональность;
	
КонецФункции

Процедура ВыполнитьПопыткуЗахватитьЛицензии(ВключеннаяНедоступнаяФункциональность)
	
	ЗахваченныеЛицензииФункциональности = Новый Массив;
	
	Для Каждого ВключеннаяФункциональность Из ВключеннаяНедоступнаяФункциональность Цикл
		Если Не ЗначениеЗаполнено(ВключеннаяФункциональность.Значение) Тогда
			Продолжить;
		КонецЕсли; 
		ОперацияСЛицензией = ВыполнитьЗахватОтменуЛицензииТарифнойОпции(ВключеннаяФункциональность.Значение, Истина);
		Если ОперацияСЛицензией <> Неопределено И ОперацияСЛицензией.Результат
			И ОперацияСЛицензией.ОперацияПодтверждена Тогда
			ЗахваченныеЛицензииФункциональности.Добавить(ВключеннаяФункциональность.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ИмяФункциональности Из ЗахваченныеЛицензииФункциональности Цикл
		ВключеннаяНедоступнаяФункциональность.Удалить(ИмяФункциональности);
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйОписаниеФункциональностиБезСвободныхЛицензий()
	
	ФункциональностьБезСвободныхЛицензий = Новый ТаблицаЗначений;
	ФункциональностьБезСвободныхЛицензий.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	ФункциональностьБезСвободныхЛицензий.Колонки.Добавить("ДоступноЛицензий", Новый ОписаниеТипов("Число"));
	
	Возврат ФункциональностьБезСвободныхЛицензий;
	
КонецФункции

Функция УстановитьФункциональностьПоОпцииРазрешенПолныйИнтерфейс() Экспорт
	
	Если ТарификацияБП.РазрешенПолныйИнтерфейс() Тогда
		// Включаем опции, для которых есть соответствующие данные, ранее введенные пользователем.
		ВключитьИспользуемуюФункциональность();
		ВыполненоУспешно = Истина;
	Иначе
		// Отключаем недоступные опции.
		ВыполненоУспешно = ОтключитьНедоступнуюФункциональность();
	КонецЕсли;
	
	Возврат ВыполненоУспешно;
	
КонецФункции

Процедура ВключитьИспользуемуюФункциональность()
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	
	ИменаКонстант = Новый Массив;
	Для каждого Функциональность Из ОписаниеФункциональности Цикл
		Если Функциональность.Доступна Тогда
			ИменаКонстант.Добавить(Функциональность.Имя);
		КонецЕсли;
	КонецЦикла;
	
	РезультатПроверки = ПроверитьФункциональностьНаВозможностьОтключения(ИменаКонстант, ОписаниеФункциональности);
	Если Не ЗначениеЗаполнено(РезультатПроверки.НевозможноОтключить) Тогда
		Возврат;
	КонецЕсли;
	ДополнитьРодительскойФункциональностью(РезультатПроверки.НевозможноОтключить);
	
	НаборКонстант = Константы.СоздатьНабор(СтрСоединить(РезультатПроверки.НевозможноОтключить, ","));
	Для каждого ИмяКонстанты Из РезультатПроверки.НевозможноОтключить Цикл
		НаборКонстант[ИмяКонстанты] = Истина;
	КонецЦикла;
	НаборКонстант.Записать();
	
КонецПроцедуры

Функция ОтключитьНедоступнуюФункциональность()
	
	ВыполненоУспешно = Истина;
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	
	ИменаКонстант = Новый Массив;
	ИзменяемыеКонстанты = Новый Массив;
	Для Каждого Функциональность Из ОписаниеФункциональности Цикл
		Если Не Функциональность.Доступна Тогда
			ИменаКонстант.Добавить(Функциональность.Имя);
		КонецЕсли;
		
		Если Не Функциональность.ДоступнаВПростомИнтерфейсе Тогда
			ИменаКонстант.Добавить(Функциональность.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ИменаКонстант) Тогда
		Возврат ВыполненоУспешно;
	КонецЕсли;
	
	РезультатПроверки = ПроверитьФункциональностьНаВозможностьОтключения(ИменаКонстант, ОписаниеФункциональности);
	ДополнитьРодительскойФункциональностью(РезультатПроверки.НевозможноОтключить);
	
	НаборКонстант = Константы.СоздатьНабор(ИменаКонстант);
	НаборКонстант.Прочитать();
	Для каждого ИмяКонстанты Из ИменаКонстант Цикл
		Если НаборКонстант[ИмяКонстанты] = Истина Тогда
			Если РезультатПроверки.НевозможноОтключить.Найти(ИмяКонстанты) <> Неопределено Тогда
				// Невозможно отключить использующуюся функциональность
				ВыполненоУспешно = Ложь;
			Иначе
				ИзменяемыеКонстанты.Добавить(ИмяКонстанты);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ИзменяемыеКонстанты.Количество() > 0 Тогда
		НаборИзменяемыхКонстант = Константы.СоздатьНабор(ИзменяемыеКонстанты);
		Для Каждого ИмяКонстанты Из ИзменяемыеКонстанты Цикл
			НаборИзменяемыхКонстант[ИмяКонстанты] = Ложь;
		КонецЦикла;
		НаборИзменяемыхКонстант.Записать();
	КонецЕсли;
	
	Возврат ВыполненоУспешно;
	
КонецФункции

Функция ОтключитьНедоступнуюФункциональностьЗарплатаКадры() Экспорт
	
	РезультатВыполнения = НовыйРезультатОтключенияНедоступнойФункциональности();
	
	Если ТарификацияБП.РазрешенУчетЗарплаты() Тогда
		Возврат РезультатВыполнения;
	КонецЕсли;
	
	ИменаКонстант = Новый Массив;
	ИзменяемыеКонстанты = Новый Массив;
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	Отбор = Новый Структура;
	Отбор.Вставить("Раздел", "ЗарплатаКадры");
	ФункциональностьЗарплатаКадры = ОписаниеФункциональности.НайтиСтроки(Отбор);
	
	Для Каждого СвойстваФункциональности Из ФункциональностьЗарплатаКадры Цикл
		ИменаКонстант.Добавить(СвойстваФункциональности.Имя);
	КонецЦикла;
	
	Если ИменаКонстант.Количество() = 0 Тогда
		Возврат РезультатВыполнения;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатПроверки = ПроверитьФункциональностьНаВозможностьОтключения(ИменаКонстант, ОписаниеФункциональности);
	ДополнитьРодительскойФункциональностью(РезультатПроверки.НевозможноОтключить);
	
	НаборКонстант = Константы.СоздатьНабор(ИменаКонстант);
	НаборКонстант.Прочитать();
	Для каждого ИмяКонстанты Из ИменаКонстант Цикл
		Если НаборКонстант[ИмяКонстанты] = Истина Тогда
			РезультатВыполнения.ПотребовалосьОтключениефункциональности = Истина;
			Если РезультатПроверки.НевозможноОтключить.Найти(ИмяКонстанты) <> Неопределено Тогда
				// Невозможно отключить использующуюся функциональность
				РезультатВыполнения.ОтключениеВыполненоУспешно = Ложь;
			Иначе
				ИзменяемыеКонстанты.Добавить(ИмяКонстанты);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ИзменяемыеКонстанты.Количество() > 0 Тогда
		НаборИзменяемыхКонстант = Константы.СоздатьНабор(ИзменяемыеКонстанты);
		Для Каждого ИмяКонстанты Из ИзменяемыеКонстанты Цикл
			НаборИзменяемыхКонстант[ИмяКонстанты] = Ложь;
		КонецЦикла;
		НаборИзменяемыхКонстант.Записать();
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатВыполнения;
	
КонецФункции

Функция НовыйРезультатОтключенияНедоступнойФункциональности()
	
	Результат = Новый Структура;
	Результат.Вставить("ПотребовалосьОтключениеФункциональности", Ложь);
	Результат.Вставить("ОтключениеВыполненоУспешно", Истина);
	
	Возврат Результат;
	
КонецФункции

Процедура ПометитьОбъектыНаУдаление(ОбъектыНаУдаление) Экспорт
	
	Для Каждого Ссылка Из ОбъектыНаУдаление Цикл
		Объект = Ссылка.ПолучитьОбъект();
		Объект.ДополнительныеСвойства.Вставить("УдаляетсяНедоступнаяФункциональность", Истина);
		Объект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция Изменить(ИмяФункциональности, НовоеЗначение, ИзменитьТолькоПростойИнтерфейс = Ложь) Экспорт
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	СписокФункциональности = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИмяФункциональности);
	Возврат ВыполнитьИзменениеСпискаФункциональности(СписокФункциональности, НовоеЗначение,
		ОписаниеФункциональности, ИзменитьТолькоПростойИнтерфейс);
	
КонецФункции

Процедура ИзменитьВФоне(ПараметрыЗадания, АдресХранилища) Экспорт
	
	РезультатИзменения = Изменить(ПараметрыЗадания.ИмяФункциональности,
		ПараметрыЗадания.НовоеЗначение, ПараметрыЗадания.ИзменитьТолькоПростойИнтерфейс);
	
	ПоместитьВоВременноеХранилище(РезультатИзменения, АдресХранилища);
	
КонецПроцедуры

Функция ВариантФункциональности() Экспорт
	
	ОписаниеФункциональности = Обработки.ФункциональностьПрограммы.ОписаниеФункциональности();
	
	СписокФункциональности = Новый Массив;
	Отбор = Новый Структура("Доступна", Истина);
	Для Каждого Функциональность Из ОписаниеФункциональности.НайтиСтроки(Отбор) Цикл
		СписокФункциональности.Добавить(Функциональность.Имя);
	КонецЦикла;
	
	СтрокаИменКонстант = СтрСоединить(СписокФункциональности, ",");
	
	НаборКонстантОбъект = Константы.СоздатьНабор(СтрокаИменКонстант);
	НаборКонстантОбъект.Прочитать();
	
	ВсеЗначенияФункциональности = Новый Структура;
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		ЗначениеКонстанты = НаборКонстантОбъект[ИмяФункциональности];
		ВсеЗначенияФункциональности.Вставить(ИмяФункциональности, ЗначениеКонстанты);
	КонецЦикла;
	
	Возврат ВариантФункциональностиПоЗначениям(ВсеЗначенияФункциональности);
	
КонецФункции

Функция ВариантФункциональностиПоЗначениям(ВсеЗначенияФункциональности) Экспорт
	
	ПредыдущееЗначениеКонстанты = Неопределено;
	Для Каждого ФункциональностьИЗначение Из ВсеЗначенияФункциональности Цикл
		ЗначениеКонстанты = ФункциональностьИЗначение.Значение;
		// Если снят или установлен хоть один флаг, то это выборочная функциональность
		Если ПредыдущееЗначениеКонстанты <> Неопределено И ПредыдущееЗначениеКонстанты <> ЗначениеКонстанты Тогда
			Возврат ФункциональностьПрограммыКлиентСервер.ВыборочнаяФункциональность();
		Иначе
			ПредыдущееЗначениеКонстанты = ЗначениеКонстанты;
		КонецЕсли;
	КонецЦикла;
	
	// Если все флаги установлены, то это полная функциональность, иначе - основная
	Если ПредыдущееЗначениеКонстанты = Истина Тогда
		Возврат ФункциональностьПрограммыКлиентСервер.ПолнаяФункциональность();
	Иначе
		Возврат ФункциональностьПрограммыКлиентСервер.ОсновнаяФункциональность();
	КонецЕсли;
	
КонецФункции

Функция ТребуетсяПолныйИнтерфейс() Экспорт
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	
	ФункциональностьНедоступнаяВПростомИнтерфейсе = ФункциональностьНедоступнаяВПростомИнтерфейсе(ОписаниеФункциональности);
	Если НЕ ЗначениеЗаполнено(ФункциональностьНедоступнаяВПростомИнтерфейсе) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИменаКонстант = СтрСоединить(ФункциональностьНедоступнаяВПростомИнтерфейсе, ",");
	НаборКонстант = Константы.СоздатьНабор(ИменаКонстант);
	НаборКонстант.Прочитать();
	
	Для Каждого ИмяФункциональности Из ФункциональностьНедоступнаяВПростомИнтерфейсе Цикл
		Если НаборКонстант[ИмяФункциональности] = Истина Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ДополнитьРодительскойФункциональностью(СписокФункциональности) Экспорт
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	
	РодительскаяФункциональность = РодительскаяФункциональностьДляСписка(СписокФункциональности, ОписаниеФункциональности);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокФункциональности, РодительскаяФункциональность, Истина);
	
КонецПроцедуры

Функция ИзменитьСписокФункциональности(СписокФункциональности, НовоеЗначение, ИзменитьТолькоПростойИнтерфейс = Ложь) Экспорт
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	
	ВключеннаяФункциональность = ВключеннаяФункциональность(СписокФункциональности);
	
	Если НовоеЗначение Тогда
		// Уже включенную функциональность повторно включать не требуется.
		ФункциональностьДляИзменения = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			СписокФункциональности, ВключеннаяФункциональность);
		ИспользуемаяФункциональность = Новый Массив;
	Иначе
		// Выключать нужно только ту функциональность, которая включена и не используется.
		ИспользуемаяФункциональность = ИспользуемаяФункциональность(ВключеннаяФункциональность, ОписаниеФункциональности);
		ФункциональностьДляИзменения = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			ВключеннаяФункциональность, ИспользуемаяФункциональность);
	КонецЕсли;
	
	РезультатИзменения = ВыполнитьИзменениеСпискаФункциональности(
		ФункциональностьДляИзменения, НовоеЗначение, ОписаниеФункциональности, ИзменитьТолькоПростойИнтерфейс);
	
	// При попытке отключения функциональности может оказаться, что часть
	// функциональности не может быть отключена. Например, из-за используемой дочерней функциональности.
	// Дополним этот список НевозможноВыключитьФункциональность опциями, которые сами используются - ИспользуемаяФункциональность.
	Если ЗначениеЗаполнено(ИспользуемаяФункциональность) Тогда
		РезультатИзменения.Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			РезультатИзменения.НевозможноВыключитьФункциональность, ИспользуемаяФункциональность, Истина);
	КонецЕсли;
	
	Возврат РезультатИзменения;
	
КонецФункции

// Выполняет изменение функциональности
//
// Параметры:
//   СтруктураПараметров - Структура - см. НовыеПараметрыИзмененияФункциональности()
//
// Возвращаемое значение:
//   Структура - См. НовыйРезультатИзмененияСпискаФункциональности()
//
Функция ИзменитьСписокФункциональностиВФоне(СтруктураПараметров) Экспорт
	
	Функциональность = СтруктураПараметров.Функциональность;
	
	Если СтруктураПараметров.ИзменитьВариантФункциональности Тогда
		НовоеЗначение = СтруктураПараметров.НовоеЗначение;
		ИзменитьТолькоПростойИнтерфейс = СтруктураПараметров.ИзменитьТолькоПростойИнтерфейс;
		РезультатИзменения = ИзменитьСписокФункциональности(Функциональность, НовоеЗначение, ИзменитьТолькоПростойИнтерфейс);
	Иначе
		РезультатИзменения = ИзменитьСписокФункциональностиПростойИнтерфейс(Функциональность);
	КонецЕсли;
	
	Возврат РезультатИзменения;
	
КонецФункции

Функция ПростойИнтерфейсФункциональности() Экспорт
	
	Возврат Константы.ИнтерфейсТаксиПростой.Получить();
	
КонецФункции

Функция ДоступнаФункциональностьГособоронзаказ() Экспорт
	
	Возврат ВариантыПриложений.ЭтоВерсияКОРП();
	
КонецФункции

// Возвращает признак возможности использования функциональности в доступном по тарифу виде интерфейса
// 
// Параметры:
//  ИмяФункциональности - Строка - имя проверяемой функциональности
//
// Возвращаемое значение:
//  Булево - Истина, если функциональность возможно использовать в простом интерфейсе либо тариф 
//           разрешает переход в полный интерфейс
//
Функция РазрешеноИспользоватьФункциональностьВДоступномИнтерфейсе(ИмяФункциональности) Экспорт
	
	ОписаниеФункциональности = НовыйОписаниеФункциональности();
	ЗаполнитьОписаниеФункциональности(ОписаниеФункциональности);
	НайденнаяСтрока = ОписаниеФункциональности.Найти(ИмяФункциональности, "Имя");
	Если НайденнаяСтрока = Неопределено Тогда
		Возврат Истина;
	ИначеЕсли НайденнаяСтрока.ДоступнаВПростомИнтерфейсе Тогда
		Возврат ТарификацияБП.РазрешенДополнительныйФункционал();
	Иначе
		Возврат ТарификацияБП.РазрешенПолныйИнтерфейс();
	КонецЕсли;
	
КонецФункции

// Возвращает описание условий доступности документов
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. НовыеУсловияДоступностиДокументовПоТарифу
//
Функция УсловияДоступностиДокументовПоТарифу() Экспорт
	
	УсловияДоступности = НовыеУсловияДоступностиДокументовПоТарифу();
	
	ДобавитьОграниченияОпцииДенежныеДокументы(УсловияДоступности);
	
	ДобавитьОграниченияОпцииПлатежныеТребования(УсловияДоступности);
	
	ДобавитьОграниченияОпцииМалоценноеОборудование(УсловияДоступности);
	
	ДобавитьОграниченияОпцииФакторинговыеОперации(УсловияДоступности);
	
	ДобавитьОграниченияОпцииВыплатыСамозанятым(УсловияДоступности);
	
	ДобавитьОграниченияОпцииКонтролируемыеСделки(УсловияДоступности);
	
	ДобавитьОграниченияОпцииНалоговыйАгентПоНДС(УсловияДоступности);
	
	ДобавитьОграниченияОпцииОбратноеНачислениеНДС(УсловияДоступности);
	
	ДобавитьОграниченияОпцииОтгрузкаБезПереходаПраваСобственности(УсловияДоступности);
	
	ДобавитьОграниченияОпцииПакетноеВыставлениеАктов(УсловияДоступности);
	
	ДобавитьОграниченияОпцииНачислениеНДССМежценовойРазницы(УсловияДоступности);
	
	ДобавитьОграниченияОпцииПодарочныеСертификаты(УсловияДоступности);
	
	ДобавитьОграниченияОпцииАлкогольнаяПродукция(УсловияДоступности);
	
	ДобавитьОграниченияОпцииПродажаТоваровУслугКомитентов(УсловияДоступности);
	
	ДобавитьОграниченияОпцииПродажаТоваровУслугЧерезКомиссионеров(УсловияДоступности);
	
	ДобавитьОграниченияОпцииПриобретениеТоваровУслугДляКомитентов(УсловияДоступности);
	
	ДобавитьОграниченияОпцииПриобретениеТоваровУслугЧерезКомиссионеров(УсловияДоступности);
	
	ДобавитьОграниченияОпцииИмпортТоваров(УсловияДоступности);
	
	ДобавитьОграниченияОпцииЭкспортТоваров(УсловияДоступности);
	
	ДобавитьОграниченияОпцииМаркировкаМеховыхИзделий(УсловияДоступности);
	
	ДобавитьОграниченияОпцииКомплексныйУчетПроизводства(УсловияДоступности);
	
	ДобавитьОграниченияОпцииЛизинг(УсловияДоступности);
	
	ДобавитьОграниченияОпцииПредметыАренды(УсловияДоступности);
	
	ДобавитьОграниченияОпцииКомпонентыАмортизации(УсловияДоступности);
	
	ДобавитьОграниченияОпцииНематериальныеАктивы(УсловияДоступности);
	
	ДобавитьОграниченияОпцииУчетПоНесколькимОрганизациям(УсловияДоступности);
	
	ДобавитьОграниченияОпцииОбособленныеПодразделения(УсловияДоступности);
	
	ДобавитьОграниченияОпцииРасширенныйФункционал(УсловияДоступности);
	
	ДобавитьОграниченияОпцииСотрудники(УсловияДоступности);
	
	Возврат УсловияДоступности;
	
КонецФункции

// Проверяет наличие документов, использующих определенную функциональность. Обрабатывает переданный параметр,
// исключая из него строки, ссылающиеся на документы, отсутствующие в базе
//
// Параметры:
//  ПроверемыеДокументы - Массив Из СтрокаТаблицыЗначений, ТаблицаЗначений
//                      - см. Обработки.ФункциональностьПрограммы.УсловияДоступностиДокументовПоТарифу
//
Процедура ПроверитьНаличиеДокументовСвязанныхСФункциональностью(ПроверяемыеДокументы) Экспорт
	
	Если Не ЗначениеЗаполнено(ПроверяемыеДокументы) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = ЗапросПоУсловиямДоступностиОбъектов(ПроверяемыеДокументы);
	Результаты = Запрос.ВыполнитьПакет();
	МаксимальныйИндекс = ПроверяемыеДокументы.Количество() - 1;
	
	СтрокиНаУдаление = Новый Массив;
	Для Индекс = 0 По МаксимальныйИндекс Цикл
		Если Результаты[Индекс].Пустой() Тогда
			СтрокиНаУдаление.Добавить(ПроверяемыеДокументы[Индекс]);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СтрокиНаУдаление) Тогда
		Если ТипЗнч(ПроверяемыеДокументы) = Тип("Массив") Тогда
			ПроверяемыеДокументы = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ПроверяемыеДокументы, СтрокиНаУдаление);
		Иначе
			Для Каждого УдаляемаяСтрока Из СтрокиНаУдаление Цикл
				ПроверяемыеДокументы.Удалить(УдаляемаяСтрока);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет изменение функциональности по переданному списку констант
//
// Параметры:
//   СписокФункциональности - Массив из Строка - Наименования констант, которые требуется изменить
//
// Возвращаемое значение:
//   Структура - См. НовыйРезультатИзмененияСпискаФункциональности()
//
Функция ИзменитьСписокФункциональностиПростойИнтерфейс(СписокФункциональности) Экспорт
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	
	ВключеннаяФункциональность = ВключеннаяФункциональность(СписокФункциональности);
	
	ФункциональностьДляИзмененияВключение = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		СписокФункциональности,
		ВключеннаяФункциональность);
	
	ИспользуемаяФункциональность = ИспользуемаяФункциональность(ВключеннаяФункциональность, ОписаниеФункциональности);
	
	ФункциональностьДляИзмененияОтключение = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ВключеннаяФункциональность,
		ИспользуемаяФункциональность);
	
	ФункциональностьДляИзменения = Новый Структура;
	ФункциональностьДляИзменения.Вставить(
		"ФункциональностьДляИзмененияВключение",
		ФункциональностьДляИзмененияВключение);
	ФункциональностьДляИзменения.Вставить(
		"ФункциональностьДляИзмененияОтключение",
		ФункциональностьДляИзмененияОтключение);
	
	РезультатИзменения = ВыполнитьИзменениеСпискаФункциональностиПростойИнтерфейс(
		ФункциональностьДляИзменения,
		ОписаниеФункциональности);
	
	// При попытке отключения функциональности может оказаться, что часть
	// функциональности не может быть отключена. Например, из-за используемой дочерней функциональности.
	// Дополним этот список НевозможноВыключитьФункциональность опциями, которые сами используются - ИспользуемаяФункциональность.
	Если ЗначениеЗаполнено(ИспользуемаяФункциональность) Тогда
		РезультатИзменения.Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			РезультатИзменения.НевозможноВыключитьФункциональность,
			ИспользуемаяФункциональность,
			Истина);
	КонецЕсли;
	
	Возврат РезультатИзменения;
	
КонецФункции

Функция ВыполнитьИзменениеСпискаФункциональностиПростойИнтерфейс(СписокФункциональности, ОписаниеФункциональности)
	
	Результат = НовыйРезультатИзмененияСпискаФункциональности(Истина);
	СписокФункциональностиВключение = СписокФункциональности.ФункциональностьДляИзмененияВключение;
	СписокФункциональностиОтключение = СписокФункциональности.ФункциональностьДляИзмененияОтключение;
	
	Если Не ЗначениеЗаполнено(СписокФункциональностиВключение)
		И Не ЗначениеЗаполнено(СписокФункциональностиОтключение) Тогда
		Возврат Результат;
	КонецЕсли;
	
	КэшЛицензий = Новый Соответствие;
	Если ЗначениеЗаполнено(СписокФункциональностиВключение) Тогда
		ДополнитьКэшЛицензийНаВозможностьИспользованияФункциональности(СписокФункциональностиВключение,
		ОписаниеФункциональности,
		КэшЛицензий);
	КонецЕсли;
	
	Для Каждого ИмяФункциональности Из СписокФункциональностиВключение Цикл
		ДобавитьНовоеЗначениеФункциональности(Результат, ИмяФункциональности, Истина, КэшЛицензий, ОписаниеФункциональности);
	КонецЦикла;
	
	Для Каждого ИмяФункциональности Из СписокФункциональностиОтключение Цикл
		ДобавитьНовоеЗначениеФункциональности(Результат, ИмяФункциональности, Ложь, КэшЛицензий, ОписаниеФункциональности);
	КонецЦикла;
	
	ПроверитьЧтоВключенаРодительскаяФункциональность(СписокФункциональностиВключение,
		ОписаниеФункциональности,
		КэшЛицензий,
		Результат);
	ПроверитьЧтоОтключенаДочерняяФункциональность(СписокФункциональностиОтключение,
		ОписаниеФункциональности,
		КэшЛицензий,
		Результат);
	
	Для Каждого ФункциональностьИЗначение Из Результат.НовыеЗначенияФункциональности Цикл
		Функциональность = СвойстваФункциональности(ФункциональностьИЗначение.Ключ, ОписаниеФункциональности);
		ЗаписатьЗначениеФункциональности(Функциональность, ФункциональностьИЗначение.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Настраивает константы, формирующие интерфейс программы через функциональные опции, и зависящие от нескольких элементов функциональности -
// то есть, чтобы какая-то функция появилась в интерфейсе должно быть включено несколько элементов функциональности.
// Такие константы называем "агрегирующими", а элементы, от которых они зависят - агрегируемые.
// Например, это может понадобиться, когда один из таких элементов - "Расширенный функционал".
// Процедура вызывается при записи агрегируемых констант.
//
// В целом, организация работы с агрегирующими константами следующая:
// - в модуле менеджера агрегирующей команды предусматривается процедура ЗаполнитьАгрегируемаяФункциональность,
//   заполняющая перечень имен констант, от которых она зависит.
// - в обработчике ПриЗаписи агрегируемых констант размещаются вызовы НастроитьАгрегирующуюКонстанту - столько вызовов,
//   сколько агрегирующих констант от нее зависит
// - в обработчике ПередЗаписью агрегирующей константы следует вызвать ТарификацияБП.КонстантаАгрегирующаяФункциональностьПроверитьПередЗаписью
// - в версии с добавлением агрегирующих констант добавляется обработчик обновления с вызовом НастроитьАгрегирующуюКонстанту.
//   Обработчик рекомендуется размещать в модуле менеджера агрегируемой константы.
// - информацию о наличии зависимых (агрегирующих) констант рекомендуется размещать в комментариях к описанию констант функциональности (агрегируемых),
//   формируемом в ЗаполнитьОписаниеФункциональности
//
// Параметры:
//  ИмяАгрегирующейКонстанты - Строка
//
Процедура НастроитьАгрегирующуюКонстанту(ИмяАгрегирующейКонстанты) Экспорт
	
	АгрегируемаяФункциональность = Новый Массив;
	Константы[ИмяАгрегирующейКонстанты].ЗаполнитьАгрегируемаяФункциональность(АгрегируемаяФункциональность);
	
	Значение = Истина;
	Для Каждого ИмяАгрегируемойКонстанты Из АгрегируемаяФункциональность Цикл
		
		Значение = Значение И Константы[ИмяАгрегируемойКонстанты].Получить();
		
		Если Не Значение Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Константы[ИмяАгрегирующейКонстанты].Установить(Значение);
	
КонецПроцедуры

// Возвращает структуру параметров для изменения функциональности в фоне
//
// Параметры:
//   ИзменяемаяФункциональность - Массив из Строка - Наименования констант, которые нужно изменить
// 
// Возвращаемое значение:
//   Структура
//      * Функциональность - Массив из Строка - Наименования констант
//      * НовоеЗначение - Булево - Признак, что устанавливается новое значение
//      * ИзменитьТолькоПростойИнтерфейс - Булево - Признак, что функциональность изменяется в простом интерфейсе
//      * ИзменитьВариантФункциональности - Булево - Признак, что функциональность изменяется в полном интерфейсе
//
Функция НовыеПараметрыИзмененияФункциональности(Знач ИзменяемаяФункциональность) Экспорт
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Функциональность", ИзменяемаяФункциональность);
	ПараметрыПроцедуры.Вставить("НовоеЗначение", Ложь);
	ПараметрыПроцедуры.Вставить("ИзменитьТолькоПростойИнтерфейс", Ложь);
	ПараметрыПроцедуры.Вставить("ИзменитьВариантФункциональности", Ложь);
	
	Возврат ПараметрыПроцедуры;
	
КонецФункции

// Возвращает список используемой функциональности в базе
//
// Параметры:
//   Функциональность - Массив из Строка - Наименовение констант, которые требуется проверить на использование в программе
//
// Возвращаемое значение:
//   Массив из Строка - Наименование констант, которые нельзя отключить в настройках функциональности
//
Функция ПроверитьВозможностьИзмененияФункциональностиВФоне(Функциональность) Экспорт
	
	ВключеннаяФункциональность = ВключеннаяФункциональность(Функциональность);
	
	ОписаниеФункциональности = ОписаниеФункциональности();
	
	Возврат ИспользуемаяФункциональность(ВключеннаяФункциональность, ОписаниеФункциональности);
	
КонецФункции

// Возвращает признак того, что для используемой функциональности нашлись свободные лицензии
// и их удалось удачно захватить
//
// Параметры:
//   Функциональность - Структура
//                        * Ключ -Строка - Наименование константы, по которой нужно занять лицензию.
//                        * Значение - Строка - Наименование тарифной опции
//                      Неопределено - Выполняется захват по всей включенной и недоступной функциональности
//
// Возвращаемое значение:
//   Булево
//
Функция ЗахватитьЛицензииДляИспользуемойФункциональности(Функциональность = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Функциональность) Тогда
		ОписаниеФункциональности = ОписаниеФункциональности();
		ВключеннаяНедоступнаяФункциональность = ВключеннаяНедоступнаяФункциональность(ОписаниеФункциональности);
	Иначе
		ВключеннаяНедоступнаяФункциональность = ОбщегоНазначения.СкопироватьРекурсивно(Функциональность);
	КонецЕсли;
	
	// Нужно попытаться захватить лицензии, возможно, в менеджере сервиса уже есть свободные.
	ВыполнитьПопыткуЗахватитьЛицензии(ВключеннаяНедоступнаяФункциональность);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВключеннаяНедоступнаяФункциональность.Количество() = 0;
	
КонецФункции

// Возвращает имена констант из переданного массива, у которых значение Истина
//
// Параметры:
//   СписокФункциональности - Массив из Строка - Наименование констант, которые нужно проверить на включение
// 
// Возвращаемое значение:
//   Массив из Строка - список включенных констант
//
Функция ВключеннаяФункциональность(СписокФункциональности) Экспорт
	
	ВключеннаяФункциональность = Новый Массив;
	
	Если Не ЗначениеЗаполнено(СписокФункциональности) Тогда
		Возврат ВключеннаяФункциональность;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СписокФункциональности.Количество() = 1 Тогда
		ИменаФункциональности = СписокФункциональности[0];
	Иначе
		ИменаФункциональности = СтрСоединить(СписокФункциональности, ",");
	КонецЕсли;
	
	НаборКонстант = Константы.СоздатьНабор(ИменаФункциональности);
	НаборКонстант.Прочитать();
	
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		Если НаборКонстант[ИмяФункциональности] = Истина Тогда
			ВключеннаяФункциональность.Добавить(ИмяФункциональности);
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВключеннаяФункциональность;
	
КонецФункции

#КонецОбласти

#Область ИзменениеФункциональности

Процедура ЗаписатьЗначениеФункциональности(Функциональность, НовоеЗначение)
	
	Если Константы[Функциональность.Имя].Получить() = НовоеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьЗначениеФункциональности(Функциональность, НовоеЗначение);
	
КонецПроцедуры

Процедура ИзменитьЗначениеФункциональности(Функциональность, НовоеЗначение)
	
	ОперацияСЛицензией = Неопределено;
	
	Попытка
		
		Если ТарификацияБП.ФункциональностьОграничиваетсяТарифом()
			И ЗначениеЗаполнено(Функциональность.ИмяТарифнойОпции) Тогда
			
			ОперацияСЛицензией = ВыполнитьЗахватОтменуЛицензииТарифнойОпции(Функциональность.ИмяТарифнойОпции, НовоеЗначение);
			
			Если НовоеЗначение
				И ОперацияСЛицензией <> Неопределено
				И Не ОперацияСЛицензией.Результат Тогда
				ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нет свободных лицензий ""%1"" для включения функциональности ""%2""'"), 
					Функциональность.ИмяТарифнойОпции, Функциональность.Имя);
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			
		КонецЕсли;
		
		ДействияПриИзмененииЗначенияФункциональности(Функциональность, НовоеЗначение);
		
	Исключение
		
		ЗаписьЖурналаРегистрации(ФункциональностьПрограммыКлиентСервер.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Константы[Функциональность.Имя], , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Если Константы[Функциональность.Имя].Получить() = НовоеЗначение Тогда
			// Возникло исключение - нужно вернуть значение константы в старое значение, если же константа все же
			// изменилась и исключение было вызвано операцией после изменения.
			Константы[Функциональность.Имя].Установить(Не НовоеЗначение);
		КонецЕсли;
		
		ОтменитьОперациюСЛицензией(Функциональность.ИмяТарифнойОпции, Не НовоеЗначение, ОперацияСЛицензией);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ДействияПриИзмененииЗначенияФункциональности(Функциональность, НовоеЗначение)
	
	// В случае необходимости выполнения дополнительных действий после изменения функциональности
	// рекомендуется устанавливать константу в транзакции вместе с выполнением последующих действий.
	// При выполнении этих действий не использовать обращение к изменяемой функциональности через
	// ПолучитьФункциональнуюОпцию(), использовать метод константы Получить().
	
	Если Функциональность.Имя = "ИспользоватьСтатьиДвиженияДенежныхСредств" Тогда
		ДействияПриИзмененииИспользоватьСтатьиДвиженияДенежныхСредств(НовоеЗначение);
	ИначеЕсли Функциональность.Имя = "ВестиУчетПоПодразделениям" Тогда
		ДействияПриИзмененииВестиУчетПоПодразделениям(НовоеЗначение);
	ИначеЕсли Функциональность.Имя = "ИспользоватьКадровыйУчет" Тогда
		ДействияПриИзмененииИспользоватьКадровыйУчет(НовоеЗначение);
	Иначе
		Константы[Функциональность.Имя].Установить(НовоеЗначение);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру результата изменения функциональности
//
// Возвращаемое значение:
//   Структура
//      * Отказ - Булево
//      * НовоеЗначение - Булево - Признак, что устанавливается новое значение
//      * ФункциональностьНеДоступнаПоТарифу - Массив из Строка - Список констант, функциональность по которым недоступна по тарифу
//      * ФункциональностьБезСвободныхЛицензий - Массив из Строка - Список констант, услуги по которым недоступны в тарифе
//      * ТребуетсяВключитьФункциональность - Массив из Строка - Список констант, функциональность по которым нужно включить
//      * НевозможноВыключитьФункциональность - Массив из Строка - Список констант, функциональность по которым нельзя выключить
//      * ТребуетсяВыключитьФункциональность - Массив из Строка - Список констант, функциональность по которым нужно выключить
//      * НовыеЗначенияФункциональности - Структура
//                                           * Ключ - Имя константы
//                                           * Значение - Булево - Новое значение опции
//      * НовыеЗначенияФункциональностиДляОтложенногоИзменения - Структура
//                                                                  * Ключ - Имя константы
//                                                                  * Значение - Булево - Новое значение опции
//
Функция НовыйРезультатИзмененияСпискаФункциональности(НовоеЗначение)
	
	Результат = Новый Структура;
	Результат.Вставить("Отказ", Ложь);
	Результат.Вставить("НовоеЗначение", НовоеЗначение);
	Результат.Вставить("ФункциональностьНеДоступнаПоТарифу", Новый Массив);
	Результат.Вставить("ФункциональностьБезСвободныхЛицензий", Новый Массив);
	Результат.Вставить("ТребуетсяВключитьФункциональность", Новый Массив);
	Результат.Вставить("НевозможноВыключитьФункциональность", Новый Массив);
	Результат.Вставить("ТребуетсяВыключитьФункциональность", Новый Массив);
	Результат.Вставить("НовыеЗначенияФункциональности", Новый Структура);
	Результат.Вставить("НовыеЗначенияФункциональностиДляОтложенногоИзменения", Новый Структура);
	Возврат Результат;
	
КонецФункции

Функция ВыполнитьЗахватОтменуЛицензииТарифнойОпции(ИмяТарифнойОпции, НовоеЗначение)
	
	Если Не ТарификацияБП.ФункциональностьОграничиваетсяТарифом() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОперацияСЛицензией = НовыйОперацияСЛицензией();
	
	ЛицензияЗахвачена = ТарификацияБП.ЗарегистрированаЛицензияУслугиФункциональности(ИмяТарифнойОпции);
	
	Если ЛицензияЗахвачена = НовоеЗначение Тогда
		// Состояние лицензии и так уже соответствует новому значению константы.
		// Ранее произошла рассинхронизация значения константы и захвата лицензии.
		ОперацияСЛицензией.Результат = Истина;
		ОперацияСЛицензией.ОперацияПодтверждена = Истина;
	Иначе
		Попытка
			Если НовоеЗначение Тогда
				РезультатЗахвата = ТарификацияБП.ЗанятьЛицензиюУслугиФункциональности(
					ИмяТарифнойОпции, ОперацияСЛицензией.ИдентификаторОперации);
				ОперацияСЛицензией.Результат = РезультатЗахвата.Результат;
			Иначе
				ОперацияСЛицензией.Результат = ТарификацияБП.ОсвободитьЛицензиюУслугиФункциональности(
					ИмяТарифнойОпции, ОперацияСЛицензией.ИдентификаторОперации);
			КонецЕсли;
		Исключение
			ОперацияСЛицензией.Результат = Ложь;
		КонецПопытки;
	КонецЕсли;
	
	Если ОперацияСЛицензией.Результат И Не ОперацияСЛицензией.ОперацияПодтверждена Тогда
		ОперацияСЛицензией.ОперацияПодтверждена = Тарификация.ПодтвердитьОперацию(ОперацияСЛицензией.ИдентификаторОперации);
	КонецЕсли;
	
	Возврат ОперацияСЛицензией;
	
КонецФункции

Процедура ОтменитьОперациюСЛицензией(ИмяТарифнойОпции, ЗначениеДоИзменения, ОперацияСЛицензией)
	
	Если ОперацияСЛицензией = Неопределено
		ИЛИ Не ОперацияСЛицензией.Результат Тогда
		Возврат;
	КонецЕсли;
	
	// Лицензия захвачена, нужно ее отменить.
	// Процедура отмены зависит от того, сумели ли мы ее подтвердить, или нет.
	// Если подтвердить не смогли, то достаточно просто отменить операцию.
	// Если же смогли - то нужно полноценно освободжать лицензию.
	
	Если ОперацияСЛицензией.ОперацияПодтверждена Тогда
		ВыполнитьЗахватОтменуЛицензииТарифнойОпции(ИмяТарифнойОпции, ЗначениеДоИзменения);
	Иначе
		Тарификация.ОтменитьОперацию(ОперацияСЛицензией.ИдентификаторОперации);
	КонецЕсли;
	
КонецПроцедуры

Функция НовыйОперацияСЛицензией()
	
	ОперацияСЛицензией = Новый Структура();
	ОперацияСЛицензией.Вставить("ИдентификаторОперации", Новый УникальныйИдентификатор);
	ОперацияСЛицензией.Вставить("ОперацияПодтверждена", Ложь);
	ОперацияСЛицензией.Вставить("Результат", Ложь);
	Возврат ОперацияСЛицензией;
	
КонецФункции

Функция ВыполнитьИзменениеСпискаФункциональности(СписокФункциональности, НовоеЗначение, ОписаниеФункциональности, ИзменитьТолькоПростойИнтерфейс = Ложь)
	
	Результат = НовыйРезультатИзмененияСпискаФункциональности(НовоеЗначение);
	
	Если Не ЗначениеЗаполнено(СписокФункциональности) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ЭтоПолныйИнтерфейс = Не ПростойИнтерфейсФункциональности();
	
	КэшЛицензий = Новый Соответствие;
	Если НовоеЗначение Тогда
		ДополнитьКэшЛицензийНаВозможностьИспользованияФункциональности(СписокФункциональности, ОписаниеФункциональности,
			КэшЛицензий);
	КонецЕсли;
	
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		ДобавитьНовоеЗначениеФункциональности(Результат, ИмяФункциональности, НовоеЗначение, КэшЛицензий, ОписаниеФункциональности);
	КонецЦикла;
	
	Если НовоеЗначение Тогда
		ПроверитьЧтоВключенаРодительскаяФункциональность(
			СписокФункциональности, ОписаниеФункциональности, КэшЛицензий, Результат);
	Иначе
		ПроверитьЧтоОтключенаДочерняяФункциональность(СписокФункциональности, ОписаниеФункциональности, КэшЛицензий, Результат);
	КонецЕсли;
	
	Если ИзменитьТолькоПростойИнтерфейс Тогда
		ЗаполнитьФункциональностьДляОтложенногоИзменения(Результат, ОписаниеФункциональности);
	КонецЕсли;
	
	Для Каждого ФункциональностьИЗначение Из Результат.НовыеЗначенияФункциональности Цикл
		Функциональность = СвойстваФункциональности(ФункциональностьИЗначение.Ключ, ОписаниеФункциональности);
		ЗаписатьЗначениеФункциональности(Функциональность, ФункциональностьИЗначение.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ПроверитьЧтоВключенаРодительскаяФункциональность(СписокФункциональности, ОписаниеФункциональности, КэшЛицензий, Результат)
	
	// При включении функциональности проверяем, не нужно ли также включить 
	// родительские константы. Если нужно - предлагаем включить.
	
	РодительскаяФункциональность = РодительскаяФункциональностьДляСписка(СписокФункциональности, ОписаниеФункциональности);
	
	// В том случае, если в простом интерфейсе изменяется только функциональность полного интерфейса,
	// при этом вся родительская функциональность должна быть обязательно включена при переходе на полный интерфейс,
	// то в этом случае не нужно проверять родительскую функциональность на включение - она будет включена в обработке
	// перехода на полный интерфейс.
	
	ПроверятьРодительскуюФункциональностьНаВключение = Истина;
	Если ПростойИнтерфейсФункциональности()
		И ВсяФункциональностьТолькоДляПолногоИнтерфейса(СписокФункциональности, ОписаниеФункциональности) Тогда
		ПроверятьРодительскуюФункциональностьНаВключение = Не ВсяФункциональностьДолжнаБытьОбязательноВключенаВПолномИнтерфейсе(
			РодительскаяФункциональность, ОписаниеФункциональности);
	КонецЕсли;
	
	Если ПроверятьРодительскуюФункциональностьНаВключение Тогда
		
		Для Каждого ИмяФункциональности Из РодительскаяФункциональность Цикл
			Включено = Константы[ИмяФункциональности].Получить();
			Если Не Включено И СписокФункциональности.Найти(ИмяФункциональности) = Неопределено Тогда
				Результат.Отказ = Истина;
				Результат.ТребуетсяВключитьФункциональность.Добавить(ИмяФункциональности);
			КонецЕсли;
		КонецЦикла;
		
		// Не будем включать сейчас ту функциональность, у которой не включена родительская.
		УдалитьБлокируемуюРодительскойДочернююФункциональность(
			Результат.НовыеЗначенияФункциональности, Результат.ТребуетсяВключитьФункциональность, ОписаниеФункциональности);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЧтоОтключенаДочерняяФункциональность(СписокФункциональности, ОписаниеФункциональности, КэшЛицензий, Результат)
	
	// При выключении функциональности проверяем, можно ли выключить и дочернюю функциональность.
	// Если зависимые константы выключить нельзя, сообщаем об этом и родительскую функциональность не выключаем.
	ДочерняяФункциональность = ДочерняяФункциональностьДляСписка(СписокФункциональности, ОписаниеФункциональности);
	
	// Если дочерняя функциональность есть в списке на отключение, то проверять ее как зависимую не требуется,
	// т.к. в СписокФункциональности должна быть уже только функциональность, которую гарантированно можно снять.
	ПроверяемаяДочерняяФункциональность = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ДочерняяФункциональность, СписокФункциональности);
	
	РезультатПроверки = ПроверитьФункциональностьНаВозможностьОтключения(ПроверяемаяДочерняяФункциональность, ОписаниеФункциональности);
	
	Если РезультатПроверки.НевозможноОтключить.Количество() > 0 Тогда
		
		Результат.Отказ = Истина;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			Результат.НевозможноВыключитьФункциональность, РезультатПроверки.НевозможноОтключить, Истина);
		
		// Не будем отключать ту функциональность, у которой нет возможности отключить дочернюю.
		УдалитьБлокируемуюДочернейРодительскуюФункциональность(
			Результат.НовыеЗначенияФункциональности, РезультатПроверки.НевозможноОтключить, ОписаниеФункциональности);
		
	Иначе
		
		// Всю дочернюю функциональность можно отключить.
		// Поэтому добавим ее в список на отключение.
		
		НовоеЗначение = Ложь;
		
		Для Каждого ИмяФункциональности Из РезультатПроверки.ВозможноОтключить Цикл
			ДобавитьНовоеЗначениеФункциональности(Результат, ИмяФункциональности, НовоеЗначение, КэшЛицензий, ОписаниеФункциональности);
		КонецЦикла;
		
		Если ПростойИнтерфейсФункциональности() Тогда
			// Добавим также и отключенную дочернюю функциональность, чтобы отключить
			// в простом интерфейсе дочернюю функциональность, если она в форме включена
			// для отложенного изменения.
			Для Каждого ИмяФункциональности Из РезультатПроверки.Отключена Цикл
				ДобавитьНовоеЗначениеФункциональности(Результат, ИмяФункциональности, НовоеЗначение, КэшЛицензий, ОписаниеФункциональности);
			КонецЦикла;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			Результат.ТребуетсяВыключитьФункциональность, РезультатПроверки.ВозможноОтключить, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДочерняяФункциональностьДляСписка(СписокФункциональности, ОписаниеФункциональности)
	
	ДочерняяФункциональность = Новый Массив;
	
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		
		Функциональность = СвойстваФункциональности(ИмяФункциональности, ОписаниеФункциональности);
		Если Функциональность = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДополнитьФункциональностьСУчетомИерархии(
			ДочерняяФункциональность, Функциональность, "ДочерняяФункциональность", ОписаниеФункциональности);
		
	КонецЦикла;
	
	Возврат ДочерняяФункциональность;
	
КонецФункции

Функция РодительскаяФункциональностьДляСписка(СписокФункциональности, ОписаниеФункциональности)
	
	РодительскаяФункциональность = Новый Массив;
	
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		
		Функциональность = СвойстваФункциональности(ИмяФункциональности, ОписаниеФункциональности);
		Если Функциональность = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДополнитьФункциональностьСУчетомИерархии(
			РодительскаяФункциональность, Функциональность, "РодительскаяФункциональность", ОписаниеФункциональности);
		
	КонецЦикла;
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(РодительскаяФункциональность);
	
КонецФункции

Процедура ДополнитьФункциональностьСУчетомИерархии(СписокФункциональности, Функциональность, ИмяСпискаДополнения, ОписаниеФункциональности)
	
	СписокФункциональностиДляДополнения = Новый Массив;
	
	Для Каждого ИмяФункциональности Из Функциональность[ИмяСпискаДополнения] Цикл
		
		ФункциональностьДляДополнения = СвойстваФункциональности(ИмяФункциональности, ОписаниеФункциональности);
		Если ФункциональностьДляДополнения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДополнитьФункциональностьСУчетомИерархии(
			СписокФункциональностиДляДополнения, ФункциональностьДляДополнения, ИмяСпискаДополнения, ОписаниеФункциональности);
		
		СписокФункциональностиДляДополнения.Добавить(ИмяФункциональности);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СписокФункциональностиДляДополнения) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			СписокФункциональности, СписокФункциональностиДляДополнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьБлокируемуюДочернейРодительскуюФункциональность(НовыеЗначенияФункциональности, ДочерняяФункциональность, ОписаниеФункциональности)
	
	РодительскаяФункциональностьКоторуюБлокируетДочерняя = РодительскаяФункциональностьДляСписка(
		ДочерняяФункциональность, ОписаниеФункциональности);
	
	Для Каждого ИмяФункциональности Из РодительскаяФункциональностьКоторуюБлокируетДочерняя Цикл
		Если НовыеЗначенияФункциональности.Свойство(ИмяФункциональности) Тогда
			НовыеЗначенияФункциональности.Удалить(ИмяФункциональности);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьБлокируемуюРодительскойДочернююФункциональность(НовыеЗначенияФункциональности, РодительскаяФункциональность, ОписаниеФункциональности)
	
	ДочерняяФункциональностьКоторуюБлокируетРодительская = ДочерняяФункциональностьДляСписка(
		РодительскаяФункциональность, ОписаниеФункциональности);
	
	Для Каждого ИмяФункциональности Из ДочерняяФункциональностьКоторуюБлокируетРодительская Цикл
		Если НовыеЗначенияФункциональности.Свойство(ИмяФункциональности) Тогда
			НовыеЗначенияФункциональности.Удалить(ИмяФункциональности);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ВсяФункциональностьТолькоДляПолногоИнтерфейса(СписокФункциональности, ОписаниеФункциональности)
	
	ВсяФункциональностьТолькоДляПолногоИнтерфейса = Истина;
	
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		
		Функциональность = СвойстваФункциональности(ИмяФункциональности, ОписаниеФункциональности);
		Если Функциональность = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Функциональность.ДоступнаВПростомИнтерфейсе Тогда
			ВсяФункциональностьТолькоДляПолногоИнтерфейса = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВсяФункциональностьТолькоДляПолногоИнтерфейса;
	
КонецФункции

Функция ВсяФункциональностьДолжнаБытьОбязательноВключенаВПолномИнтерфейсе(СписокФункциональности, ОписаниеФункциональности)
	
	ВсяФункциональностьДолжнаБытьОбязательноВключенаВПолномИнтерфейсе = Истина;
	
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		Функциональность = СвойстваФункциональности(ИмяФункциональности, ОписаниеФункциональности);
		Если Функциональность = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не Функциональность.ДолжнаБытьОбязательноВключенаВПолномИнтерфейсе Тогда
			ВсяФункциональностьДолжнаБытьОбязательноВключенаВПолномИнтерфейсе = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВсяФункциональностьДолжнаБытьОбязательноВключенаВПолномИнтерфейсе;
	
КонецФункции

Функция ПроверитьФункциональностьНаВозможностьОтключения(СписокФункциональности, ОписаниеФункциональности)
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("Отключена", Новый Массив);
	РезультатПроверки.Вставить("НевозможноОтключить", Новый Массив);
	РезультатПроверки.Вставить("ВозможноОтключить", Новый Массив);
	
	Если Не ЗначениеЗаполнено(СписокФункциональности) Тогда
		Возврат РезультатПроверки;
	КонецЕсли;
	
	НаборКонстант = Константы.СоздатьНабор(СтрСоединить(СписокФункциональности, ","));
	НаборКонстант.Прочитать();
	
	Для Каждого ИмяФункциональности Из СписокФункциональности Цикл
		
		Если Не НаборКонстант[ИмяФункциональности] Тогда
			// Дочерняя функциональность и так выключена, дополнительно проверять ее не требуется.
			РезультатПроверки.Отключена.Добавить(ИмяФункциональности);
			Продолжить;
		КонецЕсли;
		
		Если ОтказСнятьИспользованиеКонстанты(ИмяФункциональности, ОписаниеФункциональности) Тогда
			РезультатПроверки.НевозможноОтключить.Добавить(ИмяФункциональности);
		Иначе
			РезультатПроверки.ВозможноОтключить.Добавить(ИмяФункциональности);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция СвойстваФункциональности(ИмяФункциональности, ОписаниеФункциональности)
	
	Функциональность = ОписаниеФункциональности.Найти(ИмяФункциональности, "Имя");
	Если Функциональность <> Неопределено
		И Функциональность.Доступна Тогда
		Возврат Функциональность;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ДобавитьНовоеЗначениеФункциональности(Результат, ИмяФункциональности, НовоеЗначение, КэшЛицензий, ОписаниеФункциональности)
	
	Если НовоеЗначение Тогда
		
		ЛицензияФункциональности = КэшЛицензий.Получить(ИмяФункциональности);
		Если ЛицензияФункциональности <> Неопределено
			И Не ЛицензияФункциональности.ВходитВТариф Тогда
			Результат.ФункциональностьНеДоступнаПоТарифу.Добавить(ИмяФункциональности);
			Возврат;
		КонецЕсли;
		
		Если ЛицензияФункциональности <> Неопределено
			И Не ЛицензияФункциональности.ЕстьСвободныеЛицензии Тогда
			Результат.ФункциональностьБезСвободныхЛицензий.Добавить(ИмяФункциональности);
			Возврат;
		КонецЕсли;
		
	Иначе
		
		Функциональность = СвойстваФункциональности(ИмяФункциональности, ОписаниеФункциональности);
		Если Функциональность.ДолжнаБытьОбязательноВключенаВПолномИнтерфейсе
			И Не ПростойИнтерфейсФункциональности() Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.НовыеЗначенияФункциональности.Вставить(ИмяФункциональности, НовоеЗначение);
	
КонецПроцедуры

Процедура ЗаполнитьФункциональностьДляОтложенногоИзменения(Результат, ОписаниеФункциональности)
	
	// Для функциональности, которая не доступна в простом интерфейсе, не нужно устанавливать
	// соответствующую константу непосредственно. Нужно сохранить эти значения
	// и изменить только в форме для того, чтобы функциональность установилась при перезапуске.
	
	Для Каждого ФункциональностьИЗначение Из Результат.НовыеЗначенияФункциональности Цикл
		
		Функциональность = СвойстваФункциональности(ФункциональностьИЗначение.Ключ, ОписаниеФункциональности);
		Если Функциональность = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Функциональность.ДоступнаВПростомИнтерфейсе Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.НовыеЗначенияФункциональностиДляОтложенногоИзменения.Вставить(
			ФункциональностьИЗначение.Ключ, ФункциональностьИЗначение.Значение);
		
	КонецЦикла;
	
	// Очистим НовыеЗначенияФункциональности, чтобы не изменились константы, которые с ними связаны.
	Для Каждого ФункциональностьИЗначение Из Результат.НовыеЗначенияФункциональностиДляОтложенногоИзменения Цикл
		Результат.НовыеЗначенияФункциональности.Удалить(ФункциональностьИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РасширенныйФункционал

Функция РезультатЗапросаРасширенныйФункционал(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетнаяПолитика.Период КАК Период,
	|	УчетнаяПолитика.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	(УчетнаяПолитика.УчитыватьРасходыПоЭлементамЗатрат
	|			ИЛИ УчетнаяПолитика.УчитыватьРасходыПоСтатьямЗатрат)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УчетнаяПолитика.Период КАК Период,
	|	УчетнаяПолитика.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.НастройкиУчетаЗатрат.ПланСчетов.Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбслуживающиеПроизводства))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.Представление КАК Представление
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.СчетаФактурыОтИмениОрганизации
	|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.Представление КАК Представление
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.НекредитнаяФинансоваяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетнаяПолитика.Период КАК Период,
	|	УчетнаяПолитика.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.НастройкиУчетаЗатрат.ВариантУчетаСтоимостиВыпуска = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаСтоимостиВыпуска.ПлановаяСтоимость)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетнаяПолитика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетнаяПолитика.Период КАК Период,
	|	УчетнаяПолитика.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.ОбесценениеЗапасов
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетнаяПолитика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбесценениеВнеоборотныхАктивов.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ОбесценениеВнеоборотныхАктивов.Ссылка) КАК Представление
	|ИЗ
	|	Документ.ОбесценениеВнеоборотныхАктивов КАК ОбесценениеВнеоборотныхАктивов
	|ГДЕ
	|	НЕ ОбесценениеВнеоборотныхАктивов.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбесценениеВнеоборотныхАктивов.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УчетнаяПолитика.Период КАК Период,
	|	УчетнаяПолитика.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.НастройкиУчетаЗатрат.ПланСчетов.Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.БракВПроизводстве))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетнаяПолитика.Период КАК Период,
	|	УчетнаяПолитика.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.УчетнаяПолитика КАК УчетнаяПолитика
	|ГДЕ
	|	УчетнаяПолитика.ПериодАмортизации = ЗНАЧЕНИЕ(Перечисление.АлгоритмыПериодаАмортизации.СДатыПризнания)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетнаяПолитика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(Организации.Ссылка) КАК Представление
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.КрупнейшийНалогоплательщик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КрупнейшийНалогоплательщикИсторияРегистрацийВНалоговомОрганеСрезПоследних.Период КАК Период,
	|	КрупнейшийНалогоплательщикИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|ИЗ
	|	РегистрСведений.КрупнейшийНалогоплательщикИсторияРегистрацийВНалоговомОргане.СрезПоследних КАК КрупнейшийНалогоплательщикИсторияРегистрацийВНалоговомОрганеСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результаты = Новый Структура;
	Результаты.Вставить("ЭлементыЗатрат", РезультатЗапроса[0]);
	Результаты.Вставить("ОбслуживающиеПроизводства", РезультатЗапроса[1]);
	Результаты.Вставить("СчетаФактурыЭкспедиторов", РезультатЗапроса[2]);
	Результаты.Вставить("НекредитныеФинансовыеОрганизации", РезультатЗапроса[3]);
	Результаты.Вставить("ОценкаПродукцииПоПлановойСтоимости", РезультатЗапроса[4]);
	Результаты.Вставить("ОбесценениеЗапасов", РезультатЗапроса[5]);
	Результаты.Вставить("ОбесценениеВнеоборотныхАктивов", РезультатЗапроса[6]);
	Результаты.Вставить("БракВПроизводстве", РезультатЗапроса[7]);
	Результаты.Вставить("АмортизацияСДатыПризнания", РезультатЗапроса[8]);
	Результаты.Вставить("КрупнейшийНалогоплательщик", РезультатЗапроса[9]);
	Результаты.Вставить("КрупнейшийНалогоплательщикИстория", РезультатЗапроса[10]);
	
	Возврат Результаты;
	
КонецФункции

Функция ОтказСнятьРасширенныйФункционал()
	
	СтруктураРезультатов = РезультатЗапросаРасширенныйФункционал();
	
	Для Каждого Результат Из СтруктураРезультатов Цикл
		Если НЕ Результат.Значение.Пустой() Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ВестиУчетПоОрганизациям

Функция ОтказСнятьВестиУчетПоОрганизациям()
	
	УсловиеВыполненоВестиУчетПоОднойОрганизации = УсловиеВыполненоВестиУчетПоОднойОрганизации(Ложь);
	Если УсловиеВыполненоВестиУчетПоОднойОрганизации Тогда
		ОтказСнять = Справочники.Организации.КоличествоОрганизацийВАрхиве() > 0;
	Иначе
		ОтказСнять = Не УсловиеВыполненоВестиУчетПоОднойОрганизации;
	КонецЕсли;
	
	Возврат ОтказСнять;
	
КонецФункции

Функция УсловиеВыполненоВестиУчетПоОднойОрганизации(ЭтоНовый) Экспорт
	
	ДопустимоеКоличествоСуществующихОрганизаций = ?(ЭтоНовый, 0, 1);
	
	Возврат Справочники.Организации.КоличествоОрганизаций() <= ДопустимоеКоличествоСуществующихОрганизаций;
	
КонецФункции

Функция ДоступнаФункциональностьУчетПоНесколькимОрганизациям() Экспорт
	
	Возврат Не ВариантыПриложений.ЭтоБазоваяВерсия();
	
КонецФункции

#КонецОбласти

#Область Сотрудники

Функция РезультатЗапросаЮридическиеЛица() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РезультатЗапросаСотрудники(ТолькоПроверка = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Сотрудники.Ссылка) КАК Представление,
	|	Сотрудники.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РезультатЗапросаКадровыеДокументы(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ПриемНаРаботу.Ссылка) КАК Представление,
	|	ПриемНаРаботу.Ссылка КАК Ссылка,
	|	ПриемНаРаботу.Дата КАК Дата
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(КадровыйПеревод.Ссылка),
	|	КадровыйПеревод.Ссылка,
	|	КадровыйПеревод.Дата
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Увольнение.Ссылка),
	|	Увольнение.Ссылка,
	|	Увольнение.Дата
	|ИЗ
	|	Документ.Увольнение КАК Увольнение
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РезультатЗапросаНеполныйРабочийДень(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(КадроваяИсторияСотрудников.Сотрудник) КАК Представление,
		|	КадроваяИсторияСотрудников.Сотрудник КАК Ссылка
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|ГДЕ
		|	КадроваяИсторияСотрудников.КоличествоСтавок > 0
		|	И КадроваяИсторияСотрудников.КоличествоСтавок <> 1";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ТекущиеКадровыеДанныеСотрудников.Сотрудник) КАК Представление,
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Ссылка
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.КоличествоСтавок > 0
		|	И ТекущиеКадровыеДанныеСотрудников.КоличествоСтавок <> 1";
	КонецЕсли;
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РезультатЗапросаКадровыеДокументыНеполныйРабочийДень(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ПриемНаРаботу.Ссылка) КАК Представление,
	|	ПриемНаРаботу.Ссылка КАК Ссылка,
	|	ПриемНаРаботу.Дата КАК Дата
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|ГДЕ
	|	ПриемНаРаботу.КоличествоСтавок > 0
	|	И ПриемНаРаботу.КоличествоСтавок <> 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(КадровыйПеревод.Ссылка),
	|	КадровыйПеревод.Ссылка,
	|	КадровыйПеревод.Дата
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|ГДЕ
	|	КадровыйПеревод.КоличествоСтавок > 0
	|	И КадровыйПеревод.КоличествоСтавок <> 1
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РезультатЗапросаДоговораГПХ(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ДоговорРаботыУслуги.Ссылка) КАК Представление,
	|	ДоговорРаботыУслуги.Ссылка КАК Ссылка,
	|	ДоговорРаботыУслуги.Дата КАК Дата
	|ИЗ
	|	Документ.ДоговорРаботыУслуги КАК ДоговорРаботыУслуги
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РезультатЗапросаСовместительство(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ТекущиеКадровыеДанныеСотрудников.Сотрудник) КАК Представление,
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Ссылка
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	НЕ ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка))";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ВидыЗанятостиСотрудников.Сотрудник) КАК Представление,
		|	ВидыЗанятостиСотрудников.Сотрудник КАК Ссылка
		|ИЗ
		|	РегистрСведений.ВидыЗанятостиСотрудников КАК ВидыЗанятостиСотрудников
		|ГДЕ
		|	НЕ ВидыЗанятостиСотрудников.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка))";
	КонецЕсли;
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция РезультатЗапросаКадровыеДокументыСовместителей(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ПриемНаРаботу.Ссылка) КАК Представление,
	|	ПриемНаРаботу.Ссылка КАК Ссылка,
	|	ПриемНаРаботу.Дата КАК Дата
	|ИЗ
	|	Документ.ПриемНаРаботу КАК ПриемНаРаботу
	|ГДЕ
	|	НЕ ПриемНаРаботу.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(КадровыйПеревод.Ссылка),
	|	КадровыйПеревод.Ссылка,
	|	КадровыйПеревод.Дата
	|ИЗ
	|	Документ.КадровыйПеревод КАК КадровыйПеревод
	|ГДЕ
	|	НЕ КадровыйПеревод.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ОтказСнятьВедетсяУчетЗарплатыКадров()
	
	УсловияДоступностиОбъектов = НовыеУсловияДоступностиДокументовПоТарифу();
	ДобавитьОграниченияОпцииСотрудники(УсловияДоступностиОбъектов);
	ПроверитьНаличиеДокументовСвязанныхСФункциональностью(УсловияДоступностиОбъектов);
	
	Возврат УсловияДоступностиОбъектов.Количество() > 0;
	
КонецФункции

Функция ОтказСнятьИспользоватьКадровыйУчет()
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Возврат Ложь;
	Иначе
		Возврат НЕ РезультатЗапросаКадровыеДокументы().Пустой();
	КонецЕсли;
	
КонецФункции

Функция ОтказСнятьИспользоватьРаботуНаНеполнуюСтавку()
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Возврат Ложь;
	Иначе
		Возврат НЕ РезультатЗапросаНеполныйРабочийДень().Пустой()
			ИЛИ НЕ РезультатЗапросаКадровыеДокументыНеполныйРабочийДень().Пустой();
	КонецЕсли;
	
КонецФункции

Функция ОтказСнятьПоддержкаСовместительства()
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Возврат Ложь;
	Иначе
		Возврат НЕ РезультатЗапросаСовместительство().Пустой()
			ИЛИ НЕ РезультатЗапросаКадровыеДокументыСовместителей().Пустой();
	КонецЕсли;
	
КонецФункции

Функция ОтказСнятьИспользоватьНачисленияПоДоговорам()
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Возврат Ложь;
	Иначе
		Возврат НЕ РезультатЗапросаДоговораГПХ().Пустой();
	КонецЕсли;
	
КонецФункции

Функция ТребуетсяДлительнаяОперацияИспользоватьКадровыйУчет()
	
	Если ОтказСнятьИспользоватьКадровыйУчет() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат УчетЗарплаты.НеобходимоСформироватьКадровыеПриказы();
	
КонецФункции

#Область ИспользоватьКадровыйУчет

Процедура ДействияПриИзмененииИспользоватьКадровыйУчет(НовоеЗначение)
	
	НачатьТранзакцию();
	
	Константы.ИспользоватьКадровыйУчет.Установить(НовоеЗначение);
	
	Если НовоеЗначение Тогда
		УчетЗарплаты.СформироватьКадровыеПриказы();
	КонецЕсли;
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

Функция РезультатЗапросаДокументыВоинскогоУчета(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ЛистокСообщенияДляВоенкомата.Ссылка) КАК Представление,
	|	ЛистокСообщенияДляВоенкомата.Ссылка КАК Ссылка,
	|	ЛистокСообщенияДляВоенкомата.Дата КАК Дата
	|ИЗ
	|	Документ.ЛистокСообщенияДляВоенкомата КАК ЛистокСообщенияДляВоенкомата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(БронированиеГражданПребывающихВЗапасе.Ссылка),
	|	БронированиеГражданПребывающихВЗапасе.Ссылка,
	|	БронированиеГражданПребывающихВЗапасе.Дата
	|ИЗ
	|	Документ.БронированиеГражданПребывающихВЗапасе КАК БронированиеГражданПребывающихВЗапасе
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ОтменаБронированияГражданПребывающихВЗапасе.Ссылка),
	|	ОтменаБронированияГражданПребывающихВЗапасе.Ссылка,
	|	ОтменаБронированияГражданПребывающихВЗапасе.Дата
	|ИЗ
	|	Документ.ОтменаБронированияГражданПребывающихВЗапасе КАК ОтменаБронированияГражданПребывающихВЗапасе
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ХодатайствоОБронированииГражданПребывающихВЗапасе.Ссылка),
	|	ХодатайствоОБронированииГражданПребывающихВЗапасе.Ссылка,
	|	ХодатайствоОБронированииГражданПребывающихВЗапасе.Дата
	|ИЗ
	|	Документ.ХодатайствоОБронированииГражданПребывающихВЗапасе КАК ХодатайствоОБронированииГражданПребывающихВЗапасе
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(СверкаСДокументамиВоинскогоУчета.Ссылка),
	|	СверкаСДокументамиВоинскогоУчета.Ссылка,
	|	СверкаСДокументамиВоинскогоУчета.Дата
	|ИЗ
	|	Документ.СверкаСДокументамиВоинскогоУчета КАК СверкаСДокументамиВоинскогоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(СверкаСВоеннымКомиссариатом.Ссылка),
	|	СверкаСВоеннымКомиссариатом.Ссылка,
	|	СверкаСВоеннымКомиссариатом.Дата
	|ИЗ
	|	Документ.СверкаСВоеннымКомиссариатом КАК СверкаСВоеннымКомиссариатом
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(СведенияОПриемеПереводеИУвольненииДляВоенкомата.Ссылка),
	|	СведенияОПриемеПереводеИУвольненииДляВоенкомата.Ссылка,
	|	СведенияОПриемеПереводеИУвольненииДляВоенкомата.Дата
	|ИЗ
	|	Документ.СведенияОПриемеПереводеИУвольненииДляВоенкомата КАК СведенияОПриемеПереводеИУвольненииДляВоенкомата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ОтказСнятьИспользоватьВоинскийУчет()
	
	Если ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровВоВнешнейПрограмме") Тогда
		Возврат Ложь;
	Иначе
		Возврат НЕ РезультатЗапросаДокументыВоинскогоУчета().Пустой();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ВедетсяУчетУслугИностранныхИнтернетКомпаний

Функция РезультатЗапросаЭлектронныеУслугиИностранцев(ТолькоПроверка = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ЭлектронныеУслуги";
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивДоговоров = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивДоговоров;
	
КонецФункции

Функция ОтказСнятьЭлектронныеУслугиИностранцев()
	
	ОтказСнять = РезультатЗапросаЭлектронныеУслугиИностранцев().Количество() > 0;
	
	Возврат ОтказСнять;
	
КонецФункции

#КонецОбласти

#Область ИспользованиеПутевыхЛистов

Функция ТекстЗапросаВедетсяУчетПоПутевымЛистам(ТолькоПроверка = Ложь)
	
	Если ТолькоПроверка Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументПутевойЛист.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПутевойЛист КАК ДокументПутевойЛист
		|ГДЕ
		|	НЕ ДокументПутевойЛист.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВводНачальныхОстатковТопливо.Ссылка
		|ИЗ
		|	Документ.ВводНачальныхОстатков.Топливо КАК ВводНачальныхОстатковТопливо
		|ГДЕ
		|	НЕ ВводНачальныхОстатковТопливо.Ссылка.ПометкаУдаления";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументПутевойЛист.Ссылка КАК Ссылка,
		|	ДокументПутевойЛист.Дата КАК Дата,
		|	ПРЕДСТАВЛЕНИЕ(ДокументПутевойЛист.Ссылка) КАК Представление
		|ИЗ
		|	Документ.ПутевойЛист КАК ДокументПутевойЛист
		|ГДЕ
		|	НЕ ДокументПутевойЛист.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВводНачальныхОстатковТопливо.Ссылка,
		|	ВводНачальныхОстатковТопливо.Ссылка.Дата,
		|	ПРЕДСТАВЛЕНИЕ(ВводНачальныхОстатковТопливо.Ссылка)
		|ИЗ
		|	Документ.ВводНачальныхОстатков.Топливо КАК ВводНачальныхОстатковТопливо
		|ГДЕ
		|	НЕ ВводНачальныхОстатковТопливо.Ссылка.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	Ссылка";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ДокументыПрепятствующиеОтключениюПутевыхЛистов()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВедетсяУчетПоПутевымЛистам();
	
	СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
	СтрокаТаблицыДокументов.ИмяДокумента = "ПутевойЛист";
	МетаданныеДокумента = Метаданные.Документы.ПутевойЛист;
	СтрокаТаблицыДокументов.СинонимДокумента = МетаданныеДокумента.Синоним;
	СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ТаблицаРезультатовЗапросов;
	
КонецФункции

#КонецОбласти

#Область Поиск

Функция ВыделитьОсновуСловДляПоиска(КлючевыеСлова) Экспорт
	
	МинимальнаяДлинаСлова = 2;
	
	Для Индекс = 0 По КлючевыеСлова.ВГраница() Цикл
		КлючевыеСлова[Индекс] =
			РаботаСТекстомКлиентСервер.ОбработатьСловаДляПоиска(КлючевыеСлова[Индекс], МинимальнаяДлинаСлова);
	КонецЦикла;
	
	Возврат КлючевыеСлова;
	
КонецФункции

#КонецОбласти

#Область ИспользуетсяДоставкаДоставкаТранспортнойКомпанией

Функция ДокументыПрепятствующиеОтключениюДоставкаТранспортнойКомпанией()

	ТаблицаРезультатовЗапросов = ПустаяТаблицаДокументовПрепятствующиеОтключениюОпции();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Ссылка КАК Представление
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.СпособДоставки <> ЗНАЧЕНИЕ(Справочник.СпособыДоставки.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетНаОплатуПокупателю.Ссылка,
	|	СчетНаОплатуПокупателю.Дата,
	|	СчетНаОплатуПокупателю.Ссылка
	|ИЗ
	|	Документ.СчетНаОплатуПокупателю КАК СчетНаОплатуПокупателю
	|ГДЕ
	|	СчетНаОплатуПокупателю.СпособДоставки <> ЗНАЧЕНИЕ(Справочник.СпособыДоставки.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтрокаТаблицыДокументов = ТаблицаРезультатовЗапросов.Добавить();
	СтрокаТаблицыДокументов.ИмяДокумента = Метаданные.Документы.РеализацияТоваровУслуг.Имя;
	СтрокаТаблицыДокументов.СинонимДокумента = Метаданные.Документы.РеализацияТоваровУслуг.Синоним;
	СтрокаТаблицыДокументов.РезультатЗапроса = Запрос.Выполнить();
	
	// Отсортируем по алфавиту
	ТаблицаРезультатовЗапросов.Сортировать("СинонимДокумента");
	
	Возврат ТаблицаРезультатовЗапросов;

КонецФункции

#КонецОбласти

#Область ОграниченияОбъектовПоТарифу

Процедура ДобавитьОграниченияОпцииДенежныеДокументы(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетДенежныхДокументов", Тип("ДокументСсылка.ПоступлениеДенежныхДокументов"));
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетДенежныхДокументов", Тип("ДокументСсылка.ВыдачаДенежныхДокументов"));
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииПлатежныеТребования(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользоватьПлатежныеТребования", Тип("ДокументСсылка.ПлатежноеТребование"));
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииМалоценноеОборудование(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетСпецодеждыСпецоснастки", Тип("ДокументСсылка.ВозвратМатериаловИзЭксплуатации"));
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетСпецодеждыСпецоснастки", Тип("ДокументСсылка.ВыработкаМатериалов"));
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетСпецодеждыСпецоснастки", Тип("ДокументСсылка.ПартияМатериаловВЭксплуатации"));
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетСпецодеждыСпецоснастки", Тип("ДокументСсылка.СписаниеМатериаловИзЭксплуатации"));
		
	ОграничениеПередачаМатериалов = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетСпецодеждыСпецоснастки", Тип("ДокументСсылка.ПередачаМатериаловВЭксплуатацию"));
	НедоступныеТабличныеЧасти = "Спецодежда,Спецоснастка,ИнвентарьИХозяйственныеПринадлежности";
	УсловиеТабличныеЧасти = ОграничениеЗаполненныеТабличныеЧасти(НедоступныеТабличныеЧасти);
	ОграничениеПередачаМатериалов.УсловияПроверки.Добавить(УсловиеТабличныеЧасти);
	ОграничениеПередачаМатериалов.ДополнениеЗаголовка = НСтр("ru = 'спецодежда, спецоснастка, инвентарь'");

КонецПроцедуры

Процедура ДобавитьОграниченияОпцииФакторинговыеОперации(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользоватьФакторинг", Тип("ДокументСсылка.ПередачаЗадолженностиНаФакторинг"));
	
	ОграничениеДокументПоступление = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользоватьФакторинг", Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиФакторинга);
	ОграничениеДокументПоступление.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументПоступление.ДополнениеЗаголовка = НСтр("ru = 'услуги факторинга'");
	
	ОграничениеДокументПоступлениеНаСчет = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользоватьФакторинг", Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаОтФакторинговойКомпании);
	ОграничениеДокументПоступлениеНаСчет.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументПоступлениеНаСчет.ДополнениеЗаголовка = НСтр("ru = 'оплата от факторинговой компании'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииВыплатыСамозанятым(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользоватьВыплатыСамозанятым", Тип("ДокументСсылка.ВыплатыСамозанятым"));
	
	ОграничениеДокументСписаниеСРасчетногоСчета = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользоватьВыплатыСамозанятым", Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	НедоступныеВидыОпераций = Новый Массив;
	НедоступныеВидыОпераций.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыплатыСамозанятым);
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту("ВидОперации", НедоступныеВидыОпераций);
	ОграничениеДокументСписаниеСРасчетногоСчета.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументСписаниеСРасчетногоСчета.ДополнениеЗаголовка = НСтр("ru = 'выплаты самозанятым'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииКонтролируемыеСделки(УсловияДоступности)
	
	Если ВариантыПриложений.ЭтоВерсияКОРП() Тогда
		
		РасширенныйФункционал.ДобавитьОграниченияОпцииКонтролируемыеСделки(УсловияДоступности);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииНалоговыйАгентПоНДС(УсловияДоступности)
	
	ОграничениеДокументСписаниеСРасчетногоСчета = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИсполняютсяОбязанностиНалоговогоАгентаПоНДС", Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту("ДоговорКонтрагента.УчетАгентскогоНДС", Истина);
	ОграничениеДокументСписаниеСРасчетногоСчета.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументСписаниеСРасчетногоСчета.ДополнениеЗаголовка = НСтр("ru = 'организация - налоговый агент по НДС'");
	
	ОграничениеДокументВыдачаНаличных = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИсполняютсяОбязанностиНалоговогоАгентаПоНДС", Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту("ДоговорКонтрагента.УчетАгентскогоНДС", Истина);
	ОграничениеДокументВыдачаНаличных.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументВыдачаНаличных.ДополнениеЗаголовка = НСтр("ru = 'организация - налоговый агент по НДС'");
	
	ОграничениеДокументСчетФактура = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИсполняютсяОбязанностиНалоговогоАгентаПоНДС", Тип("ДокументСсылка.СчетФактураВыданный"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДокументОснование.ДоговорКонтрагента.УчетАгентскогоНДС", Истина);
	ОграничениеДокументСчетФактура.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументСчетФактура.ДополнениеЗаголовка = НСтр("ru = 'организация - налоговый агент по НДС'");
	
	ОграничениеДокументПоступление = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИсполняютсяОбязанностиНалоговогоАгентаПоНДС", Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту("ДоговорКонтрагента.УчетАгентскогоНДС", Истина);
	ОграничениеДокументПоступление.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПоступление.ДополнениеЗаголовка = НСтр("ru = 'организация - налоговый агент по НДС'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииОбратноеНачислениеНДС(УсловияДоступности)
	
	Если Не ВариантыПриложений.ЭтоВерсияКОРП() Тогда
		Возврат;
	КонецЕсли;
	
	ОграничениеДокументПоступлениеНаСчет = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ОбратноеНачислениеНДС", Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту("ДоговорКонтрагента.УчетАгентскогоНДСПокупателем", Истина);
	ОграничениеДокументПоступлениеНаСчет.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПоступлениеНаСчет.ДополнениеЗаголовка = НСтр("ru = 'обратное начисление НДС'");
	
	ОграничениеДокументПоступлениеНаличных = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ОбратноеНачислениеНДС", Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту("ДоговорКонтрагента.УчетАгентскогоНДСПокупателем", Истина);
	ОграничениеДокументПоступлениеНаличных.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПоступлениеНаличных.ДополнениеЗаголовка = НСтр("ru = 'обратное начисление НДС'");
	
	ОграничениеДокументРеализация = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ОбратноеНачислениеНДС", Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту("ДоговорКонтрагента.УчетАгентскогоНДСПокупателем", Истина);
	ОграничениеДокументРеализация.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументРеализация.ДополнениеЗаголовка = НСтр("ru = 'обратное начисление НДС'");
	
	ОграничениеДокументСчетФактура = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ОбратноеНачислениеНДС", Тип("ДокументСсылка.СчетФактураВыданный"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДокументОснование.ДоговорКонтрагента.УчетАгентскогоНДСПокупателем", Истина);
	ОграничениеДокументСчетФактура.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументСчетФактура.ДополнениеЗаголовка = НСтр("ru = 'обратное начисление НДС'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииОтгрузкаБезПереходаПраваСобственности(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности,
		"ВедетсяОтгрузкаБезПереходаПраваСобственности",
		Тип("ДокументСсылка.РеализацияОтгруженныхТоваров"));
	
	ОграничениеДокументРеализация = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяОтгрузкаБезПереходаПраваСобственности", Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийРеализацияТоваров.ОтгрузкаБезПереходаПраваСобственности);
	ОграничениеДокументРеализация.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументРеализация.ДополнениеЗаголовка = НСтр("ru = 'отгрузка без перехода права собственности'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииПакетноеВыставлениеАктов(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользоватьПакетноеВыставлениеАктов", Тип("ДокументСсылка.ОказаниеУслуг"));
	
КонецПроцедуры
	
Процедура ДобавитьОграниченияОпцииНачислениеНДССМежценовойРазницы(УсловияДоступности)
		
	ОграничениеДокументРеализация = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользоватьНачислениеНДССМежценовойРазницы", Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"НДССМежценовойРазницы", Истина);
	ОграничениеДокументРеализация.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументРеализация.ДополнениеЗаголовка = НСтр("ru = 'НДС с межценовой разницы'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииПодарочныеСертификаты(УсловияДоступности)
	
	ОграничениеОтчетОРозничныхПродажах = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользуютсяПодарочныеСертификаты", Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
	
	УсловиеПодарочныеСертификаты = ОграничениеЗаполненныеТабличныеЧасти("ПодарочныеСертификаты");
	ОграничениеОтчетОРозничныхПродажах.УсловияПроверки.Добавить(УсловиеПодарочныеСертификаты);
	
	НедоступныеТипыОплаты = Новый Массив;
	НедоступныеТипыОплаты.Добавить(Перечисления.ТипыОплат.ПодарочныйСертификатСобственный);
	НедоступныеТипыОплаты.Добавить(Перечисления.ТипыОплат.ПодарочныйСертификатСторонний);
	УсловиеВидОплаты = ОграничениеРеквизитТабличнойЧасти("Оплата", "ВидОплаты.ТипОплаты", НедоступныеТипыОплаты);
	ОграничениеОтчетОРозничныхПродажах.УсловияПроверки.Добавить(УсловиеВидОплаты);
	
	ОграничениеОтчетОРозничныхПродажах.ДополнениеЗаголовка = НСтр("ru = 'подарочные сертификаты'");
	
	ОграничениеЧек = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ИспользуютсяПодарочныеСертификаты", Тип("ДокументСсылка.РозничнаяПродажа"));
	УсловиеПодарочныеСертификаты = ОграничениеЗаполненныеТабличныеЧасти("ПодарочныеСертификаты");
	ОграничениеЧек.УсловияПроверки.Добавить(УсловиеПодарочныеСертификаты);
	ОграничениеЧек.ДополнениеЗаголовка = НСтр("ru = 'подарочные сертификаты'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииАлкогольнаяПродукция(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.АктПостановкиНаБалансЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.АктСписанияЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ВозвратИзРегистра2ЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ЗапросАкцизныхМарокЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ОстаткиЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ОтчетЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ПередачаВРегистр2ЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ТранспортнаяНакладнаяЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ТТНВходящаяЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ТТНИсходящаяЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ЧекЕГАИС"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетРозничнойПродажиАлкогольнойПродукцииПива",
		Тип("ДокументСсылка.ЧекЕГАИСВозврат"));
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииПродажаТоваровУслугКомитентов(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ОсуществляетсяРеализацияТоваровУслугКомитентов",
		Тип("ДокументСсылка.ОтчетКомитентуОПродажах"));
		
	ОграничениеДокументПоступление = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяРеализацияТоваровУслугКомитентов", Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	ОграничениеДокументПоступление.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПоступление.ДополнениеЗаголовка = НСтр("ru = 'договор с комитентом'");
	
	ОграничениеРеализацияТоваров = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ОсуществляетсяРеализацияТоваровУслугКомитентов", Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	
	УсловиеАгентскиеУслуги = ОграничениеЗаполненныеТабличныеЧасти("АгентскиеУслуги");
	ОграничениеРеализацияТоваров.УсловияПроверки.Добавить(УсловиеАгентскиеУслуги);
	ОграничениеРеализацияТоваров.ДополнениеЗаголовка = НСтр("ru = 'реализация услуг комитентов'");
	
	ОграничениеДокументСписаниеСРасчетногоСчета = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяРеализацияТоваровУслугКомитентов", Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);
	ОграничениеДокументСписаниеСРасчетногоСчета.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументСписаниеСРасчетногоСчета.ДополнениеЗаголовка = НСтр("ru = 'договор с комитентом'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииПродажаТоваровУслугЧерезКомиссионеров(УсловияДоступности)
	
	ОграничениеОтчетКомиссионера =
		ДобавитьОграничениеДоступности(УсловияДоступности, "ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров",
		Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОПродажах);
	ОграничениеОтчетКомиссионера.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеОтчетКомиссионера.ДополнениеЗаголовка = НСтр("ru = 'оптовые продажи'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииПриобретениеТоваровУслугДляКомитентов(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ОсуществляетсяЗакупкаТоваровУслугДляКомитентов",
		Тип("ДокументСсылка.ОтчетКомитентуОПродажах"));
	
	ОграничениеДокументПоступлениеНаСчет = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяЗакупкаТоваровУслугДляКомитентов", Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);
	ОграничениеДокументПоступлениеНаСчет.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПоступлениеНаСчет.ДополнениеЗаголовка = НСтр("ru = 'договор с комитентом'");
	
	ОграничениеПоступлениеТоваров = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ОсуществляетсяЗакупкаТоваровУслугДляКомитентов", Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
	
	УсловиеАгентскиеУслуги = ОграничениеЗаполненныеТабличныеЧасти("АгентскиеУслуги");
	ОграничениеПоступлениеТоваров.УсловияПроверки.Добавить(УсловиеАгентскиеУслуги);
	ОграничениеПоступлениеТоваров.ДополнениеЗаголовка = НСтр("ru = 'приобретение услуг для комитентов'");
	
	ОграничениеДокументСчетФактура = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяЗакупкаТоваровУслугДляКомитентов", Тип("ДокументСсылка.СчетФактураВыданный"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);
	ОграничениеДокументСчетФактура.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументСчетФактура.ДополнениеЗаголовка = НСтр("ru = 'договор с комитентом'");
	
	ОграничениеДокументПоступление = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяЗакупкаТоваровУслугДляКомитентов", Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);
	ОграничениеДокументПоступление.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПоступление.ДополнениеЗаголовка = НСтр("ru = 'договор с комитентом'");
	
	ОграничениеДокументПередача = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяЗакупкаТоваровУслугДляКомитентов", Тип("ДокументСсылка.ПередачаТоваров"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);
	ОграничениеДокументПередача.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПередача.ДополнениеЗаголовка = НСтр("ru = 'договор с комитентом'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииПриобретениеТоваровУслугЧерезКомиссионеров(УсловияДоступности)
	
	ОграничениеДокументСписаниеСРасчетногоСчета = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров", Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	ОграничениеДокументСписаниеСРасчетногоСчета.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументСписаниеСРасчетногоСчета.ДополнениеЗаголовка = НСтр("ru = 'договор с комиссионером'");
	
	ОграничениеДокументПоступление = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров", Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	ОграничениеДокументПоступление.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПоступление.ДополнениеЗаголовка = НСтр("ru = 'договор с комиссионером'");
	
	ОграничениеДокументПоступлениеДопРасходов = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ОсуществляетсяЗакупкаТоваровУслугЧерезКомиссионеров", Тип("ДокументСсылка.ПоступлениеДопРасходов"));
	УсловиеПоВидуДоговора = ОграничениеПоДочернемуРеквизиту(
		"ДоговорКонтрагента.ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	ОграничениеДокументПоступлениеДопРасходов.УсловияПроверки.Добавить(УсловиеПоВидуДоговора);
	ОграничениеДокументПоступлениеДопРасходов.ДополнениеЗаголовка = НСтр("ru = 'договор с комиссионером'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииИспользоватьПродажиЧерезМаркетплейс(УсловияДоступности)
	
	ОграничениеОтчетКомиссионера = ДобавитьОграничениеДоступности(УсловияДоступности, 
		"ИспользоватьПродажиЧерезМаркетплейс", Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах);
	ОграничениеОтчетКомиссионера.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеОтчетКомиссионера.ДополнениеЗаголовка = НСтр("ru = 'розничные продажи'");
	
	ОграничениеОтчетКомиссионера = ДобавитьОграничениеДоступности(УсловияДоступности, 
		"ИспользоватьПродажиЧерезМаркетплейс", Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОСписании);
	ОграничениеОтчетКомиссионера.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеОтчетКомиссионера.ДополнениеЗаголовка = НСтр("ru = 'отчет о списании'");
	
	ОграничениеОтчетКомиссионера = ДобавитьОграничениеДоступности(УсловияДоступности, 
		"ИспользоватьПродажиЧерезМаркетплейс", Тип("ДокументСсылка.ОтчетКомиссионераОПродажах"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийОтчетКомиссионера.ОтчетОбОприходовании);
	ОграничениеОтчетКомиссионера.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеОтчетКомиссионера.ДополнениеЗаголовка = НСтр("ru = 'отчет об оприходовании'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииИмпортТоваров(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетИмпортаТоваров", Тип("ДокументСсылка.ГТДИмпорт"));
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетИмпортаТоваров", Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров"));
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииЭкспортТоваров(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетТаможенныхДекларацийЭкспорт",
		Тип("ДокументСсылка.ТаможеннаяДекларацияЭкспорт"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетТаможенныхДекларацийЭкспорт",
		Тип("ДокументСсылка.ЗаявлениеОВвозеТоваровПолученное"));
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииМаркировкаМеховыхИзделий(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВестиУчетМаркировкиПродукцииВГИСМ",
		Тип("ДокументСсылка.ЗаявкаНаВыпускКиЗГИСМ"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВестиУчетМаркировкиПродукцииВГИСМ",
		Тип("ДокументСсылка.УведомлениеОбИмпортеМаркированныхТоваровГИСМ"));
		
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВестиУчетМаркировкиПродукцииВГИСМ",
		Тип("ДокументСсылка.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВестиУчетМаркировкиПродукцииВГИСМ",
		Тип("ДокументСсылка.УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВестиУчетМаркировкиПродукцииВГИСМ",
		Тип("ДокументСсылка.УведомлениеОПоступленииМаркированныхТоваровГИСМ"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВестиУчетМаркировкиПродукцииВГИСМ",
		Тип("ДокументСсылка.УведомлениеОСписанииКиЗГИСМ"));
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииИспользоватьМаркировку(УсловияДоступности)
	
	Возврат;
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииКомплексныйУчетПроизводства(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяПроизводственнаяДеятельность",
		Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяПроизводственнаяДеятельность",
		Тип("ДокументСсылка.ИнвентаризацияНЗП"));
		
	ОграничениеОтчетПроизводства = ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяПроизводственнаяДеятельность", Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийОтчетПроизводстваЗаСмену.ОтчетПроизводстваЗаСмену);
	ОграничениеОтчетПроизводства.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеОтчетПроизводства.ДополнениеЗаголовка = НСтр("ru = 'отчет производства за смену'");
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяПроизводственнаяДеятельность",
		Тип("ДокументСсылка.ПоступлениеИзПереработки"));
		
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяПроизводственнаяДеятельность",
		Тип("ДокументСсылка.РеализацияУслугПоПереработке"));
		
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяПроизводственнаяДеятельность",
		Тип("ДокументСсылка.УстановкаПорядкаЗакрытияПодразделений"));
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииЛизинг(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЛизинговогоИмущества",
		Тип("ДокументСсылка.ВыкупПредметовЛизинга"));
	
	ОграничениеДокументПоступлениеВАренду = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ВедетсяУчетЛизинговогоИмущества", Тип("ДокументСсылка.ПоступлениеВАренду"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийПоступлениеВАренду.ПоступлениеВЛизинг);
	ОграничениеДокументПоступлениеВАренду.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументПоступлениеВАренду.ДополнениеЗаголовка = НСтр("ru = 'поступление в лизинг'");
	
	ОграничениеДокументПоступление = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ВедетсяУчетЛизинговогоИмущества", Тип("ДокументСсылка.ПоступлениеТоваровУслуг"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга);
	ОграничениеДокументПоступление.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументПоступление.ДополнениеЗаголовка = НСтр("ru = 'услуги лизинга'");
	
	ОграничениеДокументИзменениеУсловий = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ВедетсяУчетЛизинговогоИмущества", Тип("ДокументСсылка.ИзменениеУсловийАренды"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийИзменениеУсловийАренды.ИзменениеУсловийЛизинга);
	ОграничениеДокументИзменениеУсловий.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументИзменениеУсловий.ДополнениеЗаголовка = НСтр("ru = 'изменение условий лизинга'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииПредметыАренды(УсловияДоступности)
	
	ОграничениеДокументПоступлениеВАренду = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ВедетсяУчетПредметовАренды", Тип("ДокументСсылка.ПоступлениеВАренду"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийПоступлениеВАренду.ПоступлениеВАренду);
	ОграничениеДокументПоступлениеВАренду.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументПоступлениеВАренду.ДополнениеЗаголовка = НСтр("ru = 'поступление в аренду'");
	
	ОграничениеДокументИзменениеУсловий = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ВедетсяУчетПредметовАренды", Тип("ДокументСсылка.ИзменениеУсловийАренды"));
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту(
		"ВидОперации", Перечисления.ВидыОперацийИзменениеУсловийАренды.ИзменениеУсловийАренды);
	ОграничениеДокументИзменениеУсловий.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументИзменениеУсловий.ДополнениеЗаголовка = НСтр("ru = 'изменение условий аренды'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииКомпонентыАмортизации(УсловияДоступности)
	
	КомпонентыАмортизацииОС.ДобавитьОграниченияОпцииКомпонентыАмортизации(УсловияДоступности);
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииНематериальныеАктивы(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетНМА", Тип("ДокументСсылка.ВыработкаНМА"));
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетНМА", Тип("ДокументСсылка.ИзменениеСпециальногоКоэффициентаНМА"));
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетНМА", Тип("ДокументСсылка.ИзменениеСпособовОтраженияРасходовПоАмортизацииНМА"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетНМА", Тип("ДокументСсылка.ПередачаНМА"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетНМА", Тип("ДокументСсылка.ПоступлениеНМА"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетНМА", Тип("ДокументСсылка.ПринятиеКУчетуНМА"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетНМА", Тип("ДокументСсылка.СписаниеНМА"));
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииУчетПоНесколькимОрганизациям(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВестиУчетПоОрганизациям", Неопределено);
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииОбособленныеПодразделения(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВестиУчетПоПодразделениям", Неопределено);
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииРасширенныйФункционал(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "РасширенныйФункционал", Неопределено);
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииСотрудники(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.ПриемНаРаботу"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.Увольнение"));
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.КадровыйПеревод"));
	
	ДобавитьОграничениеДоступности(
		УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплаты"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВБанк"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.ВедомостьНаВыплатуЗарплатыВКассу"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.ДепонированиеЗарплаты"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.НачислениеЗарплаты"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.СписаниеДепонированнойЗарплаты"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.ИсполнительныйЛист"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.Отпуск"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.ОтпускБезСохраненияОплаты"));
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.ПриемНаРаботу"));
	
	ОграничениеДокументСписаниеСРасчетногоСчета = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	НедоступныеОперации = Новый Массив;
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику);
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда);
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда);
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП);
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДепонентов);
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту("ВидОперации", НедоступныеОперации);
	ОграничениеДокументСписаниеСРасчетногоСчета.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументСписаниеСРасчетногоСчета.ДополнениеЗаголовка = НСтр("ru = 'заработная плата'");
	
	ОграничениеДокументРасход = ДобавитьОграничениеДоступности(УсловияДоступности,
		"ВедетсяУчетЗарплатыКадров", Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	НедоступныеОперации = Новый Массив;
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийРКО.ВыплатаДепонентов);
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыПоВедомостям);
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыРаботнику);
	НедоступныеОперации.Добавить(Перечисления.ВидыОперацийРКО.ВыплатаСотрудникуПоДоговоруПодряда);
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту("ВидОперации", НедоступныеОперации);
	ОграничениеДокументРасход.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеДокументРасход.ДополнениеЗаголовка = НСтр("ru = 'заработная плата'");
	
КонецПроцедуры

Процедура ДобавитьОграниченияОпцииРозничнаяТорговля(УсловияДоступности)
	
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяРозничнаяТорговля", 
		Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
	ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяРозничнаяТорговля", 
		Тип("ДокументСсылка.РозничнаяПродажа"));
	
КонецПроцедуры  

Процедура ДобавитьОграниченияОпцииПредоплатыВРознице(УсловияДоступности)
	
	ОграничениеЧек = ДобавитьОграничениеДоступности(УсловияДоступности, "ИспользоватьПредоплатыВРознице", 
		Тип("ДокументСсылка.РозничнаяПродажа"));
		
	НедоступныеВидыОпераций = Новый Массив;
	НедоступныеВидыОпераций.Добавить(Перечисления.ВидыОперацийРозничнаяПродажа.Предоплата);
	УсловиеПоВидуОперации = ОграничениеПоДочернемуРеквизиту("ВидОперации", НедоступныеВидыОпераций);
	ОграничениеЧек.УсловияПроверки.Добавить(УсловиеПоВидуОперации);
	ОграничениеЧек.ДополнениеЗаголовка = НСтр("ru = 'чеки на предоплату'");
	
	ОграничениеОРП = ДобавитьОграничениеДоступности(УсловияДоступности, "ВедетсяРозничнаяТорговля", 
		Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
	УсловиеТабличныеЧасти = ОграничениеЗаполненныеТабличныеЧасти("ОплатаСчетов,Предоплаты,ЗачетАвансов");
	ОграничениеОРП.УсловияПроверки.Добавить(УсловиеТабличныеЧасти);
	ОграничениеОРП.ДополнениеЗаголовка = НСтр("ru = 'предоплаты'");
	
КонецПроцедуры  

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Конструктор описания условий доступности документов
//
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * Функциональность - Строка - имя функциональной опции, в случае недоступности которой
//                        требуется проверка согласно условиям описанным далее
//   * ТипОбъекта - Тип - тип блокируемого объекта. Пустое значение означает необходимость блокировки всех документов
//   * УсловияПроверки -  Массив из Структура - условия срабатывания блокировки документа. Если массив пустой, то
//                        документ блокируется безусловно. Структура обязательно содержит элемент
//                        с ключом "ИмяОграничения", который определяет характер проводимых проверок
//   * ДополнениеЗаголовка - Строка - дополнительное описание для списка документов, недоступных по тарифу.
//                        Используется, когда блокируются не все документы данного типа, а только соответствующие
//                        определенному условию - например, имеющие определенный вид операции
//
Функция НовыеУсловияДоступностиДокументовПоТарифу()
	
	УсловияДоступности = Новый ТаблицаЗначений;
	УсловияДоступности.Колонки.Добавить("Функциональность", ОбщегоНазначения.ОписаниеТипаСтрока(256));
	УсловияДоступности.Колонки.Добавить("ТипОбъекта", Новый ОписаниеТипов("Тип"));
	УсловияДоступности.Колонки.Добавить("УсловияПроверки", Новый ОписаниеТипов("Массив"));
	УсловияДоступности.Колонки.Добавить("ДополнениеЗаголовка", ОбщегоНазначения.ОписаниеТипаСтрока(256));
	
	Возврат УсловияДоступности;
	
КонецФункции

Функция ДобавитьОграничениеДоступности(ВсеУсловия, Функциональность, ТипОбъекта) Экспорт
	
	СтрокаУсловий = ВсеУсловия.Добавить();
	СтрокаУсловий.Функциональность = Функциональность;
	СтрокаУсловий.ТипОбъекта = ТипОбъекта;
	
	Возврат СтрокаУсловий;
	
КонецФункции

Функция ОграничениеЗаполненныеТабличныеЧасти(НедоступныеТабличныеЧасти) Экспорт
	
	Ограничение = Новый Структура;
	Ограничение.Вставить("ИмяОграничения", ТарификацияБП.ИмяОграниченияНаЗаполненныеТабличныеЧасти());
	Ограничение.Вставить("Значение", СтрРазделить(НедоступныеТабличныеЧасти, ",", Ложь));
	
	Возврат Ограничение;
	
КонецФункции

Функция ОграничениеПоДочернемуРеквизиту(ПутьКРеквизиту, Знач ЗначениеОграничения)
	
	Если ТипЗнч(ЗначениеОграничения) <> Тип("Массив") Тогда
		ЗначениеОграничения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗначениеОграничения);
	КонецЕсли;
	
	Ограничение = Новый Структура;
	Ограничение.Вставить("ИмяОграничения", ТарификацияБП.ИмяОграниченияПоДочернемуРеквизиту());
	Ограничение.Вставить("Путь", ПутьКРеквизиту);
	Ограничение.Вставить("Значение", ЗначениеОграничения);
	
	Возврат Ограничение;
	
КонецФункции

Функция ОграничениеРеквизитТабличнойЧасти(Таблицы, ПутьКРеквизиту, Знач ЗначениеОграничения)
	
	Ограничение = Новый Структура;
	Ограничение.Вставить("ИмяОграничения", ТарификацияБП.ИмяОграниченияПоРеквизитуТабличнойЧасти());
	Ограничение.Вставить("Таблицы", СтрРазделить(Таблицы, ",", Ложь));
	Ограничение.Вставить("Путь", ПутьКРеквизиту);
	Если ТипЗнч(ЗначениеОграничения) <> Тип("Массив") Тогда
		ЗначениеОграничения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗначениеОграничения);
	КонецЕсли;
	Ограничение.Вставить("Значение", ЗначениеОграничения);
	
	Возврат Ограничение;
	
КонецФункции

Функция ЗапросПоУсловиямДоступностиОбъектов(ПроверяемыеДокументы, ТолькоПроверка = Истина)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	Запрос = Новый Запрос;
	МаксимальныйИндекс = ПроверяемыеДокументы.Количество() - 1;
	ПараметрыЗапроса = Новый Структура;
	
	Для Индекс = 0 По МаксимальныйИндекс Цикл
		Если Индекс = 0 Тогда
			ПакетЗапросов = СхемаЗапроса.ПакетЗапросов[0];
		Иначе
			ПакетЗапросов = СхемаЗапроса.ПакетЗапросов.Добавить(Тип("ЗапросВыбораСхемыЗапроса"));
		КонецЕсли;
		
		Строка = ПроверяемыеДокументы[Индекс];
		
		ТарификацияБП.ДополнитьСхемуЗапросаДляПроверкиНаличияДокументов(
			ПакетЗапросов, Строка, ПараметрыЗапроса);
		
	КонецЦикла;
	
	Если ТолькоПроверка Тогда
		Для Каждого Оператор Из ПакетЗапросов.Операторы Цикл
			Оператор.КоличествоПолучаемыхЗаписей = 1;
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Для Каждого ЭлементПараметра Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ЭлементПараметра.Ключ, ЭлементПараметра.Значение);
	КонецЦикла;
	
	Возврат Запрос;
	
КонецФункции

Процедура ЗаполнитьСписокДокументовПрепятствующихОтключениюОпции(УсловияДоступностиОбъектов, АдресХранилища)
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Результат", Новый ТабличныйДокумент);

	Если Не ЗначениеЗаполнено(УсловияДоступностиОбъектов) Тогда
		ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Запрос = ЗапросПоУсловиямДоступностиОбъектов(УсловияДоступностиОбъектов, Ложь);
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	МаксимальныйИндекс = УсловияДоступностиОбъектов.Количество() - 1;
	Для Индекс = 0 По МаксимальныйИндекс Цикл
		Результат = РезультатыЗапроса[Индекс];
		Если Результат.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		СтрокаОграничения = УсловияДоступностиОбъектов[Индекс];
		Выборка = Результат.Выбрать();
		ПараметрыШапки = Новый Структура;
		Заголовок = СтрШаблон("%1%2", Строка(СтрокаОграничения.ТипОбъекта),
			?(ЗначениеЗаполнено(СтрокаОграничения.ДополнениеЗаголовка), ", " + СтрокаОграничения.ДополнениеЗаголовка, ""));
		ПараметрыШапки.Вставить("Заголовок", Заголовок);
		ЗаполнитьТабличныйДокументПоДаннымВыборки(
			СтруктураРезультата.Результат,
			"Строка",
			Выборка,
			,
			"Шапка",
			ПараметрыШапки);
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(СтруктураРезультата, АдресХранилища);
	
КонецПроцедуры

Функция ВсяФункциональностьПоРазделу(ИмяРаздела, ОписаниеФункциональности)
	
	Результат = Новый Массив;
	
	ОтборПоРазделу = Новый Структура("Раздел", ИмяРаздела);
	ФункциональностьРаздела = ОписаниеФункциональности.НайтиСтроки(ОтборПоРазделу);
	Для Каждого СтрокаФункциональности Из ФункциональностьРаздела Цикл
		Результат.Добавить(СтрокаФункциональности.Имя);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область ИспользоватьЛиды

Процедура ЗаполнитьОписаниеРазделаЛиды(ОписаниеФункциональности)
	
	Раздел = ФункциональностьПрограммыКлиентСервер.РазделЛиды();
	
	Функциональность = ДобавитьОписаниеФункциональности(
		ОписаниеФункциональности,
		Раздел,
		ФункциональностьПрограммыВызовСервера.НаименованиеОпцииЛиды(),
		ФункциональностьПрограммыКлиентСервер.ПредставлениеРазделаЛиды(),
		НСтр("ru = 'Взаимодействие с потенциальными клиентами и покупателями'"));
	Функциональность.ДоступнаВПростомИнтерфейсе = Истина;
	Функциональность.ПроверяютсяУсловияТарификации = Истина;
	
КонецПроцедуры

Функция ОтказСнятьИспользоватьЛиды()
	
	// Опцию Взаимодействие с лидами и покупателями можно отключить, если пользователь не пользовался элементами CRM
	// - нет ни одного лида
	// - нет ни одной задачи по клиенту
	// - нет ни одного взаимодействия с клиентом
	РезультатПроверки = РезультатЗапросаОбъектыИспользоватьЛиды();
	
	Возврат Не РезультатПроверки.Пустой();
	
КонецФункции

Функция РезультатЗапросаОбъектыИспользоватьЛиды(ТолькоПроверка = Истина)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Представление(КонтактныеЛица.Ссылка) КАК Представление,
	|	КонтактныеЛица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛица КАК КонтактныеЛица
	|ГДЕ
	|	НЕ КонтактныеЛица.ПометкаУдаления
	|	И КонтактныеЛица.Лид
	|	И НЕ КонтактныеЛица.НекачественныйЛид
	|	И КонтактныеЛица.ОбъектВладелец <> Неопределено
	|	И ВЫРАЗИТЬ(КонтактныеЛица.ОбъектВладелец КАК Справочник.Контрагенты) = Значение(Справочник.Контрагенты.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Взаимодействия.Ссылка),
	|	Взаимодействия.Ссылка
	|ИЗ
	|	ЖурналДокументов.Взаимодействия КАК Взаимодействия
	|ГДЕ
	|	НЕ Взаимодействия.ПометкаУдаления");
	
	ИзменитьДляПроверкиТекстЗапроса(Запрос.Текст, ТолькоПроверка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область УчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров

Функция ОтказСнятьУчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров()
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаУчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров();
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОтказСнять = НЕ РезультатЗапроса.Пустой();
	
	Возврат ОтказСнять;
	
КонецФункции

Функция ТекстЗапросаУчетПоКодамВалютныхОперацийИНомерамВалютныхДоговоров(ТолькоПроверка = Истина)

	Если ТолькоПроверка Тогда

		Возврат "ВЫБРАТЬ ПЕРВЫЕ 1
		        |	ПлатежноеПоручение.Ссылка КАК Ссылка
		        |ИЗ
		        |	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
		        |ГДЕ
		        |	ПлатежноеПоручение.КодВалютнойОперации <> """"
		        |	И НЕ ПлатежноеПоручение.ПометкаУдаления
		        |
		        |ОБЪЕДИНИТЬ ВСЕ
		        |
		        |ВЫБРАТЬ ПЕРВЫЕ 1
		        |	ДоговорыКонтрагентов.Ссылка
		        |ИЗ
		        |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		        |ГДЕ
		        |	ДоговорыКонтрагентов.УникальныйНомерВалютногоКонтроля <> """"
		        |	И НЕ ДоговорыКонтрагентов.ПометкаУдаления";
		
	Иначе 
		
		Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
		        |	ПлатежноеПоручение.Ссылка КАК Ссылка,
		        |	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.Ссылка) КАК Представление
		        |ИЗ
		        |	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
		        |ГДЕ
		        |	ПлатежноеПоручение.КодВалютнойОперации <> """"
		        |	И НЕ ПлатежноеПоручение.ПометкаУдаления
		        |
		        |ОБЪЕДИНИТЬ ВСЕ
		        |
		        |ВЫБРАТЬ РАЗЛИЧНЫЕ
		        |	ДоговорыКонтрагентов.Ссылка,
		        |	ПРЕДСТАВЛЕНИЕ(ДоговорыКонтрагентов.Ссылка)
		        |ИЗ
		        |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		        |ГДЕ
		        |	ДоговорыКонтрагентов.УникальныйНомерВалютногоКонтроля <> """"
		        |	И НЕ ДоговорыКонтрагентов.ПометкаУдаления";
		
	
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ПодакцизныеТовары

Функция ОрганизацииИмпортирующиеПодакцизныеТовары() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПорядокУплатыАкцизов.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Импорт = ИСТИНА";
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Возврат МассивОрганизаций;
	
КонецФункции

Функция ОрганизацииПроизводящиеПодакцизныеТовары() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПорядокУплатыАкцизов.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПорядокУплатыАкцизов КАК ПорядокУплатыАкцизов
	|ГДЕ
	|	ПорядокУплатыАкцизов.Производство = ИСТИНА";
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Возврат МассивОрганизаций;

КонецФункции

#КонецОбласти

