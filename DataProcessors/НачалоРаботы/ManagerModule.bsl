#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция СистемаНалогообложенияПоУмолчанию() Экспорт
	
	Возврат "УпрощеннаяДоходы";
	
КонецФункции

Функция СистемыНалогообложенияДопускающиеСовмещение() Экспорт
	
	СистемыНалогообложенияДопускающиеСовмещение = Новый Массив;
	
	СистемыНалогообложенияДопускающиеСовмещение.Добавить("УпрощеннаяДоходы");
	СистемыНалогообложенияДопускающиеСовмещение.Добавить("УпрощеннаяДоходыМинусРасходы");
	СистемыНалогообложенияДопускающиеСовмещение.Добавить("Общая");
	
	Возврат СистемыНалогообложенияДопускающиеСовмещение;
	
КонецФункции

Функция СистемыНалогообложенияДопускающиеНДСПриУСН() Экспорт
	
	СистемыНалогообложенияДопускающиеНДСПриУСН = Новый Массив;
	
	СистемыНалогообложенияДопускающиеНДСПриУСН.Добавить("УпрощеннаяДоходы");
	СистемыНалогообложенияДопускающиеНДСПриУСН.Добавить("УпрощеннаяДоходыМинусРасходы");
	
	Возврат СистемыНалогообложенияДопускающиеНДСПриУСН;
	
КонецФункции

Функция СистемыНалогообложенияДопускающиеТрудНаемныхРаботников() Экспорт
	
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников = Новый Массив;
	
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.Добавить("УпрощеннаяДоходы");
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.Добавить("УпрощеннаяДоходыМинусРасходы");
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.Добавить("Патентная");
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.Добавить("ЕНВД");
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.Добавить("Общая");
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.Добавить("АУСНДоходы");
	СистемыНалогообложенияДопускающиеТрудНаемныхРаботников.Добавить("АУСНДоходыМинусРасходы");
	
	Возврат СистемыНалогообложенияДопускающиеТрудНаемныхРаботников;
	
КонецФункции

Функция ВидОрганизации(ИНН) Экспорт
	
	Возврат ?(СтрДлина(СокрЛП(ИНН)) = 10, "ЮридическоеЛицо", "ИндивидуальныйПредприниматель");
	
КонецФункции

Процедура ПодключитьИнтернетПоддержку(ПараметрыЗадания, АдресХранилища) Экспорт
	
	ДанныеАутентификации = Новый Структура("Логин, Пароль", ПараметрыЗадания.ЛогинИПП, ПараметрыЗадания.ПарольИПП);
	УстановитьПривилегированныйРежим(Истина);
	ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(ДанныеАутентификации);
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = "ПодключениеУспешно";
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатПолученияТикета = ИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки("https://api.orgregister.1c.ru/orgregister/");
		УстановитьПривилегированныйРежим(Ложь);
		Если Не ЗначениеЗаполнено(РезультатПолученияТикета.КодОшибки) Тогда
			// Проверить подключение к 1С-Контрагент
			
			НайденныеРеквизиты = РаботаСКонтрагентами.ЮридическиеЛицаПоНаименованию("ТестовыйКонтрагент");
			Если ЗначениеЗаполнено(НайденныеРеквизиты.ОписаниеОшибки) Тогда
				Если ВРЕГ(НайденныеРеквизиты.ОписаниеОшибки) = ВРЕГ("НеУказаныПараметрыАутентификации")
					ИЛИ ВРЕГ(НайденныеРеквизиты.ОписаниеОшибки) = ВРЕГ("НеУказанПароль") Тогда
					Результат = "ОшибкаАвторизации";
				ИначеЕсли ВРЕГ(НайденныеРеквизиты.ОписаниеОшибки) = ВРЕГ("Сервис1СКонтрагентНеПодключен") Тогда
					Результат = "Сервис1СКонтрагентНеПодключен";
				Иначе
					Результат = "Неопределено";
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если ВРЕГ(РезультатПолученияТикета.КодОшибки) = ВРЕГ("НеверныйЛогинИлиПароль") Тогда
				Результат = "ОшибкаАвторизации";
			ИначеЕсли ВРЕГ(РезультатПолученияТикета.КодОшибки) = ВРЕГ("ОшибкаПодключения") Тогда
				Результат = "ОшибкаПодключения";
			ИначеЕсли ВРЕГ(РезультатПолученияТикета.КодОшибки) = ВРЕГ("ОшибкаСервиса") Тогда
				Результат = "ОшибкаСервиса";
			Иначе
				Результат = "Неопределено";
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		Результат = "Неопределено";
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
	КонецПопытки;
	
	Если Результат <> "ПодключениеУспешно"
		И Результат <> "Сервис1СКонтрагентНеПодключен" Тогда
		// Отключаем интернет-поддержку
		УстановитьПривилегированныйРежим(Истина);
		ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(Неопределено);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Интернет-поддержка пользователей'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Функция ПолучитьДанныеОрганизации() Экспорт

	Результат = Новый Структура("Организация, ИНН, НаименованиеСокращенное");
	Результат.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	Если Не Результат.Организация.Пустая() Тогда
		
		ДанныеОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Результат.Организация, "ИНН, НаименованиеСокращенное");
		Результат.ИНН						 = ДанныеОрганизации.ИНН;
		Результат.НаименованиеСокращенное	 = ДанныеОрганизации.НаименованиеСокращенное;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РегистрацияОрганизации(ДанныеОрганизации, Регистрации) Экспорт
	
	Для Каждого Регистрация Из Регистрации Цикл
		Данные = Регистрация.СведенияОРегистрации.ДанныеРегистрации;
		Если ДанныеОрганизации.ИНН = Данные.ИНН Тогда
			Возврат Регистрация;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Процедура УдалитьТаблицыЗначенийИзСтруктуры(ОбрабатываемаяСтруктура) Экспорт
	
	КлючиКУдалению = Новый Массив;
	
	Для каждого Элемент Из ОбрабатываемаяСтруктура Цикл
		Если (ТипЗнч(Элемент.Значение)) = Тип("Структура") Тогда
			УдалитьТаблицыЗначенийИзСтруктуры(Элемент.Значение);
		ИначеЕсли (ТипЗнч(Элемент.Значение)) = Тип("ТаблицаЗначений") Тогда
			КлючиКУдалению.Добавить(Элемент.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Ключ Из КлючиКУдалению Цикл
		ОбрабатываемаяСтруктура.Удалить(Ключ);
	КонецЦикла;
	
КонецПроцедуры

#Область СозданиеОрганизации

Функция СоздатьОрганизациюПоИНН(ПараметрыСозданияОрганизации, Налогообложение) Экспорт
	
	ВидОрганизации = ВидОрганизации(ПараметрыСозданияОрганизации.ИНН);
	
	Параметры = Новый Структура();
	Параметры.Вставить("ВидОрганизации", ВидОрганизации);
	Параметры.Вставить("ИспользуетсяТрудНаемныхРаботников", Налогообложение.ИспользуетсяТрудНаемныхРаботников);
	Параметры.Вставить("ИНН", ПараметрыСозданияОрганизации.ИНН);
	Параметры.Вставить("Реквизиты", РеквизитыСозданияОрганизацииПоДаннымСервиса(ПараметрыСозданияОрганизации, ВидОрганизации));
	Параметры.Вставить("СистемаНалогообложения", Налогообложение.СистемаНалогообложения);

	Параметры.Вставить("СведенияОСтавкеНалога",
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Налогообложение, "СведенияОСтавкеНалога", Неопределено));

	Параметры.Вставить("ПлательщикЕНВД", Налогообложение.ПлательщикЕНВД);
	Параметры.Вставить("ПрименяетсяУСНПатент", Налогообложение.ПрименяетсяУСНПатент);
	Параметры.Вставить("ПлательщикТорговогоСбора", Налогообложение.ПлательщикТорговогоСбора);
	Параметры.Вставить("ВидБизнеса", ПараметрыСозданияОрганизации.ВидБизнеса);
	Параметры.Вставить("ТарифСтраховыхВзносов", ПараметрыСозданияОрганизации.ТарифСтраховыхВзносов);
	
	Возврат Справочники.Организации.СоздатьОрганизацию(Параметры);
	
КонецФункции

Функция РеквизитыСозданияОрганизацииПоДаннымСервиса(ПараметрыСозданияОрганизации, ВидОрганизации) Экспорт
	
	// Заполним сведения об организации из веб-сервиса
	Реквизиты = Неопределено;
	Если ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
		Реквизиты = РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН(ПараметрыСозданияОрганизации.ИНН);
		Реквизиты.Вставить("КодОКВЭД",
			?(ЗначениеЗаполнено(Реквизиты.ВидДеятельности), Реквизиты.ВидДеятельности.Код, Неопределено));
		Реквизиты.Вставить("ЭтоОКВЭД2",
			?(ЗначениеЗаполнено(Реквизиты.ВидДеятельности) И Реквизиты.ВидДеятельности.Классификатор = "ОКВЭД2", Истина, Ложь));
	Иначе
		СведенияОбОрганизации = РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН(ПараметрыСозданияОрганизации.ИНН);
		Реквизиты = Новый Структура("Наименование, НаименованиеПолное, НаименованиеСокращенное, ИНН, КПП,
			|РегистрационныйНомер, ДатаРегистрации, ЮридическийАдрес, КодОКВЭД, ЭтоОКВЭД2, ОписаниеОшибки,
			|РегистрацияВФСС, РегистрацияВНалоговомОргане, Руководитель");
		Если ЗначениеЗаполнено(СведенияОбОрганизации.ОписаниеОшибки) Тогда
			Реквизиты.ОписаниеОшибки = СведенияОбОрганизации.ОписаниеОшибки;
		ИначеЕсли ЗначениеЗаполнено(СведенияОбОрганизации.ЕГРЮЛ) Тогда
			ЕГРЮЛ = СведенияОбОрганизации.ЕГРЮЛ;
			ЗаполнитьЗначенияСвойств(Реквизиты, ЕГРЮЛ);
			Если ЗначениеЗаполнено(ЕГРЮЛ.Руководители) Тогда
				Руководитель = Новый Структура("Фамилия, Имя, Отчество, ИНН, Должность");
				ЗаполнитьЗначенияСвойств(Руководитель, ЕГРЮЛ.Руководители[0]);
				Реквизиты.Руководитель = Руководитель;
			КонецЕсли;
		Иначе
			ШаблонОписанияОшибки = НСтр("ru = 'Не удалось найти данные для заполнения реквизитов по ИНН %1.'");
			Реквизиты.ОписаниеОшибки = СтрШаблон(ШаблонОписанияОшибки, ПараметрыСозданияОрганизации.ИНН);
		КонецЕсли;
		Реквизиты.ИНН = ПараметрыСозданияОрганизации.ИНН;
	КонецЕсли;
	
	ЗаполнитьРеквизитыПоДаннымРегистрации(Реквизиты, ПараметрыСозданияОрганизации);
	Реквизиты.ОписаниеОшибки = "";
	
	Возврат Реквизиты;
	
КонецФункции

Функция ЗаполнитьРеквизитыПоДаннымРегистрации(Реквизиты, ПараметрыСозданияОрганизации)
	
	Реквизиты.ИНН = ПараметрыСозданияОрганизации.ИНН;
	
	Если ПараметрыСозданияОрганизации.Свойство("КПП")
		И ЗначениеЗаполнено(ПараметрыСозданияОрганизации.КПП)
		И Реквизиты.Свойство("КПП") Тогда
		Реквизиты.КПП = ПараметрыСозданияОрганизации.КПП;
	КонецЕсли;
	
	ВидОрганизации = ВидОрганизации(ПараметрыСозданияОрганизации.ИНН);
	
	НаименованиеОрганизации = Неопределено;
	ПараметрыСозданияОрганизации.Свойство("Наименование", НаименованиеОрганизации);
	
	Реквизиты.Вставить("ЮридическоеФизическоеЛицо", 
		?(ВидОрганизации = "ИндивидуальныйПредприниматель", 
			Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо, 
			Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо));
	
	ЗаполненЮридическийАдрес = Реквизиты.Свойство("ЮридическийАдрес") И ЗначениеЗаполнено(Реквизиты.ЮридическийАдрес);
	
	Если Не ЗаполненЮридическийАдрес
		И (ПараметрыСозданияОрганизации.Свойство("ЗначениеАдресаJSON") И ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ЗначениеАдресаJSON)
			Или ПараметрыСозданияОрганизации.Свойство("Адрес") И ЗначениеЗаполнено(ПараметрыСозданияОрганизации.Адрес)) Тогда
		
		Если ПараметрыСозданияОрганизации.Свойство("ЗначениеАдресаJSON")
			И ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ЗначениеАдресаJSON) Тогда
			ЗначениеАдресаJSON = ПараметрыСозданияОрганизации.ЗначениеАдресаJSON;
			ПредставлениеАдреса = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(ЗначениеАдресаJSON);
		Иначе
			ЗначениеАдресаJSON = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(
				ПараметрыСозданияОрганизации.Адрес, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
			ПредставлениеАдреса = ПараметрыСозданияОрганизации.Адрес;
		КонецЕсли;
		
		АдресВведенВСвободнойФорме= УправлениеКонтактнойИнформациейСлужебный.АдресВведенВСвободнойФорме(ЗначениеАдресаJSON);
		Если Не АдресВведенВСвободнойФорме Тогда
			ЮридическийАдрес = Новый Структура;
			ЮридическийАдрес.Вставить("КонтактнаяИнформация", ЗначениеАдресаJSON);
			ЮридическийАдрес.Вставить("Представление", ПредставлениеАдреса);
			ЮридическийАдрес.Вставить("Комментарий");
			
			Реквизиты.Вставить("ЮридическийАдрес", ЮридическийАдрес);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
		
		Если Не Реквизиты.Свойство("Фамилия") Или Не ЗначениеЗаполнено(Реквизиты.Фамилия) Тогда
			// Определим ФИО из наименования ИП
			ФИО = ОбщегоНазначенияБПКлиентСервер.ФамилияИмяОтчествоПоНаименованиюИП(НаименованиеОрганизации);
			Если ФИО = Неопределено Тогда
				// Определим ФИО предпринимателя по руководителю
				Если ПараметрыСозданияОрганизации.Свойство("ФамилияДиректора")
					И ЗначениеЗаполнено(ПараметрыСозданияОрганизации.ФамилияДиректора) Тогда
					
					Фамилия  = ПараметрыСозданияОрганизации.ФамилияДиректора;
					Имя      = "";
					Отчество = "";
					
					ПараметрыСозданияОрганизации.Свойство("ИмяДиректора", Имя);
					ПараметрыСозданияОрганизации.Свойство("ОтчествоДиректора", Отчество);
					
					ФИО = Новый Структура();
					ФИО.Вставить("Фамилия",  ?(ЗначениеЗаполнено(Фамилия), Фамилия, ""));
					ФИО.Вставить("Имя",      ?(ЗначениеЗаполнено(Имя), Имя, ""));
					ФИО.Вставить("Отчество", ?(ЗначениеЗаполнено(Отчество), Отчество, ""));
				КонецЕсли;
			КонецЕсли;
			Если ФИО <> Неопределено Тогда
				Реквизиты.Вставить("Фамилия",  ФИО.Фамилия);
				Реквизиты.Вставить("Имя",      ФИО.Имя);
				Реквизиты.Вставить("Отчество", ФИО.Отчество);
			КонецЕсли;
		КонецЕсли;
		// Устанавливаем наименование для организации-ИП в наименование из регистрации.
		// При создании организации, если заполнено ФИО, они будут заменены на канонические.
		УстановитьЗначениеРеквизита(Реквизиты, ПараметрыСозданияОрганизации, "НаименованиеСокращенное", "Наименование");
		УстановитьЗначениеРеквизита(Реквизиты, ПараметрыСозданияОрганизации, "НаименованиеПолное", "Наименование");
		УстановитьЗначениеРеквизита(Реквизиты, ПараметрыСозданияОрганизации, "Наименование", "Наименование");
	Иначе
		УстановитьЗначениеРеквизита(Реквизиты, ПараметрыСозданияОрганизации, "КПП", "КПП");
		
		Если ЗначениеЗаполнено(НаименованиеОрганизации) Тогда
			Если Реквизиты.Свойство("НаименованиеСокращенное")
				И Не ЗначениеЗаполнено(Реквизиты.НаименованиеСокращенное) Тогда
				Реквизиты.Вставить("НаименованиеСокращенное", НаименованиеОрганизации);
			КонецЕсли;
			
			Если Реквизиты.Свойство("НаименованиеПолное")
				И Не ЗначениеЗаполнено(Реквизиты.НаименованиеПолное) Тогда
				Реквизиты.Вставить("НаименованиеПолное", 
						ОбщегоНазначенияБПКлиентСервер.ПолноеНаименованиеПоСокращенномуНаименованию(НаименованиеОрганизации));
			КонецЕсли;
			
			Если Реквизиты.Свойство("Наименование")
				И Не ЗначениеЗаполнено(Реквизиты.Наименование) Тогда
				Реквизиты.Вставить("Наименование", 
						ОбщегоНазначенияБПКлиентСервер.НаименованиеПоСокращенномуНаименованию(НаименованиеОрганизации));
			КонецЕсли;
		КонецЕсли;
		
		// Заполняем руководителя
		Если Реквизиты.Руководитель = Неопределено Тогда
			
			Фамилия  = "";
			Имя      = "";
			Отчество = "";
			ПараметрыСозданияОрганизации.Свойство("ФамилияДиректора", Фамилия);
			ПараметрыСозданияОрганизации.Свойство("ИмяДиректора", Имя);
			ПараметрыСозданияОрганизации.Свойство("ОтчествоДиректора", Отчество);
			
			Если ЗначениеЗаполнено(Фамилия)
				И ЗначениеЗаполнено(Имя) Тогда
				Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2 %3'"),
								Фамилия, Имя, Отчество);
				
				Реквизиты.Руководитель = Новый Структура;
				Реквизиты.Руководитель.Вставить("Должность", "");
				Реквизиты.Руководитель.Вставить("Фамилия", Фамилия);
				Реквизиты.Руководитель.Вставить("Имя", Имя);
				Реквизиты.Руководитель.Вставить("Отчество", Отчество);
				Реквизиты.Руководитель.Вставить("Представление", Представление);
				Реквизиты.Руководитель.Вставить("ИНН", "");
				Реквизиты.Руководитель.Вставить("ДатаЗаписи", Дата('00010101'));
			КонецЕсли;
			
		КонецЕсли;
		
		Если Реквизиты.РегистрацияВНалоговомОргане = Неопределено
			И ЗначениеЗаполнено(Реквизиты.КПП) Тогда
			Результат = Новый Структура;
			Результат.Вставить("Код", Лев(Реквизиты.КПП, 4));
			Результат.Вставить("Наименование", "");
			Результат.Вставить("ОКТМО", "");
			Результат.Вставить("ОКАТО", "");
			Результат.Вставить("ДатаРегистрации", Дата('00010101'));
			Реквизиты.РегистрацияВНалоговомОргане = Результат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

Процедура УстановитьЗначениеРеквизита(Реквизиты, ДанныеРегистрации, ИмяРеквизита, ИмяРеквизитаРегистрации)
	
	ЗначениеРеквизита = Неопределено;
	ЗначениеРегистрации = Неопределено;
	Если Реквизиты.Свойство(ИмяРеквизита, ЗначениеРеквизита)
		И Не ЗначениеЗаполнено(ЗначениеРеквизита)
		И ДанныеРегистрации.Свойство(ИмяРеквизитаРегистрации, ЗначениеРегистрации)
		И ЗначениеЗаполнено(ЗначениеРегистрации) Тогда
		
		Реквизиты.Вставить(ИмяРеквизита, ЗначениеРегистрации);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает подходящие настройки интерфейса в зависимости от системы налогообложения: 
// простой или полный интерфейс, возможен ли учет без закрывающих документов.
// Если система налогообложения не указана - то она определяется по данным информационной базы.
//
// Параметры:
//  СистемаНалогообложения	 - Строка - Идентификатор системы налогообложения.
// 
// Возвращаемое значение:
//   - Структура - Настройки интерфейса.
//
Функция ПодходящиеНастройкиИнтерфейса(СистемаНалогообложения = "") Экспорт
	
	СистемаНалогообложенияДляИнтерфейса = ?(ЗначениеЗаполнено(СистемаНалогообложения),
		СистемаНалогообложения, НаиболееСложнаяСистемаНалогообложенияИзПрименяемых());
	
	СистемыНалогообложенияПростойИнтерфейс = Новый Массив;
	СистемыНалогообложенияПростойИнтерфейс.Добавить("УпрощеннаяДоходы");
	СистемыНалогообложенияПростойИнтерфейс.Добавить("УпрощеннаяДоходыМинусРасходы");
	СистемыНалогообложенияПростойИнтерфейс.Добавить("АУСНДоходы");
	СистемыНалогообложенияПростойИнтерфейс.Добавить("АУСНДоходыМинусРасходы");
	СистемыНалогообложенияПростойИнтерфейс.Добавить("Патентная");
	СистемыНалогообложенияПростойИнтерфейс.Добавить("ЕНВД");
	СистемыНалогообложенияПростойИнтерфейс.Добавить("НалогНаПрофессиональныйДоход");
	СистемыНалогообложенияПростойИнтерфейс.Добавить("Общая");
	
	ПростойИнтерфейс = Не Обработки.ФункциональностьПрограммы.ТребуетсяПолныйИнтерфейс()
		И СистемыНалогообложенияПростойИнтерфейс.Найти(СистемаНалогообложенияДляИнтерфейса) <> Неопределено
		И ВариантыПриложений.ЭтоБазоваяВерсия();
	
	СистемыНалогообложенияБезЗакрывающихДокументов = Новый Массив;
	СистемыНалогообложенияБезЗакрывающихДокументов.Добавить("УпрощеннаяДоходы");
	СистемыНалогообложенияБезЗакрывающихДокументов.Добавить("АУСНДоходы");
	СистемыНалогообложенияБезЗакрывающихДокументов.Добавить("Патентная");
	СистемыНалогообложенияБезЗакрывающихДокументов.Добавить("ЕНВД");
	СистемыНалогообложенияБезЗакрывающихДокументов.Добавить("НалогНаПрофессиональныйДоход");
	БезЗакрывающихДокументов = ПростойИнтерфейс
		И СистемыНалогообложенияБезЗакрывающихДокументов.Найти(СистемаНалогообложенияДляИнтерфейса) <> Неопределено;
	
	Если Не ТарификацияБП.РазрешенПолныйИнтерфейс() Тогда
		ВариантИнтерфейса = "ИнтерфейсТаксиПростой";
	Иначе
		ВариантИнтерфейса = ?(ПростойИнтерфейс, "ИнтерфейсТаксиПростой", "ИнтерфейсТакси");
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ВариантИнтерфейса", ВариантИнтерфейса);
	Результат.Вставить("БезЗакрывающихДокументов", БезЗакрывающихДокументов);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйПараметрыСозданияОрганизации() Экспорт
	
	ПараметрыСозданияОрганизации = Новый Структура;
	ПараметрыСозданияОрганизации.Вставить("ИНН", ""); // Обязательный
	ПараметрыСозданияОрганизации.Вставить("КПП", ""); // Необязательный
	ПараметрыСозданияОрганизации.Вставить("Наименование", ""); // Необязательный
	ПараметрыСозданияОрганизации.Вставить("Адрес", ""); // Необязательный
	ПараметрыСозданияОрганизации.Вставить("ЗначениеАдресаJSON", ""); // Необязательный
	
	ПараметрыСозданияОрганизации.Вставить("ВидБизнеса", Перечисления.ВидыБизнеса.ПустаяСсылка()); // Необязательный
	
	ПараметрыСозданияОрганизации.Вставить("ФамилияДиректора", ""); // Необязательный
	ПараметрыСозданияОрганизации.Вставить("ИмяДиректора", ""); // Необязательный
	ПараметрыСозданияОрганизации.Вставить("ОтчествоДиректора", ""); // Необязательный
	ПараметрыСозданияОрганизации.Вставить("ТарифСтраховыхВзносов", ""); // Необязательный
	
	Возврат ПараметрыСозданияОрганизации;
	
КонецФункции

Функция НовыйНалогообложениеОрганизации() Экспорт
	
	НалогообложениеОрганизации = Новый Структура;
	НалогообложениеОрганизации.Вставить("СистемаНалогообложения", ""); // Обязательный
	// Может принимать значения:
	//  - УпрощеннаяДоходы
	//  - УпрощеннаяДоходыМинусРасходы
	//  - Патентная
	//  - ЕНВД
	//  - Общая
	//  - НалогНаПрофессиональныйДоход
	НалогообложениеОрганизации.Вставить("ПлательщикЕНВД", Ложь);
	НалогообложениеОрганизации.Вставить("ПрименяетсяУСНПатент", Ложь);
	НалогообложениеОрганизации.Вставить("ПлательщикТорговогоСбора", Ложь);
	НалогообложениеОрганизации.Вставить("ИспользуетсяТрудНаемныхРаботников", Ложь); // Не обязательный
	
	Возврат НалогообложениеОрганизации;
	
КонецФункции

Функция НаиболееСложнаяСистемаНалогообложенияИзПрименяемых()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходы), ЛОЖЬ) КАК ПрименяетсяУСНДоходы,
	|	ЕСТЬNULL(МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНДоходыМинусРасходы), ЛОЖЬ) КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	ЕСТЬNULL(МАКСИМУМ(НастройкиСистемыНалогообложения.ПрименяетсяУСНПатент), ЛОЖЬ) КАК ПрименяетсяУСНПатент,
	|	ЕСТЬNULL(МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНДФЛ), ЛОЖЬ) КАК ПлательщикНДФЛ,
	|	ЕСТЬNULL(МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль), ЛОЖЬ) КАК ПлательщикНалогаНаПрибыль,
	|	ЕСТЬNULL(МАКСИМУМ(НастройкиСистемыНалогообложения.ПлательщикЕНВД), ЛОЖЬ) КАК ПлательщикЕНВД
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Если Выборка.ПлательщикНалогаНаПрибыль ИЛИ Выборка.ПлательщикНДФЛ Тогда
		Возврат "Общая";
	ИначеЕсли Выборка.ПрименяетсяУСНДоходы Тогда
		Возврат "УпрощеннаяДоходы";
	ИначеЕсли Выборка.ПрименяетсяУСНДоходыМинусРасходы Тогда
		Возврат "УпрощеннаяДоходыМинусРасходы";
	ИначеЕсли Выборка.ПрименяетсяУСНПатент Тогда
		Возврат "Патентная";
	ИначеЕсли Выборка.ПлательщикЕНВД Тогда
		Возврат "ЕНВД";
	Иначе
		Возврат "НалогНаПрофессиональныйДоход";
	КонецЕсли;
	
КонецФункции

Функция ИспользуетсяПолнаяФункциональность() Экспорт
	
	ВариантФункциональности = Обработки.ФункциональностьПрограммы.ВариантФункциональности();
	Возврат (ВариантФункциональности = ФункциональностьПрограммыКлиентСервер.ПолнаяФункциональность());
	
КонецФункции

Функция ОпределитьПодходящийВариантИнтерфейса(СистемаНалогообложения, РазрешенУчетРегулярнойДеятельности = Истина, НастройкиИнтерфейса = Неопределено) Экспорт
	
	Если НастройкиИнтерфейса = Неопределено Тогда
		НастройкиИнтерфейса = ПодходящиеНастройкиИнтерфейса(СистемаНалогообложения);
	КонецЕсли;
	
	ВариантИнтерфейса = ?(РазрешенУчетРегулярнойДеятельности,
		НастройкиИнтерфейса.ВариантИнтерфейса,
		"ИнтерфейсТаксиПростой");
	
	Возврат ВариантИнтерфейса;
	
КонецФункции

Функция УстановитьПодходящийИнтерфейс(ВидОрганизации, СистемаНалогообложения, ИспользуетсяТрудНаемныхРаботников, РазрешенУчетРегулярнойДеятельности = Истина) Экспорт
	
	НастройкиИнтерфейса = Неопределено;
	ВариантИнтерфейса = ОпределитьПодходящийВариантИнтерфейса(
		СистемаНалогообложения, РазрешенУчетРегулярнойДеятельности, НастройкиИнтерфейса);
	
	ОбщегоНазначенияБП.УстановитьРежимКомандногоИнтерфейса(ВариантИнтерфейса);
	
	Если ВидОрганизации = "ИндивидуальныйПредприниматель" Или ВидОрганизации = "Самозанятый"
		И Не ИспользуетсяТрудНаемныхРаботников Тогда
		БезЗакрывающихДокументов = НастройкиИнтерфейса.БезЗакрывающихДокументов;
	Иначе
		БезЗакрывающихДокументов = Ложь;
	КонецЕсли;
	
	Если ВариантИнтерфейса = "ИнтерфейсТаксиПростой" И Не БезЗакрывающихДокументов Тогда
		Константы.ИспользоватьДокументыПоступления.Установить(Истина);
		Константы.ИспользоватьДокументыРеализации.Установить(Истина);
	КонецЕсли;
	
	Возврат ВариантИнтерфейса;
	
КонецФункции

#КонецОбласти

#Область РежимРаботыПользователя

// Функция возвращает режим работы пользователя.
//
// Возвращаемое значение - Строка - см. НовыйРежимРаботыПользователя()
//
Функция РежимРаботыВыборПользователя() Экспорт
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ИмяНастройкиНачалаРаботыПрогрессПользователя(),
		ИмяНастройкиРежимРаботы(),
		"");
	
КонецФункции

// Процедура сохраняет режим работы пользователя.
// 
// Параметры:
//    РежимРаботы - Строка - см. НовыйРежимРаботыПользователя()
//
Процедура СохранитьРежимРаботыПользователя(Знач РежимРаботы) Экспорт
	
	РежимРаботыПользователя = НовыйРежимРаботыПользователя();
	Если Не РежимРаботыПользователя.Свойство(РежимРаботы) Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный режим работы: %1'"), РежимРаботы);
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиНачалаРаботыПрогрессПользователя(),
		ИмяНастройкиРежимРаботы(),
		РежимРаботы);

КонецПроцедуры

Функция НовыйРежимРаботыПользователя() Экспорт
	
	РежимРаботы = Новый Структура;
	РежимРаботы.Вставить("БыстрыйСтарт", "БыстрыйСтарт");
	РежимРаботы.Вставить("Регистрация", "Регистрация");
	РежимРаботы.Вставить("ВнесениеИзменений", "ВнесениеИзменений");
	Возврат РежимРаботы;
	
КонецФункции

Функция ИмяНастройкиНачалаРаботыПрогрессПользователя()

	Возврат "НачалоРаботыПрогрессПользователя";

КонецФункции

Функция ИмяНастройкиРежимРаботы()

	Возврат "РежимРаботы";

КонецФункции

#КонецОбласти

#КонецЕсли