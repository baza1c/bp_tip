
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтображатьРеквизитыПодключения = Истина;
	ЭтоРаботаВРежимеСервиса = ОбщегоНазначения.РазделениеВключено();
	Если Не ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые()
		И Не ЭтоРаботаВРежимеСервиса Тогда
		Элементы.Организация.Видимость = Ложь;
		Элементы.ГруппаОбщееОписание.Видимость = Ложь;
		Элементы.ГруппаШаги.Видимость = Ложь;
		УстановитьЗаголовок();
		ОтображатьРеквизитыПодключения = Ложь;
	ИначеЕсли ЭтоРаботаВРежимеСервиса И Пользователи.ЭтоПолноправныйПользователь(Неопределено, Истина, Ложь) Тогда
		Ошибка = Истина;
		ПредставлениеОшибки = СтрШаблон(
			НСтр("ru = 'Приложение запущено администратором информационной базы. 
				|Подключение возможно только от имени пользователя области - %1'"),
			РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
		УстановитьЗаголовок();
		ОтображатьРеквизитыПодключения = Ложь;
	КонецЕсли;
	
	АдресХранилища = ПолучитьБаннерНаСервере();
	УстановитьБаннерНаФорме(АдресХранилища);
	
	Если Не ОтображатьРеквизитыПодключения Тогда
		Возврат;
	КонецЕсли;
	
	РежимСправки = Параметры.РежимСправки;
	
	НастройкаИнтеграцииДоступна = 
		ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые);
	
	НастройкаОбсужденийДоступна = ПравоДоступа("АдминистрированиеДанных", Метаданные);
	
	Если ЗначениеЗаполнено(Параметры.ДанныеБаннера) Тогда
		Объект.Организация = Параметры.ДанныеБаннера.ДанныеСобытия.Организация;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = Параметры.Организация;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		Если ЗначениеЗаполнено(ОсновнаяОрганизация) Тогда
			Если Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(ОсновнаяОрганизация) Тогда
				Объект.Организация = ОсновнаяОрганизация;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЭтоНачалоРаботы = Параметры.НачалоРаботы;
	
	// Для подготовки справок.
	ПериодСправкиОПостановкеНаУчет = НачалоГода(ТекущаяДатаСеанса());
	ПериодСправкиОДоходах = НачалоГода(ТекущаяДатаСеанса());
	
	Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаПустая;
	
	ПолучитьДанныеНаСервере();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Управление формой нужно вызвать на клиенте, а не при создании на сервере,
	// чтобы обойти ошибку платформы с залипанием страниц в веб-клиенте.
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбновитьСтатусСообщенийИзСервиса = Ложь;
	
	Если ИмяСобытия = "ИзменениеУчетнойПолитики"
		И Параметр = Объект.Организация
		И Не ПрименяетсяНалогНаПрофессиональныйДоход Тогда
		ПолучитьДанныеНаСервере();
	ИначеЕсли ИмяСобытия = "ИзмененоСостояниеИнтеграцииСПлатформойСамозанятые" Тогда
		ОбновитьИнтерфейс();
		Если Параметр = Объект.Организация Тогда
			ОпросПлатформы = Ложь;
			Если СостояниеПодключения = ПредопределенноеЗначение(
				"Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.Подключено") Тогда
				Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаОжидание;
				ПодключитьОбработчикОжидания("Подключаемый_ОбновитьРеквизитыНалогоплательщика", 1, Истина);
			Иначе
				ОбновитьСтатусСообщенийИзСервиса = Истина;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОбсужденияПодключены" Тогда
		УправлениеФормой(ЭтотОбъект);
		ОбновитьСтатусСообщенийИзСервиса = Истина;
	КонецЕсли;
	
	Если ОбновитьСтатусСообщенийИзСервиса Тогда
		Если ОбсужденияПодключены() Тогда
			ОповещенияПлатформыСамозанятыеКлиент.ПослеПодключенияОбсуждений();
		Иначе
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Не РежимСправки Тогда
		Оповестить("ИзмененоСостояниеИнтеграцииСПлатформойСамозанятые", Объект.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация Тогда
		// Номер телефона обязателен для авторизации
		Если Не КодПодтвержденияЗапрошен(ДатаСозданияКодаПодтверждения) Тогда
			Если ЗначениеЗаполнено(Объект.НомерТелефонаПредставление) Тогда
				Если Не ОрганизацииФормыКлиентСервер.ТелефонСоответствуетТребованиям(Объект.НомерТелефонаПредставление) Тогда
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", НСтр("ru = 'Номер телефона'"));
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "НомерТелефонаПредставление", "Объект", Отказ);
				КонецЕсли;
			Иначе
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Заполнение", НСтр("ru = 'Номер телефона'"));
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "НомерТелефонаПредставление", "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.ГруппаШаги.ТекущаяСтраница <> Элементы.ГруппаАвторизация Или Не КодПодтвержденияЗапрошен(ДатаСозданияКодаПодтверждения) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодПодтверждения");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СтраницаАвторизацииНПД" Тогда
		СтандартнаяОбработка = Ложь;
		АдресСтраницы = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.АдресСтраницыАутентификацииСервиса();
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресСтраницы);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СтраницаНастройкиНПД" Тогда
		СтандартнаяОбработка = Ложь;
		АдресСтраницы = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.АдресСтраницыНастройкиСервиса();
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресСтраницы);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЖурналРегистрации" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтеграцияСПлатформойСамозанятыеКлиент.ОткрытьЖурналРегистрации(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Ошибка"));
	Иначе
		ПерсонализированныеПредложенияСервисовКлиент.ПерейтиПоСсылкеБаннера(
			НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, Баннер, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаПустая;
	Ошибка          = Ложь;
	ОпросПлатформы  = Ложь;
	
	ПолучитьДанныеНаСервере();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок    = НСтр("ru = 'Настройки налогов и отчетов'");
	ПараметрыОткрытия.ИмяФормы     = "ОбщаяФорма.НалогиИОтчеты";
	ПараметрыОткрытия.Владелец     = ЭтотОбъект;
	ПараметрыОткрытия.Уникальность = УникальныйИдентификатор;
	
	ПараметрыФормы.Вставить("Организация",      Объект.Организация);
	ПараметрыФормы.Вставить("РежимНастройки",   Истина);
	ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПроверкаПодключенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.НадписьОжидаетсяПодтверждениеМойНалогМК.Видимость = Истина;
		Элементы.КартинкаОжидаетсяПодтверждениеМойНалогМК.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПлатформаСамозанятыеПодтверждениеНажатие(Элемент)
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.НадписьОжидаетсяПодтверждениеМойНалогМК.Видимость = Истина;
		Элементы.КартинкаОжидаетсяПодтверждениеМойНалогМК.Видимость = Истина;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресСтраницыПартнеры());
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодСправкиОПостановкеНаУчетПриИзменении(Элемент)
	
	ДатаНачалаЭксперимента = НалогНаПрофессиональныйДоходКлиентСервер.ДатаНачалаЭксперимента();
	Если НачалоГода(ПериодСправкиОПостановкеНаУчет) < ДатаНачалаЭксперимента Тогда
		ПериодСправкиОПостановкеНаУчет = ДатаНачалаЭксперимента;
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Справку нельзя сформировать за период раньше %1 года'"),
			Формат(ДатаНачалаЭксперимента, "ДФ=yyyy"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ПериодСправкиОПостановкеНаУчет");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодСправкиОДоходахПриИзменении(Элемент)
	
	ДатаНачалаЭксперимента = НалогНаПрофессиональныйДоходКлиентСервер.ДатаНачалаЭксперимента();
	Если НачалоГода(ПериодСправкиОДоходах) < ДатаНачалаЭксперимента Тогда
		ПериодСправкиОДоходах = ДатаНачалаЭксперимента;
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Справку нельзя сформировать за период раньше %1 года'"),
			Формат(ДатаНачалаЭксперимента, "ДФ=yyyy"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ПериодСправкиОДоходах");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаПриИзменении(Элемент)
	
	Если Не ПустаяСтрока(Элемент.ТекстРедактирования) Тогда
		Объект.НомерТелефонаПредставление = ОрганизацииФормыКлиентСервер.ТелефонДляЗаявления(Элемент.ТекстРедактирования);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьЗначенияПолейНомераТелефона", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформации(ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ПараметрыКонтактнойИнформации.Телефон);
	ПараметрыОткрытия.Вставить("ЗначенияПолей", Объект.НомерТелефонаЗначенияПолей);
	ПараметрыОткрытия.Вставить("Представление", Объект.НомерТелефонаПредставление);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		Объект.НомерТелефонаЗначенияПолей = ВыбранноеЗначение.Значение;
		Объект.НомерТелефонаПредставление = ВыбранноеЗначение.Представление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СброситьКодПодтверждения();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьПодсказкуОбновленыСведенияПриПодключенииНажатие(Элемент)
	Элементы.ГруппаОбновленыСведенияПриПодключении.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеПроверкаПодтвержденияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресСтраницыПартнеры());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить(Команда)
	
	ОчиститьСообщения();
	Если ПроверитьЗаполнение() Тогда
		ЗапроситьSMSсКодом();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подтвердить(Команда)
	
	ОчиститьСообщения();
	Если ПроверитьЗаполнение() Тогда
		ОтключитьОбработчикОжидания_ОбновитьСтатусКодаПодтверждения();
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаОжидание;
		ПодключитьОбработчикОжидания("Подключаемый_ОтправитьЗапросНаПривязку", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)
	
	Переподключение = Ложь;
	Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаОжидание;
	Элементы.ГруппаНадписиОжидание.ТекущаяСтраница = Элементы.ГруппаОтключение;
	ПодключитьОбработчикОжидания("Подключаемый_ОтправитьЗапросНаОтвязку", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Повторить(Команда)
	
	Предупреждение  = Ложь;
	Ошибка          = Ложь;
	ОпросПлатформы  = Ложь;
	
	Если Не РежимСправки Тогда
		ОтключитьИнтеграциюСПлатформойСамозанятые(Объект.Организация);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	Если Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация Тогда
		КодПодтверждения = "";
		ОбновитьСтатусКодаПодтверждения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Предупреждение  = Ложь;
	Ошибка          = Ложь;
	ОпросПлатформы  = Ложь;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Переподключить(Команда)
	
	РежимСправки = Ложь;
	ВыводитьОкноОжидания = Ложь;
	ОпросПлатформы  = Ложь;
	
	ОтключитьИнтеграциюСПлатформойСамозанятые(Объект.Организация);
	
	ПолучитьДанныеНаСервере();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторноЗапроситьSMSсКодом(Команда)
	
	ОтключитьОбработчикОжидания_ОбновитьСтатусКодаПодтверждения();
	ЗапроситьSMSсКодом();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОбсуждения(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Обсуждения") Тогда
		
		МодульОбсужденияСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбсужденияСлужебныйКлиент");
		МодульОбсужденияСлужебныйКлиент.ПоказатьПодключение();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОбсуждения(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Обсуждения") Тогда
		
		МодульОбсужденияСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбсужденияСлужебныйКлиент");
		МодульОбсужденияСлужебныйКлиент.ПоказатьОтключение();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСправкуОПостановкеНаУчетПослеПодключения(Команда)
	
	ИмяМетодаВзаимодействия = "СправкаОПостановкеНаУчет";
	ВыводитьОкноОжидания = Ложь;
	ВыполнитьМетодВзаимодействияВФоне();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСправкаОПостановкеНаУчет(Команда)
	
	ИмяМетодаВзаимодействия = "СправкаОПостановкеНаУчет";
	ВыводитьОкноОжидания = Ложь;
	ВыполнитьМетодВзаимодействияВФоне();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСправкаОДоходах(Команда)
	
	ИмяМетодаВзаимодействия = "СправкаОДоходах";
	ВыводитьОкноОжидания = Ложь;
	ВыполнитьМетодВзаимодействияВФоне();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРазрешения(Команда)
	
	ИмяМетодаВзаимодействия = "СтатусЗапросаНаПривязку";
	ВыполнитьМетодВзаимодействияВФоне();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьДанныеНаСервере()
	
	ДанныеОбновлены = Ложь;
	
	УстановитьЗаголовок();
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформации(ЭтотОбъект);
	КонтактнаяИнформацияОбъекта = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
		ПараметрыКонтактнойИнформации.Владелец, , , Ложь);
	ТелефонОрганизации = ЗначениеКонтактнойИнформации(КонтактнаяИнформацияОбъекта, ПараметрыКонтактнойИнформации.Телефон);
	Объект.НомерТелефонаЗначенияПолей = ТелефонОрганизации.ЗначенияПолей;
	Объект.НомерТелефонаПредставление = ТелефонОрганизации.Представление;
	
	Если Не ЗначениеЗаполнено(Объект.НомерТелефонаПредставление) И ЭтоНачалоРаботы Тогда
		Объект.НомерТелефонаПредставление = ИнтеграцияСПлатформойСамозанятые.НомерТелефонаАбонента();
		ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформации(ЭтотОбъект);
		Объект.НомерТелефонаЗначенияПолей = КонтактнаяИнформацияПоПредставлению(
			Объект.НомерТелефонаПредставление, ПараметрыКонтактнойИнформации.Телефон);
	КонецЕсли;
	
	СистемаНалогообложенияПредставление =
		ОрганизацииФормыКлиентСервер.ПредставлениеСистемыНалогообложения(Объект.Организация);
	ПрименяетсяНалогНаПрофессиональныйДоход = 
		УчетнаяПолитика.ПрименяетсяНалогНаПрофессиональныйДоход(Объект.Организация, ТекущаяДатаСеанса());
	СостояниеПодключения = ПолучитьСостояниеПодключения(Объект.Организация);
	ИдентификаторЗаявки = ПолучитьИдентификаторЗаявки(Объект.Организация);
	
	Если Не РежимСправки Тогда // В этом режиме сразу показываем страницу со справками.
		Переподключение = НеобходимоПереподключение(Объект.Организация, СостояниеПодключения);
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеКонтактнойИнформации(КонтактнаяИнформацияОбъекта, ВидКонтактнойИнформации)
	
	Результат = Новый Структура("ЗначенияПолей, Представление", "", "");
	
	Отбор = Новый Структура("Вид", ВидКонтактнойИнформации);
	
	КонтактнаяИнформацияПоВиду = КонтактнаяИнформацияОбъекта.Скопировать(Отбор);
	Если КонтактнаяИнформацияПоВиду.Количество() > 0 Тогда
		КонтактнаяИнформацияПоВиду.Сортировать("Дата УБЫВ");
		Результат.ЗначенияПолей = КонтактнаяИнформацияПоВиду[0].Значение;
		Результат.Представление = КонтактнаяИнформацияПоВиду[0].Представление;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция КонтактнаяИнформацияПоПредставлению(Знач Представление, Знач ВидКонтактнойИнформации)
	
	Возврат УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ВидКонтактнойИнформации);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	ЭтоСтатусЗапросаНаПривязку = Форма.ИмяМетодаВзаимодействия = "СтатусЗапросаНаПривязку";
	ЗапросОтправлен = Форма.СостояниеПодключения = ПредопределенноеЗначение("Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.ЗапросОтправлен");
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	Если Форма.СтатусЗапроса = СтатусыЗапросов.Выполняется И Не ЭтоСтатусЗапросаНаПривязку Тогда
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаОжидание;
		Если Форма.ИмяМетодаВзаимодействия = "ЗапросНаОтвязку" Тогда
			Элементы.ГруппаНадписиОжидание.ТекущаяСтраница = Элементы.ГруппаОтключение;
		Иначе
			Элементы.ГруппаНадписиОжидание.ТекущаяСтраница = Элементы.ГруппаПодключение;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ЭтоСтатусЗапросаНаПривязку Или ЗапросОтправлен Тогда
		
		Если Форма.Переподключение Тогда
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация;
		Иначе
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаПроверкаПодключения;
		КонецЕсли;
		
	ИначеЕсли Форма.Ошибка Тогда
		// В процессе обработки запроса возникла ошибка
		// на стороне приложения, сервера или платформы.
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаОшибка;
		Элементы.ПредставлениеОшибки.Высота = СтрЧислоСтрок(Форма.ПредставлениеОшибки);
		
	ИначеЕсли Форма.Предупреждение Тогда
		
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаПредупреждение;
		Элементы.ПредставлениеПредупреждения.Высота = СтрЧислоСтрок(Форма.ПредставлениеОшибки);

	ИначеЕсли ПриложениеПодключено(Форма.СостояниеПодключения) Тогда
		// Приложение подключено к платформе.
		Если Форма.РежимСправки Тогда
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаСправки;
		Иначе
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаВыполнено;
		КонецЕсли;
		
	ИначеЕсли Не Форма.ПрименяетсяНалогНаПрофессиональныйДоход Тогда
		// В шапке указали организацию, которая не является плательщиком НПД.
		Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаУчетнаяПолитика;
		
	ИначеЕсли Форма.СостояниеПодключения = ПредопределенноеЗначение("Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.Отсутствует")
		ИЛИ Форма.СостояниеПодключения = ПредопределенноеЗначение("Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.ЗапросОтклонен")
		ИЛИ Форма.СостояниеПодключения = ПредопределенноеЗначение("Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.Ошибка") Тогда
		// Приложение никогда не подключалось, или были неуспешные попытки подключить,
		// или приложение было отключено ранее. Надо снова авторизовать его на платформе.
		Если Форма.РежимСправки
			ИЛИ Не Форма.НастройкаИнтеграцииДоступна Тогда
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНеПодключено;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"Переподключить",
				"КнопкаПоУмолчанию",
				Истина);
			
		Иначе
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация;
		КонецЕсли;
		
	ИначеЕсли Форма.СостояниеПодключения = ПредопределенноеЗначение("Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.НеПривязано")
		Или ЗапросОтправлен И Форма.ДанныеОбновлены Тогда
		// Уже обработан запрос к платформе по результатам которого понятно, что
		// на стороне платформы еще не принята заявка на подключение или сброшены разрешения.
		Если Форма.РежимСправки
			ИЛИ Не Форма.НастройкаИнтеграцииДоступна Тогда
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНеПодключено;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"Переподключить",
				"КнопкаПоУмолчанию",
				Истина);
			
		Иначе
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаПроверкаПодключения;
		КонецЕсли;
		
	ИначеЕсли Форма.СостояниеПодключения = ПредопределенноеЗначение("Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.Отключено") Тогда
		// Результат выполнения команды "Отключить" - констатация факта отключения.
		Если Форма.РежимСправки
			ИЛИ Не Форма.НастройкаИнтеграцииДоступна Тогда
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаНеПодключено;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"Переподключить",
				"КнопкаПоУмолчанию",
				Истина);
			
		ИначеЕсли Форма.Переподключение Тогда
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация;
		Иначе
			Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаОтключено;
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ГруппаНеПодключеноПодсказка.Видимость = Не Форма.НастройкаИнтеграцииДоступна;
	
	Если Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаАвторизация Тогда
		
		КодПодтвержденияЗапрошен = КодПодтвержденияЗапрошен(Форма.ДатаСозданияКодаПодтверждения);
		МожноЗапроситьКодПодтверждения = МожноЗапроситьКодПодтвержденияНаСервере(Форма.ДатаОбновленияКодаПодтверждения);
		
		УправлениеЭлементамиКодаПодтверждения(Форма.Элементы, КодПодтвержденияЗапрошен, МожноЗапроситьКодПодтверждения);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			?(КодПодтвержденияЗапрошен(Форма.ДатаСозданияКодаПодтверждения), "Подтвердить", "Подключить"),
			"КнопкаПоУмолчанию",
			Истина);
		
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаВыполнено Тогда
		
		ОбсужденияПодключены = ОбсужденияПодключены();
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаОповещенияШапка",
			"Видимость",
			МожноПодключитьОбсуждения());
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаПодключитьОповещения",
			"Видимость",
			Не ОбсужденияПодключены);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ГруппаОтключитьОповещения",
			"Видимость",
			ОбсужденияПодключены);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ПодключитьОбсуждения",
			"Видимость",
			Форма.НастройкаОбсужденийДоступна);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОтключитьОбсуждения",
			"Видимость",
			Форма.НастройкаОбсужденийДоступна);
		
	ИначеЕсли Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаПроверкаПодключения Тогда
	
		Если Форма.Ошибка Или Форма.Предупреждение Тогда
			Элементы.ПредставлениеОшибки.Высота = СтрЧислоСтрок(Форма.ПредставлениеОшибки);
			Элементы.ПроверитьРазрешения.Заголовок = НСтр("ru = 'Повторить'");
			Элементы.ПроверитьРазрешения.Доступность = Истина;
			Элементы.ОшибкаСтатусПроверки.Видимость = Истина;
			Элементы.ОписаниеОшибкиПроверкиСтатусаЗаявки.Видимость = Истина;
			Элементы.КартинкаСтатусПроверкиРазрешений.Видимость = Ложь;
			Элементы.ПояснениеСтатусПроверкиРазрешений.Видимость = Ложь;
		Иначе
			Элементы.ПроверитьРазрешения.Заголовок = НСтр("ru = 'Разрешения предоставлены'");
			Элементы.ОшибкаСтатусПроверки.Видимость = Ложь;
			Элементы.ОписаниеОшибкиПроверкиСтатусаЗаявки.Видимость = Ложь;
			Если Форма.СтатусЗапроса = СтатусыЗапросов.Выполняется Тогда
				Элементы.ПроверитьРазрешения.Доступность = Ложь;
				Форма.ПояснениеСтатусПроверкиРазрешений = НСтр("ru = 'Проверяется статус подключения с сервису Мой налог...'");
				Элементы.КартинкаСтатусПроверкиРазрешений.Видимость = Истина;
				Элементы.ПояснениеСтатусПроверкиРазрешений.Видимость = Истина;
			ИначеЕсли Не ПриложениеПодключено(Форма.СостояниеПодключения) Тогда
				МожноОтправитьПовторныйЗапрос = Не ЗначениеЗаполнено(Форма.СрокПовторногоЗапросаСтатусаПодключения);
				Элементы.ПроверитьРазрешения.Доступность = МожноОтправитьПовторныйЗапрос;
				Элементы.КартинкаСтатусПроверкиРазрешений.Видимость = Ложь;
				Элементы.ПояснениеСтатусПроверкиРазрешений.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МожноПодключитьОбсуждения()
	
	Возврат ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Обсуждения");
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбсужденияПодключены()
	
	Возврат Обсуждения.ОбсужденияДоступны();
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОписаниеСохраняемогоФайла(Ответ, ИдентификаторФормы)
	
	Если Ответ.Результат = Неопределено
		Или ТипЗнч(Ответ.Результат) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не Ответ.Результат.Свойство("АдресВременногоХранилища") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЭтоАдресВременногоХранилища(Ответ.Результат.АдресВременногоХранилища) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(Ответ.Результат.АдресВременногоХранилища);
	
	Если Не Результат.Свойство("Base64Строка") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеФайла = ПолучитьДвоичныеДанныеИзBase64Строки(Результат.Base64Строка);
	
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("Имя", Результат.ИмяФайла);
	ОписаниеФайла.Вставить("Хранение", ПоместитьВоВременноеХранилище(ДанныеФайла, ИдентификаторФормы));
	
	Возврат ОписаниеФайла;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ЗаполнитьЗначенияПолейНомераТелефона()
	
	Если Не ПустаяСтрока(Объект.НомерТелефонаПредставление) Тогда
		ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформации(ЭтотОбъект);
		Объект.НомерТелефонаЗначенияПолей = КонтактнаяИнформацияПоПредставлению(Объект.НомерТелефонаПредставление, ПараметрыКонтактнойИнформации.Телефон);
	Иначе
		Объект.НомерТелефонаЗначенияПолей = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПолученияКонтактнойИнформации(Форма)
	
	Параметры = Новый Структура;
	Параметры.Вставить("Владелец", Форма.Объект.Организация);
	Параметры.Вставить("Телефон", ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(
		"Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации"));
	
	Возврат Параметры;
	
КонецФункции

&НаКлиенте
Процедура ЗапроситьSMSсКодом()
	
	Элементы.ГруппаШаги.ТекущаяСтраница = Элементы.ГруппаОжидание;
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КодПодтверждения()
	
	ИмяМетодаВзаимодействия = "КодПодтверждения";
	ВыполнитьМетодВзаимодействияВФоне();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтправитьЗапросНаПривязку()
	
	ИмяМетодаВзаимодействия = "ЗапросНаПривязку";
	ВыполнитьМетодВзаимодействияВФоне();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтправитьЗапросНаОтвязку()
	
	ИмяМетодаВзаимодействия = "ЗапросНаОтвязку";
	ВыполнитьМетодВзаимодействияВФоне();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПриложениеПодключено(Состояние)
	
	Возврат Состояние = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СостояниеПодключенияКСервису();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПриложениеОтключено(Состояние)
	
	Возврат Состояние = ПредопределенноеЗначение("Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.Отключено");
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстПовторногоЗапросаСтатусаПодключения(СрокДействияЗапроса)
	
	Если Не ЗначениеЗаполнено(СрокДействияЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекущаяДата = УниверсальноеВремя(ТекущаяДатаСеанса(), ЧасовойПоясСеанса());
	РазностьДат = СрокДействияЗапроса - ТекущаяДата;
	
	Если РазностьДат > 0 Тогда
		Возврат СтрШаблон(НСтр("ru = 'Повторно проверить разрешения можно через %1 сек.'"), РазностьДат);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПовторнаяОтправкаЗапросаСтатусаПодключения()
	
	ТекстПовторногоЗапроса = ТекстПовторногоЗапросаСтатусаПодключения(СрокПовторногоЗапросаСтатусаПодключения);
	Элементы.ГруппаСтатусПроверкиРазрешений.Подсказка = ТекстПовторногоЗапроса;
	
	Если ЗначениеЗаполнено(ТекстПовторногоЗапроса) Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПовторнаяОтправкаЗапросаСтатусаПодключения", 1, Истина);
	Иначе
		ОтключитьОбработчикОжидания("Подключаемый_ПовторнаяОтправкаЗапросаСтатусаПодключения");
		СрокПовторногоЗапросаСтатусаПодключения = Неопределено;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция АдресСтраницыПартнеры()

	Возврат ИнтеграцияСПлатформойСамозанятыеКлиентСервер.АдресСтраницыПартнеры();

КонецФункции

&НаСервереБезКонтекста
Функция НеобходимоПереподключение(Организация, ТекущееСостояние)
	
	Если ПриложениеПодключено(ТекущееСостояние) Тогда
		Возврат Ложь;
	КонецЕсли;

	Возврат ПриложениеОтключено(ТекущееСостояние)
		Или РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ЗаявкаНаПодключениеКСервисуПросрочена(Организация);
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовок()

	Если РежимСправки Тогда
		Заголовок = НСтр("ru = 'Справки по налогу на профессиональный доход'");
	Иначе
		Заголовок = НСтр("ru = 'Подключение к сервису Мой налог'");
	КонецЕсли;

КонецПроцедуры

#Область ВыполнитьМетодВзаимодействия

&НаКлиенте
Функция ПараметрыМетодаВзаимодействия()
	
	ПараметрыМетода =
		ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия(ИмяМетодаВзаимодействия);
	ПараметрыМетода.Организация = Объект.Организация;
	
	Если ПараметрыМетода.Свойство("НомерТелефона") Тогда
		ПараметрыМетода.НомерТелефона = Объект.НомерТелефонаПредставление;
	КонецЕсли;
	
	Если ИмяМетодаВзаимодействия = "ЗапросНаПривязку" Тогда
		ПараметрыМетода.НомерТелефона = Объект.НомерТелефонаПредставление;
		ПараметрыМетода.ИдентификаторКодаПодтверждения = ИдентификаторКодаПодтверждения;
		ПараметрыМетода.КодПодтверждения = КодПодтверждения;
	КонецЕсли;
	
	Если ПараметрыМетода.Свойство("Идентификатор") Тогда
		ПараметрыМетода.Идентификатор = ИдентификаторЗаявки;
	КонецЕсли;
	
	Если ИмяМетодаВзаимодействия = "СправкаОПостановкеНаУчет" Тогда
		ПараметрыМетода.Период = ПериодСправкиОПостановкеНаУчет;
	КонецЕсли;
	
	Если ИмяМетодаВзаимодействия = "СправкаОДоходах" Тогда
		ПараметрыМетода.Период = ПериодСправкиОДоходах;
	КонецЕсли;
	
	Возврат ПараметрыМетода;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьМетодВзаимодействияВФоне()
	
	Предупреждение = Ложь;
	Ошибка = Ложь;
	ПредставлениеОшибки = "";
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ВыполнитьМетодВзаимодействияВФонеНаСервере(
		ПараметрыМетодаВзаимодействия(),
		УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		СтатусЗапроса = СтатусыЗапросов.Выполняется;
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		Обработчик = Новый ОписаниеОповещения("ОбработкаВыполненияМетодаВзаимодействия", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	Иначе
		СтатусЗапроса = СтатусыЗапросов.Ошибка;
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация.КраткоеПредставлениеОшибки,
			ДлительнаяОперация.ПодробноеПредставлениеОшибки);
		ДанныеОбновлены = Истина;
		СостояниеПодключения = ПолучитьСостояниеПодключения(Объект.Организация);
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполнитьМетодВзаимодействияВФонеНаСервере(Знач ПараметрыМетода, Знач ИдентификаторФормы)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = 
		СтрШаблон(НСтр("ru = 'Интеграция с платформой Самозанятые: выполнение метода взаимодействия платформы (%1)'"),
			ПараметрыМетода.МетодВзаимодействия);
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействияВФоне",
		ПараметрыМетода,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыполненияМетодаВзаимодействия(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" И ЭтоАдресВременногоХранилища(ДлительнаяОперация.АдресРезультата) Тогда
		Если СтатусЗапроса <> СтатусыЗапросов.Выполнено Тогда
			СтатусЗапроса = СтатусыЗапросов.Выполняется;
		КонецЕсли;
		Запрос = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		СрокПовторногоЗапросаСтатусаПодключения = Запрос.СрокПовторногоЗапроса;
		АдресЗапроса = ПоместитьВоВременноеХранилище(Запрос, УникальныйИдентификатор);
		Если ЗначениеЗаполнено(СрокПовторногоЗапросаСтатусаПодключения) Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ПовторнаяОтправкаЗапросаСтатусаПодключения", 1, Истина);
		КонецЕсли;
	Иначе
		СтатусЗапроса = СтатусыЗапросов.Ошибка;
		Запрос = Неопределено;
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация.КраткоеПредставлениеОшибки,
			ДлительнаяОперация.ПодробноеПредставлениеОшибки);
	КонецЕсли;
	
	Если Запрос <> Неопределено И Запрос.Ошибка Тогда
		СтатусЗапроса = СтатусыЗапросов.Ошибка;
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(Запрос.Сообщение);
	КонецЕсли;
	
	Если СтатусЗапроса <> СтатусыЗапросов.Ошибка Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПолучитьРезультатВыполненияВФоне", Запрос.ВремяОжидания, Истина);
	КонецЕсли;
	
	Если ОпросПлатформы Тогда
		Если СтатусЗапроса = СтатусыЗапросов.Выполнено Тогда
			СостояниеПодключения = ПолучитьСостояниеПодключения(Объект.Организация);
			Если ПриложениеПодключено(СостояниеПодключения) Тогда
				ДанныеОбновлены = Истина;
				УправлениеФормой(ЭтотОбъект);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если СтатусЗапроса = СтатусыЗапросов.Ошибка
			Или СтатусЗапроса = СтатусыЗапросов.Выполнено Тогда
			ДанныеОбновлены = Истина;
			СостояниеПодключения = ПолучитьСостояниеПодключения(Объект.Организация);
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьРезультатВыполненияВФоне()
	
	ДлительнаяОперация = ПолучитьРезультатВыполненияВФонеНаСервере(
		АдресЗапроса,
		УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		СтатусЗапроса = СтатусыЗапросов.Выполняется;
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ТекстСообщения = НСтр("ru = 'Получение ответа от сервиса Мой налог'");
		НастройкиОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
		Обработчик = Новый ОписаниеОповещения("ОбработкаРезультатаВыполненияМетода", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	Иначе
		СтатусЗапроса = СтатусыЗапросов.Ошибка;
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация.КраткоеПредставлениеОшибки,
			ДлительнаяОперация.ПодробноеПредставлениеОшибки);
		ДанныеОбновлены = Истина;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРезультатаВыполненияМетода(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация.КраткоеПредставлениеОшибки,
			ДлительнаяОперация.ПодробноеПредставлениеОшибки);
		УправлениеФормой(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(ДлительнаяОперация.АдресРезультата) Тогда
		Ответ = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		УдалитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
	КонецЕсли;
	
	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяМетодаВзаимодействия = "КодПодтверждения" Тогда
		ОбработкаОтветаМетода_КодПодтверждения(Ответ);
	ИначеЕсли СтрНачинаетсяС(ИмяМетодаВзаимодействия, "Справка") Тогда
		ОбработкаОтветаМетода_Справка(Ответ);
	ИначеЕсли ИмяМетодаВзаимодействия = "ДетальныйСтатусНалогоплательщика" Тогда
		ОбработкаОтветаМетода_ДетальныйСтатус(Ответ);
	Иначе
		ОбработкаОтветаМетода(Ответ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаМетода(Ответ)
	
	СтатусЗапроса = Ответ.Статус;
	
	Если СтатусЗапроса = СтатусыЗапросов.Выполнено Тогда
		
		ДанныеОбновлены = Истина;
		ЗаписатьСостояниеИнтеграцииСПлатформойСамозанятые(Ответ);
		СостояниеПодключения = ПолучитьСостояниеПодключения(Объект.Организация);

		Если СостояниеПодключения = ПредопределенноеЗначение("Перечисление.СостоянияИнтеграцииСПлатформойСамозанятые.ЗапросОтклонен") Тогда
			ДанныеОбновлены = Истина;
			Ошибка = Истина;
			ПредставлениеОшибки = НСтр("ru = 'Запрос на подключение сервиса Мой налог был отклонен в личном кабинете'");
			УправлениеФормой(ЭтотОбъект);
			Оповестить("ИзмененоСостояниеИнтеграцииСПлатформойСамозанятые", Объект.Организация);
		Иначе
			ДанныеОбновлены  = Истина;
			УправлениеФормой(ЭтотОбъект);
			Оповестить("ИзмененоСостояниеИнтеграцииСПлатформойСамозанятые", Объект.Организация);
		КонецЕсли;
		
		Если ЭтоАдресВременногоХранилища(АдресЗапроса) Тогда
			УдалитьИзВременногоХранилища(АдресЗапроса);
		КонецЕсли;
		
	ИначеЕсли СтатусЗапроса = СтатусыЗапросов.Выполняется Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПолучитьРезультатВыполненияВФоне", Ответ.ВремяОжидания, Истина);
	ИначеЕсли СтатусЗапроса = СтатусыЗапросов.Ошибка Тогда
		
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(Ответ.Сообщение, , КодОшибки(Ответ));
		
		Если ОпросПлатформы Тогда
			Если ИмяМетодаВзаимодействия = "СтатусЗапросаНаПривязку" И ТипЗнч(Ответ.Результат) = Тип("Структура") Тогда
				ЗаписатьСостояниеИнтеграцииСПлатформойСамозанятые(Ответ);
				СостояниеПодключения = ПолучитьСостояниеПодключения(Объект.Организация);
				Ошибка = Истина;
				УправлениеФормой(ЭтотОбъект);
				Возврат;
			КонецЕсли;
		Иначе
			Если ИмяМетодаВзаимодействия = "ЗапросНаПривязку" Тогда
				ЗаписатьСостояниеИнтеграцииСПлатформойСамозанятые(Ответ);
				СостояниеПодключения = ПолучитьСостояниеПодключения(Объект.Организация);
				Ошибка = Ложь;
			КонецЕсли;
			ДанныеОбновлены = Истина;
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаМетода_КодПодтверждения(Ответ)
	
	СтатусЗапроса = Ответ.Статус;
	
	Если СтатусЗапроса = СтатусыЗапросов.Выполнено Тогда
		
		Если ТипЗнч(Ответ.Результат) = Тип("Структура") Тогда
			Ответ.Результат.Свойство("ИдентификаторКодаПодтверждения", ИдентификаторКодаПодтверждения);
			Ответ.Результат.Свойство("ДатаСозданияКодаПодтверждения", ДатаСозданияКодаПодтверждения);
			Ответ.Результат.Свойство("ДатаОбновленияКодаПодтверждения", ДатаОбновленияКодаПодтверждения);
			Ответ.Результат.Свойство("ДатаСрокаДействияКодаПодтверждения", ДатаСрокаДействияКодаПодтверждения);
		КонецЕсли;
		
		ЗаписатьСостояниеИнтеграцииСПлатформойСамозанятые(Ответ);
		СостояниеПодключения = ПолучитьСостояниеПодключения(Объект.Организация);
		
		Если ЭтоАдресВременногоХранилища(АдресЗапроса) Тогда
			УдалитьИзВременногоХранилища(АдресЗапроса);
		КонецЕсли;
		
		ОбновитьСтатусКодаПодтверждения();
		
	ИначеЕсли СтатусЗапроса = СтатусыЗапросов.Выполняется Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПолучитьРезультатВыполненияВФоне", Ответ.ВремяОжидания, Истина);
	ИначеЕсли СтатусЗапроса = СтатусыЗапросов.Ошибка Тогда
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(Ответ.Сообщение, , КодОшибки(Ответ));
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаМетода_Справка(Ответ)
	
	СтатусЗапроса = Ответ.Статус;
	
	Если СтатусЗапроса = СтатусыЗапросов.Выполнено Тогда
		
		ОписаниеСохраняемыхФайлов = ПолучитьОписаниеСохраняемогоФайла(Ответ, УникальныйИдентификатор);
		Если ОписаниеСохраняемыхФайлов <> Неопределено Тогда
			ОбработчикЗавершения = Новый ОписаниеОповещения("Подключаемый_ОбработкаОтветаМетода_Справка_Завершение", ЭтотОбъект);
			ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
			ПараметрыСохранения.Интерактивно = Истина;
			ПараметрыСохранения.Диалог.Фильтр = НСтр("ru = 'Файлы Adobe PDF'") + "(*.pdf)|*.pdf";
			ПараметрыСохранения.Диалог.ПолноеИмяФайла = ОписаниеСохраняемыхФайлов.Имя;
			ФайловаяСистемаКлиент.СохранитьФайл(ОбработчикЗавершения, ОписаниеСохраняемыхФайлов.Хранение, ОписаниеСохраняемыхФайлов.Имя, ПараметрыСохранения);
		Иначе
			УдалитьЗапросИзВременногоХранилища();
		КонецЕсли;
		
	ИначеЕсли СтатусЗапроса = СтатусыЗапросов.Выполняется Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПолучитьРезультатВыполненияВФоне", Ответ.ВремяОжидания, Истина);
	ИначеЕсли СтатусЗапроса = СтатусыЗапросов.Ошибка Тогда
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(Ответ.Сообщение, , КодОшибки(Ответ));
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаОтветаМетода_Справка_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		УдалитьЗапросИзВременногоХранилища();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗапросИзВременногоХранилища()
	
	Если ЭтоАдресВременногоХранилища(АдресЗапроса) Тогда
		УдалитьИзВременногоХранилища(АдресЗапроса);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаМетода_ДетальныйСтатус(Ответ)
	
	СтатусЗапроса = Ответ.Статус;
	
	Если СтатусЗапроса = СтатусыЗапросов.Выполнено Тогда
		СписокИзменений = ОбработкаОтветаМетода_ДетальныйСтатусНаСервере(Ответ, Объект.Организация, ЭтоНачалоРаботы);
		Если ЭтоНачалоРаботы Тогда
			ОбновитьИнтерфейс();
			Закрыть();
		КонецЕсли;
		УправлениеФормой(ЭтотОбъект);
		ВывестиБаннерСУведомлениемОбИзмененииЗначений(СписокИзменений);
	ИначеЕсли СтатусЗапроса = СтатусыЗапросов.Выполняется Тогда
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьРезультатВыполненияВФоне", Ответ.ВремяОжидания, Истина);
	ИначеЕсли СтатусЗапроса = СтатусыЗапросов.Ошибка Тогда
		ЗаписатьОшибкуВыполненияМетодаВзаимодействия(Ответ.Сообщение, , КодОшибки(Ответ));
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиБаннерСУведомлениемОбИзмененииЗначений(СписокИзменений)
	
	Если СписокИзменений.Количество() > 0 Тогда
		Элементы.ГруппаОбновленыСведенияПриПодключении.Видимость = Истина;
		СсылкаНаОрганизацию = ПолучитьНавигационнуюСсылку(Объект.Организация);
		ЧастиБаннера = Новый Массив;
		ЧастиБаннера.Добавить(НСтр("ru='При подключении к сервису Мой налог были изменены '"));
		ЧастиБаннера.Добавить(Новый ФорматированнаяСтрока("Реквизиты организации", , , , СсылкаНаОрганизацию));
		ЧастиБаннера.Добавить(НСтр("ru=':'"));
		ЧастиБаннера.Добавить(Символы.ПС);
		ЧастиБаннера.Добавить(ОписаниеИзмененияЗначений(СписокИзменений));
		Элементы.ПодсказкаОбновленыСведенияПриПодключении.Заголовок = Новый ФорматированнаяСтрока(ЧастиБаннера);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Функция ОписаниеИзмененияЗначений(СписокИзменений)
	
	ШрифтВажный = ОбщегоНазначенияКлиент.ШрифтСтиля("ВажнаяНадписьШрифт");
	
	ЧастиОписания = Новый Массив();
	ПоследнийИндекс = СписокИзменений.ВГраница();
	Для Индекс = 0 По ПоследнийИндекс Цикл
		
		Если Индекс > 0 Тогда
			Разделитель = ", ";
			Если Индекс = ПоследнийИндекс Тогда
				Разделитель = НСтр("ru=' и '");
			КонецЕсли;
			ЧастиОписания.Добавить(Разделитель);
		КонецЕсли;
		
		Изменение = СписокИзменений.Получить(Индекс);
		ЧтоИзменили = Изменение.Представление;
		НаЧтоИзменили = Изменение.Значение;
		ЧастиОписания.Добавить(ЧтоИзменили);
		ЧастиОписания.Добавить(" ");
		ЧастиОписания.Добавить(Новый ФорматированнаяСтрока(НаЧтоИзменили, ШрифтВажный));
		
	КонецЦикла;
	
	ОписаниеИзменений = Новый ФорматированнаяСтрока(ЧастиОписания);
	
	Возврат ОписаниеИзменений;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбработкаОтветаМетода_ДетальныйСтатусНаСервере(Ответ, Организация, ЭтоНачалоРаботы)
	
	СписокИзменений = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(Ответ.Результат) = Тип("Структура") Тогда
		
		РеквизитыИзСервиса = РеквизитыИзСервиса();
		Для Каждого Реквизит Из РеквизитыИзСервиса Цикл
			ИмяРеквизита = Реквизит.Значение.Имя;
			Если Ответ.Результат.Свойство(ИмяРеквизита) Тогда
				Реквизит.Значение.Значение = Ответ.Результат[ИмяРеквизита];
			КонецЕсли;
		КонецЦикла;
		
		ОрганизацияОбъект = Организация.ПолучитьОбъект();
		
		ОбновитьПолеПоДаннымМойНалог(РеквизитыИзСервиса.Фамилия, ОрганизацияОбъект.ФамилияИП, СписокИзменений);
		ОбновитьПолеПоДаннымМойНалог(РеквизитыИзСервиса.Имя, ОрганизацияОбъект.ИмяИП, СписокИзменений);
		ОбновитьПолеПоДаннымМойНалог(РеквизитыИзСервиса.Отчество, ОрганизацияОбъект.ОтчествоИП, СписокИзменений);
		
		Если СписокИзменений.Количество() > 0 Тогда
			ОрганизацияОбъект.НаименованиеСокращенное = ОбщегоНазначенияБПКлиентСервер.СокращенноеНаименованиеФизическогоЛица(
				ОрганизацияОбъект.ФамилияИП, ОрганизацияОбъект.ИмяИП, ОрганизацияОбъект.ОтчествоИП, Ложь);
			ОрганизацияОбъект.НаименованиеПолное = ОбщегоНазначенияБПКлиентСервер.ПолноеНаименованиеФизическогоЛица(
				ОрганизацияОбъект.ФамилияИП, ОрганизацияОбъект.ИмяИП, ОрганизацияОбъект.ОтчествоИП, Ложь);
			ОрганизацияОбъект.Наименование = ОбщегоНазначенияБПКлиентСервер.НаименованиеФизическогоЛица(
				ОрганизацияОбъект.ФамилияИП, ОрганизацияОбъект.ИмяИП, ОрганизацияОбъект.ОтчествоИП, Ложь);
		КонецЕсли;
		
		ВидКИТелефон = Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации;
		Телефон = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ОрганизацияОбъект, ВидКИТелефон);
		ОбновитьПолеПоДаннымМойНалог(РеквизитыИзСервиса.НомерТелефона, Телефон, СписокИзменений);
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(ОрганизацияОбъект, Телефон, ВидКИТелефон);
		
		ВидКИEmail = Справочники.ВидыКонтактнойИнформации.EmailОрганизации;
		Email = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(ОрганизацияОбъект, ВидКИEmail);
		ОбновитьПолеПоДаннымМойНалог(РеквизитыИзСервиса.Email, Email, СписокИзменений);
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(ОрганизацияОбъект, Email, ВидКИEmail);
		
		ОрганизацияОбъект.Записать();
		
	КонецЕсли;
	
	Если ЭтоНачалоРаботы Тогда
		Константы.НачалоРаботы.Установить(Ложь);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СписокИзменений;
	
КонецФункции

&НаСервереБезКонтекста
Функция РеквизитыИзСервиса()
	
	Результат = Новый Структура;
	ДобавитьРеквизитСервиса(Результат, "Фамилия", НСтр("ru='фамилия'"));
	ДобавитьРеквизитСервиса(Результат, "Имя", НСтр("ru='имя'"));
	ДобавитьРеквизитСервиса(Результат, "Отчество", НСтр("ru='отчество'"));
	ДобавитьРеквизитСервиса(Результат, "НомерТелефона", НСтр("ru='телефон'"));
	ДобавитьРеквизитСервиса(Результат, "Email", НСтр("ru='email'"));
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьРеквизитСервиса(Реквизиты, Имя, Представление, Значение = Неопределено)
	
	Поля =  Новый Структура;
	Поля.Вставить("Имя", Имя);
	Поля.Вставить("Представление", Представление);
	Поля.Вставить("Значение", Значение);
	
	Реквизиты.Вставить(Имя, Поля);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОставитьТолькоЦифрыВСтроке(Строка)
	
	Результат = Строка;
	
	ЛишниеСимволы = СтрСоединить(СтрРазделить(Строка, "0123456789"), "");
	Если Не ПустаяСтрока(ЛишниеСимволы) Тогда
		Результат = СтрСоединить(СтрРазделить(Строка, ЛишниеСимволы), "");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОбновитьПолеПоДаннымМойНалог(Реквизит, Значение1С, СписокИзменений)
	
	Если Не ЗначениеЗаполнено(Реквизит.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияОтличаются = Ложь;
	Если Реквизит.Имя = РеквизитыИзСервиса().НомерТелефона.Имя Тогда
		Значение1СТолькоЦифры = ОставитьТолькоЦифрыВСтроке(Значение1С);
		ЗначениеМойНалогТолькоЦифры = ОставитьТолькоЦифрыВСтроке(Реквизит.Значение);
		ЗначенияОтличаются = (Значение1СТолькоЦифры <> ЗначениеМойНалогТолькоЦифры);
	Иначе
		ЗначенияОтличаются = (Значение1С <> Реквизит.Значение);
	КонецЕсли;
	
	Если ЗначенияОтличаются Тогда
		СписокИзменений.Добавить(Реквизит);
		Значение1С = Реквизит.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция КодОшибки(Ответ)
	
	Перем КодОшибки;
	
	Если Ответ.Результат = Неопределено Или ТипЗнч(Ответ.Результат) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Ответ.Результат.Свойство("Код", КодОшибки);
	
	Возврат КодОшибки;
	
КонецФункции

&НаСервереБезКонтекста
Функция КодыПредупреждений()
	
	КодыПредупреждений = Новый Структура;
	ДобавитьОписаниеОшибки(КодыПредупреждений, "ВнутренняяОшибкаСервиса", Новый ФорматированнаяСтрока(
		НСтр("ru = 'Попробуйте выполнить запрос позже.'")), Истина);
	ДобавитьОписаниеОшибки(КодыПредупреждений, "ОшибкаПарсингаЗапроса", Новый ФорматированнаяСтрока(
		НСтр("ru = 'Попробуйте выполнить запрос позже.'")), Истина);
	ДобавитьОписаниеОшибки(КодыПредупреждений, "ВременнаяРегистрация", Новый ФорматированнаяСтрока(
		НСтр("ru = 'Получение справок до завершения постановки на учет недоступно. Повторите запрос после получения статуса самозанятого.'")));
	
	Возврат КодыПредупреждений;
	
КонецФункции

&НаСервереБезКонтекста
Функция КодыОшибок()
	
	КодыОшибок = Новый Структура;
	ДобавитьОписаниеОшибки(КодыОшибок, "НалогоплательщикНеЗарегистрирован", СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Зарегистрируйтесь в сервисе <a href = ""СтраницаАвторизацииНПД"">Мой налог</a>. Если учетная запись уже создана, убедитесь, что номер телефона, который вы ввели при подключении, совпадает с номером в <a href = ""СтраницаНастройкиНПД"">Настройках</a> личного кабинета.'")));
	ДобавитьОписаниеОшибки(КодыОшибок, "НедостаточноПолномочий", СтроковыеФункции.ФорматированнаяСтрока(
		НСтр("ru = 'Необходимые полномочия не предоставлены. Настройте список разрешений в личном кабинете сервиса <a href = ""СтраницаАвторизацииНПД"">Мой налог</a>'")));
	ДобавитьОписаниеОшибки(КодыОшибок, "НалогоплательщикУжеПривязан", Новый ФорматированнаяСтрока(
		НСтр("ru = 'Налогоплательщик уже привязан. Попробуйте выполнить повторное подключение.'")));
	
	Возврат КодыОшибок;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьОписаниеОшибки(КодыОшибок, Код, Описание, ТребуетсяДетальноеОписание = Ложь)
	
	ОписаниеОшибки = Новый Структура;
	ОписаниеОшибки.Вставить("Описание", Описание);
	ОписаниеОшибки.Вставить("ТребуетсяДетальноеОписание", ТребуетсяДетальноеОписание);
	КодыОшибок.Вставить(Код, ОписаниеОшибки);
	
КонецПроцедуры

#КонецОбласти

#Область РезультатВыполнения

&НаСервереБезКонтекста
Функция ПолучитьРезультатВыполненияВФонеНаСервере(Знач АдресЗапроса, Знач ИдентификаторФормы)
	
	Запрос = ПолучитьИзВременногоХранилища(АдресЗапроса);
	Если Запрос = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = 
		СтрШаблон(НСтр("ru = 'Интеграция с платформой Самозанятые: выполнение метода взаимодействия платформы (%1)'"),
			Запрос.МетодВзаимодействия);
	НастройкиЗапуска.ДополнительныйРезультат = Запрос.ДополнительныйРезультат;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполненияВФоне",
		Запрос,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаписатьОшибкуВыполненияМетодаВзаимодействия(КраткоеПредставлениеОшибки, ПодробноеПредставлениеОшибки = Неопределено, КодОшибки = Неопределено)
	
	ПредставлениеОшибки = КраткоеПредставлениеОшибки;
	СтруктураОписанияКодаОшибки = Неопределено;
	
	Если ЗначениеЗаполнено(КодОшибки) И КодыПредупреждений().Свойство(КодОшибки, СтруктураОписанияКодаОшибки) Тогда
		Элементы.ОписаниеКодаПредупреждения.Заголовок = СтруктураОписанияКодаОшибки.Описание;
		Предупреждение = Истина;
		Ошибка = Ложь;
		УровеньЖурнала = УровеньЖурналаРегистрации.Предупреждение;
		Элементы.ПредупреждениеЖурналРегистрации.Видимость = СтруктураОписанияКодаОшибки.ТребуетсяДетальноеОписание;
	ИначеЕсли ЗначениеЗаполнено(КодОшибки) И КодыОшибок().Свойство(КодОшибки, СтруктураОписанияКодаОшибки) Тогда
		Элементы.ОписаниеКодаОшибки.Заголовок = СтруктураОписанияКодаОшибки.Описание;
		Предупреждение = Ложь;
		Ошибка = Истина;
		УровеньЖурнала = УровеньЖурналаРегистрации.Ошибка;
		Элементы.ОшибкаЖурналРегистрации.Видимость = СтруктураОписанияКодаОшибки.ТребуетсяДетальноеОписание;
	Иначе
		Элементы.ОписаниеКодаОшибки.Заголовок = "";
		Элементы.ОписаниеКодаОшибки.Видимость = Ложь;
		Предупреждение = Ложь;
		Ошибка = Истина;
		УровеньЖурнала = УровеньЖурналаРегистрации.Ошибка;
		Элементы.ОшибкаЖурналРегистрации.Видимость = Истина;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(
		ИнтеграцияСПлатформойСамозанятыеКлиентСервер.ИмяСобытияЖурналаРегистрации(),
		УровеньЖурнала,
		,
		,
		?(ПодробноеПредставлениеОшибки <> Неопределено, ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки));
	
КонецПроцедуры

#КонецОбласти

#Область РаботаССостояниями

&НаСервере
Процедура ЗаписатьСостояниеИнтеграцииСПлатформойСамозанятые(Ответ)
	
	РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ЗаписатьСостояниеИнтеграцииСПлатформойСамозанятые(
		Объект.Организация, Ответ);
	ИдентификаторЗаявки = ПолучитьИдентификаторЗаявки(Объект.Организация);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьИнтеграциюСПлатформойСамозанятые(Организация)
	РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ОтключитьИнтеграцию(Организация);
	СостояниеПодключения = ПолучитьСостояниеПодключения(Организация);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСостояниеПодключения(Организация)
	
	Возврат РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПолучитьСостояниеПодключения(Организация);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИдентификаторЗаявки(Организация)
	
	Возврат РегистрыСведений.СостоянияИнтеграцииСПлатформойСамозанятые.ПолучитьИдентификаторЗаявки(Организация);
	
КонецФункции

#КонецОбласти

#Область КодПодтверждения

&НаКлиенте
Процедура ОбновитьСтатусКодаПодтверждения()
	
	Если КодПодтвержденияАктуален() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСтатусКодаПодтверждения", 1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСтатусКодаПодтверждения()
	
	КодПодтвержденияЗапрошен = КодПодтвержденияЗапрошен(ДатаСозданияКодаПодтверждения);
	КодПодтвержденияАктуален = КодПодтвержденияАктуален();
	МожноЗапроситьКодПодтверждения = Не ЗначениеЗаполнено(ДатаОбновленияКодаПодтверждения)
		Или ДатаОбновленияКодаПодтверждения < ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Если КодПодтвержденияЗапрошен И Не КодПодтвержденияАктуален Тогда
		СброситьКодПодтверждения();
		Возврат;
	КонецЕсли;
	
	ОсталосьСекунд = ?(КодПодтвержденияЗапрошен И Не МожноЗапроситьКодПодтверждения, ДатаОбновленияКодаПодтверждения - ОбщегоНазначенияКлиент.ДатаСеанса(), 0);
	Если ОсталосьСекунд > 0 Тогда
		Если ЭтоМобильныйКлиент Тогда
			ОсталосьСекунд = Окр(ОсталосьСекунд);
		КонецЕсли;
		
		ДатаОбновленияКодаПодтвержденияПредставление = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';Повторно запросить SMS с кодом можно через %1 секунду;;Повторно запросить SMS с кодом можно через %1 секунды;Повторно запросить SMS с кодом можно через %1 секунд;Повторно запросить SMS с кодом можно через %1 секунды'"),
			ОсталосьСекунд);
	Иначе
		ДатаОбновленияКодаПодтвержденияПредставление = "";
	КонецЕсли;
	
	УправлениеЭлементамиКодаПодтверждения(Элементы, КодПодтвержденияЗапрошен, МожноЗапроситьКодПодтверждения);
	
	ОбновитьСтатусКодаПодтверждения();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиКодаПодтверждения(Элементы, КодПодтвержденияЗапрошен, МожноЗапроситьКодПодтверждения)
	
	Элементы.ГруппаНомерТелефона.Видимость = Не КодПодтвержденияЗапрошен;
	Элементы.НомерТелефонаПредставление.Видимость = КодПодтвержденияЗапрошен;
	Элементы.ГруппаКодПодтверждения.Видимость = КодПодтвержденияЗапрошен;
	Элементы.ДатаОбновленияКодаПодтвержденияПредставление.Видимость = КодПодтвержденияЗапрошен И Не МожноЗапроситьКодПодтверждения;
	Элементы.ПовторноЗапроситьSMSсКодом.Видимость = КодПодтвержденияЗапрошен И МожноЗапроситьКодПодтверждения;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОбработчикОжидания_ОбновитьСтатусКодаПодтверждения()
	
	ДатаОбновленияКодаПодтвержденияПредставление = "";
	ОтключитьОбработчикОжидания("Подключаемый_ОбновитьСтатусКодаПодтверждения");
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьКодПодтверждения()
	
	// Код просрочен, очищаем свойства кода подтверждения
	Для Каждого ИмяСвойства Из ИменаСвойствКодаПодтверждения() Цикл
		ЭтотОбъект[ИмяСвойства] = Неопределено;
	КонецЦикла;
	
	КодПодтверждения = "";
	ДатаОбновленияКодаПодтвержденияПредставление = "";
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КодПодтвержденияЗапрошен(ДатаСозданияКодаПодтверждения)
	
	Возврат ЗначениеЗаполнено(ДатаСозданияКодаПодтверждения);
	
КонецФункции

&НаСервереБезКонтекста
Функция МожноЗапроситьКодПодтвержденияНаСервере(Знач ДатаОбновленияКодаПодтверждения)
	
	Возврат Не ЗначениеЗаполнено(ДатаОбновленияКодаПодтверждения)
		Или ДатаОбновленияКодаПодтверждения < ТекущаяДатаСеанса();
	
КонецФункции

&НаКлиенте
Функция КодПодтвержденияАктуален()
	
	Возврат ЗначениеЗаполнено(ДатаСрокаДействияКодаПодтверждения)
		И ОбщегоНазначенияКлиент.ДатаСеанса() <= ДатаСрокаДействияКодаПодтверждения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИменаСвойствКодаПодтверждения()
	
	Возврат СтрРазделить("ДатаСозданияКодаПодтверждения,ДатаОбновленияКодаПодтверждения,ДатаСрокаДействияКодаПодтверждения", ",");
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбновитьРеквизитыНалогоплательщика()
	
	ИмяМетодаВзаимодействия = "ДетальныйСтатусНалогоплательщика";
	ВыводитьОкноОжидания = Истина;
	ВыполнитьМетодВзаимодействияВФоне();
	
КонецПроцедуры

#КонецОбласти

#Область Баннер

&НаСервере
Функция ПолучитьБаннерНаСервере()
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	// Баннер показываем только если отсутсвует возможность подключить сервис
	Если ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые() Тогда
		Возврат АдресХранилища;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Организация", Неопределено);
	СтруктураПараметров.Вставить("Размещение",
		ПерсонализированныеПредложенияСервисов.ИмяРазмещенияПомощникИнтеграцииСПлатформойСамозанятые());
	СтруктураПараметров.Вставить("ПоказатьПредыдущий", Ложь);
	
	ПерсонализированныеПредложенияСервисов.ПолучитьБаннер(СтруктураПараметров, АдресХранилища);
	Возврат АдресХранилища;
	
КонецФункции

&НаСервере
Процедура УстановитьБаннерНаФорме(АдресРезультата)
	
	Баннер = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Баннер = Неопределено Тогда
		Возврат;
	Иначе
		Элементы.Баннер.Видимость = Истина;
		Элементы.БаннерСФоном.ЦветФона = ЦветаСтиля[Баннер.ЦветФонаБаннер];
		ТекстБаннера = Баннер.ТекстБаннера;
		Элементы.КартинкаБаннера.Картинка   = БиблиотекаКартинок[Баннер.ИмяКартинкаЛоготипа];
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьЭлементыФормыМК();
	НастроитьОтображениеЭлементовМК();
	НастроитьПоложениеЭлементовМК();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыФормыМК()
	
	НовыйЭлемент = Элементы.Добавить("ГруппаКомандаПодключить", Тип("ГруппаФормы"), Элементы.ГруппаНомерТелефона);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Истина;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаКомандаПодтвердить", Тип("ГруппаФормы"), Элементы.ГруппаКодПодтверждения);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Истина;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаПовторноЗапросить", Тип("ГруппаФормы"), Элементы.ГруппаКодПодтверждения);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаКомандаЗакрытьМК", Тип("ГруппаФормы"), Элементы.ГруппаВыполнено);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Истина;
	НовыйЭлемент.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	
	НоваяКоманда = Команды.Добавить("ЗакрытьМК");
	НоваяКоманда.Действие = "ЗакрытьМК";
	НоваяКоманда.Заголовок = "Закрыть";
	
	НовыйЭлемент = Элементы.Добавить("КомандаЗакрытьМК", Тип("КнопкаФормы"), Элементы.ГруппаКомандаЗакрытьМК);
	
	НовыйЭлемент = Элементы.Добавить("ГруппаОжидаетсяПодтверждение", Тип("ГруппаФормы"), Элементы.ГруппаПроверкаПодключения);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("КартинкаОжидаетсяПодтверждениеМойНалогМК",
									Тип("ДекорацияФормы"),
									Элементы.ГруппаОжидаетсяПодтверждение);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Картинка;
	НовыйЭлемент.Картинка = БиблиотекаКартинок.ДлительнаяОперация48;
	НовыйЭлемент.Видимость = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("НадписьОжидаетсяПодтверждениеМойНалогМК",
									Тип("ДекорацияФормы"),
									Элементы.ГруппаОжидаетсяПодтверждение);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок = НСтр("ru='Ожидается завершение настройки в сервисе Мой налог...'");
	НовыйЭлемент.Видимость = Ложь;
	НовыйЭлемент.Шрифт = Новый Шрифт(,, Истина);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеЭлементовМК()
	
	УстановитьПоложениеЭлементовПоЦентруРекурсивноМК(Элементы);
	УстановитьГоризонтальноеПоложениеЭлементовМК();
	УстановитьВертикальноеПоложениеЭлементовМК();
	
	ОформляемыеАкцентныеКнопки = Новый Массив;
	
	ОформляемыеАкцентныеКнопки.Добавить(Элементы.КомандаЗакрытьМК);
	ОформляемыеАкцентныеКнопки.Добавить(Элементы.Подключить);
	ОформляемыеАкцентныеКнопки.Добавить(Элементы.Подтвердить);
	ОформляемыеАкцентныеКнопки.Добавить(Элементы.Повторить);
	ОформляемыеАкцентныеКнопки.Добавить(Элементы.Назад);
	ОформляемыеАкцентныеКнопки.Добавить(Элементы.Переподключить);
	
	МобильныйКлиентБПФормаОбъекта.ОформитьАкцентнуюКнопкуФормы(ОформляемыеАкцентныеКнопки);
	
	ОформляемыеНейтральныеКнопки = Новый Массив;
	
	ОформляемыеНейтральныеКнопки.Добавить(Элементы.Отключить);
	ОформляемыеНейтральныеКнопки.Добавить(Элементы.ПодключитьОбсуждения);
	ОформляемыеНейтральныеКнопки.Добавить(Элементы.ОтключитьОбсуждения);
	
	МобильныйКлиентБПФормаОбъекта.ОформитьНейтральнуюКнопкуФормы(ОформляемыеНейтральныеКнопки);
	
	Элементы.ДекорацияОписание.Заголовок = "''Мой налог'' - онлайн-сервис ФНС отправки чеков и расчета налога на профессиональный доход ";
	Элементы.ДекорацияЗапросПароля.Заголовок = "Для подключения введите номер телефона, указанный при регистрации в сервисе ''Мой налог''";	
	
	Элементы.ГруппаАвторизация.РастягиватьПоГоризонтали = Истина;
	
	Элементы.ГруппаНомерТелефона.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.НомерТелефона.Ширина = 0;
	Элементы.НомерТелефона.РастягиватьПоГоризонтали = Истина;
	
	Элементы.НомерТелефонаПредставление.РастягиватьПоГоризонтали = Истина;
	
	Элементы.КодПодтверждения.ПодсказкаВвода = "Введите код из SMS";
	
	Элементы.ГруппаКодПодтверждения.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.КодПодтверждения.Ширина = 0;
	Элементы.КодПодтверждения.РастягиватьПоГоризонтали = Истина;
	
	Элементы.ДатаОбновленияКодаПодтвержденияПредставление.Ширина = 0;
	
	Элементы.ПовторноЗапроситьSMSсКодом.Высота = 2;
	
	Элементы.ДекорацияПроверкаПодключения.Заголовок = ТекстИнструкцииПроверкаПодключенияМК();
	
	Элементы.ДекорацияПлатформаСамозанятыеПодтверждение.Картинка = БиблиотекаКартинок.СкринМойНалогМК;
	Элементы.ДекорацияПлатформаСамозанятыеПодтверждение.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	
	Элементы.ГруппаВыполненоШапка.Заголовок = "";
	Элементы.ГруппаВыполненоШапка.ОтображатьЗаголовок = Истина;
	
	Элементы.ДекорацияОтступОповещения.Видимость = Ложь;
	Элементы.ДекорацияОтступСправка.Видимость = Ложь;
	
	Элементы.ГруппаСправкаОПостановкеНаУчетВертикальная.Видимость = Ложь;
	
	Элементы.ПредставлениеОшибки.РастягиватьПоВертикали = Истина;
	Элементы.ПредставлениеПредупреждения.РастягиватьПоВертикали = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВертикальноеПоложениеЭлементовМК()
	
	Элементы.ОтключеноЗаголовок.ВертикальноеПоложение                                  = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.ДатаОбновленияКодаПодтвержденияПредставление.ВертикальноеПоложение        = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.ДекорацияЗапросПароля.ВертикальноеПоложение                               = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.ДекорацияОписание.ВертикальноеПоложение                                   = ВертикальноеПоложениеЭлемента.Центр;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьГоризонтальноеПоложениеЭлементовМК()
	
	Элементы.ДекорацияОписание.ГоризонтальноеПоложение                                   = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ДекорацияЗапросПароля.ГоризонтальноеПоложение                               = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ОтключеноЗаголовок.ГоризонтальноеПоложение                                  = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ДатаОбновленияКодаПодтвержденияПредставление.ГоризонтальноеПоложение        = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.НомерТелефонаПредставление.ГоризонтальноеПоложение                          = ГоризонтальноеПоложениеЭлемента.Лево;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПоложениеЭлементовПоЦентруРекурсивноМК(ЭлементыФормы)
	
	Для каждого ЭлементФормы Из ЭлементыФормы Цикл
		Если ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") И ЭлементФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа Тогда
			ЭлементФормы.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
			ЭлементФормы.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Центр;
			
			УстановитьПоложениеЭлементовПоЦентруРекурсивноМК(ЭлементФормы.ПодчиненныеЭлементы);
		КонецЕсли;
		
		Если ТипЗнч(ЭлементФормы.Родитель) = Тип("ГруппаФормы") Тогда
			ЭлементФормы.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
			ЭлементФормы.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПоложениеЭлементовМК()
	
	Элементы.Переместить(Элементы.Подключить, Элементы.ГруппаКомандаПодключить);
	Элементы.Переместить(Элементы.Подтвердить, Элементы.ГруппаКомандаПодтвердить);
	Элементы.Переместить(Элементы.ДатаОбновленияКодаПодтвержденияПредставление, Элементы.ГруппаПовторноЗапросить);
	Элементы.Переместить(Элементы.ПовторноЗапроситьSMSсКодом, Элементы.ГруппаПовторноЗапросить);
	Элементы.Переместить(Элементы.ГруппаПовторноЗапросить, Элементы.ГруппаКодПодтверждения, Элементы.ГруппаКомандаПодтвердить);
	
КонецПроцедуры

&НаСервере
Функция ТекстИнструкцииПроверкаПодключенияМК()
	
	ТекстИнструкции = СтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = '- Для завершения настройки перейдите <a href = ""СтраницаНастройкиНПД"">на сайт Мой налог</a>
					|- Выберите раздел Партнеры
					|- Найдите партнера Фирма 1С
					|- Подтвердите запрос прав (выберите Разрешить и нажмите Подтвердить)'"));
	
	Возврат ТекстИнструкции;
	
КонецФункции

&НаКлиенте
Процедура ЗакрытьМК(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
