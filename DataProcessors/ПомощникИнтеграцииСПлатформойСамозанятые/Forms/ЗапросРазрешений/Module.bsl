
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	Разрешения = Параметры.Разрешения;
	Идентификатор = Параметры.Идентификатор;
	
	СервисМойНалогПодключен = ИнтеграцияСПлатформойСамозанятыеВызовСервера.ПриложениеПодключено(Организация);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПояснениеШаг1ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресСтраницыПартнеры());
	
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеШаг3КартинкаМойНалогНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресСтраницыПартнеры());
	
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеШаг4ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(АдресСтраницыПартнеры());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьРазрешения(Команда)
	
	СтатусПроверкиРазрешений = "";
	
	НачатьВыполнениеМетодаВзаимодействия(МетодыВзаимодействия().СтатусЗапросаНаИзменениеРазрешений);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	Элементы.КартинкаСтатусПроверкиРазрешений.Видимость =
		(Форма.СтатусПроверкиРазрешений = СтатусыЗапросов.Выполняется);
	Элементы.ПояснениеСтатусПроверкиРазрешений.Видимость = ЗначениеЗаполнено(Форма.СтатусПроверкиРазрешений);
	Элементы.ПроверитьРазрешения.Доступность = (Форма.СтатусПроверкиРазрешений <> СтатусыЗапросов.Выполняется);
	Элементы.ПроверитьРазрешения.Заголовок = НСтр("ru = 'Разрешения предоставлены'");
	Элементы.РазрешенияПредоставленыКартинка.Видимость = Форма.РазрешенияПредоставлены;
	Элементы.ГруппаПроверкаРазрешений.Видимость = Не Форма.РазрешенияПредоставлены;
	
	Если Не Форма.СервисМойНалогПодключен Тогда
		Форма.ПояснениеСтатусПроверкиРазрешений = НСтр("ru = 'Сервис Мой налог не подключен'");
		Элементы.ПояснениеСтатусПроверкиРазрешений.ЦветТекста = ЦветНеудачнаяПроверка();
		Элементы.ГруппаСтатусПроверкиРазрешений.Видимость = Ложь;
	ИначеЕсли Форма.СтатусПроверкиРазрешений = СтатусыЗапросов.Выполняется Тогда
		Форма.ПояснениеСтатусПроверкиРазрешений = НСтр("ru = 'Проверяются разрешения...'");
		Элементы.ПояснениеСтатусПроверкиРазрешений.ЦветТекста = Новый Цвет;
	ИначеЕсли Форма.РазрешенияПредоставлены Тогда
		Элементы.ПроверитьРазрешения.Доступность = Ложь;
		Элементы.ГруппаСтатусПроверкиРазрешений.Видимость = Истина;
		Элементы.КартинкаСтатусПроверкиРазрешений.Видимость = Ложь;
	ИначеЕсли ЗначениеЗаполнено(Форма.ПредставлениеОшибки) Тогда
		Элементы.ОшибкаСтатусПроверки.Видимость = Истина;
		Элементы.ОписаниеОшибкиПроверкиСтатусаЗаявки.Видимость = Истина;
		Элементы.ГруппаСтатусПроверкиРазрешений.Видимость = Ложь;
		Элементы.ПроверитьРазрешения.Заголовок = НСтр("ru = 'Повторить'");
	Иначе
		Элементы.ОшибкаСтатусПроверки.Видимость = Ложь;
		Элементы.ОписаниеОшибкиПроверкиСтатусаЗаявки.Видимость = Ложь;
		МожноОтправитьПовторныйЗапрос = Не ЗначениеЗаполнено(Форма.СрокПовторногоЗапросаСтатусаПодключения);
		Элементы.ПроверитьРазрешения.Доступность = МожноОтправитьПовторныйЗапрос;
		Элементы.КартинкаСтатусПроверкиРазрешений.Видимость = Ложь;
		Элементы.ПояснениеСтатусПроверкиРазрешений.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЦветНеудачнаяПроверка()
	
	Возврат ЦветаСтиля.РезультатПроблемаЦвет;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция АдресСтраницыПартнеры()

	Возврат ИнтеграцияСПлатформойСамозанятыеКлиентСервер.АдресСтраницыПартнеры();

КонецФункции

#Область ВыполнениеЗапросов

&НаКлиенте
Процедура НачатьВыполнениеМетодаВзаимодействия(МетодВзаимодействия)
	
	Если Не СервисМойНалогПодключен Тогда
		УправлениеФормой(ЭтотОбъект);
		Возврат;
	КонецЕсли;

	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	
	ДлительнаяОперация = ВыполнитьМетодВзаимодействияНаСервере(
		ПараметрыМетодаВзаимодействия(МетодВзаимодействия), УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		ОбновитьСтатусы(ЭтотОбъект, МетодВзаимодействия, СтатусыЗапросов.Ошибка);
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		ОбновитьСтатусы(ЭтотОбъект, МетодВзаимодействия, СтатусыЗапросов.Выполняется);
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Запрос", МетодВзаимодействия);
		ДополнительныеПараметры.Вставить("Организация", Организация);
		
		ПредставлениеОшибки = "";
		Обработчик = Новый ОписаниеОповещения("ПослеВыполненияМетодаВзаимодействия", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	Иначе
		ПоказатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполнитьМетодВзаимодействияНаСервере(Знач ПараметрыМетода, Знач ИдентификаторФормы)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатформойСамозанятые.ВыполнитьМетодВзаимодействияВФоне",
		ПараметрыМетода,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПослеВыполненияМетодаВзаимодействия(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Организация <> Организация Тогда
		Возврат; // Задание неактуально. После запуска этого задания были изменены ключевые реквизиты.
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		
		ДанныеЗапроса = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если ЗначениеЗаполнено(ДанныеЗапроса) Тогда
			ВремяОжидания = ДанныеЗапроса.ВремяОжидания;
		Иначе 
			ВремяОжидания = 0;
		КонецЕсли;
		
		ПодключитьОбработчикОжидания(
			СтрШаблон("Подключаемый_ПолучитьРезультатВыполненияВФоне%1", ДополнительныеПараметры.Запрос),
			ВремяОжидания,
			Истина);
		
		ПредставлениеОшибки = "";
		
	Иначе
		ПоказатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьРезультатВыполненияВФонеСтатусЗапросаНаИзменениеРазрешений()
	
	НачатьПолучениеРезультатаВыполненияВФоне(МетодыВзаимодействия().СтатусЗапросаНаИзменениеРазрешений);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеРезультатаВыполненияВФоне(МетодВзаимодействия)
	
	ДлительнаяОперация = ПолучитьРезультатВыполненияВФонеНаСервере(ДанныеЗапроса, УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Запрос", МетодВзаимодействия);
		ДополнительныеПараметры.Вставить("Организация", Организация);
		
		СрокПовторногоЗапросаСтатусаПодключения = ДанныеЗапроса.СрокПовторногоЗапроса;
		Если ЗначениеЗаполнено(СрокПовторногоЗапросаСтатусаПодключения) Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ПовторнаяОтправкаЗапросаСтатусаПодключения", 1, Истина);
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
		
		ПредставлениеОшибки = "";
		Обработчик = Новый ОписаниеОповещения("ПослеПолученияРезультатаВыполненияМетода", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	Иначе
		ПоказатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРезультатВыполненияВФонеНаСервере(Знач Запрос, Знач ИдентификаторФормы)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.ДополнительныйРезультат = Запрос.ДополнительныйРезультат;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияСПлатформойСамозанятые.ПолучитьРезультатВыполненияВФоне",
		Запрос,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПослеПолученияРезультатаВыполненияМетода(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Организация <> Организация Тогда
		Возврат; // Задание неактуально. После запуска этого задания были изменены ключевые реквизиты.
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ПоказатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация);
		Возврат;
	КонецЕсли;
	
	ПредставлениеОшибки = "";
	Ответ = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
	
	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСтатусы(ЭтотОбъект, ДополнительныеПараметры.Запрос, Ответ.Статус);
	
	СтатусыЗапросов = ИнтеграцияСПлатформойСамозанятыеКлиентСервер.СтатусыЗапросов();
	Если Ответ.Статус = СтатусыЗапросов.Выполнено Тогда
		
		ОбработатьПолученныеРезультаты(Ответ.Результат, ДополнительныеПараметры.Запрос);
		
		УправлениеФормой(ЭтотОбъект);
		
	ИначеЕсли Ответ.Статус = СтатусыЗапросов.Выполняется Тогда
		ПодключитьОбработчикОжидания(
			СтрШаблон("Подключаемый_ПолучитьРезультатВыполненияВФоне%1", ДополнительныеПараметры.Запрос),
			Ответ.ВремяОжидания,
			Истина);
	ИначеЕсли Ответ.Статус = СтатусыЗапросов.Ошибка Тогда
		
		ЗаписатьОшибкуВЖурналРегистрации(Ответ.Сообщение);
		
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолученныеРезультаты(Ответ, МетодВзаимодействия)
	
	Если ТипЗнч(Ответ) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если МетодВзаимодействия = МетодыВзаимодействия().СтатусЗапросаНаИзменениеРазрешений Тогда
		РазрешенияПредоставлены = (ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Ответ, "Результат", "") = "Принято");
	КонецЕсли;
	
	Если РазрешенияПредоставлены Тогда
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Организация", Организация);
		ПараметрыОповещения.Вставить("Разрешения", Разрешения);
		
		Оповестить("Самозанятые_ИзмененыРазрешения", ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыМетодаВзаимодействия(МетодВзаимодействия)
	
	ПараметрыМетода = 
		ИнтеграцияСПлатформойСамозанятыеКлиентСервер.НовыеПараметрыМетодаВзаимодействия(МетодВзаимодействия);
	
	ЗаполнитьЗначенияСвойств(ПараметрыМетода, ЭтотОбъект);
	
	Возврат ПараметрыМетода;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСтатусы(Форма, МетодВзаимодействия, НовыйСтатус)
	
	Форма.СтатусПроверкиРазрешений = НовыйСтатус;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуВыполненияМетодаВзаимодействия(ДлительнаяОперация)
	
	ПредставлениеОшибки = ДлительнаяОперация.КраткоеПредставлениеОшибки;
	
	ЗаписатьОшибкуВЖурналРегистрации(ПредставлениеОшибки);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки)
	
	ЗаписьЖурналаРегистрации(
		Обработки.ПомощникИнтеграцииСПлатформойСамозанятые.ИмяСобытияЖурналаРегистрацииИзменениеРазрешений(),
		УровеньЖурналаРегистрации.Ошибка, , ,
		ТекстОшибки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МетодыВзаимодействия()
	
	МетодыВзаимодействия = Новый Структура;
	МетодыВзаимодействия.Вставить("СтатусЗапросаНаИзменениеРазрешений", "СтатусЗапросаНаИзменениеРазрешений");
	
	Возврат МетодыВзаимодействия;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПовторнаяОтправкаЗапросаСтатусаПодключения()
	
	ТекстПовторногоЗапроса = ТекстПовторногоЗапросаСтатусаПодключения(СрокПовторногоЗапросаСтатусаПодключения);
	
	Если ЗначениеЗаполнено(ТекстПовторногоЗапроса) И Не РазрешенияПредоставлены Тогда
		Элементы.ГруппаПроверкаРазрешений.Подсказка = ТекстПовторногоЗапроса;
		ПодключитьОбработчикОжидания("Подключаемый_ПовторнаяОтправкаЗапросаСтатусаПодключения", 1, Истина);
	Иначе
		Элементы.ГруппаПроверкаРазрешений.Подсказка = "";
		ОтключитьОбработчикОжидания("Подключаемый_ПовторнаяОтправкаЗапросаСтатусаПодключения");
		СрокПовторногоЗапросаСтатусаПодключения = Неопределено;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстПовторногоЗапросаСтатусаПодключения(СрокДействияЗапроса)
	
	Если Не ЗначениеЗаполнено(СрокДействияЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекущаяДата = УниверсальноеВремя(ТекущаяДатаСеанса(), ЧасовойПоясСеанса());
	РазностьДат = СрокДействияЗапроса - ТекущаяДата;
	
	Если РазностьДат > 0 Тогда
		Возврат СтрШаблон(НСтр("ru = 'Повторно проверить разрешения можно через %1 сек.'"), РазностьДат);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#КонецОбласти