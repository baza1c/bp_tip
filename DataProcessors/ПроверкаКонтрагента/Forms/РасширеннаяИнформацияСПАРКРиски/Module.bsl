///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем ОтключитьФормированиеОтчета;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СПАРКРиски") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МодульСПАРКРиски = ОбщегоНазначения.ОбщийМодуль("СПАРКРиски");
	ИспользованиеРазрешено = МодульСПАРКРиски.ИспользованиеРазрешено();
	
	Если Не ИспользованиеРазрешено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Подготовка временного хранилища для сохранения промежуточных результатов.
	АдресХранилищаДанныеСобытияМониторинга = ПоместитьВоВременноеХранилище(
		Неопределено,
		УникальныйИдентификатор);
	АдресХранилищаДанныеФакторыРиска = ПоместитьВоВременноеХранилище(
		Неопределено,
		УникальныйИдентификатор);
	
	СкрытьЭлементыПриСоздании();
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	ИННКонтрагента = Параметры.ИНН;
	Контрагент   = Параметры.Контрагент;
	
	Если ЗначениеЗаполнено(Контрагент)
			И ПустаяСтрока(ИННКонтрагента) Тогда
		
		ИННКонтрагента = ЗначениеИННКонтрагента(Контрагент);
	
	КонецЕсли;
	
	Если (СтрДлина(ИННКонтрагента) <> 10
		И СтрДлина(ИННКонтрагента) <> 12) Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	ЭтоЮридическоеЛицо = СтрДлина(ИННКонтрагента) = 10;
	СформироватьОтчетНаСервере();
	
	АдаптироватьФормуПодМобильныйКлиент();
	
	УведомленияПользователя = Новый Структура();
	ПоказатьУведомления();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбщиеСведения;
	Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	
	ПараметрыОтображения = Новый Структура("ВариантОтображения", "Многострочный");
	ВидКонтрагентаСПАРКРиски = ?(ЭтоЮридическоеЛицо,
		ОбщегоНазначения.ПредопределенныйЭлемент(
			"Перечисление.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо"),
		ОбщегоНазначения.ПредопределенныйЭлемент(
			"Перечисление.ВидыКонтрагентовСПАРКРиски.ИндивидуальныйПредприниматель"));
	
	Если Контрагент = Неопределено Тогда
		МодульСПАРКРиски.ПриСозданииНаСервере(
			ЭтотОбъект,
			,
			ИННКонтрагента,
			ВидКонтрагентаСПАРКРиски,
			ПараметрыОтображения);
	Иначе
		МодульСПАРКРиски.ПриСозданииНаСервере(
			ЭтотОбъект,
			,
			Контрагент,
			ВидКонтрагентаСПАРКРиски,
			ПараметрыОтображения);
	КонецЕсли;
	
	НастроитьТабличныеДокументы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Элементы.СтраницаФормированиеОтчета.Видимость = Истина;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаФормированиеОтчета;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
				ДлительнаяОперация,
				Новый ОписаниеОповещения("ПриЗавершенииЗадания", ЭтотОбъект),
				ПараметрыОжидания);
	ИначеЕсли ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПоказатьПредупреждениеОбОшибке", 0.1, Истина);
	ИначеЕсли ОжиданиеОтвета Тогда
		ПодключитьОбработчикОжидания("Подключаемый_СформироватьОтчет", 1.5, Истина);
		Элементы.СтраницаФормированиеОтчета.Видимость = Истина;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаФормированиеОтчета;
	КонецЕсли;
	
	ТекущийЭлемент = Элементы.ГруппаРазделОбщиеСведенияСтраницы;
	ПереключитьРазделГлавноеМеню(
		РазделОбщиеСведения());
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ПриОткрытии(ЭтотОбъект, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаОповещения(
		ЭтотОбъект,
		Неопределено,
		ИмяСобытия,
		Параметр,
		Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияВсеСобытияМониторингаНажатие(Элемент)
	
	СобытияМониторинга(Неопределено);
	
КонецПроцедуры

#Область СправкиСПАРКРиски

&НаКлиенте
Процедура ДекорацияПоказатьВсеСправкиСПАРКНажатие(Элемент)
	
	Состояние(, , НСтр("ru = 'Переход на Портал 1С:ИТС'"));
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	ИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуИнтегрированногоСайта(
		МодульСПАРКРискиКлиент.АдресСтраницыЛичныйКабинетВсеСправки(),
		НСтр("ru = 'Справки 1СПАРК Риски'"));
	Состояние();

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗапроситьСправкуСПАРКНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Контрагент с ИНН %1 не создан.'"),
				ИННКонтрагента));
		Возврат;
	КонецЕсли;
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОткрытьСписокСправок1СПАРКРиски(Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияАктуальнаяСправкаСПАРКНажатие(Элемент)
	
	Если АктуальнаяСправка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОткрытьСправкуСПАРКРиски(
		Неопределено,
		АктуальнаяСправка,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ДекорацияПереходВВебВерсиюСПАРКОписаниеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Данные еще не записаны.
			|Переход в веб-версию СПАРК возможен только после записи данных.'"));
		Возврат;
	КонецЕсли;
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ПерейтиВВебВерсиюСПАРК(
		Контрагент,
		Неопределено,
		Истина);
	
КонецПроцедуры

#Область ИндексыСПАРКРиски

&НаКлиенте
Процедура ДекорацияИндексыСПАРКРискиСтатусЗначениеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСводныйИндикаторОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИДОЗначениеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИФРЗначениеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИПДЗначениеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПереданаБухгалтерскаяОтчетностьОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИндексыСПАРКРискиОписаниеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура КартинкаИндексОбщийНажатие(Элемент, СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		"SPARK:WhatIsCompositeIndex",
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаИндексДОНажатие(Элемент, СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		"SPARK:WhatIsIndexOfDueDiligence",
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаИндексФРНажатие(Элемент, СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		"SPARK:WhatIsFailureScore",
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаИндексПДНажатие(Элемент, СтандартнаяОбработка)
	
	МодульСПАРКРискиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СПАРКРискиКлиент");
	МодульСПАРКРискиКлиент.ОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		Элемент,
		"SPARK:WhatIsPaymentIndex",
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ИсполнительныеПроизводстваПриИзменении(Элемент)
	
	ОбновитьСобытияМониторингСПАРКПоФильтру();
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоКритичныеПриИзменении(Элемент)
	
	ОбновитьСобытияМониторингСПАРКПоФильтру();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатСобытияМониторингаОбработкаРасшифровки(
		Элемент,
		Расшифровка,
		СтандартнаяОбработка,
		ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	
	ИдентификаторСтраницы = ИдентификаторСтраницы(Расшифровка);
	Если ИдентификаторСтраницы <> Неопределено Тогда
		ПараметрыЗапроса = Новый Структура(
			"ИдентификаторСтраницы",
			ИдентификаторСтраницы);
	Иначе
		ПараметрыЗапроса = Новый Структура(
			"ВключитьИсполнительныеПроизводства, ВключитьКритичные",
			?(ИсполнительныеПроизводства, Истина, Неопределено),
			?(ТолькоКритичные, Истина, Неопределено));
	КонецЕсли;
	
	ОбновитьСобытияМониторингСПАРК(
		ПараметрыЗапроса,
		Расшифровка,
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУведомлениеСПАРКРискиПодробнееНажатие(Элемент)
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.МониторПортала1СИТС") Тогда
		Возврат;
	КонецЕсли;
	
	МодульМониторПортала1СИТСКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("МониторПортала1СИТСКлиент");
	МодульМониторПортала1СИТСКлиент.ОткрытьСообщениеОбОпцияхИнтернетПоддержки(
		УведомленияПользователя.СПАРКРиски);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДанныеОбАрбитражныхДелахЗаголовокНажатие(Элемент)
	ПереключитьРазделГлавноеМеню(
		РазделДанныеОбАрбитражныхДелах());
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДанныеГосударственныеКонтрактыЗаголовокНажатие(Элемент)
	ПереключитьРазделГлавноеМеню(
		РазделДанныеГосударственныеКонтракты());
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьПолныйОтчет(Команда)
	
	Если Контрагент = Неопределено Тогда
		ПроверкаКонтрагентаКлиент.Проверить(ИННКонтрагента);
	Иначе
		ПроверкаКонтрагентаКлиент.Проверить(Контрагент);
	КонецЕсли;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	Если Не ОтчетСформирован Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Печать недоступна, дождитесь завершения формирования отчета.'"));
		Возврат;
	КонецЕсли;
	
	РезультатПроверкаКонтрагентаДляПечати = РезультатПроверкаКонтрагентаДляПечати();
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
		
		МодульУправлениеПечатьюКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеПечатьюКлиент");
		
		ИдентификаторПечатнойФормы = "ПроверкаКонтрагента";
		КоллекцияПечатныхФорм = МодульУправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
		
		ПечатнаяФорма = МодульУправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Проверка контрагента'");
		ПечатнаяФорма.ТабличныйДокумент = РезультатПроверкаКонтрагентаДляПечати;
		ПечатнаяФорма.ИмяФайлаПечатнойФормы = СтроковыеФункцииКлиент.СтрокаЛатиницей(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Проверка контрагента %1.pdf'"),
				ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(
					Контрагент, "")));
		
		ДополнительныеПараметры = МодульУправлениеПечатьюКлиент.ПараметрыПечати();
		ДополнительныеПараметры.ЗаголовокФормы = НСтр("ru = 'Проверка контрагента'");
		
		МодульУправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм, , ДополнительныеПараметры);
		
	Иначе
		РезультатПроверкаКонтрагентаДляПечати.Показать(НСтр("ru = 'Проверка контрагента'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкачатьКарточку(Команда)
	
	ПакетОтображаемыхДокументов = РезультатПроверкаКонтрагентаДляСохранения();
	
	НачатьПолучениеФайлаССервера(
		ПакетОтображаемыхДокументов.АдресФайла,
		ПакетОтображаемыхДокументов.Представление,
		Новый ПараметрыДиалогаПолученияФайлов(
			НСтр("ru = 'Сохранение результат проверки контрагента'")));
	
КонецПроцедуры

#Область НавигационнаяКоманднаяПанель

&НаКлиенте
Процедура ОбщиеСведения(Команда)
	
	ПереключитьРазделГлавноеМеню(
		РазделОбщиеСведения());
	
КонецПроцедуры

&НаКлиенте
Процедура СобытияМониторинга(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаСобытияМониторинга Тогда
		Возврат;
	КонецЕсли;
	
	ПереключитьРазделГлавноеМеню(
		РазделСобытияМониторинга());
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеОбАрбитражныхДелах(Команда)
	ПереключитьРазделГлавноеМеню(
		РазделДанныеОбАрбитражныхДелах());
КонецПроцедуры

&НаКлиенте
Процедура ДанныеГосударственныеКонтракты(Команда)
	ПереключитьРазделГлавноеМеню(
		РазделДанныеГосударственныеКонтракты());
КонецПроцедуры

&НаКлиенте
Процедура ФакторыТребующиеВнимания(Команда)
	ПереключитьРазделГлавноеМеню(
		РазделФакторыТребующиеВнимания());
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СкрытьЭлементыПриСоздании()
	
	Элементы.ГруппаУведомлениеСервисаСПАРКРиски.Видимость = Ложь;
	
	Элементы.ПечатьПроверкиКонтрагента.Видимость = Ложь;
	
	СкрытьКарточки1СПАРКРиски();
	Элементы.ГруппаСПАРКСобытияМониторинга.Видимость = Ложь;
	Элементы.ГруппаСобытиеМониторинга0.Видимость = Ложь;
	Элементы.ГруппаСобытиеМониторинга1.Видимость = Ложь;
	Элементы.ГруппаСобытиеМониторинга2.Видимость = Ложь;
	
	Элементы.ГруппаИндексыСПАРКРиски.Видимость = Ложь;
	
	Элементы.СтраницаСобытияМониторинга.Видимость = Ложь;
	Элементы.СтраницаДанныеОбАрбитражныхДелах.Видимость = Ложь;
	Элементы.СтраницаДанныеГосударственныеКонтракты.Видимость = Ложь;
	Элементы.СтраницаФакторыТребующиеВнимания.Видимость = Ложь;
	
	Элементы.ГруппаРазделОбщиеСведенияСтраницы.Видимость = Ложь;
	Элементы.ГруппаРазделСобытияМониторингаСтраницы.Видимость = Ложь;
	Элементы.ГруппаРазделДанныеОбАрбитражныхДелахСтраницы.Видимость = Ложь;
	Элементы.ГруппаРазделДанныеГосударственныеКонтрактыСтраницы.Видимость = Ложь;
	Элементы.ГруппаРазделФакторыТребующиеВниманияСтраницы.Видимость = Ложь;
	
	Элементы.ГруппаИндексыСПАРКРискиСтатус.Видимость = Ложь;
	Элементы.ГруппаПереданаБухгалтерскаяОтчетностьВСПАРК.Видимость = Ложь;
	Элементы.ГруппаЗначенияИндексов.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетНаКлиенте(ИННПоиска, ЭтоПроверкаРезультата = Ложь)
	
	ОтчетСформирован = Ложь;
	
	Если Не ЗначениеЗаполнено(ИННПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	ДлинаТекстПоиска = СтрДлина(ИННПоиска);
	ПоискПоИНН = (СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ИННПоиска)
		И (ДлинаТекстПоиска = 10
		Или ДлинаТекстПоиска = 12));
	Если ПоискПоИНН Тогда
		
		ИННКонтрагента = ИННПоиска;
		
		ЭтоЮридическоеЛицо = СтрДлина(ИННКонтрагента) = 10;
		
		Элементы.СкачатьКарточку.Видимость = Ложь;
		Элементы.ПечатьПроверкиКонтрагента.Видимость = Ложь;
		
		Элементы.СтраницаФормированиеОтчета.Видимость = Истина;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаФормированиеОтчета;
		
		Результат = СформироватьОтчетНаСервере(ЭтоПроверкаРезультата);
		
		Если Не Результат Тогда
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
			ДлительныеОперацииКлиент.ОжидатьЗавершение(
				ДлительнаяОперация,
				Новый ОписаниеОповещения("ПриЗавершенииЗадания", ЭтотОбъект),
				ПараметрыОжидания);
		Иначе
			
			ОбработатьОшибкиФормированияОтчета();
			
			Если ОжиданиеОтвета Тогда
				// Повторный вызов процедуры формирования при асинхронном получении данных от сервиса.
				ПодключитьОбработчикОжидания("Подключаемый_СформироватьОтчет", 1.5, Истина);
			Иначе
				Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
					Элементы.СтраницаФормированиеОтчета.Видимость = Ложь;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		ИндексыСПАРКРиски = Неопределено;
		ОбновитьОтображениеИндексыСПАРК();
		
	КонецЕсли;
	
	ОтключитьФормированиеОтчета = Истина;
	ПодключитьОбработчикОжидания("Подключаемый_ВключитьФормированиеОтчета", 0.1, Истина);
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере(Знач ЭтоПроверкаРезультата = Ложь)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	ОписаниеОшибки       = "";
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не ЭтоПроверкаРезультата Тогда
		// Очистка результата при формировании нового отчета.
		ОжиданиеОтвета = Ложь;
		СостояниеФормированияОтчета = Новый Структура;
		СостояниеФормированияОтчета.Вставить("СостояниеСобытияМониторинга", "");
		СостояниеФормированияОтчета.Вставить("СостояниеФакторыРиска",       "");
		СостояниеФормированияОтчета.Вставить("ОчищеноСодержимоеДокументов", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Контрагент)
		И ЗначениеИННКонтрагента(Контрагент) <> ИННКонтрагента Тогда
		
		Контрагент = Неопределено;
		
	КонецЕсли;
	
	Если Не РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИННКонтрагента, ЭтоЮридическоеЛицо, ОписаниеОшибки) Тогда
		ОчиститьСодержимоеДокументов();
		ЗаполнитьОписаниеОшибкиФормированияОтчета(ОписаниеОшибки);
		ОжиданиеОтвета = Ложь;
		Возврат Истина;
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ИНН",                         ИННКонтрагента);
	ПараметрыОтчета.Вставить("СостояниеСобытияМониторинга", СостояниеФормированияОтчета.СостояниеСобытияМониторинга);
	ПараметрыОтчета.Вставить("СостояниеФакторыРиска",       СостояниеФормированияОтчета.СостояниеФакторыРиска);
	ПараметрыОтчета.Вставить("Контрагент",                  Контрагент);
	ПараметрыОтчета.Вставить("ИдентификаторФормы",          УникальныйИдентификатор);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Формирование отчета: Проверка контрагента: %1'"),
		ИННКонтрагента);
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Обработки.ПроверкаКонтрагента.РасширеннаяИнформацияСПАРКРиски",
		ПараметрыОтчета);
	АдресХранилища = ДлительнаяОперация.АдресРезультата;
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
		Возврат Ложь;
	Иначе
		ИдентификаторЗадания = Неопределено;
		Если ДлительнаяОперация.Статус = "Выполнено" Тогда
			ЗагрузитьПодготовленныеДанные();
			Возврат Истина;
		ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
			ОчиститьСодержимоеДокументов();
			ЗаполнитьОписаниеОшибкиФормированияОтчета(ДлительнаяОперация.ПодробноеПредставлениеОшибки);
			ОжиданиеОтвета = Ложь;
			Возврат Ложь;
		Иначе
			ВызватьИсключение НСтр("ru = 'Задание отменено.'");
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(Знач ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеИННКонтрагента(КонтрагентСсылка)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.РаботаСКонтрагентами") Тогда
		МодульРаботаСКонтрагентами = ОбщегоНазначения.ОбщийМодуль("РаботаСКонтрагентами");
		СвойстваСправочника = МодульРаботаСКонтрагентами.СвойстваСправочникаКонтрагенты(КонтрагентСсылка);
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтрагентСсылка, СвойстваСправочника.РеквизитИНН);
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные()
	
	ДанныеОтчета = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	УдалитьИзВременногоХранилища(АдресХранилища);
	
	Если Не СостояниеФормированияОтчета.ОчищеноСодержимоеДокументов Тогда
		ОчиститьСодержимоеДокументов();
	КонецЕсли;
	
	// Если результат не сформирован или нет данных во временном хранилище
	// выполнять обработку не требуется.
	Если ДанныеОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ДанныеОтчета.СостояниеСобытияМониторинга) Тогда
		СостояниеФормированияОтчета.СостояниеСобытияМониторинга = ДанныеОтчета.СостояниеСобытияМониторинга;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ДанныеОтчета.СостояниеФакторыРиска) Тогда
		СостояниеФормированияОтчета.СостояниеФакторыРиска = ДанныеОтчета.СостояниеФакторыРиска;
	КонецЕсли;
	
	ОписаниеОшибки = ДанныеОтчета.ОписаниеОшибки;
	Контрагент     = ДанныеОтчета.Контрагент;
	ОжиданиеОтвета = ПустаяСтрока(ОписаниеОшибки)
		И (СостояниеФормированияОтчета.СостояниеФакторыРиска = "Ожидание");
	
	ДанныеСобытияМониторинга  = Неопределено;
	
	Если СостояниеФормированияОтчета.СостояниеСобытияМониторинга = "СформированОтчет" Тогда
		ДанныеСобытияМониторинга = ДанныеОтчета.ДанныеСобытияМониторинга;
		Если ОжиданиеОтвета Тогда
			ПоместитьВоВременноеХранилище(ДанныеСобытияМониторинга, АдресХранилищаДанныеСобытияМониторинга);
		КонецЕсли;
		СостояниеФормированияОтчета.СостояниеСобытияМониторинга = "Завершено";
	КонецЕсли;
	
	Если СостояниеФормированияОтчета.СостояниеФакторыРиска = "СформированОтчет" Тогда
		ДанныеФакторыРиска = ДанныеОтчета.ДанныеФакторыРиска;
		Если ОжиданиеОтвета Тогда
			ПоместитьВоВременноеХранилище(ДанныеФакторыРиска, АдресХранилищаДанныеФакторыРиска);
		КонецЕсли;
		СостояниеФормированияОтчета.СостояниеФакторыРиска = "Завершено";
	КонецЕсли;
	
	// Заполнение
	Если Не ОжиданиеОтвета
		И Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		
		Если ДанныеСобытияМониторинга = Неопределено Тогда
			// Данные во временном хранилище.
			ДанныеСобытияМониторинга = ПолучитьИзВременногоХранилища(АдресХранилищаДанныеСобытияМониторинга);
			// Очистка временного хранилища
			ПоместитьВоВременноеХранилище(Неопределено, АдресХранилищаДанныеСобытияМониторинга);
		КонецЕсли;
		
		Если ДанныеФакторыРиска = Неопределено Тогда
			// Данные во временном хранилище.
			ДанныеФакторыРиска = ПолучитьИзВременногоХранилища(АдресХранилищаДанныеФакторыРиска);
			// Очистка временного хранилища
			ПоместитьВоВременноеХранилище(Неопределено, АдресХранилищаДанныеФакторыРиска);
		КонецЕсли;
		
		ОтчетСформирован = Истина;
		
		Элементы.ГруппаСтраницы.Видимость = Истина;
		
		НастроитьВидимостьКнопки("ОбщиеСведения", Истина);
		ВывестиКарточкуСобытияМониторинга(ДанныеСобытияМониторинга);
		ВывестиКарточкуСправкиСПАРКРиски(ДанныеОтчета);
		ВывестиКарточкуФакторыРиска(ДанныеФакторыРиска);
		ВывестиКарточкуФакторыРиска115ФЗ(ДанныеФакторыРиска);
		ВывестиКарточкуНегативныеСписки(ДанныеФакторыРиска);
		ВывестиКарточкуПризнакиХозяйственнойДеятельности(ДанныеФакторыРиска);
		
		Элементы.ГруппаСПАРКРискиИндексы.Видимость = Истина;
		ВывестиКарточкуПереходВВебВерсиюСПАРК();
		
		ВывестиСтраницуСобытияМониторинга(ДанныеСобытияМониторинга);
		ВывестиСтраницуДанныеОбАрбитражныхДелах(ДанныеФакторыРиска);
		ВывестиСтраницуДанныеГосударственныеКонтракты(ДанныеФакторыРиска);
		ВывестиСтраницуФакторыТребующиеВнимания(ДанныеФакторыРиска);
		
		Элементы.СкачатьКарточку.Видимость = Истина;
		Элементы.ПечатьПроверкиКонтрагента.Видимость = Истина;
		
	КонецЕсли;
	
	УправлениеФормойНаСервере();
	
КонецПроцедуры

#Область СтраницаСобытияМониторинга

&НаСервере
Процедура ВывестиСтраницуСобытияМониторинга(
	Знач ДанныеСобытияМониторинга,
	Знач НомерСтраницы = 0,
	Знач ОчиститьСтраницы = Истина)
	
	Если ОчиститьСтраницы Тогда
		СтраницыСобытийМониторинга = Новый Структура;
		СтраницыСобытийМониторинга.Вставить("_0", Неопределено);
	КонецЕсли;
	
	Если ДанныеСобытияМониторинга.НомерСледующейСтраницы <> Неопределено
		И НомерСтраницы = СтраницыСобытийМониторинга.Количество() - 1 Тогда
		
		СтраницыСобытийМониторинга.Вставить(
			"_" + (Формат(НомерСтраницы + 1, "ЧН=0; ЧГ=")),
			ДанныеСобытияМониторинга.НомерСледующейСтраницы);
	КонецЕсли;
	
	РезультатСобытияМониторинга.Очистить();
	Если ДанныеСобытияМониторинга.РезультатСобытияМониторинга <> Неопределено Тогда
		НастроитьВидимостьКнопки("СобытияМониторинга", Истина);
		Элементы.СтраницаСобытияМониторинга.Видимость = Истина;
		РезультатСобытияМониторинга.Вывести(ДанныеСобытияМониторинга.РезультатСобытияМониторинга);
		Элементы.РезультатСобытияМониторинга.ИспользуемоеИмяФайла = СокрЛП(ДанныеСобытияМониторинга.ИмяФайлаСобытияМониторинга);
		Элементы.РезультатСобытияМониторинга.Высота = РезультатСобытияМониторинга.ВысотаТаблицы;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтраницаДанныеОбАрбитражныхДелах

&НаСервере
Процедура ВывестиСтраницуДанныеОбАрбитражныхДелах(ДанныеФакторыРиска)
	
	Если ДанныеФакторыРиска.РезультатДанныеОбАрбитражныхДелах = Неопределено Тогда
		НастроитьВидимостьКнопки("ДанныеОбАрбитражныхДелах", Ложь);
		Элементы.СтраницаДанныеОбАрбитражныхДелах.Видимость = Ложь;
	Иначе
		
		НастроитьВидимостьКнопки("ДанныеОбАрбитражныхДелах", Истина);
		Элементы.СтраницаДанныеОбАрбитражныхДелах.Видимость = Истина;
		РезультатДанныеОбАрбитражныхДелах.Очистить();
		РезультатДанныеОбАрбитражныхДелах.Вывести(ДанныеФакторыРиска.РезультатДанныеОбАрбитражныхДелах);
		Элементы.РезультатДанныеОбАрбитражныхДелах.ИспользуемоеИмяФайла =
			СокрЛП(ДанныеФакторыРиска.ИмяФайлаДанныеОбАрбитражныхДелах);
		Элементы.РезультатДанныеОбАрбитражныхДелах.Высота = РезультатДанныеОбАрбитражныхДелах.ВысотаТаблицы;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтраницаДанныеГосударственныеКонтракты

&НаСервере
Процедура ВывестиСтраницуДанныеГосударственныеКонтракты(ДанныеФакторыРиска)
	
	Если ДанныеФакторыРиска.РезультатДанныеГосударственныеКонтракты = Неопределено Тогда
		НастроитьВидимостьКнопки("ДанныеГосударственныеКонтракты", Ложь);
		Элементы.СтраницаДанныеГосударственныеКонтракты.Видимость = Ложь;
	Иначе
		
		НастроитьВидимостьКнопки("ДанныеГосударственныеКонтракты", Истина);
		Элементы.СтраницаДанныеГосударственныеКонтракты.Видимость = Истина;
		РезультатДанныеГосударственныеКонтракты.Очистить();
		РезультатДанныеГосударственныеКонтракты.Вывести(ДанныеФакторыРиска.РезультатДанныеГосударственныеКонтракты);
		Элементы.РезультатДанныеГосударственныеКонтракты.ИспользуемоеИмяФайла =
			СокрЛП(ДанныеФакторыРиска.ИмяФайлаДанныеГосударственныеКонтракты);
		Элементы.РезультатДанныеГосударственныеКонтракты.Высота = РезультатДанныеГосударственныеКонтракты.ВысотаТаблицы;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтраницаФакторыТребующиеВнимания

&НаСервере
Процедура ВывестиСтраницуФакторыТребующиеВнимания(ДанныеФакторыРиска)
	
	Если ДанныеФакторыРиска.РезультатФакторыТребующиеВнимания = Неопределено Тогда
		НастроитьВидимостьКнопки("ФакторыТребующиеВнимания", Ложь);
		Элементы.СтраницаФакторыТребующиеВнимания.Видимость = Ложь;
	Иначе
		
		НастроитьВидимостьКнопки("ФакторыТребующиеВнимания", Истина);
		Элементы.СтраницаФакторыТребующиеВнимания.Видимость = Истина;
		РезультатФакторыТребующиеВнимания.Очистить();
		РезультатФакторыТребующиеВнимания.Вывести(ДанныеФакторыРиска.РезультатФакторыТребующиеВнимания);
		Элементы.РезультатФакторыТребующиеВнимания.ИспользуемоеИмяФайла =
			СокрЛП(ДанныеФакторыРиска.ИмяФайлаФакторыТребующиеВнимания);
		Элементы.РезультатФакторыТребующиеВнимания.Высота = РезультатФакторыТребующиеВнимания.ВысотаТаблицы;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ОчиститьСодержимоеДокументов()
	
	ОчиститьТабличныйДокумент("РезультатСобытияМониторинга");
	ОчиститьТабличныйДокумент("РезультатДанныеОбАрбитражныхДелах");
	ОчиститьТабличныйДокумент("РезультатДанныеГосударственныеКонтракты");
	ОчиститьТабличныйДокумент("РезультатФакторыТребующиеВнимания");
	
	Элементы.СкачатьКарточку.Видимость = Ложь;
	Элементы.ПечатьПроверкиКонтрагента.Видимость = Ложь;
	Элементы.СтраницаОписаниеОшибки.Видимость = Ложь;
	
	СкрытьКарточки1СПАРКРиски();
	СкрытьПанельКнопок(
		МенюОтчетности());
	
	СостояниеФормированияОтчета.ОчищеноСодержимоеДокументов = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТабличныйДокумент(ИмяРеквизита);
	
	Реквизит = ЭтотОбъект[ИмяРеквизита];
	Реквизит.Очистить();
	Элементы[ИмяРеквизита].ИспользуемоеИмяФайла = Неопределено;
	Реквизит.ТекущаяОбласть = Реквизит.Область(1, 2, 1, 2);
	
КонецПроцедуры

&НаСервере
Процедура СкрытьПанельКнопок(ПанельКнопок)
	
	Для Каждого Элемент Из ПанельКнопок Цикл
		Элемент.Видимость = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция МенюОтчетности()
	
	Результат = Новый Массив;
	Если ЭтоМобильныйКлиент Тогда
		Результат.Добавить(Элементы.СобытияМониторинга);
		Результат.Добавить(Элементы.ДанныеОбАрбитражныхДелах);
		Результат.Добавить(Элементы.ДанныеГосударственныеКонтракты);
		Результат.Добавить(Элементы.ФакторыТребующиеВнимания);
	Иначе
		Результат.Добавить(Элементы.ГруппаРазделСобытияМониторингаСтраницы);
		Результат.Добавить(Элементы.ГруппаРазделДанныеОбАрбитражныхДелахСтраницы);
		Результат.Добавить(Элементы.ГруппаРазделДанныеГосударственныеКонтрактыСтраницы);
		Результат.Добавить(Элементы.ГруппаРазделФакторыТребующиеВниманияСтраницы);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СкрытьКарточки1СПАРКРиски()
	
	Элементы.ГруппаСПАРКРискиИндексы.Видимость = Ложь;
	Элементы.ГруппаДанныеСобытияМониторинга.Видимость = Ложь;
	Элементы.ДекорацияСобытиеМониторингаОписание.Видимость = Ложь;
	Элементы.ГруппаСправкиСПАРК.Видимость = Ложь;
	Элементы.ГруппаПереходВВебВерсиюСПАРК.Видимость = Ложь;
	Элементы.ГруппаСПАРКНегативныеСписки.Видимость = Ложь;
	Элементы.ГруппаСПАРКФакторыРиска.Видимость = Ложь;
	Элементы.ГруппаСПАРКФакторыРиска115ФЗ.Видимость = Ложь;
	Элементы.ГруппаСПАРКПризнакиХозяйственнойДеятельности.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьРазделГлавноеМеню(ТекущаяГиперссылка)
	
	ПредыдущаяГиперссылка    = ТекущийРазделГлавноеМеню(ТекущийРазделГлавноеМеню);
	ТекущийРазделГлавноеМеню = ТекущаяГиперссылка;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["Страница" + ТекущаяГиперссылка];
	
	ПриПереключенииРаздела(ПредыдущаяГиперссылка, ТекущаяГиперссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПереключитьРазделГлавноеМенюНаСервере(ТекущаяГиперссылка)
	
	ПредыдущаяГиперссылка    = ТекущийРазделГлавноеМеню(ТекущийРазделГлавноеМеню);
	ТекущийРазделГлавноеМеню = ТекущаяГиперссылка;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["Страница" + ТекущаяГиперссылка];
	
	Если ПустаяСтрока(ВключенРазделГлавноеМеню) Тогда
		ПредыдущийРазделСтраницы = "ГруппаРаздел" + ПредыдущаяГиперссылка + "Страницы";
		ПредыдущийРаздел = "ГруппаРаздел" + ПредыдущаяГиперссылка + "Гиперссылка";
		Элементы[ПредыдущийРазделСтраницы].ТекущаяСтраница = Элементы[ПредыдущийРаздел];
		Если ЭтоМобильныйКлиент Тогда
			Элементы[ПредыдущаяГиперссылка].ЦветТекста = Новый Цвет;
		КонецЕсли;
	Иначе
		НастроитьВидимостьКнопки(ВключенРазделГлавноеМеню, Ложь);
		ВключенРазделГлавноеМеню = "";
	КонецЕсли;
	
	АктивныйРазделСтраницы = "ГруппаРаздел" + ТекущаяГиперссылка + "Страницы";
	АктивныйРаздел = "ГруппаРаздел" + ТекущаяГиперссылка;
	Элементы[АктивныйРазделСтраницы].ТекущаяСтраница = Элементы[АктивныйРаздел];
	Если ЭтоМобильныйКлиент Тогда
		Элементы[ТекущаяГиперссылка].ЦветТекста = Новый Цвет(0, 0, 0);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекущийРазделГлавноеМеню(ТекущийРаздел)
	
	Если ПустаяСтрока(ТекущийРаздел) Тогда
		Возврат РазделОбщиеСведения();
	Иначе
		Возврат ТекущийРаздел
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РазделОбщиеСведения()
	Возврат "ОбщиеСведения";
КонецФункции

&НаКлиенте
Процедура ПриПереключенииРаздела(ПредыдущаяГиперссылкаИмя, ТекущаяГиперссылкаИмя)
	
	ВыключаемПредыдущийРаздел(ПредыдущаяГиперссылкаИмя);
	АктивизируемТекущийРаздел(ТекущаяГиперссылкаИмя);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыключаемПредыдущийРаздел(ПредыдущаяГиперссылкаИмя)
	
	Если Не ПустаяСтрока(ВключенРазделГлавноеМеню) Тогда
		НастроитьВидимостьКнопкиНаКлиенте(ВключенРазделГлавноеМеню, Ложь);
		ВключенРазделГлавноеМеню = "";
		Возврат;
	КонецЕсли;
	
	ПредыдущийРазделСтраницы = "ГруппаРаздел" + ПредыдущаяГиперссылкаИмя + "Страницы";
	ПредыдущийРаздел = "ГруппаРаздел" + ПредыдущаяГиперссылкаИмя + "Гиперссылка";
	Элементы[ПредыдущийРазделСтраницы].ТекущаяСтраница = Элементы[ПредыдущийРаздел];
	Если ЭтоМобильныйКлиент Тогда
		Элементы[ПредыдущаяГиперссылкаИмя].ЦветТекста = Новый Цвет;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктивизируемТекущийРаздел(ТекущаяГиперссылкаИмя)
	
	АктивныйРазделСтраницы = "ГруппаРаздел" + ТекущаяГиперссылкаИмя + "Страницы";
	АктивныйРаздел = "ГруппаРаздел" + ТекущаяГиперссылкаИмя;
	Элементы[АктивныйРазделСтраницы].ТекущаяСтраница = Элементы[АктивныйРаздел];
	Если ЭтоМобильныйКлиент Тогда
		Элементы[ТекущаяГиперссылкаИмя].ЦветТекста = Новый Цвет(0, 0, 0);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойНаСервере()
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Проверка контрагента: %1'"),
			Контрагент);
	Иначе
		Заголовок = НСтр("ru='Проверка контрагента'");
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОбщиеСведения;
	ТекущийЭлемент = ?(ЗначениеЗаполнено(ОписаниеОшибки),
		Элементы.СтраницаОписаниеОшибки,
		Элементы.СтраницаОбщиеСведения);
	ПереключитьРазделГлавноеМенюНаСервере(
		РазделОбщиеСведения());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОшибкиФормированияОтчета()
	
	Если Не ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОписаниеОшибки = "НеУказаныПараметрыАутентификации"
		Или ОписаниеОшибки = "НеУказанПароль" Тогда
		
		ЗаполнитьОписаниеОшибкиФормированияОтчета(
			НСтр("ru='Для формирования ""Проверка контрагента""
				|необходимо подключить Интернет-поддержку пользователей.
				|Обратитесь к администратору.'"));
		
	Иначе
		ЗаполнитьОписаниеОшибкиФормированияОтчета(ОписаниеОшибки);
	КонецЕсли;
	
	ОписаниеОшибки = "";
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеОшибкиФормированияОтчета(
		Знач ОписаниеОшибки = "")
	
	Если ОписаниеОшибки = "ОшибкаПроверкиИНН" Тогда
		ТекстОшибки = НСтр("ru = 'Поле ИНН должно иметь длину 10 или 12 цифр'");
	ИначеЕсли ОписаниеОшибки = "ОшибкаПроверкиНаименования" Тогда
		ТекстОшибки = НСтр("ru = 'Поле Наименование является обязательным.'");
	ИначеЕсли ОписаниеОшибки = "ОшибкаПроверкиКодаРегиона" Тогда
		ТекстОшибки = НСтр("ru = 'Поле Код региона должно состоять из двух цифр.'");
	ИначеЕсли ОписаниеОшибки = "ОшибкаАутентификацииВСервисе" Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка аутентификации в сервисе. Обратитесь к администратору.'");
	ИначеЕсли ОписаниеОшибки = "ПревышеноКоличествоПопыток" Тогда
		ТекстОшибки = НСтр("ru = 'Превышено количество попыток. Повторите попытку позже.'");
	ИначеЕсли ОписаниеОшибки = "ОшибкаПроверкиКПП" Тогда
		ТекстОшибки = НСтр("ru = 'Поле КПП должно иметь длину 9 цифр.'");
	ИначеЕсли ОписаниеОшибки = "ОшибкаПроверкиУникальногоНомера" Тогда
		ТекстОшибки = НСтр("ru = 'Уникальный номер передан некорректно.'");
	ИначеЕсли ОписаниеОшибки = "НеизвестнаяОшибка" Тогда
		ТекстОшибки = НСтр("ru = 'Неизвестная ошибка при подключении к сервису.'");
	ИначеЕсли ОписаниеОшибки = "ЗапрещенДоступКИнтернетСервисам" Тогда
		ТекстОшибки = НСтр("ru = 'Доступ к Интернет-сервисам в приложении запрещен администратором (""Интернет-поддержка и сервисы"" — ""Разрешить доступ к Интернет-сервисам"").'");
	Иначе
		ТекстОшибки = ОписаниеОшибки;
	КонецЕсли;
	
	ПроверкаКонтрагента.ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки);
	
	Элементы.СтраницаОписаниеОшибки.Видимость = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаОписаниеОшибки;
	Элементы.ДекорацияОписаниеОшибки.Заголовок = ТекстОшибки;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьУведомления()
	
	МодульСПАРКРиски = ОбщегоНазначения.ОбщийМодуль("СПАРКРиски");
	МодульСПАРКРискиКлиентСервер = ОбщегоНазначения.ОбщийМодуль("СПАРКРискиКлиентСервер");
	УведомленияПользователя.Вставить(
		"СПАРКРиски",
		МодульСПАРКРиски.УведомлениеПользователя(
			МодульСПАРКРискиКлиентСервер.ИдентификаторУслугиИндикаторыРиска()));
	
	Если УведомленияПользователя.СПАРКРиски = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаУведомлениеСервисаСПАРКРиски.Видимость = Истина;
	Элементы.ДекорацияТекстУведомленияСПАРКРиски.Заголовок =
		УведомленияПользователя.СПАРКРиски.ТекстУведомления;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииЗадания(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗадания = Неопределено;
	ЗагрузитьПодготовленныеДанные();
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		ОписаниеОшибки = Результат.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	ОбработатьОшибкиФормированияОтчета();
	
	Если ОжиданиеОтвета Тогда
		// Повторный вызов процедуры формирования при асинхронном получении данных от сервиса.
		ПодключитьОбработчикОжидания("Подключаемый_СформироватьОтчет", 1.5, Истина);
	Иначе
		Элементы.СтраницаФормированиеОтчета.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьПредупреждениеОбОшибке()
	
	ОбработатьОшибкиФормированияОтчета();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СформироватьОтчет()
	
	СформироватьОтчетНаКлиенте(ИННКонтрагента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьОтображениеИндексыСПАРК()
	
	ОбновитьОтображениеИндексыСПАРК();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеИндексыСПАРК()
	
	ВидКонтрагента = ?(ЭтоЮридическоеЛицо,
		ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо"),
		ОбщегоНазначенияКлиент.ПредопределенныйЭлемент("Перечисление.ВидыКонтрагентовСПАРКРиски.ИндивидуальныйПредприниматель"));
	
	ПроверкаКонтрагентаКлиент.ОтобразитьИндексыСПАРК(
		ИндексыСПАРКРиски,
		Неопределено,
		ИННКонтрагента,
		ВидКонтрагента,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВключитьФормированиеОтчета()
	
	ОтключитьФормированиеОтчета = Неопределено;
	
КонецПроцедуры

&НаСервере
Функция РезультатПроверкаКонтрагентаДляСохранения()
	
	ТабличныеДокументы = Новый Массив;
	ТабличныеДокументы.Добавить(РезультатФакторыТребующиеВнимания);
	ТабличныеДокументы.Добавить(РезультатДанныеОбАрбитражныхДелах);
	ТабличныеДокументы.Добавить(РезультатДанныеГосударственныеКонтракты);
	ТабличныеДокументы.Добавить(РезультатСобытияМониторинга);
	
	ПакетОтображаемыхДокументов = Новый ПакетОтображаемыхДокументов;
	ПакетОтображаемыхДокументов.РазборПоКопиям = Истина;
	
	Для Каждого ПечатнаяФорма Из ТабличныеДокументы Цикл
		Если ПечатнаяФорма.ВысотаТаблицы > 1 Тогда
			ТабличныйДокумент = Новый ТабличныйДокумент;
			ТабличныйДокумент.Вывести(ПечатнаяФорма);
			ЗаполнитьЗначенияСвойств(ТабличныйДокумент, ПечатнаяФорма, КопируемыеСвойстваТабличногоДокумента());
			ПакетОтображаемыхДокументов.Состав.Добавить().Данные = ПакетСОднимТабличнымДокументом(ТабличныйДокумент);
		КонецЕсли;
	КонецЦикла;
	
	ПакетКомплектов = Новый ПакетОтображаемыхДокументов;
	ПакетКомплектов.РазборПоКопиям = Истина;
	ПакетКомплектов.Состав.Добавить().Данные = ПакетОтображаемыхДокументов;
	
	ИмяФайла = ПолучитьИмяВременногоФайла("pdf");
	ПакетОтображаемыхДокументов.Записать(
		ИмяФайла,
		ТипФайлаПакетаОтображаемыхДокументов.PDF);
	ДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
	
	Результат = Новый Структура;
	Результат.Вставить(
		"АдресФайла",
		ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор));
	Результат.Вставить(
		"Представление",
		СтроковыеФункции.СтрокаЛатиницей(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Проверка контрагента %1.pdf'"),
				ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(
					Контрагент, ""))));
	
	УдалитьФайлы(ИмяФайла);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция РезультатПроверкаКонтрагентаДляПечати()
	
	Результат = Новый ТабличныйДокумент;
	
	ДобавитьТабличныйДокументДляПечати(
		Результат,
		РезультатФакторыТребующиеВнимания,
		Ложь);
	ДобавитьТабличныйДокументДляПечати(
		Результат,
		РезультатДанныеОбАрбитражныхДелах);
	ДобавитьТабличныйДокументДляПечати(
		Результат,
		РезультатДанныеГосударственныеКонтракты);
	ДобавитьТабличныйДокументДляПечати(
		Результат,
		РезультатСобытияМониторинга);
	
	Результат.АвтоМасштаб = Истина;
	
	ОчиститьДанныеРасшифровки(Результат);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ДобавитьТабличныйДокументДляПечати(РезультатДляПечати, ТабличныйДокумент, СоздаватьФорматСтрок = Истина)
	
	Если ТабличныйДокумент.ВысотаТаблицы > 1 Тогда
		Если СоздаватьФорматСтрок Тогда
			Для НомерСтроки = 1 По ТабличныйДокумент.ВысотаТаблицы Цикл
			
				НачалоНовогоФорматаСтрок = РезультатДляПечати.ВысотаТаблицы + 1;
				
				ОбластьПрямоугольная = ТабличныйДокумент.Область(НомерСтроки, ,НомерСтроки);
				РезультатДляПечати.ВставитьОбласть(
					ОбластьПрямоугольная,
					РезультатДляПечати.Область(НачалоНовогоФорматаСтрок, 1));
				РезультатДляПечати.Область(
					НачалоНовогоФорматаСтрок,
					,
					НачалоНовогоФорматаСтрок).СоздатьФорматСтрок();
				
				СтрокаРезультата = ТабличныйДокумент.ПолучитьОбласть(НомерСтроки,,НомерСтроки);
				
				Если СтрокаРезультата.ШиринаТаблицы = 0 Тогда
					СтрокаРезультата = ТабличныйДокумент.ПолучитьОбласть(НомерСтроки,1,НомерСтроки,1);
					РезультатДляПечати.Область(НачалоНовогоФорматаСтрок,1).Текст = " ";
				КонецЕсли;
				
				Для Счетчик = 1 По Макс(СтрокаРезультата.ШиринаТаблицы,1) Цикл
					РезультатДляПечати.Область(НачалоНовогоФорматаСтрок, Счетчик).РастягиватьПоГоризонтали =
						СтрокаРезультата.Область(1, Счетчик).РастягиватьПоГоризонтали;
					Если СтрокаРезультата.Область(1, Счетчик).ШиринаКолонки <> 0 Тогда
						РезультатДляПечати.Область(НачалоНовогоФорматаСтрок, Счетчик).ШиринаКолонки =
							СтрокаРезультата.Область(1, Счетчик).ШиринаКолонки;
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
		Иначе
			РезультатДляПечати.Вывести(ТабличныйДокумент);
		КонецЕсли;
		
		РезультатДляПечати.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДанныеРасшифровки(ТабличныйДокумент)
	
	Для НомерСтроки = 1 По ТабличныйДокумент.ВысотаТаблицы Цикл
		Для НомерКолонки = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
			Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки);
			Если Ячейка.Расшифровка <> Неопределено Тогда
				Ячейка.Расшифровка = Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция КопируемыеСвойстваТабличногоДокумента()
	Возврат "АвтоМасштаб,Вывод,ВысотаСтраницы,ДвусторонняяПечать,Защита,ИмяПринтера,КодЯзыка,
	|КоличествоЭкземпляров,МасштабПечати,НомерПервойСтраницы,ОриентацияСтраницы,ПолеСверху,ПолеСлева,
	|ПолеСнизу,ПолеСправа,РазборПоКопиям,РазмерКолонтитулаСверху,РазмерКолонтитулаСнизу,РазмерСтраницы,
	|ТочностьПечати,ФоноваяКартинка,ЧерноБелаяПечать,ШиринаСтраницы,ЭкземпляровНаСтранице";
КонецФункции

&НаСервере
Функция ПакетСОднимТабличнымДокументом(ТабличныйДокумент)
	
	АдресТабличногоДокументаВоВременномХранилище = ПоместитьВоВременноеХранилище(ТабличныйДокумент);
	ПакетСОднимДокументом = Новый ПакетОтображаемыхДокументов;
	ПакетСОднимДокументом.РазборПоКопиям = Истина;
	ПакетСОднимДокументом.Состав.Добавить(АдресТабличногоДокументаВоВременномХранилище);
	ЗаполнитьЗначенияСвойств(
		ПакетСОднимДокументом,
		ТабличныйДокумент,
		"Вывод, ДвусторонняяПечать, ИмяПринтера, КоличествоЭкземпляров, ТочностьПечати");
	Если ТабличныйДокумент.РазборПоКопиям <> Неопределено Тогда
		ПакетСОднимДокументом.РазборПоКопиям = ТабличныйДокумент.РазборПоКопиям;
	КонецЕсли;
	Возврат ПакетСОднимДокументом;
	
КонецФункции

&НаКлиенте
Процедура СохранитьКарточкуПриНажатииНаРезультат(ПолноеИмяФайла) Экспорт
	
	ФайловаяСистемаКлиент.ОткрытьФайл(ПолноеИмяФайла);
	
КонецПроцедуры

#Область КарточкаСобытияМониторинга

&НаСервере
Процедура ВывестиКарточкуСобытияМониторинга(ДанныеСобытияМониторинга)
	
	Если ДанныеСобытияМониторинга = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияВыведена = Ложь;
	
	Элементы.ДекорацияСобытиеМониторингаОписание.Видимость = Ложь;
	ЗначенияСобытийМониторинга = ДанныеСобытияМониторинга.КарточкаСобытияМониторинга.ЗначенияСобытийМониторинга;
	ВывестиСобытияМониторинга(ЗначенияСобытийМониторинга, ИнформацияВыведена);
	Если ЗначенияСобытийМониторинга.ДоступЗапрещен Тогда
		Элементы.ДекорацияВсеСобытияМониторинга.Видимость = Ложь;
	Иначе
		Элементы.ДекорацияВсеСобытияМониторинга.Видимость = ИнформацияВыведена;
	КонецЕсли;
	
	Элементы.ГруппаДанныеСобытияМониторинга.Видимость = ИнформацияВыведена;
		
	Если Не ИнформацияВыведена Тогда
		
		Элементы.ДекорацияСобытиеМониторингаОписание.Заголовок = НСтр("ru='Нет информации о контрагенте'");
		Элементы.ДекорацияСобытиеМониторингаОписание.Видимость = Истина;
		
	КонецЕсли;
	Элементы.ГруппаСПАРКСобытияМониторинга.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиКарточкуФакторыРиска(ДанныеФакторыРиска)
	
	Если ДанныеФакторыРиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияВыведена = Ложь;
	
	Элементы.ДекорацияФакторыРискаОписание.Видимость = Ложь;
	ФакторыРиска = ДанныеФакторыРиска.КарточкаФакторыРиска.ФакторыРиска;
	ВывестиФакторыРиска(
		ФакторыРиска,
		ИнформацияВыведена,
		"ФакторРиска");
	
	Элементы.ГруппаДанныеФакторРиска.Видимость = ИнформацияВыведена;
		
	Если Не ИнформацияВыведена Тогда
		
		Элементы.ДекорацияФакторыРискаОписание.Заголовок = НСтр("ru='Нет информации о контрагенте'");
		Элементы.ДекорацияФакторыРискаОписание.Видимость = Истина;
		
	КонецЕсли;
	Элементы.ГруппаСПАРКФакторыРиска.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиКарточкуФакторыРиска115ФЗ(ДанныеФакторыРиска)
	
	Если ДанныеФакторыРиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияВыведена = Ложь;
	
	Элементы.ДекорацияФакторыРиска115ФЗОписание.Видимость = Ложь;
	ФакторыРиска = ДанныеФакторыРиска.КарточкаФакторыРиска.ФакторыРискаПо115ФЗ;
	ВывестиФакторыРиска(
		ФакторыРиска,
		ИнформацияВыведена,
		"ФакторРиска115ФЗ");
	
	Элементы.ГруппаДанныеФакторРиска115ФЗ.Видимость = ИнформацияВыведена;
		
	Если Не ИнформацияВыведена Тогда
		
		Элементы.ДекорацияФакторыРиска115ФЗОписание.Заголовок = НСтр("ru='Нет информации о контрагенте'");
		Элементы.ДекорацияФакторыРиска115ФЗОписание.Видимость = Истина;
		
	КонецЕсли;
	Элементы.ГруппаСПАРКФакторыРиска115ФЗ.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиКарточкуНегативныеСписки(ДанныеФакторыРиска)
	
	Если ДанныеФакторыРиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияВыведена = Ложь;
	
	Элементы.ДекорацияНегативныеСпискиОписание.Видимость = Ложь;
	НегативныеСписки = ДанныеФакторыРиска.КарточкаФакторыРиска.НегативныеСписки;
	ВывестиФакторыРиска(
		НегативныеСписки,
		ИнформацияВыведена,
		"НегативныеСписки");
	
	Элементы.ГруппаДанныеНегативныеСписки.Видимость = ИнформацияВыведена;
		
	Если Не ИнформацияВыведена Тогда
		
		Элементы.ДекорацияНегативныеСпискиОписание.Заголовок = НСтр("ru='Нет информации о вхождении в негативные списки'");
		Элементы.ДекорацияНегативныеСпискиОписание.Видимость = Истина;
		
	КонецЕсли;
	Элементы.ГруппаСПАРКНегативныеСписки.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиФакторыРиска(ФакторыРиска, ИнформацияВыведена, ИмяФактора)
	
	Индекс = 1;
	Если ФакторыРиска <> Неопределено Тогда
		Для Каждого ФакторРиска Из ФакторыРиска Цикл
			ИнформацияВыведена = Истина;
			ВывестиФакторРиска(
				Элементы,
				ФакторРиска,
				ИмяФактора,
				Индекс);
			Индекс = Индекс + 1;
		КонецЦикла;
	КонецЕсли;
	
	Пока ЕстьЭлементФакторРиска(Индекс, ИмяФактора) Цикл
		СкрытьФакторРиска(
			Элементы,
			ИмяФактора,
			Индекс);
		Индекс = Индекс + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЕстьЭлементФакторРиска(ИмяФактора, Индекс)
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, "Группа" + ИмяФактора + Индекс);
КонецФункции

&НаСервере
Процедура СкрытьФакторРиска(
		Элементы,
		ИмяФактора,
		Индекс)
	
	Элементы["Группа" +ИмяФактора + Индекс].Видимость = Ложь;
	Элементы["Декорация" + ИмяФактора + "Значение" + Индекс].Заголовок = "";
	
КонецПроцедуры

&НаСервере
Процедура ВывестиФакторРиска(
		Элементы,
		ФакторРиска,
		ИмяФактора,
		Индекс)
		
	ГруппаФакторРиска = ЭлементПоИмени(
		("Группа" + ИмяФактора + Индекс),
		Тип("ГруппаФормы"),
		Элементы["ГруппаДанные" + ИмяФактора]);
	ГруппаФакторРиска.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФакторРиска.ОтображатьЗаголовок = Ложь;
	ГруппаФакторРиска.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаФакторРиска.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	ЭлементФормы = ЭлементПоИмени(
		"Декорация" + ИмяФактора + "Картинка" + Индекс,
		Тип("ДекорацияФормы"),
		ГруппаФакторРиска);
	ЭлементФормы.Вид = ВидДекорацииФормы.Картинка;
	ЭлементФормы.РазмерКартинки = РазмерКартинки.ПоРазмеруШрифта;
	ЭлементФормы.Картинка = БиблиотекаКартинок.ФакторРискаСПАРК;
	
	ЭлементФормы = ЭлементПоИмени(
		"Декорация" + ИмяФактора + "Значение" + Индекс,
		Тип("ДекорацияФормы"),
		ГруппаФакторРиска);
	ЭлементФормы.Вид = ВидДекорацииФормы.Надпись;
	ЭлементФормы.Заголовок = ФакторРиска.Название;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиКарточкуПризнакиХозяйственнойДеятельности(ДанныеФакторыРиска);
	
	Если ДанныеФакторыРиска = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КарточкаФакторыРиска = ДанныеФакторыРиска.КарточкаФакторыРиска;
	НастроитьСвойстваЭлементовПризнакиХозяйственнойДеятельности(КарточкаФакторыРиска);
	Элементы.ДекорацияДанныеОбАрбитражныхДелахЗначение.Заголовок = КарточкаФакторыРиска.ДанныеОбАрбитражныхДелах;
	Элементы.ДекорацияДанныеГосударственныеКонтрактыЗначение.Заголовок = КарточкаФакторыРиска.ДанныеГосударственныеКонтракты;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСвойстваЭлементовПризнакиХозяйственнойДеятельности(ДанныеКарточки)
	
	Элементы.ДекорацияДанныеОбАрбитражныхДелахЗаголовок.Гиперссылка =
		ДанныеКарточки.ЕстьДанныеОбАрбитражныхДелах;
	Элементы.ДекорацияДанныеГосударственныеКонтрактыЗаголовок.Гиперссылка =
		ДанныеКарточки.ЕстьДанныеГосударственныеКонтракты;
	
	Элементы.ГруппаСПАРКПризнакиХозяйственнойДеятельности.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиКарточкуСправкиСПАРКРиски(ДанныеОтчета)
	
	Элементы.ГруппаСправкиСПАРК.Видимость = Истина;
	
	ДанныеАктуальнойСправки = ДанныеОтчета.КарточкаСправкиСПАРКРиски.ДанныеАктуальнойСправки;
	АктуальнаяСправка = ДанныеАктуальнойСправки.АктуальнаяСправка;
	Элементы.ДекорацияАктуальнаяСправкаСПАРК.Заголовок = ДанныеАктуальнойСправки.Описание;
	Элементы.ГруппаСправкиСПАРККоманды.Видимость = ЗначениеЗаполнено(Контрагент);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиКарточкуПереходВВебВерсиюСПАРК()
	
	Элементы.ГруппаПереходВВебВерсиюСПАРК.Видимость = Истина;
	МодульСПАРКРиски = ОбщегоНазначения.ОбщийМодуль("СПАРКРиски");
	
	Если ЗначениеЗаполнено(Контрагент)
		И МодульСПАРКРиски.ИспользованиеРазрешено("ПереходВВебВерсиюСПАРК;") Тогда
		ТекстОписания = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru='В <a href=""ВебВерсияСПАРК"">веб-карточках СПАРК</a> доступна ключевая информация по организациям, а также показатели, требующие повышенного внимания:
				|- Общая информация (Регистрационные и контактные сведения, история изменений и т.д.)
				|- Структура компании (Структура собственников и дочерних компаний и анализ взаимосвязей)
				|- Деятельность компании (Сведения о наличии у компании или персоны залогов, лизинга, факторинга, исполнительных производств, гарантий и другое)
				|- Финансовая информация
				|- Финансовый анализ.'"));
	Иначе
		ТекстОписания = НСтр("ru='В веб-карточках СПАРК доступна ключевая информация по организациям, а также показатели, требующие повышенного внимания:
			|- Общая информация (Регистрационные и контактные сведения, история изменений и т.д.)
			|- Структура компании (Структура собственников и дочерних компаний и анализ взаимосвязей)
			|- Деятельность компании (Сведения о наличии у компании или персоны залогов, лизинга, факторинга, исполнительных производств, гарантий и другое)
			|- Финансовая информация
			|- Финансовый анализ.'");
	КонецЕсли;
	
	Элементы.ДекорацияПереходВВебВерсиюСПАРКОписание.Заголовок = ТекстОписания;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиСобытияМониторинга(СобытияМониторингСПАРК, ИнформацияВыведена)
	
	Индекс = 0;
	Пока Индекс < 3 Цикл
		
		Если Индекс > СобытияМониторингСПАРК.СобытияМониторинга.ВГраница() Тогда
			СкрытьСобытие(
				Элементы,
				Индекс);
		Иначе
			ИнформацияВыведена = Истина;
			ВывестиСобытие(
				Элементы,
				СобытияМониторингСПАРК.СобытияМониторинга[Индекс],
				Индекс,
				СобытияМониторингСПАРК.ДоступЗапрещен);
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Элементы.ДекорацияВсеСобытияМониторинга.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьСобытие(
	Элементы,
	Индекс)
	
	Элементы["ГруппаСобытиеМониторинга" + Индекс].Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиСобытие(
		Элементы,
		Событие,
		Индекс,
		ДоступЗапрещен)
	
	Элементы["ДекорацияСобытиеМониторингаДата" + Индекс].Заголовок = Формат(Событие.Дата, "ДЛФ=D");
	Если ДоступЗапрещен Тогда
		Элементы["ДекорацияСобытиеМониторингаЗначение" + Индекс].Заголовок = СлучайныйТекстСобытия(80);
	Иначе
		Элементы["ДекорацияСобытиеМониторингаЗначение" + Индекс].Заголовок = Событие.Описание;
	КонецЕсли;
	Если Событие.Критическое Тогда
		Элементы["ДекорацияСобытиеМониторингаДата" + Индекс].ЦветТекста     = ЦветаСтиля.ЦветОсобогоТекста;
		Элементы["ДекорацияСобытиеМониторингаЗначение" + Индекс].ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
	Иначе
		Элементы["ДекорацияСобытиеМониторингаДата" + Индекс].ЦветТекста     = Новый Цвет;
		Элементы["ДекорацияСобытиеМониторингаЗначение" + Индекс].ЦветТекста = Новый Цвет;
	КонецЕсли;
	
	Элементы["ГруппаСобытиеМониторинга" + Индекс].Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Функция СлучайныйТекстСобытия(РазмерСтроки)
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	ДлинаТекста = ГСЧ.СлучайноеЧисло(30, РазмерСтроки);
	Результат = СтрокаЗатемнения(2);
	Для К = 1 По ДлинаТекста Цикл
		Если ГСЧ.СлучайноеЧисло(0, 10) > 1 Тогда
			Символ = СтрокаЗатемнения();
		Иначе
			Символ = " ";
		КонецЕсли;
		Результат = Результат + Символ;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтрокаЗатемнения(ДлинаСтроки = 1)
	
	Результат = "";
	Для Итератор = 1 По ДлинаСтроки Цикл
		Результат = Результат + "░";
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ОбновитьСобытияМониторингСПАРКПоФильтру()
	
	ПараметрыЗапроса = Новый Структура(
		"ВключитьИсполнительныеПроизводства, ВключитьКритичные",
		?(ИсполнительныеПроизводства, Истина, Неопределено),
		?(ТолькоКритичные, Истина, Неопределено));
	ОбновитьСобытияМониторингСПАРК(ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСобытияМониторингСПАРК(
		ПараметрыЗапроса,
		НомерСтраницы = 0,
		ОчиститьСтраницы = Истина)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(
		Элементы.РезультатСобытияМониторинга,
		"ФормированиеОтчета");
	
	ДополнительныеПараметры = Новый Структура(
		"НомерСтраницы, ОчиститьСтраницы",
		НомерСтраницы,
		ОчиститьСтраницы);
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбновлениеСобытийМониторингСПАРКЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
	РезультатВыполнения = ОбновлениеСобытийМониторингСПАРКНаСервере(
		ПараметрыЗапроса,
		НомерСтраницы);
	Если РезультатВыполнения.Статус = "Выполнено"
		Или РезультатВыполнения.Статус = "Ошибка" Тогда
		
		ОбновлениеСобытийМониторингСПАРКЗавершение(
			РезультатВыполнения,
			ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОбновлениеСобытийМониторингСПАРКНаСервере(
	Знач ПараметрыЗапроса,
	Знач НомерСтраницы)
	
	ВидКонтрагента = ?(ЭтоЮридическоеЛицо,
		ОбщегоНазначения.ПредопределенныйЭлемент(
			"Перечисление.ВидыКонтрагентовСПАРКРиски.ЮридическоеЛицо"),
		ОбщегоНазначения.ПредопределенныйЭлемент(
			"Перечисление.ВидыКонтрагентовСПАРКРиски.ИндивидуальныйПредприниматель"));
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Обработки.ПроверкаКонтрагента.ОбновитьСобытияМониторингСПАРК",
		ИННКонтрагента,
		ВидКонтрагента,
		ПараметрыЗапроса,
		НомерСтраницы);
	
КонецФункции

&НаКлиенте
Процедура ОбновлениеСобытийМониторингСПАРКЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(
		Элементы.РезультатСобытияМониторинга,
		"НеИспользовать");
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ВывестиСтраницуСобытияМониторинга(
			РезультатОперации,
			ДополнительныеПараметры.НомерСтраницы,
			ДополнительныеПараметры.ОчиститьСтраницы);
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВывестиОписаниеОшибкиПолученияСобытийМониторинга();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиОписаниеОшибкиПолученияСобытийМониторинга()
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		ИмяОбласти = "НеизвестнаяОшибкаОткрытиеЖурналаРегистрации";
	Иначе
		ИмяОбласти = "НеизвестнаяОшибка";
	КонецЕсли;
	
	Область = Обработки.ПроверкаКонтрагента.ПолучитьМакет("ОшибкиПроверкиКонтрагента").ПолучитьОбласть(ИмяОбласти);
	Если ИмяОбласти = "НеизвестнаяОшибкаОткрытиеЖурналаРегистрации" Тогда
		Область.Параметры.ОткрытьЖурналРегистрации = "ОткрытьЖурналРегистрации";
	КонецЕсли;
	
	РезультатСобытияМониторинга.Вывести(Область);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьТабличныеДокументы()
	
	РезультатСобытияМониторинга.АвтоМасштаб = Истина;
	РезультатФакторыТребующиеВнимания.АвтоМасштаб = Истина;
	РезультатДанныеОбАрбитражныхДелах.АвтоМасштаб = Истина;
	РезультатДанныеОбАрбитражныхДелах.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	РезультатДанныеГосударственныеКонтракты.АвтоМасштаб = Истина;
	РезультатДанныеГосударственныеКонтракты.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
КонецПроцедуры

&НаКлиенте
Функция ИдентификаторСтраницы(НомерСтраницы)
	Возврат СтраницыСобытийМониторинга["_" + Формат(НомерСтраницы, "ЧН=0; ЧГ=")];
КонецФункции

&НаСервере
Процедура АдаптироватьФормуПодМобильныйКлиент()
	
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Переместить(
		Элементы.ГруппаСПАРКРискиИндексы,
		Элементы.ГруппаЛевоОбщиеСведения);
	Элементы.Переместить(
		Элементы.ГруппаСПАРКНегативныеСписки,
		Элементы.ГруппаЛевоОбщиеСведения);
	Элементы.Переместить(
		Элементы.ГруппаСПАРКФакторыРиска,
		Элементы.ГруппаЛевоОбщиеСведения);
	Элементы.Переместить(
		Элементы.ГруппаСПАРКФакторыРиска115ФЗ,
		Элементы.ГруппаЛевоОбщиеСведения);
	Элементы.Переместить(
		Элементы.ГруппаСПАРКПризнакиХозяйственнойДеятельности,
		Элементы.ГруппаЛевоОбщиеСведения);
	Элементы.Переместить(
		Элементы.ГруппаСПАРКСобытияМониторинга,
		Элементы.ГруппаЛевоОбщиеСведения);
	Элементы.Переместить(
		Элементы.ГруппаСправкиСПАРК,
		Элементы.ГруппаЛевоОбщиеСведения);
	Элементы.Переместить(
		Элементы.ГруппаПереходВВебВерсиюСПАРК,
		Элементы.ГруппаЛевоОбщиеСведения);
	
	Элементы.СтраницаСобытияМониторинга.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.Переместить(
		Элементы.ГруппаСобытияМониторингаПраво,
		Элементы.СтраницаСобытияМониторинга,
		Элементы.РезультатСобытияМониторинга);
	Элементы.ГруппаСобытияМониторингаПраво.Ширина = Элементы.РезультатСобытияМониторинга.Ширина;
	Элементы.ГруппаСобытияМониторингаФильтры.Группировка =
		ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	Элементы.Переместить(
		Элементы.ДекорацияСводныйИндикаторЗаголовок,
		Элементы.ГруппаСводныйИндикатор,
		Элементы.ГруппаКартинкаСводныйИндикатор);
	Элементы.Переместить(
		Элементы.ДекорацияИДОЗаголовок,
		Элементы.ГруппаИДО,
		Элементы.ГруппаКартинкаИДО);
	Элементы.Переместить(
		Элементы.ДекорацияИФРЗаголовок,
		Элементы.ГруппаИФР,
		Элементы.ГруппаКартинкаИФР);
	Элементы.Переместить(
		Элементы.ДекорацияИПДЗаголовок,
		Элементы.ГруппаИПД,
		Элементы.ГруппаКартинкаИПД);
		
	Элементы.ГруппаИДО.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаИФР.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаИПД.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаСводныйИндикатор.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	Элементы.ОбщиеСведения.Видимость = Ложь;
	Элементы.СобытияМониторинга.Видимость = Ложь;
	Элементы.ДанныеОбАрбитражныхДелах.Видимость = Ложь;
	Элементы.ДанныеГосударственныеКонтракты.Видимость = Ложь;
	Элементы.ФакторыТребующиеВнимания.Видимость = Ложь;
	
	Элементы.СкачатьКарточку.Отображение = ОтображениеКнопки.Авто;
	
КонецПроцедуры

&НаСервере
Функция ЭлементПоИмени(Имя, ТипЭлемента, Родитель)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, Имя) Тогда
		Результат = Элементы[Имя];
		Результат.Видимость = Истина;
		Возврат Элементы[Имя];
	Иначе
		Возврат Элементы.Добавить(Имя, ТипЭлемента, Родитель);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция РазделСобытияМониторинга()
	Возврат "СобытияМониторинга";
КонецФункции

&НаКлиенте
Функция РазделДанныеОбАрбитражныхДелах()
	Возврат "ДанныеОбАрбитражныхДелах";
КонецФункции

&НаКлиенте
Функция РазделДанныеГосударственныеКонтракты()
	Возврат "ДанныеГосударственныеКонтракты";
КонецФункции

&НаКлиенте
Функция РазделФакторыТребующиеВнимания()
	Возврат "ФакторыТребующиеВнимания";
КонецФункции

&НаСервере
Процедура НастроитьВидимостьКнопки(ИмяЭлемента, Видимость)
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы[ИмяЭлемента].Видимость = Видимость
	Иначе
		Элементы["ГруппаРаздел" + ИмяЭлемента + "Страницы"].Видимость = Видимость;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВидимостьКнопкиНаКлиенте(ИмяЭлемента, Видимость)
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы[ИмяЭлемента].Видимость = Видимость
	Иначе
		Элементы["ГруппаРаздел" + ИмяЭлемента + "Страницы"].Видимость = Видимость;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
