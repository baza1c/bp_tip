
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЦветПодсветки = ЦветаСтиля.ВыборСтандартногоПериодаФонКнопки;
	Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	ОрганизацияПредставление = Объект.Организация;
	ИспользоватьНесколькоОрганизацийБухгалтерскийУчет = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьСпискиДокументов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	СформироватьСпискиДокументов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьСписки(Команда)
	
	СформироватьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсправитьДокументы(Команда)
	
	Если Не ЗначениеЗаполнено(СтавкаНДС) И КоличествоДокументовСписания <> 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Нстр("ru = 'Не выбрана ставка НДС'"), , "СтавкаНДС");
		Возврат;
	ИначеЕсли Не ЗначениеЗаполнено(ВидОплаты) И КоличествоОплатПлатежнойКартой <> 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Нстр("ru = 'Не выбран вид оплаты для платежной карты'"), , "ВидОплаты");
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ИсправитьДокументыНаСервере(УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, ДлительнаяОперация.КраткоеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Истина;
	
	Обработчик = Новый ОписаниеОповещения("ЗавершениеИсправитьДокументы", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьСпискиДокументов()
	
	ПроверитьЗаписи();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ИсправитьДокументыНаСервере(Знач ИдентификаторФормы)
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("РезультатПроверки", ПолучитьИзВременногоХранилища(АдресВременногоХранилища));
	ПараметрыМетода.Вставить("СтавкаНДССписания", СтавкаНДС);
	ПараметрыМетода.Вставить("ВидОплаты", ВидОплаты);

	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ИсправлениеРеквизитаБезЗакрывающихДокументов.ИсправитьДокументыВФоне",
		ПараметрыМетода,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьЗаписи()
	
	ДлительнаяОперация = ПроверитьЗаписиНаСервере(УникальныйИдентификатор);
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, ДлительнаяОперация.КраткоеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Истина;
	
	Обработчик = Новый ОписаниеОповещения("ПоказатьРезультатПроверкиЗаписей", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаписиНаСервере(Знач ИдентификаторФормы)
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("Организация", Объект.Организация);
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ИсправлениеРеквизитаБезЗакрывающихДокументов.ПроверитьЗаписиВФоне",
		ПараметрыМетода,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗавершениеИсправитьДокументы(РезультатИсправления, ДополнительныеПараметры) Экспорт
	
	Если РезультатИсправления = Неопределено Тогда
		Возврат;
	ИначеЕсли РезультатИсправления.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, РезультатИсправления.КраткоеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	СформироватьСпискиДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатПроверкиЗаписей(РезультатПроверки, ДополнительныеПараметры) Экспорт

	Если РезультатПроверки = Неопределено Тогда
		Возврат;
	ИначеЕсли РезультатПроверки.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, РезультатПроверки.КраткоеПредставлениеОшибки);
		Возврат;
	КонецЕсли;
	
	ПоказатьРезультатПроверкиЗаписейНаСервере(РезультатПроверки.АдресРезультата);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьРезультатПроверкиЗаписейНаСервере(АдресРезультата)

	РезультатПоступления.Очистить();
	РезультатСписания.Очистить();
	Если Не ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		Возврат;
	КонецЕсли;
	
	АдресВременногоХранилища = АдресРезультата;
	РезультатПроверкиДокументов = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ТипЗнч(РезультатПроверкиДокументов) <> Тип("Структура")
		Или Не РезультатПроверкиДокументов.Свойство("РезультатПоступления")
		Или Не РезультатПроверкиДокументов.Свойство("РезультатСписания") Тогда
		Возврат;
	КонецЕсли;
	РезультатПоступления = РезультатПроверкиДокументов.РезультатПоступления;
	РезультатСписания = РезультатПроверкиДокументов.РезультатСписания;
	КоличествоДокументовПоступления = РезультатПроверкиДокументов.КоличествоДокументовПоступления;
	КоличествоДокументовСписания = РезультатПроверкиДокументов.КоличествоДокументовСписания;
	КоличествоОплатПлатежнойКартой = РезультатПроверкиДокументов.КоличествоОплатПлатежнойКартой;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидПоУмолчаниюОформлением(Форма, Элемент, ЭтоКнопкаПоУмолчанию)
	
	ПолужирныйШрифт  = ЭтоКнопкаПоУмолчанию;
	Элемент.Шрифт    = Новый Шрифт(Элемент.Шрифт, , , ПолужирныйШрифт);
	Элемент.ЦветФона = ?(ЭтоКнопкаПоУмолчанию, Форма.ЦветПодсветки, Новый Цвет);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	КнопкаПоУмолчаниюОбновить = Форма.КоличествоДокументовПоступления = 0 И Форма.КоличествоДокументовСписания = 0;
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.ИсправитьДокументы, Не КнопкаПоУмолчаниюОбновить);
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.ОбновитьСписок, КнопкаПоУмолчаниюОбновить);
	Элементы.ИсправитьДокументы.Доступность = Не КнопкаПоУмолчаниюОбновить;
	
	Элементы.ВидОплаты.Видимость = Форма.КоличествоОплатПлатежнойКартой <> 0;
	
	Элементы.Организация.Видимость              = Форма.ИспользоватьНесколькоОрганизацийБухгалтерскийУчет;
	Элементы.ОрганизацияПредставление.Видимость = Не Форма.ИспользоватьНесколькоОрганизацийБухгалтерскийУчет;
	
КонецПроцедуры

#КонецОбласти