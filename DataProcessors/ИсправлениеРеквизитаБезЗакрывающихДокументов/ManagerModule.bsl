#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПроверитьЗаписиВФоне(Параметры, АдресРезультата) Экспорт

	ТаблицыДокументов = ПроверитьЗаписи(Параметры.Организация);
	Результат = СформироватьТабличныеДокументы(ТаблицыДокументов);
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);

КонецПроцедуры

Процедура ИсправитьДокументыВФоне(Параметры, АдресРезультата) Экспорт

	Результат = ИсправитьДокументы(Параметры.РезультатПроверки, Параметры.СтавкаНДССписания, Параметры.ВидОплаты);
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИсправитьДокументы(РезультатПроверки, СтавкаНДССписания, ВидОплаты)
	
	ВидыОперацийСНДС = Новый Массив();
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратОтПоставщика);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийПКО.ОплатаПокупателя);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийПКО.РозничнаяВыручка);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийПКО.ВозвратОтПоставщика);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ВозвратПокупателю);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.РозничнаяВыручка);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаСчетаЧерезСБП);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ОплатаПоставщику);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВозвратПокупателю);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийРКО.ОплатаПоставщику);
	ВидыОперацийСНДС.Добавить(Перечисления.ВидыОперацийРКО.ВозвратПокупателю);

	НачатьТранзакцию();
	Попытка
		Если РезультатПроверки.ДокументыПоступленияСОшибками.Количество() <> 0 Тогда
			Для Каждого ДокументКИсправлению Из РезультатПроверки.ДокументыПоступленияСОшибками Цикл
				НоваяСтавкаНДС = Перечисления.СтавкиНДС.СтавкаНДС(ДокументКИсправлению.СтавкаНДС, ДокументКИсправлению.Дата);
				УстанавливатьСтавкуНДС = ВидыОперацийСНДС.Найти(ДокументКИсправлению.ВидОперации) <> Неопределено;
				ИсправитьДокумент(ДокументКИсправлению.Документ, НоваяСтавкаНДС, УстанавливатьСтавкуНДС, , ВидОплаты);
			КонецЦикла;
		КонецЕсли;
		Если РезультатПроверки.ДокументыСписанияСОшибками.Количество() <> 0 Тогда
			Для Каждого ДокументКИсправлению Из РезультатПроверки.ДокументыСписанияСОшибками Цикл
				УстанавливатьСтавкуНДС = ВидыОперацийСНДС.Найти(ДокументКИсправлению.ВидОперации) <> Неопределено;
				ИсправитьДокумент(ДокументКИсправлению.Документ, СтавкаНДССписания, УстанавливатьСтавкуНДС, Истина);
			КонецЦикла;
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Процедура ИсправитьДокумент(ДокументСсылка, СтавкаНДС, УстанавливатьСтавкуНДС, ДокументСписания = Ложь, ВидОплаты = Неопределено)
	
	ДокументДляИсправления = ДокументСсылка.ПолучитьОбъект();
	Если ДокументДляИсправления.БезЗакрывающихДокументов Тогда
		ДокументДляИсправления.БезЗакрывающихДокументов = Ложь;
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОплатаПлатежнойКартой") И ВидОплаты <> Неопределено Тогда
			ДокументДляИсправления.ВидОплаты = ВидОплаты;
			РеквизитыВидаОплаты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидОплаты, "Контрагент, ДоговорКонтрагента", Истина);
			ДокументДляИсправления.Эквайер = РеквизитыВидаОплаты.Контрагент;
			ДокументДляИсправления.ДоговорЭквайринга = РеквизитыВидаОплаты.ДоговорКонтрагента;
		КонецЕсли;
		Если УстанавливатьСтавкуНДС Тогда
			Для Каждого ТекущийПлатеж Из ДокументДляИсправления.РасшифровкаПлатежа Цикл
				Если Не ДокументСписания И ТипЗнч(ТекущийПлатеж.ПорядокОтраженияАванса) = Тип("СправочникСсылка.Патенты") Тогда
					ТекущийПлатеж.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
				Иначе
					ТекущийПлатеж.СтавкаНДС = СтавкаНДС;
				КонецЕсли;
				ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ПересчитатьСуммуНДС(ТекущийПлатеж);
			КонецЦикла;
		КонецЕсли;
		ДокументДляИсправления.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьЗаписи(Организация)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", УчетУСНКлиентСервер.ДатаНачалаПримененияНДС());
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ТекущаяДатаСеанса()));
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиСистемыНалогообложенияСрезПоследних.Организация КАК Организация
		|ПОМЕСТИТЬ втОрганизации
		|ИЗ
		|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&КонецПериода, Организация = &Организация) КАК НастройкиСистемыНалогообложенияСрезПоследних
		|ГДЕ
		|	НастройкиСистемыНалогообложенияСрезПоследних.ПрименяетсяУСНДоходы
		|	И НастройкиСистемыНалогообложенияСрезПоследних.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
		|	И НастройкиСистемыНалогообложенияСрезПоследних.ПлательщикНДСПрименяющийУСН
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиУчетаНДС.Период КАК Период,
		|	НастройкиУчетаНДС.Организация КАК Организация,
		|	НастройкиУчетаНДС.СтавкаНалогообложенияПриУСН КАК СтавкаНДС
		|ИЗ
		|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
		|ГДЕ
		|	НастройкиУчетаНДС.Организация В
		|			(ВЫБРАТЬ
		|				втОрганизации.Организация КАК Организация
		|			ИЗ
		|				втОрганизации КАК втОрганизации)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеНаРасчетныйСчет.Дата КАК Дата,
		|	ПоступлениеНаРасчетныйСчет.Организация КАК Организация,
		|	ПоступлениеНаРасчетныйСчет.Ссылка КАК Документ,
		|	ПоступлениеНаРасчетныйСчет.ВидОперации КАК ВидОперации,
		|	ПоступлениеНаРасчетныйСчет.СуммаДокумента КАК СуммаДокумента,
		|	ПоступлениеНаРасчетныйСчет.НазначениеПлатежа КАК НазначениеПлатежа,
		|	ПоступлениеНаРасчетныйСчет.Контрагент КАК Контрагент,
		|	ПоступлениеНаРасчетныйСчет.ДоговорКонтрагента КАК ДоговорКонтрагента
		|ИЗ
		|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
		|ГДЕ
		|	ПоступлениеНаРасчетныйСчет.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПоступлениеНаРасчетныйСчет.Организация В
		|			(ВЫБРАТЬ
		|				втОрганизации.Организация КАК Организация
		|			ИЗ
		|				втОрганизации КАК втОрганизации)
		|	И ПоступлениеНаРасчетныйСчет.Проведен
		|	И ПоступлениеНаРасчетныйСчет.БезЗакрывающихДокументов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПриходныйКассовыйОрдер.Дата,
		|	ПриходныйКассовыйОрдер.Организация,
		|	ПриходныйКассовыйОрдер.Ссылка,
		|	ПриходныйКассовыйОрдер.ВидОперации,
		|	ПриходныйКассовыйОрдер.СуммаДокумента,
		|	ВЫРАЗИТЬ(ПриходныйКассовыйОрдер.Комментарий КАК СТРОКА(210)),
		|	ПриходныйКассовыйОрдер.Контрагент,
		|	ПриходныйКассовыйОрдер.ДоговорКонтрагента
		|ИЗ
		|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
		|ГДЕ
		|	ПриходныйКассовыйОрдер.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПриходныйКассовыйОрдер.Организация В
		|			(ВЫБРАТЬ
		|				втОрганизации.Организация КАК Организация
		|			ИЗ
		|				втОрганизации КАК втОрганизации)
		|	И ПриходныйКассовыйОрдер.Проведен
		|	И ПриходныйКассовыйОрдер.БезЗакрывающихДокументов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОплатаПлатежнойКартой.Дата,
		|	ОплатаПлатежнойКартой.Организация,
		|	ОплатаПлатежнойКартой.Ссылка,
		|	ОплатаПлатежнойКартой.ВидОперации,
		|	ОплатаПлатежнойКартой.СуммаДокумента,
		|	ВЫРАЗИТЬ(ОплатаПлатежнойКартой.Комментарий КАК СТРОКА(210)),
		|	ОплатаПлатежнойКартой.Контрагент,
		|	ОплатаПлатежнойКартой.ДоговорКонтрагента
		|ИЗ
		|	Документ.ОплатаПлатежнойКартой КАК ОплатаПлатежнойКартой
		|ГДЕ
		|	ОплатаПлатежнойКартой.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ОплатаПлатежнойКартой.Организация В
		|			(ВЫБРАТЬ
		|				втОрганизации.Организация КАК Организация
		|			ИЗ
		|				втОрганизации КАК втОрганизации)
		|	И ОплатаПлатежнойКартой.Проведен
		|	И ОплатаПлатежнойКартой.БезЗакрывающихДокументов
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписаниеСРасчетногоСчета.Дата КАК Дата,
		|	СписаниеСРасчетногоСчета.Организация КАК Организация,
		|	СписаниеСРасчетногоСчета.Ссылка КАК Документ,
		|	СписаниеСРасчетногоСчета.ВидОперации КАК ВидОперации,
		|	СписаниеСРасчетногоСчета.СуммаДокумента КАК СуммаДокумента,
		|	СписаниеСРасчетногоСчета.НазначениеПлатежа КАК НазначениеПлатежа,
		|	СписаниеСРасчетногоСчета.Контрагент КАК Контрагент,
		|	СписаниеСРасчетногоСчета.ДоговорКонтрагента КАК ДоговорКонтрагента
		|ИЗ
		|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
		|ГДЕ
		|	СписаниеСРасчетногоСчета.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СписаниеСРасчетногоСчета.Организация В
		|			(ВЫБРАТЬ
		|				втОрганизации.Организация КАК Организация
		|			ИЗ
		|				втОрганизации КАК втОрганизации)
		|	И СписаниеСРасчетногоСчета.Проведен
		|	И СписаниеСРасчетногоСчета.БезЗакрывающихДокументов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасходныйКассовыйОрдер.Дата,
		|	РасходныйКассовыйОрдер.Организация,
		|	РасходныйКассовыйОрдер.Ссылка,
		|	РасходныйКассовыйОрдер.ВидОперации,
		|	РасходныйКассовыйОрдер.СуммаДокумента,
		|	ВЫРАЗИТЬ(РасходныйКассовыйОрдер.Комментарий КАК СТРОКА(210)),
		|	РасходныйКассовыйОрдер.Контрагент,
		|	РасходныйКассовыйОрдер.ДоговорКонтрагента
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
		|ГДЕ
		|	РасходныйКассовыйОрдер.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РасходныйКассовыйОрдер.Организация В
		|			(ВЫБРАТЬ
		|				втОрганизации.Организация КАК Организация
		|			ИЗ
		|				втОрганизации КАК втОрганизации)
		|	И РасходныйКассовыйОрдер.Проведен
		|	И РасходныйКассовыйОрдер.БезЗакрывающихДокументов
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	Дата";

	Результат = Запрос.ВыполнитьПакет();
	СтавкиНДСПоПериодам = Результат[1].Выгрузить();
	ДокументыПоступления = Результат[2].Выгрузить();
	ДокументыСписания = Результат[3].Выгрузить();
	
	ДокументыПоступления.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыСтавокНДС"));
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ДокументыПоступленияСОшибками", ДокументыПоступления);
	РезультатПроверки.Вставить("ДокументыСписанияСОшибками", ДокументыСписания);
	
	Если ДокументыПоступления.Количество() = 0 И ДокументыПоступления.Количество() = 0 Тогда
		Возврат РезультатПроверки;
	КонецЕсли;
	
	ОтборПоОрганизации = Новый Структура("Организация", Справочники.Организации.ПустаяСсылка());
	Для Каждого ДокументПоступления Из ДокументыПоступления Цикл
		ОтборПоОрганизации.Организация = ДокументПоступления.Организация;
		ПериодыСтавокНДС = СтавкиНДСПоПериодам.НайтиСтроки(ОтборПоОрганизации);
		
		Если ПериодыСтавокНДС.Количество() > 0 Тогда
			Ставка = Перечисления.ВидыСтавокНДС.БезНДС;
			Для Каждого ПериодСтавокНДС Из ПериодыСтавокНДС Цикл
				Если ДокументПоступления.Дата >= ПериодСтавокНДС.Период Тогда
					Ставка = ПериодСтавокНДС.СтавкаНДС;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			ДокументПоступления.СтавкаНДС = Ставка;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция СформироватьТабличныеДокументы(РезультатПроверкиДокументов)
	
	Макет = ПолучитьМакет("Макет");
	РезультатПоступления = Новый ТабличныйДокумент;
	РезультатСписания = Новый ТабличныйДокумент;
	КоличествоОплатПлатежнойКартой = 0;
	
	Если РезультатПроверкиДокументов.ДокументыПоступленияСОшибками.Количество() = 0 Тогда
		ОбластьНетОшибок = Макет.ПолучитьОбласть("НетОшибок");
		РезультатПоступления.Вывести(ОбластьНетОшибок);
	Иначе
		ОбластьЗаголовокСписка = Макет.ПолучитьОбласть("ЗаголовокСписка");
		РезультатПоступления.Вывести(ОбластьЗаголовокСписка);
		РезультатПоступления.ФиксацияСверху = РезультатПоступления.ВысотаТаблицы;
		
		ОбластьДокумент = Макет.ПолучитьОбласть("Документ");
		НомерСтроки = 1;
		Для Каждого ДокументСОшибками Из РезультатПроверкиДокументов.ДокументыПоступленияСОшибками Цикл
			ОбластьДокумент.Параметры.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки +1;
			ЗаполнитьЗначенияСвойств(ОбластьДокумент.Параметры, ДокументСОшибками, , "СтавкаНДС");
			ОбластьДокумент.Параметры.СтавкаНДС =
				Перечисления.СтавкиНДС.СтавкаНДС(ДокументСОшибками.СтавкаНДС, ДокументСОшибками.Дата);
			РезультатПоступления.Вывести(ОбластьДокумент);
			Если ТипЗнч(ДокументСОшибками.Документ) = Тип("ДокументСсылка.ОплатаПлатежнойКартой") Тогда
				КоличествоОплатПлатежнойКартой = КоличествоОплатПлатежнойКартой + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если РезультатПроверкиДокументов.ДокументыСписанияСОшибками.Количество() = 0 Тогда
		ОбластьНетОшибок = Макет.ПолучитьОбласть("НетОшибок");
		РезультатСписания.Вывести(ОбластьНетОшибок);
	Иначе
		ОбластьЗаголовокСписка = Макет.ПолучитьОбласть("ЗаголовокСпискаСписания");
		РезультатСписания.Вывести(ОбластьЗаголовокСписка);
		РезультатСписания.ФиксацияСверху = РезультатСписания.ВысотаТаблицы;
		ОбластьДокумент = Макет.ПолучитьОбласть("ДокументСписания");
		НомерСтроки = 1;
		Для Каждого ДокументСОшибками Из РезультатПроверкиДокументов.ДокументыСписанияСОшибками Цикл
			ОбластьДокумент.Параметры.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки +1;
			ЗаполнитьЗначенияСвойств(ОбластьДокумент.Параметры, ДокументСОшибками);
			РезультатСписания.Вывести(ОбластьДокумент);
		КонецЦикла;
	КонецЕсли;
	
	КоличествоДокументовПоступления = РезультатПроверкиДокументов.ДокументыПоступленияСОшибками.Количество();
	КоличествоДокументовСписания = РезультатПроверкиДокументов.ДокументыСписанияСОшибками.Количество();
	
	Результат = Новый Структура;
	Результат.Вставить("ДокументыПоступленияСОшибками", РезультатПроверкиДокументов.ДокументыПоступленияСОшибками);
	Результат.Вставить("ДокументыСписанияСОшибками", РезультатПроверкиДокументов.ДокументыСписанияСОшибками);
	Результат.Вставить("КоличествоДокументовПоступления", КоличествоДокументовПоступления);
	Результат.Вставить("КоличествоДокументовСписания", КоличествоДокументовСписания);
	Результат.Вставить("КоличествоОплатПлатежнойКартой", КоличествоОплатПлатежнойКартой);
	Результат.Вставить("РезультатПоступления", РезультатПоступления);
	Результат.Вставить("РезультатСписания", РезультатСписания);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли