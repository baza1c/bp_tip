#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкиПоУмолчанию = Новый Структура;
	НастройкиПоУмолчанию.Вставить("ВидНоменклатуры", Справочники.ВидыНоменклатуры.ЭлементВидНоменклатурыПоУмолчанию(Ложь));
	НастройкиПоУмолчанию.Вставить("ВидНоменклатурыУслуга", Справочники.ВидыНоменклатуры.ЭлементВидНоменклатурыПоУмолчанию(Истина));
	НастройкиПоУмолчанию.Вставить("ЕдиницаИзмерения", Справочники.КлассификаторЕдиницИзмерения.ПолучитьЕдиницуИзмеренияПоУмолчанию());
	НастройкиПоУмолчанию.Вставить("НоменклатурнаяГруппа", Справочники.НоменклатурныеГруппы.НайтиСоздатьОсновнуюНоменклатурнуюГруппу());
	
	ПодготовитьФормуНаСервере(Отказ);
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере(Отказ)
	
	ДокументСсылка = Параметры.ДокументСсылка;
	Если Параметры.Свойство("АдресХранилища") Тогда
		АдресХранилища        = Параметры.АдресХранилища;
		ДанныеДляСопоставления = ПолучитьИзВременногоХранилища(АдресХранилища);
	Иначе
		НесопоставленныеОбъекты = РегистрыСведений.НесопоставленныеОбъектыИнтернетМагазина.СоздатьНаборЗаписей();
		НесопоставленныеОбъекты.Отбор.Документ.Установить(ДокументСсылка);
		НесопоставленныеОбъекты.Прочитать();
		Если НесопоставленныеОбъекты.Количество() = 0 Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		ДанныеДляСопоставления = НесопоставленныеОбъекты[0].ДанныеОбъекта.Получить();
	КонецЕсли;
		
	//Если не сохранено номенклатуры для сопоставления форму не создаем
	Если ДанныеДляСопоставления = Неопределено 
		ИЛИ ТипЗнч(ДанныеДляСопоставления) <> Тип("ТаблицаЗначений") Тогда
		Отказ = Истина;
	КонецЕсли;
	
	//Если есть товары для сопоставления
	МассивИдентификаторов = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДляСопоставления, "Идентификатор", Истина);
	ИдентификаторыНоменклатуры = ОбменСYclients.НоменклатураПоИдентификаторам(МассивИдентификаторов);
	НоменклатураПоНаименованию = ОбменСYclients.НайтиНоменклатуруПоНаименованию(ДанныеДляСопоставления);
	ВсеДанныеСопоставлены = Истина;
	
	ЕдиницыИзмерения = Новый Соответствие;
	Для каждого СтрокаТаблицы из ДанныеДляСопоставления Цикл
		НоваяСтрока = НоменклатураКСопоставлению.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, , "Единица");
		
		НоваяСтрока.НаименованиеПредставление  = "Новый: " + НоваяСтрока.Наименование;
		НоваяСтрока.ИдентификаторНоменклатуры  = СтрокаТаблицы.Идентификатор;
		НоваяСтрока.ИдентификаторПредставление = "Код Yclients: " + СтрокаТаблицы.Идентификатор;
		НоваяСтрока.ЗначокДействия             = 2;
		
		НоваяСтрока.Номенклатура = ИдентификаторыНоменклатуры.Получить(СтрокаТаблицы.Идентификатор);
		Если Не ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
			ВсеДанныеСопоставлены = Ложь;
			НоваяСтрока.Номенклатура = НоменклатураПоНаименованию.Получить(СтрокаТаблицы.Идентификатор);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
			НоваяСтрока.ЗначокДействия             = 0;
			НоваяСтрока.НаименованиеПредставление  = НоваяСтрока.Наименование;
		КонецЕсли;
			
		Если ЗначениеЗаполнено(СтрокаТаблицы.Единица) Тогда
			БазоваяЕдиница = ЕдиницыИзмерения.Получить(СтрокаТаблицы.Единица);
			Если БазоваяЕдиница = Неопределено Тогда
				БазоваяЕдиница = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию(СтрокаТаблицы.Единица, Истина);
				Если ЗначениеЗаполнено(БазоваяЕдиница) Тогда
					ЕдиницыИзмерения.Вставить(СтрокаТаблицы.Единица, БазоваяЕдиница);
				КонецЕсли;
			КонецЕсли;
			НоваяСтрока.Единица = БазоваяЕдиница;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Если вся номенклатура уже сопосталена (например, при обработке других документов)
	//передаем данные в для дозаполнения документа, но форму сопоставления не открыаем
	Если ВсеДанныеСопоставлены Тогда
		
		ПараметрОповещения = Новый Структура;
		ПараметрОповещения.Вставить("Номенклатура",                    РезультатСопоставления());
		ПараметрОповещения.Вставить("ИдентификаторВызывающейФормы",    ВладелецФормы.УникальныйИдентификатор);
		ПараметрОповещения.Вставить("ДанныеСопоставленыАвтоматически", Истина);

		
		Оповестить("ЗаполненыДанныеYclients", ПараметрОповещения, ВладелецФормы);
		
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент Тогда
		
		ПараметрОповещения = Новый Структура;
		ПараметрОповещения.Вставить("Номенклатура",                 РезультатСопоставления());
		ПараметрОповещения.Вставить("ИдентификаторВызывающейФормы", ВладелецФормы.УникальныйИдентификатор);
		
		Оповестить("ЗаполненыДанныеYclients", ПараметрОповещения, ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокНоменклатурыАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено
		И НЕ ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		ТекстПодбора = НСтр("ru = 'Создать: '")
			+ ТекущиеДанные.Наименование
			+ ?(ПустаяСтрока(ТекущиеДанные.Артикул), "", ", " + ТекущиеДанные.Артикул)
			+ ?(ПустаяСтрока(ТекущиеДанные.Единица), "", ", " + ТекущиеДанные.Единица);
		ДанныеВыбора.Добавить(ТекстПодбора);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено 
		И ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ПараметрыФормы.Вставить("ТекстЗаполнения", ТекущиеДанные.Наименование);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ПараметрыЗаполненияНоменклатуры(ТекущиеДанные, НастройкиПоУмолчанию));
		ОткрытьФорму("Справочник.Номенклатура.ФормаОбъекта",ПараметрыФормы, Элемент);
	Иначе
		Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			ТекущиеДанные.ЗначокДействия = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатураОчистка(Элемент, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.ЗначокДействия = 0;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыЗаполненияНоменклатуры(ТекущиеДанные, НастройкиПоУмолчанию)
	
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Наименование",         ТекущиеДанные.Наименование);
	ДанныеЗаполнения.Вставить("НаименованиеПолное",   ТекущиеДанные.Наименование);
	ДанныеЗаполнения.Вставить("Артикул",              ТекущиеДанные.Артикул);
	ДанныеЗаполнения.Вставить("Услуга",               ТекущиеДанные.Услуга);
	ДанныеЗаполнения.Вставить("ВидНоменклатуры",      ?(ТекущиеДанные.Услуга, НастройкиПоУмолчанию.ВидНоменклатурыУслуга, НастройкиПоУмолчанию.ВидНоменклатуры));
	ДанныеЗаполнения.Вставить("НоменклатурнаяГруппа", НастройкиПоУмолчанию.НоменклатурнаяГруппа);
		
	БазоваяЕдиница = ТекущиеДанные.Единица;
	Если Не ЗначениеЗаполнено(БазоваяЕдиница) Тогда
		БазоваяЕдиница = НастройкиПоУмолчанию.ЕдиницаИзмерения;
	КонецЕсли;
	ДанныеЗаполнения.Вставить("ЕдиницаИзмерения",     БазоваяЕдиница);
	
	Возврат ДанныеЗаполнения;
		
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Серый текст для новой номенклатуры
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокНоменклатура");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"НоменклатураКСопоставлению.Номенклатура", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ТемноСерый);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("НоменклатураКСопоставлению.НаименованиеПредставление"));
	
КонецПроцедуры

&НаСервере
Функция РезультатСопоставления()
	
	РезультатСопоставления = Новый Соответствие;
	
	Для каждого НоменклатураКСопоставлениюСтрока Из НоменклатураКСопоставлению Цикл
		
		Если Не ЗначениеЗаполнено(НоменклатураКСопоставлениюСтрока.Номенклатура) Тогда
			
			Номенклатура = Справочники.Номенклатура.СоздатьЭлемент();
			Номенклатура.Заполнить(ПараметрыЗаполненияНоменклатуры(НоменклатураКСопоставлениюСтрока, НастройкиПоУмолчанию));
			Номенклатура.Записать();
			
			НоменклатураКСопоставлениюСтрока.Номенклатура = Номенклатура.Ссылка;
		КонецЕсли;
		
		РезультатСопоставления.Вставить(НоменклатураКСопоставлениюСтрока.НомерСтрокиЗаказа, НоменклатураКСопоставлениюСтрока.Номенклатура);
		
		РегистрыСведений.НоменклатураYclients.УстановитьСоответствие(НоменклатураКСопоставлениюСтрока.ИдентификаторНоменклатуры,
			НоменклатураКСопоставлениюСтрока.Номенклатура);
		
	КонецЦикла;
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		ОбменСYclients.УдалитьДанныеДляСопоставления(ДокументСсылка);
	Конецесли;
	
	Возврат РезультатСопоставления;
	
КонецФункции

#КонецОбласти