
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ДанныеДокументов") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ДанныеДокументов) = Тип("Массив") Тогда
		Для Каждого Документ Из Параметры.ДанныеДокументов Цикл
			НоваяСтрока = СозданныеДокументы.Добавить();
			НоваяСтрока.Ссылка = Документ;
			НоваяСтрока.Картинка = 1;
		КонецЦикла;
	КонецЕсли; 
	
	ОбновитьСтатусыДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура СозданныеДокументыПередНачаломИзменения(Элемент, Отказ)
	ТекущиеДанные = Элементы.СозданныеДокументы.ТекущиеДанные;
	Отказ = Истина;
	Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Ссылка);
		Если ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.ФормаОбъекта", ПараметрыФормы, , , , , );
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОтчетОРозничныхПродажах" Тогда
		ОбновитьСтатусыДокументов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусыДокументов()
	
	МассивДокументов = Новый Массив;
	Для Каждого СтрокаДокумента Из СозданныеДокументы Цикл
		МассивДокументов.Добавить(СтрокаДокумента.Ссылка);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документы", МассивДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Проведен КАК Проведен,
	|	ОтчетОРозничныхПродажах.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка В(&Документы)";
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		НайденныеСтроки = СозданныеДокументы.НайтиСтроки(Новый Структура("Ссылка", Результат.Ссылка));
		Для Каждого СтрокаДокумента Из НайденныеСтроки Цикл
			Если Результат.ПометкаУдаления Тогда
				СтрокаДокумента.Картинка = 3;
			Иначе
				СтрокаДокумента.Картинка = ?(Результат.Проведен, 2, 1);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

