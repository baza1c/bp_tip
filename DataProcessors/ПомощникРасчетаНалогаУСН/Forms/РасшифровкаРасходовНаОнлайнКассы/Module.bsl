#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Организация = Параметры.Организация;
	Объект.Период = Параметры.Период;
	Если ЗначениеЗаполнено(Параметры.РегистрацияВНалоговомОргане) Тогда
		РегистрацияВНалоговомОргане = Параметры.РегистрацияВНалоговомОргане;
	Иначе
		РегистрацияВНалоговомОргане = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.Организация, "РегистрацияВНалоговомОргане");
	КонецЕсли;
	ЗаполнитьПоказатели(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_РасходыНаКассовуюТехнику" Или (ИмяСобытия = "АктуализацияЗавершенаУспешно"
		И ТипЗнч(Параметр) = Тип("Структура")
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Организация") = Объект.Организация) Тогда
		ЗаполнитьПоказатели(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВсегоРасходовНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УСН = ПредопределенноеЗначение("Перечисление.ВидыНалоговУменьшаемыхНаРасходыККТ.УСН");
	
	Фильтр = Новый Структура;
	Фильтр.Вставить("УменьшаемыйНалог", УСН);
	Фильтр.Вставить("Организация", Объект.Организация);
	Фильтр.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	Если ВсегоРасходов = 0 Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Фильтр);
		ПараметрыФормы.Вставить("ВызовИзПомощника", Истина);
		
		ОткрытьФорму("Документ.РасходыНаОнлайнКассыУменьшающиеНалоги.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	Иначе
		ПараметрыФормы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Фильтр);
		ПараметрыФормы.Вставить("Отбор", Фильтр);
		
		ОткрытьФорму("Документ.РасходыНаОнлайнКассыУменьшающиеНалоги.Форма.ФормаСписокРасходов", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПоказатели(Форма)
	
	Объект = Форма.Объект;
	Информация = ИнформацияПоРасходам(Объект.Организация, Объект.Период);
	Форма.ВсегоРасходов = Информация.ВсегоРасходов;
	Форма.УменьшениеТекущегоПлатежа = Информация.ИспользованныйВычет;
	Форма.НеиспользованныйОстаток = Информация.Остаток;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнформацияПоРасходам(Организация, Период)
	
	Возврат РегистрыНакопления.РасходыНаОнлайнКассыУменьшающиеНалоги.ИнформацияПоСуммамРасходовЗаПериод(
		Организация, НачалоГода(Период), Период, Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.УСН);
	
КонецФункции

#КонецОбласти