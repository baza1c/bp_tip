&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияАктуализации;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЦветПодсветки = ЦветаСтиля.ВыборСтандартногоПериодаФонКнопки;
	
	МожноСоздаватьДекларацию      = ПравоДоступа("Изменение", Метаданные.Документы.РегламентированныйОтчет);
	МожноАктуализироватьДанные    = ПравоДоступа("Изменение", Метаданные.Документы.РегламентнаяОперация);
	МожноСоздаватьДокументыУплаты = ПравоДоступа("Изменение", Метаданные.Документы.ПлатежноеПоручение)
		И ПравоДоступа("Изменение", Метаданные.Документы.РасходныйКассовыйОрдер);
	МожноСоздаватьУведомления     = ПравоДоступа("Изменение", Метаданные.Документы.УведомлениеОбИсчисленныхСуммахНалогов);
	
	Параметры.Свойство("Организация", Объект.Организация);
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.КонецПериода) Тогда
		Объект.Период = Параметры.КонецПериода;
	Иначе
		ТекущаяДатаПользователя = ОбщегоНазначения.ТекущаяДатаПользователя();
		Если Не УчетнаяПолитика.ПрименяетсяУСН(Объект.Организация, КонецКвартала(ТекущаяДатаПользователя)) Тогда
			Объект.Период = КонецГода(ТекущаяДатаПользователя);
		Иначе
			Объект.Период = НачалоКвартала(ТекущаяДатаПользователя) - 1; // Конец прошлого квартала.
		КонецЕсли;
	КонецЕсли;
	
	КонтекстныйВызов = Параметры.КонтекстныйВызов;
	
	УстановитьЗаголовок(ЭтотОбъект);
	УстановитьПредставлениеПериода(ПредставлениеПериода, Объект.Период);
	
	ОрганизацияПредставление = Объект.Организация;
	ИспользоватьНесколькоОрганизацийБухгалтерскийУчет = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	ИнтерфейсИнтеграцииСБанками = ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком();
	Подключена1СОтчетность = РегламентированнаяОтчетностьБП.Подключена1СОтчетность(Объект.Организация);
	
	Если ИнтерфейсИнтеграцииСБанками Тогда
		Элементы.Доходы.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элементы.Доходы.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	ТребуетсяПроверятьАктуальность = Не ЭтоПериодПрошлыхЛет И НетОшибокБлокирующихРасчетНалога(ЭтотОбъект);
	ВозможнаБыстраяАктуализация    = Истина;
	
	Если ИнтерфейсИнтеграцииСБанками Тогда
		ПредупрежденияПриЗагрузке = НайтиПредупрежденияПриЗагрузкеВыписки(
			Объект.Организация, НачалоНалоговогоПериода, КонецКвартала(Объект.Период));
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПредупрежденияПриЗагрузке);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Обработка.ПомощникРасчетаНалогаУСН",
		"Форма",
		НСтр("ru='Новости: Отчетность по УСН'"),
		ИдентификаторыСобытийПриОткрытии);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	УправлениеФормойНаСервере();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей();
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	Если НЕ ЗавершениеРаботы Тогда
		ЗакрытиеМесяцаВызовСервера.ОтменитьВыполнениеЗадания(ИдентификаторЗаданияАктуализации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ВыполняетсяФоновоеЗадание(ЭтотОбъект) Тогда // на время актуализации не принимаем никаких оповещений
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_Организации"
		И Объект.Организация = Источник Тогда
		
		ДанныеУчетаАктуальны = Ложь;
		ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей();
		
	ИначеЕсли ИмяСобытия = "Запись_РегистрацияСуммыУбыткаУСН" И Параметр.РежимЗаписи = РежимЗаписиДокумента.Проведение
		И Параметр.Организация = Объект.Организация И Параметр.Год = Год(Объект.Период) Тогда
		
		Если ДанныеУчетаАктуальны Тогда
			ЗаполнитьАктуальныеПоказателиИПояснения();
		Иначе
			ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей();
			ТребуетсяПроверятьАктуальность = Ложь;
			УправлениеФормойНаСервере();
		КонецЕсли;
		
		ЗапуститьОбновлениеБаннераСостоянияОтправки();
		
	ИначеЕсли ИмяСобытия = "Запись_ПлатежныйДокумент_УплатаНалогов" Тогда
		
		// Записан документ ПлатежноеПоручение или РасходныйКассовыйОрдер с видом операции "Уплата налога"
		
		Налог = Неопределено;
		
		Если ДанныеУчетаАктуальны И ТипЗнч(Параметр) = Тип("Структура")
			И Параметр.Свойство("Организация") И Параметр.Организация = Объект.Организация
			И Параметр.Свойство("Налог", Налог) И ЗначениеЗаполнено(Налог) И ЭтоУплатаНалогаУСН(Налог) Тогда
			
			ЗаполнитьАктуальныеПоказателиИПояснения();
			ЗапуститьОбновлениеБаннераСостоянияОтправки();
			РассчитатьСуммуЕНС();
			ОтразитьИспользованиеОстаткаЕНС();
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_УведомлениеОбИсчисленныхСуммахНалогов" Тогда
		
		ЗаполнитьУведомленияИОтобразитьНаФорме();
		УстановитьКнопкуПоУмолчанию(ЭтотОбъект);
		УправлениеФормойНаСервере();
		
	ИначеЕсли ИмяСобытия = "Позиционирование в списке отчетов"
		Или ИмяСобытия = "Изменение пометки удаления объекта"
		Или ИмяСобытия = "Запись_РегламентированныйОтчет" Тогда
		
		// Записан регламентированный отчет
		
		Если (ДанныеУчетаАктуальны Или ЭтоПериодПрошлыхЛет)
			И ТипЗнч(Параметр) = Тип("Структура")
			И Параметр.Свойство("Организация") И Параметр.Организация = Объект.Организация
			И Параметр.Свойство("Ссылка") И ЭтоРегламентированныйОтчетУСН(Параметр.Ссылка, ИмяРеглОтчета) Тогда
			
			ЗаполнитьАктуальныеПоказателиИПояснения();
			
		КонецЕсли;
		
	ИначеЕсли ИнтерфейсИнтеграцииСБанками
		И (ИмяСобытия = "ОбновитьПоказателиРасчетаУСН" Или ИмяСобытия = "ИзменениеЗаписиКУДиР" Или ИмяСобытия = "ИзменениеУчетнойПолитики")
		И НЕ ОповещениеПолученоИзОсновнойФормыПомощника(Источник) Тогда

		ДанныеУчетаАктуальны = Ложь;
		ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей();
		
	ИначеЕсли ИнтерфейсИнтеграцииСБанками
		И (ИмяСобытия = "АктуализацияЗавершенаУспешно" Или ИмяСобытия = "АктуализацияОтменена") Тогда
		
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьНеобходимостьОбновленияДанныхПриИнтеграцииСБанком", 3, Ложь);
		
	ИначеЕсли (ИмяСобытия = "ОбновитьПоказателиРасчетаУСН" Или ИмяСобытия = "ИзменениеЗаписиКУДиР")
		И НЕ ОповещениеПолученоИзОсновнойФормыПомощника(Источник) Тогда
		
		ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей();
		
	ИначеЕсли ИмяСобытия = "ИзменениеВыписки"
		Или ИмяСобытия = "ОбновитьСостояниеОбменСБанками" Тогда
		
		ЗаполнитьАктуальныеПоказателиИПояснения();
		ЗапуститьОбновлениеБаннераСостоянияОтправки();
		РассчитатьСуммуЕНС();
		ОтразитьИспользованиеОстаткаЕНС();
		
	ИначеЕсли ИмяСобытия = "ИзмененоПредупреждениеПриЗагрузкеВыписки" И ИнтерфейсИнтеграцииСБанками Тогда
		
		ПредупрежденияПриЗагрузке = НайтиПредупрежденияПриЗагрузкеВыписки(
			Объект.Организация, НачалоНалоговогоПериода, КонецКвартала(Объект.Период));
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПредупрежденияПриЗагрузке);
		УправлениеФормойНаСервере();
		
	ИначеЕсли ИмяСобытия = "НалогиПрошлыхПериодов_ИзмененыОстатки" И Объект.Организация = Источник Тогда
		
		Если ДанныеУчетаАктуальны Тогда
			ЗаполнитьАктуальныеПоказателиИПояснения();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_НастройкиПродленияСроковНалоговОтчетов"
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("Организация")
		И Параметр.Организация = Объект.Организация Тогда
		
		ОбновитьДанныеФормы();
		
	ИначеЕсли ИмяСобытия = "ИзменениеУчетнойПолитики"
		И Параметр = Объект.Организация Тогда
		
		ОбновитьДанныеФормы();
		
	ИначеЕсли (ИмяСобытия = "Запись_СредняяЧисленностьРаботниковОрганизаций"
		Или ИмяСобытия = "Запись_УменьшениеУСННаФиксированныеВзносы")
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("Организация")
		И Параметр.Организация = Объект.Организация Тогда
		
		ДанныеУчетаАктуальны = Ложь;
		
		ОбновитьДанныеФормы();
		
	ИначеЕсли (ИмяСобытия = "ЛичныйКабинетЕНС_ОбновлениеДанных"
		Или ИмяСобытия = "Запись_СведенияОбУплатеНалогов")
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("Организация")
		И Параметр.Организация = Объект.Организация Тогда
		
		ОбновитьДанныеФормы();
		
	ИначеЕсли ИмяСобытия = "Запись_РасходыНаКассовуюТехнику" Тогда
		
		ОбновитьДанныеФормы();
		
	ИначеЕсли ИмяСобытия = "ПростойИнтерфейс_ФункциональностьИзменение"
		Или (ИмяСобытия = "Запись_НаборКонстант" И Источник = "ИспользоватьПродажиЧерезМаркетплейс") Тогда
		
		УстановитьВидимостьПредупрежденияПоОплатамМаркетплейсов();
		
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиВВводОстатковПриСменеОбъектаУСН" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ОткрытьФорму("Обработка.ВводОстатковПриИзмененииОбъектаУСН.Форма.Форма", ПараметрыФормы, ЭтотОбъект);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ЗаписьКУДиР" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор",     Новый Структура("ДеятельностьНаПатенте, Организация", Ложь, Объект.Организация));
		ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Записи книги доходов и расходов УСН'"));
		ОткрытьФорму("Документ.ЗаписьКУДиР.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылкиПредупреждениеПоРаботеСМаркетплейсами(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "ГруппаТорговля" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("ТекущаяСтраница", НавигационнаяСсылка);
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПредставление = Объект.Организация;
	ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеОбОшибкеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "УчетнаяПолитикаОрганизации" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		КлючЗаписиУчетнойПолитики = ДанныеУчетнойПолитики("НастройкиСистемыНалогообложения", Объект.Организация, Объект.Период);
		
		Если КлючЗаписиУчетнойПолитики <> Неопределено Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", КлючЗаписиУчетнойПолитики);
			
			ОткрытьФорму("РегистрСведений.НастройкиСистемыНалогообложения.ФормаЗаписи",
				ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация", Объект.Организация));
			
			ОткрытьФорму("РегистрСведений.НастройкиСистемыНалогообложения.ФормаСписка",
				ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",   Объект.Организация);
	ПараметрыФормы.Вставить("НачалоПериода", НачалоНалоговогоПериода);
	ПараметрыФормы.Вставить("КонецПериода",  КонецКвартала(Объект.Период));
	
	Если ИнтерфейсИнтеграцииСБанками Тогда
		ИмяОткрываемойФормы = "РасшифровкаДоходовИнтеграцияСБанком";
		
	Иначе
		ИмяОткрываемойФормы = "РасшифровкаДоходов";
		
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма." + ИмяОткрываемойФормы, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыТорговыйСборНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("НачалоПериода", НачалоНалоговогоПериода);
	ПараметрыФормы.Вставить("КонецПериода", КонецКвартала(Объект.Период));
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ДоходыТорговыйСбор");
	
	ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаДоходов", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",   Объект.Организация);
	ПараметрыФормы.Вставить("НачалоПериода", НачалоНалоговогоПериода);
	ПараметрыФормы.Вставить("КонецПериода",  КонецКвартала(Объект.Период));
	
	ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаРасходов", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратыПокупателямНажатие(Элемент, СтандартнаяОбработка)
	
	Если Не ИнтерфейсИнтеграцииСБанками Тогда
		ВызватьИсключение НСтр("ru = 'Функциональная опция ""Интеграция с банком"" отключена'");
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",   Объект.Организация);
	ПараметрыФормы.Вставить("НачалоПериода", НачалоНалоговогоПериода);
	ПараметрыФормы.Вставить("КонецПериода",  КонецКвартала(Объект.Период));
	
	ОткрытьФорму(
		"Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаВозвратовПокупателямИнтеграцияСБанком",
		ПараметрыФормы,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УплаченныеСтраховыеВзносыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация", Объект.Организация);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоНалоговогоПериода);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода", КонецКвартала(Объект.Период));
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	
	ПараметрыФормы = Новый Структура;
	
	Если ЭтоУпрощенныйЗачетВзносов(Объект.Организация, ПериодСобытия) Тогда
		// При упрощенном зачете с подлежащими к уплате взносами показываем признанные взносы, которые уплачены в текущем периоде
		ПараметрыФормы.Вставить("ВидОтчета", "СтраховыеВзносыПоСроку");
	Иначе
		ПараметрыФормы.Вставить("ВидОтчета", "СтраховыеВзносы");
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ВидРасшифровки",            2);
	
	ОткрытьФорму("Отчет.РасшифровкаПлатежейВБюджет.Форма.ФормаОтчета", ПараметрыФормы, ЭтотОбъект, "Взносы");
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховыеВзносыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыполняетсяФоновоеЗадание(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	НачалоПериода = НачалоГода(Объект.Период);
	КонецПериода  = КонецКвартала(Объект.Период);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыФормы.Вставить("КонецПериода", КонецПериода);
	ПараметрыФормы.Вставить("ДанныеУчетаАктуальны", ДанныеУчетаАктуальны);
	
	ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаУменьшенияНалогаИнтеграцияСБанком", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УплаченныйТорговыйСборНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация", Объект.Организация);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоНалоговогоПериода);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода", КонецКвартала(Объект.Период));
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ВидОтчета",                 "ТорговыйСбор");
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ВидРасшифровки",            2);
	
	ОткрытьФорму("Отчет.РасшифровкаПлатежейВБюджет.Форма.ФормаОтчета", ПараметрыФормы, ЭтотОбъект, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренесенныеУбыткиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(РегистрацияСуммыУбыткаУСН) Тогда
		ПоказатьЗначение(, РегистрацияСуммыУбыткаУСН);
	Иначе
		
		ЗначенияЗаполненияДокумента = Новый Структура;
		ЗначенияЗаполненияДокумента.Вставить("Организация", Объект.Организация);
		ЗначенияЗаполненияДокумента.Вставить("Дата",        КонецГода(Объект.Период));
		
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполненияДокумента);
		
		ОткрытьФорму("Документ.РегистрацияСуммыУбыткаУСН.Форма.ФормаДокумента", ПараметрыФормы, ЭтотОбъект, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНалогаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Период < УчетУСНКлиентСервер.ДатаНачалаПрогрессивнойШкалы()
		Или Объект.Период >= УчетУСНКлиентСервер.ДатаОтменыПрогрессивнойШкалы() Тогда
		
		КлючЗаписи = ДанныеУчетнойПолитики("НастройкиУчетаУСН", Объект.Организация, Объект.Период);
		
		Если КлючЗаписи = Неопределено Тогда
			Возврат;
		КонецЕсли;
				
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ", ПроверкаКлючаЗаписиНастройкиУСН(КлючЗаписи, Объект.Период));
		ПараметрыОткрытия.Вставить("Организация", Объект.Организация);
		ПараметрыОткрытия.Вставить("Период", Объект.Период);
		ПараметрыОткрытия.Вставить("ПараметрыОткрытия", Новый Структура("АктивныйЭлемент", "СтавкаНалогаУСН"));
		
		ОткрытьФорму("РегистрСведений.НастройкиУчетаУСН.Форма.ФормаЗаписиСтавкиНалога", ПараметрыОткрытия, ЭтотОбъект, "");
		
	Иначе
		
		КлючЗаписи = ДанныеУчетнойПолитики("НастройкиУчетаУСН", Объект.Организация, Объект.Период);
		Если КлючЗаписи = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ", КлючЗаписи);
		ПараметрыОткрытия.Вставить("Период", Объект.Период);
		ОткрытьФорму("РегистрСведений.НастройкиУчетаУСН.ФормаЗаписи", ПараметрыОткрытия, ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеЛьготнойСтавкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = ПараметрыОткрытияНастройкиУСНДляВводаОснованияЛьготы(Объект.Организация, Объект.Период);
	
	ОткрытьФорму("РегистрСведений.НастройкиУчетаУСН.ФормаЗаписи", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыОткрытияНастройкиУСНДляВводаОснованияЛьготы(Организация, ПериодПомощника)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтекстныйВызов", Истина);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура);
	
	КлючЗаписи = ДанныеУчетнойПолитики("НастройкиУчетаУСН", Организация, ПериодПомощника);
	
	Если КлючЗаписи = Неопределено Тогда
		ПараметрыФормы.ЗначенияЗаполнения.Вставить("Период", НачалоГода(ПериодПомощника));
		ПараметрыФормы.ЗначенияЗаполнения.Вставить("Организация", Организация);
		Возврат ПараметрыФормы;
	КонецЕсли;
	
	Если Год(КлючЗаписи.Период) < Год(ПериодПомощника) Тогда
		// Настройки записывались в прошлом году. Требуется создать новую запись началом года помощника.
		
		СуществующаяЗапись = РегистрыСведений.НастройкиУчетаУСН.СоздатьМенеджерЗаписи();
		СуществующаяЗапись.Период = КлючЗаписи.Период;
		СуществующаяЗапись.Организация = КлючЗаписи.Организация;
		СуществующаяЗапись.Прочитать();
		
		ИменаПолейСтрокой = ИменаПолейНастроекУСН();
		ЗначенияЗаполнения = Новый Структура(ИменаПолейСтрокой);
		
		ЗаполнитьЗначенияСвойств(ЗначенияЗаполнения, СуществующаяЗапись);
		ЗначенияЗаполнения.Вставить("Период", НачалоГода(ПериодПомощника));
		
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("Период", НачалоГода(ПериодПомощника));
	Иначе
		// Открываем существующую запись настроек.
		ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	КонецЕсли;
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИменаПолейНастроекУСН()
	
	Поля = Новый Массив;
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.НастройкиУчетаУСН;
	
	Для Каждого ОписаниеПоля Из МетаданныеРегистра.Измерения Цикл
		Поля.Добавить(ОписаниеПоля.Имя);
	КонецЦикла;
	
	Для Каждого ОписаниеПоля Из МетаданныеРегистра.Ресурсы Цикл
		Поля.Добавить(ОписаниеПоля.Имя);
	КонецЦикла;
	
	Для Каждого ОписаниеПоля Из МетаданныеРегистра.Реквизиты Цикл
		Поля.Добавить(ОписаниеПоля.Имя);
	КонецЦикла;
	
	Возврат СтрСоединить(Поля, ",");
	
КонецФункции

&НаКлиенте
Процедура АвансовыеПлатежиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифровкаАванса();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсвобождениеОтНалогаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьСправкуРасчетУСН(Объект.Период, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяБазаДоПревышенияЛимитовНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьСправкуРасчетУСН(НачалоКвартала(ПериодПревышенияЛимитов) - 1, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НалогДоПревышенияЛимитовНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьСправкуРасчетУСН(НачалоКвартала(ПериодПревышенияЛимитов) - 1, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеТребуютсяРеквизитыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ",        Объект.Организация);
	ПараметрыФормы.Вставить("Назначение",  "ДляОтчетности");
	ПараметрыФормы.Вставить("Контекст",    Новый Структура);
	ПараметрыФормы.Контекст.Вставить("Период",        Объект.Период);
	ПараметрыФормы.Контекст.Вставить("ИмяРеглОтчета", ИмяРеглОтчета);
	
	ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОшибкаНеЗаполненаЧисленностьРаботниковОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВРег(НавигационнаяСсылкаФорматированнойСтроки) = ВРег("УказатьЧисленностьРаботников") Тогда
		
		ПерейтиПоНавигационнойСсылке(СсылкаДляПереходаНеУказанаЧисленностьРаботников);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстРасчетыПрошлыхЛетОбработкаНавигационнойСсылки(Элемент, СтрокаСсылки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СтрокаСсылки = "ОплатитьЗадолженность" Тогда
		
		ОткрытьРасшифровкуРасчетовПрошлыхЛет();
		
	ИначеЕсли СтрокаСсылки = "ВвестиНачальныеОстатки" Тогда
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ПараметрыФормы.Вставить("Правило",     Правило);
		
		ОткрытьФорму("Обработка.МониторНалоговИОтчетности.Форма.ФормаТестПоНалогуЗаПрошлыеПериоды", ПараметрыФормы, ЭтотОбъект);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "РасшифровкаУменьшенияНалога" Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация",   Объект.Организация);
		ПараметрыФормы.Вставить("НачалоПериода", НачалоНалоговогоПериода);
		ПараметрыФормы.Вставить("КонецПериода",  КонецКвартала(Объект.Период));
		
		ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаУменьшенияНалога", ПараметрыФормы);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "РасшифровкаТорговогоСбораУменьшающегоНалог" Тогда
		
		ОткрытьРасшифровкуТорговогоСбораУменьшающегоНалог();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "РасшифровкаПереплаты" Тогда
		
		ОткрытьРасшифровкуРасчетовПрошлыхЛет();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СправкаРасчетУСН" Тогда
		
		ОткрытьСправкуРасчетУСН(Объект.Период, Истина);
	
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СправкаРасчетУСНЗаПериодДоПревышения" Тогда
		
		ОткрытьСправкуРасчетУСН(КонецКвартала(ДобавитьМесяц(ПериодПревышенияЛимитов, -3)), Ложь);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "РасшифровкаАванса" Тогда
		
		РасшифровкаАванса();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеДекларацииНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПоказатьЗначение(, Декларация);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПлатежУСНОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыПлатежныхДокументов = ПомощникиПоУплатеНалоговИВзносовКлиент.ПараметрыОбработкиПлатежныхДокументов(
		ПлатежиУСН, "ПлатежУСН", ОповещениеУдаленияПлатежногоДокумента());
	
	ПомощникиПоУплатеНалоговИВзносовКлиент.ОбработкаНавигационнойСсылкиПлатежногоДокумента(Элемент, НавигационнаяСсылкаФорматированнойСтроки, ПараметрыПлатежныхДокументов);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстОшибкиЗагрузкиВыпискиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если Не ИнтерфейсИнтеграцииСБанками Тогда
		ВызватьИсключение НСтр("ru = 'Функциональная опция ""Интеграция с банком"" отключена'");
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьДоходы" Тогда
		ИмяОткрываемойФормы = "РасшифровкаДоходовИнтеграцияСБанком";
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьВозвраты" Тогда
		ИмяОткрываемойФормы = "РасшифровкаВозвратовПокупателямИнтеграцияСБанком";
	Иначе
		ВызватьИсключение НСтр("ru = 'Неверная навигационная ссылка'");
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",   Объект.Организация);
	ПараметрыФормы.Вставить("НачалоПериода", НачалоНалоговогоПериода);
	ПараметрыФормы.Вставить("КонецПериода",  КонецКвартала(Объект.Период));
	ПараметрыФормы.Вставить("ПоказатьПредупреждения", Истина);
	
	ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма." + ИмяОткрываемойФормы, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УплаченныеСтраховыеВзносыПоСрокуНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация", Объект.Организация);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоНалоговогоПериода);
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода", КонецКвартала(Объект.Период));
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ВидОтчета",                 "СтраховыеВзносыПоСроку");
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ВидРасшифровки",            2);
	
	ОткрытьФорму("Отчет.РасшифровкаПлатежейВБюджет.Форма.ФормаОтчета", ПараметрыФормы, ЭтотОбъект,  "ВзносыПризнанные");
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаСписанияСЕНСПриИзменении(Элемент)
	
	СуммаСписанияСЕНСПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ФиксированныеСтраховыеВзносыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ОтчетныйПериод", Объект.Период);

	МинимальныйВзнос = СуммаВзносаЗаПрошлыйПериод;
	
	ПараметрыФормы.Вставить("МаксимальнаяСуммаВзноса", МаксимальнаяСуммаВзноса);
	ПараметрыФормы.Вставить("МинимальнаяСуммаВзноса", МинимальныйВзнос);
	ПараметрыФормы.Вставить("ВыбраннаяСуммаВзноса", СуммаВзносаДляУменьшения);
	ПараметрыФормы.Вставить("ВзносПоЕдиномуТарифу", ВзносПоЕдиномуТарифу);
	ПараметрыФормы.Вставить("ВзносСдоходов", ВзносСдоходов);
	ПараметрыФормы.Вставить("ВзносСДоходовПредыдущийГод", ВзносСДоходовПредыдущийГод);
	ПараметрыФормы.Вставить("ВзносСДоходовТекущийГод", ВзносСДоходовТекущийГод);
	ПараметрыФормы.Вставить("ПодсказкаВзносСДоходов", ПодсказкаВзносСДоходов);
	ПараметрыФормы.Вставить("ДоляДоходовПоУСН", ДоляДоходовПоУСН);
	ПараметрыФормы.Вставить("СуммаВзносаСкорректирована", СуммаВзносаСкорректирована);
	ПараметрыФормы.Вставить("ВзносыИспользованныеДляПатента", ВзносыИспользованныеДляПатента);
	ПараметрыФормы.Вставить("ОбъектНалогообложенияДоходы", ОбъектНалогообложенияДоходы);
	
	ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаФиксированныхВзносов",
		ПараметрыФормы,
		ЭтотОбъект, , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаИнтеграцияСЛичнымКабинетомЕНСОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ЕдиныйНалоговыйСчетИнтеграцияКлиентБП.ПодсказкаИнтеграцияСЛичнымКабинетомЕНСОбработкаНавигационнойСсылки(
		Объект.Организация, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура РасходыНаОнлайнКассыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрываемойФормы = Новый Структура;
	ПараметрыОткрываемойФормы.Вставить("Организация", Объект.Организация);
	ПараметрыОткрываемойФормы.Вставить("Период", Объект.Период);
	ПараметрыОткрываемойФормы.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаРасходовНаОнлайнКассы", ПараметрыОткрываемойФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗакрытьПодсказкуФиксированныеВзносы2024Нажатие(Элемент)
	
	ЗакрытьПодсказкуФиксированныеВзносы2024(Объект.Организация);
	Элементы.ГруппаФиксированныеВзносы2024.Видимость = Ложь;
	ПоказыватьПодсказкуФиксированныеВзносы2024 = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСкрытьПредупреждениеПоРаботеСМаркетплейсамиНажатие(Элемент)
	
	СкрытьПредупреждениеПоРаботеСМаркетплейсами(Объект.Организация);
	Элементы.ГруппаПредупреждениеПоРаботеСМаркетплейсами.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьПоказатели(Команда)
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьАнализУчетаУСН(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Организация",      Объект.Организация);
	ПараметрыФормы.Вставить("НачалоПериода",    НачалоНалоговогоПериода);
	ПараметрыФормы.Вставить("КонецПериода",     КонецКвартала(Объект.Период));
	ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	
	ОткрытьФорму("Отчет.АнализСостоянияНалоговогоУчетаПоУСН.Форма.ФормаОтчета", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АктуализироватьДанныеУчета(Команда)
	
	АктуализироватьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДекларацию(Команда)
	
	Если Не ПроверитьЗаполнениеПередСозданиемДекларации() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	ОписаниеДействияДекларация = ОписаниеДействияДекларация(Объект.Организация, Объект.Период, ТекстОшибки);
	
	Если ОписаниеДействияДекларация <> Неопределено Тогда
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействияДекларация);
	ИначеЕсли Не ПустаяСтрока(ТекстОшибки) Тогда
		ПоказатьПредупреждение(, ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьДекларацияУжеСдана(Команда)
	
	ПараметрыКоманды = ПараметрыВыполненияЗадачиПодготовкиДекларации(Объект.Организация, Объект.Период);
	
	Если ЗначениеЗаполнено(ПараметрыКоманды) Тогда
		
		ВыполнениеЗадачБухгалтераКлиент.ОтметитьЗадачуКакВыполненную(ПараметрыКоманды, ЭтотОбъект);
		
		Оповестить("ПомощникРасчетаНалогаУСН_ИзмененаЗадача");
		
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьКУДиР(Команда)
	
	НачалоПериода = НачалоГода(Объект.Период);
	КонецПериода  = КонецКвартала(Объект.Период);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Организация",      Объект.Организация);
	ПараметрыФормы.Вставить("НачалоПериода",    НачалоПериода);
	ПараметрыФормы.Вставить("КонецПериода",     КонецПериода);
	ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	
	ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходов.Форма.ФормаОтчета", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УплатитьНалогБанк(Команда)
	
	СпособУплатыНалога = ПредопределенноеЗначение("Перечисление.СпособыУплатыНалогов.БанковскийПеревод");
	ПередУплатойНалога();
	
КонецПроцедуры

&НаКлиенте
Процедура УплатитьНалогКасса(Команда)
	
	СпособУплатыНалога = ПредопределенноеЗначение("Перечисление.СпособыУплатыНалогов.НаличнымиПоКвитанции");
	ПередУплатойНалога();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодробнееНаИТС(Команда)
	
	АдресНаИТС = ЗадачиБухгалтераКлиентСервер.СсылкаНаИТС(Срок);
	ПерейтиПоНавигационнойСсылке(АдресНаИТС);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстСостояниеЗагрузкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ИнтеграцияСБанкамиФормыКлиент.ОбработкаНавигационнойСсылкиБаннера(
		ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеСформироватьУведомление(Команда)
	
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыУведомления = Новый Структура;
	ПараметрыУведомления.Вставить("Организация",                 Объект.Организация);
	ПараметрыУведомления.Вставить("Правило",                     ПравилоУведомления);
	ПараметрыУведомления.Вставить("ПериодСобытия",               ПериодСобытия);
	ПараметрыУведомления.Вставить("АдресХранилищаТаблицыНалоги", ОписаниеТаблицыНалоги());
	ПараметрыУведомления.Вставить("КонтекстныйВызов",            Истина);
	
	ОткрытьФорму("Документ.УведомлениеОбИсчисленныхСуммахНалогов.Форма.ФормаДокумента",
		ПараметрыУведомления,
		ЭтотОбъект,
		,
		,
		,
		ОткрытьДокументОповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УведомлениеНажатие(Команда)
	
	// Команды генерируются в КопироватьЭлементСПодчиненными() и содержат числовой идентификатор,
	// совпадающий с идентификаторами строк в списке Платежи
	Идентификатор = ЗадачиБухгалтераКлиентСервер.Идентификатор(Команда.Имя);
	Если Идентификатор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьУведомление(Идентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеСформироватьОперациюПоЕНС(Команда)
	
	ВыполненоВТихомРежиме = Ложь;
	СформироватьОперациюПоЕНСВТихомРежиме(ВыполненоВТихомРежиме);
	Если ВыполненоВТихомРежиме Тогда
		// Только перерисуем форму
		ОбработатьРедактированиеУведомления();
		Возврат;
	КонецЕсли;
	
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыУведомления = Новый Структура;
	ПараметрыУведомления.Вставить("Организация",                 Объект.Организация);
	ПараметрыУведомления.Вставить("Правило",                     Правило);
	ПараметрыУведомления.Вставить("ПериодСобытия",               ПериодСобытия);
	ПараметрыУведомления.Вставить("АдресХранилищаТаблицыНалоги", ОписаниеТаблицыНалоги());
	ПараметрыУведомления.Вставить("КонтекстныйВызов",            Истина);
	
	ОткрытьФорму("Документ.ОперацияПоЕдиномуНалоговомуСчету.Форма.ФормаДокумента",
		ПараметрыУведомления,
		ЭтотОбъект,
		,
		,
		,
		ОткрытьДокументОповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПлатежиВБанк(Команда)
	
	ДокументыОплатыПоБанку = ПлатежныеДокументыДляОтправкиПоДиректБанку();
	
	Если ЗначениеЗаполнено(ДокументыОплатыПоБанку) Тогда
		ОбменСБанкамиКлиент.СформироватьПодписатьОтправитьЭД(ДокументыОплатыПоБанку);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПлатежи(Команда)
	
	ДокументыОплатыВФайл = ПлатежныеДокументыДляВыгрузкиВФайл();
	
	Оповещение = Новый ОписаниеОповещения("ВыгрузитьПлатежныеДокументыЗавершение", ЭтотОбъект);
	ПомощникиПоУплатеНалоговИВзносовКлиент.ПроверитьИВыгрузитьПлатежныеДокументы(
		Объект.Организация, ДокументыОплатыВФайл, Оповещение, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьУведомлениеНажатие(Элемент)
	
	Идентификатор = ЗадачиБухгалтераКлиентСервер.Идентификатор(Элемент.Имя);
	Если Идентификатор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьУведомлениеНаСервере(Идентификатор);
	
	ЗаполнитьУведомленияИОтобразитьНаФорме();
	УстановитьКнопкуПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ЗакрытьПодсказкуФиксированныеВзносы2024(Организация)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		ИмяНастройкиПодсказкиФиксированныеВзносы2024(),
		КлючНастройкиПодсказкиФиксированныхВзносов2024(Организация),
		Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоказыватьПодсказкуФиксированныеВзносы2024(Организация, Период, ОбъектНалогообложенияДоходы)
	
	ПроверятьНастройку =
		Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация)
		И ТекущаяДатаСеанса() <= КонецДня(Дата('2025.04.30'))
		И КонецДня(Период) = КонецДня(Дата('2024.12.31'))
		И Не ОбъектНалогообложенияДоходы;
		
	Если ПроверятьНастройку Тогда
		НастройкиИзХранилища = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			ИмяНастройкиПодсказкиФиксированныеВзносы2024(),
			КлючНастройкиПодсказкиФиксированныхВзносов2024(Организация));
		Если НастройкиИзХранилища = Неопределено Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяНастройкиПодсказкиФиксированныеВзносы2024()
	Возврат "ПоказыватьПодсказкуФиксированныеВзносы2024";
КонецФункции

&НаСервереБезКонтекста
Функция КлючНастройкиПодсказкиФиксированныхВзносов2024(Организация)
	
	Возврат СтрШаблон("%1_%2", Организация.УникальныйИдентификатор(), "ФиксированныеВзносы2024");
	
КонецФункции

&НаКлиенте
Процедура РасшифровкаАванса()
	
	НачалоПериода = НачалоГода(Объект.Период);
	КонецПериода  = КонецКвартала(ДобавитьМесяц(Объект.Период, -3));
	
	Если ИнтерфейсИнтеграцииСБанками
		И Объект.Период < НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж() Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НачалоПериода", НачалоПериода);
		ПараметрыФормы.Вставить("КонецПериода",  КонецПериода);
		ПараметрыФормы.Вставить("Организация",   Объект.Организация);
		ПараметрыФормы.Вставить("Заголовок",     Элементы.АвансовыеПлатежи.Заголовок);
		
		ОткрытьФорму("Обработка.ПомощникРасчетаНалогаУСН.Форма.РасшифровкаАвансовИнтеграцияСБанком", ПараметрыФормы, ЭтотОбъект);
		
	Иначе
		
		ПользовательскиеНастройки  = Новый ПользовательскиеНастройкиКомпоновкиДанных;
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Организация", Объект.Организация);
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоПериода);
		ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("КонецПериода", КонецПериода);
		
		ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		
		ПараметрыФормы = Новый Структура;
		
		ПараметрыФормы.Вставить("ВидОтчета",                  "НачисленияПлатежиУСН");
		ПараметрыФормы.Вставить("ПользовательскиеНастройки",  ПользовательскиеНастройки);
		ПараметрыФормы.Вставить("ВидРасшифровки",             2);
		
		ОткрытьФорму("Отчет.РасшифровкаПлатежейВБюджет.Форма.ФормаОтчета", ПараметрыФормы, ЭтотОбъект, Новый УникальныйИдентификатор);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьНеобходимостьОбновленияДанныхПриИнтеграцииСБанком() Экспорт
	
	Если Не ВводДоступен() Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеУчетаАктуальны Тогда
		Возврат;
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьНеобходимостьОбновленияДанныхПриИнтеграцииСБанком");
	
	ОбновитьПоказатели(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьЗависимуюФорму(ИмяЗависимойФормы, Удалить = Ложь) Экспорт
	
	ЗависимаяФорма = ЗависимыеФормыРасшифровок.НайтиПоЗначению(ИмяЗависимойФормы);
	Если Удалить Тогда
		// При закрытии формы удалим информацию, что эту форму нужно оповещать.
		Если ЗависимаяФорма <> Неопределено Тогда
			ЗависимыеФормыРасшифровок.Удалить(ЗависимаяФорма);
		КонецЕсли;
	Иначе
		// В эти формы в дальнейшем нужно будет передать оповещение, что результат актуализации они могут узнать
		// из данной формы помощника.
		Если ЗависимаяФорма = Неопределено Тогда
			ЗависимыеФормыРасшифровок.Добавить(ИмяЗависимойФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередНачаломДлительнойОперации()
	
	СтатусФоновогоЗадания = НСтр("ru = 'Выполняется проверка данных...'");
	
	ТребуетсяПроверятьАктуальность = Не ЭтоПериодПрошлыхЛет И НетОшибокБлокирующихРасчетНалога(ЭтотОбъект);
	ДанныеУчетаАктуальны = ДеятельностьОтсутствует;
	ВозможнаБыстраяАктуализация = МожноАктуализироватьДанные;
	
	УправлениеФормойНаСервере();
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальностьДанных", 0.1, Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НетОшибокБлокирующихРасчетНалога(Форма)
	
	Возврат ПустаяСтрока(Форма.СообщениеОбОшибке) И ПустаяСтрока(Форма.ОшибкаНеЗаполненаЧисленностьРаботников);
	
КонецФункции

&НаСервере
Процедура УправлениеФормойНаСервере()
	
	Элементы.ГруппаНастройки.РасширеннаяПодсказка.Заголовок = ПодсказкаПоНалогу;
	
	ОрганизацияЗаполнена = ЗначениеЗаполнено(Объект.Организация);
	РасчетНалогаВозможен = ПустаяСтрока(СообщениеОбОшибке);
	
	ЕстьДанныеДляРасчета = РасчетНалогаВозможен И Не ЭтоПериодПрошлыхЛет И Не НалоговыйПериодПропущен;
	
	Элементы.ГруппаШапка.Видимость             = ОрганизацияЗаполнена И ЕстьДанныеДляРасчета;
	Элементы.ГруппаПодвал.Видимость            = ОрганизацияЗаполнена И ЕстьДанныеДляРасчета
		И ПустаяСтрока(ОшибкаНеЗаполненаЧисленностьРаботников) И НачальныеПоказателиРассчитаны;
	Элементы.ГруппаРасчетСуммыНалога.Видимость = ОрганизацияЗаполнена И ЕстьДанныеДляРасчета
		И ПустаяСтрока(ОшибкаНеЗаполненаЧисленностьРаботников) И Не ДеятельностьОтсутствует И НачальныеПоказателиРассчитаны;
	
	Элементы.ГруппаРасчетСуммыНалогаОшибка.Видимость =
		ОрганизацияЗаполнена И Не ПустаяСтрока(ОшибкаНеЗаполненаЧисленностьРаботников);
	
	Элементы.ГруппаОтчетностьПрошлыхЛет.Видимость = ОрганизацияЗаполнена И РасчетНалогаВозможен И ЭтоПериодПрошлыхЛет;
	
	Элементы.ГруппаИТС.Видимость    = ОрганизацияЗаполнена И ЕстьДанныеДляРасчета И Не ИнтерфейсИнтеграцииСБанками;
	
	Элементы.СообщениеОбОшибке.Видимость = Не РасчетНалогаВозможен;
	
	Элементы.ПредставлениеПериода.Видимость     = НЕ КонтекстныйВызов;
	Элементы.Организация.Видимость              = НЕ КонтекстныйВызов И ИспользоватьНесколькоОрганизацийБухгалтерскийУчет;
	Элементы.ОрганизацияПредставление.Видимость = КонтекстныйВызов ИЛИ НЕ ИспользоватьНесколькоОрганизацийБухгалтерскийУчет;
	
	Элементы.ПояснениеРасширенныйНалоговыйПериод.Видимость = РасчетНалогаВозможен И НалоговыйПериодРасширен;
	
	Если Не ОрганизацияЗаполнена Или НалоговыйПериодПропущен Тогда
		
		СтатусФоновогоЗадания = "";
		Элементы.ПомощникЕНС.Видимость = Ложь;
		Возврат;
		
	КонецЕсли;
	
	НужноПодготовитьДекларацию = НужноПодготовитьДекларацию(ЭтотОбъект);
	ВыполняетсяФоновоеЗадание = ВыполняетсяФоновоеЗадание(ЭтотОбъект);
	УпрощенныйЗачетВзносов = ЭтоУпрощенныйЗачетВзносов(Объект.Организация, ПериодСобытия);
	
	Элементы.ОбновитьПоказатели.Видимость = НЕ ИнтерфейсИнтеграцииСБанками;
	
	Элементы.ДоходыТорговыйСбор.Видимость        = УменьшатьНалогНаТорговыйСбор;
	Элементы.Расходы.Видимость                   = НЕ ОбъектНалогообложенияДоходы;
	Элементы.ВозвратыПокупателям.Видимость       = ОбъектНалогообложенияДоходы И ИнтерфейсИнтеграцииСБанками;
	Элементы.ПеренесенныеУбытки.Видимость        = НЕ ОбъектНалогообложенияДоходы И НужноПодготовитьДекларацию;
	Элементы.УплаченныйТорговыйСбор.Видимость    = ОбъектНалогообложенияДоходы И ПлательщикТорговогоСбора;
	Элементы.УплаченныеСтраховыеВзносы.Видимость = ОбъектНалогообложенияДоходы
		И Не ИнтерфейсИнтеграцииСБанками
		И (Не УпрощенныйЗачетВзносов Или УплаченныеСтраховыеВзносы > 0)
		И ДанныеУчетаАктуальны;
	Элементы.УплаченныеСтраховыеВзносыПоСроку.Видимость = ОбъектНалогообложенияДоходы 
		И Не ИнтерфейсИнтеграцииСБанками
		И Не УплаченныеСтраховыеВзносы = УплаченныеСтраховыеВзносыПоСроку
		И Не УпрощенныйЗачетВзносов;
	Элементы.РасходыНаОнлайнКассы.Видимость = ВычетыНаОнлайнКассыКлиентСервер.ВычетыПрименяются(
		Объект.Организация, Объект.Период, Перечисления.ВидыНалоговУменьшаемыхНаРасходыККТ.УСН)
		И ОбъектНалогообложенияДоходы;
	
	Элементы.ГруппаПредупреждениеОбОшибкахЗагрузки.Видимость = ИнтерфейсИнтеграцииСБанками И ОрганизацияЗаполнена
		И НЕ НалоговыйПериодПропущен И ЕстьОшибкиЗагрузкиВыписки;
	
	УстановитьВидимостьПредупрежденияПоОплатамМаркетплейсов();
	
	Если ОбъектНалогообложенияДоходы И ИнтерфейсИнтеграцииСБанками Тогда
		Элементы.СтраховыеВзносы.Видимость = (Не УпрощенныйЗачетВзносов Или УплаченныеСтраховыеВзносы > 0);
		Элементы.СтраховыеВзносы.Доступность = Не ВыполняетсяФоновоеЗадание;
	Иначе
		Элементы.СтраховыеВзносы.Видимость = Ложь;
	КонецЕсли;
	
	НастроитьОснованиеЛьготнойСтавкиНалога();
	
	Элементы.АвансовыеПлатежи.Видимость = НомерКвартала <> 1;
	Элементы.АвансовыеПлатежи.Заголовок = ?(НомерКвартала = 2, НСтр("ru = 'Авансовый платеж за '"),
		НСтр("ru = 'Авансовые платежи за '")) + ПредставлениеПериодаАванса;
	
	ПоказыватьДанныеПериодаДоПревышенияЛимитов = ПрименяетсяПовышеннаяСтавкаНалога
		И КонецКвартала(НачалоНалоговогоПериода) <> КонецКвартала(Объект.Период)
		И КонецКвартала(НачалоНалоговогоПериода) <> КонецКвартала(ПериодПревышенияЛимитов);
	
	Элементы.НалоговаяБазаДоПревышенияЛимитов.Видимость = ПоказыватьДанныеПериодаДоПревышенияЛимитов;
	Элементы.НалоговаяБазаДоПревышенияЛимитов.Заголовок =
		СтрШаблон(НСтр("ru = 'Налоговая база за %1'"), ПредставлениеПериодаДоПревышенияЛимитов);
	
	Элементы.НалогДоПревышенияЛимитов.Видимость = ПоказыватьДанныеПериодаДоПревышенияЛимитов;
	Элементы.НалогДоПревышенияЛимитов.Заголовок =
		СтрШаблон(НСтр("ru = 'Налог за %1'"), ПредставлениеПериодаДоПревышенияЛимитов);
	
	Элементы.АнализУчетаПоУСН.Видимость = Не ДеятельностьОтсутствует;
	
	Элементы.ГруппаСтатусФоновогоЗадания.Видимость = ВыполняетсяФоновоеЗадание;
	
	Элементы.АктуализироватьДанныеУчета.Видимость = Не ДанныеУчетаАктуальны
		И Не ВозможнаБыстраяАктуализация   // Налог не может быть рассчитан автоматически.
		И Не ВыполняетсяФоновоеЗадание;    // Если расчет уже выполняется, кнопка скрывается.
	
	Элементы.АктуализироватьДанныеУчета.Доступность = МожноАктуализироватьДанные;
	
	Элементы.ГруппаПояснениеРасчета.Видимость = ДанныеУчетаАктуальны;
	
	Элементы.ГруппаРасчетыПрошлыхЛет.Видимость = Задолженность > 0 Или ТребуетсяВводНачальныхОстатков;
	Элементы.НалогКУплатеОтсутствует.Видимость = ДеятельностьОтсутствует;
	
	НастроитьПодвал();
	
	УстановитьКнопкуПоУмолчанию(ЭтотОбъект);
	
	Элементы.ПодробнееНаИТС.Видимость = ЗначениеЗаполнено(Объект.Период);
	
	ПрименяетсяОсвобождениеВТекущемПериоде = УчетУСН.ПрименяетсяОсвобождениеОтНалога(Объект.Организация, Объект.Период);
	Элементы.ОсвобождениеОтНалога.Видимость = Не ПрименяетсяОсвобождениеВТекущемПериоде И ОсвобождениеОтНалога <> 0;
	
	УправлениеФормойБлокУведомление();
	
	Если КонтекстныйВызов Тогда
		Элементы.ГруппаНастройки.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	УстановитьВидимостьБаннераВводОстатковСменаОбъектаУСН();
	
	
	// Блок ЕНС
	ВсеПлатежиИсполнены = ВсеПлатежиИсполнены();
	
	Если ИспользоватьОстатокЕНС Тогда
		СуммаКДоплатеНаЕНС = Макс(0, НалогКУплате - СуммаНалогаВПлатежныхДокументах(ПлатежиУСН) - СуммаСписанияСЕНС);
	Иначе
		СуммаКДоплатеНаЕНС = Макс(0, НалогКУплате - СуммаНалогаВПлатежныхДокументах(ПлатежиУСН));
	КонецЕсли;
	Элементы.СуммаСписанияСЕНС.Доступность = ИспользоватьОстатокЕНС;
	
	ВсеПлатежиПодготовлены = (СуммаКДоплатеНаЕНС <= 0);
	
	Если ПоказыватьКомандыОплаты Тогда
		ЕстьДокументыОплатыПоКассе = ЕстьДокументыОплатыПоКассе();
		ЕстьДокументыОплатыПоБанку = ЕстьДокументыОплатыПоБанку();
		
		ЕстьДокументыОплаты = (ЕстьДокументыОплатыПоКассе Или ЕстьДокументыОплатыПоБанку);
		
		ТребуетсяОтправитьДокументыОплатыПоБанку = ТребуетсяОтправитьДокументыОплатыПоДиректБанку();
		ТребуетсяВыгрузитьДокументыОплаты = Не ТребуетсяОтправитьДокументыОплатыПоБанку
			И ТребуетсяВыгрузитьДокументыОплаты();
		
		СпособОплаты = ВыполнениеЗадачБухгалтера.СпособУплатыНалогаВзноса(Объект.Организация);
		НадоВыбратьСпособОплаты = НЕ ЗначениеЗаполнено(СпособОплаты);
		
		Элементы.ОплатаПоКвитанцииЧтоДелатьДальше.Видимость = Не ИнтерфейсИнтеграцииСБанками И ЕстьДокументыОплатыПоКассе;
		
		Элементы.ОтправитьПлатежиВБанк.Видимость =  МожноСоздаватьДокументыУплаты И Не ДеятельностьОтсутствует
			И ТребуетсяОтправитьДокументыОплатыПоБанку И Не ИнтерфейсИнтеграцииСБанками;
		Элементы.ВыгрузитьПлатежи.Видимость =  МожноСоздаватьДокументыУплаты И Не ДеятельностьОтсутствует
			И ТребуетсяВыгрузитьДокументыОплаты И Не ИнтерфейсИнтеграцииСБанками;
		
		Элементы.ПояснениеУплатыНалога.Видимость = МожноСоздаватьДокументыУплаты И Не ДеятельностьОтсутствует
			И Не ВсеПлатежиПодготовлены;
		Элементы.ПомощникЕНС.ОтображатьЗаголовок = МожноСоздаватьДокументыУплаты И Не ДеятельностьОтсутствует
			И Не (ИнтерфейсИнтеграцииСБанками И ВсеПлатежиПодготовлены);
		
		Элементы.УплатитьНалогБанк.Видимость = МожноСоздаватьДокументыУплаты И Не ДеятельностьОтсутствует
			И (НадоВыбратьСпособОплаты Или СпособОплаты = Перечисления.СпособыУплатыНалогов.БанковскийПеревод)
			И Не ВсеПлатежиПодготовлены;
		
		Элементы.УплатитьНалогКасса.Видимость = МожноСоздаватьДокументыУплаты И Не ДеятельностьОтсутствует
			И (НадоВыбратьСпособОплаты Или СпособОплаты = Перечисления.СпособыУплатыНалогов.НаличнымиПоКвитанции)
			И Не ВсеПлатежиПодготовлены;
		
		Элементы.ГруппаИспользованиеОстаткаЕНС.Видимость = МожноСоздаватьДокументыУплаты И Не ДеятельностьОтсутствует;
		
		Элементы.ГруппаВсегоПополнитьНаЕНС.Видимость = МожноСоздаватьДокументыУплаты И Не ДеятельностьОтсутствует
			И Не ВсеПлатежиПодготовлены;
		
		Элементы.ГруппаПлатежУСН.Видимость = ЕстьДокументыОплаты;
		Элементы.ПомощникЕНС.Видимость = Не ВыполняетсяФоновоеЗадание
			И Не ЭтоПериодПрошлыхЛет
			И ДанныеУчетаАктуальны;
	Иначе
		Элементы.ПомощникЕНС.Видимость = Ложь;
	КонецЕсли;
	
	Если УпрощенныйЗачетВзносов Тогда
		Элементы.ГруппаВзносыПодлежащиеУплате.Видимость = Истина;
		Элементы.ПолеРучнаяКорректировка.Видимость = СуммаВзносаСкорректирована;
		Если ОбъектНалогообложенияДоходы Тогда
			Элементы.НадписьВзносы.Заголовок = НСтр("ru = 'Взносы, подлежащие уплате:'");
		Иначе
			Элементы.НадписьВзносы.Заголовок = НСтр("ru = ' в т.ч. взносы, подлежащие уплате:'");
		КонецЕсли;
	Иначе
		Элементы.ГруппаВзносыПодлежащиеУплате.Видимость = Ложь;
		Элементы.ПолеРучнаяКорректировка.Видимость = Ложь;
	КонецЕсли;
	
	// Скрываем данные на время расчета начальных показателей
	Элементы.ГруппаПоказатели.Видимость = НачальныеПоказателиРассчитаны;
	
	// Подсказка включения фиксированных взносов ИП в расходы за 2024 год
	Элементы.ГруппаФиксированныеВзносы2024.Видимость = ПоказыватьПодсказкуФиксированныеВзносы2024;
	
	УправлениеФормойМобильныйКлиент();
КонецПроцедуры

&НаСервере
Процедура НастроитьОснованиеЛьготнойСтавкиНалога()
	
	ПоказыватьОснованиеЛьготы = ПрименяласьЛьготнаяСтавкаНалога
		И УчетУСНКлиентСервер.ТребуетсяОснованиеЛьготнойСтавкиНалога(Объект.Период);
	
	ОшибкаЗаполненияОснованияЛьготы = НужноПодготовитьДекларацию(ЭтотОбъект)
		И ПоказыватьОснованиеЛьготы И Не ОснованиеЛьготнойСтавкиЗаполнено;
	
	Элементы.ОснованиеЛьготнойСтавки.Видимость = ПоказыватьОснованиеЛьготы;
	Элементы.ОснованиеЛьготнойСтавки.ЦветТекста = ?(ОшибкаЗаполненияОснованияЛьготы,
		ЦветаСтиля.НезаполненныйРеквизит, Новый Цвет);
	
	Если ПоказыватьОснованиеЛьготы И ПрименяетсяПовышеннаяСтавкаНалога Тогда
		Элементы.ОснованиеЛьготнойСтавки.Заголовок = СтрШаблон(
			НСтр("ru = 'Основание льготной ставки за %1'"),
			ПредставлениеПериодаДоПревышенияЛимитов);
	Иначе
		Элементы.ОснованиеЛьготнойСтавки.Заголовок = НСтр("ru = 'Основание льготной ставки'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПодвал()
	
	ПодвалВиден = ПустаяСтрока(СообщениеОбОшибке)
		И (ЭтоПериодПрошлыхЛет Или ДанныеУчетаАктуальны) И НачальныеПоказателиРассчитаны;
	
	Элементы.ГруппаПодвал.Видимость = ПодвалВиден;
	
	Если ПодвалВиден Тогда
		
		НужноПодготовитьДекларацию = НужноПодготовитьДекларацию(ЭтотОбъект);
		
		Элементы.СообщениеТребуютсяРеквизиты.Видимость = НЕ РеквизитыОрганизацииЗаполнены
			И ((НужноПодготовитьДекларацию И НЕ ЕстьДекларация) // Нужна декларация, или
				ИЛИ НЕ ДеятельностьОтсутствует);                // ненулевой налог к уплате.
		
		Элементы.ДекларацияПояснение.Видимость     = НужноПодготовитьДекларацию И Не ЭтоПериодПрошлыхЛет;
		Элементы.ДекларацияПредставление.Видимость = НужноПодготовитьДекларацию И ЕстьДекларация;
		
		Элементы.СформироватьДекларацию.Видимость  = НужноПодготовитьДекларацию И НЕ ЕстьДекларация;
		Элементы.СформироватьДекларацию.Доступность = МожноСоздаватьДекларацию;
		
		Элементы.ДекларацияУжеСдана.Видимость = ЭтоПериодПрошлыхЛет И Не ЕстьДекларация;
		
		Элементы.ГиперссылкаКУДиР.Видимость = НужноПодготовитьДекларацию И Не ЭтоПериодПрошлыхЛет;
		
		Элементы.ОшибкаНесоответствиеСуммДекларация.Видимость = Не ПустаяСтрока(ОшибкаНесоответствиеСуммДекларация);
		Элементы.ОшибкаНесоответствиеСуммОплата.Видимость     = Не ПустаяСтрока(ОшибкаНесоответствиеСуммОплата);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПодсказкиРасширенныйНалоговыйПериод()
	
	Если НалоговыйПериодПропущен ИЛИ НалоговыйПериодРасширен Тогда
		
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Заголовок = ПодсказкаПереносНалоговогоПериода;
		
	Иначе
		
		Элементы.ПояснениеРасширенныйНалоговыйПериод.Заголовок = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВыполняетсяФоновоеЗадание(Форма)
	
	Возврат Форма.ТребуетсяПроверятьАктуальность
		Или ЗначениеЗаполнено(Форма.ИдентификаторЗаданияАктуализации)
		Или ЗначениеЗаполнено(Форма.ИдентификаторЗаданияПроверкиАктуализации)
		Или Не Форма.НачальныеПоказателиРассчитаны;
		
КонецФункции

&НаКлиенте
Функция ОповещениеПолученоИзОсновнойФормыПомощника(ИсточникОповещения)
	
	// Обход "паразитной обратной связи" между двумя открытыми одновременно формами помощника.
	// Если расчет налога выполняется "на лету", оповещение о завершении актуализации в одной форме
	// порождает проверку актуальности и выполнение актуализации в другой форме,
	// что приводит к генерации нового оповещения - процесс зацикливается.
	
	Если ТипЗнч(ИсточникОповещения) <> Тип("ФормаКлиентскогоПриложения") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ИсточникОповещения.ИмяФормы = ЭтотОбъект.ИмяФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКнопкуПоУмолчанию(Форма)
	
	Элементы = Форма.Элементы;
	
	КнопкаПоУмолчаниюАктуализировать = 
		Не Форма.ДанныеУчетаАктуальны 
		И Не Форма.ЭтоПериодПрошлыхЛет;
	
	КнопкаПоУмолчаниюДекларация =
		НЕ КнопкаПоУмолчаниюАктуализировать
		И Форма.РеквизитыОрганизацииЗаполнены
		И НужноПодготовитьДекларацию(Форма)
		И НЕ Форма.ЕстьДекларация;
	
	КнопкаПоУмолчаниюУведомление =
		НЕ КнопкаПоУмолчаниюАктуализировать
		И НЕ КнопкаПоУмолчаниюДекларация
		И Форма.ПлательщикЕНП
		И Форма.РеквизитыОрганизацииЗаполнены
		И НЕ Форма.ДеятельностьОтсутствует
		И НЕ Форма.УведомлениеОбИсчисленныхНалогахПодготовлено;
		
	НеОтправленныеПлатежи = НеОтправленныеПлатежи(Форма);
	
	КнопкаПоУмолчаниюОтправитьВБанк = 
		НЕ КнопкаПоУмолчаниюАктуализировать
		И НЕ КнопкаПоУмолчаниюДекларация
		И НЕ КнопкаПоУмолчаниюУведомление
		И Форма.РеквизитыОрганизацииЗаполнены
		И НЕ Форма.ДеятельностьОтсутствует
		И НЕ Форма.ИнтерфейсИнтеграцииСБанками
		И ЗначениеЗаполнено(НеОтправленныеПлатежи);
	
	КнопкаПоУмолчаниюУплата =
		НЕ КнопкаПоУмолчаниюАктуализировать
		И НЕ КнопкаПоУмолчаниюДекларация
		И НЕ КнопкаПоУмолчаниюУведомление
		И НЕ КнопкаПоУмолчаниюОтправитьВБанк
		И Форма.РеквизитыОрганизацииЗаполнены
		И НЕ Форма.ДеятельностьОтсутствует
		И НЕ Форма.НалогОплаченПолностью;
	
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.АктуализироватьДанныеУчета, КнопкаПоУмолчаниюАктуализировать);
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.СформироватьДекларацию, КнопкаПоУмолчаниюДекларация);
	
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.ВыполнитьДействиеСформироватьУведомление,  КнопкаПоУмолчаниюУведомление);
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.ВыполнитьДействиеСформироватьОперациюПоЕНС, КнопкаПоУмолчаниюУведомление);
	
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.УплатитьНалогБанк,  КнопкаПоУмолчаниюУплата);
	
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.ОтправитьПлатежиВБанк, КнопкаПоУмолчаниюОтправитьВБанк);
	УстановитьВидПоУмолчаниюОформлением(Форма, Элементы.ВыгрузитьПлатежи, КнопкаПоУмолчаниюОтправитьВБанк);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидПоУмолчаниюОформлением(Форма, Элемент, ЭтоКнопкаПоУмолчанию)
	
	ПолужирныйШрифт = ЭтоКнопкаПоУмолчанию;
	Шрифт = Новый Шрифт(Элемент.Шрифт, , , ПолужирныйШрифт);
	
	Элемент.Шрифт    = Шрифт;
	Элемент.ЦветФона = ?(ЭтоКнопкаПоУмолчанию, Форма.ЦветПодсветки, Новый Цвет);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НужноПодготовитьДекларацию(Форма)
	
	// Декларацию нужно готовить только в помощнике за год,
	// при этом не должно быть ошибок, препятствующих подготовке декларации.
	// Например, ошибкой может быть то, что организация не является плательщиком УСН.
	Возврат (Форма.НомерКвартала = 4) И НетОшибокБлокирующихРасчетНалога(Форма);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьНачальныеПоказатели(Показатели)
	
	НачальныеПоказателиРассчитаны = Истина;
	
	ДатаРегистрацииОрганизации = Справочники.Организации.ДатаРегистрацииОрганизации(Объект.Организация);
	
	МинимальныйПериод = НачалоКвартала(ДатаРегистрацииОрганизации);
	Если Объект.Период < МинимальныйПериод Тогда
		// Если при начальном заполнении по рабочей дате, восстановлении из настроек или при смене организации
		// период оказался в запрещенном интервале, нужно сдвинуть его вперед до ближайшего доступного.
		Объект.Период = МинимальныйПериод;
		Если Параметры.Свойство("Заголовок") Тогда
			Параметры.Заголовок = "";
		КонецЕсли;
		УстановитьЗаголовок(ЭтотОбъект);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Показатели);
	
	ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС();
	
	УстановитьПодсказкиРасширенныйНалоговыйПериод();
	
	ЗаполнитьПравилоИСрокЗадачи();
	
	ПроверитьРеквизитыОрганизацииДляОтчетности();
	
	ПроверитьВедениеДеятельности();
	
	ЗаполнитьОплатуНалогаИОтобразитьНаФорме();
	
	ЗаполнитьУведомленияИОтобразитьНаФорме();
	
	УстановитьПараметрыПрошлыхЛет();
	
	РассчитатьСуммуЕНС();
	
	ЗаполнитьСсылкуНаЛК_ФНС();
	
	ПоказыватьПодсказкуФиксированныеВзносы2024 =
		ПоказыватьПодсказкуФиксированныеВзносы2024(Объект.Организация, Объект.Период, ОбъектНалогообложенияДоходы);
		
	УправлениеФормойНаСервере();
	
КонецПроцедуры

&НаСервере
Функция УстановитьПараметрыПрошлыхЛет()
	
	ГраницаОтчетности = ПомощникиПоУплатеНалоговИВзносов.ГраницаОтчетностиПрошлыхПериодов(Объект.Организация);
	Если ЗначениеЗаполнено(ГраницаОтчетности)
		И Не НалоговыйПериодПропущен Тогда
		ЭтоПериодПрошлыхЛет = Объект.Период <= ГраницаОтчетности;
	Иначе
		ЭтоПериодПрошлыхЛет = Ложь;
	КонецЕсли;
	
	Если ЭтоПериодПрошлыхЛет Тогда
		
		НачалоВеденияУчета = ГраницаОтчетности + 1;
		Если НачалоВеденияУчета = НачалоГода(НачалоВеденияУчета) Тогда
			ОписаниеПериодаНачалаУчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 года'"), Формат(Год(НачалоВеденияУчета), "ЧЦ=4; ЧГ="));
		Иначе
			ОписаниеПериодаНачалаУчета = Формат(НачалоВеденияУчета, "ДЛФ=D");
		КонецЕсли;
		
		Элементы.ОписаниеПериодаПрошлыхЛет.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Заполнить декларацию по данным программы невозможно, так как учет ведется, начиная с %1.
			|Вы можете сформировать декларацию и заполнить ее самостоятельно.'"),
			ОписаниеПериодаНачалаУчета);
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьАктуальныеПоказателиИПояснения()
	
	Результат = Обработки.ПомощникРасчетаНалогаУСН.АктуальныеПоказателиРасчетаСПояснениями(
		Объект.Организация, Объект.Период);
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат.Показатели, , "ПодсказкаПоНалогу"); // Подсказка берется не из актуальных показателей.
	
	ОтобразитьРасчетыЗаПредыдущиеПериоды();
	
	ЗагрузитьПоясненияКРасчету(Результат.Пояснения);
	
	ЗаполнитьОплатуНалогаИОтобразитьНаФорме();
	
	ЗаполнитьУведомленияИОтобразитьНаФорме();
	
	ТребуетсяПроверятьАктуальность = Ложь;
	
	СуммаСписанияСЕНСПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеДействияДекларация(Знач Организация, Знач Период, ТекстОшибки = "")
	
	ОписаниеДействияДекларация = Обработки.ПомощникРасчетаНалогаУСН.ОписаниеДействияДекларация(
		Организация, Период, ТекстОшибки);
	
	Возврат ОписаниеДействияДекларация;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеУчетнойПолитики(ИмяРегистра, Организация, Период)
	
	Возврат НастройкиУчета.КлючЗаписиДействующейУчетнойПолитики(ИмяРегистра, Организация, Период);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверкаКлючаЗаписиНастройкиУСН(Ключ, Период)
	
	Если Год(Ключ.Период) <> Год(Период)
		И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиУчетаУСН) Тогда
		Возврат РегистрыСведений.НастройкиУчетаУСН.ПустойКлюч();
	Иначе
		Возврат Ключ;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПоясненияКРасчету(Пояснения)
	
	ШрифтАвто = Новый Шрифт;
	
	Для НомерПояснения = 1 По Пояснения.Количество() Цикл
		
		Этап = Пояснения[НомерПояснения - 1];
		
		Элементы["ГруппаЭтап" + НомерПояснения].Видимость = Истина;
		
		Элементы["Этап" + НомерПояснения + "НомерЭтапа"].Заголовок = Этап.НомерЭтапа;
		Элементы["Этап" + НомерПояснения].Заголовок = Этап.Пояснение;
		Элементы["Этап" + НомерПояснения + "Значение"].Заголовок = Этап.Значение;
		
		Элементы["Этап" + НомерПояснения + "Значение"].Шрифт = ШрифтАвто;
		
	КонецЦикла;
	
	ПоследнийЭтап = Пояснения.Количество();
	Если ПоследнийЭтап > 0 Тогда
		Элементы["Этап" + ПоследнийЭтап + "Значение"].Шрифт = Новый Шрифт("ШрифтДиалоговИМеню", , Истина);
	КонецЕсли;
	
	КоличествоЭтапов = Элементы.ГруппаПояснениеРасчета.ПодчиненныеЭлементы.Количество();
	Для НомерПояснения = Пояснения.Количество() + 1 По КоличествоЭтапов Цикл
		
		Элементы["ГруппаЭтап" + НомерПояснения].Видимость = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПравилоИСрокЗадачи()
	
	Если Не ЗначениеЗаполнено(Объект.Организация)
		ИЛИ Не ЗначениеЗаполнено(Объект.Период) Тогда 
		Срок = '00010101';
		Возврат
	КонецЕсли;
	
	ПорядокОтчетаУплаты = Обработки.ПомощникРасчетаНалогаУСН.ПорядокОтчетаУплатыНалога(Объект.Организация, Объект.Период);
	Налог = ПорядокОтчетаУплаты.Налог;
	Срок = ПорядокОтчетаУплаты.СрокОплаты;
	Правило = ПорядокОтчетаУплаты.ПравилоУплаты;
	СрокОтчета = ПорядокОтчетаУплаты.СрокОтчета;
	
	ПериодСобытия = НачалоДня(Объект.Период);
	
	ПодсказкаПоНалогу = Обработки.ПомощникРасчетаНалогаУСН.ПодсказкаПоНалогу(НомерКвартала, Срок, СрокОтчета);
	
	ОпределитьСвязанныеПравила();
	
	ПодаетсяУведомлениеПоНалогуЗаПериод = ?(ПериодСобытия >= НастройкиУчетаКлиентСервер.ДатаПереходаНаЕдиныйНалоговыйПлатеж(),
		ЗначениеЗаполнено(ПравилоУведомления),
		ЗначениеЗаполнено(ПравилоУведомления) И ПлательщикЕНП_ПереходныйПериод);
	
	Если ПодаетсяУведомлениеПоНалогуЗаПериод Тогда
		СрокПодачиУведомления = ВыполнениеЗадачБухгалтера.СрокВыполненияЗадачи(Объект.Организация, ПравилоУведомления, КонецКвартала(Объект.Период));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСсылкуНаЛК_ФНС()
	Если ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Объект.Организация) Тогда
		СсылкаЛК = "https://lkul.nalog.ru/";
	Иначе
		СсылкаЛК = "https://lkip2.nalog.ru/";
	КонецЕсли;
	
	Элементы.ПояснениеУплатыНалога.Заголовок = Новый ФорматированнаяСтрока(
		НСтр("ru='Перед оплатой сверьте сумму остатка с данными '"),
		Новый ФорматированнаяСтрока(НСтр("ru='Личного кабинета налогоплательщика.'"), , , , СсылкаЛК));
КонецПроцедуры

&НаСервере
Процедура ПроверитьРеквизитыОрганизацииДляОтчетности()
	
	Если ЭтоАдресВременногоХранилища(АдресХранилищаНезаполненныхРеквизитов) Тогда
		УдалитьИзВременногоХранилища(АдресХранилищаНезаполненныхРеквизитов);
		АдресХранилищаНезаполненныхРеквизитов = "";
	КонецЕсли;
	
	Если НомерКвартала = 4 И ЕстьДекларация Тогда
		// Если декларация уже подготовлена, реквизиты заведомо заполнены и проверять их не требуется.
		РеквизитыОрганизацииЗаполнены = Истина;
		Возврат;
	КонецЕсли;
	
	НезаполненныеРеквизиты = Неопределено;
	
	РеквизитыОрганизацииЗаполнены = Обработки.ПомощникРасчетаНалогаУСН.РеквизитыДляОтчетностиЗаполнены(
		Объект.Организация,
		Объект.Период,
		НезаполненныеРеквизиты);
		
	Если НЕ РеквизитыОрганизацииЗаполнены Тогда
		АдресХранилищаНезаполненныхРеквизитов = ПоместитьВоВременноеХранилище(НезаполненныеРеквизиты, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВедениеДеятельности()
	
	ДеятельностьОтсутствует = (Доходы = 0 И АвансовыеПлатежи = 0)
		И НЕ ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности();
	
	Если ДеятельностьОтсутствует Тогда
		ДанныеУчетаАктуальны = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасшифровкуТорговогоСбораУменьшающегоНалог()
	
	НастройкиОтчета = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	НастройкиОтчета.ДополнительныеСвойства.Вставить("Организация", Объект.Организация);
	НастройкиОтчета.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоКвартала(Объект.Период));
	НастройкиОтчета.ДополнительныеСвойства.Вставить("КонецПериода", КонецКвартала(Объект.Период));
	НастройкиОтчета.ДополнительныеСвойства.Вставить("КлючВарианта", "РасчетТорговогоСбораУменьшающегоНалог");
	
	ПараметрыОтчета = Новый Структура;
	
	ПараметрыОтчета.Вставить("ВидРасшифровки", 2);
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", НастройкиОтчета);
	
	ОткрытьФорму("Отчет.СправкаРасчетНалогаУСН.Форма.ФормаОтчета", ПараметрыОтчета, ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасшифровкуРасчетовПрошлыхЛет()
	
	ДатаПрошлогоГода = НачалоГода(Объект.Период) - 1;
	
	ОписаниеДействия = ОписаниеДействияРасшифровкаРасчетовПрошлыхЛет(Объект.Организация, ДатаПрошлогоГода, Правило, ПлательщикЕНП);
	
	ОткрытьФорму(ОписаниеДействия.ИмяФормы, ОписаниеДействия.ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеДействияРасшифровкаРасчетовПрошлыхЛет(Организация, Период, Правило, ПлательщикЕНП)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",   Организация);
	ПараметрыФормы.Вставить("Период",        Период);
	ПараметрыФормы.Вставить("Правило",       Правило);
	ПараметрыФормы.Вставить("ПлательщикЕНП", ПлательщикЕНП);
	
	ОписаниеДействия = Новый Структура;
	ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
	
	ОписаниеДействия.Вставить("ИмяФормы", ИмяФормыДляРасшифровкиРасчетовПрошлыхЛет(Организация, Период));
	
	Возврат ОписаниеДействия;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяФормыДляРасшифровкиРасчетовПрошлыхЛет(Организация, Период)
	
	// Для оплаты долга и расшифровки переплаты по налогу за период до начала работы в программе
	// используем помощник уплаты налога прошлых лет.
	// Исключение - ситуация, когда переплата по налогу прошлых лет образована не отраженными в программе платежами,
	// а указана в начальных остатках (сальдо счета 68.12 дебетовое).
	// Ситуация достаточно редкая, ее помощник оплаты налога прошлых лет отображать не умеет.
	
	// В остальных случаях показываем стандартную расшифровку расчетов по налогу за прошлые периоды.
	// Дебетовый начальный остаток по налогу (входящая переплата) будет отражен в сумме налога за прошлые периоды со знаком "минус".
	
	Если ТребуетсяПоказатьОплатуНалогаЗаПрошлыеПериоды(Организация, Период) Тогда
		Возврат "Обработка.ПомощникУплатыНалоговВзносовПрошлыхЛет.Форма.ФормаОплатыЗаПрошлыеПериоды";
	Иначе
		Возврат "Обработка.ПомощникРасчетаНалогаУСН.Форма.УплатаНалогаПрошлыхЛет";
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТребуетсяПоказатьОплатуНалогаЗаПрошлыеПериоды(Организация, НалоговыйПериод)
	
	ГраницаОтчетностиПрошлыхПериодов = ПомощникиПоУплатеНалоговИВзносов.ГраницаОтчетностиПрошлыхПериодов(Организация);
	
	Возврат ЗначениеЗаполнено(ГраницаОтчетностиПрошлыхПериодов)
		И Год(ГраницаОтчетностиПрошлыхПериодов) = Год(НалоговыйПериод)
		И УчетУСН.ОстатокРасчетовПоНалогуУСН(Организация, КонецГода(НалоговыйПериод)) <= 0;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоРегламентированныйОтчетУСН(Знач Ссылка, ИмяРегламентированногоОтчета)
	
	Если Не ЗначениеЗаполнено(Ссылка) Или ТипЗнч(Ссылка) <> Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИсточникОтчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ИсточникОтчета");
	
	Возврат ИсточникОтчета = ИмяРегламентированногоОтчета;
	
КонецФункции

&НаКлиенте
Функция ПроверитьЗаполнениеПередСозданиемДекларации()
	
	Результат = Истина;
	
	Если Не РеквизитыОрганизацииЗаполнены Тогда
		
		ОписаниеОбъектаПроверки = НСтр("ru = 'отчета'");
		
		СообщитьОбОшибкеЗаполненияРеквизитовОрганизации(
			АдресХранилищаНезаполненныхРеквизитов,
			Объект.Организация,
			ОписаниеОбъектаПроверки);
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Если УчетУСНКлиентСервер.ТребуетсяОснованиеЛьготнойСтавкиНалога(Объект.Период)
		И ПрименяласьЛьготнаяСтавкаНалога И Не ОснованиеЛьготнойСтавкиЗаполнено Тогда
		
		ТекстСообщения = НСтр("ru = 'Не заполнено основание для применения льготной ставки налога'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ОснованиеЛьготнойСтавкиПредставление");
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПроверитьЗаполнениеРеквизитов()
	
	Если Не РеквизитыОрганизацииЗаполнены Тогда
		
		ОписаниеОбъектаПроверки = НСтр("ru = 'оплаты налога'");
		
		СообщитьОбОшибкеЗаполненияРеквизитовОрганизации(
			АдресХранилищаНезаполненныхРеквизитов,
			Объект.Организация,
			ОписаниеОбъектаПроверки);
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СообщитьОбОшибкеЗаполненияРеквизитовОрганизации(АдресХранилищаНезаполненныхРеквизитов, Организация, ОписаниеОбъектаПроверки)
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресХранилищаНезаполненныхРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	НезаполненныеРеквизиты = ПолучитьИзВременногоХранилища(АдресХранилищаНезаполненныхРеквизитов);
	
	ПроверкаРеквизитовОрганизации.СообщитьОбОшибкеЗаполненияРеквизитовДляОтчетности(
		Организация,
		НезаполненныеРеквизиты,
		"СообщениеТребуютсяРеквизиты",
		Истина,
		ОписаниеОбъектаПроверки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоУплатаНалогаУСН(Налог)
	
	ВидыНалоговУСН = ПлатежиВБюджетКлиентСерверПереопределяемый.ВидыНалоговУСН();
	ЭтоЕдиныйНалоговыйПлатеж = ПлатежиВБюджетКлиентСерверПереопределяемый.ЭтоЕдиныйНалоговыйПлатеж(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Налог, "ВидНалога"));
	
	Возврат ВидыНалоговУСН.Найти(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Налог, "ВидНалога")) <> Неопределено
		ИЛИ ЭтоЕдиныйНалоговыйПлатеж;
	
КонецФункции

&НаКлиенте
Функция ОбновитьСтатусФоновогоЗадания()
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияАктуализации) Тогда
		
		Прогресс = ЗакрытиеМесяцаВызовСервера.ПрочитатьПрогресс(ИдентификаторЗаданияАктуализации);
		Если ТипЗнч(Прогресс) = Тип("Структура") И Прогресс.Свойство("Процент") И Прогресс.ПрогрессОбновлен Тогда
			ПроцентВыполнения = Формат(Мин(Прогресс.Процент, 99), "ЧЦ=2; ЧДЦ=; ЧВН=; ЧГ=0") + "%";
		ИначеЕсли Прав(СтатусФоновогоЗадания, 1) = "%" Тогда
			ПроцентВыполнения = Прав(СтатусФоновогоЗадания, 3);
		Иначе
			ПроцентВыполнения = "";
		КонецЕсли;
		
		СтатусФоновогоЗадания = НСтр("ru = 'Выполняется расчет налога...'") + ПроцентВыполнения;
		
	ИначеЕсли ТребуетсяПроверятьАктуальность Или ЗначениеЗаполнено(ИдентификаторЗаданияПроверкиАктуализации) Тогда
		
		СтатусФоновогоЗадания = НСтр("ru = 'Выполняется проверка данных...'");
		
	Иначе
		
		СтатусФоновогоЗадания = "";
		
	КонецЕсли;
	
	ТребуетсяПроверятьАктуальность = Ложь;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДанныеФормы()
	
	ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей();
	
	// Обновление открытых форм расшифровок
	Оповестить("ОбновитьПоказателиРасчетаУСН", , ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСправкуРасчетУСН(ОтчетныйПериод, ОткрытьВПериодеОсвобожденияОтНалогов)
	
	НастройкиОтчета = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	НастройкиОтчета.ДополнительныеСвойства.Вставить("Организация", Объект.Организация);
	НастройкиОтчета.ДополнительныеСвойства.Вставить("НачалоПериода", НачалоКвартала(ОтчетныйПериод));
	НастройкиОтчета.ДополнительныеСвойства.Вставить("КонецПериода", КонецКвартала(ОтчетныйПериод));
	НастройкиОтчета.ДополнительныеСвойства.Вставить("КлючВарианта", "РасчетНалогаУСН");
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", НастройкиОтчета);
	ПараметрыФормы.Вставить("ОткрытьВПериодеОсвобожденияОтНалогов", ОткрытьВПериодеОсвобожденияОтНалогов);
	
	ОткрытьФорму("Отчет.СправкаРасчетНалогаУСН.Форма.ФормаОтчета", ПараметрыФормы, ЭтотОбъект, Истина);
	
КонецПроцедуры

#Область УплатаНалога

&НаСервере
Процедура ЗаполнитьОплатуНалогаИОтобразитьНаФорме()
	
	Платежи = Обработки.ПомощникРасчетаНалогаУСН.ДокументыУплатыНалогов(Объект.Организация, Объект.Период, Правило);
	ПлатежиУСН.Загрузить(Платежи);
	
	ВывестиПлатежиИБаннерСостоянияОтправкиНаФорму();
	
	Обработки.ПомощникРасчетаНалогаУСН.УстановитьСтатусЗадачи(
		Объект.Организация,
		Правило,
		Объект.Период,
		ДанныеУчетаАктуальны,
		НалогКУплате,
		ПлатежиУСН);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУведомленияИОтобразитьНаФорме()
	
	МассивПравилУведомлений = Новый Массив;
	МассивПравилУведомлений.Добавить(Правило);
	МассивПравилУведомлений.Добавить(ВыполнениеЗадачБухгалтера.ПравилоУведомленияПоПравилуУплаты(Правило, Объект.Организация, ПериодСобытия));
	МассивПравилУведомлений.Добавить(ВыполнениеЗадачБухгалтера.ПравилоУведомленияПоПравилуОтчета(Правило, Объект.Организация, ПериодСобытия));
	МассивПравилУведомлений.Добавить(ВыполнениеЗадачБухгалтера.ПравилоУплатыПоПравилуОтчета(Правило, Объект.Организация, ПериодСобытия));
	МассивПравилУведомлений.Добавить(ВыполнениеЗадачБухгалтера.ПравилоОтчетаПоПравилуУплаты(Правило, Объект.Организация, ПериодСобытия));
	
	ТаблицаУведомления = ВыполнениеЗадачБухгалтера.ДанныеДляУведомлений(
		МассивПравилУведомлений,
		Объект.Организация,
		ПериодСобытия);
	
	УведомленияОбИсчисленныхНалогах.Загрузить(ТаблицаУведомления);
	
	УведомлениеОбИсчисленныхНалогахПодготовлено = УведомленияОбИсчисленныхНалогах.Количество() > 0;
	
	УправлениеФормойБлокУведомление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРедактированиеУведомления(РезультатЗакрытия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЗаполнитьУведомленияИОтобразитьНаФорме();
	УстановитьКнопкуПоУмолчанию(ЭтотОбъект);
	
	УправлениеФормойНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВывестиПлатежиИБаннерСостоянияОтправкиНаФорму()
	
	СостоянияИнтеграцииДокументов = РегистрыСведений.ДокументыИнтеграцииСБанком.СостоянияИнтеграцииДокументов(
		ДокументыОплаты(ЭтотОбъект));
	
	ПлатежиДляОтображения = ПомощникиПоУплатеНалоговИВзносов.ПлатежиДляОтображения(
		ЭтотОбъект.ПлатежиУСН, СостоянияИнтеграцииДокументов);
	ПомощникиПоУплатеНалоговИВзносов.ОтобразитьПлатежи(ЭтотОбъект, ПлатежиДляОтображения, "ПлатежУСН");
	
	ИнтеграцияСБанкамиФормы.ПолучитьДанныеИПоказатьБаннерСостоянияОтправки(ЭтотОбъект, СостоянияИнтеграцииДокументов);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРасчетыЗаПредыдущиеПериоды()
	
	СообщениеЗадолженностьЗаПредыдущийПериод = Обработки.ПомощникРасчетаНалогаУСН.ТекстСообщенияРасчетыПрошлыхЛет(
		Задолженность, ГраницаОтчетностиПрошлыхПериодов, ТребуетсяВводНачальныхОстатков);
	
	Элементы.ТекстРасчетыПрошлыхЛет.Заголовок = СообщениеЗадолженностьЗаПредыдущийПериод;
	
	// Оформление баннера расчетов.
	
	Если Задолженность > 0 Тогда
		
		Элементы.ФонРасчетыПрошлыхЛет.ЦветФона = ЦветаСтиля.ЦветФонаПредупреждения;
		
	ИначеЕсли ТребуетсяВводНачальныхОстатков Тогда
		
		Элементы.ФонРасчетыПрошлыхЛет.ЦветФона = ЦветаСтиля.БыстрыеОтборыФонГруппы;
		
	Иначе
		
		// Баннер будет скрыт в УправлениеФормой().
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеДействияДляНовогоДокументаУплатыНалога(Организация, ПлательщикЕНП, Сумма, СпособУплатыНалога, ПериодСобытия, Правило)
	
	СтруктураРеквизитовУплатыНалога = Обработки.ПомощникРасчетаНалогаУСН.СтруктураРеквизитовУплатыНалога();
	СтруктураРеквизитовУплатыНалога.Организация         = Организация;
	СтруктураРеквизитовУплатыНалога.ПлательщикЕНП       = ПлательщикЕНП;
	СтруктураРеквизитовУплатыНалога.Сумма               = Сумма;
	СтруктураРеквизитовУплатыНалога.СпособУплатыНалога  = СпособУплатыНалога;
	СтруктураРеквизитовУплатыНалога.ПериодСобытия       = ПериодСобытия;
	СтруктураРеквизитовУплатыНалога.ОплатаЗадолженности = Ложь;
	СтруктураРеквизитовУплатыНалога.Правило             = Правило;
	СтруктураРеквизитовУплатыНалога.ВидНалога           =
		Обработки.ПомощникРасчетаНалогаУСН.ВидНалога(Организация, ПериодСобытия);
	
	Возврат Обработки.ПомощникРасчетаНалогаУСН.ОписаниеДействияУплатаНалога(СтруктураРеквизитовУплатыНалога);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыВыполненияЗадачиПодготовкиДекларации(Организация, Период)
	
	Порядок = Обработки.ПомощникРасчетаНалогаУСН.ПорядокПредоставленияОтчетаУплатыНалогаЗаПериод(
		Организация, Период);
	
	Если Не ЗначениеЗаполнено(Порядок)
		Или Не Порядок.Свойство("Отчет")
		Или Не ЗначениеЗаполнено(Порядок.Отчет) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗадачи = Новый Структура;
	ПараметрыЗадачи.Вставить("Организация", Организация);
	ПараметрыЗадачи.Вставить("Правило", Порядок.Отчет.Правило);
	ПараметрыЗадачи.Вставить("ПериодСобытия", Порядок.Отчет.ПериодСобытия);
	ПараметрыЗадачи.Вставить("РегистрацияВНалоговомОргане", Порядок.Отчет.РегистрацияВНалоговомОргане);
	ПараметрыЗадачи.Вставить("Действие", Порядок.Отчет.Действие);
	
	Возврат ПараметрыЗадачи;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СуммаНалогаВПлатежныхДокументах(ПлатежиУСН)
	
	Возврат ПлатежиУСН.Итог("Сумма");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовок(Форма)
	
	Если НЕ Форма.КонтекстныйВызов Тогда
		// Помощник открыт из командного меню - отображается заголовок по умолчанию.
		Форма.Заголовок = НСтр("ru = 'Отчетность по УСН'");
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	ЧастиЗаголовка = Новый Массив;
	
	ЗаголовокИзПараметров = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Форма.Параметры, "Заголовок");
	
	Если ЗначениеЗаполнено(ЗаголовокИзПараметров) Тогда
		ЧастиЗаголовка.Добавить(ЗаголовокИзПараметров);
	Иначе
		Если КонецГода(Объект.Период) = КонецКвартала(Объект.Период) Тогда
			ШаблонЗаголовка = НСтр("ru = 'УСН, уплата за %1'");
		Иначе
			ШаблонЗаголовка = НСтр("ru = 'УСН, авансовый платеж за %1'");
		КонецЕсли;
		
		ПредставлениеПериода = НРег(ПредставлениеПериода(НачалоГода(Объект.Период), КонецКвартала(Объект.Период), "ФП=Истина"));
		
		ЧастиЗаголовка.Добавить(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, ПредставлениеПериода));
	КонецЕсли;
	
	ЧастиЗаголовка.Добавить(БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Объект.Организация));
	
	Форма.Заголовок = СтрСоединить(ЧастиЗаголовка, " ");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКнопокУведомления()
	
	ДоступностьУведомлений = МожноСоздаватьУведомления
		И НалогДляУведомления <> 0;
		
	Элементы.ВыполнитьДействиеСформироватьУведомление.Видимость = ПодаетсяУведомлениеПоНалогуЗаПериод;
	Элементы.ВыполнитьДействиеСформироватьОперациюПоЕНС.Видимость = Не ПодаетсяУведомлениеПоНалогуЗаПериод;
	Элементы.ВыполнитьДействиеСформироватьУведомление.Доступность = ДоступностьУведомлений;
	Элементы.ВыполнитьДействиеСформироватьОперациюПоЕНС.Доступность = ДоступностьУведомлений;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ХэшДанныхБаннера(Знач ДокументыОплаты)
	
	Возврат ИнтеграцияСБанкамиФормы.ХэшДанныхБаннера(ДокументыОплаты);
	
КонецФункции

&НаКлиенте
Процедура ПередУплатойНалога()
	
	Если НЕ ПроверитьЗаполнениеРеквизитов() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПлательщикЕНП И Не УведомлениеОбИсчисленныхНалогахПодготовлено И НалогКУплате > 0 Тогда
		ПредупредитьОбОтсутствииУведомления();
	Иначе
		СоздатьДокументУплатыНалога();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументУплатыНалога()
	
	СуммаПлатежа = Объект.СуммаЕНП;
	
	ОписаниеДействияПлатеж = ОписаниеДействияДляНовогоДокументаУплатыНалога(Объект.Организация, ПлательщикЕНП,
		СуммаПлатежа, СпособУплатыНалога, Объект.Период, Правило);
	
	Если ОписаниеДействияПлатеж <> Неопределено Тогда
		
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействияПлатеж);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОповещениеУдаленияПлатежногоДокумента()
	Возврат Новый ОписаниеОповещения("УдалитьДокументУплатыНаКлиентеЗавершение", ЭтотОбъект);
КонецФункции

&НаКлиенте
Процедура УдалитьДокументУплатыНаКлиентеЗавершение(ДокументУплатыДляУдаления, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДокументУплатыДляУдаления) Тогда
		УдалитьДокументУплаты(ДокументУплатыДляУдаления);
		ЗапуститьОбновлениеБаннераСостоянияОтправки();
		УправлениеФормойНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДокументУплаты(ДокументУплатыДляУдаления)
	
	ДокументУплатыОбъект = ДокументУплатыДляУдаления.ПолучитьОбъект();
	ДокументУплатыОбъект.УстановитьПометкуУдаления(Истина);
	ЗаполнитьАктуальныеПоказателиИПояснения();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредставлениеПериода(ПредставлениеПериода, Период)
	
	ПредставлениеПериода = НРег(ПредставлениеПериода(НачалоГода(Период), КонецКвартала(Период), "ФП=Истина"));
	
КонецПроцедуры

#КонецОбласти

#Область ВыборПериода

&НаКлиенте
Процедура ПредставлениеПериодаПриИзменении(Элемент)
	
	УстановитьПредставлениеПериода(ПредставлениеПериода, Объект.Период);
	
	ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НачалоПериода = НачалоГода(Объект.Период);
	КонецПериода  = КонецКвартала(Объект.Период);
	
	ПараметрыВыбора = Новый Структура;
	
	ПараметрыВыбора.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыВыбора.Вставить("КонецПериода",  КонецПериода);
	ПараметрыВыбора.Вставить("НарастающимИтогом", Истина);
	ПараметрыВыбора.Вставить("МинимальныйПериод", НачалоКвартала(ДатаРегистрацииОрганизации));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаКвартал",
		ПараметрыВыбора, Элементы.ПредставлениеПериода, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Период = РезультатВыбора.КонецПериода;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаАктуальностиДанных

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальностьДанных()
	
	Если Не ТребуетсяПроверятьАктуальность Или ДеятельностьОтсутствует Тогда
		Возврат;
	КонецЕсли;
	
	ЗаданиеАктуализации = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(Объект.Организация, УникальныйИдентификатор);
	
	Если ЗаданиеАктуализации = Неопределено Или Не ЗначениеЗаполнено(ЗаданиеАктуализации.УникальныйИдентификатор) Тогда
		ПроверитьАктуальностьДанных();
	Иначе
		
		ДанныеУчетаАктуальны = Ложь;
		ВозможнаБыстраяАктуализация = Истина;
		
		Если ИдентификаторЗаданияАктуализации <> ЗаданиеАктуализации.УникальныйИдентификатор Тогда
			// Задание запущено в другой форме
			ЗаданиеЗапущеноВДругойФорме = Истина;
			ИдентификаторЗаданияАктуализации = ЗаданиеАктуализации.УникальныйИдентификатор;
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
			ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации",
				ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);
		Иначе
			ЗаданиеЗапущеноВДругойФорме = Ложь;
		КонецЕсли;
		
		ОбновитьСтатусФоновогоЗадания();
		
	КонецЕсли;
	
	Если ИнтерфейсИнтеграцииСБанками Тогда
		ПараметрДляЗависимыхФорм = ЗависимыеФормыРасшифровок.ВыгрузитьЗначения();
		Оповестить("ОбновитьПоказателиРасчетаУСН", ПараметрДляЗависимыхФорм, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьАктуальностьДанных()
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	ИдентификаторЗаданияАктуализации = Неопределено;
	
	УИДЗамера = ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Ложь, "АктуализацияДанныхТребуетсяАктуализацияЗаПериод");
	
	ПараметрыПроверки = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыПроверкиАктуальности();
	ПараметрыПроверки.Организация                  = Объект.Организация;
	ПараметрыПроверки.Период                       = Объект.Период;
	ПараметрыПроверки.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыПроверки.УИДЗамера                    = УИДЗамера;
	ПараметрыПроверки.АктуализироватьВесьПериод    = Истина;
	ПараметрыПроверки.АктуализацияДляРасчетаНалога = Истина;
	
	РезультатВыполнения = ЗакрытиеМесяцаВызовСервера.ПроверитьАктуальность(ПараметрыПроверки);
	
	ИдентификаторЗаданияПроверкиАктуализации = РезультатВыполнения.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиАктуальности", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	
	ОбновитьСтатусФоновогоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПроверкиАктуальности(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ИдентификаторЗаданияПроверкиАктуализации = Неопределено;
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ИдентификаторЗаданияПроверкиАктуализации = Неопределено;
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если РезультатПроверки.УИДЗамера <> Неопределено Тогда
		ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(РезультатПроверки.УИДЗамера);
	КонецЕсли;
	
	ДанныеУчетаАктуальны = Не РезультатПроверки.ТребуетсяАктуализация;
	
	Если ДанныеУчетаАктуальны Тогда
		
		ИдентификаторЗаданияПроверкиАктуализации = Неопределено;
		ЗаполнитьАктуальныеПоказателиИПояснения();
		ЗапуститьОбновлениеБаннераСостоянияОтправки();
		
	ИначеЕсли ВозможнаБыстраяАктуализация
	   И Не ЗаданиеЗапущеноВДругойФорме
	   И ЗначениеЗаполнено(Объект.Организация)
	   И ЗначениеЗаполнено(Объект.Период)
	   И НетОшибокБлокирующихРасчетНалога(ЭтотОбъект) Тогда
	   
		ЗаданиеАктуализации = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(Объект.Организация, УникальныйИдентификатор);
		Если ЗаданиеАктуализации = Неопределено Или Не ЗначениеЗаполнено(ЗаданиеАктуализации.УникальныйИдентификатор) Тогда
			АктуализироватьДанные();
		Иначе
			
			ДанныеУчетаАктуальны = Ложь;
			ВозможнаБыстраяАктуализация = Истина;
			
			Если ИдентификаторЗаданияАктуализации <> ЗаданиеАктуализации.УникальныйИдентификатор Тогда
				// Задание запущено в другой форме
				ЗаданиеЗапущеноВДругойФорме = Истина;
				ИдентификаторЗаданияАктуализации = ЗаданиеАктуализации.УникальныйИдентификатор;
				ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации",
					ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);
			Иначе
				ЗаданиеЗапущеноВДругойФорме = Ложь;
			КонецЕсли;
			
			ОбновитьСтатусФоновогоЗадания();
			
		КонецЕсли;
		
	Иначе
		
		ТребуетсяПроверятьАктуальность = Ложь;
		ИдентификаторЗаданияПроверкиАктуализации = Неопределено;
		УправлениеФормойНаСервере();
		
	КонецЕсли;
	
	Если ИнтерфейсИнтеграцииСБанками Тогда
		ПараметрДляЗависимыхФорм = ЗависимыеФормыРасшифровок.ВыгрузитьЗначения();
		Оповестить("ОбновитьПоказателиРасчетаУСН", ПараметрДляЗависимыхФорм, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область АктуализацияДанных

&НаКлиенте
Процедура АктуализироватьДанные()
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеПроверкиАктуальности");
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	ИдентификаторЗаданияАктуализации = Неопределено;
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыАктуализации();
	ПараметрыАктуализации.Организация                  = Объект.Организация;
	ПараметрыАктуализации.Период                       = КонецКвартала(Объект.Период);
	ПараметрыАктуализации.ИдентификаторЗадания         = ИдентификаторЗаданияАктуализации;
	ПараметрыАктуализации.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыАктуализации.АктуализацияДляРасчетаНалога = ВозможнаБыстраяАктуализация;
	ПараметрыАктуализации.АктуализироватьВесьПериод    = Истина;
	
	РезультатВыполнения = ЗакрытиеМесяцаВызовСервера.АктуализироватьДанные(ПараметрыАктуализации);
	
	АдресХранилищаАктуализации = РезультатВыполнения.АдресРезультата;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ОбработатьРезультатАктуализации();
	Иначе
		ИдентификаторЗаданияАктуализации = РезультатВыполнения.ИдентификаторЗадания;
		ИдентификаторЗаданияПроверкиАктуализации = Неопределено;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации",
			ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);
		УправлениеФормойНаСервере();
	КонецЕсли;
	
	ОбновитьСтатусФоновогоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеАктуализации()
	
	Если ЗакрытиеМесяцаВызовСервера.ЗаданиеВыполнено(ИдентификаторЗаданияАктуализации) Тогда
		ОбработатьРезультатАктуализации();
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации",
			ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);
	КонецЕсли;
	
	ОбновитьСтатусФоновогоЗадания();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатАктуализации()
	
	ИдентификаторЗаданияПроверкиАктуализации = Неопределено;
	ИдентификаторЗаданияАктуализации = Неопределено;
	
	Если ЗаданиеЗапущеноВДругойФорме Тогда
		
		ПроверитьАктуальностьДанных();
		Если ИнтерфейсИнтеграцииСБанками Тогда
			ПараметрДляЗависимыхФорм = ЗависимыеФормыРасшифровок.ВыгрузитьЗначения();
			Оповестить("ОбновитьПоказателиРасчетаУСН", ПараметрДляЗависимыхФорм, ЭтотОбъект);
		КонецЕсли;
		Возврат;
		
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(АдресХранилищаАктуализации) Тогда
		
		РезультатАктуализации = ПолучитьИзВременногоХранилища(АдресХранилищаАктуализации);
		УдалитьИзВременногоХранилища(АдресХранилищаАктуализации);
		АдресХранилищаАктуализации = "";
		
		Если ТипЗнч(РезультатАктуализации) <> Тип("Структура")
		 Или Не РезультатАктуализации.Свойство("Выполнено") Тогда
			ПроведенАнализВариантаАктуализации = Ложь;
			РезультатАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыйРезультатАктуализации();
		Иначе
			ПроведенАнализВариантаАктуализации = ВозможнаБыстраяАктуализация
				И Не РезультатАктуализации.АктуализацияДляРасчетаНалога;
			ВозможнаБыстраяАктуализация = РезультатАктуализации.АктуализацияДляРасчетаНалога;
		КонецЕсли;
		
	Иначе
		
		ПроведенАнализВариантаАктуализации = Ложь;
		РезультатАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыйРезультатАктуализации();

	КонецЕсли;
	
	ДанныеУчетаАктуальны = РезультатАктуализации.Выполнено;
	
	Если ДанныеУчетаАктуальны Тогда
		
		ЗаполнитьАктуальныеПоказателиИПояснения();
		Оповестить("АктуализацияЗавершенаУспешно", Новый Структура("Организация", Объект.Организация));
		ЗапуститьОбновлениеБаннераСостоянияОтправки();
		
	Иначе
		
		Если Не ПроведенАнализВариантаАктуализации Тогда
			// При запуске актуализации мы уже знали, полная она или частичная.
			ЗакрытиеМесяцаКлиент.ПоказатьОшибкиАктуализации(ЭтотОбъект,
				РезультатАктуализации,
				НСтр("ru='Расчет налога не выполнен. Обнаружены ошибки.'"));
		КонецЕсли;
		
		Оповестить("АктуализацияОтменена", Новый Структура("Организация", Объект.Организация));
		ТребуетсяПроверятьАктуальность = Ложь;
		
		УправлениеФормойНаСервере();
		
	КонецЕсли;
	
	Если ИнтерфейсИнтеграцииСБанками Тогда
		ПараметрДляЗависимыхФорм = ЗависимыеФормыРасшифровок.ВыгрузитьЗначения();
		Оповестить("ОбновитьПоказателиРасчетаУСН", ПараметрДляЗависимыхФорм, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОбновлениеБаннераСостоянияОтправки()
	Если ИнтервалПроверкиСостоянияИнтеграцииСБанком > 0 Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЗапуститьОбновлениеБаннераСостояниеОтправки", ИнтервалПроверкиСостоянияИнтеграцииСБанком, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗапуститьОбновлениеБаннераСостояниеОтправки() Экспорт
	
	Если ХэшДанныхБаннера(ДокументыОплаты(ЭтотОбъект)) <> ХешДанныхБаннера Тогда
		ЗаполнитьОплатуНалогаИОтобразитьНаФорме();
	КонецЕсли;
	ЗапуститьОбновлениеБаннераСостоянияОтправки();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДокументыОплаты(Форма)
	
	ДокументыОплаты = Новый Массив;
	
	Для Каждого Платеж Из Форма.ПлатежиУСН Цикл
		ДокументыОплаты.Добавить(Платеж.Ссылка);
	КонецЦикла;
	
	Возврат ДокументыОплаты;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ПредупредитьОбОтсутствииУведомления()

	Если ПодаетсяУведомлениеПоНалогуЗаПериод Тогда
		ТекстВопроса = НСтр("ru='Не подготовлено уведомление об исчисленной сумме налога.
		|Продолжить?'");
	Иначе
		ТекстВопроса = НСтр("ru='Не сформирована операция по единому налоговому счету.
		|Продолжить?'");
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПредупредитьОбОтсутствииУведомленияЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура ПредупредитьОбОтсутствииУведомленияЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьДокументУплатыНалога();
	
КонецПроцедуры

&НаСервере
Функция ОписаниеТаблицыНалоги()
	
	ПорядокОтчетаУплаты = Обработки.ПомощникРасчетаНалогаУСН.ПорядокОтчетаУплатыНалога(Объект.Организация, Объект.Период);

	ПараметрыВыполнения = Обработки.ПомощникРасчетаНалогаУСН.НовыеПараметрыТаблицыНалоговНаЕНС();
	ПараметрыВыполнения.Организация = Объект.Организация;
	ПараметрыВыполнения.Период = ПериодСобытия;
	ПараметрыВыполнения.Налог = ПорядокОтчетаУплаты.Налог;
	ПараметрыВыполнения.Сумма = НалогДляУведомления;
	ПараметрыВыполнения.Периодичность = ПравилоУплаты.Периодичность;
	ПараметрыВыполнения.СрокУплаты = Срок;
	ПараметрыВыполнения.УникальныйИдентификатор = УникальныйИдентификатор;
	
	Возврат Обработки.ПомощникРасчетаНалогаУСН.ОписаниеТаблицыНалогиНаЕНС(ПараметрыВыполнения);
	
КонецФункции

&НаСервере
Процедура ОпределитьСвязанныеПравила()
	
	ПараметрыНабораПравил = ВыполнениеЗадачБухгалтера.ПараметрыНабораПравил();
	ЗаполнитьЗначенияСвойств(ПараметрыНабораПравил, ЭтотОбъект);
	ПараметрыНабораПравил.Организация = Объект.Организация;
	ВыполнениеЗадачБухгалтера.ОпределитьСвязанныеПравила(ПараметрыНабораПравил);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыНабораПравил);
	
	ПравилоУплатыВыполняетсяЕдинымПомощником = Ложь;
	Если ЗначениеЗаполнено(ПравилоУплаты) Тогда
		ПравилоУплатыВыполняетсяЕдинымПомощником = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПравилоУплаты, "ВыполняетсяЕдинымПомощником");
	КонецЕсли;
	
	ПоказыватьКомандыОплаты = ПомощникиПоУплатеНалоговИВзносов.ПоказыватьКомандыОплаты() И Не ПравилоУплатыВыполняетсяЕдинымПомощником;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойБлокУведомление()
	
	ПомощникиПоУплатеНалоговИВзносов.УправлениеФормойБлокУведомление(ЭтотОбъект,
		НалогДляУведомления <> 0);
		
	// Уведомления не суммируются. Более позднее сторнирует более раннее.
	Если ЭтотОбъект.Элементы.Найти("ОписаниеУведомлениеИтог0") <> Неопределено Тогда
		ЭтотОбъект.Элементы.ОписаниеУведомлениеИтог0.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьДоступностьКнопокУведомления();
	
	Элементы.ГруппаУведомлениеОНалогах.Видимость = 
		Не ПравилоУплатыВыполняетсяЕдинымПомощником
		Или (ИнтерфейсИнтеграцииСБанками И ПодаетсяУведомлениеПоНалогуЗаПериод);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУведомление(Идентификатор)
	
	ОткрытьДокументОповещение = Новый ОписаниеОповещения("ОбработатьРедактированиеУведомления", ЭтотОбъект);
	
	ПараметрыФормыУведомления = ПараметрыФормыУведомления(Идентификатор);
	
	Если ПодаетсяУведомлениеПоНалогуЗаПериод Тогда
		ИмяОбъекта = "УведомлениеОбИсчисленныхСуммахНалогов";
	Иначе
		ИмяОбъекта = "ОперацияПоЕдиномуНалоговомуСчету";
	КонецЕсли;
	
	ОткрытьФорму("Документ." + ИмяОбъекта + ".ФормаОбъекта",
		ПараметрыФормыУведомления,
		ЭтотОбъект,
		,
		,
		,
		ОткрытьДокументОповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыФормыУведомленияПоСтроке(СтрокаУведомления)
	
	Если ЗначениеЗаполнено(СтрокаУведомления.Уведомление) Тогда
		Возврат Новый Структура("Ключ, КонтекстныйВызов", СтрокаУведомления.Уведомление, Истина);
	Иначе
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПараметрыФормыПустогоУведомления()
	
	ЗначенияЗаполнения = Новый Структура("Организация", Объект.Организация);
	
	Возврат Новый Структура("ЗначенияЗаполнения, ПериодСобытия, Правило, КонтекстныйВызов",
		ЗначенияЗаполнения, ПериодСобытия, ПравилоУплаты, Истина);
	
КонецФункции

&НаСервере
Функция ПараметрыФормыУведомления(Идентификатор)
	
	Если Идентификатор = Неопределено Тогда 
		//Получим параметры формы для пустого документа
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
	СтрокаУведомления = УведомленияОбИсчисленныхНалогах.НайтиПоИдентификатору(Идентификатор);
	Если СтрокаУведомления = Неопределено Тогда
		Возврат ПараметрыФормыПустогоУведомления();
	КонецЕсли;
	
	Возврат ПараметрыФормыУведомленияПоСтроке(СтрокаУведомления);
	
КонецФункции

&НаСервере
Процедура СформироватьОперациюПоЕНСВТихомРежиме(ВыполненоВТихомРежиме)
	
	ЕдиныйНалоговыйСчет.СформироватьОперациюПоЕНСВТихомРежиме(Объект.Организация,
		ОписаниеТаблицыНалоги(),
		Правило,
		ПериодСобытия,
		ВыполненоВТихомРежиме);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьБаннераВводОстатковСменаОбъектаУСН()
	Переход = НастройкиСистемыНалогообложенияФормыВызовСервера.НастройкиДляБаннераСменыОбъектаУСН(Объект.Организация);
	
	Если Не ЗначениеЗаполнено(Переход) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаНапоминанияОВводеОстатков.Видимость = Переход.Напоминать;
	Если Не Переход.Напоминать Тогда
		Возврат;
	КонецЕсли;
	ПредыдущийОбъект = ?(НЕ Переход.ОбъектДоход, НСтр("ru='""доходы""'"), НСтр("ru='""доходы минус расходы""'"));
	ТекущийОбъект    = ?(Переход.ОбъектДоход, НСтр("ru='""доходы""'"), НСтр("ru='""доходы минус расходы""'"));
	
	ШаблонПоясненияСтрока = НСтр(
	"ru='В %1 году изменился объект налогообложения УСН с %2 на %3. Для завершения перехода воспользуйтесь помощником '");
	
	Элементы.ПодсказкаНапоминаниеОВводеОстатков.Заголовок = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(СтрШаблон(ШаблонПоясненияСтрока, Переход.Год, ПредыдущийОбъект, ТекущийОбъект)),
		Новый ФорматированнаяСтрока(НСтр("ru='Ввод остатков при изменении объекта УСН'"), , , , "ПерейтиВВводОстатковПриСменеОбъектаУСН"));
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьПлатежныеДокументыЗавершение(ПлатежныеПоручения, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ПлатежныеПоручения) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработатьВыгрузкуПлатежейНаСервере(ПлатежныеПоручения);
	
	ЗапуститьОбновлениеБаннераСостоянияОтправки();
	ОтразитьИспользованиеОстаткаЕНС();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыгрузкуПлатежейНаСервере(ПлатежныеПоручения)
	
	Для Каждого ПлатежныйДокумент Из ПлатежныеПоручения Цикл
		РегистрыСведений.СостоянияБанковскихДокументов.УстановитьСостояниеДокумента(
			ПлатежныйДокумент, Перечисления.СостоянияБанковскихДокументов.Отправлено);
	КонецЦикла;
	
	ЗаполнитьАктуальныеПоказателиИПояснения();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НеИсполненныеПлатежи(Форма)
	
	ДокументыОплаты = Новый Массив;
	
	Для Каждого СтрокаОплаты Из Форма.ПлатежиУСН Цикл
		Если ТипЗнч(СтрокаОплаты.Ссылка) = Тип("ДокументСсылка.ПлатежноеПоручение")
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеОплачено(СтрокаОплаты.Состояние)
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеОшибки(СтрокаОплаты.Состояние) Тогда
			ДокументыОплаты.Добавить(СтрокаОплаты.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументыОплаты;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НеОтправленныеПлатежи(Форма)
	
	ДокументыОплаты = Новый Массив;
	
	Для Каждого СтрокаОплаты Из Форма.ПлатежиУСН Цикл
		Если ТипЗнч(СтрокаОплаты.Ссылка) = Тип("ДокументСсылка.ПлатежноеПоручение")
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеУспешнойОтправки(СтрокаОплаты.Состояние)
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеОшибки(СтрокаОплаты.Состояние)
			И Не ПомощникиПоУплатеНалоговИВзносовКлиентСервер.СостояниеОплачено(СтрокаОплаты.Состояние) Тогда
			ДокументыОплаты.Добавить(СтрокаОплаты.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументыОплаты;
	
КонецФункции

&НаСервере
Функция ТребуетсяВыгрузитьДокументыОплаты()
	
	ДокументыОплатыВФайл = ПлатежныеДокументыДляВыгрузкиВФайл();
	
	Возврат ЗначениеЗаполнено(ДокументыОплатыВФайл);
	
КонецФункции

&НаСервере
Функция ТребуетсяОтправитьДокументыОплатыПоДиректБанку()
	
	ДокументыОплатыПоБанку = ПлатежныеДокументыДляОтправкиПоДиректБанку();
	
	Возврат ЗначениеЗаполнено(ДокументыОплатыПоБанку);
	
КонецФункции

&НаСервере
Функция ПлатежныеДокументыДляВыгрузкиВФайл()
	
	НеИсполненныеПлатежи = НеИсполненныеПлатежи(ЭтотОбъект);
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.ПлатежныеДокументыДляВыгрузкиВФайл(
		Объект.Организация, НеИсполненныеПлатежи);
	
КонецФункции

&НаСервере
Функция ПлатежныеДокументыДляОтправкиПоДиректБанку()
	
	НеОтправленныеПлатежи = НеОтправленныеПлатежи(ЭтотОбъект);
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.ПлатежныеДокументыПоДиректБанку(
		Объект.Организация, НеОтправленныеПлатежи);
	
КонецФункции

&НаСервере
Процедура РассчитатьСуммуЕНС()
	
	// С 2024 года при упрощенном учете ЕНС учитывается совокупная обязанность
	// на 68.90, поэтому нельзя определить сумму для конкретного налога.
	// Для периодов до 2024 года эта сумма определялась только на текущую дату,
	// считаем что в прошлом периоде невозможно определить сумму доступную к списанию,
	// т.к. в текущем периоде эта сумма перешла в недоимку.
	ОстатокЕНС = 0;
		
	СуммаСписанияСЕНС = 0;
	ИспользоватьОстатокЕНС = Ложь;
	Элементы.СуммаСписанияСЕНС.Доступность = Ложь;
	Элементы.ИспользоватьОстатокЕНС.Доступность = Ложь;
	Элементы.СуммаСписанияСЕНС.МаксимальноеЗначение = ОстатокЕНС;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОстатокЕНСПриИзменении(Элемент)
	
	ОтразитьИспользованиеОстаткаЕНС();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОтразитьИспользованиеОстаткаЕНС()
	Если ИспользоватьОстатокЕНС Тогда
		СуммаСписанияСЕНС = Мин(ОстатокЕНС, НалогКУплате - СуммаНалогаВПлатежныхДокументах(ПлатежиУСН));
	Иначе
		СуммаСписанияСЕНС = 0;
	КонецЕсли;
	Элементы.СуммаСписанияСЕНС.Доступность = ИспользоватьОстатокЕНС;
	
	СуммаСписанияСЕНСПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СуммаСписанияСЕНСПриИзмененииНаСервере()
	
	Объект.СуммаЕНП = Макс(0, НалогКУплате - СуммаСписанияСЕНС - СуммаНалогаВПлатежныхДокументах(ПлатежиУСН));
	
	УправлениеФормойНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ВсеПлатежиИсполнены()
	
	Если ПлатежиУСН.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ОтборСтрок = ПлатежиУСН.НайтиСтроки(Новый Структура("Оплачено", Ложь));
	Возврат ОтборСтрок.Количество() = 0;
	
КонецФункции

&НаСервере
Функция ЕстьДокументыОплатыПоБанку()
	
	Возврат ЕстьДокументОплаты(Тип("ДокументСсылка.ПлатежноеПоручение"));
	
КонецФункции

&НаСервере
Функция ЕстьДокументыОплатыПоКассе()
	
	Возврат ЕстьДокументОплаты(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	
КонецФункции

&НаСервере
Функция ЕстьДокументОплаты(ТипДокумента)
	
	Для Каждого СтрокаОплаты Из ЭтотОбъект.ПлатежиУСН Цикл
		Если ТипЗнч(СтрокаОплаты.Ссылка) = ТипДокумента Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура УдалитьУведомлениеНаСервере(Идентификатор)
	
	СтрокаУведомления = УведомленияОбИсчисленныхНалогах.НайтиПоИдентификатору(Идентификатор);	
	Если СтрокаУведомления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументУведомленияОбъект = СтрокаУведомления.Уведомление.ПолучитьОбъект();
	ДокументУведомленияОбъект.УстановитьПометкуУдаления(Истина);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоУпрощенныйЗачетВзносов(Организация, ПериодСобытия)
	
	Возврат УчетСтраховыхВзносовИППовтИсп.УпрощенныйЗачетФиксированныхВзносов(Организация, НачалоГода(ПериодСобытия));
	
КонецФункции

&НаСервере
Процедура ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС()
	
	ЕдиныйНалоговыйСчетИнтеграцияВнутреннийБП.ПоказатьПодсказкуИнтеграцияСЛичнымКабинетомЕНС(
		ЭтотОбъект, Объект.Организация, Объект.Период);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьДлительнуюОперациюРасчетаНачальныхПоказателей()
	
	НачальныеПоказателиРассчитаны = Ложь;
	УправлениеФормойНаСервере();
	
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("Организация", Объект.Организация);
	ПараметрыФункции.Вставить("Период", Объект.Период);

	ДлительнаяОперация = РассчитатьНачальныеПоказатели(ПараметрыФункции, УникальныйИдентификатор);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется подготовка данных...'");
	ОповещениеЗавершения = Новый ОписаниеОповещения("ЗаполнитьНачальныеПоказателиЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеЗавершения, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РассчитатьНачальныеПоказатели(ПараметрыФункции, УникальныйИдентификатор)
	// Функция запуска расчета на сервере
	ИмяФункции = "Обработки.ПомощникРасчетаНалогаУСН.НачальныеПоказатели";

	// Параметры длительной операции
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Расчет начальных показателей'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции, ПараметрыФункции);
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьНачальныеПоказателиЗавершение(Результат, ДопоплнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, Результат.КраткоеПредставлениеОшибки);
	Иначе
		Показатели = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
		ЗаполнитьНачальныеПоказатели(Показатели);
		ПередНачаломДлительнойОперации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(
		ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПредупрежденияПоОплатамМаркетплейсов()
	
	ПараметрыБаннера = Обработки.ПомощникРасчетаНалогаУСН.ПараметрыПредупрежденияПоРаботеСМаркетплейсами(
		Объект.Организация,
		Объект.Период,
		Обработки.ПомощникРасчетаНалогаУСН.ИмяНастройкиПредупрежденияПоРаботеСМаркетплейсами());
	Элементы.ГруппаПредупреждениеПоРаботеСМаркетплейсами.Видимость = ПараметрыБаннера.Показывать;
	Элементы.ДекорацияТекстПредупрежденияПоРаботеСМаркетплейсами.Заголовок = ПараметрыБаннера.ТекстПредупреждения;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СкрытьПредупреждениеПоРаботеСМаркетплейсами(Организация)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		Обработки.ПомощникРасчетаНалогаУСН.ИмяНастройкиПредупрежденияПоРаботеСМаркетплейсами(),
		Обработки.ПомощникРасчетаНалогаУСН.КлючНастройкиПредупрежденияПоРаботеСМаркетплейсами(),
		Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаПредупрежденийПриЗагрузке

&НаСервереБезКонтекста
Функция НайтиПредупрежденияПриЗагрузкеВыписки(Организация, НачалоПериода, КонецПериода)
	
	Возврат Обработки.ПомощникРасчетаНалогаУСН.ПредупрежденияПриЗагрузкеВыписки(Организация, НачалоПериода, КонецПериода);
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	Элементы.ГруппаОтправкаПлатежейВБанк.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
	Элементы.ГруппаОтправкаПлатежейВБанк.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ГруппаОтправкаПлатежейВБанк.РастягиватьПоГоризонтали = Истина;
	
	Элементы.ГруппаКомандыУплатыНалога.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно;
	Элементы.ГруппаКомандыУплатыНалога.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.ГруппаКомандыУплатыНалога.РастягиватьПоГоризонтали = Истина;
	
	Элементы.ГруппаИспользованиеОстаткаЕНС.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ГруппаСписатьСЕНС.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ПустойОтступ.Видимость = Ложь;
	Элементы.ГруппаОстатокЕНС.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ОстатокЕНС.Заголовок = НСтр("ru = 'Остаток на ЕНС'");
	
	Элементы.ОписаниеУведомление.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	Элементы.ГруппаСформироватьДекларациюОтказаться.Видимость = Ложь;
	
	ПредупреждениеМК = МобильныйКлиентБПФормаОбъекта.СоздатьПредупреждениеОбОграничениях(ЭтотОбъект,
		НСтр("ru = 'Отправить декларацию можно в версии для компьютера'"));
	Элементы.Переместить(ПредупреждениеМК, Элементы.ГруппаПодачаДекларации, Элементы.ФонТребуютсяРеквизиты);
	
	Элементы.ГруппаПоказатели.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойМобильныйКлиент()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.АнализУчетаПоУСН.Видимость = Ложь;
	Элементы.ДекорацияОтступ.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти
