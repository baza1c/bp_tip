#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Организация = Параметры.Организация;
	ОтчетныйПериод = Параметры.ОтчетныйПериод;

	МинимальнаяСуммаВзноса = Параметры.МинимальнаяСуммаВзноса;
	МаксимальнаяСуммаВзноса = Параметры.МаксимальнаяСуммаВзноса;
	ВыбраннаяСуммаВзноса = Параметры.ВыбраннаяСуммаВзноса;
	СуммаВзносаДляУменьшенияУСН = Параметры.ВыбраннаяСуммаВзноса;
	СуммаВзносаСкорректирована = Параметры.СуммаВзносаСкорректирована;
	
	ВзносПоЕдиномуТарифу = Параметры.ВзносПоЕдиномуТарифу;
	ВзносСДоходовПредыдущийГод = Параметры.ВзносСДоходовПредыдущийГод;
	Элементы.ВзносСДоходовПредыдущийГод.Подсказка = Параметры.ПодсказкаВзносСДоходов;
	ВзносСДоходовТекущийГод = Параметры.ВзносСДоходовТекущийГод;
	ДоляДоходовПоУСН = Параметры.ДоляДоходовПоУСН;
	ВзносыИспользованныеДляПатента = Параметры.ВзносыИспользованныеДляПатента;
	ОбъектНалогообложенияДоходы = Параметры.ОбъектНалогообложенияДоходы;
	
	Элементы.ДоляДоходовПоУСН.Видимость = ДоляДоходовПоУСН < 100;
	ИспользоватьВсюСумму = СуммаВзносаСкорректирована;
	Элементы.СуммаВзносаДляУменьшенияУСН.Доступность = ИспользоватьВсюСумму;
	Если ОбъектНалогообложенияДоходы Тогда
		Элементы.МинимальнаяСуммаВзноса.Заголовок = НСтр("ru = 'Уменьшено в прошлом периоде'");
		Элементы.ИспользоватьВсюСумму.Заголовок = НСтр("ru = 'Указать сумму взноса для уменьшения налога:'");
	Иначе
		Элементы.МинимальнаяСуммаВзноса.Заголовок = НСтр("ru = 'Включено в расходы в прошлом периоде'");
		Элементы.ИспользоватьВсюСумму.Заголовок = НСтр("ru = 'Указать сумму взноса для включения в расходы:'");
	КонецЕсли;
	УстановитьТекстПодсказки();
	НалоговаяБазаУменьшилась = УчетУСН.НалоговаяБазаУменьшилась(Организация, ОтчетныйПериод);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВзносПоЕдиномуТарифуНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеДействия = ОписаниеДействияОплатаВзносаИП(Организация, КонецГода(ОтчетныйПериод));
	Если ОписаниеДействия <> Неопределено Тогда
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносСДоходовТекущийГодНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НалоговыйПериод = КонецГода(ОтчетныйПериод);
	ПериодСобытияРасчетаВзноса = КонецДня(ОтчетныйПериод);
	
	ОписаниеДействия = ОписаниеДействияОплатаВзносаИП(Организация,
		НалоговыйПериод,
		Истина,
		ПериодСобытияРасчетаВзноса);
	
	Если ОписаниеДействия <> Неопределено Тогда
		ВыполнениеЗадачБухгалтераКлиент.ВыполнитьДействие(ОписаниеДействия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВзносСДоходовПредыдущийГодНажатие(Элемент, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ОтчетныйПериод", ОтчетныйПериод);
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("РегистрНакопления.УменьшениеНалогаНаСтраховыеВзносыИП.Форма.ВзносыИПИспользованныеДляУменьшения",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьВсюСуммуПриИзменении(Элемент)
	
	Элементы.СуммаВзносаДляУменьшенияУСН.Доступность = ИспользоватьВсюСумму;
	Если Не ИспользоватьВсюСумму Тогда
		СуммаВзносаДляУменьшенияУСН = ВыбраннаяСуммаВзноса;
		Элементы.ДекорацияОшибка.Видимость = Ложь;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПроверитьВведеннуюСумму();
	Если УказанаНеПравильнаяСумма Тогда
		ТекущийЭлемент = Элементы.СуммаВзносаДляУменьшенияУСН;
		Возврат;
	КонецЕсли;
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьВведеннуюСумму()

	Если СуммаВзносаДляУменьшенияУСН > Макс(ВыбраннаяСуммаВзноса, МаксимальнаяСуммаВзноса) Тогда
		Элементы.ДекорацияОшибка.Заголовок = НСтр("ru ='Сумма не может быть больше суммы взносов.'");
		УказанаНеПравильнаяСумма = Истина;
	ИначеЕсли СуммаВзносаДляУменьшенияУСН < МинимальнаяСуммаВзноса
		И Не (НалоговаяБазаУменьшилась И ОбъектНалогообложенияДоходы) И ИспользоватьВсюСумму Тогда
		Элементы.ДекорацияОшибка.Заголовок = НСтр("ru ='Сумма не может быть меньше использованной в прошлом периоде.'");
		УказанаНеПравильнаяСумма = Истина;
	Иначе
		УказанаНеПравильнаяСумма = Ложь;
	КонецЕсли;
	Элементы.ДекорацияОшибка.Видимость = УказанаНеПравильнаяСумма;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстПодсказки()
	
	СуммаВзносаРасчетная = Цел(
		(ВзносПоЕдиномуТарифу + ВзносСДоходовТекущийГод + ВзносСДоходовПредыдущийГод) * ДоляДоходовПоУСН / 100);
	ФорматЧиселВПояснении = НСтр("ru = 'ЧДЦ=2; ЧН=0,00; ЧГ=3,0'");
	
	Если СуммаВзносаРасчетная > МаксимальнаяСуммаВзноса И ВзносыИспользованныеДляПатента <> 0 Тогда
		ОсновнойТекст = НСтр("ru = 'Всего = Остаток за прошлый год + Единый тариф + С доходов - Использовано для Патента'");
		ДополнительныйТекст = СтрШаблон("(%1 + %2 + %3 - %4)",
			Формат(ВзносСДоходовПредыдущийГод, ФорматЧиселВПояснении),
			Формат(ВзносПоЕдиномуТарифу, ФорматЧиселВПояснении),
			Формат(ВзносСДоходовТекущийГод, ФорматЧиселВПояснении),
			Формат(ВзносыИспользованныеДляПатента, ФорматЧиселВПояснении));
	Иначе
		Если ДоляДоходовПоУСН < 100 Тогда
			ОсновнойТекст = НСтр("ru = 'Всего = (Остаток за прошлый год + Единый тариф + С доходов) * Доля доходов по УСН'");
			ДополнительныйТекст = СтрШаблон("(%1 + %2 + %3) * %4%%",
				Формат(ВзносСДоходовПредыдущийГод, ФорматЧиселВПояснении),
				Формат(ВзносПоЕдиномуТарифу, ФорматЧиселВПояснении),
				Формат(ВзносСДоходовТекущийГод, ФорматЧиселВПояснении),
				Формат(ДоляДоходовПоУСН, ФорматЧиселВПояснении));
		Иначе
			ОсновнойТекст = НСтр("ru = 'Всего = Остаток за прошлый год + Единый тариф + С доходов'");
			ДополнительныйТекст = СтрШаблон("(%1 + %2 + %3)",
				Формат(ВзносСДоходовПредыдущийГод, ФорматЧиселВПояснении),
				Формат(ВзносПоЕдиномуТарифу, ФорматЧиселВПояснении),
				Формат(ВзносСДоходовТекущийГод, ФорматЧиселВПояснении));
		КонецЕсли;
	КонецЕсли;

	ПодсказкаМаксимум = Новый Массив;
	ПодсказкаМаксимум.Добавить(ОсновнойТекст);
	ПодсказкаМаксимум.Добавить(Символы.ПС);
	ПодсказкаМаксимум.Добавить(ДополнительныйТекст);

	Элементы.ПодсказкаРасчетДоли.Заголовок = Новый ФорматированнаяСтрока(ПодсказкаМаксимум);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()

	Если ЗаписатьДанные() Тогда
		
		Если Модифицированность Тогда
			ПараметрыОповещения = Новый Структура;
			ПараметрыОповещения.Вставить("Организация", Организация);
			ПараметрыОповещения.Вставить("Период", ОтчетныйПериод);
				
			Оповестить("Запись_УменьшениеУСННаФиксированныеВзносы", ПараметрыОповещения, ЭтотОбъект);
		КонецЕсли;

		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьДанные()
	
	Если Не Модифицированность Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ВыбраннаяСуммаВзноса <> СуммаВзносаДляУменьшенияУСН Тогда
		МенеджерЗаписи = РегистрыСведений.УменьшениеНалогаНаВзносыПодлежащиеУплате.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = КонецКвартала(ОтчетныйПериод);
		МенеджерЗаписи.Организация = Организация;
		МенеджерЗаписи.Сумма = СуммаВзносаДляУменьшенияУСН;
		МенеджерЗаписи.ДеятельностьНаПатенте = Ложь;
		МенеджерЗаписи.Записать(Истина);
		ОчиститьЗаписиРегистра(Ложь);
		РегистрыСведений.УменьшениеНалогаНаВзносыПодлежащиеУплате.СдвинутьГраницуАктуальности(
			Организация, ОтчетныйПериод, ОбъектНалогообложенияДоходы);
	ИначеЕсли СуммаВзносаСкорректирована Тогда
		ОчиститьЗаписиРегистра(Истина);
		РегистрыСведений.УменьшениеНалогаНаВзносыПодлежащиеУплате.СдвинутьГраницуАктуальности(
			Организация, ОтчетныйПериод, ОбъектНалогообложенияДоходы);
	Иначе
		Модифицированность = Ложь;
	КонецЕсли;
	
	Возврат Истина; 
	
КонецФункции

&НаСервереБезКонтекста
Функция ОписаниеДействияОплатаВзносаИП(Организация, НалоговыйПериод, ВзносСДоходов = Ложь, ПериодСобытия = Неопределено)
	
	ПорядокУплаты = УчетСтраховыхВзносовИППовтИсп.ПорядокУплатыВзноса(Организация, НалоговыйПериод, ВзносСДоходов);
	Если ПорядокУплаты = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеКоманды = ВыполнениеЗадачБухгалтераКлиентСервер.НовыеПараметрыКомандЗадачи();
	ЗаполнитьЗначенияСвойств(ОписаниеКоманды, ПорядокУплаты);
	
	Если ВзносСДоходов И ЗначениеЗаполнено(ПериодСобытия) Тогда
		// В 2023 г . взнос с доходов можно заплатить в текущем налоговом периоде.
		// Аванс рассчитывается и оплачивается до 25 числа месяца, следующего за отчетным кварталом,
		// поэтому исключаем ситуацию, когда при расчете взноса с доходов попадут доходы следующего квартала
		ОписаниеКоманды.ПериодСобытия = ПериодСобытия;
		ОписаниеКоманды.Периодичность = Перечисления.Периодичность.Квартал;
		ОписаниеКоманды.Наименование = СтрШаблон(НСтр("ru ='Взносы за себя с доходов за %1'"),
			СтроковыеФункции.ПредставлениеПериодаВТексте(
				НачалоГода(НалоговыйПериод),
				ПериодСобытия));
	КонецЕсли;
	
	Возврат ВыполнениеЗадачБухгалтера.ОписаниеДействия(ОписаниеКоманды);
	
КонецФункции

&НаСервере
Процедура ОчиститьЗаписиРегистра(УдалятьТекущийПериод)
	
	ПериодыКУдалению = Новый Массив;
	Если УдалятьТекущийПериод Тогда
		ТекущийПериод = ОтчетныйПериод;
	Иначе
		ТекущийПериод = ДобавитьМесяц(ОтчетныйПериод, 3);
	КонецЕсли;
	
	Пока КонецКвартала(ТекущийПериод) <= КонецГода(ОтчетныйПериод) Цикл
		ПериодыКУдалению.Добавить(НачалоКвартала(ТекущийПериод));
		ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 3);
	КонецЦикла;
	
	СуммаВзносаДляУменьшенияУСН = МаксимальнаяСуммаВзноса;
	
	// Необходимо удалить все изменения до конца налогового периода
	Для Каждого ПериодУдаления Из ПериодыКУдалению Цикл
		МенеджерЗаписи = РегистрыСведений.УменьшениеНалогаНаВзносыПодлежащиеУплате.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = КонецКвартала(ПериодУдаления);
		МенеджерЗаписи.Организация = Организация;
		МенеджерЗаписи.ДеятельностьНаПатенте = Ложь;
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти