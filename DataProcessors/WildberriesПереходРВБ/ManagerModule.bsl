#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПеренестиОстатки(ПараметрыПереносаОстатков, АдресХранилищаДокументов) Экспорт
	
	СозданныеДокументы = Новый ТаблицаЗначений;
	СозданныеДокументы.Колонки.Добавить("Ссылка");
	СозданныеДокументы.Колонки.Добавить("Описание");
	СозданныеДокументы.Колонки.Добавить("ТекстОшибки");
	СозданныеДокументы.Колонки.Добавить("Картинка");
	
	Если НЕ ТипЗнч(ПараметрыПереносаОстатков) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
		
	Если ПараметрыПереносаОстатков.ИзменениеНастроекAPI Тогда
		Если ЗначениеЗаполнено(ПараметрыПереносаОстатков.НастройкаИнтеграции) Тогда
			
			НоваяСтрока = СозданныеДокументы.Добавить();
			НоваяСтрока.Описание = НСтр("ru = 'Заполнение истории контрагента в настройках интеграции'");
			
			Попытка
				НастройкаИнтеграции = ПараметрыПереносаОстатков.НастройкаИнтеграции.ПолучитьОбъект();
				НастройкаИнтеграции.ИсторияКонтрагентаДоговора.Очистить();
				СтрокаWB                     = НастройкаИнтеграции.ИсторияКонтрагентаДоговора.Добавить();
				СтрокаWB.Контрагент          = ПараметрыПереносаОстатков.КонтрагентИсточник;
				СтрокаWB.ДоговорКонтрагента  = ПараметрыПереносаОстатков.ДоговорКонтрагентаИсточник;
				СтрокаРВБ                    = НастройкаИнтеграции.ИсторияКонтрагентаДоговора.Добавить();
				СтрокаРВБ.Период             = ПараметрыПереносаОстатков.ДатаПереходаПрав;
				СтрокаРВБ.Контрагент         = ПараметрыПереносаОстатков.КонтрагентПриемник;
				СтрокаРВБ.ДоговорКонтрагента = ПараметрыПереносаОстатков.ДоговорКонтрагентаПриемник;
				
				НастройкаИнтеграции.Контрагент = ПараметрыПереносаОстатков.КонтрагентПриемник;
				НастройкаИнтеграции.ДоговорКонтрагента = ПараметрыПереносаОстатков.ДоговорКонтрагентаПриемник;
				НастройкаИнтеграции.Записать();
				
				НоваяСтрока.Ссылка = ПараметрыПереносаОстатков.НастройкаИнтеграции;
				НоваяСтрока.Картинка = 2;
			Исключение
				НоваяСтрока.ТекстОшибки = ОписаниеОшибки();
				НоваяСтрока.Картинка = 6;
			КонецПопытки;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПереносаОстатков.ПереносОстатков Тогда
		Отказ = Ложь;
		ТаблицаРеквизиты = Новый ТаблицаЗначений();
		ТаблицаРеквизиты.Колонки.Добавить("Регистратор");
		ТаблицаРеквизиты.Колонки.Добавить("Период");
		ТаблицаРеквизиты.Колонки.Добавить("Организация");
		ТаблицаРеквизиты.Колонки.Добавить("Подразделение");
		ТаблицаРеквизиты.Колонки.Добавить("Контрагент");
		ТаблицаРеквизиты.Колонки.Добавить("Содержание");
		ТаблицаРеквизиты.Колонки.Добавить("НДСВСтоимостиТоваров");
		
		МассивПодразделений = ПараметрыПереносаОстатков.ТаблицаТоваров.ВыгрузитьКолонку("Подразделение");
		ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивПодразделений);
		
		СтрокаРеквизиты = ТаблицаРеквизиты.Добавить();
		СтрокаРеквизиты.Период = НачалоДня(ПараметрыПереносаОстатков.ДатаПереходаПрав) - 1;
		СтрокаРеквизиты.Организация = ПараметрыПереносаОстатков.Организация;
		СтрокаРеквизиты.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
		СтрокаРеквизиты.Контрагент =  ПараметрыПереносаОстатков.КонтрагентИсточник;
		СтрокаРеквизиты.НДСВСтоимостиТоваров = Перечисления.ДействиеНДСВСтоимостиТоваров.НеИзменять;
		
		НомерСтроки = 1;
		
		ОперацияКУдалению = СуществующаяОперациюПереносаОстатков(ПараметрыПереносаОстатков.Организация, ПараметрыПереносаОстатков.ДатаПереходаПрав);
		
		ТаблицаСписанныеТовары = Неопределено;
		СписанныеПартииНДС = Неопределено;
		
		НачатьТранзакцию();
		
		Для Каждого Подразделение Из МассивПодразделений Цикл
			СтруктураОтбора = Новый Структура("Подразделение", Подразделение);
			ТоварыПодразделения = ПараметрыПереносаОстатков.ТаблицаТоваров.НайтиСтроки(СтруктураОтбора);
			
			ТаблицаТовары = УчетТоваров.НовыйТаблицаТоварыДляСписания();
			ТаблицаТовары.Колонки.Добавить("НомерСтрокиДокумента");
			
			СтрокаРеквизиты.Подразделение = Подразделение;
			Для Каждого Товар Из ТоварыПодразделения Цикл
				
				НоваяСтрока = ТаблицаТовары.Добавить();
				
				//Заполняем из таблицы остатков колонки:
				//Номенклатура, СчетУчета, Количество
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Товар);
				
				НоваяСтрока.Период = НачалоДня(ПараметрыПереносаОстатков.ДатаПереходаПрав);
				НоваяСтрока.НомерСтроки = НомерСтроки;
				НоваяСтрока.НомерСтрокиДокумента = НомерСтроки;
				
				НоваяСтрока.КорСчетСписания = НоваяСтрока.СчетУчета;
				НоваяСтрока.ВидКорСубконто1 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты;
				НоваяСтрока.ВидКорСубконто2 = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
				
				НоваяСтрока.КорСубконто1 = ПараметрыПереносаОстатков.КонтрагентПриемник;
				НоваяСтрока.КорСубконто2 = НоваяСтрока.Номенклатура;
				
				НоваяСтрока.КорПодразделение = Подразделение;
				
				//Оставляем не заполненными колонки:
				//ИмяСписка, СинонимСписка, Склад, ДокументОприходования,Себестоимость,ВидКорСубконто3,КорСубконто3,КорПодразделение
				//Комитент,ДоговорКомиссии,СчетАвансовСКомитентом,СчетРасчетовСКомитентом,ВалютаРасчетовСКомитентом,СуммаРасчетовСКомитентом
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			
			СписанныеТоварыПодразделения = УчетТоваров.ПодготовитьТаблицуСписанныеТовары(ТаблицаТовары, ТаблицаРеквизиты, Отказ);
			СписанныеПартииНДСПодразделени = УчетНДСПереопределяемый.ПолучитьТаблицуСписанныеПартииНДС(ТаблицаТовары, СписанныеТоварыПодразделения, ТаблицаРеквизиты[0], Отказ);
			
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
			
			Если ТаблицаСписанныеТовары = Неопределено Тогда
				ТаблицаСписанныеТовары = СписанныеТоварыПодразделения;
			Иначе
				ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(СписанныеТоварыПодразделения, ТаблицаСписанныеТовары);
			Конецесли; 
			
			Если СписанныеПартииНДС = Неопределено Тогда
				СписанныеПартииНДС = СписанныеПартииНДСПодразделени;
			Иначе
				ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(СписанныеПартииНДСПодразделени, СписанныеПартииНДС);
			Конецесли;
			
		КонецЦикла;
		
		Если НЕ Отказ Тогда
			
			СозданныйДокумент = СозданныеДокументы.Добавить();
			СозданныйДокумент.Описание = НСтр("ru = 'Перенос остатков товаров, переданных на реализацию'");
			
			Попытка
				ОперацияОбъект               = Документы.ОперацияБух.СоздатьДокумент();
				ОперацияОбъект.Дата          = ПараметрыПереносаОстатков.ДатаПереходаПрав;
				ОперацияОбъект.Организация   = ПараметрыПереносаОстатков.Организация;
				ОперацияОбъект.Ответственный = Пользователи.ТекущийПользователь();
				ОперацияОбъект.Содержание    = НСтр("ru = 'Перенос остатков товаров, переданных на реализацию'");
				ОперацияОбъект.Комментарий   = НСтр("ru = '#Создан автоматически при передаче прав между комиссионерами'");
				// Операция переноса остатков не должна сдвигать последовательность
				ОперацияОбъект.ДополнительныеСвойства.Вставить("ПроведенВХронологическойПоследовательности", Истина);
				ОперацияОбъект.Записать();
				
				СозданныйДокумент.Ссылка = ОперацияОбъект.Ссылка;
				СозданныйДокумент.Картинка = 1;
			Исключение
				СозданныйДокумент.ТекстОшибки = ОписаниеОшибки();
				СозданныйДокумент.Картинка = 6;
				Отказ = Истина;
			КонецПопытки; 
			
		КонецЕсли;
	
		Если НЕ Отказ Тогда
			
			ТаблицаРеквизиты[0].Период = ПараметрыПереносаОстатков.ДатаПереходаПрав;
			ТаблицаРеквизиты[0].Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			
			Если НЕ ТаблицаСписанныеТовары = Неопределено Тогда
				ОперацияОбъект.Движения.Хозрасчетный.Записывать = Истина;
				УчетТоваров.СформироватьДвиженияСписаниеТоваров(ТаблицаСписанныеТовары,
					ТаблицаРеквизиты, ОперацияОбъект.Движения, Отказ);
				
				ОперацияОбъект.Движения.НДСРаздельныйУчет.Записывать = Истина;
				УчетНДСПереопределяемый.СформироватьДвиженияВыбытиеТоваров(
					СписанныеПартииНДС, ТаблицаСписанныеТовары, ТаблицаРеквизиты[0], ОперацияОбъект.Движения, Отказ);
			КонецЕсли;
			
			Попытка
				ОперацияОбъект.Движения.Записать();
				
				СозданныйДокумент.Ссылка = ОперацияОбъект.Ссылка;
				СозданныйДокумент.Картинка = 2;
				
				Если ЗначениеЗаполнено(ОперацияКУдалению) Тогда
					ОперацияКУдалениюОбъект = ОперацияКУдалению.ПолучитьОбъект();
					ОперацияКУдалениюОбъект.УстановитьПометкуУдаления(Истина);
				КонецЕсли;
				
			Исключение
				
				СозданныйДокумент.Картинка = 6;
				СозданныйДокумент.ТекстОшибки = ОписаниеОшибки();
				Отказ = Истина;
			КонецПопытки;
			
		КонецЕсли;
		
		Если Отказ Тогда
			ОтменитьТранзакцию();
		Иначе
			ЗафиксироватьТранзакцию();
		Конецесли;
		
	КонецЕсли;
	
	Если ПараметрыПереносаОстатков.ИсправлениеДокументов Тогда
		
		Для Каждого СтрокаСписка Из ПараметрыПереносаОстатков.СписокДокументов Цикл
			Если НЕ СтрокаСписка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			Отказ = Ложь;
			
			НачатьТранзакцию();
			
			ДокументОбъект = СтрокаСписка.Значение.ПолучитьОбъект();
			
			СозданныйДокумент = СозданныеДокументы.Добавить();
			СозданныйДокумент.Описание = НСтр("ru = 'Замена контрагента в документе'");
			СозданныйДокумент.Ссылка = ДокументОбъект.Ссылка;
			
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			Исключение
				Отказ = Истина;
				СозданныйДокумент.Картинка = 6;
				СозданныйДокумент.ТекстОшибки = ОписаниеОшибки();
			КонецПопытки;
			
			Если НЕ Отказ Тогда
				
				Если ДокументОбъект.ДоговорКонтрагента = ПараметрыПереносаОстатков.ДоговорКонтрагентаИсточник Тогда
					НовыйДоговор = ПараметрыПереносаОстатков.ДоговорКонтрагентаПриемник;
				Иначе
					НовыйДоговор = НайтиДоговорКонтрагента(ПараметрыПереносаОстатков.Организация, 
					ДокументОбъект.ДоговорКонтрагента, ПараметрыПереносаОстатков.КонтрагентПриемник);
				КонецЕсли;
				
				ДокументОбъект.Контрагент = ПараметрыПереносаОстатков.КонтрагентПриемник;
				ДокументОбъект.ДоговорКонтрагента = НовыйДоговор;
				
				Если СтрокаСписка.Значение = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
					Если ЗначениеЗаполнено(ДокументОбъект.Сделка)
						И ПараметрыПереносаОстатков.СписокДокументов.НайтиПоЗначению(ДокументОбъект.Сделка) = Неопределено Тогда
						ДокументОбъект.Сделка = Неопределено;
					КонецЕсли;
				КонецЕсли;
				
				Попытка
					ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
					СозданныйДокумент.Картинка = 1;
				Исключение
					СозданныйДокумент.Картинка = 6;
					СозданныйДокумент.ТекстОшибки = ОписаниеОшибки();
					Отказ = Истина;
				КонецПопытки;
				
			КонецЕсли;
			
			Если Отказ Тогда
				ОтменитьТранзакцию();
			Иначе
				ЗафиксироватьТранзакцию();
				
				Попытка
					ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
					СозданныйДокумент.Картинка = 2;
				Исключение
					СозданныйДокумент.Картинка = 1;
					СозданныйДокумент.ТекстОшибки = ОписаниеОшибки();
				КонецПопытки;
			Конецесли;
				
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПараметрыПереносаОстатков.СопоставлениеНоменклатуры Тогда
		
		ДополнительныеПараметры = СопоставлениеНоменклатурыКонтрагентов.НовыеДополнительныеПараметрыПриЗаписиНоменклатурыКонтрагентов();
		ДополнительныеПараметры.ТребуетсяПоискСсылки = Истина;
		ДополнительныеПараметры.ТребуетсяПоискЕдиницыИзмеренияПоОКЕИ = Ложь;
		ТекстОшибки = "";
		Ошибок = 0;
		Обработано = 0;
		
		Для Каждого Товар Из ПараметрыПереносаОстатков.ТаблицаСопоставленияНоменклатуры Цикл
			Если НЕ Товар.Пометка Тогда
				Продолжить;
			КонецЕсли;
			
			НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента(ПараметрыПереносаОстатков.КонтрагентПриемник);
			ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, Товар.НоменклатураКонтрагента, , "Владелец");
			
			НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы(Товар.Номенклатура);
			
			Отказ = Ложь;
			Попытка
				СопоставлениеНоменклатурыКонтрагентов.СоздатьОбновитьНоменклатуруКонтрагента(
					НоменклатураКонтрагента, НоменклатураИБ, Отказ, ТекстОшибки, ДополнительныеПараметры);
					Если Отказ Тогда
						Ошибок = Ошибок + 1;
					Иначе
						Обработано = Обработано + 1;
					КонецЕсли;
			Исключение
				ТекстОшибки = ТекстОшибки + " Ид:" + Товар.Идентификатор + "-" + ОписаниеОшибки();
				Ошибок = Ошибок + 1;
			КонецПопытки;
		КонецЦикла;
		НоваяСтрока = СозданныеДокументы.Добавить();
		НоваяСтрока.Описание = СтрШаблон(НСтр("ru = 'Перенос настроек сопоставления номенклатуры. Успешно:%1, Ошибок:%2'"), Обработано, Ошибок);
		НоваяСтрока.Картинка = 1;
		Если Ошибок = 0 Тогда
			НоваяСтрока.Картинка = 2;
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПереносаОстатков.КорректировкаДолга Тогда
		
		ТаблицаПоДоговорам = ПараметрыПереносаОстатков.ТаблицаЗадолженности.Скопировать(,"ДоговорКонтрагента, ВидЗадолженности");
		ТаблицаПоДоговорам.Свернуть("ДоговорКонтрагента, ВидЗадолженности");
		
		Отказ = Ложь;
		СообщениеОбОшибке = "";
		УдалитьКорректировкиДолга(ПараметрыПереносаОстатков.Организация, ПараметрыПереносаОстатков.ДатаПереходаПрав, Отказ, СообщениеОбОшибке);
		
		Если Отказ Тогда
			СозданныйДокумент = СозданныеДокументы.Добавить();
			СозданныйДокумент.Описание = НСтр("ru = 'Перенос остатков взаиморасчетов'");
			СозданныйДокумент.Картинка = 6;
			СозданныйДокумент.ТекстОшибки = ОписаниеОшибки();
			
		Иначе
			
			Для Каждого КорректировкаДолга Из ТаблицаПоДоговорам Цикл
				СтруктураОтбора = Новый Структура("ДоговорКонтрагента, ВидЗадолженности, Пометка", 
				КорректировкаДолга.ДоговорКонтрагента, КорректировкаДолга.ВидЗадолженности, Истина);
				НайденныеСтроки = ПараметрыПереносаОстатков.ТаблицаЗадолженности.НайтиСтроки(СтруктураОтбора);
				
				Если НайденныеСтроки.Количество() Тогда
					Отказ = Ложь;
					СозданныйДокумент = СозданныеДокументы.Добавить();
					СозданныйДокумент.Описание = СтрШаблон(НСтр("ru = 'Перенос задолженности по договору: %1'"), КорректировкаДолга.ДоговорКонтрагента);
					
					ДокументОбъект               = Документы.КорректировкаДолга.СоздатьДокумент();
					ДокументОбъект.Дата          = ПараметрыПереносаОстатков.ДатаПереходаПрав;
					ДокументОбъект.Организация   = ПараметрыПереносаОстатков.Организация;
					ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
					ДокументОбъект.Комментарий   = НСтр("ru = '#Создан автоматически при передаче прав между комиссионерами'"); 
					ДокументОбъект.ВидОперации   = Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности;
					ДокументОбъект.КонтрагентДебитор  = ПараметрыПереносаОстатков.КонтрагентИсточник;
					ДокументОбъект.КонтрагентКредитор = ПараметрыПереносаОстатков.КонтрагентПриемник;
					ДокументОбъект.ВалютаДокумента    = Константы.ВалютаРегламентированногоУчета.Получить();
					
					ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КорректировкаДолга.ДоговорКонтрагента, "ВидДоговора");
					Если КорректировкаДолга.ВидЗадолженности = Перечисления.ВидыЗадолженности.Дебиторская Тогда
						Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком Тогда
							ДокументОбъект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.АвансыПоставщику;
						Иначе
							ДокументОбъект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПокупателя;
						КонецЕсли;
					ИначеЕсли КорректировкаДолга.ВидЗадолженности = Перечисления.ВидыЗадолженности.Кредиторская Тогда
						Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком Тогда
							ДокументОбъект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.АвансыПокупателя;
						Иначе
							ДокументОбъект.ВидАвансаЗадолженности = Перечисления.ВидыАвансаЗадолженности.ЗадолженностьПоставщику;
						КонецЕсли;
					КонецЕсли;
					
					Для Каждого СтрокаВзаиморасчетов Из НайденныеСтроки Цикл
						Если КорректировкаДолга.ВидЗадолженности = Перечисления.ВидыЗадолженности.Дебиторская Тогда
							НоваяСтрока = ДокументОбъект.ДебиторскаяЗадолженность.Добавить();
						Иначе
							НоваяСтрока = ДокументОбъект.КредиторскаяЗадолженность.Добавить();
						КонецЕсли;
						НоваяСтрока.КурсВзаиморасчетов = 1;
						НоваяСтрока.КратностьВзаиморасчетов = 1;
						
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВзаиморасчетов);
						НоваяСтрока.КорСчетУчетаРасчетов = НоваяСтрока.СчетУчетаРасчетов;
						НоваяСтрока.СуммаВзаиморасчетов  = НоваяСтрока.Сумма;
						Если НоваяСтрока.ДоговорКонтрагента = ПараметрыПереносаОстатков.ДоговорКонтрагентаИсточник Тогда
							НоваяСтрока.КорДоговорКонтрагента = ПараметрыПереносаОстатков.ДоговорКонтрагентаПриемник;
						Иначе
							НоваяСтрока.КорДоговорКонтрагента = НайтиДоговорКонтрагента(ПараметрыПереносаОстатков.Организация, 
							НоваяСтрока.ДоговорКонтрагента, ПараметрыПереносаОстатков.КонтрагентПриемник);
						КонецЕсли;
						
					КонецЦикла;
					
					Попытка
						ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
						
						СозданныйДокумент.Ссылка = ДокументОбъект.Ссылка;
						СозданныйДокумент.Картинка = 1;
					Исключение
						СозданныйДокумент.Картинка = 6;
						СозданныйДокумент.ТекстОшибки = ОписаниеОшибки();
						Отказ = Истина;
					КонецПопытки;
					
					Если Не Отказ Тогда
						Попытка
							ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
							СозданныйДокумент.Картинка = 2;
						Исключение
							СозданныйДокумент.Картинка = 1;
						КонецПопытки;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(СозданныеДокументы, АдресХранилищаДокументов);
	
КонецПроцедуры

Функция СуществующаяОперациюПереносаОстатков(Организация, ДатаПереходаПрав)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", ДатаПереходаПрав);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОперацияБух.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОперацияБух КАК ОперацияБух
	|ГДЕ
	|	ОперацияБух.Организация = &Организация
	|	И ОперацияБух.Дата = &Дата
	|	И СТРНАЙТИ(ОперацияБух.Комментарий, ""#Создан автоматически при передаче прав между комиссионерами"") > 0
	|	И НЕ ОперацияБух.ПометкаУдаления";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат Документы.ОперацияБух.ПустаяСсылка();
	КонецЕсли;
		
КонецФункции

Функция НайтиДоговорКонтрагента(Организация, Договор, НовыйКонтргаент)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("НовыйКонтргаент", НовыйКонтргаент);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов1.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов1
	|		ПО ДоговорыКонтрагентов.Организация = ДоговорыКонтрагентов1.Организация
	|			И ДоговорыКонтрагентов.ВидДоговора = ДоговорыКонтрагентов1.ВидДоговора
	|			И ДоговорыКонтрагентов.Наименование = ДоговорыКонтрагентов1.Наименование
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &Договор
	|	И ДоговорыКонтрагентов1.Владелец = &НовыйКонтргаент
	|	И НЕ ДоговорыКонтрагентов1.Ссылка ЕСТЬ NULL";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		НовыйДоговор = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НовыйДоговор, Договор, , "Код, Владелец");
		НовыйДоговор.Владелец = НовыйКонтргаент;
		Попытка
			НовыйДоговор.Записать();
		Исключение
			Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецПопытки;
		Возврат НовыйДоговор.Ссылка;
	КонецЕсли;
	
КонецФункции

Процедура УдалитьКорректировкиДолга(Организация, ДатаПереходаПрав, Отказ, СообщениеОбОшибке)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", ДатаПереходаПрав);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаДолга.Ссылка КАК Ссылка,
	|	КорректировкаДолга.Проведен КАК Проведен
	|ИЗ
	|	Документ.КорректировкаДолга КАК КорректировкаДолга
	|ГДЕ
	|	КорректировкаДолга.Организация = &Организация
	|	И КорректировкаДолга.Дата = &Дата
	|	И СТРНАЙТИ(КорректировкаДолга.Комментарий, ""#Создан автоматически при передаче прав между комиссионерами"") > 0
	|	И НЕ КорректировкаДолга.ПометкаУдаления";
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Попытка
			ДокументОбъект = Результат.Ссылка.ПолучитьОбъект();
			Если Результат.Проведен Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			КонецЕсли;
			ДокументОбъект.УстановитьПометкуУдаления(Истина);
		Исключение
			Отказ = Истина;
			СообщениеОбОшибке = ОписаниеОшибки();
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры
	
#КонецЕсли

