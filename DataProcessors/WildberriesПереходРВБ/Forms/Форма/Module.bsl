
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ДатаПереходаПрав = '20240805';
	Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	НастройкаИнтеграции = Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Объект.Организация, Перечисления.ВидыМаркетплейсов.Wildberries);
	
	ДанныеКонтрагентИсточник = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыWB(НачалоДня(Объект.ДатаПереходаПрав) - 1);
	Если ЗначениеЗаполнено(ДанныеКонтрагентИсточник.ИНН) Тогда
		Объект.КонтрагентИсточник = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ДанныеКонтрагентИсточник.ИНН, ДанныеКонтрагентИсточник.КПП);
		Если Не ЗначениеЗаполнено(Объект.КонтрагентИсточник) Тогда
			Объект.КонтрагентИсточник = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ДанныеКонтрагентИсточник.ИНН);
		КонецЕсли;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.ДоговорКонтрагентаИсточник)
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.КонтрагентИсточник) Тогда
		МассивВидовДоговоров = Новый Массив;
		МассивВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"));
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагентаИсточник,
			Объект.КонтрагентИсточник, Объект.Организация, МассивВидовДоговоров);
	КонецЕсли;
	
	ДанныеКонтрагентПриемник = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыWB(Объект.ДатаПереходаПрав);
	Если ЗначениеЗаполнено(ДанныеКонтрагентПриемник.ИНН) Тогда
		Объект.КонтрагентПриемник = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ДанныеКонтрагентПриемник.ИНН, ДанныеКонтрагентПриемник.КПП);
		Если Не ЗначениеЗаполнено(Объект.КонтрагентПриемник) Тогда
			Объект.КонтрагентПриемник = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ДанныеКонтрагентПриемник.ИНН);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДоговорКонтрагентаПриемник)
		И ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.КонтрагентПриемник) Тогда
		МассивВидовДоговоров = Новый Массив;
		МассивВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионером"));
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагентаПриемник,
			Объект.КонтрагентПриемник, Объект.Организация, МассивВидовДоговоров);
	КонецЕсли;
		
	Элементы.СоздатьКонтрагентаРВБ.Видимость = Не ЗначениеЗаполнено(Объект.КонтрагентПриемник);
	Элементы.ФормаНазад.Видимость = Ложь;
	
	ИзменениеНастроекAPI = Истина;
	ПереносОстатков = Истина;
	ИсправлениеДокументов = Истина;
	СопоставлениеНоменклатуры = Истина;
	КорректировкаДолга = Истина;
	
	Элементы.ИзменениеНастроекAPI.Видимость = Значениезаполнено(НастройкаИнтеграции);
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	НастройкаИнтеграции = Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Объект.Организация, Перечисления.ВидыМаркетплейсов.Wildberries);
	Элементы.ИзменениеНастроекAPI.Видимость = Значениезаполнено(НастройкаИнтеграции);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереносОстатковПриИзменении(Элемент)
	Элементы.ТаблицаТоваров.Видимость = ПереносОстатков;
КонецПроцедуры

&НаКлиенте
Процедура ИсправлениеДокументовПриИзменении(Элемент)
	Элементы.СписокДокументов.Видимость = ИсправлениеДокументов;
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеНоменклатурыПриИзменении(Элемент)
	Элементы.ТаблицаСопоставленияНоменклатуры.Видимость = СопоставлениеНоменклатуры;
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаДолгаПриИзменении(Элемент)
	Элементы.ТаблицаЗадолженности.Видимость = КорректировкаДолга;
КонецПроцедуры

&НаКлиенте
Процедура СозданныеДокументыПередНачаломИзменения(Элемент, Отказ)
	ТекущиеДанные = Элементы.СозданныеДокументы.ТекущиеДанные;
	Отказ = Истина;
	Если ЗначениеЗаполнено(ТекущиеДанные.Ссылка) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Ссылка);
		Если ТипЗнч(ТекущиеДанные.Ссылка) = Тип("СправочникСсылка.НастройкиИнтеграцииМаркетплейс") Тогда
			ОткрытьФорму("Справочник.НастройкиИнтеграцииМаркетплейс.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ОперацияБух") Тогда
			ОткрытьФорму("Документ.ОперацияБух.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
			ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			ОткрытьФорму("Документ.ОтчетКомиссионераОПродажах.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Ссылка) = Тип("ДокументСсылка.КорректировкаДолга") Тогда
			ОткрытьФорму("Документ.КорректировкаДолга.ФормаОбъекта", ПараметрыФормы, , , , , );
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьКонтрагентаРВБНаСервере()
	
	ДанныеКонтрагентПриемник = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыWB(Объект.ДатаПереходаПрав);
	
	Объект.КонтрагентПриемник = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", ДанныеКонтрагентПриемник.ИНН);
	
	Если НЕ ЗначениеЗаполнено(Объект.КонтрагентПриемник) Тогда
		
		КонтрагентОбъект                           = Справочники.Контрагенты.СоздатьЭлемент();
		КонтрагентОбъект.Наименование              = ДанныеКонтрагентПриемник.НаименованиеПолное;
		КонтрагентОбъект.НаименованиеПолное        = ДанныеКонтрагентПриемник.НаименованиеПолное;
		КонтрагентОбъект.ИНН                       = ДанныеКонтрагентПриемник.ИНН;
		КонтрагентОбъект.КПП                       = ДанныеКонтрагентПриемник.КПП;
		КонтрагентОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		КонтрагентОбъект.Записать();
			
		Объект.КонтрагентПриемник = КонтрагентОбъект.Ссылка;
		
		ПараметрыДоговора = Новый Структура;
		ПараметрыДоговора.Вставить("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
		ПараметрыДоговора.Вставить("Организация", Объект.Организация);
		ПараметрыДоговора.Вставить("Владелец",    Объект.КонтрагентПриемник);
		
		Если ЗначениеЗаполнено(Объект.ДоговорКонтрагентаИсточник) Тогда
			РеквизитыСтарогоДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДоговорКонтрагентаИсточник, "Наименование, Дата, Номер");
			ПараметрыДоговора.Вставить("Наименование", РеквизитыСтарогоДоговора.Наименование);
			ПараметрыДоговора.Вставить("Дата",         РеквизитыСтарогоДоговора.Дата);
			ПараметрыДоговора.Вставить("Номер",        РеквизитыСтарогоДоговора.Номер);
		КонецЕсли;
				
		ПараметрыСоздания = Новый Структура("ЗначенияЗаполнения", ПараметрыДоговора);
		Объект.ДоговорКонтрагентаПриемник = РаботаСДоговорамиКонтрагентовБПВызовСервера.СоздатьОсновнойДоговорКонтрагента(ПараметрыСоздания);
		
	КонецЕсли;
	
	Элементы.СоздатьКонтрагентаРВБ.Видимость = Не ЗначениеЗаполнено(Объект.КонтрагентПриемник);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКонтрагентаРВБ(Команда)
	СоздатьКонтрагентаРВБНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	Если НЕ ПроверкаЗаполнения() Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаГлавная Тогда
		Элементы.ФормаНазад.Видимость = Истина;
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПереносОстатков;
		ЗаполнитьОстаткиТоваров();
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПереносОстатков Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаИсправлениеДокументов;
		ЗаполнитьСписокДокументов();
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаИсправлениеДокументов Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаСоответствияНоменклатуры;
		ЗаполнитьСписокСопоставленнойНоменклатуры();
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаСоответствияНоменклатуры Тогда
		Элементы.ФормаДалее.Заголовок = "Выполнить";
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаКорректировкаДолга;
		ЗаполнитьОстаткиЗадолженности();
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаКорректировкаДолга Тогда
		Элементы.ФормаНазад.Видимость = Ложь;
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаСозданныеДокументы;
		Элементы.ФормаДалее.Заголовок = "Закрыть";
		ПодключитьОбработчикОжидания("НачатьСозданиеДокументов", 0.1, Истина);
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаСозданныеДокументы Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПереносОстатков Тогда
		Элементы.ФормаНазад.Видимость = Ложь;
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаГлавная;
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаИсправлениеДокументов Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаПереносОстатков;
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаСоответствияНоменклатуры Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаИсправлениеДокументов;
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаКорректировкаДолга Тогда
		Элементы.ФормаДалее.Заголовок = "Далее >";
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаСоответствияНоменклатуры;
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаСозданныеДокументы Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаКорректировкаДолга;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СозданныеДокументыТекстОшибки");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "СозданныеДокументы.ТекстОшибки", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СозданныеДокументыСсылка");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "СозданныеДокументы.Ссылка", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверкаЗаполнения()
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаГлавная Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
			ТекстСообщения = НСтр("ru = 'Не указана организация'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"Организация");
			Возврат Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.КонтрагентИсточник) Тогда
			ТекстСообщения = НСтр("ru = 'Не указан контрагент ООО ""Вайлдберриз""'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"КонтрагентИсточник");
			Возврат Ложь;
		КонецЕсли;
	
		Если НЕ ЗначениеЗаполнено(Объект.КонтрагентПриемник) Тогда
			СоздатьКонтрагентаРВБНаСервере();
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ДатаПереходаПрав) Тогда
			Объект.ДатаПереходаПрав = '20240805';
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОстаткиТоваров()
	
	ДатаОстатков = НачалоДня(Объект.ДатаПереходаПрав) - 1;
	Запрос = Новый Запрос;
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Контрагент", Объект.КонтрагентИсточник);
	Запрос.УстановитьПараметр("ДатаОстатков", Новый Граница(ДатаОстатков, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Счета45", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные));
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Номенклатура,
	|	ХозрасчетныйОстатки.КоличествоОстатокДт КАК Количество,
	|	ХозрасчетныйОстатки.Счет КАК СчетУчета,
	|	ХозрасчетныйОстатки.Подразделение КАК Подразделение
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаОстатков,
	|			Счет В (&Счета45),
	|			&ВидыСубконто,
	|			Организация = &Организация
	|				И Субконто2 = &Контрагент) КАК ХозрасчетныйОстатки";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицаТоваров.Очистить();
	
	Для каждого СтрокаТовара Из Результат Цикл
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара);
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДокументов()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Контрагент", Объект.КонтрагентИсточник);
	Запрос.УстановитьПараметр("ДатаПерехода", Объект.ДатаПереходаПрав);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Дата КАК Дата
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата >= &ДатаПерехода
	|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
	|	И РеализацияТоваровУслуг.Организация = &Организация
	|	И (РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПередачаТоваров)
	|			ИЛИ РеализацияТоваровУслуг.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ВыкупТоваровКомиссионером))
	|	И РеализацияТоваровУслуг.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажах.Ссылка,
	|	ОтчетКомиссионераОПродажах.Дата
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|ГДЕ
	|	ОтчетКомиссионераОПродажах.Дата >= &ДатаПерехода
	|	И ОтчетКомиссионераОПродажах.Организация = &Организация
	|	И ОтчетКомиссионераОПродажах.Контрагент = &Контрагент
	|	И (ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОРозничныхПродажах)
	|			ИЛИ ОтчетКомиссионераОПродажах.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОСписании))
	|	И ОтчетКомиссионераОПродажах.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателя.Ссылка,
	|	ВозвратТоваровОтПокупателя.Дата
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Дата >= &ДатаПерехода
	|	И ВозвратТоваровОтПокупателя.Организация = &Организация
	|	И ВозвратТоваровОтПокупателя.Контрагент = &Контрагент
	|	И ВозвратТоваровОтПокупателя.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВозвратТоваровОтПокупателя.ПереданныеТовары)
	|	И ВозвратТоваровОтПокупателя.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	СписокДокументов.Очистить();
	
	Пока Результат.Следующий() Цикл
		СписокДокументов.Добавить(Результат.Ссылка, , Истина);
	КонецЦикла
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСопоставленнойНоменклатуры()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец",      Объект.КонтрагентИсточник);
	Запрос.УстановитьПараметр("НовыйВладелец", Объект.КонтрагентПриемник);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураКонтрагентов.Номенклатура КАК Номенклатура,
	|	НоменклатураКонтрагентов.Ссылка КАК НоменклатураКонтрагента,
	|	НоменклатураКонтрагентов.Идентификатор КАК Идентификатор
	|ИЗ
	|	Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентовПриемник
	|		ПО НоменклатураКонтрагентов.Номенклатура = НоменклатураКонтрагентовПриемник.Номенклатура
	|			И НоменклатураКонтрагентов.Идентификатор = НоменклатураКонтрагентовПриемник.Идентификатор
	|			И (НоменклатураКонтрагентовПриемник.ВладелецНоменклатуры = &НовыйВладелец)
	|ГДЕ
	|	НоменклатураКонтрагентов.ВладелецНоменклатуры = &Владелец
	|	И НЕ НоменклатураКонтрагентов.Недействителен
	|	И (НоменклатураКонтрагентовПриемник.Ссылка ЕСТЬ NULL ИЛИ НоменклатураКонтрагентовПриемник.ПометкаУдаления)";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	ТаблицаСопоставленияНоменклатуры.Очистить();
	
	Пока Результат.Следующий() Цикл
		НоваяСтрока = ТаблицаСопоставленияНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
		НоваяСтрока.Пометка = Истина;
	КонецЦикла
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткиЗадолженности()

	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Дата",               НачалоДня(Объект.ДатаПереходаПрав) - 1);
	ПараметрыЗаполнения.Вставить("Ссылка",             Документы.КорректировкаДолга.ПустаяСсылка());
	ПараметрыЗаполнения.Вставить("ЭтоНовый",           Истина);
	ПараметрыЗаполнения.Вставить("Организация",        Объект.Организация);
	ПараметрыЗаполнения.Вставить("ВалютаДокумента",    Константы.ВалютаРегламентированногоУчета.Получить());
	ПараметрыЗаполнения.Вставить("КурсДокумента",      1);
	ПараметрыЗаполнения.Вставить("КратностьДокумента", 1);
	ПараметрыЗаполнения.Вставить("ВидОперации",        Перечисления.ВидыОперацийКорректировкаДолга.ПереносЗадолженности);
	
	СписокВидовДоговоровДебиторскойЗадолженности = Новый Массив;
	СписокВидовДоговоровДебиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	СписокВидовДоговоровДебиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
	СписокВидовДоговоровДебиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	
	СписокВидовДоговоровКредиторскойЗадолженности = Новый Массив;
	СписокВидовДоговоровКредиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	СписокВидовДоговоровКредиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером);
	СписокВидовДоговоровКредиторскойЗадолженности.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	
	ЗаполняемыеТаблицы = Новый ТаблицаЗначений;
	ЗаполняемыеТаблицы.Колонки.Добавить("ПоКонтрагенту",        Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ЗаполняемыеТаблицы.Колонки.Добавить("ВидЗадолженности",     Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЗадолженности"));
	ЗаполняемыеТаблицы.Колонки.Добавить("МассивВидовДоговоров", Новый ОписаниеТипов("Массив"));

	ЗаполняемаяТаблица = ЗаполняемыеТаблицы.Добавить();
	ЗаполняемаяТаблица.ПоКонтрагенту        = Объект.КонтрагентИсточник;
	ЗаполняемаяТаблица.ВидЗадолженности     = Перечисления.ВидыЗадолженности.Дебиторская;
	ЗаполняемаяТаблица.МассивВидовДоговоров = СписокВидовДоговоровДебиторскойЗадолженности;
	
	ЗаполняемаяТаблица = ЗаполняемыеТаблицы.Добавить();
	ЗаполняемаяТаблица.ПоКонтрагенту        = Объект.КонтрагентИсточник;
	ЗаполняемаяТаблица.ВидЗадолженности     = Перечисления.ВидыЗадолженности.Кредиторская;
	ЗаполняемаяТаблица.МассивВидовДоговоров = СписокВидовДоговоровКредиторскойЗадолженности;
			
	ПараметрыЗаполнения.Вставить("ЗаполняемыеТаблицы", ЗаполняемыеТаблицы);
	
	СтруктураДанныхЗаполнения = Новый Структура();
	СтруктураДанныхЗаполнения.Вставить("Успешно",            Истина);
	СтруктураДанныхЗаполнения.Вставить("ЗаполненныеТаблицы", Новый Соответствие);
	
	ТаблицаЗадолженности.Очистить();
	
	Для каждого ЗаполняемаяТаблица Из ПараметрыЗаполнения.ЗаполняемыеТаблицы Цикл
		
		ТаблицаРезультат = Документы.КорректировкаДолга.ПолучитьОстаткиВзаиморасчетовПоВидуЗадолженности(ПараметрыЗаполнения, ЗаполняемаяТаблица);
		СтруктураДанныхЗаполнения.ЗаполненныеТаблицы.Вставить(ЗаполняемаяТаблица.ВидЗадолженности, ТаблицаРезультат);
		
		Для Каждого СтрокаЗадолженности Из ТаблицаРезультат Цикл
			НоваяСтрока = ТаблицаЗадолженности.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗадолженности);
			НоваяСтрока.ВидЗадолженности = ЗаполняемаяТаблица.ВидЗадолженности;
			НоваяСтрока.Пометка = Истина;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#Область СозданиеДокументов

&НаКлиенте
Процедура НачатьСозданиеДокументов()
	
	ФоновоеЗадание = ЗапуститьФоновоеСозданиеДокументов();
	
	Если ФоновоеЗадание = Неопределено Тогда
		ЭтотОбъект.Заголовок = НСтр("ru = 'Ошибка при создании документов'");
		Элементы.ФормаДалее.Заголовок = НСтр("ru = 'Закрыть'");
		Элементы.ФормаНазад.Видимость = Истина;
		Возврат;
	КонецЕсли;
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Истина;
	
	Обработчик = Новый ОписаниеОповещения("ПослеСозданияДокументов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновоеСозданиеДокументов()
	
	ПараметрыПереносаОстатков = Новый Структура;
	ПараметрыПереносаОстатков.Вставить("Организация",                      Объект.Организация);
	ПараметрыПереносаОстатков.Вставить("КонтрагентИсточник",               Объект.КонтрагентИсточник);
	ПараметрыПереносаОстатков.Вставить("КонтрагентПриемник",               Объект.КонтрагентПриемник);
	ПараметрыПереносаОстатков.Вставить("ДоговорКонтрагентаИсточник",       Объект.ДоговорКонтрагентаИсточник);
	ПараметрыПереносаОстатков.Вставить("ДоговорКонтрагентаПриемник",       Объект.ДоговорКонтрагентаПриемник);
	ПараметрыПереносаОстатков.Вставить("ДатаПереходаПрав",                 Объект.ДатаПереходаПрав);
	
	ПараметрыПереносаОстатков.Вставить("ИзменениеНастроекAPI",             ИзменениеНастроекAPI);
	ПараметрыПереносаОстатков.Вставить("НастройкаИнтеграции",              НастройкаИнтеграции);
	
	ПараметрыПереносаОстатков.Вставить("ПереносОстатков",                  ПереносОстатков);
	ПараметрыПереносаОстатков.Вставить("ТаблицаТоваров",                   ТаблицаТоваров.Выгрузить());
	
	ПараметрыПереносаОстатков.Вставить("ИсправлениеДокументов",            ИсправлениеДокументов);
	ПараметрыПереносаОстатков.Вставить("СписокДокументов",                 СписокДокументов);
	
	ПараметрыПереносаОстатков.Вставить("СопоставлениеНоменклатуры",        СопоставлениеНоменклатуры);
	ПараметрыПереносаОстатков.Вставить("ТаблицаСопоставленияНоменклатуры", ТаблицаСопоставленияНоменклатуры.Выгрузить());
	
	ПараметрыПереносаОстатков.Вставить("КорректировкаДолга",               КорректировкаДолга);
	ПараметрыПереносаОстатков.Вставить("ТаблицаЗадолженности",             ТаблицаЗадолженности.Выгрузить());
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Перенос остатков при смене юридического лица Wildberries'");
	
	АдресХранилищаДокументов = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
	
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"Обработки.WildberriesПереходРВБ.ПеренестиОстатки",
		ПараметрыПереносаОстатков, АдресХранилищаДокументов);
	
	Возврат ФоновоеЗадание;
		
КонецФункции

&НаКлиенте
Процедура ПослеСозданияДокументов(Задание, Контекст) Экспорт
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//Элементы.СостояниеЗагрузки.Видимость = Ложь;
	//ЭтотОбъект.Заголовок = НСтр("ru = 'Созданные документы'");
	Элементы.ФормаДалее.Заголовок = НСтр("ru = 'Готово'");
	
	ПослеСозданияДокументовНаСервере();
	
	Для Каждого СозданныйДокумент Из СозданныеДокументы Цикл
		Если ЗначениеЗаполнено(СозданныйДокумент.Ссылка) Тогда
			ОповеститьОбИзменении(СозданныйДокумент.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПослеСозданияДокументовНаСервере()
	
	СозданныеДокументы.Очистить();
	
	ДанныеДокументов = ПолучитьИзВременногоХранилища(АдресХранилищаДокументов);
	Если ТипЗнч(ДанныеДокументов) = Тип("ТаблицаЗначений") Тогда
		Для Каждого Документ Из ДанныеДокументов Цикл
			НоваяСтрока = СозданныеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Документ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущийЭлемент.Имя = "СписокДокументовЗначение" Тогда
		
		ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
		Отказ = Истина;
		Если ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
			ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Значение);
			Если ТипЗнч(ТекущиеДанные.Значение) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы, , , , , );
			ИначеЕсли ТипЗнч(ТекущиеДанные.Значение) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") Тогда
				ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.ФормаОбъекта", ПараметрыФормы, , , , , );
			ИначеЕсли ТипЗнч(ТекущиеДанные.Значение) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
				ОткрытьФорму("Документ.ОтчетКомиссионераОПродажах.ФормаОбъекта", ПараметрыФормы, , , , , );
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


