#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список дат перехода организации с УСН на ОСНО.
//
// Параметры:
//   Организация - СправочникСсылка.Организации - организация, для которой необходимо получить даты перехода.
//
// Возвращаемое значение:
//   СписокЗначений - список дат перехода с УСН на ОСНО
//       * Значение - Дата - конец последнего дня применения УСН.
//       * Представление - Строка - строка вида "Переход на ОСНО с 1 января 2026 г.".
Функция ДатыПереходаСУСННаОСНО(Организация) Экспорт
	
	ДатыПерехода = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиСистемыНалогообложения.Период КАК Период,
	|	НастройкиСистемыНалогообложения.ПрименяетсяУСН,
	|	НастройкиСистемыНалогообложения.ПлательщикНалогаНаПрибыль КАК ПрименяетсяОСНО
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|ГДЕ
	|	НастройкиСистемыНалогообложения.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ДатыПерехода;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	ВПредыдущемПериодеПрименяласьУСН = Ложь;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ПрименяетсяОСНО И ВПредыдущемПериодеПрименяласьУСН Тогда
			ДатаПерехода = НачалоДня(Выборка.Период) - 1;
			Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Переход на ОСНО с %1'"), Формат(Выборка.Период, "ДЛФ=DD;"));
			ДатыПерехода.Добавить(ДатаПерехода, Представление);
		КонецЕсли;
		ВПредыдущемПериодеПрименяласьУСН = Выборка.ПрименяетсяУСН;
	КонецЦикла;
	
	Возврат ДатыПерехода;
	
КонецФункции

// Возвращает данные по операциям на основе переданных параметров.
//
// Параметры:
//   ПараметрыВыполнения - Структура - содержит параметры выполнения, 
//       * Организация - СправочникСсылка.Организации - организация, для которой формируются данные.
//       * ДатаПерехода - Дата - дата перехода с УСН на ОСНО.
//
// Возвращаемое значение:
//   Структура - содержит данные по операциям перехода:
//       * ВидОперации - ПеречислениеСсылка.ВидыОперацийПереходаСУСННаОСНО - вид операции,
//       * Состояние - ПеречислениеСсылка.ВидыСостоянийРегламентныхОпераций - состояние операции,
//       * Организация - СправочникСсылка.Организации - организация,
//       * НаправлениеПерехода - ПеречислениеСсылка.НаправленияИзмененийНалоговыхРежимов - направление перехода,
//       * ДатаПерехода - Дата - дата перехода,
//       * Актуальность - Булево - признак актуальности,
//       * Сумма - Число - сумма операции.
Функция ДанныеПоОперациям(ПараметрыВыполнения) Экспорт
	
	Организация = ПараметрыВыполнения.Организация;
	ДатаПерехода = ПараметрыВыполнения.ДатаПерехода;
	
	ТаблицаОпераций = ТаблицаОперацийИзРегистра(Организация, ДатаПерехода);
	
	ПроверитьЗаполнитьАктуальность(ТаблицаОпераций);
	
	ДанныеПоОперациям = Новый Структура;
	Для Каждого Строка Из ТаблицаОпераций Цикл
		Ключ = XMLСтрока(Строка.ВидОперации);
		Параметры = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Строка);
		Параметры.Состояние = XMLСтрока(Параметры.Состояние);
		ДанныеПоОперациям.Вставить(Ключ, Параметры);
	КонецЦикла;
	
	ПараметрыВыполнения.Вставить("ДанныеПоОперациям", ДанныеПоОперациям);
	ОбновитьЗаписиРегистра(ПараметрыВыполнения);
	
	Возврат ДанныеПоОперациям;
	
КонецФункции

// Обновляет записи регистра сведений "ВыполнениеОперацийИзмененияНалоговогоРежима" на основании переданных данных.
//
// Параметры:
//   ПараметрыВыполнения - Структура - содержит параметры для обновления записей регистра
//     * Организация - СправочникСсылка.Организации - организация, для которой обновляются записи
//     * ДатаПерехода - Дата - дата перехода с УСН на ОСНО
//     * ДанныеПоОперациям - Структура - см. ДанныеПоОперациям()
Процедура ОбновитьЗаписиРегистра(ПараметрыВыполнения) Экспорт
	
	НаборЗаписей = РегистрыСведений.ВыполнениеОперацийИзмененияНалоговогоРежима.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Организация.Установить(ПараметрыВыполнения.Организация);
	НаборЗаписей.Отбор.ДатаПерехода.Установить(ПараметрыВыполнения.ДатаПерехода);
	НаборЗаписей.Отбор.НаправлениеПерехода.Установить(Перечисления.НаправленияИзмененийНалоговыхРежимов.УСНнаОСН);
	
	Для Каждого Свойство Из ПараметрыВыполнения.ДанныеПоОперациям Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Свойство.Значение);
		НоваяЗапись.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций[Свойство.Значение.Состояние];
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

// Вводит остатки по налоговому учету.
//
// Параметры:
//   Параметры - Структура - содержит параметры для ввода остатков
//     * Организация - СправочникСсылка.Организации - организация, для которой вводятся остатки
//     * ДатаПерехода - Дата - дата перехода с УСН на ОСНО
Процедура ВвестиОстаткиПоНалоговомуУчету(Параметры) Экспорт
	
	НовыеДокументы = Новый Массив;
	РанееВведенныеДокументы = РанееВведенныеДокументы(Параметры);
	ВестиУчетПоПодразделениям = БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям();
	
	ПараметрыСозданияОбновленияДокументов = НовыйПараметрыСозданияОбновленияДокументов(Параметры);
	ПараметрыСозданияОбновленияДокументов.Вставить("РанееВведенныеДокументы", РанееВведенныеДокументы);
	
	УдалитьРанееВведенныеДокументы(РанееВведенныеДокументы);
	
	Запрос = Новый Запрос;
	УстановитьПараметрыЗапроса(Запрос, Параметры);
	Запрос.Текст = ТекстЗапросаВводОстатковПоНалоговомуУчету();
	
	Результат = Запрос.ВыполнитьПакет();
	
	РезультатНоменклатураНаСкладе = Результат[1];
	Если Не РезультатНоменклатураНаСкладе.Пустой() Тогда
		Если ВестиУчетПоПодразделениям Тогда
			ВыборкаПодразделение = РезультатНоменклатураНаСкладе.Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам, "Подразделение");
			Пока ВыборкаПодразделение.Следующий() Цикл
				ПараметрыСозданияОбновленияДокументов.Подразделение = ВыборкаПодразделение.Подразделение;
				ВыборкаРазделыУчета = ВыборкаПодразделение.Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам, "РазделУчета");
				Пока ВыборкаРазделыУчета.Следующий() Цикл
					РазделУчета = ВыборкаРазделыУчета.РазделУчета;
					Выборка = ВыборкаРазделыУчета.Выбрать();
					НовыйДокумент = СоздатьОбновитьДокументВводаОстатков(
					ПараметрыСозданияОбновленияДокументов, РазделУчета, Выборка);
					Если НовыйДокумент <> Неопределено Тогда
						НовыеДокументы.Добавить(НовыйДокумент);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		Иначе
			ПараметрыСозданияОбновленияДокументов.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			ВыборкаРазделыУчета = РезультатНоменклатураНаСкладе.Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам, "РазделУчета");
			Пока ВыборкаРазделыУчета.Следующий() Цикл
				РазделУчета = ВыборкаРазделыУчета.РазделУчета;
				Выборка = ВыборкаРазделыУчета.Выбрать();
				НовыйДокумент = СоздатьОбновитьДокументВводаОстатков(
					ПараметрыСозданияОбновленияДокументов, РазделУчета, Выборка);
				Если НовыйДокумент <> Неопределено Тогда
					НовыеДокументы.Добавить(НовыйДокумент);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	РезультатРасчетыСПерсоналомПоОплатеТруда = Результат[2];
	Если Не РезультатРасчетыСПерсоналомПоОплатеТруда.Пустой() Тогда
		ПараметрыСозданияОбновленияДокументов.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
		ВыборкаРазделыУчета = РезультатРасчетыСПерсоналомПоОплатеТруда.Выбрать(
			ОбходРезультатаЗапроса.ПоГруппировкам, "РазделУчета");
		Пока ВыборкаРазделыУчета.Следующий() Цикл
			РазделУчета = ВыборкаРазделыУчета.РазделУчета;
			Выборка = ВыборкаРазделыУчета.Выбрать();
			НовыйДокумент = СоздатьОбновитьДокументВводаОстатков(
				ПараметрыСозданияОбновленияДокументов, РазделУчета, Выборка);
			Если НовыйДокумент <> Неопределено Тогда
				НовыеДокументы.Добавить(НовыйДокумент);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РезультатПрочиеСчета = Результат[0];
	Если Не РезультатПрочиеСчета.Пустой() Тогда
		Если ВестиУчетПоПодразделениям Тогда
			ВыборкаПодразделение = РезультатПрочиеСчета.Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам, "Подразделение");
			Пока ВыборкаПодразделение.Следующий() Цикл
				ПараметрыСозданияОбновленияДокументов.Подразделение = ВыборкаПодразделение.Подразделение;
				ВыборкаРазделыУчета = ВыборкаПодразделение.Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам, "РазделУчета");
				Пока ВыборкаРазделыУчета.Следующий() Цикл
					РазделУчета = ВыборкаРазделыУчета.РазделУчета;
					Выборка = ВыборкаРазделыУчета.Выбрать();
					НовыйДокумент = СоздатьОбновитьДокументВводаОстатков(
					ПараметрыСозданияОбновленияДокументов, РазделУчета, Выборка);
					Если НовыйДокумент <> Неопределено Тогда
						НовыеДокументы.Добавить(НовыйДокумент);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
	Иначе
			ПараметрыСозданияОбновленияДокументов.Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			ВыборкаРазделыУчета = РезультатПрочиеСчета.Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам, "РазделУчета");
			Пока ВыборкаРазделыУчета.Следующий() Цикл
				РазделУчета = ВыборкаРазделыУчета.РазделУчета;
				Выборка = ВыборкаРазделыУчета.Выбрать();
				НовыйДокумент = СоздатьОбновитьДокументВводаОстатков(
					ПараметрыСозданияОбновленияДокументов, РазделУчета, Выборка);
				Если НовыйДокумент <> Неопределено Тогда
					НовыеДокументы.Добавить(НовыйДокумент);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если НовыеДокументы.Количество() > 0 Тогда
		ДобавитьДокументыВРегистр(НовыеДокументы, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Отражает доходы переходного периода.
//
// Параметры:
//   Параметры - Структура - содержит параметры для отражения доходов
//     * Организация - СправочникСсылка.Организации - организация, для которой выполняется отражение доходов
//     * ДатаПерехода - Дата - дата перехода с УСН на ОСНО
Процедура ОтразитьДоходы(Параметры) Экспорт
	
	НовыеДокументы = Новый Массив;
	РанееВведенныеДокументы = РанееВведенныеДокументы(Параметры);
	
	ПараметрыСозданияОбновленияДокументов = НовыйПараметрыСозданияОбновленияДокументов(Параметры);
	ПараметрыСозданияОбновленияДокументов.Вставить("РанееВведенныеДокументы", РанееВведенныеДокументы);
	
	УдалитьРанееВведенныеДокументы(РанееВведенныеДокументы);
	
	Запрос = Новый Запрос;
	УстановитьПараметрыЗапроса(Запрос, Параметры);
	Запрос.Текст = ТекстЗапросаДебиторскаяЗадолженность();
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	НовыйДокумент = СоздатьОбновитьДокументОперацияБухДоходы(ПараметрыСозданияОбновленияДокументов, Выборка);
	Если НовыйДокумент <> Неопределено Тогда
		НовыеДокументы.Добавить(НовыйДокумент);
	КонецЕсли;
	
	Если НовыеДокументы.Количество() > 0 Тогда
		ДобавитьДокументыВРегистр(НовыеДокументы, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Отражает расходы переходного периода.
//
// Параметры:
//   Параметры - Структура - содержит параметры для отражения расходов
//       * Организация - СправочникСсылка.Организации - Организация, для которой выполняется отражение расходов
//       * ДатаПерехода - Дата - Дата перехода с УСН на ОСНО
Процедура ОтразитьРасходы(Параметры) Экспорт
	
	НовыеДокументы = Новый Массив;
	РанееВведенныеДокументы = РанееВведенныеДокументы(Параметры);
	
	ПараметрыСозданияОбновленияДокументов = НовыйПараметрыСозданияОбновленияДокументов(Параметры);
	ПараметрыСозданияОбновленияДокументов.Вставить("РанееВведенныеДокументы", РанееВведенныеДокументы);
	
	УдалитьРанееВведенныеДокументы(РанееВведенныеДокументы);
	
	Запрос = Новый Запрос;
	УстановитьПараметрыЗапроса(Запрос, Параметры);
	Запрос.Текст = ТекстЗапросаРасходыПоТоварамИУслугам();
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Содержание = НСтр("ru = 'Признание расходов по ОСНО при переходе с УСН (товары, работы, услуги)'");
		НовыйДокумент = СоздатьОбновитьДокументОперацияБухРасходы(
			ПараметрыСозданияОбновленияДокументов, Выборка, Содержание);
		Если НовыйДокумент <> Неопределено Тогда
			НовыеДокументы.Добавить(НовыйДокумент);
		КонецЕсли;
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапросаРасходыПоЗарплате();
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Содержание = НСтр("ru = 'Признание расходов по ОСНО при переходе с УСН (Зарплата и страховые взносы)'");
		НовыйДокумент = СоздатьОбновитьДокументОперацияБухРасходы(
			ПараметрыСозданияОбновленияДокументов, Выборка, Содержание);
		Если НовыйДокумент <> Неопределено Тогда
			НовыеДокументы.Добавить(НовыйДокумент);
		КонецЕсли;
	КонецЕсли;
	
	Если НовыеДокументы.Количество() > 0 Тогда
		ДобавитьДокументыВРегистр(НовыеДокументы, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Удаляет ранее созданные документы по оперделенной операции перехода с УСН на ОСНО.
//
// Параметры:
//   Параметры - Структура - содержит параметры для выполнения удаления
//       * Организация - СправочникСсылка.Организации - Организация, для которой выполняется отражение расходов
//       * ДатаПерехода - Дата - Дата перехода с УСН на ОСНО
//       * ВидОперации - ПеречислениеСсылка.ВидыОперацийПереходаСУСННаОСНО - вид операции
Процедура УдалитьРанееСозданныеДокументыПоОперации(Параметры) Экспорт
	
	ВидОперации = Параметры.ВидОперации;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводОстатковПоНалоговомуУчету И ВидОперации
		<> Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводДоходовПереходногоПериода И ВидОперации
		<> Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводРасходовПереходногоПериода Тогда
		Возврат;
	КонецЕсли;
	
	РанееВведенныеДокументы = РанееВведенныеДокументы(Параметры);
	
	НачатьТранзакцию();
	
	Попытка
		УдалитьРанееВведенныеДокументы(РанееВведенныеДокументы);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

// Возвращает таблицу с данными для расшифровки сумм доходов или расходов.
//
// Параметры:
//   Параметры - Структура - содержит настройки для формирования данных отчета
//       * Организация - СправочникСсылка.Организации - Организация, для которой формируются данные 
//       * ДатаПерехода - Дата - Дата перехода организации с УСН на ОСНО
//       * ДоходРасход - Строка - определяет, какую сумму необходимо расшифровать. 
//       Допустимые значения: "Доход" или "Расход".
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с данными расшифровки: 
//    * Сумма - Число - сумма расчетного документа;
//    * Субконто3 - ДокументСсылка - расчетный документ.
Функция ДанныеДляРасшифровкиСуммДоходовРасходов(Знач Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Если Параметры.ДоходРасход = "Доход" Тогда
		Запрос.Текст = ТекстЗапросаДебиторскаяЗадолженность(Ложь);
	Иначе
		Запрос.Текст = ТекстЗапросаРасходыПоТоварамИУслугам(Ложь)
			+ ОбщегоНазначения.ТекстОбъединитьВсе()
			+ ТекстЗапросаРасходыПоЗарплате(Ложь);
	КонецЕсли;
	
	УстановитьПараметрыЗапроса(Запрос, Параметры);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Таблица.Свернуть("Субконто3", "Сумма");
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТаблицаОперацийИзРегистра(Организация, ДатаПерехода)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаПерехода", ДатаПерехода);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыполнениеОперацийИзмененияНалоговогоРежима.ВидОперации,
	|	ВыполнениеОперацийИзмененияНалоговогоРежима.Состояние,
	|	ВыполнениеОперацийИзмененияНалоговогоРежима.Организация,
	|	ВыполнениеОперацийИзмененияНалоговогоРежима.НаправлениеПерехода,
	|	ВыполнениеОперацийИзмененияНалоговогоРежима.ДатаПерехода,
	|	ВыполнениеОперацийИзмененияНалоговогоРежима.Актуальность,
	|	0 КАК Сумма
	|ИЗ
	|	РегистрСведений.ВыполнениеОперацийИзмененияНалоговогоРежима КАК ВыполнениеОперацийИзмененияНалоговогоРежима
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ВидыОперацийПереходаСУСННаОСНО КАК ВидыОперацийПереходаСУСННаОСНО
	|		ПО ВыполнениеОперацийИзмененияНалоговогоРежима.ВидОперации = ВидыОперацийПереходаСУСННаОСНО.Ссылка
	|ГДЕ
	|	ВыполнениеОперацийИзмененияНалоговогоРежима.Организация = &Организация
	|	И ВыполнениеОперацийИзмененияНалоговогоРежима.ДатаПерехода = НАЧАЛОПЕРИОДА(&ДатаПерехода, ДЕНЬ)
	|	И
	|		ВыполнениеОперацийИзмененияНалоговогоРежима.НаправлениеПерехода = ЗНАЧЕНИЕ(Перечисление.НаправленияИзмененийНалоговыхРежимов.УСНнаОСН)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	ТаблицаОпераций = Запрос.Выполнить().Выгрузить();
	Если ТаблицаОпераций.Количество() = 0 Тогда
		ТаблицаОпераций = НовыйТаблицаОпераций(Организация, ДатаПерехода);
	КонецЕсли;
	
	Возврат ТаблицаОпераций;
	
КонецФункции

Функция НовыйТаблицаОпераций(Организация, ДатаПерехода)
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ВидОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПереходаСУСННаОСНО"));
	Таблица.Колонки.Добавить("Состояние", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыСостоянийРегламентныхОпераций"));
	Таблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("НаправлениеПерехода",
		Новый ОписаниеТипов("ПеречислениеСсылка.НаправленияИзмененийНалоговыхРежимов"));
	Таблица.Колонки.Добавить("ДатаПерехода", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Таблица.Колонки.Добавить("Актуальность", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Таблица.Добавить().ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводНачальныхОстатковПоОС;
	Таблица.Добавить().ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводНачальныхОстатковПоНМА;
	Таблица.Добавить().ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ПодготовкаКВычетуНДС;
	Таблица.Добавить().ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводОстатковПоНалоговомуУчету;
	Таблица.Добавить().ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводДоходовПереходногоПериода;
	Таблица.Добавить().ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводРасходовПереходногоПериода;
	
	Таблица.ЗаполнитьЗначения(Организация, "Организация");
	Таблица.ЗаполнитьЗначения(Перечисления.ВидыСостоянийРегламентныхОпераций.НеВыполнено, "Состояние");
	Таблица.ЗаполнитьЗначения(Перечисления.НаправленияИзмененийНалоговыхРежимов.УСНнаОСН, "НаправлениеПерехода");
	Таблица.ЗаполнитьЗначения(ДатаПерехода, "ДатаПерехода");
	Таблица.ЗаполнитьЗначения(Ложь, "Актуальность");
	
	Возврат Таблица;
	
КонецФункции

#Область ПроверитьЗаполнитьАктуальность

Процедура ПроверитьЗаполнитьАктуальность(ТаблицаОпераций)
	
	Если ТаблицаОпераций.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Организация = ТаблицаОпераций[0].Организация;
	ДатаПерехода = КонецДня(ТаблицаОпераций[0].ДатаПерехода);
	ПрименяетсяУСНДоходыМинусРасходы = УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходыЗаПериод(
		Организация, НачалоДня(ДатаПерехода), ДатаПерехода);
	ПрименяетсяОбщаяСтавкаНДС = УчетНДС.ПрименяетсяОбщаяСтавкаНДС(Организация, ДатаПерехода);
	
	Справочники.ПрочиеДоходыИРасходы.ДобавитьРеквизит_ДоходыИРасходыПриПереходеСУСННаОСНО();
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", Организация);
	Параметры.Вставить("ДатаПерехода", ДатаПерехода);
	
	НомераТаблиц = Новый Структура;
	НомераТаблиц.Вставить("АктуальныйИндекс", -1);
	ТекстЗапрос = ТекстЗапросаПоВнеоборотнымАктивам(НомераТаблиц) + ТекстЗапросаПоВнеоборотнымАктивам(НомераТаблиц,
		"НМА");
	
	Если Не ПрименяетсяОбщаяСтавкаНДС Тогда
		ТекстЗапрос = ТекстЗапрос + ТекстЗапросаПоНДС(НомераТаблиц);
	КонецЕсли;
	
	ТекстЗапрос = ТекстЗапрос + ТекстЗапросаПоОстаткамНУ(НомераТаблиц) + ТекстЗапросаДоходыПереходногоПериода(
		НомераТаблиц);
	
	Если ПрименяетсяУСНДоходыМинусРасходы Тогда
		ТекстЗапрос = ТекстЗапрос + ТекстЗапросаРасходыПереходногоПериода(НомераТаблиц);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	УстановитьПараметрыЗапроса(Запрос, Параметры);
	Запрос.Текст = ТекстЗапрос;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого Строка Из ТаблицаОпераций Цикл
		Индекс = Неопределено;
		Если Строка.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводНачальныхОстатковПоОС
			И НомераТаблиц.Свойство("ОС", Индекс) Тогда
			ПроверитьЗаполнитьАктуальностьВнеоборотныхАктивов(Строка, Результат[Индекс].Выгрузить());
		ИначеЕсли Строка.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводНачальныхОстатковПоНМА
			И НомераТаблиц.Свойство("НМА", Индекс) Тогда
			ПроверитьЗаполнитьАктуальностьВнеоборотныхАктивов(Строка, Результат[Индекс].Выгрузить());
		ИначеЕсли Строка.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ПодготовкаКВычетуНДС
			И НомераТаблиц.Свойство("НДС", Индекс) Тогда
			ПроверитьЗаполнитьАктуальностьПодготовкаКВычетуНДС(Строка, Результат[Индекс].Выгрузить());
		ИначеЕсли Строка.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводОстатковПоНалоговомуУчету
			И НомераТаблиц.Свойство("ОстаткиНУ", Индекс) Тогда
			ПроверитьЗаполнитьАктуальностьОстаткиНУ(Строка, Результат[Индекс].Выгрузить());
		ИначеЕсли Строка.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводДоходовПереходногоПериода
			И НомераТаблиц.Свойство("Доходы", Индекс) Тогда
			ПроверитьЗаполнитьАктуальностьДоходыПереходногоПериода(Строка, Результат[Индекс].Выгрузить());
		ИначеЕсли Строка.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводРасходовПереходногоПериода
			И НомераТаблиц.Свойство("Расходы", Индекс) Тогда
			ПроверитьЗаполнитьАктуальностьРасходыПереходногоПериода(Строка, Результат[Индекс].Выгрузить());
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьАктуальностьВнеоборотныхАктивов(Строка, Данные)
	
	Строка.Актуальность = Истина;
	Если Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Пропущено
		Или Данные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДанных = Данные[0];
	
	Строка.Актуальность = (СтрокаДанных.ПрименяласьОСНОДоУСН И СтрокаДанных.Количество > 0)
		Или СтрокаДанных.КоличествоВведенныхДокументов > 0;
	
	Если СтрокаДанных.КоличествоВведенныхДокументов > 0 Тогда
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Выполнено;
	Иначе
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.НеВыполнено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьАктуальностьПодготовкаКВычетуНДС(Строка, Данные)
	
	Строка.Актуальность = Истина;
	Если Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Пропущено
		Или Данные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДанных = Данные[0];
	
	Если СтрокаДанных.КоличествоВведенныхДокументов > 0 Тогда
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Выполнено;
	Иначе
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.НеВыполнено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьАктуальностьОстаткиНУ(Строка, Данные)
	
	Если Данные.Количество() = 0 Тогда
		Строка.Актуальность = Истина;
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Выполнено;
		Возврат;
	КонецЕсли;
	
	СтрокаДанных = Данные[0];
	
	Строка.Актуальность = Истина;
	
	Если Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Пропущено Тогда
		Возврат;
	ИначеЕсли СтрокаДанных.КоличествоРасхождений > 0 И СтрокаДанных.КоличествоВведенныхДокументов > 0 Тогда
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.ВыполненоСОшибками;
	ИначеЕсли СтрокаДанных.КоличествоРасхождений > 0 Тогда
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.НеВыполнено;
	Иначе
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Выполнено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьАктуальностьДоходыПереходногоПериода(Строка, Данные)
	
	Строка.Актуальность = Истина;
	Если Данные.Количество() = 0 Тогда
		Строка.Сумма = 0;
		Возврат;
	КонецЕсли;
	
	СтрокаДанных = Данные[0];
	КоличествоВведенныхДокументов = СтрокаДанных.КоличествоВведенныхДокументов;
	СуммаДебиторскойЗадолженности = СтрокаДанных.СуммаДебиторскойЗадолженности;
	СуммаДебиторскойЗадолженностиПоДокументам = СтрокаДанных.СуммаДебиторскойЗадолженностиПоДокументам;
	
	Строка.Сумма = СуммаДебиторскойЗадолженности;
	
	Если Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Пропущено Тогда
		Возврат;
	ИначеЕсли КоличествоВведенныхДокументов > 0
		И СуммаДебиторскойЗадолженности	<> СуммаДебиторскойЗадолженностиПоДокументам Тогда
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.ВыполненоСОшибками;
	ИначеЕсли СуммаДебиторскойЗадолженности = СуммаДебиторскойЗадолженностиПоДокументам Тогда
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Выполнено;
	Иначе
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.НеВыполнено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнитьАктуальностьРасходыПереходногоПериода(Строка, Данные)
	
	Строка.Актуальность = Истина;
	Если Данные.Количество() = 0 Тогда
		Строка.Сумма = 0;
		Возврат;
	КонецЕсли;
	
	СтрокаДанных = Данные[0];
	КоличествоВведенныхДокументов = СтрокаДанных.КоличествоВведенныхДокументов;
	СуммаРасходов = СтрокаДанных.СуммаРасходов;
	СуммаРасходовПоДокументам = СтрокаДанных.СуммаРасходовПоДокументам;
	
	Строка.Сумма = СуммаРасходов;
	
	Если Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Пропущено Тогда
		Возврат;
	ИначеЕсли КоличествоВведенныхДокументов > 0 И СуммаРасходов <> СуммаРасходовПоДокументам Тогда
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.ВыполненоСОшибками;
	ИначеЕсли СуммаРасходов = СуммаРасходовПоДокументам Тогда
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.Выполнено;
	Иначе
		Строка.Состояние = Перечисления.ВидыСостоянийРегламентныхОпераций.НеВыполнено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ТекстыЗапросов

Функция ТекстЗапросаПоВнеоборотнымАктивам(НомераТаблиц, ИмяТаблицы = "ОС")
	
	ИндексВыборкиДанныхВЗапросе = 3;
	ИндексВыборкиДанных = НомераТаблиц.АктуальныйИндекс + ИндексВыборкиДанныхВЗапросе;
	НомераТаблиц.Вставить(ИмяТаблицы, ИндексВыборкиДанных);
	НомераТаблиц.АктуальныйИндекс = ИндексВыборкиДанных;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ХозрасчетныйОстатки.Субконто1) КАК Количество,
	|	ХозрасчетныйОстатки.Организация КАК Организация
	|ПОМЕСТИТЬ вт_КоличествоОС
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментОпределенияОстатков, Счет В (&СчетаОсновныеСредства), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ВводНачальныхОстатков.Ссылка) КАК КоличествоВведенныхДокументов,
	|	ВводНачальныхОстатков.Организация КАК Организация
	|ПОМЕСТИТЬ вт_КоличествоВведенныхДокументовОС
	|ИЗ
	|	Документ.ВводНачальныхОстатков КАК ВводНачальныхОстатков
	|ГДЕ
	|	ВводНачальныхОстатков.Дата = &ДатаПерехода
	|	И ВводНачальныхОстатков.Проведен
	|	И ВводНачальныхОстатков.Организация = &Организация
	|	И ВводНачальныхОстатков.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.ОсновныеСредства)
	|СГРУППИРОВАТЬ ПО
	|	ВводНачальныхОстатков.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСистемыНалогообложенияСрезПоследних.ДатаПереходаНаУСН <> ДАТАВРЕМЯ(1, 1, 1) КАК ПрименяласьОСНОДоУСН,
	|	ЕСТЬNULL(вт_КоличествоВведенныхДокументовОС.КоличествоВведенныхДокументов, 0) КАК КоличествоВведенныхДокументов,
	|	ЕСТЬNULL(вт_КоличествоОС.Количество, 0) КАК Количество
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&ДатаПерехода, Организация = &Организация) КАК
	|		НастройкиСистемыНалогообложенияСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_КоличествоВведенныхДокументовОС КАК вт_КоличествоВведенныхДокументовОС
	|		ПО НастройкиСистемыНалогообложенияСрезПоследних.Организация = вт_КоличествоВведенныхДокументовОС.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_КоличествоОС КАК вт_КоличествоОС
	|		ПО НастройкиСистемыНалогообложенияСрезПоследних.Организация = вт_КоличествоОС.Организация";
	
	Если ИмяТаблицы = "НМА" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.ОсновныеСредства)",
			"ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.НематериальныеАктивыИНИОКР)");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СчетаОсновныеСредства", "&СчетаНематериальныеАктивы");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "вт_КоличествоОС", "вт_КоличествоНМА");
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса, "вт_КоличествоВведенныхДокументовОС", "вт_КоличествоВведенныхДокументовНМА");
	КонецЕсли;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоНДС(НомераТаблиц)
	
	ИндексВыборкиДанныхВЗапросе = 1;
	ИндексВыборкиДанных = НомераТаблиц.АктуальныйИндекс + ИндексВыборкиДанныхВЗапросе;
	НомераТаблиц.Вставить("НДС", ИндексВыборкиДанных);
	НомераТаблиц.АктуальныйИндекс = ИндексВыборкиДанных;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ВводНачальныхОстатков.Ссылка) КАК КоличествоВведенныхДокументов
	|ИЗ
	|	Документ.ВводНачальныхОстатков КАК ВводНачальныхОстатков
	|ГДЕ
	|	ВводНачальныхОстатков.Дата = &ДатаПерехода
	|	И ВводНачальныхОстатков.Проведен
	|	И ВводНачальныхОстатков.Организация = &Организация
	|	И ВводНачальныхОстатков.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.ПустаяСсылка)";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаПоОстаткамНУ(НомераТаблиц)
	
	ИндексВыборкиДанныхВЗапросе = 2;
	ИндексВыборкиДанных = НомераТаблиц.АктуальныйИндекс + ИндексВыборкиДанныхВЗапросе;
	НомераТаблиц.Вставить("ОстаткиНУ", ИндексВыборкиДанных);
	НомераТаблиц.АктуальныйИндекс = ИндексВыборкиДанных;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК КоличествоРасхождений,
	|	0 КАК КоличествоВведенныхДокументов
	|ПОМЕСТИТЬ ВТ_ОстаткиНУ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаНачалаПримененияОСНО, Счет.НалоговыйУчет
	|	И НЕ Счет В (&СчетаВнеоборотныеАктивыИАмортизация), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстаток <> ХозрасчетныйОстатки.СуммаНУОстаток
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	КОЛИЧЕСТВО(*)
	|ИЗ
	|	РегистрСведений.ДокументыОперацийИзмененияНалоговогоРежима КАК ДокументыОперацийИзмененияНалоговогоРежима
	|ГДЕ
	|	ДокументыОперацийИзмененияНалоговогоРежима.Организация = &Организация
	|	И
	|		ДокументыОперацийИзмененияНалоговогоРежима.НаправлениеПерехода = ЗНАЧЕНИЕ(Перечисление.НаправленияИзмененийНалоговыхРежимов.УСНнаОСН)
	|	И ДокументыОперацийИзмененияНалоговогоРежима.ДатаПерехода = НАЧАЛОПЕРИОДА(&ДатаПерехода, ДЕНЬ)
	|	И
	|		ДокументыОперацийИзмененияНалоговогоРежима.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПереходаСУСННаОСНО.ВводОстатковПоНалоговомуУчету)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(вт_ОстаткиНУ.КоличествоРасхождений) КАК КоличествоРасхождений,
	|	СУММА(вт_ОстаткиНУ.КоличествоВведенныхДокументов) КАК КоличествоВведенныхДокументов
	|ИЗ
	|	вт_ОстаткиНУ КАК вт_ОстаткиНУ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВводОстатковПоНалоговомуУчету()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Валюта КАК Валюта,
	|	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК Сумма,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаРасчетыСПоставщикамиИПодрядчиками)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.РасчетыСПоставщикамиИПодрядчиками)
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаРасчетыСПокупателямиИЗаказчиками)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.РасчетыСПокупателямиИЗаказчиками)
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаРасчетыСПрочимиДебиторамиИКредиторами)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.РасчетыСПрочимиДебиторамиИКредиторами)
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаТоварыОтгруженные)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.ТоварыОтгруженные)
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаПродажиПоПлатежнымКартам)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.ДенежныеСредства)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК РазделУчета,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество,
	|	ХозрасчетныйОстатки.СуммаНУОстаток КАК СуммаНУОстаток,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	ХозрасчетныйОстатки.СуммаОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаНачалаПримененияОСНО, Счет.НалоговыйУчет
	|	И (Счет В (&СчетаРасчетыСПоставщикамиИПодрядчиками)
	|	ИЛИ Счет В (&СчетаРасчетыСПокупателямиИЗаказчиками)
	|	ИЛИ Счет В (&СчетаРасчетыСПрочимиДебиторамиИКредиторами)
	|	ИЛИ Счет В (&СчетаТоварыОтгруженные)
	|	ИЛИ Счет В (&СчетаПродажиПоПлатежнымКартам)), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстаток <> ХозрасчетныйОстатки.СуммаНУОстаток
	|ИТОГИ
	|ПО
	|	Подразделение,
	|	РазделУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Валюта КАК Валюта,
	|	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК Сумма,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаМатериалы)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.Материалы)
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаТовары)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.Товары)
	|	КОНЕЦ КАК РазделУчета,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество,
	|	ХозрасчетныйОстатки.СуммаНУОстаток КАК СуммаНУОстаток,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	ХозрасчетныйОстатки.СуммаОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаНачалаПримененияОСНО, Счет.НалоговыйУчет
	|	И (Счет В (&СчетаМатериалы)
	|	ИЛИ Счет В (&СчетаТовары)), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстаток <> ХозрасчетныйОстатки.СуммаНУОстаток
	|ИТОГИ
	|ПО
	|	Подразделение,
	|	РазделУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Валюта КАК Валюта,
	|	ХозрасчетныйОстатки.Подразделение КАК Подразделение,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляВводаОстатков.РасчетыСПерсоналомПоОплатеТрудаИДепонентам) КАК РазделУчета,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество,
	|	ХозрасчетныйОстатки.СуммаНУОстаток КАК СуммаНУОстаток,
	|	ХозрасчетныйОстатки.СуммаОстатокДт,
	|	ХозрасчетныйОстатки.СуммаОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаНачалаПримененияОСНО, Счет.НалоговыйУчет
	|	И Счет В (&СчетаРасчетыСПерсоналомПоОплатеТруда), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстаток <> ХозрасчетныйОстатки.СуммаНУОстаток
	|ИТОГИ
	|ПО
	|	РазделУчета";
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	Если Не БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям() Тогда
		СхемаЗапроса.ПакетЗапросов[0].КонтрольныеТочкиИтогов.Удалить(0);
		СхемаЗапроса.ПакетЗапросов[1].КонтрольныеТочкиИтогов.Удалить(0);
	КонецЕсли;
	
	ИмяСубконтоНеопределено = "Неопределено";
	ПсевдонимСклад = "Склад";
	ПсевдонимПартия = "Партия";
	ВидыСубконтоПоУчетуЗапасов = ПланыСчетов.Хозрасчетный.ВидыСубконтоПоСчету(
		ПланыСчетов.Хозрасчетный.СырьеИМатериалы, Истина);
	ЗапросПакета = СхемаЗапроса.ПакетЗапросов[1];
	
	НайденнаяСтрока = ВидыСубконтоПоУчетуЗапасов.Найти(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии, "ВидСубконто");
	Если НайденнаяСтрока <> Неопределено Тогда
		ИмяСубконто = "ХозрасчетныйОстатки.Субконто" + Строка(НайденнаяСтрока.ПорядокСубконто);
		ДобавитьОператорВЗапрос(ЗапросПакета, ИмяСубконто, ПсевдонимПартия);
	Иначе
		ДобавитьОператорВЗапрос(ЗапросПакета, ИмяСубконтоНеопределено, ПсевдонимПартия);
	КонецЕсли;
	
	НайденнаяСтрока = ВидыСубконтоПоУчетуЗапасов.Найти(
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады, "ВидСубконто");
	Если НайденнаяСтрока <> Неопределено Тогда
		ИмяСубконто = "ХозрасчетныйОстатки.Субконто" + Строка(НайденнаяСтрока.ПорядокСубконто);
		ДобавитьОператорВЗапрос(ЗапросПакета, ИмяСубконто, ПсевдонимСклад);
	Иначе
		ДобавитьОператорВЗапрос(ЗапросПакета, ИмяСубконтоНеопределено, ПсевдонимСклад);
	КонецЕсли;
	
	Возврат СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецФункции

Функция ТекстЗапросаДоходыПереходногоПериода(НомераТаблиц)
	
	ИндексВыборкиДанныхВЗапросе = 6;
	ИндексВыборкиДанных = НомераТаблиц.АктуальныйИндекс + ИндексВыборкиДанныхВЗапросе;
	НомераТаблиц.Вставить("Доходы", ИндексВыборкиДанных);
	НомераТаблиц.АктуальныйИндекс = ИндексВыборкиДанных;
	
	ТекстЗапроса = ТекстЗапросаДебиторскаяЗадолженность(Истина)
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	СУММА(вт_ДоходыСДокументами.Сумма) КАК СуммаДебиторскойЗадолженности,
	|	СУММА(0) КАК СуммаДебиторскойЗадолженностиПоДокументам,
	|	СУММА(0) КАК КоличествоВведенныхДокументов
	|ПОМЕСТИТЬ вт_Доходы
	|ИЗ
	|	вт_ДоходыСДокументами КАК вт_ДоходыСДокументами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ХозрасчетныйОбороты.СуммаНУОборотКт,
	|	0
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&ДатаНачалаПримененияОСНО, ДОБАВИТЬКДАТЕ(&ДатаПерехода, ДЕНЬ,
	|		1), , Счет В (&СчетаПрочиеДоходы), , Организация = &Организация, ,) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.Субконто1 = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ДоходыРасходыПриПереходеСУСННаОСНО)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	КОЛИЧЕСТВО(*)
	|ИЗ
	|	РегистрСведений.ДокументыОперацийИзмененияНалоговогоРежима КАК ДокументыОперацийИзмененияНалоговогоРежима
	|ГДЕ
	|	ДокументыОперацийИзмененияНалоговогоРежима.Организация = &Организация
	|	И
	|		ДокументыОперацийИзмененияНалоговогоРежима.НаправлениеПерехода = ЗНАЧЕНИЕ(Перечисление.НаправленияИзмененийНалоговыхРежимов.УСНнаОСН)
	|	И ДокументыОперацийИзмененияНалоговогоРежима.ДатаПерехода = НАЧАЛОПЕРИОДА(&ДатаПерехода, ДЕНЬ)
	|	И
	|		ДокументыОперацийИзмененияНалоговогоРежима.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПереходаСУСННаОСНО.ВводДоходовПереходногоПериода)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(вт_Доходы.СуммаДебиторскойЗадолженности) КАК СуммаДебиторскойЗадолженности,
	|	СУММА(вт_Доходы.СуммаДебиторскойЗадолженностиПоДокументам) КАК СуммаДебиторскойЗадолженностиПоДокументам,
	|	СУММА(вт_Доходы.КоличествоВведенныхДокументов) КАК КоличествоВведенныхДокументов
	|ИЗ
	|	вт_Доходы КАК вт_Доходы";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаДебиторскаяЗадолженность(СоздатьВиртуальнуюТаблицу = Ложь)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.Субконто3 КАК Субконто3,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	СУММА(ХозрасчетныйОстатки.СуммаОстатокДт) КАК Сумма
	|ПОМЕСТИТЬ вт_ДебиторскаяЗадолженность
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментОпределенияОстатков, Счет В (&СчетаПоДебиторскойЗадолженности), ,
	|		Организация = &Организация) КАК ХозрасчетныйОстатки
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Организация,
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Субконто2,
	|	ХозрасчетныйОстатки.Субконто3,
	|	ХозрасчетныйОстатки.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Регистратор КАК ДокументРеализации,
	|	СУММА(НДСЗаписиКнигиПродажОбороты.НДСОборот) КАК НДСОборот,
	|	СУММА(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот) КАК СуммаБезНДСОборот
	|ПОМЕСТИТЬ вт_НДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(, &МоментОпределенияОстатков, Регистратор,
	|		Организация = &Организация) КАК НДСЗаписиКнигиПродажОбороты
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродажОбороты.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.ДокументРеализации,
	|	СУММА(НДСЗаписиКнигиПродажОбороты.НДСОборот),
	|	СУММА(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот)
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(, &МоментОпределенияОстатков, Регистратор,
	|		Организация = &Организация) КАК НДСЗаписиКнигиПродажОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ПО НДСЗаписиКнигиПродажОбороты.Регистратор = КорректировкаРеализации.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализации.ДокументРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.ДокументРеализации,
	|	СУММА(-НДСПредъявленныйОбороты.НДСОборот),
	|	СУММА(-НДСПредъявленныйОбороты.СуммаБезНДСОборот)
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Обороты( , &МоментОпределенияОстатков, Регистратор, Организация = &Организация) КАК
	|		НДСПредъявленныйОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ПО НДСПредъявленныйОбороты.Регистратор = КорректировкаРеализации.Ссылка
	|ГДЕ
	|	НЕ КорректировкаРеализации.Ссылка ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаРеализации.ДокументРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_НДС.ДокументРеализации КАК ДокументРеализации,
	|	СУММА(вт_НДС.НДСОборот) КАК НДСОборот,
	|	СУММА(вт_НДС.СуммаБезНДСОборот) КАК СуммаБезНДСОборот
	|ПОМЕСТИТЬ вт_НДССвернутая
	|ИЗ
	|	вт_НДС КАК вт_НДС
	|СГРУППИРОВАТЬ ПО
	|	вт_НДС.ДокументРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ДебиторскаяЗадолженность.Организация КАК Организация,
	|	вт_ДебиторскаяЗадолженность.Субконто1 КАК Субконто1,
	|	вт_ДебиторскаяЗадолженность.Субконто2 КАК Субконто2,
	|	вт_ДебиторскаяЗадолженность.Субконто3 КАК Субконто3,
	|	вт_ДебиторскаяЗадолженность.Счет КАК Счет,
	|	вт_ДебиторскаяЗадолженность.Сумма - ВЫБОР
	|		КОГДА вт_НДССвернутая.НДСОборот ЕСТЬ NULL
	|		ИЛИ вт_НДССвернутая.СуммаБезНДСОборот + вт_НДССвернутая.НДСОборот = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(вт_НДССвернутая.НДСОборот / (вт_НДССвернутая.СуммаБезНДСОборот + вт_НДССвернутая.НДСОборот) *
	|			вт_ДебиторскаяЗадолженность.Сумма КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ вт_ДоходыСДокументами
	|ИЗ
	|	вт_ДебиторскаяЗадолженность КАК вт_ДебиторскаяЗадолженность
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_НДССвернутая КАК вт_НДССвернутая
	|		ПО вт_ДебиторскаяЗадолженность.Субконто3 = вт_НДССвернутая.ДокументРеализации";
	
	Если Не СоздатьВиртуальнуюТаблицу Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ вт_ДоходыСДокументами", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасходыПереходногоПериода(НомераТаблиц)
	
	ИндексВыборкиДанныхВЗапросе = 7;
	ИндексВыборкиДанных = НомераТаблиц.АктуальныйИндекс + ИндексВыборкиДанныхВЗапросе;
	НомераТаблиц.Вставить("Расходы", ИндексВыборкиДанных);
	НомераТаблиц.АктуальныйИндекс = ИндексВыборкиДанных;
	
	ТекстЗапроса = ТекстЗапросаРасходыПоТоварамИУслугам(Истина)
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета() + ТекстЗапросаРасходыПоЗарплате(Истина)
		+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	СУММА(вт_РасходыПоТоварамИУслугамСДокументами.Сумма) КАК СуммаРасходов,
	|	СУММА(0) КАК СуммаРасходовПоДокументам,
	|	СУММА(0) КАК КоличествоВведенныхДокументов
	|ПОМЕСТИТЬ вт_Расходы
	|ИЗ
	|	вт_РасходыПоТоварамИУслугамСДокументами КАК вт_РасходыПоТоварамИУслугамСДокументами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(вт_РасходыПоЗарплатеСДокументами.Сумма),
	|	СУММА(0),
	|	СУММА(0)
	|ИЗ
	|	вт_РасходыПоЗарплатеСДокументами КАК вт_РасходыПоЗарплатеСДокументами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	ХозрасчетныйОбороты.СуммаНУОборотДт,
	|	0
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&ДатаНачалаПримененияОСНО, ДОБАВИТЬКДАТЕ(&ДатаПерехода, ДЕНЬ,
	|		1), , Счет В (&СчетаПрочиеРасходы), , Организация = &Организация, ,) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.Субконто1 = ЗНАЧЕНИЕ(Справочник.ПрочиеДоходыИРасходы.ДоходыРасходыПриПереходеСУСННаОСНО)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	КОЛИЧЕСТВО(*)
	|ИЗ
	|	РегистрСведений.ДокументыОперацийИзмененияНалоговогоРежима КАК ДокументыОперацийИзмененияНалоговогоРежима
	|ГДЕ
	|	ДокументыОперацийИзмененияНалоговогоРежима.Организация = &Организация
	|	И
	|		ДокументыОперацийИзмененияНалоговогоРежима.НаправлениеПерехода = ЗНАЧЕНИЕ(Перечисление.НаправленияИзмененийНалоговыхРежимов.УСНнаОСН)
	|	И ДокументыОперацийИзмененияНалоговогоРежима.ДатаПерехода = НАЧАЛОПЕРИОДА(&ДатаПерехода, ДЕНЬ)
	|	И
	|		ДокументыОперацийИзмененияНалоговогоРежима.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПереходаСУСННаОСНО.ВводРасходовПереходногоПериода)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(вт_Расходы.СуммаРасходов) КАК СуммаРасходов,
	|	СУММА(вт_Расходы.СуммаРасходовПоДокументам) КАК СуммаРасходовПоДокументам,
	|	СУММА(вт_Расходы.КоличествоВведенныхДокументов) КАК КоличествоВведенныхДокументов
	|ИЗ
	|	вт_Расходы КАК вт_Расходы";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаРасходыПоТоварамИУслугам(СоздатьВиртуальнуюТаблицу = Ложь)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность,
	|	ВалютаРегламентированногоУчета.Значение КАК ВалютаРегламентированногоУчета
	|ПОМЕСТИТЬ вт_КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаНачалаПримененияОСНО,) КАК КурсыВалютСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|		ПО ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыПриУСН.РасчетныйДокумент КАК РасчетныйДокумент
	|ПОМЕСТИТЬ вт_РасходыПриУСН
	|ИЗ
	|	РегистрНакопления.РасходыПриУСН КАК РасходыПриУСН
	|ГДЕ
	|	РасходыПриУСН.ИсключатьНДС
	|	И РасходыПриУСН.Период <= &ДатаПерехода
	|	И РасходыПриУСН.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|СГРУППИРОВАТЬ ПО
	|	РасходыПриУСН.РасчетныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(РасходыПриУСНОстатки.СуммаОстаток * ВЫБОР
	|		КОГДА вт_КурсыВалют.Курс ЕСТЬ NULL
	|		ИЛИ вт_КурсыВалют.Кратность = 0
	|		ИЛИ РасходыПриУСНОстатки.Валюта = вт_КурсыВалют.ВалютаРегламентированногоУчета
	|			ТОГДА 1
	|		ИНАЧЕ вт_КурсыВалют.Курс / вт_КурсыВалют.Кратность
	|	КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаОстаток,
	|	ВЫРАЗИТЬ(РасходыПриУСНОстатки.НДСОстаток * ВЫБОР
	|		КОГДА вт_КурсыВалют.Курс ЕСТЬ NULL
	|		ИЛИ вт_КурсыВалют.Кратность = 0
	|		ИЛИ РасходыПриУСНОстатки.Валюта = вт_КурсыВалют.ВалютаРегламентированногоУчета
	|			ТОГДА 1
	|		ИНАЧЕ вт_КурсыВалют.Курс / вт_КурсыВалют.Кратность
	|	КОНЕЦ КАК ЧИСЛО(15, 2)) КАК НДСОстаток,
	|	РасходыПриУСНОстатки.СчетУчета КАК Счет,
	|	РасходыПриУСНОстатки.ЭлементРасхода КАК Субконто1,
	|	РасходыПриУСНОстатки.ДоговорКонтрагента КАК Субконто2,
	|	РасходыПриУСНОстатки.Партия КАК Субконто3,
	|	РасходыПриУСНОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА вт_РасходыПриУСН.РасчетныйДокумент ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИсключатьНДС,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СуммаОстаток ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьСуммаНДСКВычету
	|ПОМЕСТИТЬ вт_РасходыУСНОстатки
	|ИЗ
	|	РегистрНакопления.РасходыПриУСН.Остатки(&МоментОпределенияРасходовУСН, Организация = &Организация
	|	И ВидРасхода = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Номенклатура)
	|	И СтатусыПартийУСН = ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Списанные)
	|	И СтатусыОплатыРасходовУСН = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходовУСН.НеОплачено)) КАК РасходыПриУСНОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_КурсыВалют КАК вт_КурсыВалют
	|		ПО РасходыПриУСНОстатки.Валюта = вт_КурсыВалют.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментОпределенияОстатков, Счет В
	|		(&СчетаНДСпоПриобретеннымЦенностям), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|		ПО РасходыПриУСНОстатки.РасчетныйДокумент = ХозрасчетныйОстатки.Субконто2
	|		И НЕ ХозрасчетныйОстатки.Субконто2 ЕСТЬ NULL
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_РасходыПриУСН КАК вт_РасходыПриУСН
	|		ПО РасходыПриУСНОстатки.РасчетныйДокумент = вт_РасходыПриУСН.РасчетныйДокумент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(РасходыПриУСНОстатки.СуммаОстаток * ВЫБОР
	|		КОГДА вт_КурсыВалют.Курс ЕСТЬ NULL
	|		ИЛИ вт_КурсыВалют.Кратность = 0
	|		ИЛИ РасходыПриУСНОстатки.Валюта = вт_КурсыВалют.ВалютаРегламентированногоУчета
	|			ТОГДА 1
	|		ИНАЧЕ вт_КурсыВалют.Курс / вт_КурсыВалют.Кратность
	|	КОНЕЦ КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(РасходыПриУСНОстатки.НДСОстаток * ВЫБОР
	|		КОГДА вт_КурсыВалют.Курс ЕСТЬ NULL
	|		ИЛИ вт_КурсыВалют.Кратность = 0
	|		ИЛИ РасходыПриУСНОстатки.Валюта = вт_КурсыВалют.ВалютаРегламентированногоУчета
	|			ТОГДА 1
	|		ИНАЧЕ вт_КурсыВалют.Курс / вт_КурсыВалют.Кратность
	|	КОНЕЦ КАК ЧИСЛО(15, 2)),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщиками),
	|	ЕСТЬNULL(ДоговорыКонтрагентов.Владелец, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	РасходыПриУСНОстатки.ДоговорКонтрагента,
	|	РасходыПриУСНОстатки.РасчетныйДокумент,
	|	РасходыПриУСНОстатки.РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА вт_РасходыПриУСН.РасчетныйДокумент ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.СуммаОстаток ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.РасходыПриУСН.Остатки(&МоментОпределенияРасходовУСН, Организация = &Организация
	|	И ВидРасхода = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовУСН.Услуги)
	|	И СтатусыПартийУСН = ЗНАЧЕНИЕ(Перечисление.СтатусыПартийУСН.Купленные)
	|	И СтатусыОплатыРасходовУСН = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходовУСН.НеОплачено)) КАК РасходыПриУСНОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РасходыПриУСНОстатки.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_КурсыВалют КАК вт_КурсыВалют
	|		ПО РасходыПриУСНОстатки.Валюта = вт_КурсыВалют.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментОпределенияОстатков, Счет В
	|		(&СчетаНДСпоПриобретеннымЦенностям), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|		ПО РасходыПриУСНОстатки.РасчетныйДокумент = ХозрасчетныйОстатки.Субконто2
	|		И НЕ ХозрасчетныйОстатки.Субконто2 ЕСТЬ NULL
	|		ЛЕВОЕ СОЕДИНЕНИЕ вт_РасходыПриУСН КАК вт_РасходыПриУСН
	|		ПО РасходыПриУСНОстатки.РасчетныйДокумент = вт_РасходыПриУСН.РасчетныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА вт_РасходыУСНОстатки.ИсключатьНДС
	|		ИЛИ вт_РасходыУСНОстатки.ЕстьСуммаНДСКВычету
	|			ТОГДА вт_РасходыУСНОстатки.СуммаОстаток - вт_РасходыУСНОстатки.НДСОстаток
	|		ИНАЧЕ вт_РасходыУСНОстатки.СуммаОстаток
	|	КОНЕЦ КАК Сумма,
	|	вт_РасходыУСНОстатки.Счет КАК Счет,
	|	вт_РасходыУСНОстатки.Субконто1 КАК Субконто1,
	|	вт_РасходыУСНОстатки.Субконто2 КАК Субконто2,
	|	вт_РасходыУСНОстатки.Субконто3 КАК Субконто3
	|ПОМЕСТИТЬ вт_РасходыПоТоварамИУслугамСДокументами
	|ИЗ
	|	вт_РасходыУСНОстатки КАК вт_РасходыУСНОстатки";
	
	Если Не СоздатьВиртуальнуюТаблицу Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ вт_РасходыПоТоварамИУслугамСДокументами", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасходыПоЗарплате(СоздатьВиртуальнуюТаблицу = Ложь)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.СуммаОстатокКт КАК Сумма,
	|	ХозрасчетныйОстатки.Счет,
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Субконто2,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет В (&СчетаРасчетыСПерсоналомПоОплатеТруда)
	|			ТОГДА &РасшифровкаЗарплата
	|		ИНАЧЕ &РасшифровкаВзносы
	|	КОНЕЦ КАК Субконто3
	|ПОМЕСТИТЬ вт_РасходыПоЗарплатеСДокументами
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&МоментОпределенияОстатков, Счет В
	|		(&СчетаРасчетыСПерсоналомПоОплатеТрудаИСоциальномуСтрахованию), , Организация = &Организация) КАК ХозрасчетныйОстатки";
	
	Если Не СоздатьВиртуальнуюТаблицу Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ вт_РасходыПоЗарплатеСДокументами", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ВводОстатков

Функция СоздатьОбновитьДокументВводаОстатков(Параметры, РазделУчета, ВыборкаДанных)
	
	Если РазделУчета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	Подразделение = Параметры.Подразделение;
	ДатаПерехода = Параметры.ДатаПерехода;
	РанееВведенныеДокументы = Параметры.РанееВведенныеДокументы;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ПодразделениеОрганизации", Подразделение);
	ПараметрыОтбора.Вставить("РазделУчета", РазделУчета);
	
	НайденныеСтроки = РанееВведенныеДокументы.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() = 0 Тогда
		ДокументОбъект = Документы.ВводНачальныхОстатков.СоздатьДокумент();
	Иначе
		ДокументОбъект = НайденныеСтроки[0].ДокументОперации.ПолучитьОбъект();
	КонецЕсли;
	ЗаполнениеДокументов.Заполнить(ДокументОбъект);
	
	ДокументОбъект.Дата = КонецДня(ДатаПерехода);
	ДокументОбъект.Организация = Организация;
	ДокументОбъект.ПодразделениеОрганизации = Подразделение;
	ДокументОбъект.РазделУчета = РазделУчета;
	ДокументОбъект.ОтражатьВНалоговомУчете = Истина;
	
	Если РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.РасчетыСПоставщикамиИПодрядчиками
		Или РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.РасчетыСПокупателямиИЗаказчиками
		Или РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.РасчетыСПрочимиДебиторамиИКредиторами Тогда
		ЗаполнитьРасчетыСКонтрагентами(ДокументОбъект, ВыборкаДанных);
	ИначеЕсли РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.РасчетыСПерсоналомПоОплатеТрудаИДепонентам Тогда
		ЗаполнитьРасчетыСПерсоналомПоОплатеТрудаИДепонентам(ДокументОбъект, ВыборкаДанных);
	ИначеЕсли РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.Материалы
		Или РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.Товары Тогда
		ЗаполнитьНоменклатуруНаСкладе(ДокументОбъект, ВыборкаДанных);
	ИначеЕсли РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.ТоварыОтгруженные Тогда
		ЗаполнитьМПЗОтгруженные(ДокументОбъект, ВыборкаДанных);
	ИначеЕсли РазделУчета = Перечисления.РазделыУчетаДляВводаОстатков.ДенежныеСредства Тогда
		ЗаполнитьДенежныеСредства(ДокументОбъект, ВыборкаДанных);
	КонецЕсли;
	
	ДокументОбъект.Комментарий = ТекстКомментарияДокументов();
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПроведенВХронологическойПоследовательности", Истина);
	ДокументОбъект.ПометкаУдаления = Ложь;
	
	ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат ДокументОбъект.Ссылка;
	
КонецФункции

Процедура ЗаполнитьРасчетыСКонтрагентами(Документ, ВыборкаДанных)
	
	Документ.РасчетыСКонтрагентами.Очистить();
	
	Пока ВыборкаДанных.Следующий() Цикл
	
		СтрокаТабличнойЧасти = Документ.РасчетыСКонтрагентами.Добавить();
	
		СтрокаТабличнойЧасти.СчетУчета = ВыборкаДанных.Счет;
		СтрокаТабличнойЧасти.Контрагент = ВыборкаДанных.Субконто1;
		СтрокаТабличнойЧасти.ДоговорКонтрагента = ВыборкаДанных.Субконто2;
		СтрокаТабличнойЧасти.Документ = ВыборкаДанных.Субконто3;
		СтрокаТабличнойЧасти.Сумма = ВыборкаДанных.СуммаОстатокДт;
		СтрокаТабличнойЧасти.СуммаКт = ВыборкаДанных.СуммаОстатокКт;
		СуммаНУ = ?(ЗначениеЗаполнено(ВыборкаДанных.СуммаОстатокДт), ВыборкаДанных.СуммаОстатокДт,
			ВыборкаДанных.СуммаОстатокКт);
		СтрокаТабличнойЧасти.СуммаНУ = СуммаНУ;
		
		Если ЗначениеЗаполнено(ВыборкаДанных.Валюта) Тогда
			СтрокаТабличнойЧасти.Валюта = ВыборкаДанных.Валюта;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьРасчетыСПерсоналомПоОплатеТрудаИДепонентам(Документ, ВыборкаДанных)
	
	Документ.РасчетыПоЗаработнойПлате.Очистить();
	
	Руководитель = Справочники.Сотрудники.ПустаяСсылка();
	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Документ.Организация, Документ.Дата);
	Если ЗначениеЗаполнено(ОтветственныеЛица) Тогда
		РуководительФизическоеЛицо = ОтветственныеЛица.Руководитель;
		Сотрудники = СотрудникиФормы.СотрудникиФизическогоЛица(РуководительФизическоеЛицо, , Документ.Организация);
		Если Сотрудники.Количество() > 0 Тогда
			Руководитель = Сотрудники[0];
		КонецЕсли;
	КонецЕсли;
	
	Пока ВыборкаДанных.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Документ.РасчетыПоЗаработнойПлате.Добавить();
		
		СтрокаТабличнойЧасти.СчетУчета = ВыборкаДанных.Счет;
		СтрокаТабличнойЧасти.Сотрудник = Руководитель;
		СтрокаТабличнойЧасти.РаботникОрганизации = ВыборкаДанных.Субконто1;
		СтрокаТабличнойЧасти.ПериодРегистрации = Документ.Дата;
		СтрокаТабличнойЧасти.СпособВыплаты = Перечисления.СпособыВыплатыЗарплаты.ЧерезБанк;
		СтрокаТабличнойЧасти.Сумма = - ВыборкаДанных.Сумма;
		
	КонецЦикла;
	
	ЗаполнитьКолонкуСотрудники(Документ.Организация, Документ.РасчетыПоЗаработнойПлате);
	
	Документ.ДополнительныеСвойства.Вставить("ПропуститьЗаписьВедомостей", Истина);
	
КонецПроцедуры

Процедура ЗаполнитьНоменклатуруНаСкладе(Документ, ВыборкаДанных)
	
	Документ.НоменклатураНаСкладе.Очистить();
	
	Пока ВыборкаДанных.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Документ.НоменклатураНаСкладе.Добавить();
		
		СтрокаТабличнойЧасти.СчетУчета = ВыборкаДанных.Счет;
		СтрокаТабличнойЧасти.Номенклатура = ВыборкаДанных.Субконто1;
		СтрокаТабличнойЧасти.Количество = ВыборкаДанных.Количество;
		СтрокаТабличнойЧасти.СуммаНУ = ВыборкаДанных.Сумма;
		СтрокаТабличнойЧасти.Склад = ВыборкаДанных.Склад;
		СтрокаТабличнойЧасти.Партия = ВыборкаДанных.Партия;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьМПЗОтгруженные(Документ, ВыборкаДанных)
	
	Документ.МПЗОтгруженные.Очистить();
	
	Пока ВыборкаДанных.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Документ.МПЗОтгруженные.Добавить();
		
		СтрокаТабличнойЧасти.СчетУчета = ВыборкаДанных.Счет;
		СтрокаТабличнойЧасти.Покупатель= ВыборкаДанных.Субконто1;
		СтрокаТабличнойЧасти.Номенклатура = ВыборкаДанных.Субконто2;
		СтрокаТабличнойЧасти.Партия = ВыборкаДанных.Субконто3;
		СтрокаТабличнойЧасти.Количество = ВыборкаДанных.Количество;
		СтрокаТабличнойЧасти.СуммаНУ = ВыборкаДанных.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры


Процедура ЗаполнитьДенежныеСредства(Документ, ВыборкаДанных)
	
	Документ.БухСправка.Очистить();
	
	Пока ВыборкаДанных.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Документ.БухСправка.Добавить();
		
		СтрокаТабличнойЧасти.СчетУчета = ВыборкаДанных.Счет;
		СтрокаТабличнойЧасти.Субконто1 = ВыборкаДанных.Субконто1;
		СтрокаТабличнойЧасти.Субконто2 = ВыборкаДанных.Субконто2;
		СтрокаТабличнойЧасти.Субконто3 = ВыборкаДанных.Субконто3;
		СтрокаТабличнойЧасти.Количество = ВыборкаДанных.Количество;
		СтрокаТабличнойЧасти.СуммаНУ = ВыборкаДанных.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Функция РанееВведенныеДокументы(Параметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыОперацийИзмененияНалоговогоРежима.ДокументОперации КАК ДокументОперации,
	|	ЕСТЬNULL(ВводНачальныхОстатков.РазделУчета, НЕОПРЕДЕЛЕНО) КАК РазделУчета,
	|	ВводНачальныхОстатков.ПодразделениеОрганизации
	|ИЗ
	|	РегистрСведений.ДокументыОперацийИзмененияНалоговогоРежима КАК ДокументыОперацийИзмененияНалоговогоРежима
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводНачальныхОстатков КАК ВводНачальныхОстатков
	|		ПО ДокументыОперацийИзмененияНалоговогоРежима.ДокументОперации = ВводНачальныхОстатков.Ссылка
	|ГДЕ
	|	ДокументыОперацийИзмененияНалоговогоРежима.Организация = &Организация
	|	И ДокументыОперацийИзмененияНалоговогоРежима.ДатаПерехода = &ДатаПерехода
	|	И ДокументыОперацийИзмененияНалоговогоРежима.ВидОперации = &ВидОперации";
	
	Запрос.УстановитьПараметр("ДатаПерехода", НачалоДня(Параметры.ДатаПерехода));
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("ВидОперации", Параметры.ВидОперации);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура УдалитьРанееВведенныеДокументы(Документы)
	
	Для Каждого Строка Из Документы Цикл
		Если Строка.ДокументОперации.ПометкаУдаления Тогда
			Продолжить;
		КонецЕсли;
		ДокументОперации = Строка.ДокументОперации.ПолучитьОбъект();
		ДокументОперации.ПометкаУдаления = Истина;
		Если ДокументОперации.Проведен Тогда
			ДокументОперации.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ДокументОперации.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьДокументыВРегистр(Документы, Параметры)
	
	НаборЗаписей = РегистрыСведений.ДокументыОперацийИзмененияНалоговогоРежима.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Организация.Установить(Параметры.Организация);
	НаборЗаписей.Отбор.ДатаПерехода.Установить(Параметры.ДатаПерехода);
	НаборЗаписей.Отбор.ВидОперации.Установить(Параметры.ВидОперации);
	НаборЗаписей.Отбор.НаправлениеПерехода.Установить(Перечисления.НаправленияИзмененийНалоговыхРежимов.УСНнаОСН);
	
	Для Каждого Документ Из Документы Цикл
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Активность = Истина;
		НоваяЗапись.Организация = Параметры.Организация;
		НоваяЗапись.ДатаПерехода = Параметры.ДатаПерехода;
		НоваяЗапись.ВидОперации = Параметры.ВидОперации;
		НоваяЗапись.НаправлениеПерехода = Перечисления.НаправленияИзмененийНалоговыхРежимов.УСНнаОСН;
		НоваяЗапись.ДокументОперации = Документ;
		
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

Функция НовыйПараметрыСозданияОбновленияДокументов(ОсновныеПараметры)
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", ОсновныеПараметры.Организация);
	Параметры.Вставить("Подразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	Параметры.Вставить("ДатаПерехода", ОсновныеПараметры.ДатаПерехода);
	Параметры.Вставить("ВидОперации", ОсновныеПараметры.ВидОперации);
	Параметры.Вставить("РанееВведенныеДокументы", Новый ТаблицаЗначений);
	Параметры.Вставить("ИндексРанееСозданногоДокумента", 0);
	
	Возврат Параметры;
	
КонецФункции

Функция СоздатьОбновитьДокументОперацияБухДоходы(Параметры, Выборка)
	
	ДатаДокумента = Параметры.ДатаПерехода + 1;
	Организация = Параметры.Организация;
	РанееВведенныеДокументы = Параметры.РанееВведенныеДокументы;
	СчетКт = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
	
	Справочники.ПрочиеДоходыИРасходы.ДобавитьРеквизит_ДоходыИРасходыПриПереходеСУСННаОСНО();
	ДоходыРасходыПриПереходеСУСННаОСНО = Справочники.ПрочиеДоходыИРасходы.ДоходыРасходыПриПереходеСУСННаОСНО;
	
	Если РанееВведенныеДокументы.Количество() = 0 Тогда
		ДокументОбъект = Документы.ОперацияБух.СоздатьДокумент();
	Иначе
		ДокументОбъект = РанееВведенныеДокументы[0].ДокументОперации.ПолучитьОбъект();
	КонецЕсли;
	
	ДокументОбъект.Дата = ДатаДокумента;
	ДокументОбъект.Организация = Организация;
	ДокументОбъект.Содержание = НСтр("ru = 'Признание доходов по ОСНО при переходе с УСН'");
	ДокументОбъект.Комментарий = ТекстКомментарияДокументов();
	ДокументОбъект.ПометкаУдаления = Ложь;
	ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	Набор = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка, Истина);
	Пока Выборка.Следующий() Цикл
		
		Проводка = Набор.Добавить();
		
		Проводка.Активность  = Истина;
		Проводка.Организация = Организация;
		Проводка.Период      = ДатаДокумента;
		Проводка.СчетДт      = Выборка.Счет;
		Проводка.СчетКт      = СчетКт;
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетДт, Проводка.СубконтоДт, 1, Выборка.Субконто1);
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетДт, Проводка.СубконтоДт, 2, Выборка.Субконто2);
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетДт, Проводка.СубконтоДт, 3, Выборка.Субконто3);
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетКт, Проводка.СубконтоКт, 1, ДоходыРасходыПриПереходеСУСННаОСНО);
		Проводка.СуммаНУКт = Выборка.Сумма;
		
	КонецЦикла;
	Набор.Записать(Истина);
	
	Возврат ДокументОбъект.Ссылка;
	
КонецФункции

Функция СоздатьОбновитьДокументОперацияБухРасходы(Параметры, Выборка, Содержание)
	
	ДатаДокумента = Параметры.ДатаПерехода + 1;
	Организация = Параметры.Организация;
	РанееВведенныеДокументы = Параметры.РанееВведенныеДокументы;
	СчетДт = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
	ИндексРанееСозданногоДокумента = Параметры.ИндексРанееСозданногоДокумента;
	
	Справочники.ПрочиеДоходыИРасходы.ДобавитьРеквизит_ДоходыИРасходыПриПереходеСУСННаОСНО();
	ДоходыРасходыПриПереходеСУСННаОСНО = Справочники.ПрочиеДоходыИРасходы.ДоходыРасходыПриПереходеСУСННаОСНО;
	
	КоличествоРанееВведенныеДокументов = РанееВведенныеДокументы.Количество();
	Если КоличествоРанееВведенныеДокументов = 0
		Или ИндексРанееСозданногоДокумента >= КоличествоРанееВведенныеДокументов Тогда
		ДокументОбъект = Документы.ОперацияБух.СоздатьДокумент();
	Иначе
		ДокументОбъект = РанееВведенныеДокументы[ИндексРанееСозданногоДокумента].ДокументОперации.ПолучитьОбъект();
		Параметры.ИндексРанееСозданногоДокумента = ИндексРанееСозданногоДокумента + 1;
	КонецЕсли;
	
	ДокументОбъект.Дата = ДатаДокумента;
	ДокументОбъект.Организация = Организация;
	ДокументОбъект.Содержание = Содержание;
	ДокументОбъект.Комментарий = ТекстКомментарияДокументов();
	ДокументОбъект.ПометкаУдаления = Ложь;
	ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	Набор = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка, Истина);
	Пока Выборка.Следующий() Цикл
		
		Проводка = Набор.Добавить();
		
		Проводка.Активность  = Истина;
		Проводка.Организация = Организация;
		Проводка.Период      = ДатаДокумента;
		Проводка.СчетДт      = СчетДт;
		Проводка.СчетКт      = Выборка.Счет;
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетКт, Проводка.СубконтоКт, 1, Выборка.Субконто1);
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетКт, Проводка.СубконтоКт, 2, Выборка.Субконто2);
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетКт, Проводка.СубконтоКт, 3, Выборка.Субконто3);
		БухгалтерскийУчет.УстановитьСубконто(
			Проводка.СчетДт, Проводка.СубконтоДт, 1, ДоходыРасходыПриПереходеСУСННаОСНО);
		Проводка.СуммаНУДт = Выборка.Сумма;
		
	КонецЦикла;
	Набор.Записать(Истина);
	
	Возврат ДокументОбъект.Ссылка;
	
КонецФункции

Функция ТекстКомментарияДокументов()
	Возврат Нстр("ru = '#Создан автоматически обработкой ""Помощник перехода с УСН на ОСНО""'");
КонецФункции

Функция МоментОпределенияРасходовУСН(Параметры)
	
	РегламентнаяОперацияЗакрытиеРегистровУСН = РегламентнаяОперацияЗакрытиеРегистровУСН(Параметры);
	Если ЗначениеЗаполнено(РегламентнаяОперацияЗакрытиеРегистровУСН) Тогда
		ДатаПереходаДляЗапроса = Новый МоментВремени(РегламентнаяОперацияЗакрытиеРегистровУСН.Дата,
			РегламентнаяОперацияЗакрытиеРегистровУСН);
	Иначе
		ДатаПереходаДляЗапроса = Новый Граница(КонецДня(Параметры.ДатаПерехода), ВидГраницы.Включая);
	КонецЕсли;
	
	Возврат ДатаПереходаДляЗапроса;
	
КонецФункции

Функция РегламентнаяОперацияЗакрытиеРегистровУСН(Параметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Дата = &Дата
	|	И РегламентнаяОперация.Организация = &Организация
	|	И НЕ РегламентнаяОперация.ПометкаУдаления
	|	И РегламентнаяОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыРегламентныхОпераций.ЗакрытиеУчетныхРегистровУСН)";
	
	Запрос.УстановитьПараметр("Дата", Параметры.ДатаПерехода);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура УстановитьПараметрыЗапроса(Запрос, Параметры)
	
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("ДатаПерехода", Параметры.ДатаПерехода);
	Запрос.УстановитьПараметр("ДатаНачалаПримененияОСНО", Параметры.ДатаПерехода + 1);
	Запрос.УстановитьПараметр("МоментОпределенияРасходовУСН", МоментОпределенияРасходовУСН(Параметры));
	Запрос.УстановитьПараметр("МоментОпределенияОстатков",
		Новый Граница(КонецДня(Параметры.ДатаПерехода), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("РасшифровкаЗарплата", НСтр("ru = 'Зарплата (счет 70)'"));
	Запрос.УстановитьПараметр("РасшифровкаВзносы", НСтр("ru = 'Взносы (счет 69)'"));
	
	Счета = Новый Массив;
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателями);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиВал);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиУЕ);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчиками);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчикамиВал);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчикамиУЕ);
	Запрос.УстановитьПараметр("СчетаПоДебиторскойЗадолженности", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям);
	Запрос.УстановитьПараметр("СчетаНДСпоПриобретеннымЦенностям", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
	Запрос.УстановитьПараметр(
		"СчетаРасчетыСПоставщикамиИПодрядчиками", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	Запрос.УстановитьПараметр(
		"СчетаРасчетыСПокупателямиИЗаказчиками", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчиками);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПоставщикамиИПодрядчиками);
	Запрос.УстановитьПараметр(
		"СчетаРасчетыСПрочимиДебиторамиИКредиторами", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
	Запрос.УстановитьПараметр(
		"СчетаРасчетыСПерсоналомПоОплатеТруда", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.Материалы);
	Запрос.УстановитьПараметр("СчетаМатериалы", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.Товары);
	Запрос.УстановитьПараметр("СчетаТовары", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);
	Запрос.УстановитьПараметр("СчетаТоварыОтгруженные", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ПродажиПоПлатежнымКартам);
	Запрос.УстановитьПараметр("СчетаПродажиПоПлатежнымКартам", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияОсновныхСредств);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияНематериальныхАктивов);
	Запрос.УстановитьПараметр(
		"СчетаВнеоборотныеАктивыИАмортизация", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию);
	Запрос.УстановитьПараметр("СчетаРасчетыСПерсоналомПоОплатеТрудаИСоциальномуСтрахованию",
		БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	Запрос.УстановитьПараметр("СчетаОсновныеСредства", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
	Запрос.УстановитьПараметр("СчетаНематериальныеАктивы", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	Запрос.УстановитьПараметр("СчетаПрочиеДоходы", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
	Счета.Очистить();
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасходы);
	Запрос.УстановитьПараметр("СчетаПрочиеРасходы", БухгалтерскийУчет.СформироватьМассивСубсчетов(Счета));
	
КонецПроцедуры

Процедура ЗаполнитьКолонкуСотрудники(Организация, РасчетыПоЗаработнойПлате)
	
	Если ПолучитьФункциональнуюОпцию("ВедетсяУчетРасчетовПоЗарплатеСводно") Тогда
		Возврат;
	КонецЕсли;
	
	РаботникиОрганизации = РасчетыПоЗаработнойПлате.ВыгрузитьКолонку("РаботникОрганизации");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОсновныеСотрудникиФизическихЛиц.Сотрудник,
		|	ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо КАК РаботникОрганизации
		|ИЗ
		|	РегистрСведений.ОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц
		|ГДЕ
		|	ОсновныеСотрудникиФизическихЛиц.ГоловнаяОрганизация = &Организация
		|	И ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо В (&ФизическиеЛица)
		|СГРУППИРОВАТЬ ПО
		|	ОсновныеСотрудникиФизическихЛиц.Сотрудник,
		|	ОсновныеСотрудникиФизическихЛиц.ФизическоеЛицо";
	
	Запрос.УстановитьПараметр("ФизическиеЛица", РаботникиОрганизации);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НайденнаяСтрока = РасчетыПоЗаработнойПлате.Найти(
			ВыборкаДетальныеЗаписи.РаботникОрганизации, "РаботникОрганизации");
		Если НайденнаяСтрока <> Неопределено Тогда
			НайденнаяСтрока.Сотрудник = ВыборкаДетальныеЗаписи.Сотрудник;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОператорВЗапрос(ЗапросПакета, ИмяСубконто, Псевдоним)
	Оператор = ЗапросПакета.Операторы[0];
	Оператор.ВыбираемыеПоля.Добавить(ИмяСубконто);
	ЗапросПакета.Колонки[ЗапросПакета.Колонки.Количество()-1].Псевдоним = Псевдоним;
КонецПроцедуры

#КонецОбласти

#КонецЕсли