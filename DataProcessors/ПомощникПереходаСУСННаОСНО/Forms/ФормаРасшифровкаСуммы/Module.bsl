#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаОбработка = РеквизитФормыВЗначение("Объект");
	НаборДанных = Новый Структура;
	СКД = ЭтаОбработка.ПолучитьМакет("РасшифровкаСуммы");
	
	Если Параметры.ДоходРасход = "Расход" Тогда
		ТекстПоказателяЗаголовка = НСтр("ru = 'расходов'");
		ТекстПоказателяШапки = НСтр("ru = 'Расходы'");
	Иначе
		ТекстПоказателяЗаголовка = НСтр("ru = 'доходов'");
		ТекстПоказателяШапки = НСтр("ru = 'Доходы'");
	КонецЕсли;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Расшифровка %1 переходного периода'"),
		ТекстПоказателяЗаголовка);
	ЗаголовокШапки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 переходного периода'"),
		ТекстПоказателяШапки);
	
	НаборДанных.Вставить(
		"Данные", Обработки.ПомощникПереходаСУСННаОСНО.ДанныеДляРасшифровкиСуммДоходовРасходов(Параметры));
	
	ИнформацияШапка = Новый Структура();
	ИнформацияШапка.Вставить("Организация", Параметры.Организация);
	ИнформацияШапка.Вставить("Заголовок", ЗаголовокШапки);
	
	ВывестиШапкуОтчета(Результат, ИнформацияШапка);
	
	Настройки = СКД.ВариантыНастроек.Основной.Настройки;
	
	ВывестиВТаблицу(СКД, Настройки, НаборДанных);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	ПолученныеДанныеРасшифровки = РасшифровкаИзХранилища(Расшифровка, АдресДанныхВХранилище);
	
	Если (ПолученныеДанныеРасшифровки <> Null) И (ПолученныеДанныеРасшифровки <> Неопределено) Тогда
		ПоказатьЗначение( , ПолученныеДанныеРасшифровки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВывестиШапкуОтчета(Результат, Контекст)
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	
	// Организация
	ОбластьВывода = Макет.ПолучитьОбласть("Организация");
	ОбластьВывода.Параметры.НазваниеОрганизации = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(
		Контекст.Организация, Ложь);
	Результат.Вывести(ОбластьВывода);
	
	// Название отчета
	ОбластьВывода = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьВывода.Параметры.ЗаголовокОтчета = Контекст.Заголовок; 
	Результат.Вывести(ОбластьВывода);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиВТаблицу(СКД, Настройки, ВнешняяТаблицаДанных)
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпановщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпановщикМакета.Выполнить(СКД, Настройки, ДанныеРасшифровки);
	
	ПроцессорКД = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКД.Инициализировать(МакетКомпоновки, ВнешняяТаблицаДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	
	АдресДанныхВХранилище = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, УникальныйИдентификатор);
	
	ПроцессорВывода.Вывести(ПроцессорКД);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РасшифровкаИзХранилища(Расшифровка, АдресДанныхВХранилище)
	
	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(АдресДанныхВХранилище);
	Если Тип(ДанныеРасшифровки) = Тип("ДанныеРасшифровкиКомпоновкиДанных") Тогда
		Возврат ДанныеРасшифровки.Элементы[Расшифровка].ПолучитьПоля().Получить(0).Значение;
	КонецЕсли;
	
КонецФункции

#КонецОбласти