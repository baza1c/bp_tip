#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияАктуализации Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Организация.Пустая() Тогда
		Объект.Организация = Параметры.Организация;
	Иначе
		Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	КонецЕсли;
	ОрганизацияПредставление = Объект.Организация;
	
	Если ЗначениеЗаполнено(Параметры.ДатаПерехода) Тогда
		Объект.ДатаПерехода = Параметры.ДатаПерехода;
	Иначе
		ДатыПерехода = Обработки.ПомощникПереходаСУСННаОСНО.ДатыПереходаСУСННаОСНО(Объект.Организация);
		Объект.ДатаПерехода = АктуальнаяДатаПерехода(ДатыПерехода);
	КонецЕсли;
	
	БылПереходСУСННаОСНО = ЗначениеЗаполнено(Объект.ДатаПерехода);
	
	Если БылПереходСУСННаОСНО Тогда
		ПараметрыФункции = НовыйПараметрыВыполнения();
		ДлительнаяОперация = ДанныеПоОперациям(ПараметрыФункции, УникальныйИдентификатор);
	КонецЕсли;
	
	ДоступностьКнопок = ПравоДоступа("Редактирование", Метаданные.Документы.ВводНачальныхОстатков)
		И ПравоДоступа("Редактирование", Метаданные.Документы.ОперацияБух);
	
	ОбъектМетаданных = Метаданные.Обработки.Найти("ПодготовкаКВычетуНДСУчтенногоВСтоимости");
	ДоступенПомощникПодготовкиКВычетуНДС =
		(ОбъектМетаданных <> Неопределено) И ПравоДоступа("Использование", ОбъектМетаданных);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОбработкаОповещенияАктуализации(
		ЭтотОбъект, Объект.Организация, КонецДня(Объект.ДатаПерехода), ИмяСобытия, Параметр, Источник);
	Если ИмяСобытия = "АктуализацияЗавершенаУспешно" Тогда
		Если Параметр.Организация = Объект.Организация Тогда
			ПолучитьДанныеПоОперациям(Ложь)
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если БылПереходСУСННаОСНО Тогда
		ПолучитьДанныеПоОперациям(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкрытьНажатие(Элемент)
	
	БухгалтерскийУчетКлиентПереопределяемый.СкрытьПанельАктуализации(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходыНажатие(Элемент)
	
	ОткрытьФормуРасшифровки("Доход");
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаРасходыНажатие(Элемент)
	
	ОткрытьФормуРасшифровки("Расход");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ДатыПерехода = ДатыПерехода(Объект.Организация);
	Объект.ДатаПерехода = АктуальнаяДатаПерехода(ДатыПерехода);
	БылПереходСУСННаОСНО = ЗначениеЗаполнено(Объект.ДатаПерехода);
	
	Если БылПереходСУСННаОСНО Тогда
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("Организация", Объект.Организация);
		ПараметрыФункции.Вставить("ДатаПерехода", Объект.ДатаПерехода);
		ДлительнаяОперация = ДанныеПоОперациям(ПараметрыФункции, УникальныйИдентификатор);
		ПолучитьДанныеПоОперациям(Истина);
	Иначе
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПериодНажатие(Элемент)
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ВыборПериодаЗавершение", ЭтаФорма), ДатыПерехода);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Актуализировать(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.Актуализировать(
		ЭтотОбъект, Объект.Организация, КонецДня(Объект.ДатаПерехода));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	БухгалтерскийУчетКлиентПереопределяемый.ОтменитьАктуализацию(
		ЭтотОбъект, Объект.Организация, КонецДня(Объект.ДатаПерехода));
	
КонецПроцедуры

&НаКлиенте
Процедура ВводНачальныхОстатковПоОС(Команда)
	
	РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаДляВводаОстатков.ОсновныеСредства");
	ВводНачальныхОстатковПоВнеоборотнымАктивам(РазделУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводНачальныхОстатковПоНМА(Команда)
	
	РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыУчетаДляВводаОстатков.НематериальныеАктивыИНИОКР");
	ВводНачальныхОстатковПоВнеоборотнымАктивам(РазделУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиОстаткиПоНалоговомуУчету(Команда)
	ВвестиОстаткиПоНалоговомуУчетуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВводОстатковПоНУСозданныеДокументы(Команда)
	
	ПараметрыОтбора = Новый Структура("Дата, Организация", Объект.ДатаПерехода, Объект.Организация);
	ПараметрыОтбора.Вставить("ПомощникВводаНачальныхОстатков", Истина);
	ПараметрыФормы = Новый Структура("ПараметрыОтбораСписка", ПараметрыОтбора);
	
	ОткрытьФорму("Документ.ВводначальныхОстатков.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВводОстатковПоНУ(Команда)
	
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомость.Форма.ФормаОтчета", ПараметрыОтчета(), ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьДоходы(Команда)
	
	ОтразитьДоходыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьРасходы(Команда)
	ОтразитьРасходыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СозданныеДокументы(Команда)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Дата", Объект.ДатаПерехода + 1);
	ПараметрыОтбора.Вставить("Организация", Объект.Организация);
	ПараметрыОтбора.Вставить("ПометкаУдаления", Ложь);
	
	ПараметрыФормы = Новый Структура("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("Документ.ОперацияБух.Форма.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

#Область ИзменитьСтатусОперации

&НаКлиенте
Процедура ИзменитьСтатусОС(Команда)
	ИзменитьСтатус("ВводНачальныхОстатковПоОС");
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатусНМА(Команда)
	ИзменитьСтатус("ВводНачальныхОстатковПоНМА");
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатусНДС(Команда)
	ИзменитьСтатус("ПодготовкаКВычетуНДС");
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатусВводОстатковПоНУ(Команда)
	ИзменитьСтатус("ВводОстатковПоНалоговомуУчету");
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатусДоходы(Команда)
	ИзменитьСтатус("ВводДоходовПереходногоПериода");
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатусРасходы(Команда)
	ИзменитьСтатус("ВводРасходовПереходногоПериода");
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(
		ЭтотОбъект, Объект.Организация, КонецДня(Объект.ДатаПерехода));

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеАктуализации()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьВыполнениеАктуализацииОтчета(
		ЭтотОбъект, Объект.Организация, КонецДня(Объект.ДатаПерехода));

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьЗавершениеАктуализации()
	
	БухгалтерскийУчетКлиентПереопределяемый.ПроверитьЗавершениеАктуализации(
		ЭтотОбъект, Объект.Организация, КонецДня(Объект.ДатаПерехода));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АктуальнаяДатаПерехода(ДатыПерехода)
	
	ДатаПерехода = '00010101';
	
	Если ДатыПерехода.Количество() = 0 Тогда
		Возврат ДатаПерехода;
	КонецЕсли;
	
	ТекущаяДата = ТекущаяДатаСеанса();
	ЭлементТекущийГод = ДатыПерехода.НайтиПоЗначению(КонецГода(ТекущаяДата));
	ЭлементПрошлыйГод = ДатыПерехода.НайтиПоЗначению(НачалоГода(ТекущаяДата) - 1);
	
	Если ЭлементТекущийГод <> Неопределено Тогда
		ДатаПерехода = ЭлементТекущийГод.Значение;
	ИначеЕсли ЭлементПрошлыйГод <> Неопределено Тогда
		ДатаПерехода = ЭлементПрошлыйГод.Значение;
	Иначе
		ДатаПерехода = ДатыПерехода[ДатыПерехода.Количество() - 1].Значение;
	КонецЕсли;
	
	Возврат ДатаПерехода;
	
КонецФункции

&НаКлиенте
Процедура ВыборПериодаЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыборПериодаЗавершениеНаСервере(ВыбранныйЭлемент.Значение);
	
	Если БылПереходСУСННаОСНО Тогда
		ПолучитьДанныеПоОперациям(Истина);
	Иначе
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыборПериодаЗавершениеНаСервере(ДатаПерехода)
	
	Объект.ДатаПерехода = ДатаПерехода;
	БылПереходСУСННаОСНО = ЗначениеЗаполнено(Объект.ДатаПерехода);
	ПараметрыФункции = НовыйПараметрыВыполнения();
	ДлительнаяОперация = ДанныеПоОперациям(ПараметрыФункции, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	БылПереходСУСННаОСНО = Форма.БылПереходСУСННаОСНО;
	ДанныеПоОперациям = Форма.ДанныеПоОперациям;
	ДоступностьКнопок = Форма.ДоступностьКнопок;
	
	НастроитьОтображениеШапки(Форма);
	
	Если Не БылПереходСУСННаОСНО Или ДанныеПоОперациям = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьОтображениеГруппыВнеоборотныхАктивов(Элементы, ДанныеПоОперациям, "ОС");
	НастроитьОтображениеГруппыВнеоборотныхАктивов(Элементы, ДанныеПоОперациям, "НМА");
	НастроитьОтображениеГруппыНДС(Элементы, ДанныеПоОперациям, Форма.ДоступенПомощникПодготовкиКВычетуНДС);
	НастроитьОтображениеГруппыВводОстатковПоНУ(Элементы, ДанныеПоОперациям);
	НастроитьОтображениеГруппыДоходыРасходы(Элементы, ДанныеПоОперациям, "Доходы");
	НастроитьОтображениеГруппыДоходыРасходы(Элементы, ДанныеПоОперациям, "Расходы");
	
	Элементы.ВвестиОстаткиПоНалоговомуУчету.Доступность = ДоступностьКнопок;
	Элементы.ОтразитьДоходы.Доступность = ДоступностьКнопок;
	Элементы.ОтразитьРасходы.Доступность = ДоступностьКнопок;
	
КонецПроцедуры

#Область НастройкиОтображенияЭлементов

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьОтображениеШапки(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	БылПереходСУСННаОСНО = Форма.БылПереходСУСННаОСНО;
	
	Элементы.ГруппаДоПерехода.Видимость = БылПереходСУСННаОСНО;
	Элементы.ГруппаПослеПерехода.Видимость = БылПереходСУСННаОСНО;
	Элементы.ГруппаСтатусы.Видимость = БылПереходСУСННаОСНО;
	
	Если Форма.ДанныеПоОперациям = Неопределено Тогда
		Элементы.ГруппаОС.Видимость = Ложь;
		Элементы.ГруппаНМА.Видимость = Ложь;
	КонецЕсли;
	
	Если БылПереходСУСННаОСНО Тогда
		
		Элементы.НадписьПодсказка.Заголовок = НСтр(
			"ru = 'Перед началом работы на ОСНО введите начальные остатки и отразите операции переходного периода.'");
		
		ТекстНадписьПериод = НСтр("ru = 'Переход на ОСНО с %1'");
		Элементы.НадписьПериод.Заголовок = СтрШаблон(ТекстНадписьПериод, Формат(Объект.ДатаПерехода + 1, "ДЛФ=DD;"));
		Элементы.НадписьПериод.Гиперссылка = Форма.ДатыПерехода.Количество() > 1; 
		
		ТекстГруппаДоПерехода = НСтр("ru = 'Подготовка к переходу на ОСНО (выполняется - %1)'");
		Элементы.ГруппаДоПерехода.Заголовок = СтрШаблон(ТекстГруппаДоПерехода, Формат(Объект.ДатаПерехода, "ДЛФ=D;"));
		
		ТекстГруппаПослеПерехода = НСтр("ru = 'После перехода на ОСНО  (выполняется - %1)'");
		Элементы.ГруппаПослеПерехода.Заголовок = СтрШаблон(ТекстГруппаПослеПерехода,
			Формат(Объект.ДатаПерехода + 1, "ДЛФ=D;"));
		
	Иначе
		
		Элементы.НадписьПодсказка.Заголовок = НСтр(
			"ru = 'Организация не меняла систему налогообложения с УСН на ОСНО, выполнение переходных операций не требуется.'");
		Элементы.НадписьПериод.Заголовок = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьОтображениеГруппыВнеоборотныхАктивов(Элементы, ДанныеПоОперациям, Имя)
	
	ИмяГруппа = "Группа" + Имя;
	ИмяСвойства = "ВводНачальныхОстатковПо" + Имя;
	ДанныеПоОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеПоОперациям, ИмяСвойства);
	
	Если ДанныеПоОперации = Неопределено Тогда
		Элементы[ИмяГруппа].Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ИмяСтатус = "Статус" + Имя;
	ИмяИзменитьСтатус = "ИзменитьСтатус" + Имя;
	
	Элементы[ИмяГруппа].Видимость = ДанныеПоОперации.Актуальность;
	Элементы[ИмяСтатус].Картинка = КартинкаСостоянияОперации(ДанныеПоОперации.Состояние);
	Элементы[ИмяИзменитьСтатус].Заголовок = ЗаголовокКомандыИзменитьСтатус(ДанныеПоОперации.Состояние);
	
	Если ДанныеПоОперации.Состояние = СостояниеВыполнено()
		Или ДанныеПоОперации.Состояние = СостояниеПропущено() Тогда
		Элементы[ИмяСвойства].Заголовок = НСтр("ru = 'Созданные документы'");
	Иначе
		Элементы[ИмяСвойства].Заголовок = НСтр("ru = 'Ввести начальные остатки'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьОтображениеГруппыНДС(Элементы, ДанныеПоОперациям, ДоступенПомощникПодготовкиКВычетуНДС)
	
	ДанныеПоОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеПоОперациям, "ПодготовкаКВычетуНДС");
	
	Если Не ДоступенПомощникПодготовкиКВычетуНДС Или ДанныеПоОперации = Неопределено Тогда
		Элементы.ГруппаНДС.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаНДС.Видимость = ДанныеПоОперации.Актуальность;
	Элементы.ИзменитьСтатусНДС.Заголовок = ЗаголовокКомандыИзменитьСтатус(ДанныеПоОперации.Состояние);	
	Элементы.СтатусНДС.Картинка = КартинкаСостоянияОперации(ДанныеПоОперации.Состояние);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьОтображениеГруппыВводОстатковПоНУ(Элементы, ДанныеПоОперациям)
	
	ДанныеПоОперации =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеПоОперациям, "ВводОстатковПоНалоговомуУчету");
	
	Если ДанныеПоОперации = Неопределено Тогда
		Элементы.ГруппаВводОстатковПоНУ.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаВводОстатковПоНУ.Видимость = ДанныеПоОперации.Актуальность;
	Элементы.ВводОстатковПоНУ.Картинка = КартинкаСостоянияОперации(ДанныеПоОперации.Состояние);
	Элементы.ИзменитьСтатусВводОстатковПоНУ.Заголовок =
		ЗаголовокКомандыИзменитьСтатус(ДанныеПоОперации.Состояние);
	Элементы.ВводОстатковПоНУСозданныеДокументы.Видимость = Ложь;
	Элементы.ВвестиОстаткиПоНалоговомуУчету.Заголовок = НСтр("ru = 'Перезаполнить остатки'");
	Элементы.ВвестиОстаткиПоНалоговомуУчету.Видимость = ДанныеПоОперации.Состояние <> СостояниеПропущено();
	
	Если ДанныеПоОперации.Состояние = СостояниеВыполнено()
		Или ДанныеПоОперации.Состояние = СостояниеВыполненоСОшибками() Тогда
		Элементы.ВводОстатковПоНУСозданныеДокументы.Видимость = Истина;
	Иначе
		Элементы.ВвестиОстаткиПоНалоговомуУчету.Заголовок = НСтр("ru = 'Ввести остатки'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьОтображениеГруппыДоходыРасходы(Элементы, ДанныеПоОперациям, Имя)
	
	ИмяГруппа = "Группа" + Имя;
	
	Если Имя = "Доходы" Тогда
		ИмяСвойства = "ВводДоходовПереходногоПериода";
	Иначе
		ИмяСвойства = "ВводРасходовПереходногоПериода";
	КонецЕсли;
	ДанныеПоОперации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеПоОперациям, ИмяСвойства);
	
	Если ДанныеПоОперации = Неопределено Тогда
		Элементы[ИмяГруппа].Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ИмяОтразить = "Отразить" + Имя;
	ИмяСозданныеДокументы = "СозданныеДокументы" + Имя;
	ИмяРасшифровка = "Расшифровка" + Имя;
	ИмяСтатус = "Статус" + Имя;
	ИмяИзменитьСтатус = "ИзменитьСтатус" + Имя;
	ТекстПоказателя = НРег(Имя);
	
	Элементы[ИмяГруппа].Видимость = ДанныеПоОперации.Актуальность;
	Элементы[ИмяОтразить].Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Отразить %1'"),
		ТекстПоказателя);
	Элементы[ИмяОтразить].Доступность = Истина;
	Элементы[ИмяОтразить].Видимость = ДанныеПоОперации.Состояние <> СостояниеПропущено();
	Элементы[ИмяСозданныеДокументы].Видимость = Ложь;
	Элементы[ИмяРасшифровка].Заголовок = ЗаголовокСуммы(ДанныеПоОперации.Сумма);
	
	Элементы[ИмяСтатус].Картинка = КартинкаСостоянияОперации(ДанныеПоОперации.Состояние);
	Элементы[ИмяИзменитьСтатус].Заголовок = ЗаголовокКомандыИзменитьСтатус(ДанныеПоОперации.Состояние);
	
	Если ДанныеПоОперации.Состояние = СостояниеВыполнено() Тогда
		Элементы[ИмяОтразить].Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Перезаполнить %1'"),
		ТекстПоказателя);
		Элементы[ИмяСозданныеДокументы].Видимость = Истина;
	ИначеЕсли ДанныеПоОперации.Состояние = СостояниеВыполненоСОшибками() Тогда
		Элементы[ИмяСозданныеДокументы].Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПолучитьДанныеПоОперациям(АктуализироватьДанные)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется подготовка данных...'");
	ОповещениеЗавершения = Новый ОписаниеОповещения("ПолучитьДанныеПоОперациямЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеЗавершения, ПараметрыОжидания);
	
	Если АктуализироватьДанные Тогда
		БухгалтерскийУчетКлиентПереопределяемый.ПроверитьАктуальность(
			ЭтотОбъект, Объект.Организация, КонецДня(Объект.ДатаПерехода));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеПоОперациям(ПараметрыФункции, УникальныйИдентификатор)
	
	// Функция запуска расчета на сервере
	ИмяФункции = "Обработки.ПомощникПереходаСУСНнаОСНО.ДанныеПоОперациям";
	
	// Параметры длительной операции
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Получение данных по операциям перехода с УСН на ОСНО'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции, ПараметрыФункции);
	
КонецФункции

&НаКлиенте
Процедура ПолучитьДанныеПоОперациямЗавершение(Результат, ДопоплнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, Результат.КраткоеПредставлениеОшибки);
	Иначе
		ДанныеПоОперациям = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВвестиОстаткиПоНалоговомуУчетуНаСервере()
	
	ПараметрыВыполнения = НовыйПараметрыВыполнения();
	ПараметрыВыполнения.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводОстатковПоНалоговомуУчету;
	
	НачатьТранзакцию();
	
	Попытка
		Обработки.ПомощникПереходаСУСННаОСНО.ВвестиОстаткиПоНалоговомуУчету(ПараметрыВыполнения);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
	ДанныеПоОперациям = Обработки.ПомощникПереходаСУСНнаОСНО.ДанныеПоОперациям(ПараметрыВыполнения);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОтразитьДоходыНаСервере()
	
	ПараметрыВыполнения = НовыйПараметрыВыполнения();
	ПараметрыВыполнения.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводДоходовПереходногоПериода;
	
	НачатьТранзакцию();
	
	Попытка
		Обработки.ПомощникПереходаСУСННаОСНО.ОтразитьДоходы(ПараметрыВыполнения);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
	ДанныеПоОперациям = Обработки.ПомощникПереходаСУСНнаОСНО.ДанныеПоОперациям(ПараметрыВыполнения);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОтразитьРасходыНаСервере()
	
	ПараметрыВыполнения = НовыйПараметрыВыполнения();
	ПараметрыВыполнения.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО.ВводРасходовПереходногоПериода;
	
	НачатьТранзакцию();
	
	Попытка
		Обработки.ПомощникПереходаСУСННаОСНО.ОтразитьРасходы(ПараметрыВыполнения);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
	ДанныеПоОперациям = Обработки.ПомощникПереходаСУСНнаОСНО.ДанныеПоОперациям(ПараметрыВыполнения);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСтатус(ВидОперации)
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеПоОперациям, ВидОперации) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = НовыйПараметрыВыполнения();
	ПараметрыВыполнения.ВидОперации = Перечисления.ВидыОперацийПереходаСУСННаОСНО[ВидОперации];
	
	ДанныеПоОперации = ДанныеПоОперациям[ВидОперации];
	
	Если ДанныеПоОперации.Состояние <> СостояниеПропущено() Тогда
		ДанныеПоОперации.Состояние = СостояниеПропущено();
		Обработки.ПомощникПереходаСУСНнаОСНО.УдалитьРанееСозданныеДокументыПоОперации(ПараметрыВыполнения);
	Иначе
		ДанныеПоОперации.Состояние = СостояниеНеВыполнено();
	КонецЕсли;
	
	ПараметрыВыполнения.Вставить("ДанныеПоОперациям", ДанныеПоОперациям);
	Обработки.ПомощникПереходаСУСНнаОСНО.ОбновитьЗаписиРегистра(ПараметрыВыполнения);
	ДанныеПоОперациям = Обработки.ПомощникПереходаСУСНнаОСНО.ДанныеПоОперациям(ПараметрыВыполнения);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДатыПерехода(Организация)
	
	Возврат Обработки.ПомощникПереходаСУСННаОСНО.ДатыПереходаСУСННаОСНО(Организация);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокСуммы(Сумма)
	
	ФорматСуммы = "ЧДЦ=2; ЧН='0,00 руб.'; ЧГ=3,0; ЧФ='Ч руб.';";
	Возврат Формат(Сумма, ФорматСуммы);
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуРасшифровки(ДоходРасход)
	
	ПараметрыРасшифровки = Новый Структура;
	ПараметрыРасшифровки.Вставить("Организация", Объект.Организация);
	ПараметрыРасшифровки.Вставить("ДатаПерехода", Объект.ДатаПерехода);
	ПараметрыРасшифровки.Вставить("ДоходРасход", ДоходРасход);
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок               = НСтр("ru = 'Расшифровка сумм'");
	ПараметрыОткрытия.ИмяФормы                = "Обработка.ПомощникПереходаСУСНнаОСНО.Форма.ФормаРасшифровкаСуммы";
	ПараметрыОткрытия.ЗамерПроизводительности = "СозданиеФормыРасшифровкиСуммПереходСУСНнаОСНО";
	ПараметрыОткрытия.НавигационнаяСсылка     = Неопределено;
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыРасшифровки, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводНачальныхОстатковПоВнеоборотнымАктивам(РазделУчета)
	
	ПараметрыОтбора = Новый Структура("Дата, Организация, РазделУчета",
		Объект.ДатаПерехода, Объект.Организация, РазделУчета);
	ПараметрыОтбора.Вставить("ПомощникВводаНачальныхОстатков", Истина);
	ПараметрыФормы = Новый Структура("ПараметрыОтбораСписка", ПараметрыОтбора);
	
	ОткрытьФорму("Документ.ВводначальныхОстатков.ФормаСписка", ПараметрыФормы, ЭтотОбъект, РазделУчета);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыОтчета()
	
	ИмяОтчета = "ОборотноСальдоваяВедомость";
	ДатаФормированияОтчета = Объект.ДатаПерехода + 1;
	
	ПользовательскиеНастройкиКомпоновкиДанных = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("НачалоПериода", ДатаФормированияОтчета);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КонецПериода", ДатаФормированияОтчета);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Организация", Объект.Организация);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("ПоСубсчетам", Истина);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("ПоказательБУ", Ложь);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("ПоказательНУ", Ложь);
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("ПоказательСверкаНУ", Истина);
	
	НастройкиРасшифровки = Новый Структура;
	НастройкиРасшифровки.Вставить(ИмяОтчета, ПользовательскиеНастройкиКомпоновкиДанных);
	
	УсловияОтбора = Новый Структура();
	УсловияОтбора.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	ОбщиеНастройки = Новый Структура();
	ОбщиеНастройки.Вставить("Объект", УсловияОтбора);
	ОбщиеНастройки.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ОбщиеНастройки, Новый УникальныйИдентификатор);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ВидРасшифровки", 1);
	ПараметрыОтчета.Вставить("АдресНастроек", АдресХранилища);
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ИДРасшифровки", ИмяОтчета);
	ПараметрыОтчета.Вставить("РежимРасшифровки", Истина);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Функция НовыйПараметрыВыполнения()
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Организация", Объект.Организация);
	ПараметрыВыполнения.Вставить("ДатаПерехода", Объект.ДатаПерехода);
	ПараметрыВыполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийПереходаСУСННаОСНО.ПустаяСсылка());
	
	Возврат ПараметрыВыполнения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КартинкаСостоянияОперации(Состояние)
	
	Если Состояние = СостояниеВыполнено() Тогда
		Возврат БиблиотекаКартинок.ВыполненоУспешно;
	ИначеЕсли Состояние = СостояниеВыполненоСОшибками() Тогда
		Возврат БиблиотекаКартинок.ОперацияВыполненаСОшибками;
	ИначеЕсли Состояние = СостояниеПропущено() Тогда
		Возврат БиблиотекаКартинок.ОперацияПропущена;
	Иначе
		Возврат БиблиотекаКартинок.ОперацияРучнаяКорректировка;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокКомандыИзменитьСтатус(Состояние)
	
	Если Состояние = СостояниеПропущено() Тогда
		Возврат НСтр("ru = 'Вернуть автоматический статус'");
	Иначе
		Возврат НСтр("ru = 'Пропустить'");
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеВыполнено()
	Возврат "Выполнено";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеПропущено()
	Возврат "Пропущено";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеВыполненоСОшибками()
	Возврат "ВыполненоСОшибками";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеНеВыполнено()
	Возврат "НеВыполнено";
КонецФункции

#КонецОбласти