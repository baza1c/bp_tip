
#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем СоответствиеЭлементаФормыЗначению;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнформационнаяБазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	ДлинаСуток = 86400;
	Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	ИнформацияЭкрана = ОбщегоНазначенияКлиентСервер.ПараметрыЭкранаУстройства();
	
	Если ИнформацияЭкрана.Ширина > ВысокоеРазрешение() Тогда
		Элементы.СтраницаНизкоеРазрешение.Видимость = Ложь;
		РазрешениеЭкрана = "ВысокоеРазрешение";  
		Если ИнформацияЭкрана.Ширина > РазрешениеFullHD() Тогда 
			Элементы.Страницы.РастягиватьПоГоризонтали = Ложь;
	    КонецЕсли;
	Иначе 
		Элементы.СтраницаВысокоеРазрешение.Видимость = Ложь;
		РазрешениеЭкрана = "НизкоеРазрешение";
	КонецЕсли; 
	
	ВалютаРубль = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаЕвро = Справочники.Валюты.НайтиПоКоду("978");
	ВалютаДоллар = Справочники.Валюты.НайтиПоКоду("840");
	ВалютаЮань = Справочники.Валюты.НайтиПоКоду("156");
	
	ДатаАктуальности = ДатаАктуальности();
	ВариантКредиторскойЗадолженности= "Поставщикам";
	ЦветНеАктивнойСтроки = ЦветаСтиля.ЦветНеАктивнойСтроки;	
	ДлительнаяОперацияПлатежныйКалендарь = Новый Структура();
	ДлительнаяОперацияПлатежныйКалендарь.Вставить("Имя", "");
	ДлительнаяОперацияПлатежныйКалендарь.Вставить("ДлительнаяОперация");

	ЗаполнитьДанныеФинансыОрганизации();
	
	СведенияОДлительнойОперации = Новый Структура();
	СведенияОДлительнойОперации.Вставить("Имя", "");
	СведенияОДлительнойОперации.Вставить("ДлительнаяОперация");
	
	ОбновитьСведенияОСервисе();
	
	УстановитьПараметрыОтбора();
	СоздатьЭлементыФормы();
	
	Если Не Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда 
		ИзменитьПоложениеЭлементовНаФорме();
	КонецЕсли;
		
	ИспользуетсяСогласованиеПлатежей = ПолучитьФункциональнуюОпцию("ИспользоватьСогласованиеПлатежей");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьДоступныеПредложенияБанковНаКлиенте();		
	ВывестиДанныеРазделов(); 

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" И (Источник = "ИспользоватьПланированиеПлатежейОтПокупателей" 
			Или Источник = "ИспользоватьПланированиеПлатежейПоставщикам") Тогда
		ВывестиДанныеПлатежногоКалендаря();
	ИначеЕсли ИмяСобытия = "ИнтернетПоддержкаПодключена" И ОшибкаОбновленияСервиса Тогда
		ОбновитьСведенияОСервисе();
		ОбновитьДоступныеПредложенияБанковНаКлиенте();
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаПланированиеНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("Организация, СформироватьПриОткрытии", Организация, Истина);
	ОткрытьФорму("Обработка.ПлатежныйКалендарь.Форма", ПараметрыФормы);
		
КонецПроцедуры

&НаКлиенте
Процедура НадписьКредитыНажатие(Элемент)
	
	ФинУслугиКлиент.ПерейтиПоСсылкеКредитЛизинг(Ложь, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЛизингНажатие(Элемент)
	
	ФинУслугиКлиент.ПерейтиПоСсылкеКредитЛизинг(Истина, Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаСогласованоНажатие(Элемент)
	
	Если ИспользуетсяСогласованиеПлатежей Тогда
		ОткрытьСогласованиеПлатежей();
	Иначе
		ОткрытьСписокПлатежныхПоручений();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОбновитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	
	КраткоеНаименованиеБанка = НаименованиеБанка(Банк);
	// При изменении банка переполучать данные платежного календаря не нужно.
	ОбновитьФорму(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантКЗПриИзменении(Элемент)
	
	ВывестиДанныеКредиторскойЗадолженности();

КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбратнаяСвязь(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодменюПоСчету(Команда)
	
	ИмяЭлемента = ТекущийЭлемент.Имя; 
	ИмяЭлементаБезПостфикса = СтрЗаменить(ИмяЭлемента, РазрешениеЭкрана, "");
	НомерСтроки = СтрЗаменить(ИмяЭлементаБезПостфикса, Команда.Имя, "");  
	НомерСтроки = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(НомерСтроки);
	СтрокаТаблицыСчетов = СоответствиеЭлементаФормыЗначению[НомерСтроки];

	СписокВыбора = Новый СписокЗначений; 
	СписокВыбора.Добавить("Выписка",  НСтр("ru='Выписка по счету'"));
	Если Не СтрокаТаблицыСчетов.ЦифровойСчет Тогда
		СписокВыбора.Добавить("Платежи",  НСтр("ru='Платежные поручения'"));
	КонецЕсли; 
	СписокВыбора.Добавить("Обмен",  НСтр("ru='Обмен с банком'"));
	СписокВыбора.Добавить("БанковскиеОперации",  НСтр("ru='Отчет по банковским операциям'")); 
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("БанковскийСчет", СтрокаТаблицыСчетов.БанковскийСчет);
	ДополнительныеПараметры.Вставить("Организация", СтрокаТаблицыСчетов.Организация);

	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМенюПоСчету", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы[ИмяЭлемента]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораИзМенюПоСчету(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	БанковскийСчет = ДополнительныеПараметры.БанковскийСчет;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", ДополнительныеПараметры.Организация);
	
	Если ВыбранныйЭлемент.Значение = "Выписка" Тогда
		
		НаименованиеФормы = "ЖурналДокументов.Деньги.Форма.БанковскиеВыписки";
		Отбор.Вставить("БанковскийСчет", БанковскийСчет);

		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);

		ОткрытьФормуСОтбором(НаименованиеФормы, ПараметрыФормы);
		
	ИначеЕсли ВыбранныйЭлемент.Значение = "Платежи" Тогда
		
		НаименованиеФормы = "Документ.ПлатежноеПоручение.ФормаСписка";  
		Отбор.Вставить("СчетОрганизации", БанковскийСчет);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		ОткрытьФормуСОтбором(НаименованиеФормы, ПараметрыФормы);
		
	ИначеЕсли ВыбранныйЭлемент.Значение = "Обмен" Тогда
		
		НаименованиеФормы = "Обработка.КлиентБанк.Форма.Форма";
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("БанковскийСчет", БанковскийСчет); 
		ПараметрыФормы.Вставить("Организация", ДополнительныеПараметры.Организация); 
		ПараметрыФормы.Вставить("РежимПоУмолчанию", "ГруппаЗагрузка");
		ОткрытьФормуСОтбором(НаименованиеФормы, ПараметрыФормы);
		
	ИначеЕсли ВыбранныйЭлемент.Значение = "БанковскиеОперации" Тогда
		
		МассивОтборов = Новый Массив;
		ДобавитьВМассивОтборов(МассивОтборов, "РасчетныйСчет", БанковскийСчет, ВидСравненияКомпоновкиДанных.Равно);
		МассивГруппировок = Новый Массив;
		МассивСвойств = Новый Массив;

		ПользовательскиеНастройки = ПользовательскиеНастройкиОтчета(МассивОтборов, МассивГруппировок, МассивСвойств);
		
		ПараметрыОтчета = Новый Структура("ПользовательскиеНастройки", ПользовательскиеНастройки);
		ОткрытьФормуОтчета("ОтчетПоБанковскимОперациям", ПараметрыОтчета);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодменюБаннер(Команда)
	
	ФинУслугиКлиент.ПодменюБаннер(ЭтотОбъект, Команда, Организация, Банк, РазрешениеЭкрана);
		
КонецПроцедуры

&НаКлиенте
Процедура ПодменюЗадолженностьВсего(Команда)
	
	СписокВыбора = Новый СписокЗначений; 

	СписокВыбора.Добавить("ПоДоговорам", НСтр("ru='Задолженность по договорам'")); 
	СписокВыбора.Добавить("ПоСрокамДолга", НСтр("ru='Задолженность по срокам долга'"));
	СписокВыбора.Добавить("НеоплаченныеСчета", НСтр("ru='Неоплаченные счета'"));
	
	ИмяЭлемента = ТекущийЭлемент.Имя; 
	ИмяЭлементаБезПостфикса = СтрЗаменить(ИмяЭлемента, РазрешениеЭкрана, "");
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ИмяЭлемента", ИмяЭлементаБезПостфикса); 

	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМенюЗадолженность", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы[ИмяЭлемента]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораИзМенюЗадолженность(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура; 
	
	Если СтрНайти(ДополнительныеПараметры.ИмяЭлемента, "ДЗ") > 0 Тогда
		ВидОтчета = "Покупателей";
	Иначе
		ВидОтчета = "Поставщикам";
	КонецЕсли;

	Если ВыбранныйЭлемент.Значение = "ПоДоговорам" Тогда
		
		ВариантОтчета = СтрШаблон("Задолженность%1ПоОрганизациямИДоговорам", ВидОтчета);
		НаименованиеОтчета = СтрШаблон("Задолженность%1", ВидОтчета);

		ПараметрыОтчета.Вставить("КлючВарианта", ВариантОтчета);
						
	ИначеЕсли ВыбранныйЭлемент.Значение = "ПоСрокамДолга" Тогда
		
		НаименованиеОтчета = СтрШаблон("Задолженность%1ПоСрокамДолга", ВидОтчета);
				
	ИначеЕсли ВыбранныйЭлемент.Значение = "НеоплаченныеСчета" Тогда
		
		Если СтрНайти(ДополнительныеПараметры.ИмяЭлемента, "ДЗ") > 0 Тогда
			ВидОтчета = "Покупателям";
		Иначе
			ВидОтчета = "Поставщиков";
		КонецЕсли;

		НаименованиеОтчета = СтрШаблон("АнализНеоплаченныхСчетов%1", ВидОтчета);
		
	ИначеЕсли ВыбранныйЭлемент.Значение = "Динамика" Тогда
		
		ПараметрыОтчета.Вставить("ПоказательПросроченнаяЗадолженность", ДополнительныеПараметры.ПоказательПросроченнаяЗадолженность);
		ПараметрыОтчета.Вставить("ПоказательЗадолженность", ДополнительныеПараметры.ПоказательЗадолженность); 
		НаименованиеОтчета = СтрШаблон("ДинамикаЗадолженности%1", ВидОтчета);
		
	КонецЕсли;
	
	ОткрытьФормуОтчета(НаименованиеОтчета, ПараметрыОтчета);

КонецПроцедуры

&НаКлиенте
Процедура ПодменюЗадолженностьПросроченная(Команда)
	
	СписокВыбора = Новый СписокЗначений; 
	СписокВыбора.Добавить("Динамика", НСтр("ru='Динамика задолженности'"));
		
	ИмяЭлемента = ТекущийЭлемент.Имя; 
	ИмяЭлементаБезПостфикса = СтрЗаменить(ИмяЭлемента, РазрешениеЭкрана, "");
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ПоказательПросроченнаяЗадолженность", Истина);
	ДополнительныеПараметры.Вставить("ПоказательЗадолженность", Ложь); 
	ДополнительныеПараметры.Вставить("ИмяЭлемента", ИмяЭлементаБезПостфикса); 

	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМенюЗадолженность", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы[ИмяЭлемента]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодменюЗадолженностьТекущая(Команда)
	
	СписокВыбора = Новый СписокЗначений; 
	СписокВыбора.Добавить("Динамика", НСтр("ru='Динамика задолженности'")); 
		
	ИмяЭлемента = ТекущийЭлемент.Имя; 
	ИмяЭлементаБезПостфикса = СтрЗаменить(ИмяЭлемента, РазрешениеЭкрана, "");
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ПоказательПросроченнаяЗадолженность", Истина);
	ДополнительныеПараметры.Вставить("ПоказательЗадолженность", Истина); 
	ДополнительныеПараметры.Вставить("ИмяЭлемента", ИмяЭлементаБезПостфикса); 

	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМенюЗадолженность", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы[ИмяЭлемента]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодменюКЗКредиты(Команда)
	
	СписокВыбора = Новый СписокЗначений; 

	СписокВыбора.Добавить("Платежи",  НСтр("ru='Платежи по кредиту'"));
	
	ИмяЭлемента = ТекущийЭлемент.Имя; 
	ИмяЭлементаБезПостфикса = СтрЗаменить(ИмяЭлемента, РазрешениеЭкрана, "");
	НомерСтроки = СтрЗаменить(ИмяЭлементаБезПостфикса, Команда.Имя, "");
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("НомерСтроки", СтроковыеФункцииКлиентСервер.СтрокаВЧисло(НомерСтроки));
	ДополнительныеПараметры.Вставить("ИмяЭлемента", Команда.Имя); 

	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМенюКредитыИЛизинг", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы[ИмяЭлемента]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораИзМенюКредитыИЛизинг(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "Платежи" Тогда

		Раздел = "КредиторскаяЗадолженность";
		Индекс = НомерСтрокиВРазделеПоПозиции(Раздел, ДополнительныеПараметры.НомерСтроки, ТекущаяПозицияКредитыИЛизинг) - 1;
		
		Если ДополнительныеПараметры.ИмяЭлемента = "ПодменюКЗКредиты" Тогда
			Контрагент = ТаблицаКредиты[Индекс].Контрагент;
			СчетаРасчетов = СчетаРасчетовКредиты();
		Иначе
			Контрагент = ТаблицаЛизинг[Индекс].Контрагент;
			СчетаРасчетов = СчетаРасчетовЛизинг();
		КонецЕсли;
		
		ПараметрыОтчета = ПараметрыОтчетаАнализСубконто(Контрагент, СчетаРасчетов);
		ОткрытьФормуОтчета("АнализСубконто", ПараметрыОтчета);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыОтчетаАнализСубконто(Контрагент, СчетаРасчетов) 
	
	МассивОтборов = Новый Массив;
	ДобавитьВМассивОтборов(МассивОтборов, "Субконто1", Контрагент, ВидСравненияКомпоновкиДанных.Равно);
	ДобавитьВМассивОтборов(МассивОтборов, "Счет", СчетаРасчетов, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии);
	
	МассивГруппировок = Новый Массив;
	ДобавитьВМассивГруппировок(МассивГруппировок, "Субконто1", "Контрагенты");
	ДобавитьВМассивГруппировок(МассивГруппировок, "Субконто2", "Договоры");
	
	МассивСвойств = Новый Массив;
	ДобавитьВМассивСвойств(МассивСвойств, "СписокВидовСубконто", СписокВидовСубконтоДляАнализаСубконто());
	ДобавитьВМассивСвойств(МассивСвойств, "ПоСубсчетам", Истина);
	
	ПользовательскиеНастройки = ПользовательскиеНастройкиОтчета(МассивОтборов, МассивГруппировок, МассивСвойств);
		
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыОтчета.Вставить("ПоСубсчетам", Истина);
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);  
	ПараметрыОтчета.Вставить("РежимРасшифровки", Истина);
	ПараметрыОтчета.Вставить("ВидРасшифровки", 2);

	Возврат ПараметрыОтчета;
	
КонецФункции

&НаКлиенте
Процедура ПодменюКЗЛизинг(Команда)
	
	СписокВыбора = Новый СписокЗначений; 

	СписокВыбора.Добавить("Платежи",  НСтр("ru='Платежи по лизингу'"));
	
	ИмяЭлемента = ТекущийЭлемент.Имя; 
	ИмяЭлементаБезПостфикса = СтрЗаменить(ИмяЭлемента, РазрешениеЭкрана, "");
	НомерСтроки = СтрЗаменить(ИмяЭлементаБезПостфикса, Команда.Имя, "");
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("НомерСтроки", СтроковыеФункцииКлиентСервер.СтрокаВЧисло(НомерСтроки));
	ДополнительныеПараметры.Вставить("ИмяЭлемента", Команда.Имя); 

	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМенюКредитыИЛизинг", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВыборИзМеню(Оповещение, СписокВыбора, Элементы[ИмяЭлемента]);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьСтраницуБаннеров(Команда)
	
	ТекущаяПозицияБаннеры = ТекущаяПозицияБаннеры + 1;
	ФинУслугиКлиент.ОбновитьРотациюБаннеров(ЭтотОбъект, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьОбратноСтраницуБаннеров(Команда)
	
	ТекущаяПозицияБаннеры = ТекущаяПозицияБаннеры - 1;
	ФинУслугиКлиент.ОбновитьРотациюБаннеров(ЭтотОбъект, Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПанель(Команда)
	
	ОбновитьДанныеМонитораРуководителя();
	
КонецПроцедуры

&НаКлиенте
Процедура СкроллВлевоСчета(Команда)
	
	ТекущаяПозицияСчета = ТекущаяПозицияСчета - 1;

	// Смещаем курсор в начало предыдущей страницы
	ПереместитьПозициюВНачалоСтраницы(ТекущаяПозицияСчета, "Счета");	
	ВывестиСтрокиТаблицыПоСчетам();
	
КонецПроцедуры

&НаКлиенте
Процедура СкроллВправоСчета(Команда)
	
	ТекущаяПозицияСчета = ТекущаяПозицияСчета + 1;
	ВывестиСтрокиТаблицыПоСчетам(); 
	
КонецПроцедуры

&НаКлиенте
Процедура СкроллВлевоКредиторскаяЗадолженность(Команда)
	
	ТекущаяПозицияКредитыИЛизинг = ТекущаяПозицияКредитыИЛизинг - 1;

	// Смещаем курсор в начало предыдыдущей страницы
	ПереместитьПозициюВНачалоСтраницы(ТекущаяПозицияКредитыИЛизинг, "КредиторскаяЗадолженность");	
	ВывестиСтрокиТаблицыПоВарианту();
	
КонецПроцедуры

&НаКлиенте
Процедура СкроллВправоКредиторскаяЗадолженность(Команда) 
	
	ТекущаяПозицияКредитыИЛизинг = ТекущаяПозицияКредитыИЛизинг + 1;
	ВывестиСтрокиТаблицыПоВарианту(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОпцияПланированиеПлатежейНажатие(Элемент)
	ПараметрыФормы = Новый Структура("ТекущаяСтраница", "ГруппаПланированиеПлатежей");
	ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма", ПараметрыФормы);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеДанных

&НаСервере
Процедура ЗаполнитьДанныеФинансыОрганизации(ПолучитьДанныеПлатежногоКалендаря = Истина)
	
	Если ПолучитьДанныеПлатежногоКалендаря Тогда
		НачатьЗаполнениеКалендаряНаСервере();
	КонецЕсли;
	ЗаполнитьОстаткиПоРасчетнымСчетам();
	ЗаполнитьОстаткиПоКредитамИЛизингу();
	ЗаполнитьКоличествоПодготовленныхПлатежей();
	ЗаполнитьПлатежиСегодня();
	ЗаполнитьОстаткиВзаиморасчетов();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткиВзаиморасчетов()
	
	// Получение данных дебиторской и кредиторской задолженности из ДанныеМонитораРуководителя
	ВыбранныеРазделы = СписокРазделовВзаиморасчетыСКонтрагентами();
	
	ДанныеРазделовМонитора = МониторРуководителя.ПолучитьДанныеРазделовМонитора(Организация, ВыбранныеРазделы, 1);
	
	Если ДанныеРазделовМонитора <> Неопределено Тогда
		
		Для Каждого РазделМонитора Из ВыбранныеРазделы Цикл
			
			ЗаполнитьРазделПоДаннымМонитораРуководителя(РазделМонитора, ДанныеРазделовМонитора);
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРазделПоДаннымМонитораРуководителя(Раздел, ДанныеРаздела)
	
	Если Раздел = Перечисления.РазделыМонитораРуководителя.ЗадолженностьПокупателей Тогда
		
		ЗаполнитьРазделВзаиморасчетов("ДЗВсего", ДанныеРаздела, Раздел);
		
	ИначеЕсли Раздел = Перечисления.РазделыМонитораРуководителя.ЗадолженностьПоставщикам Тогда
		
		ЗаполнитьРазделВзаиморасчетов("КЗПоставщикамВсего", ДанныеРаздела, Раздел);
		
	ИначеЕсли Раздел = Перечисления.РазделыМонитораРуководителя.ПросроченнаяЗадолженностьПокупателей Тогда
		
		ЗаполнитьРазделВзаиморасчетов("ДЗПросроченная", ДанныеРаздела, Раздел);
		
	ИначеЕсли Раздел = Перечисления.РазделыМонитораРуководителя.ПросроченнаяЗадолженностьПоставщикам Тогда
		
		ЗаполнитьРазделВзаиморасчетов("КЗПоставщикамПросроченная", ДанныеРаздела, Раздел);

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРазделВзаиморасчетов(КлючРаздела, ДанныеРазделаМонитора, Раздел)
	
	Порядок = ПорядокИтоговПоВзаиморасчетамКонтрагентов(Раздел);
	
	Отбор = Новый Структура;
	Отбор.Вставить("РазделМонитора", Раздел);
	Отбор.Вставить("Порядок", Порядок);
	
	ДанныеРаздела = ДанныеРазделаМонитора.НайтиСтроки(Отбор);
	Если ДанныеРаздела.Количество() > 0 Тогда
		
		ЗначениеРаздела = ДанныеРаздела[0];
		
		ЭтотОбъект[КлючРаздела] = ЗначениеРаздела.Сумма;
		
	Иначе
		
		ЭтотОбъект[КлючРаздела] = 0;

	КонецЕсли;
	
	РассчитатьПоказателиВзаиморасчетов();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьПоказателиВзаиморасчетов()
	
	ДЗТекущая = ДЗВсего - ДЗПросроченная;
	КЗПоставщикамТекущая = КЗПоставщикамВсего - КЗПоставщикамПросроченная;
	
КонецПроцедуры

&НаСервере
Функция ПорядокИтоговПоВзаиморасчетамКонтрагентов(Раздел)
	
	Если Раздел = Перечисления.РазделыМонитораРуководителя.ЗадолженностьПокупателей Тогда
		
		Порядок = Отчеты.ЗадолженностьПокупателей.ПорядокИтоговВМониторе();
		
	ИначеЕсли Раздел = Перечисления.РазделыМонитораРуководителя.ЗадолженностьПоставщикам Тогда
		
		Порядок = Отчеты.ЗадолженностьПоставщикам.ПорядокИтоговВМониторе();
		
	ИначеЕсли Раздел = Перечисления.РазделыМонитораРуководителя.ПросроченнаяЗадолженностьПокупателей Тогда
		
		Порядок = Отчеты.ЗадолженностьПокупателейПоСрокамДолга.ПорядокИтоговВМониторе();
		
	ИначеЕсли Раздел = Перечисления.РазделыМонитораРуководителя.ПросроченнаяЗадолженностьПоставщикам Тогда
		
		Порядок = Отчеты.ЗадолженностьПоставщикамПоСрокамДолга.ПорядокИтоговВМониторе();
		
	Иначе
		
		Порядок = 0;
		
	КонецЕсли;
	
	Возврат Порядок;
	
КонецФункции

&НаСервере
Функция СписокРазделовВзаиморасчетыСКонтрагентами()
	
	Разделы = Новый Массив;
	Разделы.Добавить(Перечисления.РазделыМонитораРуководителя.ЗадолженностьПокупателей);
	Разделы.Добавить(Перечисления.РазделыМонитораРуководителя.ЗадолженностьПоставщикам);
	Разделы.Добавить(Перечисления.РазделыМонитораРуководителя.ПросроченнаяЗадолженностьПокупателей);
	Разделы.Добавить(Перечисления.РазделыМонитораРуководителя.ПросроченнаяЗадолженностьПоставщикам);
	
	Возврат Разделы;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОстаткиПоРасчетнымСчетам()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ХозрасчетныйОстатки.Счет) КАК СчетБанк,
	|	МИНИМУМ(ХозрасчетныйОстатки.Счет.Наименование) КАК НаименованиеСчета,
	|	МИНИМУМ(ХозрасчетныйОстатки.Организация) КАК Организация,
	|	МИНИМУМ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.БанковскиеСчета)) КАК БанковскийСчет,
	|	МИНИМУМ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.БанковскиеСчета).Банк.Наименование) КАК НаименованиеБанка,
	|	МИНИМУМ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.БанковскиеСчета).Банк) КАК Банк,
	|	МИНИМУМ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.БанковскиеСчета).ЦифровойСчет) КАК ЦифровойСчет,
	|	МИНИМУМ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.БанковскиеСчета).НомерСчета) КАК НомерСчета,
	|	МИНИМУМ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.БанковскиеСчета).Наименование) КАК НаименованиеБанковскогоСчета,
	|	МИНИМУМ(ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.БанковскиеСчета).Валютный) КАК Валютный,
	|	СУММА(ВЫБОР
	|			КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.Валюта, &ВалютаРеглУчета) = &ВалютаРеглУчета
	|				ТОГДА ХозрасчетныйОстатки.СуммаОстаток
	|			ИНАЧЕ ХозрасчетныйОстатки.ВалютнаяСуммаОстаток
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК РублеваяСумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.Валюта, &ВалютаРеглУчета) = &ВалютаРеглУчета
	|			ТОГДА 0
	|		ИНАЧЕ ХозрасчетныйОстатки.Валюта.Код
	|	КОНЕЦ КАК Порядок,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.Валюта, &ВалютаРеглУчета) КАК Валюта
	|ПОМЕСТИТЬ ВТ_ОстаткиПоРасчетнымСчетам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ГраницаОстатков, Счет В (&СчетаУчета), , Организация В (&ДоступныеОрганизации)) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.БанковскиеСчета),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.Валюта, &ВалютаРеглУчета) = &ВалютаРеглУчета
	|			ТОГДА 0
	|		ИНАЧЕ ХозрасчетныйОстатки.Валюта.Код
	|	КОНЕЦ,
	|	ХозрасчетныйОстатки.Валюта,
	|	ХозрасчетныйОстатки.Счет.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БанковскиеСчета.Владелец КАК Организация,
	|	БанковскиеСчета.Ссылка КАК БанковскийСчет,
	|	БанковскиеСчета.Банк.Наименование КАК НаименованиеБанка,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	БанковскиеСчета.Наименование КАК НаименованиеБанковскогоСчета,
	|	БанковскиеСчета.Валютный КАК Валютный,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.СчетБанк = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ВТ_ОстаткиПоРасчетнымСчетам.СчетБанк
	|		ИНАЧЕ БанковскиеСчета.СчетБанк
	|	КОНЕЦ КАК СчетБанк,
	|	ЕСТЬNULL(ВТ_ОстаткиПоРасчетнымСчетам.Сумма, 0) КАК Сумма,
	|	ЕСТЬNULL(ВТ_ОстаткиПоРасчетнымСчетам.РублеваяСумма, 0) КАК РублеваяСумма,
	|	ЕСТЬNULL(ВТ_ОстаткиПоРасчетнымСчетам.Порядок, ВЫБОР
	|			КОГДА БанковскиеСчета.ВалютаДенежныхСредств = &ВалютаРеглУчета
	|					ИЛИ БанковскиеСчета.ВалютаДенежныхСредств = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ БанковскиеСчета.ВалютаДенежныхСредств.Код
	|		КОНЕЦ) КАК Порядок,
	|	ЕСТЬNULL(ВТ_ОстаткиПоРасчетнымСчетам.Валюта, ВЫБОР
	|			КОГДА БанковскиеСчета.ВалютаДенежныхСредств = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				ТОГДА &ВалютаРеглУчета
	|			ИНАЧЕ БанковскиеСчета.ВалютаДенежныхСредств
	|		КОНЕЦ) КАК Валюта,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.СчетБанк = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ВТ_ОстаткиПоРасчетнымСчетам.СчетБанк.Наименование
	|		ИНАЧЕ БанковскиеСчета.СчетБанк.Наименование
	|	КОНЕЦ КАК НаименованиеСчета,
	|	БанковскиеСчета.ЦифровойСчет КАК ЦифровойСчет,
	|	БанковскиеСчета.Банк КАК Банк
	|ПОМЕСТИТЬ ВТ_ТаблицаСчетовБезОтбора
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиПоРасчетнымСчетам КАК ВТ_ОстаткиПоРасчетнымСчетам
	|		ПО (ВТ_ОстаткиПоРасчетнымСчетам.БанковскийСчет = БанковскиеСчета.Ссылка)
	|ГДЕ
	|	БанковскиеСчета.Владелец ССЫЛКА Справочник.Организации
	|	И БанковскиеСчета.Владелец В(&ДоступныеОрганизации)
	|	И (БанковскиеСчета.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ БанковскиеСчета.ДатаЗакрытия > &ДатаАктуальности
	|			ИЛИ ВТ_ОстаткиПоРасчетнымСчетам.РублеваяСумма <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ОстаткиПоРасчетнымСчетам.Организация,
	|	ВТ_ОстаткиПоРасчетнымСчетам.БанковскийСчет,
	|	ВТ_ОстаткиПоРасчетнымСчетам.НаименованиеБанка,
	|	ВТ_ОстаткиПоРасчетнымСчетам.НомерСчета,
	|	ВТ_ОстаткиПоРасчетнымСчетам.НаименованиеБанковскогоСчета,
	|	ВТ_ОстаткиПоРасчетнымСчетам.Валютный,
	|	ВТ_ОстаткиПоРасчетнымСчетам.СчетБанк,
	|	ВТ_ОстаткиПоРасчетнымСчетам.Сумма,
	|	ВТ_ОстаткиПоРасчетнымСчетам.РублеваяСумма,
	|	ВТ_ОстаткиПоРасчетнымСчетам.Порядок,
	|	ВТ_ОстаткиПоРасчетнымСчетам.Валюта,
	|	ВТ_ОстаткиПоРасчетнымСчетам.НаименованиеСчета,
	|	ВТ_ОстаткиПоРасчетнымСчетам.ЦифровойСчет,
	|	ВТ_ОстаткиПоРасчетнымСчетам.Банк
	|ИЗ
	|	ВТ_ОстаткиПоРасчетнымСчетам КАК ВТ_ОстаткиПоРасчетнымСчетам
	|ГДЕ
	|	ВТ_ОстаткиПоРасчетнымСчетам.РублеваяСумма <> 0
	|	И ВТ_ОстаткиПоРасчетнымСчетам.БанковскийСчет = ЗНАЧЕНИЕ(Справочник.БанковскиеСчета.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаСчетовБезОтбора.Организация КАК Организация,
	|	ВТ_ТаблицаСчетовБезОтбора.БанковскийСчет КАК БанковскийСчет,
	|	ВТ_ТаблицаСчетовБезОтбора.НаименованиеБанка КАК НаименованиеБанка,
	|	ВТ_ТаблицаСчетовБезОтбора.НомерСчета КАК НомерСчета,
	|	ВТ_ТаблицаСчетовБезОтбора.НаименованиеБанковскогоСчета КАК НаименованиеБанковскогоСчета,
	|	ВТ_ТаблицаСчетовБезОтбора.Валютный КАК Валютный,
	|	ВТ_ТаблицаСчетовБезОтбора.СчетБанк КАК СчетБанк,
	|	ВТ_ТаблицаСчетовБезОтбора.Сумма КАК Сумма,
	|	ВТ_ТаблицаСчетовБезОтбора.РублеваяСумма КАК РублеваяСумма,
	|	ВТ_ТаблицаСчетовБезОтбора.Порядок КАК Порядок,
	|	ВТ_ТаблицаСчетовБезОтбора.Валюта КАК Валюта,
	|	ВТ_ТаблицаСчетовБезОтбора.НаименованиеСчета КАК НаименованиеСчета,
	|	ВТ_ТаблицаСчетовБезОтбора.ЦифровойСчет КАК ЦифровойСчет,
	|	ВТ_ТаблицаСчетовБезОтбора.Банк КАК Банк,
	|	ВТ_ТаблицаСчетовБезОтбора.Валюта.Представление КАК ПредставлениеВалюты
	|ИЗ
	|	ВТ_ТаблицаСчетовБезОтбора КАК ВТ_ТаблицаСчетовБезОтбора
	|ГДЕ
	|	&ОтборБанковскогоСчетаПоБанку
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Сумма УБЫВ,
	|	Организация
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(РублеваяСумма)
	|ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаСчетовБезОтбора.Организация КАК Организация,
	|	ВТ_ТаблицаСчетовБезОтбора.БанковскийСчет КАК БанковскийСчет,
	|	ВТ_ТаблицаСчетовБезОтбора.НаименованиеБанка КАК НаименованиеБанка,
	|	ВТ_ТаблицаСчетовБезОтбора.НомерСчета КАК НомерСчета,
	|	ВТ_ТаблицаСчетовБезОтбора.НаименованиеБанковскогоСчета КАК НаименованиеБанковскогоСчета,
	|	ВТ_ТаблицаСчетовБезОтбора.Валютный КАК Валютный,
	|	ВТ_ТаблицаСчетовБезОтбора.СчетБанк КАК СчетБанк,
	|	ВТ_ТаблицаСчетовБезОтбора.Сумма КАК Сумма,
	|	ВТ_ТаблицаСчетовБезОтбора.РублеваяСумма КАК РублеваяСумма,
	|	ВТ_ТаблицаСчетовБезОтбора.Порядок КАК Порядок,
	|	ВТ_ТаблицаСчетовБезОтбора.Валюта КАК Валюта,
	|	ВТ_ТаблицаСчетовБезОтбора.НаименованиеСчета КАК НаименованиеСчета,
	|	ВТ_ТаблицаСчетовБезОтбора.ЦифровойСчет КАК ЦифровойСчет,
	|	ВТ_ТаблицаСчетовБезОтбора.Банк КАК Банк,
	|	ВТ_ТаблицаСчетовБезОтбора.Валюта.Представление КАК ПредставлениеВалюты
	|ИЗ
	|	ВТ_ТаблицаСчетовБезОтбора КАК ВТ_ТаблицаСчетовБезОтбора
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Сумма УБЫВ,
	|	Организация
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(РублеваяСумма)
	|ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_ТаблицаСчетовБезОтбора.РублеваяСумма) КАК РублеваяСумма
	|ИЗ
	|	ВТ_ТаблицаСчетовБезОтбора КАК ВТ_ТаблицаСчетовБезОтбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаСчетовБезОтбора.НаименованиеБанка КАК НаименованиеБанка,
	|	ВТ_ТаблицаСчетовБезОтбора.Банк КАК Банк
	|ИЗ
	|	ВТ_ТаблицаСчетовБезОтбора КАК ВТ_ТаблицаСчетовБезОтбора
	|ГДЕ
	|	ВТ_ТаблицаСчетовБезОтбора.ЦифровойСчет = ЛОЖЬ
	|	И ВТ_ТаблицаСчетовБезОтбора.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчета.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаСчетовБезОтбора.НаименованиеБанка,
	|	ВТ_ТаблицаСчетовБезОтбора.Банк
	|
	|УПОРЯДОЧИТЬ ПО
	|	НаименованиеБанка");
	
	ИзменитьТекстЗапросаПоОрганизации(Запрос);
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);

	МассивДоступныхОрганизаций = ДоступныеОрганизации;
	Если ЗначениеЗаполнено(Организация) Тогда
		МассивДоступныхОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	КонецЕсли;
	Запрос.УстановитьПараметр("ДоступныеОрганизации", МассивДоступныхОрганизаций);

	Если ЗначениеЗаполнено(Банк) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоБанку", "ВЫРАЗИТЬ(Субконто1 КАК Справочник.БанковскиеСчета).Банк = (&Банк)"); 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборБанковскогоСчетаПоБанку", "ВТ_ТаблицаСчетовБезОтбора.Банк = (&Банк)");

		Запрос.УстановитьПараметр("Банк", Банк);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоБанку", "Истина");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборБанковскогоСчетаПоБанку", "Истина");

	КонецЕсли;
	
	МассивСчетов = УчетДенежныхСредствПовтИсп.ПолучитьСчетаССубконтоБанковскиеСчета();
	
	Запрос.УстановитьПараметр("СчетаУчета", МассивСчетов);
	
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);
	Запрос.УстановитьПараметр("ДатаАктуальности", КонецДня(ДатаАктуальности));
	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(КонецДня(ДатаАктуальности), ВидГраницы.Включая));

	Результат = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	ТаблицаБанковскихСчетов.Очистить();
	ТаблицаДвиженийПоСчету.Очистить();
	ИтогВРублях = 0;
	ТаблицаБанков = Результат[5].Выгрузить();
	ДоступныеБанки.Загрузить(ТаблицаБанков);
	
	Если ЗначениеЗаполнено(Банк) И ТаблицаБанков.Найти(Банк) = Неопределено Тогда
		// Выбран Банк несовместимый с организацией
		Банк = Неопределено;
		РезультатОстатковПоСчетам = Результат[3];
	Иначе
		РезультатОстатковПоСчетам = Результат[2];
	КонецЕсли;
	
	Если РезультатОстатковПоСчетам.Пустой() Тогда
		СоздатьПустуюТаблицуСчетов();
	Иначе
		ТаблицаОстатки = РезультатОстатковПоСчетам.Выгрузить(); 
		ТаблицаБанковскихСчетов.Загрузить(ТаблицаОстатки);
		ПроверитьНаименованияСчетов();
		
		ВыборкаИтогов = Результат[4].Выбрать();
		Если ВыборкаИтогов.Следующий() Тогда
			ИтогВРублях = ВыборкаИтогов.РублеваяСумма;
		КонецЕсли;
	
		// В таблице содержится данные только по одному счету
		Если ТаблицаОстатки.Количество() = 2 Тогда 
			ЗаполнитьДвиженияПоСчету(ТаблицаОстатки[1].БанковскийСчет);
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНаименованияСчетов()
	
	СоответствиеИменСчетов = СоответствиеИменСчетов();
	Для Каждого СтрокаТаблицы Из ТаблицаБанковскихСчетов Цикл 
		АвтонаименованиеСчета = ЭтоАвтоНаименованиеБанковскогоСчета(СтрокаТаблицы);
		НаименованиеСчетаУчета = СоответствиеИменСчетов.Получить(СтрокаТаблицы.СчетБанк);
		Если АвтонаименованиеСчета
			Или Не ЗначениеЗаполнено(СтрокаТаблицы.БанковскийСчет)Тогда
		
			Если НаименованиеСчетаУчета <> Неопределено Тогда
				СтрокаТаблицы.НаименованиеСчета = СтрШаблон("%1, %2", НаименованиеСчетаУчета, СтрокаТаблицы.Валюта);
			КонецЕсли; 
			
		Иначе
			
			НомерСчета = СтрокаТаблицы.НомерСчета + ",";
			Если НаименованиеСчетаУчета <> Неопределено 
				И СтрНачинаетсяС(СтрокаТаблицы.НаименованиеБанковскогоСчета, НомерСчета) Тогда

				Наименование = СтрЗаменить(СтрокаТаблицы.НаименованиеБанковскогоСчета, НомерСчета, НаименованиеСчетаУчета);
				СтрокаТаблицы.НаименованиеСчета = СтрШаблон("%1, %2", Наименование, СтрокаТаблицы.Валюта);
			Иначе
				СтрокаТаблицы.НаименованиеСчета = СтрШаблон("%1, %2", СтрокаТаблицы.НаименованиеБанковскогоСчета, СтрокаТаблицы.Валюта);
			КонецЕсли;
							
		КонецЕсли;

	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция ЭтоАвтоНаименованиеБанковскогоСчета(СтрокаТаблицы)
	
	ПредставлениеВалюты = "" + СтрокаТаблицы.Валюта;
	ПредставлениеБанка = СокрЛП(СтрокаТаблицы.НаименованиеБанка);
	
	СтрокаНаименования0 = УчетДенежныхСредствКлиентСервер.НаименованиеБанковскогоСчетаПоУмолчанию(
		СтрокаТаблицы.НомерСчета,
		ПредставлениеБанка,
		ПредставлениеВалюты,
		СтрокаТаблицы.Валютный,
		0); 

	СтрокаНаименования1 = УчетДенежныхСредствКлиентСервер.НаименованиеБанковскогоСчетаПоУмолчанию(
		СтрокаТаблицы.НомерСчета,
		ПредставлениеБанка,
		ПредставлениеВалюты,
		СтрокаТаблицы.Валютный,
		1); 
			
	СтрокаНаименования2 = УчетДенежныхСредствКлиентСервер.НаименованиеБанковскогоСчетаПоУмолчанию(
		СтрокаТаблицы.НомерСчета,
		ПредставлениеБанка,
		ПредставлениеВалюты,
		СтрокаТаблицы.Валютный,
		2);
		
	СтрокаНаименования3 = БанковскиеСчетаФормыКлиентСервер.НаименованиеБанковскогоСчета(СтрокаТаблицы.БанковскийСчет, ПредставлениеБанка);
	
	Наименование = Врег(СокрЛП(СтрокаТаблицы.НаименованиеБанковскогоСчета));
	Если Наименование = Врег(СтрокаНаименования0)
		Или Наименование = Врег(СтрокаНаименования1)
		Или Наименование = Врег(СтрокаНаименования2)
		Или Наименование = Врег(СтрокаНаименования3) Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура СоздатьПустуюТаблицуСчетов()
	
	// Группа
	СтрокаСчетов = ТаблицаБанковскихСчетов.Добавить();
	СтрокаСчетов.Валюта = ВалютаРубль;
	// Строка
	СтрокаСчетов = ТаблицаБанковскихСчетов.Добавить();
	СтрокаСчетов.СчетБанк = ПланыСчетов.Хозрасчетный.РасчетныеСчета;
	СтрокаСчетов.НаименованиеБанковскогоСчета = НСтр("ru='Расчетный счет'");
	СтрокаСчетов.Валюта = ВалютаРубль;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДвиженияПоСчету(БанковскийСчет)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОбороты.Субконто1 КАК БанковскийСчет,
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотДт) КАК Приход,
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК Расход
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &ОкончаниеМесяца, Авто, Счет В (&СчетаУчета), , Субконто1 = &БанковскийСчет, , ) КАК ХозрасчетныйОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто1");
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ДатаАктуальности));
	Запрос.УстановитьПараметр("ОкончаниеМесяца", КонецМесяца(ДатаАктуальности));

	МассивСчетов = УчетДенежныхСредствПовтИсп.ПолучитьСчетаССубконтоБанковскиеСчета();
	
	Запрос.УстановитьПараметр("СчетаУчета", МассивСчетов);
	
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);

	Результат = Запрос.Выполнить();
	
	ТаблицаОбороты = Результат.Выгрузить();
	Если Результат.Пустой() Тогда
		СоздатьПустуюСтрокуДвиженийПоСчету(БанковскийСчет);
	Иначе
		ТаблицаДвиженийПоСчету.Загрузить(ТаблицаОбороты);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткиПоКредитамИЛизингу()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
	|	СУММА(ХозрасчетныйОстатки.СуммаОстатокКт) КАК Сумма,
	|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Контрагенты).Представление КАК ПредставлениеКонтрагента
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаОстатков,
	|			Счет В (&СчетаРасчетов),
	|			,
	|			&ОтборПоОрганизации
	|				И &ОтборПоКонтрагенту) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Организация,
	|	ХозрасчетныйОстатки.Субконто1");
	
	ИзменитьТекстЗапросаПоОрганизации(Запрос);
	
	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(КонецДня(ДатаАктуальности), ВидГраницы.Включая));

	Если ЗначениеЗаполнено(Банк) Тогда
	
		ДанныеБанка = РаботаСБанкамиБП.ПолучитьДанныеКонтрагентаБанка(Банк, Ложь);
		КонтрагентыГруппыКомпаний = РегистрыСведений.ГруппыКомпанийБанков.КонтрагентыГруппыКомпанийБанка(ДанныеБанка.ИНН);
				
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоКонтрагенту", "Субконто1 В (&Контрагенты)");
		Запрос.УстановитьПараметр("Контрагенты", КонтрагентыГруппыКомпаний);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоКонтрагенту", "Истина");

	КонецЕсли;
			
	// Лизинг
	СчетаРасчетовЛизинг = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаРасчетовЛизинг());
	Запрос.УстановитьПараметр("СчетаРасчетов", СчетаРасчетовЛизинг);
	
	ТаблицаОстатки = Запрос.Выполнить().Выгрузить();
	ТаблицаЛизинг.Загрузить(ТаблицаОстатки);
	ЗаполнитьПредставление(ТаблицаЛизинг);
	// Кредиты
	СчетаРасчетовКредиты = БухгалтерскийУчет.СформироватьМассивСубсчетов(СчетаРасчетовКредиты());
	Запрос.УстановитьПараметр("СчетаРасчетов", СчетаРасчетовКредиты);
	
	ТаблицаОстатки = Запрос.Выполнить().Выгрузить();
	ТаблицаКредиты.Загрузить(ТаблицаОстатки);
	ЗаполнитьПредставление(ТаблицаКредиты);
	
КонецПроцедуры

&НаСервере
Процедура  ЗаполнитьПредставление(ТаблицаДанных)
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		СтрокаТаблицы.ПредставлениеКонтрагента = СтрШаблон(НСтр("ru='%1, руб'"), СтрокаТаблицы.ПредставлениеКонтрагента);
	КонецЦикла;
			
КонецПроцедуры

&НаСервере
Функция СчетаРасчетовКредиты()
	
	СчетаРасчетов = Новый Массив;
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоКраткосрочнымКредитамИЗаймам); // 66
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоДолгосрочнымКредитамИЗаймам);  // 67
	
	Возврат СчетаРасчетов;
	
КонецФункции

&НаСервере
Функция СчетаРасчетовЛизинг()
	
	СчетаРасчетов = Новый Массив;
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАренде);    // 76.07
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАрендеВал); // 76.27
	СчетаРасчетов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАрендеУЕ);  // 76.37
	
	Возврат СчетаРасчетов;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПлатежиСегодня()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.БанковскиеСчета).Банк КАК Банк,
	|	ХозрасчетныйОбороты.СуммаОборотДт КАК Приход,
	|	ХозрасчетныйОбороты.СуммаОборотКт КАК Расход
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоДня, &КонецДня, Авто, Счет В (&СчетаУчета), , &ОтборПоОрганизации, , ) КАК ХозрасчетныйОбороты");
	
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ДатаАктуальности));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(ДатаАктуальности));
	
	ИзменитьТекстЗапросаПоОрганизации(Запрос);
		
	МассивСчетов = УчетДенежныхСредствПовтИсп.ПолучитьСчетаССубконтоБанковскиеСчета();
	
	Запрос.УстановитьПараметр("СчетаУчета", МассивСчетов);
	
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ВалютаРеглУчета);

	Результат = Запрос.Выполнить();
	
	ТаблицаПлатежиСегодняПоБанкам.Очистить();
	ПлатежиСегодняПриход = 0;
	ПлатежиСегодняРасход = 0;

	ТаблицаОбороты = Результат.Выгрузить();
	Если Не Результат.Пустой() Тогда
		ТаблицаПлатежиСегодняПоБанкам.Загрузить(ТаблицаОбороты);
		ТаблицаОбороты.Свернуть(, "Приход, Расход");
		ПлатежиСегодняПриход = ТаблицаОбороты[0].Приход;
		ПлатежиСегодняРасход = ТаблицаОбороты[0].Расход;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТекстЗапросаПоОрганизации(Запрос)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "Организация = &Организация"); 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборБанковскогоСчетаПоОрганизации", "БанковскиеСчета.Владелец = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "Истина");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборБанковскогоСчетаПоОрганизации", "Истина");

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКоличествоПодготовленныхПлатежей()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПлатежноеПоручение.Ссылка) КАК КоличествоПлатежей
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|		ПО ПлатежноеПоручение.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект
	|ГДЕ
	|	&ОтборПоОрганизации
	|	И &ОтборПоБанку
	|	И НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И СостоянияБанковскихДокументов.Состояние = &Состояние
	|	И ПлатежноеПоручение.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияБанковскихДокументов.Подготовлено);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(ДатаАктуальности) - ДлинаСуток * 9);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДатаАктуальности));

	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "ПлатежноеПоручение.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "Истина");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Банк) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоБанку", "ПлатежноеПоручение.СчетОрганизации.Банк = (&Банк)");
		Запрос.УстановитьПараметр("Банк", Банк);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоБанку", "Истина");
		
	КонецЕсли;

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		КоличествоПодготовленныхПлатежей = 0;
		КоличествоПодготовленныхПлатежейВинительныйПадеж = "";
		Возврат;
	КонецЕсли;
		
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда 
		КоличествоПодготовленныхПлатежей = Выборка.КоличествоПлатежей;  
		КоличествоПодготовленныхПлатежейВинительныйПадеж = ВинительныйПадеж("платеж", КоличествоПодготовленныхПлатежей);
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура СоздатьПустуюСтрокуДвиженийПоСчету(БанковскийСчет)
	
	СтрокаДвижений = ТаблицаДвиженийПоСчету.Добавить();
	СтрокаДвижений.БанковскийСчет = БанковскийСчет;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаполнениеКалендаряНаКлиенте()
	
	УстановитьВидимостьДанныхПлатежногоКалендаря(Ложь, Ложь);
	УстановитьНадписьПолучениеДанных(0);
	ОжидатьЗаполнениеКалендаря();
		
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(АдресХранилища)
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		
		ПлатежиПоДатам = РезультатВыполнения.Платежи;
		ПлатежиПоДатам.Свернуть("НомерРаздела, Документ, ДатаПлатежа", "Сумма"); 
		ТипизироватьТаблицуПлатежей(ПлатежиПоДатам);
		ЗаполнитьТаблицыПлатежногоКалендаря(ПлатежиПоДатам);
			
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура ТипизироватьТаблицуПлатежей(ПлатежиПоДатам)
	
	ПлатежиПоДатам.Колонки.Добавить("ТипизированныйДокумент", Документы.ТипВсеСсылки());
	МассивДокументов = ПлатежиПоДатам.ВыгрузитьКолонку("Документ");
	ПлатежиПоДатам.ЗагрузитьКолонку(МассивДокументов, "ТипизированныйДокумент");
	ПлатежиПоДатам.Колонки.Удалить("Документ");
	ПлатежиПоДатам.Колонки.ТипизированныйДокумент.Имя = "Документ"; // Переименовываем колонку
	
КонецПроцедуры

&НаСервере
Процедура НачатьЗаполнениеКалендаряНаСервере()
			
	ПараметрыПроцедуры = ПодготовитьПараметрыЗаполненияКалендаря();
		
	Если ДлительнаяОперацияПлатежныйКалендарь.ДлительнаяОперация <> Неопределено Тогда
		// Не завершено предыдущее фоновое задание, новое не запускаем.
		Возврат;
	КонецЕсли;
		
	НаименованиеЗадания   = НСтр("ru = 'Получение данных платежного календаря.'");
	ИмяПроцедуры        = "Обработки.ПлатежныйКалендарь.ЗаполнитьКалендарь";
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ДлительнаяОперацияПлатежныйКалендарь.Имя = "ЗаполнитьКалендарь";
	ДлительнаяОперацияПлатежныйКалендарь.ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		ИмяПроцедуры, ПараметрыПроцедуры, ПараметрыВыполнения);
		
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗаполнениеКалендаря()
	
	Если ДлительнаяОперацияПлатежныйКалендарь.ДлительнаяОперация <> Неопределено Тогда
		
		// В настоящее время еще выполняется фоновое задание по отправке статистики в сервис,
		// дождемся его завершения.
		ТекстПоясненияОжидания = НСтр("ru = 'Получение данных о платежах'");
		
	Иначе
		
		// Фоновое задание не запущено.
		Возврат;
		
	КонецЕсли;
	
	ОповещенияОЗавершении = Новый ОписаниеОповещения("ЗаполнениеПлатежногоКалендаряЗавершение", ЭтотОбъект);
	ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ВывестиПрогрессВыполнения", ЭтотОбъект);

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессеВыполнения;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперацияПлатежныйКалендарь.ДлительнаяОперация,
		ОповещенияОЗавершении,
		ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ВывестиПрогрессВыполнения(Результат, ДополнительныеПараметры = Неопределено) Экспорт

	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Результат.Свойство("Прогресс")
	 Или ТипЗнч(Результат.Прогресс) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Прогресс = Результат.Прогресс;
	Если Не Прогресс.Свойство("Процент") Тогда
		Возврат;
	КонецЕсли;

	Процент = Прогресс.Процент;
	УстановитьНадписьПолучениеДанных(Процент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеПлатежногоКалендаряЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;	
	КонецЕсли;
	
	ДлительнаяОперацияПлатежныйКалендарь.ДлительнаяОперация = Неопределено; // Сбросим признак выполнения.
	
	ОписаниеОшибки = "";
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Статус", "") = "Ошибка" Тогда
		ОписаниеОшибки = Результат.КраткоеПредставлениеОшибки;
		УстановитьТекстОшибкиДлительнойОперации(ОписаниеОшибки);
	Иначе
		ЗагрузитьПодготовленныеДанныеНаСервере(Результат.АдресРезультата);
		ВывестиДанныеПлатежногоКалендаря();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекстОшибкиДлительнойОперации(ОписаниеОшибки) 

	ИмяЭлемента = СтрШаблон("ТекстДлительнойОперации%1", РазрешениеЭкрана);
	Элементы[ИмяЭлемента].Заголовок = СтрШаблон(НСтр("ru = 'Ошибка получения данных: %1'"), ОписаниеОшибки);
	УстановитьВидимостьЭлемента("ДекорацияДлительнаяОперация", Ложь);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьНадписьПолучениеДанных(Процент) 

	ИмяЭлемента = СтрШаблон("ТекстДлительнойОперации%1", РазрешениеЭкрана);
	Элементы[ИмяЭлемента].Заголовок = СтрШаблон(НСтр("ru = 'Получение данных %1 %2...'"), Процент, "%");

КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыЗаполненияКалендаря()
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Организация",      Организация);
	ПараметрыЗадания.Вставить("ДнейПланирования", ГоризонтПланирования());  
	ПараметрыЗадания.Вставить("ОтключитьВыводВТабличныйДокумент", Истина);
	
	Возврат ПараметрыЗадания;
	
КонецФункции

&НаСервере
Функция ГоризонтПланирования()
		
	Возврат 30;
	
КонецФункции

&НаСервере
Процедура ДополнитьТаблицуДатами(ПланируемыеПлатежи)

	КоличествоЗаполненныхДней = ПланируемыеПлатежи.Количество();
	Если КоличествоЗаполненныхДней = 0 Тогда
		ПоследнийЗаполненныйДень = ДатаАктуальности;
	Иначе
		ПоследнийЗаполненныйДень = ПланируемыеПлатежи[КоличествоЗаполненныхДней - 1].ДатаПлатежа;
	КонецЕсли;
	
	МаксимальноеКоличествоДней = МаксимальноеКоличествоЭлементовРаздела("Планирование"); 
	ДобавитьДней = МаксимальноеКоличествоДней - КоличествоЗаполненныхДней;
		
	Для НомерДня = 1 По ДобавитьДней Цикл
		
		СтрокаПлатежиПоДням = ПланируемыеПлатежи.Добавить();
		ДатаПлатежа = ПоследнийЗаполненныйДень + ДлинаСуток * НомерДня;
		СтрокаПлатежиПоДням.ДатаПлатежа = ДатаПлатежа;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыПлатежногоКалендаря(ПлатежиПоДатам)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПлатежиПоДатам.ДатаПлатежа КАК ДатаПлатежа,
		|	ПлатежиПоДатам.Сумма КАК Сумма,
		|	ПлатежиПоДатам.Документ КАК Документ,
		|	ПлатежиПоДатам.НомерРаздела КАК НомерРаздела
		|ПОМЕСТИТЬ ВТ_ПлатежиПоДатам
		|ИЗ
		|	&ПлатежиПоДатам КАК ПлатежиПоДатам
		|ГДЕ
		|	ПлатежиПоДатам.Сумма <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлатежноеПоручение.ДокументОснование КАК ДокументОснование,
		|	СУММА(ПлатежноеПоручение.СуммаДокумента) КАК Сумма
		|ПОМЕСТИТЬ ПодготовленныеПлатежи
		|ИЗ
		|	РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
		|		ПО СостоянияБанковскихДокументов.СсылкаНаОбъект = ПлатежноеПоручение.Ссылка
		|			И СостоянияБанковскихДокументов.Организация = ПлатежноеПоручение.Организация
		|ГДЕ
		|	ПлатежноеПоручение.Дата МЕЖДУ &ДатаПлатежаНачало И &ДатаПлатежаКонец
		|	И ПлатежноеПоручение.ПометкаУдаления = ЛОЖЬ
		|	И &ОтборПоОрганизации
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлатежноеПоручение.ДокументОснование
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОснование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПлатежиПоДатам.Документ КАК Документ,
		|	ВЫБОР
		|		КОГДА ВТ_ПлатежиПоДатам.НомерРаздела В (&НомераРазделовПоступлений)
		|			ТОГДА ВТ_ПлатежиПоДатам.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Приход,
		|	ВЫБОР
		|		КОГДА ВТ_ПлатежиПоДатам.НомерРаздела В (&НомераРазделовСписаний)
		|			ТОГДА ВЫБОР
		|					КОГДА ВТ_ПлатежиПоДатам.Сумма < 0
		|						ТОГДА -ВТ_ПлатежиПоДатам.Сумма
		|					ИНАЧЕ ВТ_ПлатежиПоДатам.Сумма
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Расход
		|ПОМЕСТИТЬ ВТ_ПлатежиКалендаряСегодня
		|ИЗ
		|	ВТ_ПлатежиПоДатам КАК ВТ_ПлатежиПоДатам
		|ГДЕ
		|	ВТ_ПлатежиПоДатам.ДатаПлатежа = &ДатаПлатежа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВТ_ПлатежиКалендаряСегодня.Приход) КАК Приход,
		|	СУММА(ВТ_ПлатежиКалендаряСегодня.Расход) КАК Расход
		|ПОМЕСТИТЬ ВТ_ПлатежиСегодня
		|ИЗ
		|	ВТ_ПлатежиКалендаряСегодня КАК ВТ_ПлатежиКалендаряСегодня
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	СУММА(ПодготовленныеПлатежи.Сумма)
		|ИЗ
		|	ПодготовленныеПлатежи КАК ПодготовленныеПлатежи
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПлатежиКалендаряСегодня КАК ВТ_ПлатежиКалендаряСегодня
		|		ПО (ВТ_ПлатежиКалендаряСегодня.Документ = ПодготовленныеПлатежи.ДокументОснование)
		|ГДЕ
		|	ВТ_ПлатежиКалендаряСегодня.Документ ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПлатежиПоДатам.ДатаПлатежа КАК ДатаПлатежа,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_ПлатежиПоДатам.НомерРаздела В (&НомераРазделовПоступлений)
		|				ТОГДА ВТ_ПлатежиПоДатам.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Приход,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_ПлатежиПоДатам.НомерРаздела В (&НомераРазделовСписаний)
		|				ТОГДА ВЫБОР
		|						КОГДА ВТ_ПлатежиПоДатам.Сумма < 0
		|							ТОГДА -ВТ_ПлатежиПоДатам.Сумма
		|						ИНАЧЕ ВТ_ПлатежиПоДатам.Сумма
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Расход
		|ПОМЕСТИТЬ ВТ_ПланируемыеПлатежи
		|ИЗ
		|	ВТ_ПлатежиПоДатам КАК ВТ_ПлатежиПоДатам
		|ГДЕ
		|	ВТ_ПлатежиПоДатам.ДатаПлатежа <> &ДатаПлатежа
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ПлатежиПоДатам.ДатаПлатежа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 5
		|	ВТ_ПланируемыеПлатежи.ДатаПлатежа КАК ДатаПлатежа,
		|	ВТ_ПланируемыеПлатежи.Приход КАК Приход,
		|	ВТ_ПланируемыеПлатежи.Расход КАК Расход
		|ИЗ
		|	ВТ_ПланируемыеПлатежи КАК ВТ_ПланируемыеПлатежи
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПлатежа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ВТ_ПлатежиСегодня.Приход), 0) КАК Приход,
		|	ЕСТЬNULL(СУММА(ВТ_ПлатежиСегодня.Расход), 0) КАК Расход
		|ИЗ
		|	ВТ_ПлатежиСегодня КАК ВТ_ПлатежиСегодня
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ВТ_ПлатежиКалендаряСегодня.Приход), 0) КАК Приход,
		|	ЕСТЬNULL(СУММА(ВТ_ПлатежиКалендаряСегодня.Расход), 0) КАК Расход
		|ИЗ
		|	ВТ_ПлатежиКалендаряСегодня КАК ВТ_ПлатежиКалендаряСегодня";
	
	Запрос.УстановитьПараметр("ДатаПлатежа", НачалоДня(ДатаАктуальности)); 
	Запрос.УстановитьПараметр("ДатаПлатежаНачало", НачалоДня(ДатаАктуальности) - ДлинаСуток * 9);
	Запрос.УстановитьПараметр("ДатаПлатежаКонец", КонецДня(ДатаАктуальности));
	Запрос.УстановитьПараметр("ПлатежиПоДатам", ПлатежиПоДатам);
	Запрос.УстановитьПараметр("Состояние", Перечисления.СостоянияБанковскихДокументов.Подготовлено);
	Запрос.УстановитьПараметр("НомераРазделовПоступлений", НомераРазделовПоступленийПлатежногоКалендаря());
	Запрос.УстановитьПараметр("НомераРазделовСписаний", НомераРазделовСписанийПлатежногоКалендаря());
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "ПлатежноеПоручение.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "Истина");
	
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	ПланируемыеПлатежиСегодняПриход = 0;
	ПланируемыеПлатежиСегодняРасход = 0;
	
	ПланируемыеПлатежиСегодня = РезультатЗапроса[6].Выбрать();
	Если ПланируемыеПлатежиСегодня.Следующий() Тогда
		// В планируемые платежи на сегодня
		// добавляются данные платежных поручений, которых нет в платежном календаре
		ПланируемыеПлатежиСегодняПриход = ПланируемыеПлатежиСегодня.Приход;
		ПланируемыеПлатежиСегодняРасход = ПланируемыеПлатежиСегодня.Расход;
	КонецЕсли; 
	
	ПланируемыеПлатежиКалендаряСегодняПриход = 0;
	ПланируемыеПлатежиКалендаряСегодняРасход = 0;
	
	ПланируемыеПлатежиКалендаряСегодня = РезультатЗапроса[7].Выбрать();
	
	Если ПланируемыеПлатежиКалендаряСегодня.Следующий() Тогда	
		ПланируемыеПлатежиКалендаряСегодняПриход = ПланируемыеПлатежиКалендаряСегодня.Приход;
		ПланируемыеПлатежиКалендаряСегодняРасход = ПланируемыеПлатежиКалендаряСегодня.Расход;
	КонецЕсли; 

	ПланируемыеПлатежи = РезультатЗапроса[5].Выгрузить();
	ДополнитьТаблицуДатами(ПланируемыеПлатежи);
	ТаблицаПланируемыеПлатежи.Загрузить(ПланируемыеПлатежи);

КонецПроцедуры

&НаСервере
Функция НомераРазделовПоступленийПлатежногоКалендаря()
	
	МассивНомеров = Новый Массив;
	МассивНомеров.Добавить(1);
	МассивНомеров.Добавить(2);

	Возврат МассивНомеров;
	
КонецФункции

&НаСервере
Функция НомераРазделовСписанийПлатежногоКалендаря()
	
	МассивНомеров = Новый Массив;
	МассивНомеров.Добавить(3);
	МассивНомеров.Добавить(4);
	МассивНомеров.Добавить(5);
	МассивНомеров.Добавить(6);

	Возврат МассивНомеров;
	
КонецФункции

#КонецОбласти

#Область РазделСчета

&НаКлиенте
Процедура ВывестиДанныеПоСчетам()
	
	ТекущаяПозицияСчета = 1;
		
	Если ТаблицаБанковскихСчетов.Количество() > 0 Тогда
		ВывестиСтрокиТаблицыПоСчетам();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСтрокиТаблицыПоСчетам()
	
	// Банковские счета, отображенные на форме 
	СоответствиеЭлементаФормыЗначению = Новый Соответствие;
	
	ВыводитьДвиженияПоСчету = ТаблицаДвиженийПоСчету.Количество() <> 0;
	
	УстановитьВидимостьПодчиненныхЭлементов("ГруппаДанныеСчета", Ложь, Ложь);
	УстановитьВидимостьЭлемента("ЗаголовокСчета", Истина);
	УстановитьВидимостьЭлемента("ОтступСчета", Истина);
	
	// При обратном скролле необходимо учесть перенос группы
	// с предыдущей страницы, если таковой был 
	Если ТекущаяПозицияСчета > 1 И Не ЗначениеЗаполнено(ТаблицаБанковскихСчетов[ТекущаяПозицияСчета - 2].Организация) Тогда
		ТекущаяПозицияСчета = ТекущаяПозицияСчета - 1;
	КонецЕсли;
	
	КоличествоСтрокРаздела = КоличествоСтрокРаздела("Счета");
	
	КоличествоСтрокТаблицы = ТаблицаБанковскихСчетов.Количество();
	СмещениеПозиции = КоличествоСтрокРаздела - 1;
	ПоследняяПозицияВИтерации = Мин(ТекущаяПозицияСчета + СмещениеПозиции, КоличествоСтрокТаблицы);
	
	Для Итератор = ТекущаяПозицияСчета По ПоследняяПозицияВИтерации Цикл
		
		НомерСтроки = ФинУслугиКлиент.НомерСтрокиПоПозиции(Итератор, ТекущаяПозицияСчета);
		ИндексТаблицы = Итератор - 1;

		// Элементы итоговой группы 
		ВидимостьГруппыИтогов = Не ЗначениеЗаполнено(ТаблицаБанковскихСчетов[ИндексТаблицы].Организация)
			И Не ЗначениеЗаполнено(ТаблицаБанковскихСчетов[ИндексТаблицы].НаименованиеБанковскогоСчета); 
		Если ВидимостьГруппыИтогов Тогда
			
			// Не выводим группу последней на странице
			Если НомерСтроки = КоличествоСтрокРаздела Тогда 
				ПоследняяПозицияВИтерации = ПоследняяПозицияВИтерации - 1;
				Прервать;
			КонецЕсли;
			УстановитьВидимостьЭлемента("ГруппаИтогПоСчетам", Истина, , НомерСтроки);
			УстановитьВидимостьЭлемента("ОтступСчет", Истина, , НомерСтроки);
			ВывестиИтоговуюСтрокуТаблицыСчетов(НомерСтроки, ТаблицаБанковскихСчетов[ИндексТаблицы]);
			
		Иначе	
			
			// Устанавливается видимость элементов строки по счету
			УстановитьВидимостьЭлемента("ГруппаСчет", Истина, , НомерСтроки);
			УстановитьВидимостьЭлемента("ПодробноПоСчету", Истина, , НомерСтроки);
			УстановитьВидимостьЭлемента("ОтступРазделитель", Истина, , НомерСтроки);
			ВывестиСтрокуТаблицыСчетов(НомерСтроки, ТаблицаБанковскихСчетов[ИндексТаблицы]);
			СоответствиеЭлементаФормыЗначению.Вставить(НомерСтроки, ТаблицаБанковскихСчетов[ИндексТаблицы]);
			
			Если ВыводитьДвиженияПоСчету Тогда
				
				УстановитьВидимостьЭлемента("ГруппаПоступлениеСчет", Истина, , НомерСтроки);
				УстановитьВидимостьЭлемента("ОтступДвижения", Истина, , НомерСтроки);
				УстановитьВидимостьЭлемента("ГруппаСписаниеСчет", Истина, , НомерСтроки);
				УстановитьВидимостьЭлемента("ОтступПослеДвижений", Истина, , НомерСтроки);
				ВывестиСтрокуДвиженийТаблицыСчетов(НомерСтроки, ТаблицаДвиженийПоСчету[0]);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НомерСтроки > 1 Тогда
			
			// Элементы разделителя выводятся только если предыдущей была выведена группа
			НомерПредыдущейСтроки = НомерСтроки - 1;
			УстановитьВидимостьЭлемента("ГруппаРазделитель", ВидимостьГруппыИтогов, , НомерПредыдущейСтроки);
			УстановитьВидимостьЭлемента("ОтступПослеРазделителя", ВидимостьГруппыИтогов, , НомерПредыдущейСтроки); 
						
		КонецЕсли;
		
				
	КонецЦикла; 
	
	ТекущаяПозицияСчета = ПоследняяПозицияВИтерации;

	УстановитьВидимостьСкролла(ТекущаяПозицияСчета, КоличествоСтрокТаблицы, КоличествоСтрокРаздела, "Счета");
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСтрокуТаблицыСчетов(НомерСтроки, Строка)
	
	Наименование = СтрШаблон("НаименованиеСчета%1%2",  НомерСтроки, РазрешениеЭкрана);
	Элементы[Наименование].Заголовок = Строка.НаименованиеСчета;
	КлючРаздела = СтрШаблон("ПоСчету%1", НомерСтроки);
	ВывестиЧисловойПоказательВРаздел(КлючРаздела, Строка.Сумма); 
	Наименование = СтрШаблон("ПодробноПоСчету%1%2", НомерСтроки, РазрешениеЭкрана);
	Элементы[Наименование].Заголовок = ТекстРасчетныйСчетПодробно(Строка);
	
КонецПроцедуры 

&НаКлиенте
Процедура ВывестиИтоговуюСтрокуТаблицыСчетов(НомерСтроки, Строка)
	
	Наименование = СтрШаблон("ЗаголовокПоСчетам%1%2", НомерСтроки, РазрешениеЭкрана);
	Текст = ТекстЗаголовкаПоВалюте(Строка.Валюта, Строка.ПредставлениеВалюты);
	Элементы[Наименование].Заголовок = Текст;
	
	Наименование = СтрШаблон("КартинкаВалюта%1%2", НомерСтроки, РазрешениеЭкрана);
	Элементы[Наименование].Картинка = КартинкаВалюты(Строка.Валюта);

	КлючРаздела = СтрШаблон("ПоСчетам%1", НомерСтроки);
	ВывестиЧисловойПоказательВРаздел(КлючРаздела, Строка.Сумма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСтрокуДвиженийТаблицыСчетов(НомерСтроки, Строка)
	
	КлючРаздела = СтрШаблон("ПоступлениеПоСчету%1", НомерСтроки);
	Наименование = СтрШаблон("%1%2",  КлючРаздела, РазрешениеЭкрана);
	Элементы[Наименование].Заголовок = СтрШаблон("за %1:", Нрег(Формат(ДатаАктуальности, "ДФ='MMMM'")));
	Знак =  "+ ";
	ВывестиЧисловойПоказательВРаздел(КлючРаздела, Строка.Приход, Знак);
	
	Знак =  "- ";
	КлючРаздела = СтрШаблон("СписаниеПоСчету%1", НомерСтроки);
	ВывестиЧисловойПоказательВРаздел(КлючРаздела, Строка.Расход, Знак);

КонецПроцедуры 

&НаКлиенте
Функция ТекстРасчетныйСчетПодробно(СтрокаТаблицы)
	
	Если Не ЗначениеЗаполнено(СтрокаТаблицы.БанковскийСчет) Тогда
		Возврат ?(ЗначениеЗаполнено(Организация), "", СтрокаТаблицы.Организация);
	КонецЕсли;
	
	СтрокаОрганизация = СтрШаблон("%1%2", ", ", СтрокаТаблицы.Организация);
	ОрганизацияСчета = ?(ЗначениеЗаполнено(Организация), "", СтрокаОрганизация);
	СтрокаНаименованиеБанк = СтрШаблон("%1%2", ", ", СтрокаТаблицы.НаименованиеБанка);
	НаименованиеБанка = ?(ЗначениеЗаполнено(Банк) Или СтрокаТаблицы.ЦифровойСчет, "", СтрокаНаименованиеБанк);
	
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Банк) Тогда
		НомерСчета = СтрокаТаблицы.НомерСчета;  
	Иначе
		НомерСчета = СтрШаблон("*%1", Прав(СокрЛП(СтрокаТаблицы.НомерСчета), 4));
	КонецЕсли;
	
	Возврат СтрШаблон("%1%2%3", НомерСчета, НаименованиеБанка, ОрганизацияСчета);

КонецФункции

&НаСервере
Функция СоответствиеИменСчетов()
	
	СоответствиеИмен = Новый Соответствие;
	
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.ПустаяСсылка(), НСтр("ru='Расчетный счет'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.РасчетныеСчета, НСтр("ru='Расчетный счет'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.ВалютныеСчета, НСтр("ru='Валютный счет'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.ЦифровойРубль, НСтр("ru='Цифровой рубль'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.Аккредитивы, НСтр("ru='Счет аккредитива'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.ЧековыеКнижки, НСтр("ru='Чековая книжка'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.ДепозитныеСчета, НСтр("ru='Депозитный счет'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.ПрочиеСпециальныеСчета,НСтр("ru='Специальный счет'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.АккредитивыВал, НСтр("ru='Счет аккредитива'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.ДепозитныеСчетаВал, НСтр("ru='Депозитный счет'"));
	СоответствиеИмен.Вставить(ПланыСчетов.Хозрасчетный.ПрочиеСпециальныеСчетаВал, НСтр("ru='Специальный счет'"));
	
	Возврат СоответствиеИмен;
	
КонецФункции

&НаКлиенте
Функция ТекстЗаголовкаПоВалюте(Валюта, ПредставлениеВалюты)
	
	Если Валюта = ВалютаРубль Тогда
		Возврат НСтр("ru='На рублевых счетах'");
	Иначе
		Возврат СтрШаблон(НСтр("ru='На счетах, %1'"), ПредставлениеВалюты);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция КартинкаВалюты(Валюта)
	
	СоответствиеКартинок = СоответствиеКартинокВалютам();
	КартинкаВалюты = СоответствиеКартинок.Получить(Валюта); 
	Если КартинкаВалюты = Неопределено Тогда
		Возврат БиблиотекаКартинок.ПустаяВалютаФинансы;
	Иначе
		Возврат КартинкаВалюты;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция СоответствиеКартинокВалютам()
	
	СоответствиеКартинок = Новый Соответствие;
	
	СоответствиеКартинок.Вставить(ВалютаРубль, БиблиотекаКартинок.РубльФинансы);
	СоответствиеКартинок.Вставить(ВалютаДоллар, БиблиотекаКартинок.ДолларФинансы);
	СоответствиеКартинок.Вставить(ВалютаЕвро, БиблиотекаКартинок.ЕвроФинансы);
	СоответствиеКартинок.Вставить(ВалютаЮань, БиблиотекаКартинок.ЮаньФинансы);

	Возврат СоответствиеКартинок;
	
КонецФункции

#КонецОбласти

#Область РазделыПоВзаиморасчетам

&НаКлиенте
Процедура ВывестиДанныеКредиторскойЗадолженности()

	УстановитьВидимостьПодчиненныхЭлементов("ГруппаГоризонтальнаяДанныеКЗ", Ложь, Истина);

	Если ВариантКредиторскойЗадолженности = "Кредиты" И ТаблицаКредиты.Количество() = 0 
		Или ВариантКредиторскойЗадолженности = "Лизинг" И ТаблицаЛизинг.Количество() = 0 Тогда
		
		Наименование = "Группа";
		ВывестиНадписьКредиторскойЗадолженностиПоВарианту();
		
	Иначе
		
		Наименование = "ГруппаДанныеКЗ";
		ВывестиРазделДанныхКредиторскойЗадолженностиПоВарианту();
		
	КонецЕсли;
	
	УстановитьВидимостьЭлемента(Наименование, Истина, ВариантКредиторскойЗадолженности);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиНадписьКредиторскойЗадолженностиПоВарианту()
	
	Наименование = СтрШаблон("Надпись%1%2", ВариантКредиторскойЗадолженности, РазрешениеЭкрана);
	Элементы[Наименование].Заголовок = ТекстБаннераСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиРазделДанныхКредиторскойЗадолженностиПоВарианту()
	
	Если ВариантКредиторскойЗадолженности = "Поставщикам" Тогда
		
		ВывестиЧисловойПоказательВРаздел("КЗПоставщикамВсего");
		ВывестиЧисловойПоказательВРаздел("КЗПоставщикамТекущая");
		ВывестиЧисловойПоказательВРаздел("КЗПоставщикамПросроченная"); 
		
		Возврат;
		
	КонецЕсли;
	
	ТекущаяПозицияКредитыИЛизинг = 1;
	
	ВывестиСтрокиТаблицыПоВарианту();
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСтрокиТаблицыПоВарианту()
	
	УстановитьВидимостьПодчиненныхЭлементов("ГруппаДанныеКЗ", Ложь, Ложь, ВариантКредиторскойЗадолженности);

	Если ВариантКредиторскойЗадолженности = "Кредиты" Тогда
		ТаблицаРаздела = ТаблицаКредиты; 
	Иначе
		ТаблицаРаздела = ТаблицаЛизинг;
	КонецЕсли;
	
	КоличествоСтрокРаздела = КоличествоСтрокРаздела("КредиторскаяЗадолженность");
	КоличествоСтрокТаблицы = ТаблицаРаздела.Количество();
	СмещениеПозиции = КоличествоСтрокРаздела - 1;
	ПоследняяПозицияВИтерации = Мин(ТекущаяПозицияКредитыИЛизинг + СмещениеПозиции, КоличествоСтрокТаблицы);
		
	Для Итератор = ТекущаяПозицияКредитыИЛизинг По ПоследняяПозицияВИтерации Цикл
		
		НомерСтроки = ФинУслугиКлиент.НомерСтрокиПоПозиции(Итератор, ТекущаяПозицияКредитыИЛизинг);
		УстановитьВидимостьПодчиненныхЭлементов("ГруппаДанныеКЗ", Истина, Ложь, ВариантКредиторскойЗадолженности, НомерСтроки);
		ВывестиСтрокуТаблицыПоВарианту(НомерСтроки, ТаблицаРаздела[Итератор - 1]);
		
	КонецЦикла;
	
	ТекущаяПозицияКредитыИЛизинг = ПоследняяПозицияВИтерации;
	
	УстановитьВидимостьСкролла(ТекущаяПозицияКредитыИЛизинг, КоличествоСтрокТаблицы, КоличествоСтрокРаздела, "КЗ");

КонецПроцедуры

&НаКлиенте
Процедура ВывестиСтрокуТаблицыПоВарианту(НомерСтроки, Строка)

	Наименование = СтрШаблон("НадписьСтрокаКЗ%1%2%3", ВариантКредиторскойЗадолженности, НомерСтроки, РазрешениеЭкрана);
	Элементы[Наименование].Заголовок = Строка.ПредставлениеКонтрагента;
	КлючРаздела = СтрШаблон("КЗ%1%2", ВариантКредиторскойЗадолженности, НомерСтроки);
	ВывестиЧисловойПоказательВРаздел(КлючРаздела, Строка.Сумма);
	Наименование = СтрШаблон("ПодробноКЗ%1%2%3", ВариантКредиторскойЗадолженности, НомерСтроки, РазрешениеЭкрана);
	Элементы[Наименование].Заголовок = Строка.Организация;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиДанныеДебиторскойЗадолженности()

	ВывестиЧисловойПоказательВРаздел("ДЗВсего");
	ВывестиЧисловойПоказательВРаздел("ДЗТекущая"); 
	ВывестиЧисловойПоказательВРаздел("ДЗПросроченная");

КонецПроцедуры

&НаКлиенте
Функция ТекстБаннераСервиса()
	
	Если ВариантКредиторскойЗадолженности = "Кредиты" Тогда
		Возврат НСтр("ru = 'Нужен кредит? Оставьте заявку'");
	Иначе
		Возврат НСтр("ru = '1С:Лизинг - выгодные условия с экспертом по лизингу'");
	КонецЕсли;
		
КонецФункции

#КонецОбласти

#Область РазделПлатежныйКалендарь

&НаКлиенте
Процедура ВывестиДанныеПлатежногоКалендаря()
		
	ОпцияПлатежи = ИспользоватьПланированиеПлатежей();
	
	ОтстутствуютДанныеПлатежногоКалендаря = ОтстутствуютДанныеПлатежногоКалендаря() 
		И (Не ОпцияПлатежи.ОтПокупателей Или Не ОпцияПлатежи.Поставщикам);
		
	УстановитьВидимостьДанныхПлатежногоКалендаря(Истина, ОтстутствуютДанныеПлатежногоКалендаря);
	ВывестиДанныеПлатежиСегодня();
	Если Не ОтстутствуютДанныеПлатежногоКалендаря Тогда
		ВывестиДанныеПланирование(); 
	Иначе
		ЗаполнитьДекорациюПланирование(ОпцияПлатежи.ОтПокупателей, ОпцияПлатежи.Поставщикам);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДекорациюПланирование(ОпцияПлатежиОтПокупателей, ОпцияПлатежиПоставщикам)
		
	ЗаголовокПланирование = НСтр("ru = 'Платежный календарь пуст. Начните планировать:'");
	Наименование = СтрШаблон("ЗаголовокПланированиеПустое%1", РазрешениеЭкрана);
	Элементы[Наименование].Заголовок = ЗаголовокПланирование;
	УстановитьВидимостьЭлемента("ТекстОпцияПоставщики", Не ОпцияПлатежиПоставщикам); 
	УстановитьВидимостьЭлемента("ТекстОпцияПокупатели", Не ОпцияПлатежиОтПокупателей); 

КонецПроцедуры

&НаКлиенте
Процедура ВывестиДанныеПлатежиСегодня()
	
	ВывестиГиперссылкуСогласование();
		
	СуммаПолучено = ПлатежиСегодняПриход;
	СуммаОсталосьПолучить = ПланируемыеПлатежиСегодняПриход;
	СуммаКПоступлению = СуммаПолучено + СуммаОсталосьПолучить;
	СуммаИсполнено = ПлатежиСегодняРасход;
	СуммаОсталосьОплатить = ПланируемыеПлатежиСегодняРасход;
	СуммаКОплате = СуммаИсполнено + СуммаОсталосьОплатить;
	
	ЕстьПлатежиСегодня = (СуммаКПоступлению <> 0 Или СуммаКОплате <> 0); 
	
	УстановитьВидимостьЭлемента("ГруппаПлатежиЛевая", ЕстьПлатежиСегодня);
	УстановитьВидимостьЭлемента("ГруппаПлатежиПравая", ЕстьПлатежиСегодня);
	УстановитьВидимостьЭлемента("ГруппаПлатежиПустая", Не ЕстьПлатежиСегодня);
	
	Если ЕстьПлатежиСегодня Тогда
		
		СуммыПоБанку = ПлатежиСегодняПоБанку();
		ВывестиЧисловойПоказательВРаздел("Получено", СуммаПолучено); 
		ВывестиДополнениеПоБанку("ПолученоБанк");
		ВывестиЧисловойПоказательВРаздел("ПолученоБанк", СуммыПоБанку.Приход);
		ВывестиЧисловойПоказательВРаздел("ОсталосьПолучить", СуммаОсталосьПолучить);
		ВывестиЧисловойПоказательВРаздел("КПоступлению", СуммаКПоступлению);
		ВывестиЧисловойПоказательВРаздел("Исполнено", СуммаИсполнено);
		ВывестиДополнениеПоБанку("ИсполненоБанк");
		ВывестиЧисловойПоказательВРаздел("ИсполненоБанк", СуммыПоБанку.Расход);
		ВывестиЧисловойПоказательВРаздел("ОсталосьОплатить", СуммаОсталосьОплатить);
		ВывестиЧисловойПоказательВРаздел("КОплате", СуммаКОплате);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПлатежиСегодняПоБанку()
	
	СуммыПоБанку = Новый Структура;
	СуммыПоБанку.Вставить("Приход", 0);
	СуммыПоБанку.Вставить("Расход", 0);
	
	Если ЗначениеЗаполнено(Банк) Тогда
		
		Отбор = Новый Структура("Банк", Банк);
		СтрокиБанка = ТаблицаПлатежиСегодняПоБанкам.НайтиСтроки(Отбор);
		Если СтрокиБанка.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(СуммыПоБанку, СтрокиБанка[0]);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СуммыПоБанку;
	
КонецФункции

&НаКлиенте
Процедура ВывестиДополнениеПоБанку(Раздел)
	
	ВыбранБанк = ЗначениеЗаполнено(Банк);
	Наименование = СтрШаблон("Группа%1%2", Раздел, РазрешениеЭкрана);
	Элементы[Наименование].Видимость = ВыбранБанк;
	Если ВыбранБанк Тогда
		Наименование = СтрШаблон("Заголовок%1%2", Раздел, РазрешениеЭкрана);
		ЗаголовокБанк = СтрШаблон(НСтр("ru = 'из них в %1'"), КраткоеНаименованиеБанка);
		Элементы[Наименование].Заголовок = ЗаголовокБанк;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиГиперссылкуСогласование()
	
	Наименование = СтрШаблон("СсылкаСогласовано%1", РазрешениеЭкрана); 
	Если ИспользуетсяСогласованиеПлатежей Тогда
		ЗаголовокСогласование = НСтр("ru = 'Согласование платежей'");
	Иначе
		РезультатСклонения = КоличествоПодготовленныхПлатежейВинительныйПадеж;
		Окончание = ?(КоличествоПодготовленныхПлатежей = 1, "", "о");
		ЗаголовокСогласование = СтрШаблон(НСтр("ru = 'Подготовлен%1 %2'"),Окончание, РезультатСклонения);
	КонецЕсли;
	Элементы[Наименование].Заголовок = ЗаголовокСогласование;

КонецПроцедуры

&НаСервере
Функция ВинительныйПадеж(Слово, Числительное)
	
	Результат = ПолучитьСклоненияСтрокиПоЧислу(Слово, Числительное, , , "ПД=Винительный");
	Возврат Результат[0];
	
КонецФункции

&НаКлиенте
Процедура ВывестиДанныеПланирование()
	
	КоличествоДней = Мин(КоличествоЭлементовКалендаря(), ТаблицаПланируемыеПлатежи.Количество()); 
	ТекущийОстатокВРублях = ИтогВРублях + ПланируемыеПлатежиКалендаряСегодняПриход - ПланируемыеПлатежиКалендаряСегодняРасход; 
	ПредыдущаяДата = ПоследнийЗаполненныйДень;

	Для НомерСтроки = 1 По КоличествоДней Цикл
		
		ВывестиДеньКалендаря(НомерСтроки, ТекущийОстатокВРублях);
		УстановитьВидимостьЭлемента("ГруппаДень", Истина, , НомерСтроки);

		Если НомерСтроки > 1 Тогда
			УстановитьРазделительКалендаря(НомерСтроки);
		КонецЕсли;
				
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ОтстутствуютДанныеПлатежногоКалендаря()
	
	ОтсутствуютСписания = ТаблицаПланируемыеПлатежи.Итог("Расход") = 0;
	ОтсутствуютПоступления = ТаблицаПланируемыеПлатежи.Итог("Приход") = 0; 
	
	Возврат ОтсутствуютСписания И ОтсутствуютПоступления;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИспользоватьПланированиеПлатежей()
	
	Результат = Новый Структура;
	Результат.Вставить("ОтПокупателей", ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеПлатежейОтПокупателей"));
	Результат.Вставить("Поставщикам", ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеПлатежейПоставщикам"));

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВывестиДеньКалендаря(НомерСтроки, ТекущийОстатокВРублях)
	
	СтрокаПлатежей = ТаблицаПланируемыеПлатежи[НомерСтроки - 1];

	Наименование = СтрШаблон("ЗаголовокДень%1%2", НомерСтроки, РазрешениеЭкрана); 
	ТекстДата = Формат(СтрокаПлатежей.ДатаПлатежа, "ДФ=dd.MM");	  
	ТекстДеньНедели = Формат(СтрокаПлатежей.ДатаПлатежа, "ДФ=дддд");
	Элементы[Наименование].Заголовок = СтрШаблон("%1, %2", ТекстДата, ТекстДеньНедели);

	Знак = ?(СтрокаПлатежей.Приход = 0, "", "+ ");
	КлючРаздела = СтрШаблон("Поступление%1", НомерСтроки);
	ВывестиЧисловойПоказательВРаздел(КлючРаздела, СтрокаПлатежей.Приход, Знак);
	Знак = ?(СтрокаПлатежей.Расход = 0, "", "- ");
	КлючРаздела = СтрШаблон("Списание%1", НомерСтроки);
	ВывестиЧисловойПоказательВРаздел(КлючРаздела, СтрокаПлатежей.Расход, Знак);
	
	ТекущийОстатокВРублях = ТекущийОстатокВРублях + СтрокаПлатежей.Приход - СтрокаПлатежей.Расход;  
	
	КлючРаздела = СтрШаблон("Остаток%1", НомерСтроки);
	ВывестиЧисловойПоказательВРаздел(КлючРаздела, ТекущийОстатокВРублях);
	
КонецПроцедуры

&НаКлиенте
Функция КоличествоЭлементовКалендаря()
	
	Если РазрешениеЭкрана = "ВысокоеРазрешение" Тогда
		Возврат 5;
	Иначе
		// В низком разрешении размер раздела на 1 меньше
		Возврат 4;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьДанныхПлатежногоКалендаря(Видимость, ОтсутствуютДанные)
	
	УстановитьВидимостьЭлемента("ГруппаВертикальнаяПлатежи", Видимость);
	УстановитьВидимостьЭлемента("ГруппаВертикальнаяЗаголовокПланирование", Видимость);
	УстановитьВидимостьЭлемента("ГруппаДанныеПланирование", Видимость И Не ОтсутствуютДанные);
	УстановитьВидимостьЭлемента("ГруппаПланированиеПустая", Видимость И ОтсутствуютДанные);
	УстановитьВидимостьЭлемента("ГруппаОжиданиеПлатежи", Не Видимость);
	УстановитьВидимостьПодчиненныхЭлементов("ГруппаОжиданиеПлатежи", Не Видимость, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРазделительКалендаря(НомерЭлемента)
	
	Индекс = НомерЭлемента - 1;
	ДеньКалендаря = ТаблицаПланируемыеПлатежи[Индекс];
	ПредыдущийДеньКалендаря = ТаблицаПланируемыеПлатежи[Индекс - 1];
	РазницаДат = ДеньКалендаря.ДатаПлатежа - ПредыдущийДеньКалендаря.ДатаПлатежа;
	ВидимостьПропускаДней = РазницаДат > ДлинаСуток;
	УстановитьВидимостьЭлемента("ДекорацияТриТочки", ВидимостьПропускаДней, , Индекс);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокПлатежныхПоручений()
	
	НаименованиеФормы = "Документ.ПлатежноеПоручение.Форма.ФормаСписка";
	ОтборСписка = Новый Структура;
	ОтборСписка.Вставить("Состояние", ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Подготовлено"));
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОтборСписка.Вставить("Организация", Организация);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Банк) Тогда
		ОтборСписка.Вставить("БанкСчетаОрганизации", Банк);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ОтборСписка);
	ОткрытьФормуСОтбором(НаименованиеФормы, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСогласованиеПлатежей()
	
	ПараметрыФормы = Новый Структура();
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФормы.Вставить("Организация", Организация);  
	КонецЕсли;
	ОткрытьФорму("Обработка.СогласованиеПлатежей.Форма.Форма", ПараметрыФормы); 

КонецПроцедуры

#КонецОбласти

#Область Отчеты

&НаКлиенте
Функция ПользовательскиеНастройкиОтчета(МассивОтборов, МассивГруппировок, МассивСвойств)
		
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
	Для Каждого ПользовательскийОтбор Из МассивОтборов Цикл  
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, ПользовательскийОтбор.ИмяОтбора,
			ПользовательскийОтбор.Отбор, ПользовательскийОтбор.ВидСравнения);
	КонецЦикла;
		
	ТабличнаяЧастьГруппировка = Новый Массив; 
	Для Каждого ПользовательскаяГруппировка Из МассивГруппировок Цикл
		ДобавитьГруппировку(ТабличнаяЧастьГруппировка, ПользовательскиеНастройки, 
			ПользовательскаяГруппировка.Имя, ПользовательскаяГруппировка.Представление);
	КонецЦикла;
	
	Для Каждого ПользовательскоеСвойство Из МассивСвойств Цикл
		 ПользовательскиеНастройки.ДополнительныеСвойства.Вставить(ПользовательскоеСвойство.ИмяСвойства, ПользовательскоеСвойство.ЗначениеСвойства);
	КонецЦикла;
	
	Возврат ПользовательскиеНастройки;
	
КонецФункции

&НаКлиенте
Функция СписокВидовСубконтоДляАнализаСубконто()
	
	СписокВидовСубконто = Новый СписокЗначений;  
	СписокВидовСубконто.Добавить(ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты"));
	СписокВидовСубконто.Добавить(ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры"));
	
	Возврат СписокВидовСубконто;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьВМассивОтборов(Массив, ИмяОтбора, Отбор, ПользовательскийВидСравнения)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяОтбора", ИмяОтбора);
	СтруктураОтбора.Вставить("Отбор", Отбор);
	СтруктураОтбора.Вставить("ВидСравнения", ПользовательскийВидСравнения);
	
	Массив.Добавить(СтруктураОтбора);
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВМассивГруппировок(Массив, ИмяГруппировки, ПредставлениеГруппировки)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Имя", ИмяГруппировки);
	СтруктураОтбора.Вставить("Представление", ПредставлениеГруппировки);
		
	Массив.Добавить(СтруктураОтбора);
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВМассивСвойств(Массив, ИмяСвойства, ЗначениеСвойства)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяСвойства", ИмяСвойства);
	СтруктураОтбора.Вставить("ЗначениеСвойства", ЗначениеСвойства);
		
	Массив.Добавить(СтруктураОтбора);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСОтбором(НаименованиеФормы, ПараметрыФормы)
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.ИмяФормы = НаименованиеФормы;
	ПараметрыОткрытия.Уникальность = Новый УникальныйИдентификатор;
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтчета(ИмяОтчета, ПараметрыОтчета)

	ФормаОтчета = ПолучитьФорму(СтрШаблон("Отчет.%1.ФормаОбъекта", ИмяОтчета), 
		ПараметрыОтчета, ЭтотОбъект, Новый УникальныйИдентификатор);
		
	ФормаОтчета.Отчет.Организация = Организация; 
	
	Если ПараметрыОтчета.Свойство("ПоказательЗадолженность") Тогда
		ФормаОтчета.Отчет.ПоказательЗадолженность = ПараметрыОтчета.ПоказательЗадолженность;
	КонецЕсли;
	
	Если ПараметрыОтчета.Свойство("ПоказательПросроченнаяЗадолженность") Тогда
		ФормаОтчета.Отчет.ПоказательПросроченнаяЗадолженность = ПараметрыОтчета.ПоказательПросроченнаяЗадолженность;
	КонецЕсли;
	
	Если СтрНайти(ИмяОтчета,"ПоСрокамДолга") > 0 Тогда
		ФормаОтчета.Отчет.Период = ДатаАктуальности;
	ИначеЕсли СтрНайти(ИмяОтчета,"АнализНеоплаченныхСчетов") = 0 Тогда
		ФормаОтчета.Отчет.НачалоПериода = НачалоМесяца(ДатаАктуальности);
		ФормаОтчета.Отчет.КонецПериода = КонецДня(ДатаАктуальности);
	КонецЕсли;
	
	// Для всех открываемых отчетов кроме Анализа субконто
	Если Не ПараметрыОтчета.Свойство("РежимРасшифровки") Тогда 
		ФормаОтчета.ПодключитьОбработчикОжидания("Подключаемый_СформироватьПриОткрытии",
		БухгалтерскиеОтчетыКлиент.ИнтервалЗапускаФормированияОтчетаПриОткрытии(),
		Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ФормаОтчета.Элементы.Результат,
		"ФормированиеОтчета"); 
	КонецЕсли;
	
	ФормаОтчета.Открыть();

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГруппировку(ТабличнаяЧастьГруппировка, ПользовательскиеНастройки, Поле, Представление)
	
	ОписаниеГруппировки = Новый Структура;
	ОписаниеГруппировки.Вставить("Использование", Истина);
	ОписаниеГруппировки.Вставить("Поле", Поле);
	ОписаниеГруппировки.Вставить("Представление", Представление);
	ОписаниеГруппировки.Вставить("ТипГруппировки", 0);
	ТабличнаяЧастьГруппировка.Добавить(ОписаниеГруппировки);
	
	ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("Группировка", ТабличнаяЧастьГруппировка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбщиеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьФорму(ПолучитьДанныеПлатежногоКалендаря = Истина)
	
	ПодготовитьДанныеДляЗаполнения(ПолучитьДанныеПлатежногоКалендаря);
	ТаблицаПредложенийБанков.Очистить();
	ФинУслугиКлиент.ОбновитьДоступныеПредложения(ЭтотОбъект);
	ОбновитьБаннеры();
	ВывестиДанныеРазделов(ПолучитьДанныеПлатежногоКалендаря);

КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеДляЗаполнения(ПолучитьДанныеПлатежногоКалендаря)
	
	ДатаАктуальности = ДатаАктуальности();
	ЗаполнитьДанныеФинансыОрганизации(ПолучитьДанныеПлатежногоКалендаря);
	УстановитьПараметрыОтбора();

КонецПроцедуры

&НаСервере
Функция ДатаАктуальности()
	Возврат ТекущаяДатаСеанса();
КонецФункции

&НаКлиенте
Процедура ВывестиДанныеРазделов(ПолучитьДанныеПлатежногоКалендаря = Истина)
	
	Если ПолучитьДанныеПлатежногоКалендаря Тогда
		НачатьЗаполнениеКалендаряНаКлиенте(); 
	ИначеЕсли ДлительнаяОперацияПлатежныйКалендарь.ДлительнаяОперация = Неопределено Тогда
		// Данные платежного календаря загружены
		ВывестиДанныеПлатежногоКалендаря()
	КонецЕсли;
 
	ВывестиЗаголовок();
	ВывестиДанныеДебиторскойЗадолженности();
	ВывестиДанныеКредиторскойЗадолженности();
	ВывестиДанныеПоСчетам();

КонецПроцедуры

&НаКлиенте
Процедура ВывестиЗаголовок()
	
	Наименование = СтрШаблон("%1%2", "ЗаголовокФинансы", РазрешениеЭкрана);
	Элементы[Наименование].Заголовок = СтрШаблон(НСтр("ru='Финансы: %1'"), Формат(ДатаАктуальности, "ДФ=""дд ММММ ЧЧ:мм"""));

КонецПроцедуры

&НаКлиенте
Процедура ПереместитьПозициюВНачалоСтраницы(ТекущаяПозиция, Раздел)
	
	КоличествоСтрок = КоличествоСтрокРаздела(Раздел);
	НомерСтраницы = Цел(ТекущаяПозиция / КоличествоСтрок);
	НомерПоследнейСтроки = НомерСтраницы * КоличествоСтрок;
	ТекущаяПозиция = НомерПоследнейСтроки - КоличествоСтрок + 1;
	
КонецПроцедуры

&НаКлиенте
Функция НомерСтраницы(КоличествоСтрок, ТекущаяПозиция)
	
	РасчетныйНомерСтраницы = Цел(ТекущаяПозиция / КоличествоСтрок);
	НомерСтраницы = ?(ТекущаяПозиция % КоличествоСтрок = 0, РасчетныйНомерСтраницы, РасчетныйНомерСтраницы + 1);
	
	Возврат НомерСтраницы;
	
КонецФункции

&НаКлиенте
Функция НомерСтрокиВРазделеПоПозиции(Раздел, Позиция, ТекущаяПозиция)
	
	КоличествоСтрок = КоличествоСтрокРаздела(Раздел);
	НомерСтраницы = НомерСтраницы(КоличествоСтрок, ТекущаяПозиция);
	СтрокПредыдущейСтраницы = (НомерСтраницы - 1) * КоличествоСтрок;
	НомерСтроки = СтрокПредыдущейСтраницы + Позиция;
	
	Возврат НомерСтроки;
	
КонецФункции

&НаКлиенте
Процедура ВывестиЧисловойПоказательВРаздел(КлючРаздела, Значение = Неопределено, Знак = "")

	Идентификаторы = ИдентификаторыСтроки(КлючРаздела);
	
	Если  Значение = Неопределено Тогда
		Значение = ЭтотОбъект[КлючРаздела];
	КонецЕсли;
	
	ЧастиЧисла = ЧастиЧисла(Значение);
	ЦелаяЧасть = СтрШаблон("%1%2", Знак, ЧастиЧисла.ЦелаяЧастьЧисла); 
	ДробнаяЧасть = Новый ФорматированнаяСтрока(ЧастиЧисла.ДробнаяЧастьЧисла, , ЦветНеАктивнойСтроки);
	Элементы[Идентификаторы.Получить("ЧисловойПоказатель")].Заголовок = Новый ФорматированнаяСтрока(ЦелаяЧасть, ДробнаяЧасть);
		
КонецПроцедуры

&НаКлиенте
Функция ЧастиЧисла(Число)
	
	ЧастиЧисла = СтрРазделить(Формат(Число, "ЧДЦ=2; ЧРД=.; ЧН=0; ЧВН=; ЧГ=3,0"), ".");
	
	Если ЧастиЧисла.Количество() > 0 Тогда
		ЦелаяЧастьЧисла = ЧастиЧисла[0] + ",";
	Иначе
		ЦелаяЧастьЧисла = "0";
	КонецЕсли;
		
	Если ЧастиЧисла.Количество() > 1 Тогда
		ДробнаяЧастьЧисла = ЧастиЧисла[1];
	Иначе
		ДробнаяЧастьЧисла = "00";
	КонецЕсли;
	
	Возврат Новый Структура("ЦелаяЧастьЧисла, ДробнаяЧастьЧисла", ЦелаяЧастьЧисла, ДробнаяЧастьЧисла);

КонецФункции

&НаКлиенте
Функция КоличествоСтрокРаздела(Раздел)
	
	СтруктураКоличества = СтруктураКоличестваЭлементовРаздела();
	КоличествоЭлементов = СтруктураКоличества[Раздел];
		
	Возврат КоличествоЭлементов;
		
КонецФункции

&НаКлиенте
Функция СтруктураКоличестваЭлементовРаздела()
	
	КоличествоЭлементовФормы = Новый Структура();
	КоличествоЭлементовФормы.Вставить("КредиторскаяЗадолженность", 
		МаксимальноеКоличествоЭлементовРаздела("КредиторскаяЗадолженность"));
	КоличествоЭлементовФормы.Вставить("Баннеры", КоличествоБаннеров());

	
	Если РазрешениеЭкрана = "ВысокоеРазрешение" Тогда
		КоличествоЭлементовФормы.Вставить("Счета", МаксимальноеКоличествоЭлементовРаздела("Счета"));
	Иначе
		// В низком разрешении размер раздела на 3 строки меньше
		КоличествоЭлементовФормы.Вставить("Счета", МаксимальноеКоличествоЭлементовРаздела("Счета") - 3);
	КонецЕсли;
		
	Возврат КоличествоЭлементовФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МаксимальноеКоличествоЭлементовРаздела(Раздел)
	
	Если Раздел = "КредиторскаяЗадолженность" Тогда
		Возврат 2; 
	ИначеЕсли Раздел = "Счета" Тогда
		Возврат 6;
	Иначе
		Возврат 5;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьСкролла(ТекущаяПозиция, КоличествоСтрокТаблицы, СтрокВРазделе, ПостфиксРаздела)
	 
	ЕстьСледующиеСтраницы = ТекущаяПозиция < КоличествоСтрокТаблицы;
	УстановитьВидимостьЭлемента("СкроллВправо", ЕстьСледующиеСтраницы, ПостфиксРаздела);  
	УстановитьВидимостьЭлемента("СкроллВправоНедоступный", Не ЕстьСледующиеСтраницы, ПостфиксРаздела);
	
	ЕстьПредыдущиеСтраницы = ТекущаяПозиция > СтрокВРазделе;
	УстановитьВидимостьЭлемента("СкроллВлево", ЕстьПредыдущиеСтраницы, ПостфиксРаздела);
	УстановитьВидимостьЭлемента("СкроллВлевоНедоступный", Не ЕстьПредыдущиеСтраницы, ПостфиксРаздела);
	
	ОтображатьСкролл = ЕстьСледующиеСтраницы Или ЕстьПредыдущиеСтраницы; 
	УстановитьВидимостьЭлемента("ГруппаСкролла", Истина, ПостфиксРаздела);
	УстановитьВидимостьЭлемента("ГруппаКнопокСкролла", ОтображатьСкролл, ПостфиксРаздела);

КонецПроцедуры

&НаСервере
Функция ВысокоеРазрешение()
	
	Возврат 1280;
		
КонецФункции

&НаСервере
Функция РазрешениеFullHD()
	
	Возврат 1920;
		
КонецФункции

&НаКлиенте
Функция ИдентификаторыСтроки(КлючСтроки)
	
	ПоляСтроки = Новый Соответствие;
	ПоляСтроки.Вставить("ЧисловойПоказатель",   СтрШаблон("ЧисловойПоказатель%1%2", КлючСтроки, РазрешениеЭкрана));
	
	Возврат ПоляСтроки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КоличествоБаннеров()
	Возврат 4;
КонецФункции

&НаСервере
Процедура УстановитьПараметрыОтбора()
	
	ПараметрВыбораОтборПоОрганизации = Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеОрганизации);
	ПараметрыВыбораОрганизации = Новый Массив;
	ПараметрыВыбораОрганизации.Добавить(ПараметрВыбораОтборПоОрганизации); 
	ИмяЭлемента = СтрШаблон("Организация%1", РазрешениеЭкрана);
	Элементы[ИмяЭлемента].ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораОрганизации);	
	УстановитьСписокВыбораЭлементаБанк();
		
КонецПроцедуры

&НаСервере
Процедура УстановитьСписокВыбораЭлементаБанк()
	
	ИмяЭлемента = СтрШаблон("Банк%1", РазрешениеЭкрана);
	Элементы[ИмяЭлемента].СписокВыбора.Очистить();
	
	Для Каждого СтрокаБанков Из ДоступныеБанки Цикл
		Элементы[ИмяЭлемента].СписокВыбора.Добавить(СтрокаБанков.Банк, СтрокаБанков.НаименованиеБанка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы

&НаСервере
Процедура ИзменитьПоложениеЭлементовНаФорме()
	
	ИмяЭлементаБанк = СтрШаблон("Банк%1", РазрешениеЭкрана);
	ИмяЭлементаГруппаФинансы = СтрШаблон("ГруппаФинансы%1", РазрешениеЭкрана);
	ИмяЭлементаЛеваяПанель = СтрШаблон("ЛеваяПанель%1", РазрешениеЭкрана);

	Элементы.Переместить(Элементы[ИмяЭлементаБанк], Элементы[ИмяЭлементаЛеваяПанель], Элементы[ИмяЭлементаГруппаФинансы]);  
	ИмяЭлементаОрганизация = СтрШаблон("Организация%1", РазрешениеЭкрана);
	Элементы[ИмяЭлементаОрганизация].Видимость = Ложь;

КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыФормы()
	
	МассивИменЭлементов = МассивИменЭлементовФормыДляКопирования();
	
	Для Каждого ИмяЭлемента Из МассивИменЭлементов Цикл 
		
		СоздатьЭлементыФормыГруппы(ИмяЭлемента, РазрешениеЭкрана);
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыФормыГруппы(ИмяЭлемента, Постфикс)
	
	ГруппаЭлементов = ЭлементГруппыПоИмени(ИмяЭлемента, "ГруппаДанные", Постфикс);
		
	Если ИмяЭлемента = "Счета" Тогда
		КоличествоГрупп = МаксимальноеКоличествоЭлементовРаздела(ИмяЭлемента);
	ИначеЕсли ИмяЭлемента = "Планирование" Тогда
		КоличествоГрупп = МаксимальноеКоличествоЭлементовРаздела(ИмяЭлемента);
	Иначе
		КоличествоГрупп = МаксимальноеКоличествоЭлементовРаздела("КредиторскаяЗадолженность");
	КонецЕсли;
	
	Для Индекс = 2 По КоличествоГрупп Цикл

		Для Каждого Элемент Из ГруппаЭлементов.ПодчиненныеЭлементы Цикл
			
			Если СтрНайти(Элемент.Имя, "1") <> 0 Тогда 
				СоздатьКопиюЭлемента(Элемент, Индекс, ГруппаЭлементов); 
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ЭлементГруппыПоИмени(ИмяЭлемента, Префикс, Постфикс)
	
	ИмяГруппы = СтрШаблон("%1%2%3", Префикс, ИмяЭлемента, Постфикс);
	ГруппаЭлементов = Элементы[ИмяГруппы];
		
	Возврат ГруппаЭлементов;
	
КонецФункции

&НаСервере
Функция МассивИменЭлементовФормыДляКопирования()
	
	МассивИмен = Новый Массив(); 
	МассивИмен.Добавить("Счета");
	МассивИмен.Добавить("КЗКредиты");
	МассивИмен.Добавить("КЗЛизинг");
	МассивИмен.Добавить("Планирование");

	Возврат МассивИмен;
	
КонецФункции

&НаСервере
Функция СоздатьКопиюЭлемента(ЭлементИсточника, Индекс, ГруппаЭлементов = Неопределено, ГруппаЭлементовПосле = Неопределено)
	
	Индекс = Строка(Индекс);
	Тип = ТипЗнч(ЭлементИсточника);
	Наименование = СтрЗаменить(ЭлементИсточника.Имя, "1", Индекс);
	ЭлементПриемника = Элементы.Вставить(Наименование, Тип, ГруппаЭлементов, ГруппаЭлементовПосле);
	
	Если Тип = Тип("ГруппаФормы") Тогда
		
		ЭлементПриемника.Вид = ЭлементИсточника.Вид;
		
		Если ЭлементИсточника.Вид = ВидГруппыФормы.ОбычнаяГруппа Тогда
			
			ЗаполнитьЗначенияСвойств(ЭлементПриемника, ЭлементИсточника,,"ПутьКДаннымЗаголовка");
			
		Иначе
			
			ЗаполнитьЗначенияСвойств(ЭлементПриемника, ЭлементИсточника);
			
		КонецЕсли;
		
		СкопироватьПодчиненныеЭлементы(ЭлементПриемника, ЭлементИсточника, Индекс);

	ИначеЕсли Тип = Тип("КнопкаФормы") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭлементПриемника, ЭлементИсточника);
				
	Иначе
		
		ЗаполнитьЗначенияСвойств(ЭлементПриемника, ЭлементИсточника);
		
	КонецЕсли;
	
	Если ГруппаЭлементовПосле <> Неопределено Тогда
		ЭлементПриемника.Видимость = Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СкопироватьПодчиненныеЭлементы(Приемник, Источник, Индекс) Экспорт
	
	Для Каждого ЭлементИсточника Из Источник.ПодчиненныеЭлементы Цикл
		СоздатьКопиюЭлемента(ЭлементИсточника, Индекс, Приемник);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьПодчиненныхЭлементов(ИмяГруппы, Видимость, ТолькоГруппы, Подраздел = "", Индекс = Неопределено)
	
	ИмяГруппы = СтрШаблон("%1%2%3", ИмяГруппы, Подраздел, РазрешениеЭкрана);
	Для Каждого ПодчиненныйЭлемент Из Элементы[ИмяГруппы].ПодчиненныеЭлементы Цикл
		
		Если ЭлементСоответствуетУсловиям(ПодчиненныйЭлемент, ТолькоГруппы, Индекс) Тогда
			Элементы[ПодчиненныйЭлемент.Имя].Видимость = Видимость;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Функция ЭлементСоответствуетУсловиям(ПодчиненныйЭлемент, ТолькоГруппы, Индекс)
	
	УсловиеПоИндексуВыполнено = Истина;
	УсловиеПоГруппеВыполнено = Ложь;
		
	Если Индекс <> Неопределено Тогда
		
		ИндексСтрокой = Строка(Индекс);
		УсловиеПоИндексуВыполнено = СтрНайти(ПодчиненныйЭлемент.Имя, ИндексСтрокой) <> 0;
		
	КонецЕсли;
	
	Если ТолькоГруппы И ПодчиненныйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа Или Не ТолькоГруппы Тогда
		УсловиеПоГруппеВыполнено = Истина;
	КонецЕсли;
		
	Возврат УсловиеПоИндексуВыполнено И УсловиеПоГруппеВыполнено;
			
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьЭлемента(Раздел, Видимость, ПостфиксРаздела = "", Индекс = "", Вариант = "")
	
	ИмяЭлемента = СтрШаблон("%1%2%3%4%5", Раздел, ПостфиксРаздела, Вариант, Индекс, РазрешениеЭкрана);
	Элементы[ИмяЭлемента].Видимость = Видимость;
		
КонецПроцедуры

#КонецОбласти

#Область ИнтернетПоддержкаПользователей

&НаКлиенте
Процедура Подключаемый_ПодключитьИнтернетПоддержкуПользователей()

	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", ЭтотОбъект);	

	ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		// Повторно получаем данные о сервисе и запускаем обновление данных.
		ОбновитьСведенияОСервисе();
		ФинУслугиКлиент.ОжидатьЗавершениеОбновленияДанныхСервиса(ЭтотОбъект);
	Иначе
		// Пользователь отказался от подключения либо у него не хватило прав.
		ТекстСообщения = НСтр("ru = 'Необходимо подключение Интернет-поддержки'");
		ПоказатьПредупреждение(, ТекстСообщения); 
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияОСервисе()
	
	ОшибкаОбновленияСервиса = Ложь;
	СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ФинУслуги;
	
	Если Параметры.Свойство("СведенияОСервисе") Тогда
		СведенияОСервисе = Параметры.СведенияОСервисе;
	Иначе
		СведенияОСервисе = УниверсальныйОбменСБанками.СведенияОСервисе(СервисОбменаСБанками);
	КонецЕсли;
	
	ФинУслуги.НачатьОбновлениеДанныхСервиса(СервисОбменаСБанками, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДанныхСервиса

&НаКлиенте
Процедура ОбновитьФормуПослеОбновленияДанныхСервиса(ТекстОшибки) Экспорт
	
	УстановитьВидимостьПанелиВиджетов(ТекстОшибки);
	ТаблицаПредложенийБанков.Очистить();
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		ФинУслугиКлиент.ОбновитьДоступныеПредложения(ЭтотОбъект);
		ОбновитьБаннеры();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьПанелиВиджетов(ТекстОшибки)
	
	ЕстьОшибки = ЗначениеЗаполнено(ТекстОшибки); 
	ОшибкаОбновленияСервиса = ЕстьОшибки;
	УстановитьВидимостьЭлемента("ОшибкиОбновления", ЕстьОшибки);
	УстановитьВидимостьЭлемента("Виджеты", Не ЕстьОшибки); 
	УстановитьВидимостьЭлемента("ЗаголовокБаннеры", Не ЕстьОшибки);

	ИмяЭлемента = СтрШаблон("СообщениеПроОшибки%1", РазрешениеЭкрана);
	Элементы[ИмяЭлемента].Заголовок = ТекстОшибки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступныеПредложенияБанковНаКлиенте()

	Если СведенияОСервисе.ТребуетсяПодключениеИнтернетПоддержки Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПодключитьИнтернетПоддержкуПользователей", 0.5, Истина);
	Иначе
		ФинУслугиКлиент.ОжидатьЗавершениеОбновленияДанныхСервиса(ЭтотОбъект);
		ПодключитьОбработчикОжидания("Подключаемый_ОтправитьСтатистику", 1, Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПредложенияФинУслуг

&НаКлиенте
Процедура ОбновитьБаннеры(НаправлениеВперед = Неопределено) Экспорт
	
	ФинУслугиКлиент.ОчиститьБаннеры(ЭтотОбъект, РазрешениеЭкрана);
	ЕстьПредложенияБанков = Ложь;
	
	КоличествоПредложений = ТаблицаПредложенийБанков.Количество();

	// Если нет предложений, панель остается пустой,
	// но не скрывается
	Если КоличествоПредложений = 0 Тогда
		УстановитьВидимостьЭлемента("ЗаголовокБаннеры", ЕстьПредложенияБанков);
		ФинУслугиКлиент.УстановитьВидимостьПереключателяБаннеров(ЭтотОбъект, 0, 0, 0, РазрешениеЭкрана);
		Возврат;
	КонецЕсли;
	
	СмещениеПозиции = КоличествоБаннеров() - 1;
	ПоследняяПозиция = ТекущаяПозицияБаннеры + СмещениеПозиции;
	ПоследняяПозицияВИтерации = Мин(ПоследняяПозиция, КоличествоПредложений);
	
	Для Итератор = ТекущаяПозицияБаннеры По ПоследняяПозицияВИтерации Цикл
		
		НомерСтроки = ФинУслугиКлиент.НомерСтрокиПоПозиции(Итератор, ТекущаяПозицияБаннеры);		
		ПредложениеБанка = ТаблицаПредложенийБанков.Получить(Итератор - 1);
		ФинУслугиКлиент.УстановитьПараметрыБаннера(ЭтотОбъект, ПредложениеБанка, НомерСтроки, РазрешениеЭкрана);
		Если НаправлениеВперед = Неопределено 
			Или (НаправлениеВперед = Истина И Итератор = ПоследняяПозицияВИтерации)
			Или (НаправлениеВперед = Ложь И Итератор = ТекущаяПозицияБаннеры) Тогда
			
			НазваниеФормы  = СтрЗаменить(ЭтотОбъект.ИмяФормы, ".Форма", "");
			ФинУслугиКлиент.ДополнитьТаблицуСтатистики(ТаблицаСтатистики, ПредложениеБанка, 1, 0, ИмяФормы); 
			
		КонецЕсли;
		Если ПредложениеБанка.Сервис1С = Ложь Тогда
			ЕстьПредложенияБанков = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьВидимостьЭлемента("ЗаголовокБаннеры", ЕстьПредложенияБанков);
	ФинУслугиКлиент.УстановитьВидимостьПереключателяБаннеров(
		ЭтотОбъект,ТекущаяПозицияБаннеры, ПоследняяПозиция, КоличествоПредложений, РазрешениеЭкрана);

КонецПроцедуры

&НаКлиенте
Процедура БаннерНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИмяРеквизита = СтрЗаменить(Элемент.Имя, РазрешениеЭкрана, "Идентификатор");
	ФинУслугиКлиент.ПерейтиПоНавигационнойСсылкеБаннера(ЭтотОбъект, ИмяРеквизита, Организация, Банк);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОтправитьСтатистику()
	
	Если ТаблицаСтатистики.Количество() = 0 Или СведенияОДлительнойОперации.ДлительнаяОперация <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтправитьСтатистикуНаСервере();
	ФинУслугиКлиент.ОжидатьЗавершениеОтправкиСтатистикиВСервис(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ОтправитьСтатистикуНаСервере()
	
	ФинУслуги.ОтправитьСтатистику(ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбратнаяСвязь

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = НСтр("ru='Отзыв о ""Финансы организации""'");
	ПараметрыПисьма.Текст = НСтр("ru='Ваши предложения о функционале ""Финансы организации""'");
	
	ОтправкаПочтовыхСообщенийКлиент.ДополнитьПараметрыПисьмаДляОтправкиЛегкойПочтой(ПараметрыПисьма);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция НаименованиеБанка(Банк)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Наименование");
КонецФункции

&НаСервере
Функция  ОбновитьДанныеМонитора()
		
	// Параметры обернем в структуру для их передачи через механизм ДлительныеОперации.
	ПараметрыОбновления = Новый Структура();
	ПараметрыОбновления.Вставить("Организация", Организация);
	ПараметрыОбновления.Вставить("РазделыМонитора", СписокРазделовВзаиморасчетыСКонтрагентами());
	ПараметрыОбновления.Вставить("ПолучатьПрошлыйПериод", Ложь);
	ПараметрыОбновления.Вставить("СрокХраненияТоваров", 6);
	
	ДлительнаяОперация = МониторРуководителя.ЗапуститьОбновлениеМонитораВФоне(
		ПараметрыОбновления, УникальныйИдентификатор, Ложь, Истина);
		
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДанныеМонитораРуководителя()
	
	ДлительнаяОперация = ОбновитьДанныеМонитора();
	ОжидатьЗавершенияДлительнойОперации(ДлительнаяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершенияДлительнойОперации(ДлительнаяОперация)
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершениеФоновогоЗаданияЗаполнения", ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеФоновогоЗаданияЗаполнения(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьФорму();
	
КонецПроцедуры

#КонецОбласти
