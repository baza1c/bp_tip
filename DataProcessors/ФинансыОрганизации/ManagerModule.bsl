#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает массив доступных организаций.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - Выбранная организация.
//
// Возвращаемое значение:
//	Массив.
//
Функция МассивОрганизаций(Организация) Экспорт
	
	МассивОрганизаций = Новый Массив;
	Если ЗначениеЗаполнено(Организация) Тогда
		
		МассивОрганизаций.Добавить(Организация);
		Возврат МассивОрганизаций;
		
	Иначе
				
		Возврат Новый Массив(ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь));
		
	КонецЕсли;
	
КонецФункции

// Возвращает таблицу с данными о банках, которые требуются для подбора подходящих предложений.
//
// Параметры:
//	СведенияОбОрганизациях - Массив структур - см. НовыеСведенияОбОрганизации().
//	ДатаПолучения - Дата - Дата, по состоянию на которую возвращаются данные.
//
// Возвращаемое значение:
//	ТаблицаЗначений - см. переменную ТаблицаБанков.
//
Функция СведенияОПредложенияхБанков(СведенияОбОрганизациях, Банк, ДатаПолучения, ИмяФормы) Экспорт

	ТипЦелое     = ОбщегоНазначения.ОписаниеТипаЧисло(4);
	ТипСтрока    = ОбщегоНазначения.ОписаниеТипаСтрока(0);
	ТипБулево    = Новый ОписаниеТипов("Булево");

	ТаблицаБанков = Новый ТаблицаЗначений;
	ТаблицаБанков.Колонки.Добавить("Банк",         Новый ОписаниеТипов("СправочникСсылка.БанкиУниверсальногоОбмена"));
	ТаблицаБанков.Колонки.Добавить("НаименованиеБанка", ТипСтрока);
	ТаблицаБанков.Колонки.Добавить("ПорядокСортировки",          ТипЦелое);  // Порядок сортировки продукта в общем списке.
	ТаблицаБанков.Колонки.Добавить("ПорядокСортировкиБанка",     ТипЦелое);  // Порядок сортировки банка в общем списке.
	ТаблицаБанков.Колонки.Добавить("СтепеньСоответствия",        ТипЦелое);
		
	// Продукт
	ТаблицаБанков.Колонки.Добавить("ИдентификаторУслуги", ТипСтрока);
	ТаблицаБанков.Колонки.Добавить("НавигационнаяСсылка", ТипСтрока); 
	ТаблицаБанков.Колонки.Добавить("НавигационнаяСсылкаСПараметрами", ТипСтрока);
	ТаблицаБанков.Колонки.Добавить("УслугаАктивна", ТипБулево);	
	ТаблицаБанков.Колонки.Добавить("ИмяФайлаКреатива", ТипСтрока);
	ТаблицаБанков.Колонки.Добавить("ИдентификаторРекламы", ТипСтрока);
	ТаблицаБанков.Колонки.Добавить("Сервис1С", ТипБулево); // Баннер 1С
	ТаблицаБанков.Колонки.Добавить("КраткоеОписание", ТипСтрока);
    ТаблицаБанков.Колонки.Добавить("ПодробноеОписание", ТипСтрока);

	// Данные свойства указывают на явный признак продукта банка
	ТаблицаБанков.Колонки.Добавить("УслугаСБП", ТипБулево);
    ТаблицаБанков.Колонки.Добавить("УслугаЭквайринг", ТипБулево);
    ТаблицаБанков.Колонки.Добавить("УслугаБизнесКарты", ТипБулево);
    ТаблицаБанков.Колонки.Добавить("УслугаЗарплатныйПроект", ТипБулево);
    	
	УстановитьПривилегированныйРежим(Истина);

	// Получим условия банков-партнеров сервиса.
	
	СервисОбменаСБанками = Перечисления.СервисыОбменаСБанками.ФинУслуги;
	
	ПредложенияУслуг = УниверсальныйОбменСБанками.ХарактеристикиУслугБанков(
		СервисОбменаСБанками,
		Перечисления.ХарактеристикиСервисаФинУслуги.ПредложенияУслуг,
		,
		Ложь);
		
	ДанныеГруппыПолучателейУслуг = УниверсальныйОбменСБанками.ХарактеристикиУслугБанков(
		СервисОбменаСБанками,
		Перечисления.ХарактеристикиСервисаФинУслуги.ГруппыПолучателейУслуг,
		,
		Ложь);
		
	ТаблицаПолучателейУслуг = ТаблицаПолучателейУслугБанков(ДанныеГруппыПолучателейУслуг);
		
	КомпонентыСоответствияПродукту = Новый Структура;
	
	// Этот стоп-фактор считаем самым значимым, поэтому при соответствии им, степень соответствия продукта для 
	// клиента увеличиваем на 100.
	КомпонентыСоответствияПродукту.Вставить("ПолучательУслуги", 100);

	// Эти стоп-факторы считаем более значимыми, поэтому при соответствии им, степень соответствия продукта для 
	// клиента увеличиваем на 10 за каждый пункт.
	КомпонентыСоответствияПродукту.Вставить("МинСуммаВыручки", 10);
	КомпонентыСоответствияПродукту.Вставить("СреднемесячноеПоступлениеНаСчет", 10);
	КомпонентыСоответствияПродукту.Вставить("МинимальноеЕжемесячноеПоступлениеНаСчет", 10);
	КомпонентыСоответствияПродукту.Вставить("ГруппыКонтрагентов", 10);
	
	//Белые списки считаем более значимыми, т.к. таких клиентов предположительно меньше, совпадения будут более точными.
 	КомпонентыСоответствияПродукту.Вставить("РегионыБелыйСписок", 10);
	КомпонентыСоответствияПродукту.Вставить("ОКОПФБелыйСписок", 10);
	КомпонентыСоответствияПродукту.Вставить("ВидыДеятельностиБелыйСписок", 10);
	
	// Эти стоп-факторы считаем менее значимыми, поэтому при соответствии им, степень соответствия продукта для 
	// клиента увеличиваем на 1 за каждый пункт.
	КомпонентыСоответствияПродукту.Вставить("ЮрФизЛицо", 1);
	КомпонентыСоответствияПродукту.Вставить("ВозрастИП", 1);
	КомпонентыСоответствияПродукту.Вставить("ДатаРегистрации", 1);
	КомпонентыСоответствияПродукту.Вставить("СрокФактическогоВеденияБизнеса", 1);
	
	//Черные списки считаем менее значимыми, т.к. таких клиентов больше, совпадения будут более частыми и менее точными.
	КомпонентыСоответствияПродукту.Вставить("РегионыЧерныйСписок", 1);
	КомпонентыСоответствияПродукту.Вставить("ОКОПФЧерныйСписок", 1);
	КомпонентыСоответствияПродукту.Вставить("ВидыДеятельностиЧерныйСписок", 1);
		
	ГСЧ = Новый ГенераторСлучайныхЧисел;
	
	ЗакрытыеПользователемБаннеры = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		Обработки.ФинансыОрганизации.ИмяНастройкиСервиса(),
		Обработки.ФинансыОрганизации.ИмяКлючНастройкиЗакрытыеПользователемБаннеры(),
		Новый Соответствие);
		
	Если СведенияОбОрганизациях.Количество() = 1 Тогда
		Организация = СведенияОбОрганизациях[0].Организация;
	КонецЕсли;
	
	МассивОрганизаций = МассивОрганизаций(Организация);
	ДоступныеБанки = ДоступныеБанки(МассивОрганизаций, Банк, ИмяФормы);	
	МассивИННБанков = МассивИННБанков(ДоступныеБанки);
	БанкиУниверсальногоОбмена = ОбщегоНазначения.ВыгрузитьКолонку(ПредложенияУслуг, "Банк", Истина); 
    ТаблицаБанкОрганизация = ТаблицаБанкОрганизация(МассивОрганизаций, Банк);
	ДоступныеБанкиУниверсальногоОбмена = БанкиУниверсальногоОбменаПоМассивуБанков(БанкиУниверсальногоОбмена, МассивИННБанков);

	ПараметрыПредложений = Новый Структура;
	ПараметрыПредложений.Вставить("СведенияОбОрганизациях", СведенияОбОрганизациях);
	ПараметрыПредложений.Вставить("ДатаПолучения", НачалоДня(ДатаПолучения));
	ПараметрыПредложений.Вставить("Банк", Банк);
	ПараметрыПредложений.Вставить("СведенияОБанках", ТаблицаБанков);
	ПараметрыПредложений.Вставить("ТаблицаПолучателейУслуг", ТаблицаПолучателейУслуг);
	ПараметрыПредложений.Вставить("КомпонентыСоответствияПродукту", КомпонентыСоответствияПродукту);
	ПараметрыПредложений.Вставить("ГСЧ", ГСЧ);
	ПараметрыПредложений.Вставить("ЗакрытыеПользователемБаннеры", ЗакрытыеПользователемБаннеры);
	ПараметрыПредложений.Вставить("МассивОрганизаций", МассивОрганизаций);
	ПараметрыПредложений.Вставить("ДоступныеБанки", ДоступныеБанки); 
	ПараметрыПредложений.Вставить("МассивИННБанков", МассивИННБанков);
	ПараметрыПредложений.Вставить("ДоступныеБанкиУниверсальногоОбмена", ДоступныеБанкиУниверсальногоОбмена);
    ПараметрыПредложений.Вставить("ТаблицаБанкОрганизация", ТаблицаБанкОрганизация);
	ПараметрыПредложений.Вставить("ИмяФормы", ИмяФормы);

	Для Каждого СтрокаТаблицы Из ПредложенияУслуг Цикл
		
		УслугаАктивна = СтрокаТаблицы.НачалоДействия <= ДатаПолучения И (СтрокаТаблицы.КонецДействия = Дата(1,1,1)
			Или СтрокаТаблицы.КонецДействия >= ДатаПолучения);
			
		Если УслугаАктивна Тогда
				
			ПараметрыПредложений.Вставить("УслугаАктивна", УслугаАктивна);
			ПараметрыПредложений.Вставить("БанкУниверсальногоОбмена", СтрокаТаблицы.Банк);
			ПараметрыПредложений.Вставить("ХранилищеXML", СтрокаТаблицы.Значение);
		
			ПрочитатьПредложенияУслугБанка(ПараметрыПредложений);
				
		КонецЕсли;
	
	КонецЦикла;
	
	// Индексируем таблицу после заполнения для быстрого поиска банков и услуг.
	ТаблицаБанков.Индексы.Добавить("Банк, ИдентификаторУслуги");
		
	ТаблицаБанков.Сортировать("ПорядокСортировки, ПорядокСортировкиБанка, СтепеньСоответствия Убыв");
	
	УстановитьПривилегированныйРежим(Ложь);

	Возврат ТаблицаБанков;

КонецФункции

// Возвращает имя сервиса для сохранения настройки.
//
Функция ИмяНастройкиСервиса() Экспорт
	
	Возврат "ФинУслуги";
	
КонецФункции

// Возвращает ключ для сохранения настройки.
//
Функция ИмяКлючНастройкиЗакрытыеПользователемБаннеры() Экспорт
	
	Возврат "ЗакрытыеПользователемБаннеры";
	
КонецФункции

// Возвращает структуру для параметра СведенияОбОрганизации функции СведенияОБанках().
//
// Возвращаемое значение:
//	Структура - см. переменную Результат.
//
Функция НовыеСведенияОбОрганизации() Экспорт

	Результат = Новый Структура();
	
	Результат.Вставить("Организация",               Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("ИНН",                       "");
	Результат.Вставить("ЮридическоеФизическоеЛицо", Перечисления.ЮридическоеФизическоеЛицо.ПустаяСсылка());
	Результат.Вставить("Нерезидент",                Ложь);
	Результат.Вставить("ДатаРождения",              '0001-01-01');
	Результат.Вставить("ДатаРегистрации",           '0001-01-01');
	Результат.Вставить("КодОКВЭД2",                 "");
	Результат.Вставить("КодОКОПФ",                  "");
	Результат.Вставить("ЮрАдресОрганизации",        ""); //  Значения полей в формате XML/JSON подсистемы КонтактнаяИнформация БСП.
	Результат.Вставить("ДатаФактическогоНачалаВеденияБизнеса", '0001-01-01');
	Результат.Вставить("СреднемесячноеПоступлениеНаСчет", 0);  // в рублях
	Результат.Вставить("МинимальноеЕжемесячноеПоступлениеНаСчет", 0);  // в рублях
	Результат.Вставить("СуммаВыручки", 0);  // в рублях
	
	// Параметры, которые не требуется заполнять, они будут рассчитаны автоматически.
	Результат.Вставить("ВозрастИП",                      0); // полных лет
	Результат.Вставить("СрокОтДатыРегистрации",          0); // в месяцах
	Результат.Вставить("СрокФактическогоВеденияБизнеса", 0); // в месяцах
	Результат.Вставить("КодРегиона",                     "");
	
	// Вспомогательная структура для кэширования временных данных при обработке разных банков.
	Результат.Вставить("ДополнительныеСвойства",         Новый Структура());
	
	Возврат Результат;

КонецФункции

// Процедура - обработчик начала получения предложений в сервисе ФинУслуги.
// Предназначена для вызова из модуля форм, в которых требуются данные предложений в сервисе.
//
// Параметры:
//  СервисОбменаСБанками - Перечисление.СервисыОбменаСБанками - Сервис обмена.
//
Функция ОбновитьДанныеСервиса(СервисОбменаСБанками) Экспорт

	РезультатОбновленияДанных = УниверсальныйОбменСБанками.АктуализироватьДанныеСервиса(СервисОбменаСБанками);
		
	Если РезультатОбновленияДанных.ИмяСобытия <> "ДанныеАктуальны" Тогда
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'ФинансыОрганизации.ОбновитьДанныеСервиса'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, , ,
			РезультатОбновленияДанных.ОписаниеСобытия);
		
	КонецЕсли;
	
	Возврат РезультатОбновленияДанных;

КонецФункции

#КонецОбласти

#Область ПредложенияУслуг

Процедура ПрочитатьПредложенияУслугБанка(ПараметрыПредложений)
	
	КомпонентыСоответствияПродукту = ПараметрыПредложений.КомпонентыСоответствияПродукту;
	ПредложенияУслуг = ФинУслуги.ОбъектXDTOХарактеристики(
		Перечисления.СервисыОбменаСБанками.ФинУслуги, ПараметрыПредложений.ХранилищеXML);
		
	Если ПредложенияУслуг = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяВерсияСервиса = УниверсальныйОбменСБанками.ВерсияСервиса(
		Перечисления.СервисыОбменаСБанками.ФинУслуги);
	
	ПараметрыПроверкиПоГруппамКонтрагентов = РегистрыСведений.ГруппыКонтрагентовЗаявкиВБанки.НовыеПараметрыПроверки();
	ПараметрыПроверкиПоГруппамКонтрагентов.Банк        = ПараметрыПредложений.БанкУниверсальногоОбмена; 
	ПараметрыПроверкиПоГруппамКонтрагентов.ДатаЗаявки  = ПараметрыПредложений.ДатаПолучения; 
	
	// Найдем актуальные предложения банка.
	Для Каждого ФинансоваяУслуга Из ПредложенияУслуг.ФинансоваяУслуга Цикл
		
		Сервис1С = ?(ЗначениеЗаполнено(ФинансоваяУслуга.Сервис1С), ФинансоваяУслуга.Сервис1С, Ложь);
		
		ЭтоФормаБанковскогоСчета = ЭтоФормаБанковскогоСчета(ПараметрыПредложений.ИмяФормы);
		
		// Для формы банковского счета могут показываться 
		// только предложения банка
		Если ЭтоФормаБанковскогоСчета И Сервис1С Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоФормаБанковскогоСчета 
			И ЗначениеЗаполнено(ФинансоваяУслуга.ТолькоФинансыОрганизации)
			И ФинансоваяУслуга.ТолькоФинансыОрганизации Тогда
			Продолжить;
		КонецЕсли;

		Если ФинансоваяУслуга.ДатаНачала > ПараметрыПредложений.ДатаПолучения
			Или ФинансоваяУслуга.ДатаОкончания < ПараметрыПредложений.ДатаПолучения Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверим версии сервиса, для которых применяются условия.
		Если ЗначениеЗаполнено(ФинансоваяУслуга.МинВерсияСервиса)
			И УниверсальныйОбменСБанкамиКлиентСервер.СравнитьВерсии(ТекущаяВерсияСервиса, ФинансоваяУслуга.МинВерсияСервиса) < 0 Тогда
			// Продукт может быть корректно отображен только в следующих версиях программы, 
			// для текущей версии его пропускаем.
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ФинансоваяУслуга.МаксВерсияСервиса)
			И УниверсальныйОбменСБанкамиКлиентСервер.СравнитьВерсии(ТекущаяВерсияСервиса, ФинансоваяУслуга.МаксВерсияСервиса) > 0 Тогда
			// Продукт предназначен для отображения на предыдущих версиях программы,
			// для текущей версии его пропускаем.
			Продолжить;
		КонецЕсли;			
				
		Если Не БанкВходитВГруппуКомпаний(ПараметрыПредложений.БанкУниверсальногоОбмена, 
			ПараметрыПредложений.ДоступныеБанкиУниверсальногоОбмена, Сервис1С) Тогда
			Продолжить;
		КонецЕсли;
		
		// Проверка для баннера сервиса ФинОтчетность
		Если ФинансоваяУслуга.НавигационнаяСсылка = "ФинОтчетность" И Не ПоддерживаетсяСервисФинОтчетность(
				ПараметрыПредложений.МассивИННБанков, ПараметрыПредложений.ДоступныеБанки) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Сервис1С 
			И СервисПодключен(ФинансоваяУслуга.НавигационнаяСсылка, 
				ПараметрыПредложений.ДатаПолучения, ПараметрыПредложений.МассивОрганизаций, 
				ПараметрыПредложений.ТаблицаБанкОрганизация) Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоМесяцевОтключенияБаннера = ФинансоваяУслуга.КоличествоМесяцевОтключенияБаннера;
		Если Не ЗначениеЗаполнено(КоличествоМесяцевОтключенияБаннера) Тогда
			КоличествоМесяцевОтключенияБаннера = 1;
		КонецЕсли;
		
		Если ПредложениеНеактуальноДляПользователя(ФинансоваяУслуга.Идентификатор, ПараметрыПредложений.ЗакрытыеПользователемБаннеры,
				КоличествоМесяцевОтключенияБаннера) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПройденаПроверкаПодключеннойУслуги(ПараметрыПредложений, ФинансоваяУслуга.ПроверкаНаличияЗППроекта, "ЗарплатныйПроект") Тогда
			Продолжить;
		КонецЕсли;

		Если Не ПройденаПроверкаПодключеннойУслуги(ПараметрыПредложений, ФинансоваяУслуга.ПроверкаНаличияКорпКарт, "КорпоративныеКарты") Тогда
			Продолжить;
		КонецЕсли;

		Если Не ПройденаПроверкаПодключеннойУслуги(ПараметрыПредложений, ФинансоваяУслуга.ПроверкаПодключенияСБП, "СБП") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПройденаПроверкаПодключеннойУслуги(ПараметрыПредложений, ФинансоваяУслуга.ПроверкаПодключенияЭквайринг, "Эквайринг") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПройденаПроверкаПодключеннойУслуги(ПараметрыПредложений, ФинансоваяУслуга.ПроверкаПодключенияДиректБанк, "ДиректБанк") Тогда
			Продолжить;
		КонецЕсли;

		МассивХешСуммИНН = МассивХешСуммИНН(ПараметрыПредложений.ТаблицаПолучателейУслуг, ФинансоваяУслуга, 
			ПараметрыПредложений.БанкУниверсальногоОбмена);

		СтопФакторыПройдены = Ложь;
		
		Для Каждого СведенияОбОрганизации Из ПараметрыПредложений.СведенияОбОрганизациях Цикл
			
			// Если стоп-факторы пройдены хотя бы для одной организации
			// то считаем предложение подходящим
			Если СтопФакторыПройдены Тогда
				Прервать;
			КонецЕсли;
			
			СтепеньСоответствия = 0;

			// Проверяем стоп-факторы.
			
			Если МассивХешСуммИНН.Количество() > 0 И Не ПроверитьПолучателейУслуг(СведенияОбОрганизации, ФинансоваяУслуга, МассивХешСуммИНН, 
				СтепеньСоответствия, КомпонентыСоответствияПродукту["ПолучательУслуги"]) Тогда	
				Продолжить;
			КонецЕсли;

			Если Не ПроверитьЮрФизЛицоКлиента(СведенияОбОрганизации, ФинансоваяУслуга, СтепеньСоответствия,
				КомпонентыСоответствияПродукту["ЮрФизЛицо"]) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ФинансоваяУслуга.МинВозрастИП) Или ЗначениеЗаполнено(ФинансоваяУслуга.МаксВозрастИП) Тогда
				
				Если Не ПроверитьВозрастКлиента(СведенияОбОрганизации, ПараметрыПредложений.ДатаПолучения, ФинансоваяУслуга) Тогда
					Продолжить;
				Иначе
					СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["ВозрастИП"];
				КонецЕсли;
					
			КонецЕсли;

			Если ЗначениеЗаполнено(ФинансоваяУслуга.МинСрокОтДатыРегистрации) Тогда
				
				Если Не ПроверитьСрокОтДатыРегистрации(СведенияОбОрганизации, ПараметрыПредложений.ДатаПолучения, ФинансоваяУслуга) Тогда
					Продолжить;
				Иначе
					СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["ДатаРегистрации"];
				КонецЕсли;
					
			КонецЕсли;

			Если Не ПроверитьСрокФактическогоВеденияБизнеса(СведенияОбОрганизации, ПараметрыПредложений.ДатаПолучения, 
				ФинансоваяУслуга, СтепеньСоответствия, КомпонентыСоответствияПродукту["СрокФактическогоВеденияБизнеса"]) Тогда	
				Продолжить;
			КонецЕсли;

			Если НЕ ПроверитьСреднемесячноеПоступлениеНаСчет(СведенияОбОрганизации, ПараметрыПредложений.ДатаПолучения,
				ФинансоваяУслуга, СтепеньСоответствия, КомпонентыСоответствияПродукту["СреднемесячноеПоступлениеНаСчет"]) Тогда	
				Продолжить;
			КонецЕсли;
			
			Если Не ПроверитьМинимальноеЕжемесячноеПоступлениеНаСчет(СведенияОбОрганизации, ПараметрыПредложений.ДатаПолучения,
				ФинансоваяУслуга, СтепеньСоответствия, КомпонентыСоответствияПродукту["МинимальноеЕжемесячноеПоступлениеНаСчет"]) Тогда	
				Продолжить;
			КонецЕсли;
			
			Если Не ПроверитьМинСуммаВыручки(СведенияОбОрганизации, ПараметрыПредложений.ДатаПолучения,
				ФинансоваяУслуга, СтепеньСоответствия, КомпонентыСоответствияПродукту["МинСуммаВыручки"]) Тогда		
				Продолжить;
			КонецЕсли;

			// Списки регионов.
			Если ПредложенияУслуг.Регионы.Количество() > 0 Тогда
				
				АктуальныйСписок = НайтиАктуальнуюНастройку(ПараметрыПредложений.ДатаПолучения, ФинансоваяУслуга.Идентификатор, 
					ПредложенияУслуг.Регионы); 
				Если АктуальныйСписок <> Неопределено Тогда				
					Если Не ПроверитьКодПоСписку(СведенияОбОрганизации.КодРегиона, АктуальныйСписок, "КодРегиона", Ложь) Тогда
						Продолжить;
					ИначеЕсли АктуальныйСписок.БелыйСписок Тогда
						СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["РегионыБелыйСписок"];
					Иначе
						СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["РегионыЧерныйСписок"];
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			// Списки организационно-правовых форм.
			Если ПредложенияУслуг.ОрганизационноПравовыеФормы.Количество() > 0 Тогда
				
				АктуальныйСписок = НайтиАктуальнуюНастройку(ПараметрыПредложений.ДатаПолучения, ФинансоваяУслуга.Идентификатор, 
					ПредложенияУслуг.ОрганизационноПравовыеФормы); 
				Если АктуальныйСписок <> Неопределено Тогда
					Если Не ПроверитьКодПоСписку(СведенияОбОрганизации.КодОКОПФ, АктуальныйСписок, "КодОКОПФ", Истина) Тогда
						Продолжить;
					ИначеЕсли АктуальныйСписок.БелыйСписок Тогда
						СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["ОКОПФБелыйСписок"];
					Иначе
						СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["ОКОПФЧерныйСписок"];
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			
			// Списки виды деятельности - этот список потенциально самый большой, его проверяем в последнюю очередь.
			Если ПредложенияУслуг.ВидыДеятельности.Количество() > 0 Тогда
				
				АктуальныйСписок = НайтиАктуальнуюНастройку(ПараметрыПредложений.ДатаПолучения, ФинансоваяУслуга.Идентификатор, 
					ПредложенияУслуг.ВидыДеятельности); 
				Если АктуальныйСписок <> Неопределено Тогда				
					Если Не ПроверитьКодПоСписку(СведенияОбОрганизации.КодОКВЭД2, АктуальныйСписок, "КодОКВЭД2", Истина) Тогда
						Продолжить;
					ИначеЕсли АктуальныйСписок.БелыйСписок Тогда
						СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["ВидыДеятельностиБелыйСписок"];
					Иначе
						СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["ВидыДеятельностиЧерныйСписок"];
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			// Проверяем обороты с определенными группами контрагентов.
			ПараметрыПроверкиПоГруппамКонтрагентов.Организация = СведенияОбОрганизации.Организация; 
			ПараметрыПроверкиПоГруппамКонтрагентов.Кэш = Новый Соответствие; 

			ЗаполнитьЗначенияСвойств(ПараметрыПроверкиПоГруппамКонтрагентов, ФинансоваяУслуга, 
				"ГруппаКонтрагентов,
				|КоличествоМесяцевПоГруппеКонтрагентов,
				|МинСреднемесячноеПоГруппеКонтрагентов,
				|МинЕжемесячноеПоГруппеКонтрагентов");
			
			Если РегистрыСведений.ГруппыКонтрагентовЗаявкиВБанки.ЕстьСтопФактор(ПараметрыПроверкиПоГруппамКонтрагентов) Тогда
				Если Не РегистрыСведений.ГруппыКонтрагентовЗаявкиВБанки.СтопФакторПройден(ПараметрыПроверкиПоГруппамКонтрагентов) Тогда				
					Продолжить;	
				Иначе
					СтепеньСоответствия = СтепеньСоответствия + КомпонентыСоответствияПродукту["ГруппыКонтрагентов"];
				КонецЕсли;
			КонецЕсли;
			
			// Проверяем обороты с определенными группами поставщиков.
			
			ЗаполнитьЗначенияСвойств(ПараметрыПроверкиПоГруппамКонтрагентов, ФинансоваяУслуга, 
				"ГруппаПоставщиков,
				|КоличествоМесяцевПоГруппеПоставщиков,
				|МинСреднемесячноеПоГруппеПоставщиков,
				|МинЕжемесячноеПоГруппеПоставщиков");
			
			Если РегистрыСведений.ГруппыКонтрагентовЗаявкиВБанки.ЕстьСтопФактор(ПараметрыПроверкиПоГруппамКонтрагентов, Истина) Тогда
				Если Не РегистрыСведений.ГруппыКонтрагентовЗаявкиВБанки.СтопФакторПройден(ПараметрыПроверкиПоГруппамКонтрагентов, Истина) Тогда
					Продолжить;
				Иначе
					СтепеньСоответствия = СтепеньСоответствия 
						+ КомпонентыСоответствияПродукту["ГруппыКонтрагентов"];
				КонецЕсли;
			КонецЕсли;
			
					
			СтрокаТаблицы = ПараметрыПредложений.СведенияОБанках.Добавить();
			
			СтрокаТаблицы.СтепеньСоответствия = СтепеньСоответствия; 
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ФинансоваяУслуга);
			СтрокаТаблицы.Банк = ПараметрыПредложений.БанкУниверсальногоОбмена;
			СтрокаТаблицы.НаименованиеБанка = ПараметрыПредложений.БанкУниверсальногоОбмена;
			ИдентификаторБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыПредложений.БанкУниверсальногоОбмена, "Идентификатор");
			
			Если ЗначениеЗаполнено(ФинансоваяУслуга.СлучайныйПорядокСортировкиОт)
				И ЗначениеЗаполнено(ФинансоваяУслуга.СлучайныйПорядокСортировкиДо) Тогда
				ПорядокСортировкиБанка = ПараметрыПредложений.ГСЧ.СлучайноеЧисло(ФинансоваяУслуга.СлучайныйПорядокСортировкиОт, 
					ФинансоваяУслуга.СлучайныйПорядокСортировкиДо);
			Иначе
				ПорядокСортировкиБанка = 0;
			КонецЕсли;
			
			СтрокаТаблицы.ПорядокСортировкиБанка = ПорядокСортировкиБанка;
			СтрокаТаблицы.ИдентификаторУслуги = ФинансоваяУслуга.Идентификатор;
			СтрокаТаблицы.ИмяФайлаКреатива = ФинансоваяУслуга.ИмяФайлаКреатива;
			СтрокаТаблицы.ИдентификаторРекламы = ФинансоваяУслуга.ИдентификаторРекламы;
			СтрокаТаблицы.НавигационнаяСсылка = ФинансоваяУслуга.НавигационнаяСсылка;
			СтрокаТаблицы.НавигационнаяСсылкаСПараметрами = 
				НавигационнаяСсылкаСПараметрами(ФинансоваяУслуга.НавигационнаяСсылкаСПараметрами, ПараметрыПредложений.СведенияОбОрганизациях);
			СтрокаТаблицы.Сервис1С = Сервис1С;
			СтрокаТаблицы.УслугаСБП = ЗначениеЗаполнено(ФинансоваяУслуга.ПроверкаПодключенияСБП);
            СтрокаТаблицы.УслугаЭквайринг = ЗначениеЗаполнено(ФинансоваяУслуга.ПроверкаПодключенияЭквайринг);
			СтрокаТаблицы.УслугаБизнесКарты = ЗначениеЗаполнено(ФинансоваяУслуга.ПроверкаНаличияКорпКарт);
            СтрокаТаблицы.УслугаЗарплатныйПроект = ЗначениеЗаполнено(ФинансоваяУслуга.ПроверкаНаличияЗППроекта);
			СтрокаТаблицы.КраткоеОписание = ФинансоваяУслуга.КраткоеОписание;
			СтрокаТаблицы.ПодробноеОписание = ФинансоваяУслуга.ПодробноеОписание;
			СтрокаТаблицы.УслугаАктивна = ПараметрыПредложений.УслугаАктивна;
			СтопФакторыПройдены = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПроверитьПолучателейУслуг(СведенияОКлиенте, ФинансоваяУслуга, МассивХешСуммИНН, СтепеньСоответствия, КомпонентСоответствия) 
	
	Если ЗначениеЗаполнено(ФинансоваяУслуга.ГруппаПолучателейУслуг) Тогда
		
		ХешСуммаИНН = ХешСуммаСтроки(СведенияОКлиенте.ИНН);
		
		Если МассивХешСуммИНН.Найти(ХешСуммаИНН) = Неопределено Тогда
			Возврат Ложь;
		Иначе
			
			СтепеньСоответствия = СтепеньСоответствия + КомпонентСоответствия;		
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

Функция ПроверитьЮрФизЛицоКлиента(СведенияОКлиенте, ДействующаяФинансоваяУслуга, СтепеньСоответствия, КомпонентСоответствия) 
	
	// В значениях может быть Неопределено, если условия были получены в версии, 
	// когда такого стоп-фактора еще не было.
	ВозможноОбслуживаниеИП = ДействующаяФинансоваяУслуга.ВозможноОбслуживаниеИП <> Ложь;
	ВозможноОбслуживаниеЮЛ = ДействующаяФинансоваяУслуга.ВозможноОбслуживаниеЮЛ <> Ложь;

	Если СведенияОКлиенте.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ВозможноОбслуживание = ВозможноОбслуживаниеИП;
	Иначе
		ВозможноОбслуживание = ВозможноОбслуживаниеЮЛ;
	КонецЕсли;
	
	Если ВозможноОбслуживание Тогда
		
		// Степень соответствия не добавляем для продуктов, предназначенных для ИП и ЮЛ одновременно.
		Если НЕ (ВозможноОбслуживаниеИП	И ВозможноОбслуживаниеЮЛ) Тогда
			СтепеньСоответствия = СтепеньСоответствия + КомпонентСоответствия;
		КонецЕсли;
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ПроверитьВозрастКлиента(СведенияОКлиенте, ДатаПолучения, ФинансоваяУслуга)

	Если СведенияОКлиенте.ЮридическоеФизическоеЛицо <> Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		// Не является физлицом, не проверяем условие по возрасту совсем.
		Возврат Истина;
	КонецЕсли;

	Если ЗначениеЗаполнено(ФинансоваяУслуга.МинВозрастИП) Тогда
		Если СведенияОКлиенте.ВозрастИП < ФинансоваяУслуга.МинВозрастИП Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФинансоваяУслуга.МаксВозрастИП) Тогда
		Если СведенияОКлиенте.ВозрастИП > ФинансоваяУслуга.МаксВозрастИП Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

Функция ПроверитьСрокОтДатыРегистрации(СведенияОКлиенте, ДатаПолучения, ФинансоваяУслуга)

	Если ЗначениеЗаполнено(ФинансоваяУслуга.МинСрокОтДатыРегистрации) Тогда
		Если СведенияОКлиенте.СрокОтДатыРегистрации < ФинансоваяУслуга.МинСрокОтДатыРегистрации Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

Функция ПроверитьСрокФактическогоВеденияБизнеса(СведенияОКлиенте, ДатаПолучения, ФинансоваяУслуга, СтепеньСоответствия, КомпонентСоответствия)

	Если ЗначениеЗаполнено(ФинансоваяУслуга.МинСрокФактическойДеятельности) Тогда
		Если СведенияОКлиенте.СрокФактическогоВеденияБизнеса < ФинансоваяУслуга.МинСрокФактическойДеятельности Тогда
			Возврат Ложь;
		Иначе
			СтепеньСоответствия = СтепеньСоответствия + КомпонентСоответствия;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

Функция ПроверитьСреднемесячноеПоступлениеНаСчет(СведенияОКлиенте, ДатаПолучения, ФинансоваяУслуга, СтепеньСоответствия, КомпонентСоответствия)

	Если ЗначениеЗаполнено(ФинансоваяУслуга.МинСреднемесячноеПоступлениеНаСчет) Тогда
		Если СведенияОКлиенте.СреднемесячноеПоступлениеНаСчет < ФинансоваяУслуга.МинСреднемесячноеПоступлениеНаСчет Тогда
			Возврат Ложь;
		Иначе
			СтепеньСоответствия = СтепеньСоответствия + КомпонентСоответствия;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

Функция ПроверитьМинимальноеЕжемесячноеПоступлениеНаСчет(СведенияОКлиенте, ДатаПолучения, ФинансоваяУслуга, СтепеньСоответствия, КомпонентСоответствия)

	Если ЗначениеЗаполнено(ФинансоваяУслуга.МинЕжемесячноеПоступлениеНаСчет) Тогда
		Если СведенияОКлиенте.МинимальноеЕжемесячноеПоступлениеНаСчет < ФинансоваяУслуга.МинЕжемесячноеПоступлениеНаСчет Тогда
			Возврат Ложь;
		Иначе
			СтепеньСоответствия = СтепеньСоответствия + КомпонентСоответствия;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

Функция ПроверитьКодПоСписку(ИскомыйКод, ДействующийСписок, ИмяПоля, ПоискПоПервымСимволам)
	
	ЭтоБелыйСписок = (ДействующийСписок.БелыйСписок = Истина);
	
	// Проверим код.
	СписокКодов = ДействующийСписок[ИмяПоля];
	Для Каждого ТекущийКод Из СписокКодов Цикл
		Если ПоискПоПервымСимволам И СтрНачинаетсяС(ИскомыйКод, СокрЛП(ТекущийКод)) Тогда
			Возврат ЭтоБелыйСписок;
		ИначеЕсли СокрЛП(ТекущийКод) = ИскомыйКод Тогда
			Возврат ЭтоБелыйСписок;
		КонецЕсли;
	КонецЦикла; 
	
	Возврат Не ЭтоБелыйСписок;

КонецФункции

Функция НайтиАктуальнуюНастройку(ДатаПолучения, ИдентификаторПродукта, СписокXDTO)

	Если СписокXDTO = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Найдем актуальный список для указанного продукта.
	Результат = Неопределено;
	Для Каждого ТекущиеДанные Из СписокXDTO Цикл
		Если ТекущиеДанные.Свойства().Получить("ИдентификаторПродукта") <> Неопределено
			И ТекущиеДанные.ИдентификаторПродукта = ИдентификаторПродукта
			И ТекущиеДанные.ДатаНачала <= ДатаПолучения
			И ТекущиеДанные.ДатаОкончания >= ДатаПолучения Тогда
			Результат = ТекущиеДанные;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	// Если список для указанного продукта не найден,
	// найдем актуальный список, общий для всех продуктов.
	Если Результат = Неопределено Тогда
		Для Каждого ТекущиеДанные Из СписокXDTO Цикл
			Если (ТекущиеДанные.Свойства().Получить("ИдентификаторПродукта") = Неопределено
				Или НЕ ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторПродукта))
				И ТекущиеДанные.ДатаНачала <= ДатаПолучения
				И ТекущиеДанные.ДатаОкончания >= ДатаПолучения Тогда
				Результат = ТекущиеДанные;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ПроверитьМинСуммаВыручки(СведенияОКлиенте, ДатаПолучения, ФинансоваяУслуга, СтепеньСоответствия, КомпонентСоответствия)

	Если ЗначениеЗаполнено(ФинансоваяУслуга.МинСуммаВыручки) Тогда
		Если СведенияОКлиенте.СуммаВыручки < ФинансоваяУслуга.МинСуммаВыручки Тогда
			Возврат Ложь;
		Иначе
			СтепеньСоответствия = СтепеньСоответствия + КомпонентСоответствия;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

Функция БанкиУниверсальногоОбменаПоМассивуБанков(БанкиУниверсальногоОбмена, МассивИННБанков)
	
	Результат = Новый Массив;
	
	РеквизитыБанков = УниверсальныйОбменСБанками.РеквизитыБанков(БанкиУниверсальногоОбмена);
	ТаблицаИННГруппыКомпанийБанков = РегистрыСведений.ГруппыКомпанийБанков.ТаблицаИННГруппыКомпанийБанков(РеквизитыБанков);
	Для Каждого СтрокаРеквизитов Из РеквизитыБанков Цикл  
		// Для каждого банка универсального обмена 
		// получим полный список ИНН всей группы компаний банка
		Отбор = Новый Структура("ИННБанка", СтрокаРеквизитов.ИНН);
		ИННГруппыКомпанийБанка = ТаблицаИННГруппыКомпанийБанков.Скопировать(Отбор);
		МассивИННГруппыКомпанийБанка = ИННГруппыКомпанийБанка.ВыгрузитьКолонку("ИНН");
		РазностьМассивов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(МассивИННГруппыКомпанийБанка, МассивИННБанков);
		// Добавляем только в том случае, если список ИНН группы компаний банка
		// и список ИНН банков, в которых открыты счета - пересекаются
		Если РазностьМассивов.Количество() <> МассивИННГруппыКомпанийБанка.Количество() Тогда
			Результат.Добавить(СтрокаРеквизитов.Банк);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Функция БанкВходитВГруппуКомпаний(БанкУниверсальногоОбмена, ДоступныеБанкиУниверсальногоОбмена, Сервис1С)
		
	Возврат ДоступныеБанкиУниверсальногоОбмена.Найти(БанкУниверсальногоОбмена) <> Неопределено Или Сервис1С;

КонецФункции

Функция СервисПодключен(Сервис, ДатаПолучения, МассивОрганизаций, ТаблицаБанкОрганизация)
	
	Результат = Ложь;
	
	Если Сервис = "Контрагент" Тогда
		
		Возврат РаботаСКонтрагентамиБП.Сервис1СКонтрагентПодключен();
		
	ИначеЕсли Сервис = "СПАРКРиски" Тогда
		
		Если РаботаВМоделиСервиса.РазделениеВключено() Тогда
			СервисПодключен = ИнтернетПоддержкаПользователей.УслугаПодключена(
				СПАРКРискиКлиентСервер.ИдентификаторУслугиИндикаторыРиска());
		Иначе
			ВидОшибки = СПАРКРискиМониторингСобытий.ОбновитьСобытияМониторинга();
			СервисПодключен = НЕ ЗначениеЗаполнено(ВидОшибки);
		КонецЕсли;

		Возврат СервисПодключен;
		
	ИначеЕсли Сервис = "ОткрытиеСчета" Тогда
		
		Возврат  ЕстьЗаявкиНаОткрытиеСчета(ДатаПолучения, МассивОрганизаций);
		
	ИначеЕсли Сервис = "Кредит" Тогда
		
		Возврат  ЕстьЗаявкиНаКредит(ДатаПолучения, МассивОрганизаций);
		
	ИначеЕсли Сервис = "СБП" Тогда
		
		Возврат  СервисСБППодключен(ТаблицаБанкОрганизация);

	КонецЕсли;
 		
	Возврат Результат;

КонецФункции

Функция ПредложениеНеактуальноДляПользователя(Идентификатор, ЗакрытыеПользователемБаннеры, КоличествоМесяцев) 
		
	ДатаЗакрытия = ЗакрытыеПользователемБаннеры[Идентификатор];
	Если ДатаЗакрытия <> Неопределено Тогда 
		Если ДобавитьМесяц(ДатаЗакрытия, КоличествоМесяцев) > ТекущаяДатаСеанса() Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПоддерживаетсяСервисФинОтчетность(МассивИННБанков, Банки)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БанкиУниверсальногоОбмена.ИНН КАК ИНН
	|ПОМЕСТИТЬ ВТ_ДоступныеБанки
	|ИЗ
	|	Справочник.БанкиУниверсальногоОбмена КАК БанкиУниверсальногоОбмена
	|ГДЕ
	|	БанкиУниверсальногоОбмена.Ссылка В(&ДоступныеБанки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КлассификаторБанков.ИНН
	|ИЗ
	|	Справочник.Банки КАК Банки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК КлассификаторБанков
	|		ПО Банки.Код = КлассификаторБанков.Код
	|			И Банки.КоррСчет = КлассификаторБанков.КоррСчет
	|			И (НЕ КлассификаторБанков.ДеятельностьПрекращена)
	|ГДЕ
	|	Банки.Ссылка В(&ДоступныеБанки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ГруппыКомпанийБанков.ИНН, ВТ_ДоступныеБанки.ИНН) КАК ИНН
	|ИЗ
	|	ВТ_ДоступныеБанки КАК ВТ_ДоступныеБанки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГруппыКомпанийБанков КАК ГруппыКомпанийБанков
	|		ПО ВТ_ДоступныеБанки.ИНН = ГруппыКомпанийБанков.Банк.ИНН 
	|ГДЕ
	|	ВТ_ДоступныеБанки.ИНН В (&МассивИННБанков) ИЛИ ГруппыКомпанийБанков.ИНН В (&МассивИННБанков)";
		
	Запрос.УстановитьПараметр("ДоступныеБанки", ФинОтчетностьВБанки.ДоступныеБанки());
	Запрос.УстановитьПараметр("Банки", Банки); 
	Запрос.УстановитьПараметр("МассивИННБанков", МассивИННБанков);

	РезультатЗапроса = Запрос.Выполнить();	
		
	Возврат Не РезультатЗапроса.Пустой();

КонецФункции

Функция ПройденаПроверкаПодключеннойУслуги(ПараметрыПредложений, ПроверкаНаличияУслуги, ТипУслуги)
	
	ПроверкаПройдена = Истина;
	ВозможноПодключение = Истина;
	
	Если Не ЗначениеЗаполнено(ПроверкаНаличияУслуги) Или ПроверкаНаличияУслуги = "НеПроверять" Тогда
		Возврат ПроверкаПройдена;
	КонецЕсли;
	
	ТаблицаСОтбором = ТаблицаСОтборомПоБанкуУниверсальногоОбмена(
		ПараметрыПредложений.ТаблицаБанкОрганизация, ПараметрыПредложений.БанкУниверсальногоОбмена);
		
	Для Каждого СтрокаТаблицы Из ТаблицаСОтбором Цикл

		Если ТипУслуги = "КорпоративныеКарты" Тогда
			
			Запрос = ЗапросКорпоративныеКарты();
			НаличиеУслуги = УслугаПодключена(Запрос, СтрокаТаблицы, Неопределено);
			
		ИначеЕсли ТипУслуги = "ЗарплатныйПроект" Тогда
			
			Запрос = ЗапросЗарплатныйПроект();
			НаличиеУслуги = УслугаПодключена(Запрос, СтрокаТаблицы, Неопределено);
			
		ИначеЕсли ТипУслуги = "Эквайринг" Тогда
			
			Запрос = ЗапросВидОплаты();
			НаличиеУслуги = УслугаПодключена(Запрос, СтрокаТаблицы, Перечисления.ТипыОплат.ПлатежнаяКарта);
			
		ИначеЕсли ТипУслуги = "СБП" Тогда
			
			Запрос = ЗапросВидОплаты();
			НаличиеУслуги = УслугаПодключена(Запрос, СтрокаТаблицы, Перечисления.ТипыОплат.СБП);
			
		ИначеЕсли ТипУслуги = "ДиректБанк" Тогда
						
			НастройкаОбмена = ОбменСБанками.НастройкаОбмена(СтрокаТаблицы.Организация, СтрокаТаблицы.Банк);
			НаличиеУслуги = ЗначениеЗаполнено(НастройкаОбмена);
			
			Если Не НаличиеУслуги Тогда
				ВозможноПодключение = ОбменСБанками.ВозможенПрямойОбменСБанком(СтрокаТаблицы.БИК);
			КонецЕсли;
			
		Иначе
			НаличиеУслуги = Ложь;
		КонецЕсли;
		
		ПроверкаПройдена = ПроверкаНаличияУслуги = "Подключен" И НаличиеУслуги 
			Или (ПроверкаНаличияУслуги = "НеПодключен" И Не НаличиеУслуги И ВозможноПодключение);
			
		Если ПроверкаПройдена Тогда
			Возврат ПроверкаПройдена;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПроверкаПройдена;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьЗаявкиНаОткрытиеСчета(ТекущаяДата, Организация)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИСТИНА КАК Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаОткрытиеСчета КАК ЗаявкаНаОткрытиеСчета
	|ГДЕ
	|	ЗаявкаНаОткрытиеСчета.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ТекущаяДата, МЕСЯЦ, -12), ДЕНЬ) И КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|	И &ОтборПоОрганизации";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "ЗаявкаНаОткрытиеСчета.Организация В(&Организация)");
		Запрос.УстановитьПараметр("Организация", Организация);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "Истина");
		
	КонецЕсли;

	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция ЕстьЗаявкиНаКредит(ТекущаяДата, Организация)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИСТИНА КАК Ссылка
	|ИЗ
	|	Документ.ЗаявкаНаКредит КАК ЗаявкаНаКредит
	|ГДЕ
	|	ЗаявкаНаКредит.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ТекущаяДата, МЕСЯЦ, -6), ДЕНЬ) И КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|	И ЗаявкаНаКредит.СервисОбменаСБанками = ЗНАЧЕНИЕ(Перечисление.СервисыОбменаСБанками.ЗаявкиНаКредит)
	|	И &ОтборПоОрганизации";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "ЗаявкаНаКредит.Организация В(&Организация)");
		Запрос.УстановитьПараметр("Организация", Организация);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "Истина");
		
	КонецЕсли;

	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция СервисСБППодключен(ТаблицаБанкОрганизация)
	
	СервисПодключен = Ложь;
	
	Для Каждого СтрокаТаблицы Из ТаблицаБанкОрганизация Цикл
		Если Не ЕстьНастройкаСБП(СтрокаТаблицы.Банк, СтрокаТаблицы.Организация) Тогда 
			Возврат СервисПодключен;
		Иначе 
			СервисПодключен = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СервисПодключен;

КонецФункции

Функция ЕстьНастройкаСБП(Банк, Организация)

	ИнтеграцияДоступна = СистемаБыстрыхПлатежей.НастройкаПодключенияДоступна();
	Если ИнтеграцияДоступна Тогда
		БИК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Банк, "Код");
		НастройкиИнтеграцииСБП = ИнтеграцияССБПБП.НастройкиИнтеграцииСБП(Организация, БИК);
		ИнтеграцияДоступна = НастройкиИнтеграцииСБП.ИнтеграцияДоступнаСБПc2b Или НастройкиИнтеграцииСБП.ИнтеграцияДоступнаСБПb2b
	КонецЕсли;
	
	Если ИнтеграцияДоступна Тогда
		НастройкаСБП = НастройкиИнтеграцииСБП.Настройка; 
		НастройкаСБПb2b = НастройкиИнтеграцииСБП.Настройкаb2b;
		Если ЗначениеЗаполнено(НастройкаСБП) Или ЗначениеЗаполнено(НастройкаСБПb2b) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

Функция ХешСуммаСтроки(ИсходнаяСтрока)
	
	ХешированиеДанныхОбъект = Новый ХешированиеДанных(ХешФункция.SHA256);
	ХешированиеДанныхОбъект.Добавить(ИсходнаяСтрока);
	
	Возврат Base64Строка(ХешированиеДанныхОбъект.ХешСумма);
	
КонецФункции

Функция ТаблицаПолучателейУслугБанков(ДанныеГруппыПолучателейУслуг)
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Банк", Новый ОписаниеТипов("СправочникСсылка.БанкиУниверсальногоОбмена"));
	ТаблицаДанных.Колонки.Добавить("ГруппаПолучателейУслуг", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаДанных.Колонки.Добавить("ХешСуммаИНН", ОбщегоНазначения.ОписаниеТипаСтрока(0));

	Для Каждого СтрокаДанных Из ДанныеГруппыПолучателейУслуг Цикл
		
		ОбъектXDTOХарактеристики = ФинУслуги.ОбъектXDTOХарактеристики(
			Перечисления.СервисыОбменаСБанками.ФинУслуги, СтрокаДанных.Значение);
		ЗаполнитьТаблицуИзХарактеристики(СтрокаДанных.Банк, ОбъектXDTOХарактеристики, ТаблицаДанных);
		
	КонецЦикла;

	Возврат ТаблицаДанных;
	
КонецФункции  

Процедура ЗаполнитьТаблицуИзХарактеристики(Банк, ОбъектXDTOХарактеристики, ТаблицаДанных)
	
	Если ОбъектXDTOХарактеристики = Неопределено 
		Или ОбъектXDTOХарактеристики.Свойства().Получить("ГруппыПолучателейУслуг") = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого СтрокаИНН Из ОбъектXDTOХарактеристики.ГруппыПолучателейУслуг Цикл
		
		ГруппаПолучателейУслуг = СокрЛП(СтрокаИНН.Идентификатор);
		DataJSON = СтрокаИНН.DATA;
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(DataJSON);
		
		Попытка
			Данные = ПрочитатьJSON(ЧтениеJSON);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ИмяСобытия = УниверсальныйОбменСБанкамиОбщегоНазначения.СобытиеЖурналаРегистрации(
				Перечисления.СервисыОбменаСБанками.ФинУслуги,
				НСтр("ru = 'Чтение группы получателей услуг'", ОбщегоНазначения.КодОсновногоЯзыка()));
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			Продолжить;
		КонецПопытки;
		
		Если ТипЗнч(Данные) <> Тип("Массив") Или Данные.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
				
		Для Каждого Характеристика Из Данные Цикл
			Если ТипЗнч(Характеристика) <> Тип("Структура")
				Или Не Характеристика.Свойство("ИНН") Или ПустаяСтрока(Характеристика.ИНН) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицы = ТаблицаДанных.Добавить();
			СтрокаТаблицы.Банк = Банк;
			СтрокаТаблицы.ГруппаПолучателейУслуг = ГруппаПолучателейУслуг;
			СтрокаТаблицы.ХешСуммаИНН = СокрЛП(Характеристика.ИНН);
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

Функция МассивХешСуммИНН(ТаблицаДанных, ФинансоваяУслуга, Банк)
	
	ГруппаПолучателейУслуг = ФинансоваяУслуга.ГруппаПолучателейУслуг;
	
	Если Не ЗначениеЗаполнено(ГруппаПолучателейУслуг) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Отбор = Новый Структура("Банк, ГруппаПолучателейУслуг", Банк, ГруппаПолучателейУслуг);
	ТаблицаИНН = ТаблицаДанных.Скопировать(Отбор, "ХешСуммаИНН");
	
	Возврат ТаблицаИНН.ВыгрузитьКолонку("ХешСуммаИНН");

КонецФункции

Функция МассивИННБанков(Банки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КлассификаторБанков.ИНН КАК ИНН
	|ИЗ
	|	Справочник.Банки КАК Банки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК КлассификаторБанков
	|		ПО Банки.Код = КлассификаторБанков.Код
	|			И Банки.КоррСчет = КлассификаторБанков.КоррСчет
	|			И (НЕ КлассификаторБанков.ДеятельностьПрекращена)
	|ГДЕ
	|	Банки.Ссылка В(&Банки)
	|	И КлассификаторБанков.ИНН <> """"";
	
	Запрос.УстановитьПараметр("Банки", Банки);
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаИНН = РезультатЗапроса.Выгрузить();
	
	Возврат ТаблицаИНН.ВыгрузитьКолонку("ИНН");
	
КонецФункции

Функция ТаблицаБанкОрганизация(Организации, Банк)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	БанковскиеСчета.Банк КАК Банк,
	|	БанковскиеСчета.Владелец КАК Организация
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец ССЫЛКА Справочник.Организации
	|	И БанковскиеСчета.Владелец В (&Организации)
	|	И &ОтборПоБанку
	|	И БанковскиеСчета.Банк <> ЗНАЧЕНИЕ(Справочник.Банки.ПустаяСсылка)
	|	И БанковскиеСчета.ВалютаДенежныхСредств = &ВалютаРеглУчета
	|	И БанковскиеСчета.ЦифровойСчет = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	БанковскиеСчета.Банк,
	|	БанковскиеСчета.Владелец");
	
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	Запрос.УстановитьПараметр("Организации", Организации);
			
	Если ЗначениеЗаполнено(Банк) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоБанку", "БанковскиеСчета.Банк = &Банк");
		Запрос.УстановитьПараметр("Банк", Банк);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоБанку", "Истина");
		
	КонецЕсли;
		
	ТаблицаБанков = Запрос.Выполнить().Выгрузить();
	
	// Для отображения баннеров в банковских счетах,
	// которые еще не записаны
	Если ТаблицаБанков.Количество() = 0 И Организации.Количество() = 1 И ЗначениеЗаполнено(Банк) Тогда
		
		СтрокаТаблицы = ТаблицаБанков.Добавить();
		СтрокаТаблицы.Организация = Организации[0];
		СтрокаТаблицы.Банк = Банк;
		
	КонецЕсли;

	Возврат ТаблицаБанков;
	
КонецФункции

Функция ДоступныеБанки(Организация, Банк, ИмяФормы) 
	
	Если ЭтоФормаБанковскогоСчета(ИмяФормы) Тогда
		Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Банк);
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	БанковскиеСчета.Банк КАК Банк
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец ССЫЛКА Справочник.Организации
	|	И &ОтборПоОрганизации
	|	И &ОтборПоБанку
	|	И БанковскиеСчета.Банк <> ЗНАЧЕНИЕ(Справочник.Банки.ПустаяСсылка)");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "БанковскиеСчета.Владелец В (&Организация)");
		Запрос.УстановитьПараметр("Организация", Организация);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоОрганизации", "Истина");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Банк) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоБанку", "БанковскиеСчета.Банк = &Банк");
		Запрос.УстановитьПараметр("Банк", Банк);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоБанку", "Истина");
		
	КонецЕсли;
	
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Банк");
	
КонецФункции

Функция УслугаПодключена(Запрос, СтрокаТаблицы, ТипОплаты)
	
	Организация = СтрокаТаблицы.Организация;
	Банк = СтрокаТаблицы.Банк;
	ИНН = СтрокаТаблицы.ИНН;
	Контрагенты = РегистрыСведений.ГруппыКомпанийБанков.КонтрагентыГруппыКомпанийБанка(ИНН);
	Запрос.УстановитьПараметр("ТипОплаты", ТипОплаты);
	Запрос.УстановитьПараметр("Организация", Организация);
    Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
    Запрос.УстановитьПараметр("Банк", Банк);

	РезультатЗапроса = Запрос.Выполнить();	
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

Функция ТаблицаСОтборомПоБанкуУниверсальногоОбмена(Таблица, БанкУниверсальногоОбмена)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Таблица.Банк КАК Справочник.Банки) КАК Банк,
	|	Таблица.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ТаблицаСоответствий
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаСоответствий.Банк КАК Банк,
	|	ВТ_ТаблицаСоответствий.Организация КАК Организация,
	|	КлассификаторБанков.ИНН КАК ИНН,
	|	КлассификаторБанков.Код КАК БИК
	|ИЗ
	|	ВТ_ТаблицаСоответствий КАК ВТ_ТаблицаСоответствий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторБанков КАК КлассификаторБанков
	|		ПО (КлассификаторБанков.ИНН В (&ИННБанкаУниверсальногоОбмена))
	|			И ВТ_ТаблицаСоответствий.Банк.Код = КлассификаторБанков.Код
	|			И ВТ_ТаблицаСоответствий.Банк.КоррСчет = КлассификаторБанков.КоррСчет
	|			И (НЕ КлассификаторБанков.ДеятельностьПрекращена)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаСоответствий.Банк,
	|	ВТ_ТаблицаСоответствий.Организация,
	|	КлассификаторБанков.ИНН,
	|	КлассификаторБанков.Код");
	
	РеквизитыБанков = УниверсальныйОбменСБанками.РеквизитыБанков(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(БанкУниверсальногоОбмена));
	ТаблицаИННГруппыКомпанийБанков = РегистрыСведений.ГруппыКомпанийБанков.ТаблицаИННГруппыКомпанийБанков(РеквизитыБанков);

	Запрос.УстановитьПараметр("Таблица", Таблица);
	Запрос.УстановитьПараметр("ИННБанкаУниверсальногоОбмена", ТаблицаИННГруппыКомпанийБанков.ВыгрузитьКолонку("ИНН"));  
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция ЗапросВидОплаты()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВидыОплатОрганизаций.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыОплатОрганизаций КАК ВидыОплатОрганизаций
		|ГДЕ
		|	ВидыОплатОрганизаций.ПометкаУдаления = ЛОЖЬ
		|	И ВидыОплатОрганизаций.ТипОплаты = &ТипОплаты
		|	И ВидыОплатОрганизаций.Организация = &Организация
		|	И ВидыОплатОрганизаций.Контрагент В(&Контрагенты)";
			
	Возврат Запрос;

КонецФункции

Функция ЗапросКорпоративныеКарты()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КорпоративныеКарты.НомерКарты КАК НомерКарты
		|ИЗ
		|	РегистрСведений.КорпоративныеКарты КАК КорпоративныеКарты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
		|		ПО КорпоративныеКарты.БанковскийСчет = БанковскиеСчета.Ссылка
		|ГДЕ
		|	КорпоративныеКарты.Организация = &Организация
		|	И БанковскиеСчета.Банк = &Банк
		|	И БанковскиеСчета.ПометкаУдаления = ЛОЖЬ";
		
	Возврат Запрос;
	
КонецФункции

Функция ЗапросЗарплатныйПроект()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗарплатныеПроекты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЗарплатныеПроекты КАК ЗарплатныеПроекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Банки КАК Банки
		|		ПО (Банки.Код = ЗарплатныеПроекты.Банк.Код)
		|			И (Банки.КоррСчет = ЗарплатныеПроекты.Банк.КоррСчет)
		|			И (ЗарплатныеПроекты.ПометкаУдаления = ЛОЖЬ)
		|			И (Банки.Ссылка = &Банк)
		|			И (ЗарплатныеПроекты.Организация = &Организация)";
		
	Возврат Запрос;
	
КонецФункции

Функция ЭтоФормаБанковскогоСчета(ИмяФормы)
	Возврат ИмяФормы = "Справочник.БанковскиеСчета.Форма.ФормаЭлемента";
КонецФункции

Функция НавигационнаяСсылкаСПараметрами(ИсходнаяНавигационнаяСсылка, СведенияОбОрганизациях)
	
	Если СведенияОбОрганизациях.Количество() = 1 Тогда   
		ИНН = СведенияОбОрганизациях[0].ИНН;
	Иначе 
		ИНН = ""; 
	КонецЕсли;
	
	Возврат СтрЗаменить(ИсходнаяНавигационнаяСсылка, "{ИННОрганизации}", ИНН);

КонецФункции

#КонецОбласти

#КонецЕсли
