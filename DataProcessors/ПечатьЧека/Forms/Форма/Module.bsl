
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем СообщениеОбОшибке;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ВалютаДокумента", Параметры.Ссылка.Метаданные()) 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ссылка, "ВалютаДокумента") <> ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета() Тогда
		
		ВызватьИсключение НСтр("ru='Печать чека при расчетах в иностранной валюте не поддерживается.'");
	КонецЕсли;
	
	Объект.СуммаВключаетНДС = Истина;
	
	ОснованиеПечатиЧека = Параметры.Ссылка;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ИдентификаторУстройства");
	
	ПараметрыУстройства = МенеджерОборудованияВызовСервера.ДанныеУстройства(Параметры.ИдентификаторУстройства);
	
	НомерСекции                = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыУстройства, "НомерСекции", 0);
	КодыСистемыНалогообложения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыУстройства.ПараметрыРегистрации, "КодыСистемыНалогообложения", "");

	Если ЗначениеЗаполнено(КодыСистемыНалогообложения) Тогда
		Элементы.СистемаНалогообложения.РежимВыбораИзСписка = Истина;
	
		Для каждого КодСистемыНалогообложения Из СтрРазделить(КодыСистемыНалогообложения, ",") Цикл
			Элементы.СистемаНалогообложения.СписокВыбора.Добавить(
				ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СистемаНалогообложенияККТПоКоду(Число(КодСистемыНалогообложения)));
		КонецЦикла;
	КонецЕсли;
	
	ДанныеОплатыСертификатНСПК = Новый Структура;
	ДанныеОплатыСертификатНСПК.Вставить("СуммаСертификатамиНСПК", Параметры.СуммаСертификатамиНСПК);
	ДанныеОплатыСертификатНСПК.Вставить("ИдентификаторКорзины", Параметры.ИдентификаторКорзины);
	
	
	МассивЧеков = ПечатьФискальныхДокументов.СобратьДанныеЧеков(
		ОснованиеПечатиЧека, 
		НомерСекции, 
		Параметры.ВерсияФормата.ВерсияФД, 
		Параметры.АдресУстановкиККТ,
		Параметры.ПараметрыОбработчика, 
		СообщениеОбОшибке,
		УникальныйИдентификатор,
		Параметры.АдресХранилищаПечатьЧека,
		ДанныеОплатыСертификатНСПК);
		
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		ТекстСообщения = НСтр("ru = 'При подготовке параметров печати чека произошла ошибка.
			|%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,
			"%ДополнительноеОписание%",
			СообщениеОбОшибке);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ,,,Отказ);
		Возврат;
	КонецЕсли; 
		
	ПараметрыОперацииФискализацииЧека = МассивЧеков[0];
	
	Если ЗначениеЗаполнено(ПараметрыОперацииФискализацииЧека.Организация) Тогда
		РеквизитыОрганизации = 
			БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
				ПараметрыОперацииФискализацииЧека.Организация, 
				ПараметрыОперацииФискализацииЧека.ДатаВремя);
				
		ОрганизацияСтрокой = РеквизитыОрганизации.Представление;
	КонецЕсли; 
	
	Для каждого ПозицияЧека Из ПараметрыОперацииФискализацииЧека.ПозицииЧека Цикл
		ПозицияЧека.Наименование = Лев(СокрЛП(ПозицияЧека.Наименование), 128);
	КонецЦикла; 
	
	ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ПараметрыОперацииФискализацииЧека, ИдентификаторУстройства);
	
	АдресХранилищаИсходныеПараметры = ПоместитьВоВременноеХранилище(ПараметрыОперацииФискализацииЧека, УникальныйИдентификатор);

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыОперацииФискализацииЧека, "СистемаНалогообложения, ПризнакАгента, ДанныеАгента, ДанныеПоставщика, Кассир, КассирИНН");
	
	ПризнакАгентаСтрокой = Обработки.ПечатьЧека.ПризнакАгентаПоПредметуРасчетаСтрокой(ПризнакАгента);
	
	КассирПредставление = ПредставлениеКассира(Кассир, КассирИНН);
	
	ЭтоВерсияФФД12  = НЕ ОбщегоНазначенияКлиентСервер.СравнитьВерсии("1.2.0.0", Параметры.ВерсияФормата.ВерсияФД) > 0;
	ЭтоВерсияФФД11  = НЕ ОбщегоНазначенияКлиентСервер.СравнитьВерсии("1.1.0.0", Параметры.ВерсияФормата.ВерсияФД) > 0;
	ЭтоВерсияФФД105 = НЕ ОбщегоНазначенияКлиентСервер.СравнитьВерсии("1.0.5.0", Параметры.ВерсияФормата.ВерсияФД) > 0;
	
	ВерсияККТ120    = НЕ ОбщегоНазначенияКлиентСервер.СравнитьВерсии("1.2.0.0", Параметры.ВерсияФормата.ВерсияККТ) > 0;;
	
	ЗаполнитьТаблицаТоваров();
			
	Для каждого СтрокаОплаты Из ПараметрыОперацииФискализацииЧека.ТаблицаОплат Цикл
		Если СтрокаОплаты.ТипОплаты = Перечисления.ТипыОплатыККТ.Наличные Тогда
			Наличные = СтрокаОплаты.Сумма;
		ИначеЕсли СтрокаОплаты.ТипОплаты = Перечисления.ТипыОплатыККТ.Электронно Тогда 
			Электронно = СтрокаОплаты.Сумма;
		ИначеЕсли СтрокаОплаты.ТипОплаты = Перечисления.ТипыОплатыККТ.ВстречноеПредоставление Тогда 
			ВстречноеПредоставление = СтрокаОплаты.Сумма;
		ИначеЕсли СтрокаОплаты.ТипОплаты = Перечисления.ТипыОплатыККТ.Предоплата Тогда 
			Предоплата = СтрокаОплаты.Сумма;
		ИначеЕсли СтрокаОплаты.ТипОплаты = Перечисления.ТипыОплатыККТ.Постоплата Тогда 
			Кредит = СтрокаОплаты.Сумма;
		КонецЕсли;
	КонецЦикла; 
	
	// Установим параметры печати чека
	ПараметрыПечатиЧека = МенеджерОборудованияБП.ПараметрыПодключаемогоОборудования(Параметры.ИдентификаторУстройства);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыПечатиЧека, "ОтправлятьEmail, ОтправлятьSMS, ПечататьЧек,Отправляет1СSMS, Отправляет1СEmail");
	
	АдресЭлектроннойПочты = ПараметрыОперацииФискализацииЧека.ПокупательEmail;
	НомерТелефона         = ПараметрыОперацииФискализацииЧека.ПокупательНомер;
	
	МестоРасчетов = Параметры.АдресУстановкиККТ;
	
	ЭтоОблачнаяККТ = ПараметрыПечатиЧека.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ;
	Если ПараметрыПечатиЧека.РасчетыВИнтернет 
		И Электронно <> 0 Тогда
		
		Элементы.МестоРасчетов.Видимость = Истина;
		
		Элементы.МестоРасчетов.СписокВыбора.Добавить(Ложь,   МестоРасчетов);
		Элементы.МестоРасчетов.СписокВыбора.Добавить(Истина, ПараметрыПечатиЧека.АдресСайта);
	КонецЕсли;
	
	ВсегоПоЧеку = Объект.ТаблицаТоваров.Итог("Сумма");
	
	Элементы.ГруппаПечататьЧек.Видимость = НЕ ЭтоОблачнаяККТ;
	ПечататьЧек = ПечататьЧек И НЕ ЭтоОблачнаяККТ;
	
	Элементы.Наличные.Видимость = (Наличные > 0);
	Элементы.ВстречноеПредоставление.Видимость = (ВстречноеПредоставление > 0);
	Элементы.Электронно.Видимость = (Электронно > 0);
	
	Элементы.Предоплата.Видимость = ЭтоВерсияФФД105;
	Элементы.Кредит.Видимость     = ЭтоВерсияФФД105;  
	
	ВидОперации = ПараметрыОперацииФискализацииЧека.ТипРасчета;
	
	ВидОперацииПриход = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;
	ВидОперацииРасход = Перечисления.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств;
	
	ДанныеФискальнойОперации = ОборудованиеЧекопечатающиеУстройстваВызовСервера.ДанныеФискальнойОперации(ОснованиеПечатиЧека,,,ПараметрыОперацииФискализацииЧека.ТипРасчета);
	Если ДанныеФискальнойОперации <> Неопределено Тогда
		// Чек из данного документа уже был пробит
		Если ЭтоОблачнаяККТ 
			И РегистрыСведений.ДанныеОперацийОблачныхКасс.ДанныеОперации(ОснованиеПечатиЧека) <> Неопределено Тогда
			
			// на облачных кассах нельзя пробить второй чек к существующему документу
			ВызватьИсключение НСтр("ru = 'Корректируемый чек был пробит на облачной кассе.
                                    |Повторное пробитие чеков на облачной кассе по тому же документу не поддерживается'");
		КонецЕсли;
		
		Объект.ЧекКоррекции                         = Истина;
		Объект.ДатаКоррекции                        = ДанныеФискальнойОперации.Дата;
		Объект.КорректируемыйДокумент               = ОснованиеПечатиЧека;
		Элементы.ЧекКоррекции.Видимость           = Ложь;
		Элементы.ПричинаКоррекции.Видимость           = Истина;
		Элементы.КорректируемыйДокумент.Видимость = Ложь;
		Заголовок = НСтр("ru = 'Чек коррекции'");
	Иначе
		// Для ФФД ниже 1.1 можно делать чек коррекции только для операций Приход и Расход.
		Элементы.ЧекКоррекции.Видимость = (ЭтоВерсияФФД11 
			ИЛИ ВидОперации = ВидОперацииПриход ИЛИ ВидОперации = ВидОперацииРасход);
			
	КонецЕсли;
		
	ПодготовитьСписокПодключаемогоОборудования();
	
	УстановитьУсловноеОформлениеВидимость();
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Обработка.ПечатьЧека",
		"Форма",
		НСтр("ru = 'Новости: Печать кассового чека'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры 

&НаСервере
Процедура ПодготовитьСписокПодключаемогоОборудования()
	// Типы оборудования которые надо подключать при открытии формы
	ТипыОборудования = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
	ТипыОборудования.СканерШтрихкода       = МенеджерОборудованияБП.ИспользуетсяОборудование("СканерШтрихкода");
	
	// ККТ при открытии формы подключаем только в случае маркировки, чтобы обеспечить неразрывность проверк марок и пробития чека в рамках одного РМ
	// без маркировки ККТ подключаем непосредственно в момент пробития чека, чтобы не блокировать работу сетевых касс
	ТипыОборудования.ККТ               = ШтрихкодыУпаковок.Количество() > 0;
	
	МенеджерОборудования.ПриСозданииНаСервере(ЭтаФорма, ТипыОборудования);
	
	СписокОборудования = ЭтотОбъект.ПодключаемоеОборудованиеБПО.СписокОборудования;
	КоличествоОборудования = СписокОборудования.Количество();
	Если КоличествоОборудования < 2 
		ИЛИ НЕ ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		
		Возврат;
	КонецЕсли;
	
	Для Поз = 1 По КоличествоОборудования Цикл
		Индекс = КоличествоОборудования - Поз;
		Если СписокОборудования[Индекс].ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ 
			И СписокОборудования[Индекс].Ссылка <> ИдентификаторУстройства Тогда
		
			СписокОборудования.Удалить(Индекс);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ПараметрыОперацииФискализацииЧека.ТаблицаОплат.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Чек не содержит данных.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	ОбновитьПредставлениеЧека();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			Если Параметр[1] = Неопределено Тогда
				ТекущийКод = Параметр[0];
			Иначе
				ТекущийКод = Параметр[1][1];
			КонецЕсли;
			
			ДанныеОбъекта = Новый Структура("АдресЭлектроннойПочты, ОтправлятьEmail, НомерТелефона, ОтправлятьSMS");
			ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ЭтотОбъект);
			ПечатьФискальныхДокументовКлиент.ЗаполнитьКонтактнуюИнформациюПоШтрихкоду(ДанныеОбъекта, ТекущийКод);
			ЗаполнитьЗначенияСвойств(Объект, ДанныеОбъекта);
			
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если НЕ ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗакрытииЗавершение", ЭтотОбъект);
		
		ТекстПредупреждения = НСтр("ru = 'Вы уверены, что хотите выйти без печати чека?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстПредупреждения, РежимДиалогаВопрос.ДаНет, 60);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли; 
	
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("РегистрСведенийКлючЗаписи.ФискальныеОперации") Тогда
		Если ЕстьИсправленияПоДокументу(ВыбранноеЗначение) Тогда
			
			ТекстВопроса = НСтр("ru = 'К выбранному документу уже  был пробит чек коррекции. Хотите пробить еще один?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("КорректируемыйДокументПриИзмененииЗавершение", ЭтотОбъект, Новый структура("КлючЗаписи", ВыбранноеЗначение));
		
			ПоказатьВопрос(ОписаниеОповещения,  ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			УстановитьДокументНаСервере(ВыбранноеЗначение);
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	ЗаполнитьЗначенияСвойств(ПараметрыОперацииФискализацииЧека, ЭтотОбъект, "СистемаНалогообложения");
	
	ОбновитьПредставлениеЧека();
КонецПроцедуры 



&НаКлиенте
Процедура КассирНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	РедактированиеРеквизитовКассира();
КонецПроцедуры

&НаКлиенте
Процедура ПредоплатаПриИзменении(Элемент)
	ЗаполнитьТаблицуОплат();
	
	ОбновитьПредставлениеЧека();
КонецПроцедуры

&НаКлиенте
Процедура ОтправлятьEmailПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтправлятьSMSПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АдресЭлектроннойПочтыПриИзменении(Элемент)
	ОтправлятьEmail = ЗначениеЗаполнено(АдресЭлектроннойПочты);
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаПриИзменении(Элемент)
	ОтправлятьSMS = ЗначениеЗаполнено(НомерТелефона);
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КорректируемыйДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("Отбор", Новый Структура("ТипРасчета", ПараметрыОперацииФискализацииЧека.ТипРасчета));
	ОткрытьФорму("РегистрСведений.ФискальныеОперации.Форма.ФормаПросмотраФискальныхОпераций", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЧекКоррекцииПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВидКоррекцииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Объект.КоррекцияПоПредписанию = (ВыбранноеЗначение = 1);
	Объект.КоррекцияНепринятогоЧека = (ВыбранноеЗначение = 2);
	
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ДобавитьСтрокуОплаты(ТипОплаты, СуммаОплаты)
	Если СуммаОплаты = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаОплаты = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыСтрокиОплаты();
	
	СтрокаОплаты.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ."+ТипОплаты);
	СтрокаОплаты.Сумма     = СуммаОплаты;
	
	ПараметрыОперацииФискализацииЧека.ТаблицаОплат.Добавить(СтрокаОплаты);
КонецПроцедуры 

&НаКлиенте
Процедура ТаблицаТоваровПриИзменении(Элемент)
	ПараметрыОперацииФискализацииЧека.ПозицииЧека = Новый Массив;
	
	Для каждого СтрокаТаблицыТоваров Из Объект.ТаблицаТоваров Цикл
		Если СтрокаТаблицыТоваров.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаПозицииЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыФискальнойСтрокиЧека();
		
		ЗаполнитьЗначенияСвойств(СтрокаПозицииЧека, СтрокаТаблицыТоваров,, "СтавкаНДС");
		СтрокаПозицииЧека.СтавкаНДС         = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДСдляККТ(СтрокаТаблицыТоваров.СтавкаНДС, ЭтоВерсияФФД105);
		СтрокаПозицииЧека.НомерСтрокиТовара = СтрокаТаблицыТоваров.НомерСтроки;
		
		ПараметрыОперацииФискализацииЧека.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	КонецЦикла;
	
	ВсегоПоЧеку = Объект.ТаблицаТоваров.Итог("Сумма");
	
	ЗаполнитьТаблицуОплат();
	
	ОбновитьПредставлениеЧека();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущиеДанные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	Если НоваяСтрока И НЕ Копирование Тогда
		ТекущиеДанные.ПризнакАгентаПоПредметуРасчета = ПараметрыОперацииФискализацииЧека.ПризнакАгента;
		ТекущиеДанные.ПризнакАгентаСтрокой           = ПризнакАгентаПоПредметуРасчетаСтрокой(ТекущиеДанные.ПризнакАгентаПоПредметуРасчета);
		ТекущиеДанные.ДанныеАгента                   = ПараметрыОперацииФискализацииЧека.ДанныеАгента;
		ТекущиеДанные.ДанныеПоставщика               = ПараметрыОперацииФискализацииЧека.ДанныеПоставщика;
	ИначеЕсли Копирование Тогда
		 ТекущиеДанные.Штрихкод = "";
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровПередУдалением(Элемент, Отказ)
	ТекущиеДанные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	Отказ = ЗначениеЗаполнено(ТекущиеДанные.Штрихкод);
	
	Если Отказ Тогда
		Текст = НСтр("ru = 'В форме печати чека нельзя удалять строки с маркируемыми товарами.
                                |При необходимости, удалите строку в исходном документе и сформируйте чек еще раз'");
		
		ПоказатьПредупреждение(,Текст,15);
		
	КонецЕсли; 
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаТоваровЦенаСоСкидкамиПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	СтрокаТаблицы.Цена = СтрокаТаблицы.ЦенаСоСкидками;
	СтрокаТаблицы.СуммаСкидок = 0;
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "ТаблицаТоваров");
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровКоличествоПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииКоличествоЦена(ЭтотОбъект, "ТаблицаТоваров");
	
	СтрокаТаблицы.ЦенаСоСкидками = СтрокаТаблицы.Цена;
	СтрокаТаблицы.СуммаСкидок = 0;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровСуммаПриИзменении(Элемент)
	СтрокаТаблицы = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "ТаблицаТоваров");
	
	СтрокаТаблицы.ЦенаСоСкидками = СтрокаТаблицы.Цена;
	СтрокаТаблицы.СуммаСкидок = 0;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровСтавкаНДСПриИзменении(Элемент)
	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСтавкаНДС(ЭтотОбъект, "ТаблицаТоваров");
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаТоваровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ТаблицаТоваровПризнакАгентаСтрокой" Тогда
		ОткрытьФормуРеквизитыАгента(Элемент.ТекущиеДанные);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПризнакАгентаНажатие(Элемент, СтандартнаяОбработка)
	ОткрытьФормуРеквизитыАгента(ЭтотОбъект);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровНомерТаможеннойДекларацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОповещениеЗавершение = Новый ОписаниеОповещения("ВыборТаможеннойДекларацииЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элементы.ТаблицаТоваров.ТекущиеДанные));
	ОткрытьФорму("Справочник.НомераГТД.ФормаВыбора",,ЭтотОбъект,,,,ОповещениеЗавершение);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаТоваровСтранаПроисхожденияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	
	КодСтраныПроисхожденияТовара = КодСтраныПроисхождения(ТекущиеДанные.СтранаПроисхождения);
	ПозицияЧека = ПараметрыОперацииФискализацииЧека.ПозицииЧека[ТекущиеДанные.НомерСтроки-1];
	Если ТипЗнч(ПозицияЧека) = Тип("Структура") Тогда
		ПозицияЧека.Вставить("КодСтраныПроисхожденияТовара", КодСтраныПроисхожденияТовара);
		ТекущиеДанные.КодСтраныПроисхожденияТовара = КодСтраныПроисхожденияТовара;
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПечатьЧека(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли; 
	
	Доступность = Ложь; // Блокируем пользовательский интерфейс на время работы с оборудованием

	
	Если ШтрихкодыУпаковок.Количество() > 0 Тогда
		ПараметрыНачалаПроверки 							= ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ПараметрыНачалаПроверкиКодовМаркировкиСредствамиККТ();
		ПараметрыНачалаПроверки.ОповещениеОЗавершении       = Новый ОписаниеОповещения("ПроверкаКодовМаркировкиЗавершение", ЭтотОбъект);
		ПараметрыНачалаПроверки.ДанныеДляПроверки           = Новый Массив(ДанныеДляПроверкиКодовМаркировки);
		ПараметрыНачалаПроверки.ФормаОсновногоОбъекта       = ВладелецФормы;
		ПараметрыНачалаПроверки.ФормаВспомогательная        = ЭтотОбъект;
		ПараметрыНачалаПроверки.ЗаголовокКнопкиИгнорировать = НСтр("ru = 'Пробить чек'");
		
		ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования(ВладелецФормы);
		Если НЕ ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ 
			ИЛИ ПараметрыСканирования.ККТФФД12ИСМП <> ИдентификаторУстройства Тогда
			
			Если ОборудованиеЧекопечатающиеУстройстваВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства) Тогда
				ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ   = Истина;
				ПараметрыСканирования.ККТФФД12ИСМП                     = ИдентификаторУстройства;
				
				ПараметрыРегистрации = МенеджерОборудованияБПВызовСервера.ПараметрыРегистрацииУстройства(ИдентификаторУстройства);
				ПараметрыСканирования.НомерФискальногоНакопителя = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыРегистрации, "ЗаводскойНомерФН");
			КонецЕсли;
		КонецЕсли;
		
		// Для включения разрешительного режима
		Если ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ Тогда
			ПараметрыСканирования.ВидОперацииИСМП = ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ВыводИзОборотаРозничнаяПродажа");
		КонецЕсли;
		
		ПараметрыНачалаПроверки.ПараметрыСканирования = ПараметрыСканирования;
		
		ШтрихкодированиеОбщегоНазначенияИСМПКлиент.НачатьПроверкуКодовМаркировкиСредствамиККТ(ПараметрыНачалаПроверки);
	Иначе
		ПечатьЧекаНаФискальномУстройстве();
	КонецЕсли; 
КонецПроцедуры 
	
&НаКлиенте
Процедура ПроверкаКодовМаркировкиЗавершение(РезультатПроверки, Параметры = Неопределено) Экспорт
 	Если НЕ РезультатПроверки.ВыполнитьФискализацию Тогда
		ОписаниеОшибок = Новый Массив;
		Если ЗначениеЗаполнено(РезультатПроверки.ТекстОшибки) Тогда
			ОписаниеОшибок.Добавить(РезультатПроверки.ТекстОшибки);
		КонецЕсли;
		Если ЗначениеЗаполнено(РезультатПроверки.ТекстОшибкиГИСМТ) Тогда
			ОписаниеОшибок.Добавить(РезультатПроверки.ТекстОшибкиГИСМТ);
		КонецЕсли;
		
		РезультатВыполнения = Новый Структура;
		РезультатВыполнения.Вставить("Результат",      Ложь);
		РезультатВыполнения.Вставить("ОписаниеОшибки", СтрСоединить(ОписаниеОшибок, символы.ПС));
		РезультатВыполнения.Вставить("ЗакрытьФорму",   РезультатПроверки.ЗакрытьВспомогательнуюФорму);
		
		ПечатьЧекаНаФискальномУстройствеЗавершение(РезультатВыполнения);
		Возврат;
	КонецЕсли;
	
	// Заполнение идентификаторов разрешительного режима
	Для каждого ЭлементПроверки Из РезультатПроверки.ЭлементыПроверки Цикл
		ДанныеПроверки = РезультатПроверки.ДанныеПроверки[ЭлементПроверки.ИдентификаторЭлемента];
		Если ДанныеПроверки = Неопределено 
			ИЛИ НЕ ДанныеПроверки.ТребуетсяРазрешительныйЗапросГИСМТ Тогда
		
			Продолжить;
		КонецЕсли;
		
		НайденныеСтроки = ШтрихкодыУпаковок.НайтиСтроки(Новый Структура("КонтрольнаяМарка", ЭлементПроверки.ПолныйКодМаркировки));
		Если НайденныеСтроки.Количество() = 1 Тогда
			ЗаполнитьЗначенияСвойств(НайденныеСтроки[0], ЭлементПроверки, 
				"РазрешительныйРежимИдентификаторЗапросаГИСМТ, РазрешительныйРежимДатаЗапросаГИСМТ, РазрешительныйРежимИдентификаторЭкземпляраПО, РазрешительныйРежимВерсияБазы");
		КонецЕсли;
	КонецЦикла;
	
	ШтрихкодированиеИСМПВызовСервера.ОчиститьРезультатыПроверкиСредствамиККТ(ОснованиеПечатиЧека);
	
	ПечатьЧекаНаФискальномУстройстве();
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧекаНаФискальномУстройстве()
	ПараметрыОперации = ПараметрыОперацииФискализацииЧека;
	Если ШтрихкодыУпаковок.Количество() > 0 Тогда
		ПараметрыОперации = ПараметрыОперацииФискализацииЧекаСоШтрихкодами();
	КонецЕсли;
	
	// Для ФФД 1.2 принудительно очистим данные агента и данные поставщика в шапке если они введены вручную
	Если ЭтоВерсияФФД12 
		И ЗначениеЗаполнено(ПараметрыОперации.ПризнакАгента) Тогда
		
		ПараметрыОперации.Вставить("ПризнакАгента"   , Неопределено);
		ПараметрыОперации.Вставить("ДанныеАгента"    , ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеАгента());
		ПараметрыОперации.Вставить("ДанныеПоставщика", ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеПоставщика());
	КонецЕсли; 
	
	Если ОтправлятьEmail Тогда
		ПараметрыОперации.ПокупательEmail = АдресЭлектроннойПочты;
		ПараметрыОперации.Отправляет1СEmail = Отправляет1СEmail;
	Иначе
		ПараметрыОперации.ПокупательEmail = "";
	КонецЕсли;
	
	Если ОтправлятьSMS Тогда
		ПараметрыОперации.ПокупательНомер = НомерТелефона;
		ПараметрыОперации.Отправляет1СSMS = Отправляет1СSMS;
	Иначе
		ПараметрыОперации.ПокупательНомер = "";
	КонецЕсли; 
	
	Если ОтправлятьEmail ИЛИ ОтправлятьSMS Тогда
		ПараметрыОперации.Электронно = НЕ ПечататьЧек;
	КонецЕсли; 
	
	// То что печать будет на облачной ККТ в момент подготовки чека еще не известно, 
	// устанавливаем признак здесь
	ПараметрыОперации.ОблачнаяККТ = ЭтоОблачнаяККТ;
	
	Оповещение = Новый ОписаниеОповещения("ПечатьЧекаНаФискальномУстройствеЗавершение", ЭтотОбъект);
	Если Объект.ЧекКоррекции Тогда
		
		ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФормированиеЧекаКоррекцииНаФискальномУстройстве(Оповещение, 
			УникальныйИдентификатор, 
			ИдентификаторУстройства,
			ДанныеКоррекции(ПараметрыОперации));
		
	Иначе
		ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(Оповещение, 
			УникальныйИдентификатор, 
			ИдентификаторУстройства,
			ПараметрыОперации);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеКоррекции(ПараметрыОперации)
	ДанныеКоррекции = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеКоррекции();
	
	ДанныеКоррекции.ТипКоррекции      = ?(Объект.КоррекцияПоПредписанию, 1, 0);
	ДанныеКоррекции.НомерПредписания  = ?(Объект.КоррекцияПоПредписанию, Объект.НомерПредписания, "0");
	ДанныеКоррекции.ОписаниеКоррекции = Объект.ОписаниеКоррекции;
	ДанныеКоррекции.ДатаКоррекции     = Объект.ДатаКоррекции;
	
	ПараметрыЧекаКоррекции = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииЧекаКоррекции();
	
	ЗаполнитьЗначенияСвойств(ПараметрыЧекаКоррекции.ДанныеКоррекции, ДанныеКоррекции);
	ЗаполнитьЗначенияСвойств(ПараметрыЧекаКоррекции, ПараметрыОперации);
	
	ПараметрыЧекаКоррекции.КорректируемыйДокумент = Объект.КорректируемыйДокумент;
	ПараметрыЧекаКоррекции.НеприменениеККТ        = НЕ ЗначениеЗаполнено(Объект.КорректируемыйДокумент);
	
	// Для коррекции чеков, не принятых ФНС - сторно делать не надо
	// см. письмо ФНС N АБ-4-20/15520@ от 12.12.2023 
	ПараметрыЧекаКоррекции.СформироватьЧекКоррекцииСторно = НЕ Объект.КоррекцияНепринятогоЧека;
	
	Возврат ПараметрыЧекаКоррекции;
КонецФункции 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДокументНаСервере(КлючЗаписи)

	Объект.КорректируемыйДокумент = КлючЗаписи.ДокументОснование;

КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьТаблицаТоваров()
	СтранаПроисхожденияПоКоду = Новый Соответствие;
	ДанныеДляПроверкиСредствамиККТ = Новый Массив;

	
	Объект.ТаблицаТоваров.Очистить();
	
	ЕстьИнформацияОбАгентахПоСтрокам = Ложь;
	
	Для каждого ПозицияЧека Из ПараметрыОперацииФискализацииЧека.ПозицииЧека Цикл
		НоваяСтрока = Объект.ТаблицаТоваров.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ПозицияЧека,,"СтавкаНДС");
		
		Если ЗначениеЗаполнено(ПозицияЧека.ПризнакАгентаПоПредметуРасчета) 
			И (ПозицияЧека.ПризнакАгентаПоПредметуРасчета <> ПараметрыОперацииФискализацииЧека.ПризнакАгента
			ИЛИ СравнитьСтруктурыРекурсивно(ПозицияЧека.ДанныеАгента, ПараметрыОперацииФискализацииЧека.ДанныеАгента).Количество() <> 0
			ИЛИ СравнитьСтруктурыРекурсивно(ПозицияЧека.ДанныеПоставщика, ПараметрыОперацииФискализацииЧека.ДанныеПоставщика).Количество() <> 0) Тогда
		
			ЕстьИнформацияОбАгентахПоСтрокам = Истина;
			
		КонецЕсли; 
		
		НоваяСтрока.ПризнакАгентаСтрокой = 
			ПризнакАгентаПоПредметуРасчетаСтрокой(НоваяСтрока.ПризнакАгентаПоПредметуРасчета);
			
		Если ЗначениеЗаполнено(ПозицияЧека.КодСтраныПроисхожденияТовара) Тогда
			СтранаПроисхожденияСсылка = СтранаПроисхожденияПоКоду[ПозицияЧека.КодСтраныПроисхожденияТовара];
			
			Если НЕ ЗначениеЗаполнено(СтранаПроисхожденияСсылка) Тогда
				ДанныеСтраныПроисхождения = УправлениеКонтактнойИнформацией.ДанныеСтраныМира(ПозицияЧека.КодСтраныПроисхожденияТовара);
				
				СтранаПроисхожденияСсылка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСтраныПроисхождения, "Ссылка");
				Если ЗначениеЗаполнено(СтранаПроисхожденияСсылка) Тогда
					СтранаПроисхожденияПоКоду.Вставить(ПозицияЧека.КодСтраныПроисхожденияТовара, СтранаПроисхожденияСсылка);
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтранаПроисхожденияСсылка) Тогда
				НоваяСтрока.СтранаПроисхождения = СтранаПроисхожденияСсылка;
			КонецЕсли;
		КонецЕсли;
		
		НоваяСтрока.ИндексАкцизнойМарки = ?(ЗначениеЗаполнено(ПозицияЧека.Штрихкод), 1, 0);
		
		НоваяСтрока.СтавкаНДС = УчетНДС.СтавкаНДСПоЧислу(ПозицияЧека.СтавкаНДС);
		
		Если ЗначениеЗаполнено(ПозицияЧека.Штрихкод) И ЭтоАдресВременногоХранилища(ПозицияЧека.Штрихкод) Тогда
		
			МассивШтрихкодов = ПолучитьИзВременногоХранилища(ПозицияЧека.Штрихкод);
			Для каждого СтруктураШтрихкода Из МассивШтрихкодов Цикл
				СтрокаТаблицаШтрихкоды = ШтрихкодыУпаковок.Добавить();
				
				// В качестве ключа используем адрес временного хранилища
				СтрокаТаблицаШтрихкоды.КлючСтроки = ПозицияЧека.Штрихкод;
				
				// обработка кода маркировки в соотвествии с ФФД
				
				// ККТ и ФН - работают по любому ФФД
				СтрокаТаблицаШтрихкоды.Штрихкод = СтруктураШтрихкода.Штрихкод;
				
				// теги для ОСУ
				СтрокаТаблицаШтрихкоды.ОбъемноСортовойУчет = 
					СтруктураШтрихкода.ВидУпаковки = Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет;
					
				// количество упаковок весового товара
				Если СтрокаТаблицаШтрихкоды.ОбъемноСортовойУчет Тогда
					СтрокаТаблицаШтрихкоды.КоличествоВесовогоТовара = СтруктураШтрихкода.СоставКодаМаркировки.КоличествоВложенныхЕдиниц;
				КонецЕсли;
				// Фактическое количество товара (вес или упаковки) для печати чека по ОСУ
				// для остальных товаров - в чек не выводится и 
				// используется только для расчета цены в процедуре ПараметрыОперацииФискализацииЧекаСоШтрихкодами
				СтрокаТаблицаШтрихкоды.Количество = СтруктураШтрихкода.Количество;
				
				Если ВерсияККТ120 Тогда
					// ККТ работает по 1.2, ФН - любой
					СтрокаТаблицаШтрихкоды.ШтрихкодBase64 = СтруктураШтрихкода.ПолныйКодМаркировки;
					
					Если ЭтоВерсияФФД12 Тогда
						// И ККТ и ФН работают по 1.2
						СтрокаТаблицаШтрихкоды.КонтрольнаяМарка = СтруктураШтрихкода.ПолныйКодМаркировки;
					КонецЕсли; 
				КонецЕсли;
				
				СтрокаТаблицаШтрихкоды.ЧастичноеВыбытие = СтруктураШтрихкода.ЧастичноеВыбытие;
				
				ЭлементДанных = ШтрихкодированиеОбщегоНазначенияИСМПКлиентСервер.НовыйЭлементДанныхПроверкиСредствамиККТ();
				ЭлементДанных.ВидПродукции         = СтруктураШтрихкода.ВидПродукции;
				ЭлементДанных.ВидУпаковки          = СтруктураШтрихкода.ВидУпаковки;
				ЭлементДанных.КодМаркировки        = СтруктураШтрихкода.КодМаркировки;
				ЭлементДанных.ПолныйКодМаркировки  = СтруктураШтрихкода.ПолныйКодМаркировки;
				ЭлементДанных.СоставКодаМаркировки = СтруктураШтрихкода.СоставКодаМаркировки;
				ЭлементДанных.ШтрихкодУпаковки     = СтруктураШтрихкода.ШтрихкодУпаковки;
				
				ДанныеДляПроверкиСредствамиККТ.Добавить(ЭлементДанных);
			КонецЦикла;
		КонецЕсли; 
	КонецЦикла; 
	
	ДанныеДляПроверкиКодовМаркировки = Новый ФиксированныйМассив(ДанныеДляПроверкиСредствамиККТ);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьДанныеБизнесСтатистики(ИзмененнаяСтруктура, АдресХранилищаИсходныеПараметры)
	Если НЕ ЦентрМониторинга.ЗаписыватьОперацииБизнесСтатистики() Тогда
		Возврат;
	КонецЕсли; 
	
	ИсходнаяСтруктура   = ПолучитьИзВременногоХранилища(АдресХранилищаИсходныеПараметры);
	
	ИзмененныеРеквизиты = СравнитьСтруктурыРекурсивно(ИсходнаяСтруктура, ИзмененнаяСтруктура, "НомерЧека");
	
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики("Обработка.ПечатьЧека.НапечатанЧек", 1);
	Если ИзмененныеРеквизиты.Количество() > 0 Тогда
		ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики("Обработка.ПечатьЧека.ИзмененыРеквизиты", ИзмененныеРеквизиты.Количество());
		Для каждого ИзмененныйРеквизит Из ИзмененныеРеквизиты Цикл
			ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики("Обработка.ПечатьЧека.ИзмененРеквизит."+ИзмененныйРеквизит, 1);
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция СравнитьСтруктурыРекурсивно(ИсходнаяСтруктура, ИзмененнаяСтруктура, ИсключаемыеРеквизиты = "")
	ИзмененныеРеквизиты = Новый Массив;
	Для каждого КлючИЗначение Из ИсходнаяСтруктура Цикл
		ИмяРеквизита     = КлючИЗначение.Ключ;
		
		МассивИсключаемыеРеквизиты = СтрРазделить(ИсключаемыеРеквизиты, ",");
		Если МассивИсключаемыеРеквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		ИсходноеЗначение = КлючИЗначение.Значение;
		НовоеЗначение    = ИзмененнаяСтруктура[ИмяРеквизита];
		Если ТипЗнч(ИсходноеЗначение) = Тип("Структура") Тогда
			ИзмененныеРеквизитыСтруктуры = СравнитьСтруктурыРекурсивно(ИсходноеЗначение, НовоеЗначение, ИсключаемыеРеквизиты);
			Для каждого ИмяИзмененногоРеквизитаСтруктуры Из ИзмененныеРеквизитыСтруктуры Цикл
				ИзмененныеРеквизиты.Добавить(СтрШаблон("%1.%2", ИмяРеквизита, ИмяИзмененногоРеквизитаСтруктуры));
			КонецЦикла;
		ИначеЕсли ТипЗнч(КлючИЗначение.Значение) = Тип("Массив") Тогда
			
			Если ИсходноеЗначение.Количество() <> НовоеЗначение.Количество() Тогда
				// Если в массивах разное количество элементов то построчно сравнивать смысла нет
				ИзмененныеРеквизиты.Добавить(ИмяРеквизита);
			Иначе
				Для ПозицияМассива = 0 По ИсходноеЗначение.Количество()-1 Цикл
					ИзмененныеРеквизитыСтруктуры = СравнитьСтруктурыРекурсивно(ИсходноеЗначение[ПозицияМассива], НовоеЗначение[ПозицияМассива], ИсключаемыеРеквизиты);
					Для каждого ИзмененныйРеквизит Из ИзмененныеРеквизитыСтруктуры Цикл
						ИзмененныеРеквизиты.Добавить(СтрШаблон("%1[%2].%3", ИмяРеквизита, ПозицияМассива, ИзмененныйРеквизит));
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
		ИначеЕсли (ЗначениеЗаполнено(НовоеЗначение) ИЛИ ЗначениеЗаполнено(ИсходноеЗначение)) И НовоеЗначение <> ИсходноеЗначение Тогда 
			ИзмененныеРеквизиты.Добавить(ИмяРеквизита);
		КонецЕсли; 
	КонецЦикла; 

	Возврат ИзмененныеРеквизиты;
КонецФункции 

&НаКлиенте
Процедура ЗаполнитьТаблицуОплат()

	Кредит = Макс(ВсегоПоЧеку - Предоплата - Наличные - ВстречноеПредоставление - Электронно, 0);
	
	ПараметрыОперацииФискализацииЧека.ТаблицаОплат = Новый Массив;
	
	ДобавитьСтрокуОплаты("Предоплата",              ВсегоПоЧеку - Кредит - Наличные - Электронно - ВстречноеПредоставление);
	ДобавитьСтрокуОплаты("ВстречноеПредоставление", ВстречноеПредоставление);
	ДобавитьСтрокуОплаты("Наличные",                Наличные);
	ДобавитьСтрокуОплаты("Электронно",              Электронно);
	ДобавитьСтрокуОплаты("Постоплата",              Кредит);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеКассира(Кассир, КассирИНН)

	Если ПустаяСтрока(Кассир) Тогда
		Возврат "< .. >";
	КонецЕсли; 
	
	ПредставлениеКассира = Новый Массив;
	ПредставлениеКассира.Добавить(Кассир);
	Если ЗначениеЗаполнено(КассирИНН) Тогда
		ПредставлениеКассира.Добавить(СтрШаблон("(ИНН: %1)", КассирИНН));
	КонецЕсли;
	
	Возврат СтрСоединить(ПредставлениеКассира, " ");
КонецФункции 

&НаСервере
Процедура УстановитьУсловноеОформлениеВидимость()
	
	УсловноеОформление.Элементы.Очистить();
	
	// ТаблицаТоваровКоличество
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаТоваровКоличество");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ТаблицаТоваров.ИндексАкцизнойМарки", ВидСравненияКомпоновкиДанных.Равно, 1);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
	// ТаблицаТоваровПризнакАгентаСтрокой
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТаблицаТоваровПризнакАгентаСтрокой");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЭтоВерсияФФД105", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// АдресПокупателя
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "АдресЭлектроннойПочты");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтправлятьEmail", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"АдресЭлектроннойПочты", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// АдресПокупателя
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НомерТелефона");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтправлятьSMS", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"НомерТелефона", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// НомерПредписания
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НомерПредписания");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.КоррекцияПоПредписанию", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НомерПредписания", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// ДатаКоррекции
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДатаКоррекции");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.КорректируемыйДокумент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ДатаКоррекции", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// КорректируемыйДокумент
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректируемыйДокумент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.КорректируемыйДокумент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.КоррекцияНепринятогоЧека", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧекаНаФискальномУстройствеЗавершение(РезультатВыполнения, ДополнительныеПараметры = Неопределено) Экспорт
	
	Доступность = Истина; // Разблокируем пользовательский интерфейс
	
	НастройкиОтправкиСообщений = Новый Структура("ОтправлятьEmail, ОтправлятьSMS, ПечататьЧек");
	ЗаполнитьЗначенияСвойств(НастройкиОтправкиСообщений, ЭтотОбъект);
	
	ОбщегоНазначенияКлиент.ХранилищеНастроекДанныхФормСохранить("ПечатьЧека", ИдентификаторНастроек, НастройкиОтправкиСообщений);
	
	Если РезультатВыполнения.Результат Тогда
		Модифицированность = Ложь;
		ЗаписатьДанныеБизнесСтатистики(ПараметрыОперацииФискализацииЧека, АдресХранилищаИсходныеПараметры);
		РезультатВыполнения.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
		Если НЕ РезультатВыполнения.Свойство("ПараметрыОперации") Тогда
			РезультатВыполнения.Вставить("ПараметрыОперации", ПараметрыОперацииФискализацииЧека);
		КонецЕсли;
		Закрыть(РезультатВыполнения);
		
	ИначеЕсли РезультатВыполнения.Свойство("ЗакрытьФорму") И РезультатВыполнения.ЗакрытьФорму Тогда
		
		Закрыть(РезультатВыполнения);
		
	Иначе
		ТекстСообщения = НСтр("ru = 'При печати чека произошла ошибка.
			|Чек не напечатан на фискальном устройстве.
			|Дополнительное описание:
			|%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения,
			"%ДополнительноеОписание%",
			РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыОперацииФискализацииЧекаСоШтрихкодами()
	НовыеПараметрыФискализации = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПараметрыОперацииФискализацииЧека);
	
	ПозицииЧека =  НовыеПараметрыФискализации.ПозицииЧека;
	
	ПозицииЧека.Очистить();
	Для каждого СтрокаТовары Из ПараметрыОперацииФискализацииЧека.ПозицииЧека Цикл
		Если ЗначениеЗаполнено(СтрокаТовары.Штрихкод) Тогда
		
			ШтрихкодыПоТовару = ШтрихкодыУпаковок.НайтиСтроки(Новый Структура("КлючСтроки", СтрокаТовары.Штрихкод));
			
			ОстатокСумма      = СтрокаТовары.Сумма;
			ОстатокСуммаНДС   = СтрокаТовары.СуммаНДС;
			ОстатокКоличество = СтрокаТовары.Количество;
			
			Для каждого СтрокаШтрихкода Из ШтрихкодыПоТовару Цикл
				
				ПозицияЧека = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(СтрокаТовары);
				
				ПозицияЧека.Штрихкод = СтрокаШтрихкода.Штрихкод;
				
				// Если параметр передавать не надо - в нем должно быть НЕОПРЕДЕЛЕНО, а не пустая строка
				Если ЗначениеЗаполнено(СтрокаШтрихкода.ШтрихкодBase64) Тогда
					ПозицияЧека.ШтрихкодBase64 = СтрокаШтрихкода.ШтрихкодBase64;
				КонецЕсли; 
				
				Если ЗначениеЗаполнено(СтрокаШтрихкода.КонтрольнаяМарка) Тогда
					ПозицияЧека.КонтрольнаяМарка = СтрокаШтрихкода.КонтрольнаяМарка;
				КонецЕсли; 
				
				Если ЗначениеЗаполнено(СтрокаШтрихкода.РазрешительныйРежимИдентификаторЗапросаГИСМТ) Тогда
					ПозицияЧека.ЗапросПроверкиКода.ИдентификаторЗапроса = СтрокаШтрихкода.РазрешительныйРежимИдентификаторЗапросаГИСМТ;
				КонецЕсли; 
				
				Если ЗначениеЗаполнено(СтрокаШтрихкода.РазрешительныйРежимДатаЗапросаГИСМТ) Тогда
					ПозицияЧека.ЗапросПроверкиКода.ВременнаяМетка = СтрокаШтрихкода.РазрешительныйРежимДатаЗапросаГИСМТ;
				КонецЕсли; 
				
				Если ЗначениеЗаполнено(СтрокаШтрихкода.РазрешительныйРежимИдентификаторЭкземпляраПО) Тогда
					ПозицияЧека.ЗапросПроверкиКода.ИдентификаторЭкземпляра = СтрокаШтрихкода.РазрешительныйРежимИдентификаторЭкземпляраПО;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаШтрихкода.РазрешительныйРежимВерсияБазы) Тогда
					ПозицияЧека.ЗапросПроверкиКода.ВерсияБазы = СтрокаШтрихкода.РазрешительныйРежимВерсияБазы;
				КонецЕсли;
				
				Если СтрокаШтрихкода.ОбъемноСортовойУчет Тогда
					ПозицияЧека.ОбъемноСортовойУчет = Истина;
					ПозицияЧека.КоличествоВесовогоТовара = СтрокаШтрихкода.КоличествоВесовогоТовара;
				КонецЕсли; 
				
				Если ПозицияЧека.Количество <> СтрокаШтрихкода.Количество 
					И СтрокаШтрихкода.ОбъемноСортовойУчет Тогда
					
					ПозицияЧека.Количество     = СтрокаШтрихкода.Количество;
				ИначеЕсли НЕ СтрокаШтрихкода.ОбъемноСортовойУчет Тогда
					// Для маркируемых товаров неподконтрольных ОСУ количество всегда = "1"
					ПозицияЧека.Количество = 1;
					
					Если НЕ СтрокаШтрихкода.ЧастичноеВыбытие Тогда
						// при отсутствии частичного выбытия ед.измерения всегда  = шт (потребительская упаковка)
						ПозицияЧека.КодЕдиницыИзмерения = "796";
					КонецЕсли;
				КонецЕсли; 
				
				// если позиция чека распадается на несколько марок, то корректируем цену, сумму и НДС
				ПозицияЧека.Сумма    = ?(ОстатокСумма = 0, 0, Окр(ОстатокСумма/ОстатокКоличество*СтрокаШтрихкода.Количество,2));
				ПозицияЧека.СуммаНДС = ?(ОстатокСуммаНДС = 0, 0, Окр(ОстатокСуммаНДС/ОстатокКоличество*СтрокаШтрихкода.Количество,2));
				
				ОстатокСумма      = ОстатокСумма - ПозицияЧека.Сумма;
				ОстатокСуммаНДС   = ОстатокСуммаНДС - ПозицияЧека.СуммаНДС;
				ОстатокКоличество = ОстатокКоличество - СтрокаШтрихкода.Количество;
				
				Цена = 0;
				Если ПозицияЧека.Количество <> 0 Тогда
					Цена = Окр(ПозицияЧека.Сумма/ПозицияЧека.Количество, 2);
				КонецЕсли;
				
				ПозицияЧека.Цена           = Цена;
				ПозицияЧека.ЦенаСоСкидками = Цена;
				
				ПозицииЧека.Добавить(ПозицияЧека);
			КонецЦикла;
		Иначе
			ПозицияЧека = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(СтрокаТовары);
			ПозицииЧека.Добавить(ПозицияЧека);
		КонецЕсли; 
	КонецЦикла; 
	
	ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(НовыеПараметрыФискализации, ИдентификаторУстройства);
	
	Возврат НовыеПараметрыФискализации;
КонецФункции
 
&НаКлиенте
Процедура ОбновитьПредставлениеЧека()
	ПараметрыОперации = ПараметрыОперацииФискализацииЧека;
	Если ШтрихкодыУпаковок.Количество() > 0 Тогда
		ПараметрыОперации = ПараметрыОперацииФискализацииЧекаСоШтрихкодами();
	КонецЕсли;
	
	ФискальныйДокумент = СформироватьФискальныйДокумент(ПараметрыОперации);
КонецПроцедуры 

&НаСервереБезКонтекста
Функция СформироватьФискальныйДокумент(ОбщиеПараметры)
	Возврат ОборудованиеЧекопечатающиеУстройства.СформироватьФискальныйДокумент(0, ОбщиеПараметры);
КонецФункции 


&НаКлиенте
Процедура ОткрытьФормуРеквизитыАгента(Элемент)
	ПараметрыФормы = Новый Структура();
	
	ПараметрыФормы.Вставить("ТолькоПросмотр",         ТолькоПросмотр);
	Если Элемент.ДанныеАгента <> Неопределено Тогда
		ПараметрыФормы.Вставить("ДанныеАгента",           Элемент.ДанныеАгента);
	КонецЕсли;
	Если Элемент.ДанныеПоставщика <> Неопределено Тогда
		ПараметрыФормы.Вставить("ДанныеПоставщика",       Элемент.ДанныеПоставщика);
	КонецЕсли; 
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элемент,"ПризнакАгента") Тогда
		ПараметрыФормы.Вставить("ПризнакАгента",          Элемент.ПризнакАгента);
	Иначе
		ПараметрыФормы.Вставить("ПризнакАгента",          Элемент.ПризнакАгентаПоПредметуРасчета);
	КонецЕсли; 
	ПараметрыФормы.Вставить("ЭтоВерсияФФД105",        ЭтоВерсияФФД105);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПризнакАгентаЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент));
	
	ОткрытьФорму("Обработка.ПечатьЧека.Форма.РеквизитыАгента", ПараметрыФормы, ЭтотОбъект,,,,ОписаниеОповещения);
КонецПроцедуры 

&НаКлиенте
Процедура ПризнакАгентаЗавершение(Значение, Параметры) Экспорт
	Если ТипЗнч(Значение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Параметры.Элемент, Значение);
	
	Параметры.Элемент.ПризнакАгентаСтрокой = ПризнакАгентаПоПредметуРасчетаСтрокой(Значение.ПризнакАгента);
	
	Если ТипЗнч(Параметры.Элемент) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		ЗаполнитьЗначенияСвойств(
			ПараметрыОперацииФискализацииЧека, 
			Значение, 
			"ПризнакАгента, ДанныеАгента, ДанныеПоставщика");
			
		// Если признак агента установлен в шапке то по каждой строке он должен совпадать
		Если ЗначениеЗаполнено(Значение.ПризнакАгента) Тогда
			Для каждого ПозицияЧека Из ПараметрыОперацииФискализацииЧека.ПозицииЧека Цикл
				Если ТипЗнч(ПозицияЧека) = Тип("Структура") Тогда
					ПозицияЧека.Вставить("ПризнакАгентаПоПредметуРасчета", Значение.ПризнакАгента);
					ПозицияЧека.Вставить("ДанныеАгента",                   Значение.ДанныеАгента);
					ПозицияЧека.Вставить("ДанныеПоставщика",               Значение.ДанныеПоставщика);
				КонецЕсли; 
			КонецЦикла;
			
			ЗаполнитьТаблицаТоваров();
		КонецЕсли; 
	Иначе
		Параметры.Элемент.ПризнакАгентаПоПредметуРасчета = Значение.ПризнакАгента;
		
		ПозицияЧека = ПараметрыОперацииФискализацииЧека.ПозицииЧека[Параметры.Элемент.НомерСтроки-1];
		Если ТипЗнч(ПозицияЧека) = Тип("Структура") Тогда
			ПозицияЧека.Вставить("ПризнакАгентаПоПредметуРасчета", Значение.ПризнакАгента);
			ПозицияЧека.Вставить("ДанныеАгента",                   Значение.ДанныеАгента);
			ПозицияЧека.Вставить("ДанныеПоставщика",               Значение.ДанныеПоставщика);
		КонецЕсли;
		
		Если Значение.ПризнакАгента <> ПараметрыОперацииФискализацииЧека.ПризнакАгента
			ИЛИ СравнитьСтруктурыРекурсивно(Значение.ДанныеАгента, ПараметрыОперацииФискализацииЧека.ДанныеАгента).Количество() <> 0
			ИЛИ СравнитьСтруктурыРекурсивно(Значение.ДанныеПоставщика, ПараметрыОперацииФискализацииЧека.ДанныеПоставщика).Количество() <> 0 Тогда
			
			Если ПараметрыОперацииФискализацииЧека.ПозицииЧека.Количество() > 1 Тогда
			
				ПараметрыОперацииФискализацииЧека.ПризнакАгента    = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.ПустаяСсылка");
				ПараметрыОперацииФискализацииЧека.ДанныеАгента     = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеАгента();
				ПараметрыОперацииФискализацииЧека.ДанныеПоставщика = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеПоставщика();
				
				ЕстьИнформацияОбАгентахПоСтрокам = ЗначениеЗаполнено(Значение.ПризнакАгента);
				
			Иначе
				
				ЗаполнитьЗначенияСвойств(
					ПараметрыОперацииФискализацииЧека, 
					Значение, 
					"ПризнакАгента, ДанныеАгента, ДанныеПоставщика");
			
			КонецЕсли; 
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыОперацииФискализацииЧека, "ПризнакАгента, ДанныеАгента, ДанныеПоставщика");
			
			ПризнакАгентаСтрокой = ПризнакАгентаПоПредметуРасчетаСтрокой(ПризнакАгента);
		Иначе
			ЕстьИнформацияОбАгентахПоСтрокам = Ложь;
			Для каждого ПозицияЧека Из ПараметрыОперацииФискализацииЧека.ПозицииЧека Цикл
				Если ЗначениеЗаполнено(ПозицияЧека.ПризнакАгентаПоПредметуРасчета)
					И   (ПозицияЧека.ПризнакАгентаПоПредметуРасчета <> ПараметрыОперацииФискализацииЧека.ПризнакАгента
					ИЛИ СравнитьСтруктурыРекурсивно(Значение.ДанныеАгента, ПараметрыОперацииФискализацииЧека.ДанныеАгента).Количество() <> 0
					ИЛИ СравнитьСтруктурыРекурсивно(Значение.ДанныеПоставщика, ПараметрыОперацииФискализацииЧека.ДанныеПоставщика).Количество() <> 0) Тогда
					
					ЕстьИнформацияОбАгентахПоСтрокам = Истина;
					Прервать;
					
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли; 
		
	КонецЕсли; 
	
	ОбновитьПредставлениеЧека();
	
	УправлениеФормой(ЭтотОбъект);
	
	Модифицированность = Истина;
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПризнакАгентаПоПредметуРасчетаСтрокой(ПризнакАгента)
	Возврат Обработки.ПечатьЧека.ПризнакАгентаПоПредметуРасчетаСтрокой(ПризнакАгента);
КонецФункции 

&НаКлиенте
Процедура РедактированиеРеквизитовКассира()
	ДанныеКассира = Новый Структура("Кассир, КассирИНН");
	ЗаполнитьЗначенияСвойств(ДанныеКассира, ЭтотОбъект);
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактированиеРеквизитовКассираЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ПечатьЧека.Форма.РеквизитыКассира", ДанныеКассира, ЭтотОбъект, ,,,ОписаниеОповещения);
КонецПроцедуры 

&НаКлиенте
Процедура РедактированиеРеквизитовКассираЗавершение(РеквизитыКассира, Параметры) Экспорт
	Если ТипЗнч(РеквизитыКассира) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыКассира);
	
	КассирПредставление = ПредставлениеКассира(Кассир, КассирИНН);
	
	ЗаполнитьЗначенияСвойств(ПараметрыОперацииФискализацииЧека, ЭтотОбъект, "Кассир, КассирИНН");
	
	ОбновитьПредставлениеЧека();
КонецПроцедуры 

&НаКлиенте
Процедура ПриЗакрытииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		Закрыть(Неопределено);
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ЧекКоррекции 
			И Объект.КоррекцияПоПредписанию 
			И НЕ ЗначениеЗаполнено(Объект.НомерПредписания) Тогда
		ТекстСообщения = "";
		ВидСообщения   = "Заполнение";
		
		ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", ВидСообщения,"Номер предписания налогового органа",,,ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, , "НомерПредписания", "Объект", Отказ);
	КонецЕсли; 
	
	Если Объект.ЧекКоррекции 
		И НЕ ЗначениеЗаполнено(Объект.КорректируемыйДокумент) 
		И НЕ ЗначениеЗаполнено(Объект.ДатаКоррекции) Тогда
		ТекстСообщения = "";
		ВидСообщения   = "Заполнение";
		
		ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", ВидСообщения,"Дата коррекции",,,ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, , "ДатаКоррекции", "Объект", Отказ);
	КонецЕсли;  
	
	Если Объект.ЧекКоррекции 
		И Объект.КоррекцияНепринятогоЧека
		И НЕ ЗначениеЗаполнено(Объект.КорректируемыйДокумент)  Тогда
		ТекстСообщения = "";
		ВидСообщения   = "Заполнение";
		
		ОписаниеОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", ВидСообщения,"Корректируемый документ",,,ТекстСообщения);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки, , "КорректируемыйДокумент", "Объект", Отказ);
	КонецЕсли;
	
	Если РасчетыВИнтернет 
		И НЕ ОтправлятьSMS И НЕ ОтправлятьEmail Тогда
	
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'При расчетах в интернет отправка чека по email или sms является обязательной'"),,"МестоРасчетов",,Отказ);
	КонецЕсли;
	
	ПараметрыОбъекта = МенеджерОборудованияБП.НовыйПараметрыПроверкиЗаполнения();
	
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, ЭтотОбъект);
	
	Если ПараметрыОбъекта.РасчетыВИнтернет Тогда
		ПараметрыОбъекта.АдресСайта = МестоРасчетов;
	КонецЕсли;
	
	МенеджерОборудованияБП.ОбработкаПроверкиЗаполненияПечатьЧека(ПараметрыОбъекта, Истина, Отказ);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.ТаблицаТоваровИндексАкцизнойМарки.Видимость = Форма.ШтрихкодыУпаковок.Количество();
	
	Элементы.ГруппаКоррекция.Видимость    = Объект.ЧекКоррекции;
	Элементы.НомерПредписания.Доступность = Объект.КоррекцияПоПредписанию;
	Элементы.ПризнакАгента.Видимость      = НЕ Форма.ЕстьИнформацияОбАгентахПоСтрокам И НЕ Форма.ЭтоВерсияФФД12;
КонецПроцедуры 

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры


&НаКлиенте
Процедура ВыборТаможеннойДекларацииЗавершение(Значение, ДополнительныеПараметры) Экспорт
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	РегистрационныйНомерТаможеннойДекларации = РегистрационныйНомерТаможеннойДекларации(Значение);
	
	ДополнительныеПараметры.Элемент.НомерТаможеннойДекларации = РегистрационныйНомерТаможеннойДекларации;
	
	ПозицияЧека = ПараметрыОперацииФискализацииЧека.ПозицииЧека[ДополнительныеПараметры.Элемент.НомерСтроки-1];
	Если ТипЗнч(ПозицияЧека) = Тип("Структура") Тогда
		ПозицияЧека.Вставить("НомерТаможеннойДекларации", РегистрационныйНомерТаможеннойДекларации);
	КонецЕсли; 
	
	ОбновитьПредставлениеЧека();
КонецПроцедуры

&НаСервереБезКонтекста
Функция РегистрационныйНомерТаможеннойДекларации(ТаможеннаяДекларация)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТаможеннаяДекларация, "РегистрационныйНомер");

КонецФункции 

&НаСервереБезКонтекста
Функция КодСтраныПроисхождения(Страна)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Страна, "Код");

КонецФункции 

&НаКлиенте
Процедура КорректируемыйДокументПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
	
		УстановитьДокументНаСервере(ДополнительныеПараметры.КлючЗаписи);
	
	КонецЕсли; 

КонецПроцедуры


&НаСервереБезКонтекста
Функция ЕстьИсправленияПоДокументу(КлючЗаписи)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", КлючЗаписи.ДокументОснование);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФискальныеОперации.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|ГДЕ
	|	ФискальныеОперации.ДокументОснование = &ДокументСсылка
	|	И ФискальныеОперации.КорректируемыйДокумент = &ДокументСсылка";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции 

&НаКлиенте
Процедура МестоРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	РасчетыВИнтернет = ВыбранноеЗначение;
	МестоРасчетов = Элементы.МестоРасчетов.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение).Представление;
	
	ПараметрыОперацииФискализацииЧека.Интернет = ВыбранноеЗначение;
	ПараметрыОперацииФискализацииЧека.МестоРасчетов = МестоРасчетов;
	
	ОбновитьПредставлениеЧека();
	СтандартнаяОбработка = Ложь;
КонецПроцедуры


#КонецОбласти