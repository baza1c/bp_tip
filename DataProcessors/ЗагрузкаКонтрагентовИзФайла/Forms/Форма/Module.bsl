
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	АдресФайла = Параметры.АдресФайла;
	РасширениеФайла = Параметры.РасширениеФайла;
	ПрикладнаяЗагрузка = Параметры.ПрикладнаяЗагрузка;
	
	Если ПрикладнаяЗагрузка Тогда
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.ПараметрыПрикладнойЗагрузки, "Организация");
		ВидДоговора = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.ПараметрыПрикладнойЗагрузки, "ВидДоговора");
	КонецЕсли;
	
	ВестиУчетПоДоговорам = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	
	Если Не ЗначениеЗаполнено(АдресФайла) Или Не ЭтоАдресВременногоХранилища(АдресФайла) Тогда
		ТекстИсключения = НСтр("ru = 'Не выбран файл для загрузки.
			|Откройте обработку из формы списка контрагентов с помощью команды ""Загрузить"".'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораОтбораТаблицы();
	
	ОписаниеКолонок.Загрузить(Обработки.ЗагрузкаКонтрагентовИзФайла.ОписаниеЗагружаемыхКолонок(ПрикладнаяЗагрузка));
	
	ИзменитьШаг("Шаг1");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_ПодготовитьТабличныйДокумент", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборТаблицыПриИзменении(Элемент)
	
	УстановитьОтборВТаблице(ЭтотОбъект, ОтборТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеФайлаВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если Область.Верх = 1 И Область.Лево <= ДанныеФайла.ШиринаТаблицы Тогда
		
		ДопПараметрОповещения = Новый Структура("ТекущаяОбласть", Область);
		ОписаниеОповещения = Новый ОписаниеОповещения("ДанныеФайлаВыборЗавершение", ЭтотОбъект, ДопПараметрОповещения);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОписаниеКолонок", ОписаниеКолонокВоВременномХранилище());
		ОткрытьФорму("ОбщаяФорма.ВыборКолонокПриЗагрузкеИзФайла", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗагружаемыеДанные

&НаКлиенте
Процедура ЗагружаемыеДанныеПослеУдаления(Элемент)
	ТабличнаяЧастьИзменена = Истина;
	ИзменитьТекстПереключателяОтбора();
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеДанныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеДанныеКонтрагентПриИзменении(Элемент)
	
	ТабличнаяЧастьИзменена = Истина;
	ТекущиеДанные = Элементы.ЗагружаемыеДанные.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Контрагент) Тогда
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПолучитьДанныеКонтрагента(ТекущиеДанные.Контрагент));
		Если ПрикладнаяЗагрузка И ВестиУчетПоДоговорам Тогда
			ТекущиеДанные.Статус = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовыйДоговор();
			ТекущиеДанные.СтатусКонтрагента = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНайденный();
			ТекущиеДанные.СтатусДоговора = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый();
		Иначе
			ТекущиеДанные.Статус = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНайденный();
			ТекущиеДанные.СтатусКонтрагента = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНайденный();
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Наименование) Тогда
		ТекущиеДанные.Статус = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый();
		ТекущиеДанные.СтатусКонтрагента = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый();
		ТекущиеДанные.СтатусДоговора = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый();
	КонецЕсли;
	
	ИзменитьТекстПереключателяОтбора();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагружаемыеДанныеДоговорКонтрагентаПриИзменении(Элемент)

	ТабличнаяЧастьИзменена = Истина;
	ТекущиеДанные = Элементы.ЗагружаемыеДанные.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ДоговорКонтрагента) Тогда
		ТекущиеДанные.Статус = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНайденный();
		ТекущиеДанные.СтатусДоговора = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНайденный();
	Иначе
		ТекущиеДанные.Статус = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовыйДоговор();
		ТекущиеДанные.СтатусДоговора = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый();
	КонецЕсли;
	
	ИзменитьТекстПереключателяОтбора();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	
	Верх = ДанныеФайла.ТекущаяОбласть.Верх;
	Низ  = ДанныеФайла.ТекущаяОбласть.Низ;
	Лево = ДанныеФайла.ТекущаяОбласть.Лево;
	
	Если Верх = 1 И Низ = 1 Тогда
		Возврат; // выделена первая строка (строка заголовка)
	КонецЕсли;
	
	УдалитьСтрокиНаСервере(Верх, Низ);
	Элементы.ДанныеФайла.ТекущаяОбласть = ДанныеФайла.Область(Верх, Лево);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКолонку(Команда)
	
	Лево  = ДанныеФайла.ТекущаяОбласть.Лево;
	Право = ДанныеФайла.ТекущаяОбласть.Право;
	Верх  = ДанныеФайла.ТекущаяОбласть.Верх;
	
	УдалитьКолонкиНаСервере(Лево, Право);
	Элементы.ДанныеФайла.ТекущаяОбласть = ДанныеФайла.Область(Верх, Лево);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЛишнее(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьЛишнееПослеЗакрытияВопроса", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Найти и удалить строки и колонки, не содержащие информацию для загрузки?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	ОчиститьСообщения();
	
	ЗаголовкиЗаполнены = ЗагрузкаДанныхИзВнешнихФайловКлиент.ЗаполненыВсеЗаголовкиКолонок(
		ДанныеФайла, "ДанныеФайла", ОписаниеКолонок);
	
	Если Не ЗаголовкиЗаполнены Тогда
		Возврат;
	КонецЕсли;
	
	ОтборТаблицы = 0;
	УстановитьОтборВТаблице(ЭтотОбъект, ОтборТаблицы);
	
	ДлительнаяОперация = ПодготовитьТаблицуДанныхДляЗагрузкиНаСервере();
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		Обработчик = Новый ОписаниеОповещения("ПодготовитьТаблицуДанныхДляЗагрузкиЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если ТабличнаяЧастьИзменена Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОПереходеНаПервыйШаг", ЭтотОбъект);
		ТекстВопроса = НСтр(
			"ru = 'Все изменения, произведенные вами в таблице, будут потеряны.
				  |
				  |Вернуться на предыдущий шаг?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ИзменитьШаг("Шаг1");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	ДлительнаяОперация = ЗагрузитьДанныеИзФайлаНаСервере();
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // найдены незаполненные обязательные реквизиты
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		Обработчик = Новый ОписаниеОповещения("ЗагрузкаДанныхИзФайлаЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УправлениеФормой

&НаСервере
Процедура ИзменитьШаг(ТекущийШаг)
	
	Элементы.ГруппаШаг1.Видимость = (ТекущийШаг = "Шаг1");
	Элементы.ГруппаШаг2.Видимость = (ТекущийШаг = "Шаг2");
	Элементы.Далее.КнопкаПоУмолчанию = (ТекущийШаг = "Шаг1");
	Элементы.Загрузить.КнопкаПоУмолчанию = (ТекущийШаг = "Шаг2");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Серый текст для новых элементов
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеКонтрагент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ЗагружаемыеДанные.Контрагент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ЗагружаемыеДанные.СтатусКонтрагента", ВидСравненияКомпоновкиДанных.Равно,
		ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый());
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ТемноСерый);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.ЗагружаемыеДанные.Представление"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеДоговорКонтрагента");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ЗагружаемыеДанные.ДоговорКонтрагента", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ЗагружаемыеДанные.СтатусДоговора", ВидСравненияКомпоновкиДанных.Равно,
		ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый());
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ТемноСерый);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.ЗагружаемыеДанные.ПредставлениеДоговора"));
	
	// Устанавливаем отметку незаполненного, если не заполнены ссылка и наименование
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеКонтрагент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ЗагружаемыеДанные.Контрагент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.ЗагружаемыеДанные.СтатусКонтрагента", ВидСравненияКомпоновкиДанных.Равно,
		ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНайденный());
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// Устанавливаем видимость договора
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеКартинкаДоговор");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеДоговорКонтрагента");
	ГруппаИли = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ВестиУчетПоДоговорам", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ПрикладнаяЗагрузка", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Устанавливаем доступность договора
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеДоговорКонтрагента");
	ГруппаИли = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"Организация", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"Объект.ЗагружаемыеДанные.Контрагент", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// Устанавливаем видимость задолженности и аванса
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеАванс");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеЗадолженность");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ПрикладнаяЗагрузка", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Устанавливаем видимость ИНН/КПП
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеИНН");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗагружаемыеДанныеКПП");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ПрикладнаяЗагрузка", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТекстПереключателяОтбора()
	
	Статистика = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатистикаСтатусовСтрокТаблицы(Объект.ЗагружаемыеДанные);
	
	СписокВыбора = Элементы.ОтборТаблицы.СписокВыбора;
	СписокВыбора[0].Представление = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.ТекстСКоличеством(НСтр("ru = 'Все'"), Статистика.Всего);
	СписокВыбора[1].Представление = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.ТекстСКоличеством(НСтр("ru = 'Найденные в программе'"), Статистика.Найденные);
	СписокВыбора[2].Представление = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.ТекстСКоличеством(НСтр("ru = 'Новые контрагенты'"), Статистика.Новые);
	
	Если ПрикладнаяЗагрузка И ВестиУчетПоДоговорам Тогда
		СписокВыбора[3].Представление = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.ТекстСКоличеством(НСтр("ru = 'Новые договоры'"), Статистика.НовыеДоговоры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодготовкаТабличногоДокумента

&НаКлиенте
Процедура Подключаемый_ПодготовитьТабличныйДокумент()
	
	ДлительнаяОперация = ПодготовитьТабличныйДокументСервер(АдресФайла, РасширениеФайла, УникальныйИдентификатор, ОписаниеКолонок);
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		Обработчик = Новый ОписаниеОповещения("ПодготовитьТабличныйДокументЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодготовитьТабличныйДокументСервер(Знач АдресФайла, Знач РасширениеФайла, Знач ИдентификаторФормы, Знач ОписаниеКолонок)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
	ХранилищеДанных = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
	УдалитьИзВременногоХранилища(АдресФайла);
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("РасширениеФайла", РасширениеФайла);
	ПараметрыЗадания.Вставить("ХранилищеДанныхФайла", ХранилищеДанных);
	ПараметрыЗадания.Вставить("ОписаниеКолонок", ОписаниеКолонок.Выгрузить());
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подготовка табличного документа к загрузке контрагентов.'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ЗагрузкаКонтрагентовИзФайла.ОбработатьДанныеИзФайла",
		ПараметрыЗадания, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПодготовитьТабличныйДокументЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ЗаполнитьТабличныйДокументСервер(ДлительнаяОперация.АдресРезультата);
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличныйДокументСервер(АдресРезультата)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресРезультата);
	ДанныеФайла = ЗагруженныеДанные.ДанныеФайла.Получить();
	УдалитьИзВременногоХранилища(АдресРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область УдалениеЛишнихДанных

&НаСервере
Процедура УдалитьСтрокиНаСервере(Верх, Низ)
	
	Если Верх = 1 Тогда
		Верх = 2; // чтобы не удалить заголовки колонок
	КонецЕсли;
	
	Область = ДанныеФайла.Область(Верх,, Низ);
	ДанныеФайла.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоВертикали);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьКолонкиНаСервере(Лево, Право)
	
	Область = ДанныеФайла.Область(, Лево,, Право);
	ДанныеФайла.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЛишнееПослеЗакрытияВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ДанныеФайла, "Ожидание");
	
	ОчиститьСообщения();
	
	ДлительнаяОперация = УдалитьЛишнееСервер();
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("УдалитьЛишнееЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УдалитьЛишнееСервер()
	
	ПараметрыЗадания = Новый Структура;
	ХранилищеДанных = Новый ХранилищеЗначения(ДанныеФайла, Новый СжатиеДанных(9));
	ПараметрыЗадания.Вставить("ХранилищеДанных", ХранилищеДанных);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Удаление незначимых строк табличного документа.'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ЗагрузкаКонтрагентовИзФайла.УдалитьВсеНенужныеСтрокиТаблицы",
		ПараметрыЗадания, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура УдалитьЛишнееЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		НайденыНенужныеСтроки = Ложь;
		ОбработатьРезультатУдаленияЛишнихСтрок(ДлительнаяОперация.АдресРезультата, НайденыНенужныеСтроки);
		Если НЕ НайденыНенужныеСтроки Тогда
			
			ТекстСообщения = НСтр("ru = 'Строк и колонок, не содержащих информацию для загрузки, не обнаружено.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		Иначе
			
			ТекстСообщения = НСтр("ru = 'Удаление завершено.'");
			ТекстПояснения = НСтр("ru = 'Удаление незначимых данных завершено.'");
			ПоказатьОповещениеПользователя(ТекстСообщения,, ТекстПояснения);
			
		КонецЕсли;
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатУдаленияЛишнихСтрок(АдресРезультата, НайденыНенужныеСтроки)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	НайденыНенужныеСтроки = Результат.НайденыНенужныеСтроки;
	Если НайденыНенужныеСтроки Тогда
		ДанныеФайла = Результат.ХранилищеДанных.Получить();
	КонецЕсли;
	
	ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ДанныеФайла, "НеИспользовать");
	
КонецПроцедуры

#КонецОбласти

#Область ПодготовкаТаблицыДанныхДляЗагрузки

&НаСервере
Функция ПодготовитьТаблицуДанныхДляЗагрузкиНаСервере()
	
	ПараметрыЗадания = Новый Структура;
	ХранилищеДанных = Новый ХранилищеЗначения(ДанныеФайла, Новый СжатиеДанных(9));
	ПараметрыЗадания.Вставить("ХранилищеДанных", ХранилищеДанных);
	ПараметрыЗадания.Вставить("ПрикладнаяЗагрузка", ПрикладнаяЗагрузка);
	ПараметрыЗадания.Вставить("Организация", Организация);
	ПараметрыЗадания.Вставить("ВестиУчетПоДоговорам", ВестиУчетПоДоговорам);
	ПараметрыЗадания.Вставить("ВидДоговора", ВидДоговора);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подготовка таблицы данных при загрузке контрагентов.'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ЗагрузкаКонтрагентовИзФайла.ПолучитьТаблицуДанныхДляЗагрузки",
		ПараметрыЗадания, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПодготовитьТаблицуДанныхДляЗагрузкиЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ЗаполнитьТаблицуЗагружаемыхДанных(ДлительнаяОперация.АдресРезультата);
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЗагружаемыхДанных(АдресРезультата)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	Объект.ЗагружаемыеДанные.Загрузить(Результат);
	
	ИзменитьШаг("Шаг2");
	ИзменитьТекстПереключателяОтбора();
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДанныхИзФайла

&НаСервере
Функция ЗагрузитьДанныеИзФайлаНаСервере()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ЗагружаемыеДанные", Объект.ЗагружаемыеДанные.Выгрузить());
	ПараметрыЗадания.Вставить("ГруппаДляНовыхКонтрагентов", ГруппаДляНовыхКонтрагентов);
	ПараметрыЗадания.Вставить("ПрикладнаяЗагрузка", ПрикладнаяЗагрузка);
	ПараметрыЗадания.Вставить("Организация", Организация);
	ПараметрыЗадания.Вставить("ВестиУчетПоДоговорам", ВестиУчетПоДоговорам);
	ПараметрыЗадания.Вставить("ВидДоговора", ВидДоговора);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка данных файла.'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ЗагрузкаКонтрагентовИзФайла.ЗагрузитьДанныеИзФайла",
		ПараметрыЗадания, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузкаДанныхИзФайлаЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ОповеститьОбИзменении(Тип("СправочникСсылка.Контрагенты"));
		Результат = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		Если Результат.Ошибки <> Неопределено
			И Результат.Ошибки.СписокОшибок.Количество() > 0 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'При загрузке данных произошли ошибки.'"));
			ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Результат.Ошибки);
		ИначеЕсли ПрикладнаяЗагрузка Тогда
			Закрыть(Результат.ОстаткиВзаиморасчетов);
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка контрагентов произведена.'"));
			Закрыть();
		КонецЕсли;
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДанныеФайлаВыборЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Или РезультатЗакрытия = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ТекстНезаполненногоЗаголовка = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.ТекстЗаголовкаНесопоставленнойКолонки();
	ШиринаТаблицы = ДанныеФайла.ШиринаТаблицы;
	Для НомерКолонки = 1 по ШиринаТаблицы Цикл
		
		Область = ДанныеФайла.Область(1, НомерКолонки);
		Если Область.ПараметрРасшифровки = РезультатЗакрытия.Идентификатор Тогда
			Область.Текст = ТекстНезаполненногоЗаголовка;
			Область.ПараметрРасшифровки = "";
			Область.ЦветТекста = ОбщегоНазначенияКлиентПовтИсп.ЦветСтиля("НезаполненныйРеквизит");
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнительныеПараметры.ТекущаяОбласть.Текст      = РезультатЗакрытия.Представление;
	ДополнительныеПараметры.ТекущаяОбласть.ЦветТекста = ОбщегоНазначенияКлиентПовтИсп.ЦветСтиля("ЦветГиперссылки");
	ДополнительныеПараметры.ТекущаяОбласть.ПараметрРасшифровки = РезультатЗакрытия.Идентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОПереходеНаПервыйШаг(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ТабличнаяЧастьИзменена = Ложь;
		ИзменитьШаг("Шаг1");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеКонтрагента(Контрагент)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "ИНН, КПП");
	
КонецФункции

&НаСервере
Функция ОписаниеКолонокВоВременномХранилище()
	Возврат ПоместитьВоВременноеХранилище(ОписаниеКолонок.Выгрузить());
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораОтбораТаблицы()
	
	СписокВыбора = Элементы.ОтборТаблицы.СписокВыбора;
	
	СписокВыбора.Очистить();
	
	СписокВыбора.Добавить(0, НСтр("ru = 'Все'"));
	СписокВыбора.Добавить(1, НСтр("ru = 'Найденные в программе'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Новые контрагенты'"));

	Если ПрикладнаяЗагрузка И ВестиУчетПоДоговорам Тогда
		СписокВыбора.Добавить(3, НСтр("ru = 'Новые договоры'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборВТаблице(Форма, ОтборТаблицы)
	
	Элементы = Форма.Элементы;
	
	Если ОтборТаблицы = 1 Тогда
		
		ЗначениеОтбора = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНайденный();
		
	ИначеЕсли ОтборТаблицы = 2 Тогда
		
		ЗначениеОтбора = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовый();
		
	ИначеЕсли ОтборТаблицы = 3 Тогда
		
		ЗначениеОтбора = ЗагрузкаДанныхИзВнешнихФайловКлиентСервер.СтатусНовыйДоговор();
		
	Иначе
		
		ЗначениеОтбора = Неопределено; // показываем все
		
	КонецЕсли;
	
	Если ЗначениеОтбора = Неопределено Тогда
		Элементы.ЗагружаемыеДанные.ОтборСтрок = Неопределено;
	Иначе
		Элементы.ЗагружаемыеДанные.ОтборСтрок = Новый ФиксированнаяСтруктура("Статус", ЗначениеОтбора);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
