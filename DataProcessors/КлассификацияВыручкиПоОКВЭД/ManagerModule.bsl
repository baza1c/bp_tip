#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Заполняет данные распределения выручки в разрезе кодов ОКВЭД.
//
// Параметры:
//  Параметры - Структура:
//    Организация - СправочникСсылка.Организации
//    ДатаНачала - Дата
//    ДатаОкончания - Дата
//    ИмяОтчета - Строка
//    ДанныеНоменклатуры - ТаблицаЗначений
//    ПерезаполнитьДанныеНоменклатуры - Булево
//    ПоляГруппировок - СписокЗначений
//    ДанныеДерева - ДеревоЗначений
//    ДиаграммаАнализ - Диаграмма
//    СоответствиеЦветов - ТаблицаЗначений
//    ОтборПоПолюГруппировкиТаблица - ТаблицаЗначений
// 
// Возвращаемое значение:
//  Структура:
//    ДанныеНоменклатуры - ТаблицаЗначений
//    ПоляГруппировок - СписокЗначений
//    ДеревоДанных - ДеревоЗначений
//    ДиаграммаАнализ - Диаграмма
//    СоответствиеЦветов - ТаблицаЗначений
//    СписокВыбораДляОтбора - Массив
//    ОтборПоПолюГруппировкиТаблица - ТаблицаЗначений
//    СуммаВыручки - Число
//    ОКВЭДОрганизации - Структура
//    ЗаголовокПоляГруппировкиНоменклатуры - Строка
//
Функция ДанныеРаспределенияВыручки(Параметры) Экспорт
	
	ДанныеНоменклатуры = Параметры.ДанныеНоменклатуры;
	ПоляГруппировок = Параметры.ПоляГруппировок;
	ДеревоДанных = Параметры.ДанныеДерева;
	ДиаграммаАнализ = Параметры.ДиаграммаАнализ;
	СоответствиеЦветов = Параметры.СоответствиеЦветов;
	ОтборПоПолюГруппировкиТаблица = Параметры.ОтборПоПолюГруппировкиТаблица;
	ЗаголовокПоляГруппировкиНоменклатуры = "";
	
	РазделыКодов = КлассификаторОКВЭД.РазделыКодов();
	НастройкиОКВЭД = НастройкиОКВЭД(Параметры.Организация);
	ПервоначальноеЗаполнениеОКВЭДомИзОрганизации = Не ЗначениеЗаполнено(НастройкиОКВЭД);
	Если Не ЗначениеЗаполнено(ДанныеНоменклатуры) Или Параметры.ПерезаполнитьДанныеНоменклатуры Тогда
		ПолучитьДанные(ДанныеНоменклатуры, НастройкиОКВЭД, Параметры.Организация, Параметры.ДатаНачала, Параметры.ДатаОкончания, Параметры.ИмяОтчета, РазделыКодов);
	КонецЕсли;
	СуммаВыручки = ДанныеНоменклатуры.Итог("СуммаВыручки");
	
	ДанныеОКВЭД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Организация, "КодОКВЭД2, НаименованиеОКВЭД2");
	ОКВЭДОрганизации = Новый Структура;
	ОКВЭДОрганизации.Вставить("Код", ДанныеОКВЭД.КодОКВЭД2);
	ОКВЭДОрганизации.Вставить("Наименование", ДанныеОКВЭД.НаименованиеОКВЭД2);
	ОКВЭДОрганизацииПредставление = "";
	Если Не ПустаяСтрока(ДанныеОКВЭД.КодОКВЭД2) И Не ПустаяСтрока(ДанныеОКВЭД.НаименованиеОКВЭД2) Тогда
		ОКВЭДОрганизацииПредставление = ДанныеОКВЭД.КодОКВЭД2 + " " + ДанныеОКВЭД.НаименованиеОКВЭД2;
	КонецЕсли;
	ОКВЭДОрганизации.Вставить("Представление", ОКВЭДОрганизацииПредставление);
	
	ОпределитьПоляГруппировки(ПоляГруппировок, ДанныеНоменклатуры, ЗаголовокПоляГруппировкиНоменклатуры);
	Если ПервоначальноеЗаполнениеОКВЭДомИзОрганизации Тогда
		ЗаполнитьОКВЭДомИзОрганизации(ПоляГруппировок, ДанныеНоменклатуры, СуммаВыручки, ОКВЭДОрганизации, СоответствиеЦветов, РазделыКодов);
	КонецЕсли;
	
	ЦветаДиаграммы = ЦветаДиаграммы(ДиаграммаАнализ);
	ЗаполнитьДеревоДанных(ДеревоДанных, ПоляГруппировок, ДанныеНоменклатуры, ЦветаДиаграммы, СоответствиеЦветов, СуммаВыручки);
	
	ЗаполнитьДанныеДиаграммы(ДиаграммаАнализ, ДанныеНоменклатуры, ДеревоДанных, ПоляГруппировок, ЦветаДиаграммы, СоответствиеЦветов);
	
	СписокВыбораДляОтбора = Новый Массив;
	ЗаполнитьОтборПоПолюГруппировки(СписокВыбораДляОтбора, ОтборПоПолюГруппировкиТаблица, ДеревоДанных, ДанныеНоменклатуры, ПоляГруппировок);
	ДополнитьИзбранноеНовымиКодами(Параметры.Организация, ДанныеНоменклатуры);
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("ДанныеНоменклатуры",                   ДанныеНоменклатуры);
	РезультатВыполнения.Вставить("ПоляГруппировок",                      ПоляГруппировок);
	РезультатВыполнения.Вставить("ДеревоДанных",                         ДеревоДанных);
	РезультатВыполнения.Вставить("ДиаграммаАнализ",                      ДиаграммаАнализ);
	РезультатВыполнения.Вставить("СоответствиеЦветов",                   СоответствиеЦветов);
	РезультатВыполнения.Вставить("СписокВыбораДляОтбора",                СписокВыбораДляОтбора);
	РезультатВыполнения.Вставить("ОтборПоПолюГруппировкиТаблица",        ОтборПоПолюГруппировкиТаблица);
	РезультатВыполнения.Вставить("СуммаВыручки",                         СуммаВыручки);
	РезультатВыполнения.Вставить("ОКВЭДОрганизации",                     ОКВЭДОрганизации);
	РезультатВыполнения.Вставить("ЗаголовокПоляГруппировкиНоменклатуры", ЗаголовокПоляГруппировкиНоменклатуры);
	
	Если Параметры.ОткрытаИзСпискаЗадач Тогда
		ДанныеПроверкиОсновногоКодаОКВЭД = Новый Структура;
		ДанныеПроверкиОсновногоКодаОКВЭД.Вставить("ОсновнойКод", Неопределено);
		ДанныеПроверкиОсновногоКодаОКВЭД.Вставить("Комментарий", "");
		Если Не ПервоначальноеЗаполнениеОКВЭДомИзОрганизации Или ДеревоДанных.Строки.Количество() = 1 Тогда
			ДанныеПроверкиОсновногоКодаОКВЭД = ДанныеПроверкиОсновногоКодаОКВЭД(ДанныеНоменклатуры, СуммаВыручки, ОКВЭДОрганизации.Код);
		КонецЕсли;
		РезультатВыполнения.Вставить("ОсновнойКодОКВЭД", ДанныеПроверкиОсновногоКодаОКВЭД.ОсновнойКод);
		РезультатВыполнения.Вставить("КомментарийОсновнойКодОКВЭД", ДанныеПроверкиОсновногоКодаОКВЭД.Комментарий);
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

// Обновляет данные распределения выручки в разрезе кодов ОКВЭД после изменения кода ОКВЭД.
//
// Параметры:
//  Параметры - Структура:
//    ПараметрыОтбора - Структура - структура с ключами:
//      * ОтборПоПолюГруппировкиСтрока - Строка
//      * ОтборПоПолюГруппировкиТаблица - ТаблицаЗначений
//      * ОтборФлаг - Булево
//    ОКВЭД - Структура - новый код ОКВЭД, ключи:
//      * Код - Строка
//      * Представление - Строка
//    ПоляПоискаГруппировкиНоменклатуры - Массив Из Структура - данные строки, в которой поменяли ОКВЭД
//    ДанныеНоменклатуры - ТаблицаЗначений
//    ПоляГруппировок - СписокЗначений
//    ДанныеДерева - ДеревоЗначений
//    ДиаграммаАнализ - Диаграмма
//    СоответствиеЦветов - ТаблицаЗначений
// 
// Возвращаемое значение:
//  Структура:
//    ДанныеНоменклатуры - ТаблицаЗначений
//    ДеревоДанных - ДеревоЗначений
//    ДиаграммаАнализ - Диаграмма
//    СоответствиеЦветов - ТаблицаЗначений
//    СписокВыбораДляОтбора - Массив
//    ОтборПоПолюГруппировкиСтрока - Строка
//    ОтборПоПолюГруппировкиТаблица - ТаблицаЗначений
//
Функция ОбновитьДанныеРаспределенияВыручки(Параметры) Экспорт
	
	ОКВЭД = Параметры.ОКВЭД;
	ПоляПоискаГруппировкиНоменклатуры = Параметры.ПоляПоискаГруппировкиНоменклатуры;
	ПараметрыОтбора = Параметры.ПараметрыОтбора;
	ДанныеНоменклатуры = Параметры.ДанныеНоменклатуры;
	ПоляГруппировок = Параметры.ПоляГруппировок;
	ДеревоДанных = Параметры.ДанныеДерева;
	ДиаграммаАнализ = Параметры.ДиаграммаАнализ;
	СоответствиеЦветов = Параметры.СоответствиеЦветов;
	
	РазделыКодов = КлассификаторОКВЭД.РазделыКодов();
	ОбновитьДанныеНоменклатуры(ОКВЭД, ПоляПоискаГруппировкиНоменклатуры, ДанныеНоменклатуры, ПоляГруппировок, СоответствиеЦветов, РазделыКодов);
	СуммаВыручки = ДанныеНоменклатуры.Итог("СуммаВыручки");
	
	СписокВыбораДляОтбора = Новый Массив;
	ЗаполнитьОтборПоПолюГруппировки(
		СписокВыбораДляОтбора, ПараметрыОтбора.ОтборПоПолюГруппировкиТаблица, ДеревоДанных, ДанныеНоменклатуры, ПоляГруппировок);
	
	Если ПараметрыОтбора.ОтборФлаг Тогда
		УстановитьОтменитьОтбор(ПараметрыОтбора, ДеревоДанных, ДиаграммаАнализ, ПоляГруппировок, ДанныеНоменклатуры, СоответствиеЦветов, СуммаВыручки);
	Иначе
		ЦветаДиаграммы = ЦветаДиаграммы(ДиаграммаАнализ);
		ЗаполнитьДеревоДанных(ДеревоДанных, ПоляГруппировок, ДанныеНоменклатуры, ЦветаДиаграммы, СоответствиеЦветов, СуммаВыручки);
		ЗаполнитьДанныеДиаграммы(ДиаграммаАнализ, ДанныеНоменклатуры, ДеревоДанных, ПоляГруппировок, ЦветаДиаграммы, СоответствиеЦветов);
		ДополнитьИзбранноеНовымиКодами(Параметры.Организация, ДанныеНоменклатуры);
	КонецЕсли;
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("ДанныеНоменклатуры",            ДанныеНоменклатуры);
	РезультатВыполнения.Вставить("ДеревоДанных",                  ДеревоДанных);
	РезультатВыполнения.Вставить("ДиаграммаАнализ",               ДиаграммаАнализ);
	РезультатВыполнения.Вставить("СоответствиеЦветов",            СоответствиеЦветов);
	РезультатВыполнения.Вставить("СписокВыбораДляОтбора",         СписокВыбораДляОтбора);
	РезультатВыполнения.Вставить("ОтборПоПолюГруппировкиСтрока",  ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока);
	РезультатВыполнения.Вставить("ОтборПоПолюГруппировкиТаблица", ПараметрыОтбора.ОтборПоПолюГруппировкиТаблица);
	
	Если Параметры.ОткрытаИзСпискаЗадач Тогда
		ДанныеПроверкиОсновногоКодаОКВЭД = ДанныеПроверкиОсновногоКодаОКВЭД(ДанныеНоменклатуры, СуммаВыручки, Параметры.ОКВЭДОрганизацииКод);
		РезультатВыполнения.Вставить("ОсновнойКодОКВЭД", ДанныеПроверкиОсновногоКодаОКВЭД.ОсновнойКод);
		РезультатВыполнения.Вставить("КомментарийОсновнойКодОКВЭД", ДанныеПроверкиОсновногоКодаОКВЭД.Комментарий);
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

// Устанавливает отбор или сбрасывает его по данным распределения выручки.
//
// Параметры:
//  Параметры - Структура:
//    ПараметрыОтбора - Структура - структура с ключами:
//      * ОтборПоПолюГруппировкиСтрока - Строка
//      * ОтборПоПолюГруппировкиТаблица - ТаблицаЗначений
//      * ОтборФлаг - Булево
//      * ПоляОтбора - Структура - данные поля группировки, по которому устанавливается отбор
//      * ОтменитьОтбор - Булево
//    ДанныеНоменклатуры - ТаблицаЗначений
//    ПоляГруппировок - СписокЗначений
//    ДанныеДерева - ДеревоЗначений
//    ДиаграммаАнализ - Диаграмма
//    СоответствиеЦветов - ТаблицаЗначений
// 
// Возвращаемое значение:
//  Структура:
//    ДеревоДанных - ДеревоЗначений
//    ДиаграммаАнализ - Диаграмма
//    ОтборПоПолюГруппировкиСтрока - Строка
//    ОтборПоПолюГруппировкиТаблица - ТаблицаЗначений
//    ОтборФлаг - Булево
//    СуммаВыручки - Число
//
Функция ОтобратьДанныеРаспределенияВыручки(Параметры) Экспорт
	
	ПараметрыОтбора = Параметры.ПараметрыОтбора;
	ДеревоДанных = Параметры.ДанныеДерева;
	ДиаграммаАнализ = Параметры.ДиаграммаАнализ;
	ДанныеНоменклатуры = Параметры.ДанныеНоменклатуры;
	ПоляГруппировок = Параметры.ПоляГруппировок;
	СоответствиеЦветов = Параметры.СоответствиеЦветов;
	
	СуммаВыручки = ДанныеНоменклатуры.Итог("СуммаВыручки");
	Если ПараметрыОтбора.ОтменитьОтбор Тогда
		ОтменитьОтбор(ПараметрыОтбора, ДеревоДанных, ДиаграммаАнализ, ПоляГруппировок, ДанныеНоменклатуры, СоответствиеЦветов, СуммаВыручки);
	ИначеЕсли ПараметрыОтбора.ПоляОтбора <> Неопределено Тогда
		УстановитьОтборКоманда(ПараметрыОтбора, ДеревоДанных, ДиаграммаАнализ, ПоляГруппировок, ДанныеНоменклатуры, СоответствиеЦветов, СуммаВыручки);
	Иначе
		УстановитьОтменитьОтбор(ПараметрыОтбора, ДеревоДанных, ДиаграммаАнализ, ПоляГруппировок, ДанныеНоменклатуры, СоответствиеЦветов, СуммаВыручки);
	КонецЕсли;
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("ДеревоДанных",                  ДеревоДанных);
	РезультатВыполнения.Вставить("ДиаграммаАнализ",               ДиаграммаАнализ);
	РезультатВыполнения.Вставить("ОтборПоПолюГруппировкиСтрока",  ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока);
	РезультатВыполнения.Вставить("ОтборПоПолюГруппировкиТаблица", ПараметрыОтбора.ОтборПоПолюГруппировкиТаблица);
	РезультатВыполнения.Вставить("ОтборФлаг",                     ПараметрыОтбора.ОтборФлаг);
	РезультатВыполнения.Вставить("СуммаВыручки",                  СуммаВыручки);
	
	Возврат РезультатВыполнения;
	
КонецФункции

// Возвращает список настраиваемых полей группировки.
//
// Возвращаемое значение:
//   СписокЗначений Из Строка - содержит перечень настраиваемых полей группировки с их представлениями
//
Функция НастраиваемыеПоляГруппировки() Экспорт
	
	НастраиваемыеПоляГруппировки = Новый СписокЗначений;
	НастраиваемыеПоляГруппировки.Добавить("НоменклатурнаяГруппа", НСтр("ru = 'Номенклатурная группа'"));
	НастраиваемыеПоляГруппировки.Добавить("ГруппаНоменклатурыВерхнегоУровня", НСтр("ru = 'Группа номенклатуры верхнего уровня'"));
	НастраиваемыеПоляГруппировки.Добавить("ГруппаНоменклатурыВторогоУровня", НСтр("ru = 'Группа номенклатуры второго уровня'"));
	НастраиваемыеПоляГруппировки.Добавить("ВидНоменклатуры", НСтр("ru = 'Вид номенклатуры'"));
	
	Возврат НастраиваемыеПоляГруппировки;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПоляГруппировкиПоУмолчанию(ПараметрыФормы) Экспорт
	
	Если Не ЗначениеЗаполнено(ПараметрыФормы.Организация) Тогда
		Возврат НастраиваемыеПоляГруппировки();
	КонецЕсли;
	
	ДанныеНоменклатуры = ДанныеНоменклатурыДетальнаяАналитика(ПараметрыФормы);
	ЗаполнитьГруппыНоменклатуры(ДанныеНоменклатуры);
	
	ВычисленныеПоляГруппировки = ВычисленныеПоляГруппировки(ДанныеНоменклатуры);
	Если Не ЗначениеЗаполнено(ВычисленныеПоляГруппировки) Тогда
		ЗаполнитьПоляГруппировки(ДанныеНоменклатуры, ВычисленныеПоляГруппировки);
	КонецЕсли;
	
	Возврат ВычисленныеПоляГруппировки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьДанные(ДанныеНоменклатуры, НастройкиОКВЭД, Организация, ДатаНачала, ДатаОкончания, ИмяОтчета, РазделыКодов)
	
	Запрос = Новый Запрос;
	
	Счета9001 = Новый Массив;
	Счета9001.Добавить(ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД);
	Счета9001.Добавить(ПланыСчетов.Хозрасчетный.ВыручкаЕНВД);
	Запрос.УстановитьПараметр("Счета9001", Счета9001);
	
	СчетНДС = ПланыСчетов.Хозрасчетный.Продажи_НДС;
	Запрос.УстановитьПараметр("СчетНДС", СчетНДС);
	
	Организации = Новый Массив;
	ЭтоИП = Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если ЭтоИП Тогда
		Организации.Добавить(Организация);
	Иначе
		ФормыЗаполняемыеПоВсейОрганизации = Новый Массив;
		ЗаполнениеФормСтатистикиПоОбособленнымПодразделениям.ДобавитьФормыЗаполняемыеВЦеломПоОрганизации(ФормыЗаполняемыеПоВсейОрганизации);
		Если ФормыЗаполняемыеПоВсейОрганизации.Найти(ИмяОтчета) <> Неопределено Тогда
			Организации = БухгалтерскийУчетПереопределяемый.ВсяОрганизация(Организация);
		Иначе
			Организации.Добавить(Организация);
		КонецЕсли;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(ДатаОкончания));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.НоменклатурныеГруппы) КАК НоменклатурнаяГруппа,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаЕНВД)
	|			ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.Номенклатура)
	|		ИНАЧЕ ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто3 КАК Справочник.Номенклатура)
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаЕНВД)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Перечисление.СтавкиНДС)
	|	КОНЕЦ КАК СтавкаНДС,
	|	ХозрасчетныйОбороты.Счет КАК СчетВыручки,
	|	ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаВыручки
	|ПОМЕСТИТЬ Обороты9001
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&Счета9001), , Организация В (&Организации), , ) КАК ХозрасчетныйОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НоменклатурнаяГруппа,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоляНДС.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ДоляНДС.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ДоляНДС.ВыручкаВключаяНДС) КАК ВыручкаВключаяНДС,
	|	СУММА(ДоляНДС.НДС) КАК НДС
	|ПОМЕСТИТЬ ДоляНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫРАЗИТЬ(Выручка.Субконто1 КАК Справочник.НоменклатурныеГруппы) КАК НоменклатурнаяГруппа,
	|		ВЫРАЗИТЬ(Выручка.Субконто2 КАК Перечисление.СтавкиНДС) КАК СтавкаНДС,
	|		Выручка.СуммаОборотКт КАК ВыручкаВключаяНДС,
	|		0 КАК НДС
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаНеЕНВД), , Организация В (&Организации), , ) КАК Выручка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫРАЗИТЬ(НДС.Субконто1 КАК Справочник.НоменклатурныеГруппы),
	|		ВЫРАЗИТЬ(НДС.Субконто2 КАК Перечисление.СтавкиНДС),
	|		0,
	|		НДС.СуммаОборотДт
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет = &СчетНДС, , Организация В (&Организации), , ) КАК НДС) КАК ДоляНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоляНДС.НоменклатурнаяГруппа,
	|	ДоляНДС.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НоменклатурнаяГруппа,
	|	СтавкаНДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обороты9001.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Обороты9001.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(Обороты9001.Номенклатура.ВидНоменклатуры, НЕОПРЕДЕЛЕНО) КАК ВидНоменклатуры,
	|	Обороты9001.СчетВыручки КАК Субсчет9001,
	|	СУММА(Обороты9001.СуммаВыручки) КАК СуммаВыручки,
	|	ЕСТЬNULL(Обороты9001.Номенклатура.Услуга, ЛОЖЬ) КАК ЭтоУслуга,
	|	ЕСТЬNULL(Обороты9001.Номенклатура.КодОКВЭД2.Код, """") КАК ОКВЭДНоменклатурыКод,
	|	ЕСТЬNULL(Обороты9001.Номенклатура.КодОКВЭД2.Наименование, """") КАК ОКВЭДНоменклатурыНаименование,
	|	Обороты9001.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ДанныеВыручки
	|ИЗ
	|	Обороты9001 КАК Обороты9001
	|
	|СГРУППИРОВАТЬ ПО
	|	Обороты9001.СчетВыручки,
	|	Обороты9001.Номенклатура,
	|	Обороты9001.НоменклатурнаяГруппа,
	|	Обороты9001.СтавкаНДС,
	|	ЕСТЬNULL(Обороты9001.Номенклатура.Услуга, ЛОЖЬ),
	|	ЕСТЬNULL(Обороты9001.Номенклатура.ВидНоменклатуры, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(Обороты9001.Номенклатура.КодОКВЭД2.Код, """"),
	|	ЕСТЬNULL(Обороты9001.Номенклатура.КодОКВЭД2.Наименование, """")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеВыручки.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ДанныеВыручки.Номенклатура КАК Номенклатура,
	|	ДанныеВыручки.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ДанныеВыручки.Субсчет9001 КАК Субсчет9001,
	|	ДанныеВыручки.СуммаВыручки КАК СуммаВыручки,
	|	ДанныеВыручки.СуммаВыручки - ВЫБОР
	|		КОГДА ЕСТЬNULL(ДоляНДС.ВыручкаВключаяНДС, 0) = 0
	|			ТОГДА 0
	|		КОГДА ДоляНДС.ВыручкаВключаяНДС = ДанныеВыручки.СуммаВыручки
	|			ТОГДА ДоляНДС.НДС
	|		ИНАЧЕ ДоляНДС.НДС * ДанныеВыручки.СуммаВыручки / ДоляНДС.ВыручкаВключаяНДС
	|	КОНЕЦ КАК СуммаВыручкиБезНДС,
	|	ДанныеВыручки.ЭтоУслуга КАК ЭтоУслуга,
	|	ДанныеВыручки.ОКВЭДНоменклатурыКод КАК ОКВЭДНоменклатурыКод,
	|	ДанныеВыручки.ОКВЭДНоменклатурыНаименование КАК ОКВЭДНоменклатурыНаименование,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ГруппаНоменклатурыВерхнегоУровня,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ГруппаНоменклатурыВторогоУровня
	|ИЗ
	|	ДанныеВыручки КАК ДанныеВыручки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоляНДС КАК ДоляНДС
	|		ПО ДанныеВыручки.НоменклатурнаяГруппа = ДоляНДС.НоменклатурнаяГруппа
	|			И ДанныеВыручки.СтавкаНДС = ДоляНДС.СтавкаНДС";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	ЗаполнитьГруппыНоменклатуры(ТаблицаДанных);
	
	НастройкиОКВЭДПоГруппамИ = НастройкиОКВЭДПоГруппамИ(НастройкиОКВЭД);
	
	ДанныеНоменклатуры.Очистить();
	ИспользоватьУведомленияОКонтролируемыхСделках = Константы.ИспользоватьУведомленияОКонтролируемыхСделках.Получить();
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		
		НовСтрока = ДанныеНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, СтрокаДанных);
		
		НовСтрока.СуммаВыручки = СтрокаДанных.СуммаВыручкиБезНДС;
		
		ЗаполнитьОКВЭДИзНастройки(НовСтрока, НастройкиОКВЭД, НастройкиОКВЭДПоГруппамИ, РазделыКодов);
		
		// ОКВЭД заполняем из номенклатуры только для услуг и если есть контролируемые сделки.
		Если ИспользоватьУведомленияОКонтролируемыхСделках И СтрокаДанных.ЭтоУслуга
			И ЗначениеЗаполнено(СтрокаДанных.ОКВЭДНоменклатурыКод) Тогда
			НовСтрока.ОКВЭД = СтрокаДанных.ОКВЭДНоменклатурыКод + " " + СтрокаДанных.ОКВЭДНоменклатурыНаименование;
			НовСтрока.ОКВЭДКод = СтрокаДанных.ОКВЭДНоменклатурыКод;
			НовСтрока.ОКВЭДНаименование = СтрокаДанных.ОКВЭДНоменклатурыНаименование;
			НовСтрока.ОКВЭДРаздел = РазделыКодов[(Лев(НовСтрока.ОКВЭДКод, 2))];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НастройкиОКВЭД(Организация)
	
	НастройкиОКВЭД = Новый ТаблицаЗначений;
	НастройкиОКВЭД.Колонки.Добавить("ОКВЭДКод", ОбщегоНазначения.ОписаниеТипаСтрока(8));
	НастройкиОКВЭД.Колонки.Добавить("ОКВЭД", ОбщегоНазначения.ОписаниеТипаСтрока(500));
	НастройкиОКВЭД.Колонки.Добавить("ОКВЭДНаименование", ОбщегоНазначения.ОписаниеТипаСтрока(500));
	НастройкиОКВЭД.Колонки.Добавить("НаименованиеПоля", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТипыЗначенийНастройки = Новый Массив;
	ТипыЗначенийНастройки.Добавить(Тип("СправочникСсылка.НоменклатурныеГруппы"));
	ТипыЗначенийНастройки.Добавить(Тип("СправочникСсылка.ВидыНоменклатуры"));
	ТипыЗначенийНастройки.Добавить(Тип("СправочникСсылка.Номенклатура"));
	ТипыЗначенийНастройки.Добавить(Тип("Строка"));
	НастройкиОКВЭД.Колонки.Добавить("ЗначениеНастройки", Новый ОписаниеТипов(ТипыЗначенийНастройки));
	НастройкиОКВЭД.Колонки.Добавить("НаименованиеГруппаИ", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	НастройкиОКВЭД.Колонки.Добавить("ЭтоГруппаИ", Новый ОписаниеТипов("Булево"));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиКлассификацииВыручки.Детализация.Код КАК ОКВЭДКод,
	|	НастройкиКлассификацииВыручки.Детализация.Наименование КАК ОКВЭДНаименование,
	|	НастройкиКлассификацииВыручки.Настройка КАК Настройка
	|ИЗ
	|	РегистрСведений.НастройкиКлассификацииВыручки КАК НастройкиКлассификацииВыручки
	|ГДЕ
	|	НастройкиКлассификацииВыручки.Организация = &Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		// Если первый раз настраиваем, то настройки можно получить из регистра НастройкаЗаполненияСвободныхСтрокФормСтатистики.
		// Если несколько объектов наблюдения с детализацией по видам деятельности, выбираем первый с приоритетом "Продукция и услуги".
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Организация);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(НастройкаЗаполненияСвободныхСтрокФормСтатистики.ДетализацияОбъектаНаблюдения КАК Справочник.КлассификаторВидовЭкономическойДеятельности).Код КАК ОКВЭДКод,
		|	ВЫРАЗИТЬ(НастройкаЗаполненияСвободныхСтрокФормСтатистики.ДетализацияОбъектаНаблюдения КАК Справочник.КлассификаторВидовЭкономическойДеятельности).Наименование КАК ОКВЭДНаименование,
		|	НастройкаЗаполненияСвободныхСтрокФормСтатистики.Настройка КАК Настройка,
		|	НастройкаЗаполненияСвободныхСтрокФормСтатистики.ОбъектНаблюдения КАК ОбъектНаблюдения,
		|	ВЫБОР
		|		КОГДА НастройкаЗаполненияСвободныхСтрокФормСтатистики.ОбъектНаблюдения.Код = ""ПродукцияИУслуги""
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	РегистрСведений.НастройкаЗаполненияСвободныхСтрокФормСтатистики КАК НастройкаЗаполненияСвободныхСтрокФормСтатистики
		|ГДЕ
		|	НастройкаЗаполненияСвободныхСтрокФормСтатистики.Организация = &Организация
		|	И НастройкаЗаполненияСвободныхСтрокФормСтатистики.ОбъектНаблюдения.Детализация = ЗНАЧЕНИЕ(Перечисление.ВидыСвободныхСтрокФормСтатистики.ВидыДеятельности)
		|	И НЕ НастройкаЗаполненияСвободныхСтрокФормСтатистики.ОбъектНаблюдения.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет
		|ИТОГИ ПО
		|	ОбъектНаблюдения";
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаОбъектНаблюдения = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаОбъектНаблюдения.Следующий() Тогда
			Выборка = ВыборкаОбъектНаблюдения.Выбрать();
		КонецЕсли;
	КонецЕсли;
	
	НастройкиОтбораКомпоновки = НастройкиОтбораКомпоновки();
	Пока Выборка.Следующий() Цикл
		
		Отбор = ЗаполнениеФормСтатистики.ПолучитьОтборКомпоновкиДанных(Выборка.Настройка);
		ОбойтиЭлементыОтбора(Отбор.Элементы, Выборка.ОКВЭДКод, Выборка.ОКВЭДНаименование, НастройкиОКВЭД, НастройкиОтбораКомпоновки);
	КонецЦикла;
	
	Возврат НастройкиОКВЭД;
КонецФункции

Процедура ЗаполнитьОКВЭДИзНастройки(Строка, НастройкиОКВЭД, НастройкиОКВЭДПоГруппамИ, РазделыКодов)
	
	// Поиск по группам И
	ВыборкаДетальныеЗаписи = НастройкиОКВЭДПоГруппамИ.ВыборкаДетальныеЗаписи;
	Для Каждого СтрокаГруппировок Из НастройкиОКВЭДПоГруппамИ.Группировки Цикл
		
		ОКВЭДПодходит = Истина;
		Пока ВыборкаДетальныеЗаписи.НайтиСледующий(
			Новый Структура("ОКВЭДКод, НаименованиеГруппаИ", СтрокаГруппировок.ОКВЭДКод, СтрокаГруппировок.НаименованиеГруппаИ)) Цикл
			
			ИмяПоля = ВыборкаДетальныеЗаписи.НаименованиеПоля;
			Если ТипЗнч(ВыборкаДетальныеЗаписи.ЗначениеНастройки) = Тип("Строка") Тогда
				Список = ОбщегоНазначения.ЗначениеИзСтрокиXML(ВыборкаДетальныеЗаписи.ЗначениеНастройки);
				Если ИмяПоля = "ГруппаНоменклатуры" Тогда
					Если Список.НайтиПоЗначению(Строка.ГруппаНоменклатурыВерхнегоУровня) = Неопределено
						И Список.НайтиПоЗначению(Строка.ГруппаНоменклатурыВторогоУровня) = Неопределено Тогда
						ОКВЭДПодходит = Ложь;
					КонецЕсли;
				Иначе
					Если Список.НайтиПоЗначению(Строка[ИмяПоля]) = Неопределено Тогда
						ОКВЭДПодходит = Ложь;
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли ИмяПоля = "ГруппаНоменклатуры" Тогда
				Если Строка.ГруппаНоменклатурыВерхнегоУровня <> ВыборкаДетальныеЗаписи.ЗначениеНастройки
					И Строка.ГруппаНоменклатурыВторогоУровня <> ВыборкаДетальныеЗаписи.ЗначениеНастройки Тогда
					ОКВЭДПодходит = Ложь;
				КонецЕсли;
				
			Иначе
				Если Строка[ИмяПоля] <> ВыборкаДетальныеЗаписи.ЗначениеНастройки Тогда
					ОКВЭДПодходит = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если ОКВЭДПодходит Тогда
			Строка.ОКВЭД = СтрокаГруппировок.ОКВЭД;
			Строка.ОКВЭДКод = СтрокаГруппировок.ОКВЭДКод;
			Строка.ОКВЭДНаименование = СтрокаГруппировок.ОКВЭДНаименование;
			Строка.ОКВЭДРаздел = РазделыКодов[(Лев(Строка.ОКВЭДКод, 2))];
			ВыборкаДетальныеЗаписи.Сбросить();
			Возврат;
		КонецЕсли;
	КонецЦикла;
	ВыборкаДетальныеЗаписи.Сбросить();
	
	// Группа Или: поиск по любому из полей
	СписокПолей = Новый Массив;
	СписокПолей.Добавить("Номенклатура");
	СписокПолей.Добавить("НоменклатурнаяГруппа");
	СписокПолей.Добавить("ГруппаНоменклатурыВторогоУровня");
	СписокПолей.Добавить("ГруппаНоменклатурыВерхнегоУровня");
	СписокПолей.Добавить("ВидНоменклатуры");
	
	Для Каждого ИмяПоля Из СписокПолей Цикл
		НайденныеСтроки = НастройкиОКВЭД.НайтиСтроки(Новый Структура("ЗначениеНастройки, ЭтоГруппаИ", Строка[ИмяПоля], Ложь));
		Если НайденныеСтроки.Количество() <> 0 Тогда
			Строка.ОКВЭД = НайденныеСтроки[0].ОКВЭД;
			Строка.ОКВЭДКод = НайденныеСтроки[0].ОКВЭДКод;
			Строка.ОКВЭДНаименование = НайденныеСтроки[0].ОКВЭДНаименование;
			Строка.ОКВЭДРаздел = РазделыКодов[(Лев(Строка.ОКВЭДКод, 2))];
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НастройкиОКВЭДПоГруппамИ(НастройкиОКВЭД)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НастройкиОКВЭД", НастройкиОКВЭД);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиОКВЭД.ОКВЭДКод КАК ОКВЭДКод,
	|	НастройкиОКВЭД.ОКВЭДНаименование КАК ОКВЭДНаименование,
	|	НастройкиОКВЭД.ОКВЭД КАК ОКВЭД,
	|	НастройкиОКВЭД.НаименованиеПоля КАК НаименованиеПоля,
	|	НастройкиОКВЭД.ЗначениеНастройки КАК ЗначениеНастройки,
	|	НастройкиОКВЭД.НаименованиеГруппаИ КАК НаименованиеГруппаИ,
	|	НастройкиОКВЭД.ЭтоГруппаИ КАК ЭтоГруппаИ
	|ПОМЕСТИТЬ НастройкиОКВЭД
	|ИЗ
	|	&НастройкиОКВЭД КАК НастройкиОКВЭД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиОКВЭД.ОКВЭДКод КАК ОКВЭДКод,
	|	НастройкиОКВЭД.ОКВЭДНаименование КАК ОКВЭДНаименование,
	|	НастройкиОКВЭД.ОКВЭД КАК ОКВЭД,
	|	НастройкиОКВЭД.НаименованиеГруппаИ КАК НаименованиеГруппаИ
	|ИЗ
	|	НастройкиОКВЭД КАК НастройкиОКВЭД
	|ГДЕ
	|	НастройкиОКВЭД.ЭтоГруппаИ
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиОКВЭД.ОКВЭДКод,
	|	НастройкиОКВЭД.ОКВЭДНаименование,
	|	НастройкиОКВЭД.НаименованиеГруппаИ,
	|	НастройкиОКВЭД.ОКВЭД
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОКВЭДКод,
	|	НаименованиеГруппаИ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиОКВЭД.ОКВЭДКод КАК ОКВЭДКод,
	|	НастройкиОКВЭД.ОКВЭДНаименование КАК ОКВЭДНаименование,
	|	НастройкиОКВЭД.ОКВЭД КАК ОКВЭД,
	|	НастройкиОКВЭД.НаименованиеГруппаИ КАК НаименованиеГруппаИ,
	|	НастройкиОКВЭД.НаименованиеПоля КАК НаименованиеПоля,
	|	НастройкиОКВЭД.ЗначениеНастройки КАК ЗначениеНастройки
	|ИЗ
	|	НастройкиОКВЭД КАК НастройкиОКВЭД
	|ГДЕ
	|	НастройкиОКВЭД.ЭтоГруппаИ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОКВЭДКод,
	|	НаименованиеГруппаИ";
	
	Результаты = Запрос.ВыполнитьПакет();
	Группировки = Результаты[Результаты.Количество() -2].Выгрузить();
	ВыборкаДетальныеЗаписи = Результаты[Результаты.Количество() -1].Выбрать();
	
	Возврат Новый Структура("Группировки, ВыборкаДетальныеЗаписи", Группировки, ВыборкаДетальныеЗаписи);
	
КонецФункции

Процедура ОбойтиЭлементыОтбора(ЭлементыОтбора, ОКВЭДКод, ОКВЭДНаименование, НастройкиОКВЭД, НастройкиОтбораКомпоновки, ЭтоГруппаИ = Истина, Счетчик = 1)
	
	// В элементах отбора могут быть как условия (например, по номенклатурной группе, номенклатуре) так и группы условий:
	// группа И (одновременное выполнение условий), группа Или (выполнение любого из условий).
	// Может быть несколько групп И, которые объединены в группу Или.
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		Если ЭлементОтбора.Использование = Ложь Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			ЭтоГруппаИ = Не ЭлементОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			НайденныеСтроки = НастройкиОКВЭД.НайтиСтроки(Новый Структура("ОКВЭДКод, ЭтоГруппаИ", ОКВЭДКод, Истина));
			УжеЕстьГруппаИВНастройках = НайденныеСтроки.Количество() > 0;
			Если ЭтоГруппаИ И УжеЕстьГруппаИВНастройках Тогда
				Счетчик = Счетчик + 1;
			КонецЕсли;
			ОбойтиЭлементыОтбора(ЭлементОтбора.Элементы, ОКВЭДКод, ОКВЭДНаименование, НастройкиОКВЭД, НастройкиОтбораКомпоновки, ЭтоГруппаИ, Счетчик);
			
		ИначеЕсли ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СписокЗначений") Тогда
			Если ЭтоГруппаИ Тогда
				ДобавитьСтрокуСоответствияПолейГруппировкиОКВЭД(
					ЭлементОтбора, ЭлементОтбора.ПравоеЗначение, ОКВЭДКод, ОКВЭДНаименование, НастройкиОКВЭД, НастройкиОтбораКомпоновки, ЭтоГруппаИ, Счетчик);
			Иначе
				Для Каждого ЗначениеЭлементаОтбора Из ЭлементОтбора.ПравоеЗначение Цикл
					ДобавитьСтрокуСоответствияПолейГруппировкиОКВЭД(
						ЭлементОтбора, ЗначениеЭлементаОтбора.Значение, ОКВЭДКод, ОКВЭДНаименование, НастройкиОКВЭД, НастройкиОтбораКомпоновки, ЭтоГруппаИ, Счетчик);
				КонецЦикла;
			КонецЕсли;
		Иначе
			Если ТипЗнч(ЭлементОтбора.Родитель) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				ЭтоГруппаИ = Не ЭлементОтбора.Родитель.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			КонецЕсли;
			ДобавитьСтрокуСоответствияПолейГруппировкиОКВЭД(
				ЭлементОтбора, ЭлементОтбора.ПравоеЗначение, ОКВЭДКод, ОКВЭДНаименование, НастройкиОКВЭД, НастройкиОтбораКомпоновки, ЭтоГруппаИ, Счетчик);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьСтрокуСоответствияПолейГруппировкиОКВЭД(
	ЭлементОтбора, ЗначениеЭлементаОтбора, ОКВЭДКод, ОКВЭДНаименование, НастройкиОКВЭД, НастройкиОтбораКомпоновки, ЭтоГруппаИ, Счетчик)
	
	Если НастройкиОтбораКомпоновки.ПоляОтбора.Найти(ЭлементОтбора.ЛевоеЗначение) = Неопределено
		Или НастройкиОтбораКомпоновки.ВидыСравненияИсключения.Найти(ЭлементОтбора.ВидСравнения) <> Неопределено
		Или ЗначениеЭлементаОтбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = НастройкиОКВЭД.Добавить();
	
	Если ЭтоГруппаИ Тогда
		НоваяСтрока.НаименованиеГруппаИ = "ГруппаИ" + Счетчик;
		НоваяСтрока.ЭтоГруппаИ = Истина;
	КонецЕсли;
	
	Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидДеятельности") Тогда
		НоваяСтрока.НаименованиеПоля = "НоменклатурнаяГруппа";
		
	ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НоменклатураВыручки")
		Или ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Номенклатура") Тогда
		НоваяСтрока.НаименованиеПоля = "Номенклатура";
		Если ТипЗнч(ЗначениеЭлементаОтбора) = Тип("СписокЗначений") Тогда
			Для Каждого Элемент Из ЗначениеЭлементаОтбора Цикл
				Если Элемент.Значение.ЭтоГруппа Тогда
					НоваяСтрока.НаименованиеПоля = "ГруппаНоменклатуры";
					Прервать;
				КонецЕсли;
			КонецЦикла;
			// Если в настройке указано одно пустое значение и вид сравнения: в группе из списка,
			// то поле группировки не номенклатура, а группа номенклатуры (пустая папка).
			Если ЗначениеЭлементаОтбора.Количество() = 1 И Не ЗначениеЗаполнено(ЗначениеЭлементаОтбора[0].Значение)
				И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии Тогда
				НоваяСтрока.НаименованиеПоля = "ГруппаНоменклатуры";
			КонецЕсли;
		ИначеЕсли ЗначениеЭлементаОтбора.ЭтоГруппа Тогда
			НоваяСтрока.НаименованиеПоля = "ГруппаНоменклатуры";
		КонецЕсли;
		
	ИначеЕсли ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НоменклатураВыручки.ВидНоменклатуры") Тогда
		НоваяСтрока.НаименованиеПоля = "ВидНоменклатуры";
	КонецЕсли;
	
	НоваяСтрока.ЗначениеНастройки = ЗначениеЭлементаОтбора;
	Если ТипЗнч(ЗначениеЭлементаОтбора) = Тип("СписокЗначений") Тогда
		НоваяСтрока.ЗначениеНастройки = ОбщегоНазначения.ЗначениеВСтрокуXML(ЗначениеЭлементаОтбора);
	КонецЕсли;
	
	НоваяСтрока.ОКВЭДКод = ОКВЭДКод;
	НоваяСтрока.ОКВЭДНаименование = ОКВЭДНаименование;
	НоваяСтрока.ОКВЭД = ОКВЭДКод + " " + ОКВЭДНаименование;
	
КонецПроцедуры

Процедура ОпределитьПоляГруппировки(ПоляГруппировок, ДанныеНоменклатуры, ЗаголовокПоляГруппировкиНоменклатуры)
	
	Если Не ЗначениеЗаполнено(ПоляГруппировок) Тогда
		ПоляГруппировок.Добавить("ОКВЭД", "ОКВЭД");
		ЗаполнитьПоляГруппировки(ДанныеНоменклатуры, ПоляГруппировок);
	КонецЕсли;
	
	ИндексНоменклатурнаяГруппа = Неопределено;
	ИндексВидНоменклатуры = Неопределено;
	Если ПоляГруппировок.НайтиПоЗначению("НоменклатурнаяГруппа") <> Неопределено Тогда
		ИндексНоменклатурнаяГруппа = ПоляГруппировок.Индекс(ПоляГруппировок.НайтиПоЗначению("НоменклатурнаяГруппа"));
	КонецЕсли;
	Если ПоляГруппировок.НайтиПоЗначению("ВидНоменклатуры") <> Неопределено Тогда
		ИндексВидНоменклатуры = ПоляГруппировок.Индекс(ПоляГруппировок.НайтиПоЗначению("ВидНоменклатуры"));
	КонецЕсли;
	
	Если ИндексНоменклатурнаяГруппа = Неопределено И ИндексВидНоменклатуры = Неопределено Тогда
		ЗаголовокПоляГруппировкиНоменклатуры = НСтр("ru = 'Номенклатура'");
	Иначе
		Если ИндексНоменклатурнаяГруппа <> Неопределено И ИндексВидНоменклатуры = Неопределено Тогда
			ТекстПоляГруппировки = НСтр("ru = 'Номенклатурная группа'");
		ИначеЕсли ИндексНоменклатурнаяГруппа = Неопределено И ИндексВидНоменклатуры <> Неопределено Тогда
			ТекстПоляГруппировки = НСтр("ru = 'Вид номенклатуры'");
		Иначе
			Если ИндексВидНоменклатуры > ИндексНоменклатурнаяГруппа Тогда
				ТекстПоляГруппировки = НСтр("ru = 'Номенклатурная группа / Вид номенклатуры'");
			Иначе
				ТекстПоляГруппировки = НСтр("ru = 'Вид номенклатуры / Номенклатурная группа'");
			КонецЕсли;
		КонецЕсли;
		ЗаголовокПоляГруппировкиНоменклатуры = СтрШаблон(НСтр("ru = '%1 / Номенклатура'"), ТекстПоляГруппировки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоляГруппировки(ДанныеНоменклатуры, ПоляГруппировок)
	
	ДанныеПоНоменклатурнымГруппам = ДанныеНоменклатуры.Скопировать();
	ДанныеПоНоменклатурнымГруппам.Свернуть("НоменклатурнаяГруппа");
	Если ДанныеПоНоменклатурнымГруппам.Количество() > 1 Тогда
		ПоляГруппировок.Добавить("НоменклатурнаяГруппа", НСтр("ru = 'Номенклатурная группа'"));
		ПоляГруппировок.Добавить("ГруппаНоменклатурыВерхнегоУровня", НСтр("ru = 'Группа номенклатуры верхнего уровня'"));
	Иначе
		ДанныеПоГруппамНоменклатуры = ДанныеНоменклатуры.Скопировать();
		ДанныеПоГруппамНоменклатуры.Свернуть("ГруппаНоменклатурыВерхнегоУровня");
		// Если и групп нет, то надо делить по виду номенклатуры
		Если ДанныеПоГруппамНоменклатуры.Количество() <= 1 Тогда
			ПоляГруппировок.Добавить("ВидНоменклатуры", НСтр("ru = 'Вид номенклатуры'"));
		Иначе
			ПоляГруппировок.Добавить("ГруппаНоменклатурыВерхнегоУровня", НСтр("ru = 'Группа номенклатуры верхнего уровня'"));
			ДанныеПоГруппамНоменклатурыВторогоУровня = ДанныеНоменклатуры.Скопировать();
			ДанныеПоГруппамНоменклатурыВторогоУровня.Свернуть("ГруппаНоменклатурыВторогоУровня");
			Если ДанныеПоГруппамНоменклатурыВторогоУровня.Количество() > 1 Тогда
				ПоляГруппировок.Добавить("ГруппаНоменклатурыВторогоУровня", НСтр("ru = 'Группа номенклатуры'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьОКВЭДомИзОрганизации(ПоляГруппировок, ДанныеНоменклатуры, СуммаВыручки, ОКВЭДОрганизации, СоответствиеЦветов, РазделыКодов)
	
	ДанныеПоПолюГруппировок = ДанныеНоменклатуры.Скопировать();
	КолонкиГруппировок = "";
	ИндексУровня = 1;
	Если ПоляГруппировок.Количество() = 1 Тогда
		ИмяПоля = "Номенклатура";
	Иначе
		КолонкиГруппировок = СтрШаблон("%1, %2", ПоляГруппировок[0].Значение, ПоляГруппировок[1].Значение);
		ИмяПоля = ПоляГруппировок[ИндексУровня].Значение;
	КонецЕсли;
	Если ЗначениеЗаполнено(КолонкиГруппировок) Тогда
		ДанныеПоПолюГруппировок.Свернуть(КолонкиГруппировок, "СуммаВыручки");
	КонецЕсли;
	ДанныеПоПолюГруппировок.Сортировать("СуммаВыручки УБЫВ");
	СуммаВыручкиПоловина = СуммаВыручки * 0.51;
	ТекСуммаВыручки = 0;
	
	Для Каждого СтрокаДанных Из ДанныеПоПолюГруппировок Цикл
		ТекСуммаВыручки = ТекСуммаВыручки + СтрокаДанных.СуммаВыручки;
		
		Если ТекСуммаВыручки <= СуммаВыручки Тогда
			Если Не ЗначениеЗаполнено(СтрокаДанных.ОКВЭД) Тогда
				ПоляПоискаГруппировкиНоменклатуры = Новый Массив;
				ЭлементПолейПоиска = Новый Структура();
				ЭлементПолейПоиска.Вставить("ПолеГруппировкиНоменклатуры", СтрокаДанных[ИмяПоля]);
				ЭлементПолейПоиска.Вставить("ПолеГруппировкиРодитель", Неопределено);
				ЭлементПолейПоиска.Вставить("ИндексУровня", ИндексУровня);
				ПоляПоискаГруппировкиНоменклатуры.Добавить(ЭлементПолейПоиска);
				
				ОбновитьДанныеНоменклатуры(ОКВЭДОрганизации, ПоляПоискаГруппировкиНоменклатуры, ДанныеНоменклатуры, ПоляГруппировок, СоответствиеЦветов, РазделыКодов);
			КонецЕсли;
			Если ТекСуммаВыручки > СуммаВыручкиПоловина Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоДанных(ДеревоДанных, ПоляГруппировок, ДанныеНоменклатуры, ЦветаДиаграммы, СоответствиеЦветов, СуммаВыручки,
	ПоляОтбора = Неопределено)
	
	ДеревоДанных.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеНоменклатуры.ОКВЭД КАК ОКВЭД,
	|	ДанныеНоменклатуры.ОКВЭДКод КАК ОКВЭДКод,
	|	ДанныеНоменклатуры.ОКВЭДНаименование КАК ОКВЭДНаименование,
	|	ДанныеНоменклатуры.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ДанныеНоменклатуры.ГруппаНоменклатурыВерхнегоУровня КАК ГруппаНоменклатурыВерхнегоУровня,
	|	ДанныеНоменклатуры.ГруппаНоменклатурыВторогоУровня КАК ГруппаНоменклатурыВторогоУровня,
	|	ДанныеНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ДанныеНоменклатуры.Субсчет9001 КАК Субсчет9001,
	|	ДанныеНоменклатуры.СуммаВыручки КАК СуммаВыручки
	|ПОМЕСТИТЬ ТаблицаДанных
	|ИЗ
	|	&ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|ГДЕ
	|	&УсловиеОтбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.ОКВЭД КАК ОКВЭД,
	|	ТаблицаДанных.ОКВЭДКод КАК ОКВЭДКод,
	|	ТаблицаДанных.ОКВЭДНаименование КАК ОКВЭДНаименование,
	|	ТаблицаДанных.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ТаблицаДанных.ГруппаНоменклатурыВерхнегоУровня КАК ГруппаНоменклатурыВерхнегоУровня,
	|	ТаблицаДанных.ГруппаНоменклатурыВторогоУровня КАК ГруппаНоменклатурыВторогоУровня,
	|	ТаблицаДанных.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	ТаблицаДанных.Субсчет9001 КАК Субсчет9001,
	|	СУММА(ТаблицаДанных.СуммаВыручки) КАК СуммаВыручки,
	|	ТаблицаДанных.ОКВЭД КАК КоличествоОКВЭД
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.ОКВЭД,
	|	ТаблицаДанных.ОКВЭДКод,
	|	ТаблицаДанных.ОКВЭДНаименование,
	|	ТаблицаДанных.НоменклатурнаяГруппа,
	|	ТаблицаДанных.ГруппаНоменклатурыВерхнегоУровня,
	|	ТаблицаДанных.ГруппаНоменклатурыВторогоУровня,
	|	ТаблицаДанных.ВидНоменклатуры,
	|	ТаблицаДанных.Номенклатура,
	|	ТаблицаДанных.Субсчет9001,
	|	ТаблицаДанных.ОКВЭД
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаВыручки УБЫВ
	|ИТОГИ
	|	МИНИМУМ(ОКВЭД),
	|	МИНИМУМ(ОКВЭДКод),
	|	МИНИМУМ(ОКВЭДНаименование),
	|	МИНИМУМ(Субсчет9001),
	|	СУММА(СуммаВыручки),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КоличествоОКВЭД)
	|ПО
	|	&УсловиеИтогов";
	Запрос.УстановитьПараметр("ДанныеНоменклатуры", ДанныеНоменклатуры);
	
	МассивПоляГруппировок = ПоляГруппировок.ВыгрузитьЗначения();
	
	ВсеПоляГруппировок = НастраиваемыеПоляГруппировки();
	НенужныеПоля = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ВсеПоляГруппировок.ВыгрузитьЗначения(), МассивПоляГруппировок);
	
	// Удаляем группировку по ОКВЭД, т.к. в дереве ее не показываем.
	МассивПоляГруппировок.Удалить(0);
	УстановитьУсловияИтоговОтборов(Запрос, МассивПоляГруппировок, ПоляОтбора, НенужныеПоля);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбойтиВыборкуДляДеревоДанныхРекурсивно(
		МассивПоляГруппировок, 0, РезультатЗапроса, ДеревоДанных, СоответствиеЦветов, ЦветаДиаграммы, СуммаВыручки, ДанныеНоменклатуры);

КонецПроцедуры

Процедура УстановитьУсловияИтоговОтборов(Запрос, МассивПоляГруппировок, ПоляОтбора, НенужныеПоля)
	
	Если ЗначениеЗаполнено(НенужныеПоля) Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		ПакетЗапроса = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
		ВыбираемыеПоля = ПакетЗапроса.Операторы[0].ВыбираемыеПоля;
		Для Каждого Поле Из НенужныеПоля Цикл
			ВыражениеЗапроса = ВыбираемыеПоля.Найти("ТаблицаДанных." + Поле);
			ВыбираемыеПоля.Удалить(ВыбираемыеПоля.Индекс(ВыражениеЗапроса));
		КонецЦикла;
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	КонецЕсли;
	
	УсловиеИтогов = СтрСоединить(МассивПоляГруппировок, ", ");
	Если ПустаяСтрока(УсловиеИтогов) Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		ПакетЗапроса = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
		ПакетЗапроса.ВыраженияИтогов.Очистить();
		ПакетЗапроса.КонтрольныеТочкиИтогов.Очистить();
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеИтогов", УсловиеИтогов);
	КонецЕсли;
		
	Если ПоляОтбора <> Неопределено Тогда
		Если ПоляОтбора.Свойство("ОКВЭДКод") Тогда
			Если ТипЗнч(ПоляОтбора.ОКВЭДКод) = Тип("Массив") Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбора", "ДанныеНоменклатуры.ОКВЭДКод В (&ОКВЭДКод)");
			Иначе
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбора", "ДанныеНоменклатуры.ОКВЭДКод = &ОКВЭДКод");
			КонецЕсли;
			Запрос.УстановитьПараметр("ОКВЭДКод", ПоляОтбора.ОКВЭДКод);
		Иначе
			НаименованиеПоля = ПоляОтбора.НаименованиеПоля;
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбора", СтрШаблон("ДанныеНоменклатуры.%1 = &ПолеГруппировкиНоменклатуры", НаименованиеПоля));
			Запрос.УстановитьПараметр("ПолеГруппировкиНоменклатуры", ПоляОтбора.ПолеГруппировкиНоменклатуры);
		КонецЕсли;
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОтбора", "Истина");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбойтиВыборкуДляДеревоДанныхРекурсивно(МассивПоляГруппировок, ИндексУровня, ВыборкаРодитель, СтрокаРодитель, СоответствиеЦветов,
	ЦветаДиаграммы, СуммаВыручки, ДанныеНоменклатуры)
	
	Если ИндексУровня = МассивПоляГруппировок.Количество() Тогда
		ИмяПоля = "Номенклатура";
	Иначе
		ИмяПоля = МассивПоляГруппировок[ИндексУровня];
	КонецЕсли;
	ИмяПоляРодителя = Неопределено;
	Если ИндексУровня > 0 Тогда
		ИмяПоляРодителя = МассивПоляГруппировок[ИндексУровня - 1];
	КонецЕсли;
	
	Выборка = ВыборкаРодитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		
		СтрокаДерева = СтрокаДерева(
			СтрокаРодитель, Выборка, ИмяПоля, ИмяПоляРодителя, ИндексУровня, СоответствиеЦветов, ЦветаДиаграммы, СуммаВыручки, ДанныеНоменклатуры);
		Если ИндексУровня < МассивПоляГруппировок.Количество() Тогда 
			ОбойтиВыборкуДляДеревоДанныхРекурсивно(
				МассивПоляГруппировок, ИндексУровня + 1, Выборка, СтрокаДерева, СоответствиеЦветов, ЦветаДиаграммы, СуммаВыручки, ДанныеНоменклатуры);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СтрокаДерева(СтрокаРодитель, Выборка, ИмяПоля, ИмяПоляРодителя, ИндексУровня, СоответствиеЦветов, ЦветаДиаграммы, СуммаВыручки,
	ДанныеНоменклатуры)
	
	Строка = СтрокаРодитель.Строки.Добавить();
	
	Если ТипЗнч(Выборка.КоличествоОКВЭД) = Тип("Число") И Выборка.КоличествоОКВЭД > 1 Тогда
		Строка.ЦветОКВЭДИндекс = 1;
		ДанныеПоПолюГруппировок = ДанныеНоменклатуры.Скопировать();
		Если ИмяПоляРодителя <> Неопределено Тогда
			КолонкиГруппировок = СтрШаблон("%1, %2, %3", ИмяПоля, ИмяПоляРодителя, "ОКВЭДКод");
		Иначе
			КолонкиГруппировок = СтрШаблон("%1, %2", ИмяПоля, "ОКВЭДКод");
		КонецЕсли;
		ДанныеПоПолюГруппировок.Свернуть(КолонкиГруппировок);
		НайденныеСтроки = ДанныеПоПолюГруппировок.НайтиСтроки(Новый Структура(ИмяПоля, Выборка[ИмяПоля]));
		
		СписокКодов = Новый Массив;
		НужноДобавитьПустойКод = Ложь;
		Для Каждого СтрокаДанныеНоменклатуры Из НайденныеСтроки Цикл
			Если ИмяПоляРодителя <> Неопределено И СтрокаДанныеНоменклатуры[ИмяПоляРодителя] <> СтрокаРодитель.ПолеГруппировкиНоменклатуры Тогда
				Продолжить;
			КонецЕсли;
			Если ПустаяСтрока(СтрокаДанныеНоменклатуры.ОКВЭДКод) Тогда
				НужноДобавитьПустойКод = Истина;
			Иначе
				СписокКодов.Добавить(СтрокаДанныеНоменклатуры.ОКВЭДКод);
			КонецЕсли;
		КонецЦикла;
		Если НужноДобавитьПустойКод Тогда
			СписокКодов.Добавить(ПустойОКВЭД());
		КонецЕсли;
		Строка.ОКВЭД = СтрСоединить(СписокКодов, "; ");
		
	ИначеЕсли ПустаяСтрока(Выборка.ОКВЭД) Тогда
		Строка.ЦветОКВЭД = ЦветаДиаграммы.ЦветПустойОКВЭД;
		Строка.ЦветОКВЭДИндекс = 0;
		
	Иначе
		// Цветов в палитре только 30, а кодов ОКВЭД могут выбрать больше 30.
		КоличествоЦветовДляПалитры = СоответствиеЦветов.Количество();
		Пока КоличествоЦветовДляПалитры >= 30 Цикл
			КоличествоЦветовДляПалитры = КоличествоЦветовДляПалитры - 30;
		КонецЦикла;
		НайденныеСтроки = СоответствиеЦветов.НайтиСтроки(Новый Структура("ОКВЭД", Выборка.ОКВЭД));
		Если НайденныеСтроки.Количество() = 0 Тогда
			ЦветОКВЭД = ЦветаДиаграммы.ПалитраЦветов[КоличествоЦветовДляПалитры];
			СтрокаСоответствиеЦветов = СоответствиеЦветов.Добавить();
			СтрокаСоответствиеЦветов.ОКВЭД = Выборка.ОКВЭД;
			СтрокаСоответствиеЦветов.ОКВЭДКод = Выборка.ОКВЭДКод;
			СтрокаСоответствиеЦветов.Цвет = ЦветОКВЭД;
			СтрокаСоответствиеЦветов.ИндексЦвета = КоличествоЦветовДляПалитры;
		Иначе
			ЦветОКВЭД = НайденныеСтроки[0].Цвет;
		КонецЕсли;
		
		НайденныеСтроки = СоответствиеЦветов.НайтиСтроки(Новый Структура("ОКВЭД", Выборка.ОКВЭД));
		Строка.ОКВЭД = Выборка.ОКВЭД;
		Строка.ОКВЭДКод = Выборка.ОКВЭДКод;
		Строка.ОКВЭДНаименование = Выборка.ОКВЭДНаименование;
		Строка.ЦветОКВЭД = ЦветОКВЭД;
		Строка.ЦветОКВЭДИндекс = НайденныеСтроки[0].ИндексЦвета + 2;
	КонецЕсли;

	Строка.ПолеГруппировкиНоменклатуры = Выборка[ИмяПоля];
	Если СуммаВыручки <> 0 Тогда
		Строка.Доля = Окр(Выборка.СуммаВыручки / СуммаВыручки * 100, 2);
	КонецЕсли;
	Строка.Субсчет9001 = Выборка.Субсчет9001;
	Строка.СуммаВыручки = Выборка.СуммаВыручки;
	Строка.ИндексУровня = ИндексУровня;
	
	Возврат Строка;
	
КонецФункции

Процедура ОбновитьДанныеНоменклатуры(ОКВЭД, ПоляПоискаГруппировкиНоменклатуры, ДанныеНоменклатуры, ПоляГруппировок, СоответствиеЦветов, РазделыКодов)
	
	Для Каждого СтрокаПоиска Из ПоляПоискаГруппировкиНоменклатуры Цикл
		Если СтрокаПоиска.ИндексУровня = ПоляГруппировок.Количество() Тогда
			НаименованиеПоля = "Номенклатура";
		Иначе
			НаименованиеПоля = ПоляГруппировок[СтрокаПоиска.ИндексУровня].Значение;
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить(НаименованиеПоля, СтрокаПоиска.ПолеГруппировкиНоменклатуры);
		Если СтрокаПоиска.ПолеГруппировкиРодитель <> Неопределено Тогда
			НаименованиеПоляРодителя = ПоляГруппировок[СтрокаПоиска.ИндексУровня - 1].Значение;
			ПараметрыОтбора.Вставить(НаименованиеПоляРодителя, СтрокаПоиска.ПолеГруппировкиРодитель);
		КонецЕсли;
		
		НайденныеСтроки = ДанныеНоменклатуры.НайтиСтроки(ПараметрыОтбора);
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка.ОКВЭДКод = ОКВЭД.Код;
			Строка.ОКВЭД = ОКВЭД.Представление;
			Строка.ОКВЭДНаименование = ОКВЭД.Наименование;
			Строка.ОКВЭДРаздел = РазделыКодов[(Лев(ОКВЭД.Код, 2))];
		КонецЦикла;
		
	КонецЦикла;
	
	// Если старый ОКВЭД пропадает, то новому возможно стоит присвоить его же цвет.
	Для Каждого СтрокаПоиска Из ПоляПоискаГруппировкиНоменклатуры Цикл
		
		Если Не СтрокаПоиска.Свойство("ОКВЭД") Или ПустаяСтрока(СтрокаПоиска.ОКВЭД) Тогда
			Продолжить;
		КонецЕсли;
		
		СтарыйОКВЭД = СтрокаПоиска.ОКВЭД;
		Если СтрНайти(СтарыйОКВЭД, ";") <> 0 Тогда
			
			Если СтрНайти(СтарыйОКВЭД, ПустойОКВЭД()) <> 0 Тогда
				ОКВЭДСтрока = СтрЗаменить(СтарыйОКВЭД, ПустойОКВЭД(), "");
				СписокОКВЭД = СтрРазделить(ОКВЭДСтрока, "; ", Ложь);
			Иначе
				СписокОКВЭД = СтрРазделить(СтарыйОКВЭД, "; ", Ложь);
			КонецЕсли;
			
			Для Каждого ОКВЭДКод Из СписокОКВЭД Цикл
				ОбработатьСоответствиеЦветов("ОКВЭДКод", ОКВЭДКод, ОКВЭД.Представление, ДанныеНоменклатуры, СоответствиеЦветов);
			КонецЦикла;
			
		Иначе
			ОбработатьСоответствиеЦветов("ОКВЭД", СтрокаПоиска.ОКВЭД, ОКВЭД.Представление, ДанныеНоменклатуры, СоответствиеЦветов);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьСоответствиеЦветов(ИмяПоля, ЗначениеОКВЭД, ОКВЭДПредставление, ДанныеНоменклатуры, СоответствиеЦветов)
	
	НайденныеСтроки = ДанныеНоменклатуры.НайтиСтроки(Новый Структура(ИмяПоля, ЗначениеОКВЭД));
	Если НайденныеСтроки.Количество() = 0 Тогда
		СоответствиеЦветовСтарыйОКВЭД = СоответствиеЦветов.НайтиСтроки(Новый Структура(ИмяПоля, ЗначениеОКВЭД));
		СоответствиеЦветовНовыйОКВЭД = СоответствиеЦветов.НайтиСтроки(Новый Структура("ОКВЭД", ОКВЭДПредставление));
		Если ЗначениеЗаполнено(СоответствиеЦветовСтарыйОКВЭД) И Не ЗначениеЗаполнено(СоответствиеЦветовНовыйОКВЭД) Тогда
			СоответствиеЦветовСтарыйОКВЭД[0].ОКВЭД = ОКВЭДПредставление;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеДиаграммы(ДиаграммаАнализ, ДанныеНоменклатуры, ДеревоДанных, ПоляГруппировок, ЦветаДиаграммы, СоответствиеЦветов,
	ПоляОтбора = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеНоменклатуры.ОКВЭД КАК ОКВЭД,
	|	ДанныеНоменклатуры.ОКВЭДКод КАК ОКВЭДКод,
	|	ДанныеНоменклатуры.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ДанныеНоменклатуры.ГруппаНоменклатурыВерхнегоУровня КАК ГруппаНоменклатурыВерхнегоУровня,
	|	ДанныеНоменклатуры.ГруппаНоменклатурыВторогоУровня КАК ГруппаНоменклатурыВторогоУровня,
	|	ДанныеНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ДанныеНоменклатуры.Номенклатура КАК Номенклатура,
	|	ДанныеНоменклатуры.СуммаВыручки КАК СуммаВыручки
	|ПОМЕСТИТЬ ТаблицаДанных
	|ИЗ
	|	&ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|ГДЕ
	|	&УсловиеОтбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ВложенныйЗапрос.Номенклатура) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаДанных.ОКВЭД КАК ОКВЭД,
	|		ТаблицаДанных.ОКВЭДКод КАК ОКВЭДКод,
	|		ТаблицаДанных.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|		ТаблицаДанных.ГруппаНоменклатурыВерхнегоУровня КАК ГруппаНоменклатурыВерхнегоУровня,
	|		ТаблицаДанных.ГруппаНоменклатурыВторогоУровня КАК ГруппаНоменклатурыВторогоУровня,
	|		ТаблицаДанных.ВидНоменклатуры КАК ВидНоменклатуры,
	|		ТаблицаДанных.Номенклатура КАК Номенклатура,
	|		СУММА(ТаблицаДанных.СуммаВыручки) КАК СуммаВыручки
	|	ИЗ
	|		ТаблицаДанных КАК ТаблицаДанных
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаДанных.ОКВЭДКод,
	|		ТаблицаДанных.ОКВЭД,
	|		ТаблицаДанных.НоменклатурнаяГруппа,
	|		ТаблицаДанных.ГруппаНоменклатурыВерхнегоУровня,
	|		ТаблицаДанных.ГруппаНоменклатурыВторогоУровня,
	|		ТаблицаДанных.ВидНоменклатуры,
	|		ТаблицаДанных.Номенклатура) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.ОКВЭД КАК ОКВЭД,
	|	ТаблицаДанных.ОКВЭДКод КАК ОКВЭДКод,
	|	ТаблицаДанных.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ТаблицаДанных.ГруппаНоменклатурыВерхнегоУровня КАК ГруппаНоменклатурыВерхнегоУровня,
	|	ТаблицаДанных.ГруппаНоменклатурыВторогоУровня КАК ГруппаНоменклатурыВторогоУровня,
	|	ТаблицаДанных.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	СУММА(ТаблицаДанных.СуммаВыручки) КАК СуммаВыручки
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.ОКВЭД,
	|	ТаблицаДанных.ОКВЭДКод,
	|	ТаблицаДанных.НоменклатурнаяГруппа,
	|	ТаблицаДанных.ГруппаНоменклатурыВерхнегоУровня,
	|	ТаблицаДанных.ГруппаНоменклатурыВторогоУровня,
	|	ТаблицаДанных.ВидНоменклатуры,
	|	ТаблицаДанных.Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаВыручки УБЫВ
	|ИТОГИ
	|	МИНИМУМ(ОКВЭД),
	|	МИНИМУМ(ОКВЭДКод),
	|	СУММА(СуммаВыручки)
	|ПО
	|	&УсловиеИтогов";
	
	Запрос.УстановитьПараметр("ДанныеНоменклатуры", ДанныеНоменклатуры);
	
	МассивПоляГруппировок = ПоляГруппировок.ВыгрузитьЗначения();
	ВсеПоляГруппировок = НастраиваемыеПоляГруппировки();
	НенужныеПоля = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ВсеПоляГруппировок.ВыгрузитьЗначения(), МассивПоляГруппировок);

	УстановитьУсловияИтоговОтборов(Запрос, МассивПоляГруппировок, ПоляОтбора, НенужныеПоля);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Выборка = Результаты[Результаты.Количество() - 2].Выбрать();
	ГруппироватьНоменклатуру = Ложь;
	Если Выборка.Следующий() Тогда
		ГруппироватьНоменклатуру = Выборка.Количество > 1000;
	КонецЕсли;
	
	ДиаграммаАнализ.Очистить();
	ДиаграммаАнализ.КоличествоТочек = ПоляГруппировок.Количество() + 1;
	
	СчетчикиДляЧередованияЦвета = Новый ТаблицаЗначений;
	СчетчикиДляЧередованияЦвета.Колонки.Добавить("ИндексУровня", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	СчетчикиДляЧередованияЦвета.Колонки.Добавить("Счетчик", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	Счетчик = 0;
	Для Каждого ПолеГруппировки Из ПоляГруппировок Цикл
		ДиаграммаАнализ.Точки[Счетчик].Текст = ПолеГруппировки;
		СтрокаСчетчикиДляЧередованияЦвета = СчетчикиДляЧередованияЦвета.Добавить();
		СтрокаСчетчикиДляЧередованияЦвета.ИндексУровня = Счетчик;
		СтрокаСчетчикиДляЧередованияЦвета.Счетчик = 0;
		Счетчик = Счетчик + 1;
	КонецЦикла;
	ДиаграммаАнализ.Точки[Счетчик].Текст = "Номенклатура";
	СтрокаСчетчикиДляЧередованияЦвета = СчетчикиДляЧередованияЦвета.Добавить();
	СтрокаСчетчикиДляЧередованияЦвета.ИндексУровня = Счетчик;
	СтрокаСчетчикиДляЧередованияЦвета.Счетчик = 0;

	ДиаграммаАнализ.ПоложениеПодписей = ПоложениеПодписейКДиаграмме.КрайВнутри;
	ДиаграммаАнализ.АвтоУстановкаИменТочек = Ложь;
	ДиаграммаАнализ.ВнутреннийРадиусКольцевойДиаграммы = ВнутреннийРадиусДиаграммы(ДиаграммаАнализ.КоличествоТочек);
	
	ВыборкаПоОКВЭД = Результаты[Результаты.Количество() - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ИндексУровня = 0;
	Пока ВыборкаПоОКВЭД.Следующий() Цикл
		КоличествоСерий = ДиаграммаАнализ.Серии.Количество();
		ДиаграммаАнализ.КоличествоСерий = КоличествоСерий + 1;
		ДиаграммаАнализ.Серии[КоличествоСерий].Значение = ВыборкаПоОКВЭД.ОКВЭД;
		ДиаграммаАнализ.Серии[КоличествоСерий].Текст = ВыборкаПоОКВЭД.ОКВЭД;
		
		Если ПустаяСтрока(ВыборкаПоОКВЭД.ОКВЭД) Тогда
			ЦветОКВЭД = ЦветаДиаграммы.ЦветПустойОКВЭД;
			ДиаграммаАнализ.Серии[КоличествоСерий].Текст = ПустойОКВЭД();
		Иначе
			НайденныеСтроки = СоответствиеЦветов.НайтиСтроки(Новый Структура("ОКВЭД", ВыборкаПоОКВЭД.ОКВЭД));
			ЦветОКВЭД = НайденныеСтроки[0].Цвет;
		КонецЕсли;
		ДиаграммаАнализ.Серии[КоличествоСерий].Цвет = ЦветОКВЭД;
		ДиаграммаАнализ.УстановитьЗначение(ИндексУровня, КоличествоСерий, ?(ВыборкаПоОКВЭД.СуммаВыручки < 0, 0, ВыборкаПоОКВЭД.СуммаВыручки));
		
		ЦветаЧередование = ЦветаЧередование(ЦветОКВЭД);
		ОбойтиВыборкуДляДиаграммыРекурсивно(ДиаграммаАнализ, ВыборкаПоОКВЭД, ИндексУровня + 1, ДеревоДанных, МассивПоляГруппировок, 
			ЦветаЧередование, СчетчикиДляЧередованияЦвета, ГруппироватьНоменклатуру);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбойтиВыборкуДляДиаграммыРекурсивно(ДиаграммаАнализ, ВыборкаРодитель, ИндексУровня, ДеревоДанных, МассивПоляГруппировок,
	ЦветаЧередование, СчетчикиДляЧередованияЦвета, ГруппироватьНоменклатуру)
	
	Если ИндексУровня = МассивПоляГруппировок.Количество() Тогда
		ИмяПоля = "Номенклатура";
	Иначе
		ИмяПоля = МассивПоляГруппировок[ИндексУровня];
	КонецЕсли;
	ИмяПоляРодителя = МассивПоляГруппировок[ИндексУровня - 1];

	Выборка = ВыборкаРодитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ГруппироватьСектор = Ложь;
	СуммаВыручкиРодителя = ВыборкаРодитель.СуммаВыручки;
	Если ИмяПоля = "Номенклатура" И ГруппироватьНоменклатуру Тогда
		ГруппироватьСектор = Выборка.Количество() > 10;
	КонецЕсли;
	
	СуммаГруппировки = Неопределено;
	СуммаНарастающимИтогом = 0;
	ПерваяПозиция = Истина;
	Пока Выборка.Следующий() Цикл
		
		Если ГруппироватьСектор Тогда
			Если СуммаГруппировки = Неопределено Тогда
				СуммаНарастающимИтогом = СуммаНарастающимИтогом + Выборка.СуммаВыручки;
			КонецЕсли;
			
			Если СуммаНарастающимИтогом > СуммаВыручкиРодителя * 0.6 И Не ПерваяПозиция Тогда
				Если СуммаГруппировки = Неопределено Тогда
					НайденныеСтроки = СчетчикиДляЧередованияЦвета.НайтиСтроки(Новый Структура("ИндексУровня", ИндексУровня));
					Счетчик = НайденныеСтроки[0].Счетчик;
					КоличествоСерий = ДиаграммаАнализ.Серии.Количество();
					ДиаграммаАнализ.КоличествоСерий = КоличествоСерий + 1;
					
					ДиаграммаАнализ.Серии[КоличествоСерий].Значение = НСтр("ru = 'Прочее'");
					ДиаграммаАнализ.Серии[КоличествоСерий].Текст = НСтр("ru = 'Прочее'");
					ДиаграммаАнализ.Серии[КоличествоСерий].Цвет = ЦветаЧередование[Счетчик];
					ДиаграммаАнализ.Серии[КоличествоСерий].ОтображатьГрафическоеПредставлениеДанныхВЛегендеДиаграммы = ОтображениеВЛегендеДиаграммы.НеОтображать;
					СуммаГруппировки = Выборка.СуммаВыручки;
					Если НайденныеСтроки[0].Счетчик = 1 Тогда
						НайденныеСтроки[0].Счетчик = 0;
					Иначе
						НайденныеСтроки[0].Счетчик = НайденныеСтроки[0].Счетчик + 1;
					КонецЕсли;
					
				Иначе
					СуммаГруппировки = СуммаГруппировки + Выборка.СуммаВыручки;
				КонецЕсли;
				УстановитьИндексСерии(ДеревоДанных, КоличествоСерий, Выборка[ИмяПоля], ВыборкаРодитель[ИмяПоляРодителя]);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		НайденныеСтроки = СчетчикиДляЧередованияЦвета.НайтиСтроки(Новый Структура("ИндексУровня", ИндексУровня));
		Счетчик = НайденныеСтроки[0].Счетчик;
		КоличествоСерий = ДиаграммаАнализ.Серии.Количество();
		ДиаграммаАнализ.КоличествоСерий = КоличествоСерий + 1;
		
		ДиаграммаАнализ.Серии[КоличествоСерий].Значение = Выборка[ИмяПоля];
		ДиаграммаАнализ.Серии[КоличествоСерий].Текст = Выборка[ИмяПоля];
		ДиаграммаАнализ.Серии[КоличествоСерий].Цвет = ЦветаЧередование[Счетчик];
		ДиаграммаАнализ.Серии[КоличествоСерий].ОтображатьГрафическоеПредставлениеДанныхВЛегендеДиаграммы = ОтображениеВЛегендеДиаграммы.НеОтображать;
		ДиаграммаАнализ.УстановитьЗначение(ИндексУровня, КоличествоСерий, ?(Выборка.СуммаВыручки < 0, 0, Выборка.СуммаВыручки));
		Если НайденныеСтроки[0].Счетчик = 1 Тогда
			НайденныеСтроки[0].Счетчик = 0;
		Иначе
			НайденныеСтроки[0].Счетчик = НайденныеСтроки[0].Счетчик + 1;
		КонецЕсли;
		
		УстановитьИндексСерии(ДеревоДанных, КоличествоСерий, Выборка[ИмяПоля], ВыборкаРодитель[ИмяПоляРодителя]);
		
		Если ИндексУровня < МассивПоляГруппировок.Количество() Тогда 
			ОбойтиВыборкуДляДиаграммыРекурсивно(ДиаграммаАнализ, Выборка, ИндексУровня + 1, ДеревоДанных, МассивПоляГруппировок, 
				ЦветаЧередование, СчетчикиДляЧередованияЦвета, ГруппироватьНоменклатуру);
		КонецЕсли;
		
		ПерваяПозиция = Ложь;
		
	КонецЦикла;
	
	Если ГруппироватьСектор И СуммаГруппировки <> Неопределено Тогда
		ДиаграммаАнализ.УстановитьЗначение(ИндексУровня, КоличествоСерий, СуммаГруппировки);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьИндексСерии(ДеревоДанных, КоличествоСерий, ПолеГруппировкиНоменклатуры, ПолеГруппировкиРодитель)
	
	НайденныеСтроки = ДеревоДанных.Строки.НайтиСтроки(Новый Структура("ПолеГруппировкиНоменклатуры", ПолеГруппировкиНоменклатуры), Истина);
	Для Каждого СтрокаДерева Из НайденныеСтроки Цикл
		Если СтрокаДерева.Родитель <> Неопределено И СтрокаДерева.Родитель.ПолеГруппировкиНоменклатуры <> ПолеГруппировкиРодитель Тогда
			Продолжить;
		КонецЕсли;
		Если Не ПустаяСтрока(СтрокаДерева.ИндексСерии) Тогда
			СтрокаДерева.ИндексСерии = СтрокаДерева.ИндексСерии + "; " + КоличествоСерий;
		Иначе
			СтрокаДерева.ИндексСерии = КоличествоСерий;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ВнутреннийРадиусДиаграммы(КоличествоТочек)
	
	Если КоличествоТочек = 1 Тогда
		Возврат 65;
	ИначеЕсли КоличествоТочек = 2 Тогда
		Возврат 50;
	Иначе
		Возврат 30;
	КонецЕсли;
		
КонецФункции

Функция ЦветаЧередование(ЦветОКВЭД)
	
	МассивЦветов = Новый Массив;
	МассивЦветов.Добавить(ЦветОКВЭД);
	
	КонечныйЦвет = ЦветСветлее(ЦветОКВЭД);
	
	МассивЦветов.Добавить(КонечныйЦвет);
	Возврат МассивЦветов;
	
КонецФункции

Функция ЦветСветлее(ЦветОКВЭД)
	
	Шаг = 12; // прибавляем 12 для более светлого оттенка
	ТекКрасный = Мин(ЦветОКВЭД.Красный + Шаг, 255);
	ТекЗеленый = Мин(ЦветОКВЭД.Зеленый + Шаг, 255);
	ТекСиний = Мин(ЦветОКВЭД.Синий + Шаг, 255);
	
	Возврат Новый Цвет(ТекКрасный, ТекЗеленый, ТекСиний);
	
КонецФункции

Процедура ЗаполнитьОтборПоПолюГруппировки(СписокВыбораДляОтбора, ОтборПоПолюГруппировкиТаблица, ДеревоДанных, ДанныеНоменклатуры, ПоляГруппировок)
	
	ОтборПоПолюГруппировкиТаблица.Очистить();
	
	Индекс = 1;
	Пока Индекс < ПоляГруппировок.Количество() Цикл
		ДанныеПоПолюГруппировки = ДанныеНоменклатуры.Скопировать();
		НаименованиеПоля = ПоляГруппировок[Индекс].Значение;
		ДанныеПоПолюГруппировки.Свернуть(НаименованиеПоля, "СуммаВыручки");
		
		Для Каждого СтрокаДанные Из ДанныеПоПолюГруппировки Цикл
			ПолеГруппировкиНоменклатурыСтрока = ПоляГруппировок[Индекс].Представление + ": "
				+ ?(ЗначениеЗаполнено(СтрокаДанные[НаименованиеПоля]), СтрокаДанные[НаименованиеПоля], ПустойОКВЭД());
			СписокВыбораДляОтбора.Добавить(ПолеГруппировкиНоменклатурыСтрока);
			НоваяСтрока = ОтборПоПолюГруппировкиТаблица.Добавить();
			НоваяСтрока.ПолеГруппировкиНоменклатуры = СтрокаДанные[НаименованиеПоля];
			НоваяСтрока.ПолеГруппировкиНоменклатурыСтрока = ПолеГруппировкиНоменклатурыСтрока;
			НоваяСтрока.НаименованиеПоля = НаименованиеПоля;
		КонецЦикла;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	// Добавим коды ОКВЭД для отбора
	ДанныеОКВЭД = ДанныеНоменклатуры.Скопировать();
	ДанныеОКВЭД.Свернуть("ОКВЭД, ОКВЭДКод", "СуммаВыручки");
	Для Каждого СтрокаДанные Из ДанныеОКВЭД Цикл
		ОКВЭДСтрока = ?(ЗначениеЗаполнено(СтрокаДанные.ОКВЭД), СтрокаДанные.ОКВЭД, ПустойОКВЭД());
		СписокВыбораДляОтбора.Добавить(СтрШаблон(НСтр("ru = 'ОКВЭД: %1'"), ОКВЭДСтрока));
		НоваяСтрока = ОтборПоПолюГруппировкиТаблица.Добавить();
		НоваяСтрока.ПолеГруппировкиНоменклатуры = ОКВЭДСтрока;
		НоваяСтрока.ПолеГруппировкиНоменклатурыСтрока = СтрШаблон(НСтр("ru = 'ОКВЭД: %1'"), ОКВЭДСтрока);
		НоваяСтрока.НаименованиеПоля = НСтр("ru = 'ОКВЭД'");
		НоваяСтрока.ОКВЭДКод = СтрокаДанные.ОКВЭДКод;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьОтменитьОтбор(ПараметрыОтбора, ДеревоДанных, ДиаграммаАнализ, ПоляГруппировок, ДанныеНоменклатуры, СоответствиеЦветов, СуммаВыручки)
	
	Если ЗначениеЗаполнено(ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока) Тогда
		НайденныеСтрокиОтбора = ПараметрыОтбора.ОтборПоПолюГруппировкиТаблица.НайтиСтроки(
			Новый Структура("ПолеГруппировкиНоменклатурыСтрока", ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока));
		Если ЗначениеЗаполнено(НайденныеСтрокиОтбора) Тогда
			ПолеГруппировкиНоменклатуры = НайденныеСтрокиОтбора[0].ПолеГруппировкиНоменклатуры;
			НаименованиеПоля = НайденныеСтрокиОтбора[0].НаименованиеПоля;
			ПоляОтбора = Новый Структура;
			Если НаименованиеПоля = "ОКВЭД" Тогда
				ПоляОтбора.Вставить("ОКВЭД", ПолеГруппировкиНоменклатуры);
				СписокОКВЭД = Неопределено;
				РазложитьОКВЭД(ПолеГруппировкиНоменклатуры, СписокОКВЭД);
				Если ЗначениеЗаполнено(СписокОКВЭД) Тогда
					ПоляОтбора.Вставить("ОКВЭДКод", СписокОКВЭД);
				Иначе
					ПоляОтбора.Вставить("ОКВЭДКод", НайденныеСтрокиОтбора[0].ОКВЭДКод);
				КонецЕсли;
			Иначе
				ПоляОтбора.Вставить("ПолеГруппировкиНоменклатуры", ПолеГруппировкиНоменклатуры);
				ПоляОтбора.Вставить("НаименованиеПоля", НаименованиеПоля);
			КонецЕсли;
			Если ПолеГруппировкиНоменклатуры <> Неопределено И Не ПараметрыОтбора.ОтборФлаг Тогда
				ПараметрыОтбора.ОтборФлаг = Истина;
			КонецЕсли;
		Иначе
			ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока = "";
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыОтбора.ОтборФлаг И ЗначениеЗаполнено(ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока) Тогда
		ЦветаДиаграммы = ЦветаДиаграммы(ДиаграммаАнализ);
		ЗаполнитьДеревоДанных(ДеревоДанных, ПоляГруппировок, ДанныеНоменклатуры, ЦветаДиаграммы, СоответствиеЦветов, СуммаВыручки, ПоляОтбора);
		РассчитатьИтог(СуммаВыручки, ДеревоДанных);
		ЗаполнитьДанныеДиаграммы(ДиаграммаАнализ, ДанныеНоменклатуры, ДеревоДанных, ПоляГруппировок, ЦветаДиаграммы, СоответствиеЦветов, ПоляОтбора);
	Иначе
		ОтменитьОтбор(ПараметрыОтбора, ДеревоДанных, ДиаграммаАнализ, ПоляГруппировок, ДанныеНоменклатуры, СоответствиеЦветов, СуммаВыручки);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтборКоманда(ПараметрыОтбора, ДеревоДанных, ДиаграммаАнализ, ПоляГруппировок, ДанныеНоменклатуры, СоответствиеЦветов, СуммаВыручки)
	
	ПоляОтбора = ПараметрыОтбора.ПоляОтбора;
	Если ПоляОтбора.Свойство("ОКВЭД") Тогда
		НайденныеСтрокиОтбора = ПараметрыОтбора.ОтборПоПолюГруппировкиТаблица.НайтиСтроки(Новый Структура("ПолеГруппировкиНоменклатуры", ПоляОтбора.ОКВЭД));
	Иначе
		НайденныеСтрокиОтбора = ПараметрыОтбора.ОтборПоПолюГруппировкиТаблица.НайтиСтроки(
			Новый Структура("ПолеГруппировкиНоменклатуры", ПоляОтбора.ПолеГруппировкиНоменклатуры));
	КонецЕсли;
	Если ЗначениеЗаполнено(НайденныеСтрокиОтбора) Тогда
		ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока = НайденныеСтрокиОтбора[0].ПолеГруппировкиНоменклатурыСтрока;
	КонецЕсли;
	
	Если ПоляОтбора.Свойство("ОКВЭД") Тогда
		ОКВЭД = ПоляОтбора.ОКВЭД;
		СписокОКВЭД = Неопределено;
		РазложитьОКВЭД(ОКВЭД, СписокОКВЭД);
		Если ЗначениеЗаполнено(СписокОКВЭД) Тогда
			ПоляОтбора.ОКВЭДКод = СписокОКВЭД;
			
			ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока = СтрШаблон(НСтр("ru = 'ОКВЭД: %1'"), ОКВЭД);
			НоваяСтрока = ПараметрыОтбора.ОтборПоПолюГруппировкиТаблица.Добавить();
			НоваяСтрока.ПолеГруппировкиНоменклатуры = ОКВЭД;
			НоваяСтрока.ПолеГруппировкиНоменклатурыСтрока = ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока;
			НоваяСтрока.НаименованиеПоля = НСтр("ru = 'ОКВЭД'");
		КонецЕсли;
	КонецЕсли;
	ПараметрыОтбора.ОтборФлаг = Истина;
	
	ЦветаДиаграммы = ЦветаДиаграммы(ДиаграммаАнализ);
	ЗаполнитьДеревоДанных(ДеревоДанных, ПоляГруппировок, ДанныеНоменклатуры, ЦветаДиаграммы, СоответствиеЦветов, СуммаВыручки, ПоляОтбора);
	РассчитатьИтог(СуммаВыручки, ДеревоДанных);
	ЗаполнитьДанныеДиаграммы(ДиаграммаАнализ, ДанныеНоменклатуры, ДеревоДанных, ПоляГруппировок, ЦветаДиаграммы, СоответствиеЦветов, ПоляОтбора);
	
КонецПроцедуры

Процедура РазложитьОКВЭД(ОКВЭД, СписокОКВЭД)
	
	Если СтрНайти(ОКВЭД, ";") <> 0 Тогда
		Если СтрНайти(ОКВЭД, ПустойОКВЭД()) <> 0 Тогда
			ОКВЭДСтрока = СтрЗаменить(ОКВЭД, ПустойОКВЭД(), "");
			СписокОКВЭД = СтрРазделить(ОКВЭДСтрока, "; ", Ложь);
			СписокОКВЭД.Добавить("");
		Иначе
			СписокОКВЭД = СтрРазделить(ОКВЭД, "; ", Ложь);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ОтменитьОтбор(ПараметрыОтбора, ДеревоДанных, ДиаграммаАнализ, ПоляГруппировок, ДанныеНоменклатуры, СоответствиеЦветов, СуммаВыручки)
	
	ПараметрыОтбора.ОтборПоПолюГруппировкиСтрока = Неопределено;
	ПараметрыОтбора.ОтборФлаг = Ложь;
	
	ЦветаДиаграммы = ЦветаДиаграммы(ДиаграммаАнализ);
	ЗаполнитьДеревоДанных(ДеревоДанных, ПоляГруппировок, ДанныеНоменклатуры, ЦветаДиаграммы, СоответствиеЦветов, СуммаВыручки);
	ЗаполнитьДанныеДиаграммы(ДиаграммаАнализ, ДанныеНоменклатуры, ДеревоДанных, ПоляГруппировок, ЦветаДиаграммы, СоответствиеЦветов);
	
КонецПроцедуры

Процедура РассчитатьИтог(СуммаВыручки, ДеревоДанных)
	
	СуммаВыручки = 0;
	Для Каждого СтрокаДерева Из  ДеревоДанных.Строки Цикл
		СуммаВыручки = СуммаВыручки + СтрокаДерева.СуммаВыручки;
	КонецЦикла;
	
КонецПроцедуры

Функция НастройкиОтбораКомпоновки()
	
	ПоляОтбора = Новый Массив;
	ПоляОтбора.Добавить(Новый ПолеКомпоновкиДанных("ВидДеятельности"));
	ПоляОтбора.Добавить(Новый ПолеКомпоновкиДанных("НоменклатураВыручки"));
	ПоляОтбора.Добавить(Новый ПолеКомпоновкиДанных("Номенклатура"));
	ПоляОтбора.Добавить(Новый ПолеКомпоновкиДанных("НоменклатураВыручки.ВидНоменклатуры"));
	
	ВидыСравненияИсключения = Новый Массив;
	ВидыСравненияИсключения.Добавить(ВидСравненияКомпоновкиДанных.НеРавно);
	ВидыСравненияИсключения.Добавить(ВидСравненияКомпоновкиДанных.НеВСписке);
	ВидыСравненияИсключения.Добавить(ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии);
	ВидыСравненияИсключения.Добавить(ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	Возврат Новый Структура("ПоляОтбора, ВидыСравненияИсключения", ПоляОтбора, ВидыСравненияИсключения);
	
КонецФункции

Функция ЦветаДиаграммы(ДиаграммаАнализ)
	
	ПалитраЦветов = ДиаграммаАнализ.ОписаниеПалитрыЦветов.ПолучитьПалитру();
	ЦветПустойОКВЭД = Новый Цвет(220, 220, 220);
	
	Возврат Новый Структура("ПалитраЦветов, ЦветПустойОКВЭД", ПалитраЦветов, ЦветПустойОКВЭД);
	
КонецФункции

Функция ПустойОКВЭД()
	
	Возврат ОбщегоНазначенияБПКлиентСервер.ПредставлениеНезаполненногоЗначения();
	
КонецФункции

#Область ПоляГруппировкиПоУмолчанию

// Код в этой области используется для определения аналитик, по которым оптимально
// разделять данные о выручке. Сейчас поля группировки ограничены фиксированным списком: группы номенклатуры,
// номенклатурная группа, вид номенклатуры. Также присутствует фиксированная аналитика "Номенклатура", всегда выступающая
// последним уровнем детализации. В перспективе возможно добавление других полей для распределения выручки по кодам ОКВЭД:
// ставка НДС, детали операций (опт/розница, продажа в магазинах/на маркетплейсах и др.), а также поддержка
// произвольного порядка группировок.
// Метод, реализованный в коде, основан на разбиении данных о выручке на максимально однородные блоки,
// например, продажа конкретной группы товаров по определённой ставке НДС целевой группе контрагентов,
// исходя из гипотезы, что выручка в рамках одного вида деятельности имеет схожую структуру.
// Первое поле группировки определяется для всей таблицы, последующие — для крупнейшего блока,
// сформированного предыдущим разбиением.
// Если дальнейшее деление текущей крупнейшей группы теряет смысл
// (например, количество уникальных значений по каждому из полей меньше или равно 1), алгоритм переходит к следующему по размеру блоку,
// оказавшемуся наибольшим после предыдущих группировок.
// Запрос для получения данных о выручке аналогичен основному запросу формы распределения, но дополнен вспомогательными полями
// и исключает сумму выручки. Для оптимизации реализовано получение этих данных отдельными методами через различные запросы.

Функция ДанныеНоменклатурыДетальнаяАналитика(Контекст)

	Запрос = Новый Запрос;

	Счета9001 = Новый Массив;
	Счета9001.Добавить(ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД);
	Счета9001.Добавить(ПланыСчетов.Хозрасчетный.ВыручкаЕНВД);
	Запрос.Параметры.Вставить("Счета9001", Счета9001);

	Счета9002 = Новый Массив;
	Счета9002.Добавить(ПланыСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД);
	Счета9002.Добавить(ПланыСчетов.Хозрасчетный.СебестоимостьПродажЕНВД);
	Запрос.Параметры.Вставить("Счета9002", Счета9002);
	Запрос.УстановитьПараметр("СубконтоНоменклатура", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);

	Запрос.Параметры.Вставить("НачалоПериода", Контекст.ДатаНачала);
	Запрос.Параметры.Вставить("КонецПериода", КонецДня(Контекст.ДатаОкончания));
	
	Запрос.Параметры.Вставить("Организация", Контекст.Организация);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйДвиженияССубконто.Регистратор КАК Регистратор,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК НоменклатурнаяГруппа,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ХозрасчетныйДвиженияССубконто.СчетКт КАК СчетаВыручки,
	|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Справочник.Контрагенты) КАК Контрагент,
	|	ХозрасчетныйДвиженияССубконто.СчетДт КАК СчетРасчетовСКонтрагентом
	|ПОМЕСТИТЬ Движения9001
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			СчетКт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Выручка))
	|				И Организация = &Организация,
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйДвиженияССубконто.Регистратор,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК Справочник.Номенклатура),
	|	ХозрасчетныйДвиженияССубконто.СчетКт,
	|	ВЫРАЗИТЬ(ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК Справочник.Контрагенты),
	|	ХозрасчетныйДвиженияССубконто.СчетДт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ХозрасчетныйОбороты.СчетКт) КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.ВидСубконтоКт1 = &СубконтоНоменклатура
	|			ТОГДА ХозрасчетныйОбороты.СубконтоКт1
	|		КОГДА ХозрасчетныйОбороты.ВидСубконтоКт2 = &СубконтоНоменклатура
	|			ТОГДА ХозрасчетныйОбороты.СубконтоКт2
	|		ИНАЧЕ ХозрасчетныйОбороты.СубконтоКт3
	|	КОНЕЦ КАК Номенклатура,
	|	ХозрасчетныйОбороты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ СчетаУчетаНоменклатуры
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			СчетДт В ИЕРАРХИИ (&Счета9002)
	|				И Организация = &Организация,
	|			,
	|			) КАК ХозрасчетныйОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Регистратор,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОбороты.ВидСубконтоКт1 = &СубконтоНоменклатура
	|			ТОГДА ХозрасчетныйОбороты.СубконтоКт1
	|		КОГДА ХозрасчетныйОбороты.ВидСубконтоКт2 = &СубконтоНоменклатура
	|			ТОГДА ХозрасчетныйОбороты.СубконтоКт2
	|		ИНАЧЕ ХозрасчетныйОбороты.СубконтоКт3
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения9001.Регистратор КАК Регистратор,
	|	Движения9001.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Движения9001.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(Движения9001.Номенклатура.Услуга, ИСТИНА) КАК ЭтоУслуга,
	|	Движения9001.СтавкаНДС КАК СтавкаНДС,
	|	Движения9001.СчетаВыручки КАК СчетаВыручки,
	|	Движения9001.Контрагент.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|		И НЕ Движения9001.Контрагент.ИндивидуальныйПредприниматель КАК ЭтоРозница,
	|	СчетаУчетаНоменклатуры.СчетУчета КАК СчетУчета,
	|	Движения9001.СчетРасчетовСКонтрагентом КАК СчетРасчетовСКонтрагентом,
	|	Движения9001.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ИтоговыеДанные
	|ИЗ
	|	Движения9001 КАК Движения9001
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаУчетаНоменклатуры КАК СчетаУчетаНоменклатуры
	|		ПО Движения9001.Регистратор = СчетаУчетаНоменклатуры.Регистратор
	|			И Движения9001.Номенклатура = СчетаУчетаНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ИтоговыеДанные.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|			ТОГДА ТИП(Документ.РеализацияТоваровУслуг)
	|		ИНАЧЕ ТИПЗНАЧЕНИЯ(ИтоговыеДанные.Регистратор)
	|	КОНЕЦ КАК ДокументРегистратор,
	|	ИтоговыеДанные.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ИтоговыеДанные.Номенклатура КАК Номенклатура,
	|	ИтоговыеДанные.ЭтоУслуга КАК Услуга,
	|	ИтоговыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	ИтоговыеДанные.СчетаВыручки КАК Субсчет9001,
	|	ИтоговыеДанные.ЭтоРозница КАК ЭтоРозница,
	|	ИтоговыеДанные.СчетУчета КАК СчетУчета,
	|	ИтоговыеДанные.Контрагент КАК Контрагент,
	|	ИтоговыеДанные.СчетРасчетовСКонтрагентом КАК СчетРасчетовСКонтрагентом,
	|	ИтоговыеДанные.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ГруппаНоменклатурыВерхнегоУровня,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ГруппаНоменклатурыВторогоУровня
	|ИЗ
	|	ИтоговыеДанные КАК ИтоговыеДанные";

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Процедура ЗаполнитьГруппыНоменклатуры(ДанныеНоменклатуры)
	
	ИерархияСправочника = ИерархияСправочника(ДанныеНоменклатуры);
	
	ОтборЭлементов = Новый Структура("ЭтоГруппа", Ложь);
	СтрокиЭлементов = ИерархияСправочника.Строки.НайтиСтроки(ОтборЭлементов, Истина);
	
	СоответствиеГрупп = СоответствиеГрупп(СтрокиЭлементов);
	
	Для Каждого СтрокаДанных Из ДанныеНоменклатуры Цикл
		
		ГруппыНоменклатуры = СоответствиеГрупп[СтрокаДанных.Номенклатура];
		
		Если ГруппыНоменклатуры = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаДанных, ГруппыНоменклатуры);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоответствиеГрупп(Знач СтрокиЭлементов)
	
	СоответствиеГрупп = Новый Соответствие;
	
	Для Каждого СтрокаЭлемента Из СтрокиЭлементов Цикл
		ГруппыНоменклатуры = НовыйГруппыНоменклатуры();
		СтрокаРодителя = СтрокаЭлемента.Родитель;
		Пока СтрокаРодителя <> Неопределено Цикл
			Уровень = УровеньИерархии(СтрокаРодителя);
			Если Уровень = 1 Тогда
				ГруппыНоменклатуры.ГруппаНоменклатурыВерхнегоУровня = СтрокаРодителя.Номенклатура;
			ИначеЕсли Уровень = 2 Тогда
				ГруппыНоменклатуры.ГруппаНоменклатурыВторогоУровня = СтрокаРодителя.Номенклатура;
			КонецЕсли;
			СтрокаРодителя = СтрокаРодителя.Родитель;
		КонецЦикла;
		СоответствиеГрупп.Вставить(СтрокаЭлемента.Номенклатура, ГруппыНоменклатуры);
	КонецЦикла;
	
	Возврат СоответствиеГрупп;

КонецФункции

Функция ИерархияСправочника(Знач ДанныеНоменклатуры)
	
	ВсяНоменклатура = ДанныеНоменклатуры.ВыгрузитьКолонку("Номенклатура");
	
	СКДИерархии = ПолучитьМакет("ИерархияНоменклатуры");
	Настройки = СКДИерархии.НастройкиПоУмолчанию;
	
	Параметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВсяНоменклатура"));
	
	Если Параметр <> Неопределено Тогда
		Параметр.Значение = ВсяНоменклатура;
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СКДИерархии, Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет);
	
	ИерархияСправочника = Новый ДеревоЗначений;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ИерархияСправочника);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат ИерархияСправочника;

КонецФункции

Функция УровеньИерархии(СтрокаДерева)
	
	Возврат СтрокаДерева["СистемныеПоляУровень"];
	
КонецФункции

Функция НовыйГруппыНоменклатуры()
	
	ГруппыНоменклатуры = Новый Структура;
	
	ГруппыНоменклатуры.Вставить("ГруппаНоменклатурыВерхнегоУровня", Справочники.Номенклатура.ПустаяСсылка());
	ГруппыНоменклатуры.Вставить("ГруппаНоменклатурыВторогоУровня", Справочники.Номенклатура.ПустаяСсылка());
	
	Возврат ГруппыНоменклатуры;
	
КонецФункции

Функция ВычисленныеПоляГруппировки(ДанныеНоменклатуры)
	
	ВычисленныеПоляГруппировки = Новый СписокЗначений;
	СократитьСтатичныеКолонки(ДанныеНоменклатуры, "Номенклатура");
	
	Если ДанныеНоменклатуры.Количество() <= 1 Тогда
		Возврат ВычисленныеПоляГруппировки;
	КонецЕсли;
	
	ПоляДеления = Новый Массив;
	// При определении полей не принимается во внимание сумма выручки, разбиение выполняется так,
	// чтобы в результате разбиения части таблицы получались как можно более "однородными".
	// Очевидно, что лучшую одноднородность обеспечивает наиболее детальное разбиение (например, по номенклатуре).
	// Чтобы нивелировать этот эффект увеличиваем приоритет у полей, которые дают "разумное" количество уникальных значений.
	// В качестве порога используем количество, равное 10.
	ЗаполнитьПоляИерархии(ДанныеНоменклатуры.Скопировать(), ПоляДеления, ДанныеНоменклатуры);
	СтандартныеПоля = НастраиваемыеПоляГруппировки();
	
	Для Каждого ПолеДеления Из ПоляДеления Цикл
		
		ПолеГруппировки = СтандартныеПоля.НайтиПоЗначению(ПолеДеления);
		
		Если ПолеГруппировки <> Неопределено Тогда
			ВычисленныеПоляГруппировки.Добавить(ПолеГруппировки.Значение, ПолеГруппировки.Представление, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВычисленныеПоляГруппировки;
	
КонецФункции

Процедура СократитьСтатичныеКолонки(ТаблицаАнализа, ИсключаяПоле = "")

	ИндексПоследнейКолонки = ТаблицаАнализа.Колонки.Количество() - 1;
	
	Для ИндексКолонки = 0 По ИндексПоследнейКолонки Цикл
		
		ИндексУдаляемойКолонки = ИндексПоследнейКолонки - ИндексКолонки;
		ИмяКолонки = ТаблицаАнализа.Колонки.Получить(ИндексУдаляемойКолонки).Имя;
		
		Если ИмяКолонки = ИсключаяПоле Тогда
			Продолжить;
		КонецЕсли;
		
		Проверка = ТаблицаАнализа.Скопировать(, ИмяКолонки);
		Проверка.Свернуть(ИмяКолонки);
		
		Если Проверка.Количество() = 1 Тогда
			
			Если ИсключаяПоле = "" Тогда
				ТаблицаАнализа.Колонки.Удалить(ИмяКолонки);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоляИерархии(ТаблицаАнализа, ПоляДеления, ИсходнаяТаблица)

	Оценка = Оценка(ТаблицаАнализа);
	Поле = "";
	НастраиваемыеГруппировки = НастраиваемыеПоляГруппировки();
	
	Если Оценка.Количество() > 0
		И НастраиваемыеГруппировки.НайтиПоЗначению(Оценка[0].Показатель) <> Неопределено Тогда
		
		Поле = Оценка[0].Показатель;
		ПоляДеления.Добавить(Оценка[0].Показатель);
		
	Иначе
		// Ищем поля для следующей "наибольшей" части.
		ТестированиеГруппировки = ИсходнаяТаблица.Скопировать();
		ТестированиеГруппировки.Свернуть(СтрСоединить(ПоляДеления, ","));
		
		Если ТестированиеГруппировки.Количество() = ИсходнаяТаблица.Количество() Тогда
			// Разбиение будет по всем группам по детальным записям.
			Возврат;
		КонецЕсли;
		
		МаксимальноеКоличествоСтрок = 1;
		СоставКолонок = Новый Структура;
		
		Для Каждого СтрокаПоиска Из ТестированиеГруппировки Цикл
			
			Отбор = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаПоиска);
			КоличествоНайденныхСтрок = ИсходнаяТаблица.НайтиСтроки(Отбор).Количество();
			
			Если КоличествоНайденныхСтрок > МаксимальноеКоличествоСтрок Тогда
				СоставКолонок = Отбор;
				МаксимальноеКоличествоСтрок = КоличествоНайденныхСтрок;
			КонецЕсли;
			
		КонецЦикла;
		
		Если МаксимальноеКоличествоСтрок > 1 Тогда
			
			ТаблицаАнализа = ИсходнаяТаблица.Скопировать(СоставКолонок);
			СписокКолонок = СписокКолонокСвернутойТаблицы(ТаблицаАнализа, "");
			СписокКолонок = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СписокКолонок, ПоляДеления);
			ТаблицаАнализа.Свернуть(СтрСоединить(СписокКолонок, ","));
			СократитьСтатичныеКолонки(ТаблицаАнализа);
			
			СписокКолонок = СписокКолонокСвернутойТаблицы(ТаблицаАнализа, "");
			КоличествоНастраиваемыхПолей = НастраиваемыеГруппировки.Количество();
			// Проверяем, осталось ли вообще что-то для анализа: из массива настраиваемых полей вычтем массив имен колонок таблицы.
			// Если в массиве настраиваемых группировок ничего не изменилось, значит поля таблицы и полей настроек не пересекаются, больше настраивать нечего.
			// Разность массивов можно делать и в противоположную сторону.
			Если ОбщегоНазначенияКлиентСервер.РазностьМассивов(НастраиваемыеГруппировки.ВыгрузитьЗначения(), СписокКолонок).Количество() = КоличествоНастраиваемыхПолей Тогда
				Возврат;
			КонецЕсли;
			
			ЗаполнитьПоляИерархии(ТаблицаАнализа, ПоляДеления, ИсходнаяТаблица);
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;

	ТаблицаАнализаПоПолю = ТаблицаАнализа.Скопировать(, Поле);
	ТаблицаАнализаПоПолю.Колонки.Добавить("Количество");
	ТаблицаАнализаПоПолю.ЗаполнитьЗначения(1, "Количество");
	ТаблицаАнализаПоПолю.Свернуть(Поле, "Количество");
	ТаблицаАнализаПоПолю.Сортировать("Количество Убыв");

	СамоеЧастоеЗначение = ТаблицаАнализаПоПолю[0][Поле];

	СписокКолонок = СписокКолонокСвернутойТаблицы(ТаблицаАнализа, Поле);
	КолонкиСвернутойТаблицы = СтрСоединить(СписокКолонок, ",");
	ОтборСтрок = Новый Структура(Поле, СамоеЧастоеЗначение);
	
	ТаблицаСамоеЧастоеЗначение = ТаблицаАнализа.Скопировать(ОтборСтрок, КолонкиСвернутойТаблицы);
	ТаблицаСамоеЧастоеЗначение.Свернуть(КолонкиСвернутойТаблицы);
	
	СократитьСтатичныеКолонки(ТаблицаСамоеЧастоеЗначение);
	
	ЗаполнитьПоляИерархии(ТаблицаСамоеЧастоеЗначение, ПоляДеления, ИсходнаяТаблица);

КонецПроцедуры

Функция СписокКолонокСвернутойТаблицы(ТаблицаАнализа, Поле)
	
	СписокКолонок = Новый Массив;
	Для Каждого Колонка Из ТаблицаАнализа.Колонки Цикл
		Если Колонка.Имя = Поле Тогда
			Продолжить;
		КонецЕсли;
		СписокКолонок.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Возврат СписокКолонок;
	
КонецФункции

Функция НовыйОценка()

	Оценка = Новый ТаблицаЗначений();
	Оценка.Колонки.Добавить("Показатель", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	Оценка.Колонки.Добавить("Значение", ОбщегоНазначения.ОписаниеТипаЧисло(15, 7));
	Оценка.Колонки.Добавить("Приоритет", ОбщегоНазначения.ОписаниеТипаЧисло(2, 0));
	Оценка.Колонки.Добавить("КоличествоСтрок", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));
	
	Возврат Оценка;
	
КонецФункции

Функция Оценка(ТаблицаАнализа, ОграничиватьМаксимальноеКоличествоУзлов = Истина)
	
	ОграничениеКоличествоСтрок = 10;
	Оценка = НовыйОценка();
	ЗависимыеКолонки = ЗависимыеКолонки();

	Для Каждого Колонка Из ТаблицаАнализа.Колонки Цикл
		
		ЭнтропияНенормированная = Новый Структура;
		
		Поле = Колонка.Имя;
		
		РазличныеЗначения = РазличныеЗначения(ТаблицаАнализа, Поле);
		
		Если РазличныеЗначения.Количество() = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		КоличестваРазличныхЗначений = Новый Соответствие();
		
		Для Каждого КолонкаТаблицы Из ТаблицаАнализа.Колонки Цикл
			
			ИмяКолонки = КолонкаТаблицы.Имя;
			
			Если ЗависимыеКолонки.Найти(ИмяКолонки) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ЭнтропияНенормированная.Вставить(ИмяКолонки, 0);
			
		КонецЦикла;
		
		Для Каждого Значение Из РазличныеЗначения Цикл
			СтрокиПоЗначению = ТаблицаАнализа.Скопировать(Новый Структура(Поле, Значение));
			ДобавитьЭнтропию(ЭнтропияНенормированная, СтрокиПоЗначению, ТаблицаАнализа, КоличестваРазличныхЗначений);
		КонецЦикла;
		
		ЭнтропияНормированнаяСумма = 0;

		Для Каждого КлючИЗНачение Из ЭнтропияНенормированная Цикл
			
			КоличествоРазличныхЗначенийПоПолю = РазличныеЗначения(ТаблицаАнализа, КлючИЗНачение.Ключ).Количество();
			Дельта = Макс(КоличествоРазличныхЗначенийПоПолю, РазличныеЗначения.Количество());
			Делитель = Мин(КоличествоРазличныхЗначенийПоПолю * РазличныеЗначения.Количество(), ТаблицаАнализа.Количество()) - Дельта;
			ЭнтропияНормированнаяСумма = ЭнтропияНормированнаяСумма + ?(Делитель = 0, 0, (ЭнтропияНенормированная[КлючИЗНачение.Ключ] - Дельта) / Делитель);
			
		КонецЦикла;
		
		НоваяСтрока = Оценка.Добавить();
		НоваяСтрока.Показатель = Поле;
		НоваяСтрока.Значение = ЭнтропияНормированнаяСумма;
		
		Если РазличныеЗначения.Количество() > ОграничениеКоличествоСтрок И ОграничениеКоличествоСтрок <> 0  Тогда
			НоваяСтрока.Приоритет = 1;
		КонецЕсли;
		
		НоваяСтрока.КоличествоСтрок = РазличныеЗначения.Количество();
		
	КонецЦикла;
	
	НастраиваемыеПоляГруппировки = НастраиваемыеПоляГруппировки();
	
	Для Каждого СтрокаОценки Из Оценка Цикл
		
		Если НастраиваемыеПоляГруппировки.НайтиПоЗначению(СтрокаОценки.Показатель) = Неопределено Тогда
			СтрокаОценки.Приоритет = 2;
		КонецЕсли;
		
	КонецЦикла;
	
	Оценка.Сортировать("Приоритет, Значение Возр");

	Возврат Оценка;

КонецФункции

Процедура ДобавитьЭнтропию(ЭнтропияНормированнаяСумма, СтрокиПоЗначению, ТаблицаАнализа, КоличестваРазличныхЗначений)

	ЗависимыеКолонки = ЗависимыеКолонки();
	Для Каждого Колонка Из ТаблицаАнализа.Колонки Цикл
		
		Поле = Колонка.Имя;
		
		Если ЗависимыеКолонки.Найти(Поле) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоРазличныхЗначенийИсходное = КоличестваРазличныхЗначений[Поле + "Исходная"];
		
		Если КоличествоРазличныхЗначенийИсходное = Неопределено Тогда
			
			РазличныеЗначенияИсходная = РазличныеЗначения(ТаблицаАнализа, Поле);
			КоличествоРазличныхЗначенийИсходное = РазличныеЗначенияИсходная.Количество();
			КоличестваРазличныхЗначений.Вставить(Поле + "Исходная", КоличествоРазличныхЗначенийИсходное);
			
		КонецЕсли;
		
		РазличныеЗначения = РазличныеЗначения(СтрокиПоЗначению, Поле);
		КоличествоРазличныхЗначенийПослеДеления = РазличныеЗначения.Количество();
		ЭнтропияНормированнаяСумма.Вставить(Поле, ЭнтропияНормированнаяСумма[Поле] + КоличествоРазличныхЗначенийПослеДеления);
		
	КонецЦикла;

КонецПроцедуры

// Список колонок, значения в которых напрямую зависят от значений других колонок, поэтому "хаотичность" значений в этих колонках
// неправильно принимать во внимание при выборе группировок деления. Однако это не исключает того, что эти разрезы,
// являющиеся зависимыми колонками, могут выступать в качестве группировок деления.
// 
// Возвращаемое значение:
//  Массив Из Строка
//
Функция ЗависимыеКолонки()
	
	ЗависимыеКолонки = Новый Массив;
	ЗависимыеКолонки.Добавить("Услуга");
	ЗависимыеКолонки.Добавить("ГруппаНоменклатурыВерхнегоУровня");
	ЗависимыеКолонки.Добавить("ГруппаНоменклатурыВторогоУровня");
	ЗависимыеКолонки.Добавить("ГруппаНоменклатурыТретьегоУровня");
	ЗависимыеКолонки.Добавить("ВидНоменклатуры");

	Возврат ЗависимыеКолонки;
	
КонецФункции

Функция РазличныеЗначения(ТаблицаАнализа, Поле)
	
	Копия = ТаблицаАнализа.Скопировать(, Поле);
	Копия.Свернуть(Поле);
	
	Возврат Копия.ВыгрузитьКолонку(Поле);
	
КонецФункции

#КонецОбласти

Процедура ДополнитьИзбранноеНовымиКодами(Организация, ДанныеНоменклатуры)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныеКодыОКВЭД = ДанныеНоменклатуры.ВыгрузитьКолонку("ОКВЭДКод"); // Массив из Строка
	ВыбранныеКодыОКВЭД = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВыбранныеКодыОКВЭД); // Массив из Строка
	ИндексПустогоГода = ВыбранныеКодыОКВЭД.Найти("");
	
	Если ИндексПустогоГода <> Неопределено Тогда
		ВыбранныеКодыОКВЭД.Удалить(ИндексПустогоГода);
	КонецЕсли;
	
	НастройкаФормы = КлассификаторОКВЭДБП.НастройкаФормыВыбораКодов(Организация);
	
	ИзбранноеВключеноТекущее = НастройкаФормы.ИзбранноеВключено.ВыгрузитьЗначения();
	ИзбранноеВключеноНовое = ОбщегоНазначения.СкопироватьРекурсивно(ИзбранноеВключеноТекущее);
	ИзбранноеИсключено = НастройкаФормы.ИзбранноеИсключено.ВыгрузитьЗначения();
	
	ДобавитьВИзбранное = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ВыбранныеКодыОКВЭД, ИзбранноеИсключено);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИзбранноеВключеноНовое, ДобавитьВИзбранное, Истина);
	
	ТребуетсяЗаписьНастроек = Не ОбщегоНазначения.КоллекцииИдентичны(
		ИзбранноеВключеноНовое,
		ИзбранноеВключеноТекущее);
		
	Если ТребуетсяЗаписьНастроек Тогда
		
		НастройкаФормы.ИзбранноеВключено.ЗагрузитьЗначения(ИзбранноеВключеноНовое);
		
		КлассификаторОКВЭДБП.СохранитьНастройкуФормыВыбораКодов(НастройкаФормы, Организация);
			
	КонецЕсли;
		
КонецПроцедуры

Функция ДанныеПроверкиОсновногоКодаОКВЭД(ДанныеНоменклатуры, СуммаВыручки, ОКВЭДОрганизацииКод)
	
	ДанныеПроверкиОсновногоКодаОКВЭД = Новый Структура;
	ДанныеПроверкиОсновногоКодаОКВЭД.Вставить("ОсновнойКод", Неопределено);
	ДанныеПроверкиОсновногоКодаОКВЭД.Вставить("Комментарий", "");
	
	// В первом результате запроса отбираем данные с максимальной выручкой "сверху вниз", т.е. сначала определяем раздел, потом класс, потом подкласс кода ОКВЭД.
	// Во втором определяем код ОКВЭД с максимальной долей выручки, нужен для вывода комментария.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДанныеНоменклатуры", ДанныеНоменклатуры);
	Запрос.УстановитьПараметр("ОКВЭДОрганизацииКод", ОКВЭДОрганизацииКод);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеНоменклатуры.ОКВЭДРаздел КАК ОКВЭДРаздел,
	|	ДанныеНоменклатуры.ОКВЭД КАК ОКВЭД,
	|	ДанныеНоменклатуры.ОКВЭДКод КАК ОКВЭДКод,
	|	ДанныеНоменклатуры.СуммаВыручки КАК СуммаВыручки,
	|	ВЫБОР
	|		КОГДА ДанныеНоменклатуры.ОКВЭДКод = &ОКВЭДОрганизацииКод
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Приоритет
	|ПОМЕСТИТЬ ДанныеНоменклатуры
	|ИЗ
	|	&ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеНоменклатуры.ОКВЭД КАК ОКВЭД,
	|	ДанныеНоменклатуры.ОКВЭДКод КАК ОКВЭДКод,
	|	ДанныеНоменклатуры.ОКВЭДРаздел КАК ОКВЭДРаздел,
	|	ВЫРАЗИТЬ(ДанныеНоменклатуры.ОКВЭДКод КАК СТРОКА(2)) КАК ОКВЭДКласс,
	|	ВЫРАЗИТЬ(ДанныеНоменклатуры.ОКВЭДКод КАК СТРОКА(4)) КАК ОКВЭДПодкласс,
	|	СУММА(ДанныеНоменклатуры.СуммаВыручки) КАК СуммаВыручки,
	|	МАКСИМУМ(ДанныеНоменклатуры.Приоритет) КАК Приоритет
	|ИЗ
	|	ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|ГДЕ
	|	ДанныеНоменклатуры.ОКВЭДРаздел <> """"
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеНоменклатуры.ОКВЭД,
	|	ДанныеНоменклатуры.ОКВЭДКод,
	|	ДанныеНоменклатуры.ОКВЭДРаздел,
	|	ВЫРАЗИТЬ(ДанныеНоменклатуры.ОКВЭДКод КАК СТРОКА(2)),
	|	ВЫРАЗИТЬ(ДанныеНоменклатуры.ОКВЭДКод КАК СТРОКА(4))
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаВыручки УБЫВ,
	|	Приоритет
	|ИТОГИ
	|	СУММА(СуммаВыручки)
	|ПО
	|	ОБЩИЕ,
	|	ОКВЭДРаздел,
	|	ОКВЭДКласс,
	|	ОКВЭДПодкласс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеНоменклатуры.ОКВЭДКод КАК ОКВЭДКод,
	|	ДанныеНоменклатуры.ОКВЭДРаздел КАК ОКВЭДРаздел,
	|	СУММА(ДанныеНоменклатуры.СуммаВыручки) КАК СуммаВыручки,
	|	МАКСИМУМ(ДанныеНоменклатуры.Приоритет) КАК Приоритет
	|ИЗ
	|	ДанныеНоменклатуры КАК ДанныеНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеНоменклатуры.ОКВЭДКод,
	|	ДанныеНоменклатуры.ОКВЭДРаздел
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаВыручки УБЫВ,
	|	Приоритет";
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ВыборкаИтоги = Результаты[Результаты.Количество() - 2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаИтоги.Следующий() Тогда
		СуммаВыручкиОКВЭДУказан = ВыборкаИтоги.СуммаВыручки;
		СуммаВыручкиОсталось = СуммаВыручки - СуммаВыручкиОКВЭДУказан;
		
		ВыборкаРаздел = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаРаздел.Следующий() Тогда
			ОКВЭДРаздел = ВыборкаРаздел.ОКВЭДРаздел;
			Если Не ДостаточноУказаноКодов(ВыборкаРаздел, СуммаВыручкиОсталось) Тогда
				Возврат ДанныеПроверкиОсновногоКодаОКВЭД;
			КонецЕсли;
			
			ВыборкаКласс = ВыборкаРаздел.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Если ВыборкаКласс.Следующий() Тогда
				
				Если Не ДостаточноУказаноКодов(ВыборкаКласс, СуммаВыручкиОсталось) Тогда
					Возврат ДанныеПроверкиОсновногоКодаОКВЭД;
				КонецЕсли;
				
				ВыборкаПодкласс = ВыборкаКласс.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Если ВыборкаПодкласс.Следующий() Тогда
					Если Не ДостаточноУказаноКодов(ВыборкаПодкласс, СуммаВыручкиОсталось) Тогда
						Возврат ДанныеПроверкиОсновногоКодаОКВЭД;
					КонецЕсли;
					
					Выборка = ВыборкаПодкласс.Выбрать();
					Если Выборка.Следующий() Тогда
						ДанныеПроверкиОсновногоКодаОКВЭД.ОсновнойКод = Новый Структура;
						ДанныеПроверкиОсновногоКодаОКВЭД.ОсновнойКод.Вставить("Код", Выборка.ОКВЭДКод);
						ДанныеПроверкиОсновногоКодаОКВЭД.ОсновнойКод.Вставить("Представление", Выборка.ОКВЭД);
						ДанныеПроверкиОсновногоКодаОКВЭД.ОсновнойКод.Вставить("Наименование", КлассификаторОКВЭДБП.НаименованиеПоКоду(Выборка.ОКВЭДКод));
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеПроверкиОсновногоКодаОКВЭД.ОсновнойКод) Тогда
		Возврат ДанныеПроверкиОсновногоКодаОКВЭД;
	КонецЕсли;
	
	// Комментарий будем выводить только, если разделы не совпадают.
	Выборка = Результаты[Результаты.Количество() - 1].Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если Выборка.ОКВЭДРаздел <> ОКВЭДРаздел Тогда
			Разделы = КлассификаторОКВЭД.Разделы();
			СтрокаРаздела = Разделы.Найти(ОКВЭДРаздел, "Раздел");
			ТекстКомментария = СтрШаблон(НСтр("ru = 'Самый большой процент выручки у раздела %1. %2.'"),
				ОКВЭДРаздел, СтрокаРаздела.Наименование);
			ШаблонСтроки =
				"%1
				|%2 <a href = ""%3"">%4</a>.";
			
			ДанныеПроверкиОсновногоКодаОКВЭД.Комментарий = СтроковыеФункции.ФорматированнаяСтрока(
				ШаблонСтроки,
				ТекстКомментария,
				НСтр("ru = 'Именно в этом разделе'"),
				"https://its.1c.ru/db/newscomm/content/495964/hdoc",
				НСтр("ru = 'определяем код с максимальной выручкой'"));
				
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеПроверкиОсновногоКодаОКВЭД;
	
КонецФункции

Функция ДостаточноУказаноКодов(Выборка, СуммаВыручкиОсталось)
	
	// Проверим следующий раздел/класс/подкласс, если его укажут позициям, где еще не выбран ОКВЭД, то сумма по нему может превысить сумму по данному разделу/классу/подклассу.
	// В этом случае раздел/класс/подкласс с максимальной выручкой еще пока установить нельзя.
	Сумма = Выборка.СуммаВыручки;
	
	Если Сумма > СуммаВыручкиОсталось Тогда
		Если Выборка.Следующий() Тогда
			Если Сумма < СуммаВыручкиОсталось + Выборка.СуммаВыручки Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Выборка.Сбросить();
		Выборка.Следующий();
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли