#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ПолеГруппировки Из Параметры.ПоляГруппировок Цикл
		Если ПолеГруппировки.Значение = "ОКВЭД" Тогда
			Продолжить;
		КонецЕсли;
		Список.Добавить(ПолеГруппировки.Значение, ПолеГруппировки.Представление, Истина);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры, "Организация, ДатаНачала, ДатаОкончания");
	
	Список.Добавить("Номенклатура", НСтр("ru = 'Номенклатура'"), Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокЗначение");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СписокПометка");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.Значение", ВидСравненияКомпоновкиДанных.Равно, "Номенклатура");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ОбщегоНазначенияВызовСервера.ЦветСтиля("ЦветНеАктивнойСтроки"));
	
	ВсеПоляГруппировок = Обработки.КлассификацияВыручкиПоОКВЭД.НастраиваемыеПоляГруппировки();
	
	СписокПолей = Список.ВыгрузитьЗначения();
	ЗаполнитьСписокВыбора(СписокПолей, ВсеПоляГруппировок, Элементы.СписокЗначение);
	
	Элементы.Список.ТекущаяСтрока = Список.Получить(0).ПолучитьИдентификатор();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ВыбранныеЗначения = Новый СписокЗначений;
	
	ВыбранныеЗначения.Добавить("ОКВЭД", "ОКВЭД");
	Для Каждого ЭлементСписка Из Список Цикл
		Если ЭлементСписка.Пометка И Не ЭлементСписка.Значение = "Номенклатура"
			И Не ПустаяСтрока(ЭлементСписка.Значение) Тогда
			ВыбранныеЗначения.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(ВыбранныеЗначения);

КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)

	ПозицияВставки = Список.Количество() - 1;
	НовыйЭлемент = Список.Вставить(ПозицияВставки, "",, Истина);
	
	Элементы.Список.ТекущаяСтрока = НовыйЭлемент.ПолучитьИдентификатор();
	Элементы.Список.ТекущийЭлемент = Элементы.СписокЗначение;
	Элементы.Список.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВверх(Команда)
	
	СдвинутьПолеГруппировки(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьВниз(Команда)
	
	СдвинутьПолеГруппировки(1);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиПоУмолчанию(Команда)
	
	ПараметрыЗаполнения = НовыйПараметрыЗаполнения();
	
	ПараметрыЗаполнения.ДатаНачала = Объект.ДатаНачала;
	ПараметрыЗаполнения.ДатаОкончания = Объект.ДатаОкончания;
	ПараметрыЗаполнения.Организация = Объект.Организация;
	
	ДлительнаяОперация = НачатьЗаполнениеПолейГруппировки(ПараметрыЗаполнения, УникальныйИдентификатор);
	
	ОжидатьПолучениеПолейГруппировки(ДлительнаяОперация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Индекс = Список.Индекс(Список.НайтиПоЗначению(ТекущиеДанные.Значение));
	СписокПолей = Список.ВыгрузитьЗначения();
	СписокПолей.Удалить(Индекс);
	
	ЗаполнитьСписокВыбора(СписокПолей, ВсеПоляГруппировок, Элементы.СписокЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ЭлементСписка = Список.НайтиПоЗначению(ТекущиеДанные.Значение);
	ЭлементСписка.Пометка = Истина;
	ЭлементСписка.Представление = Элементы.СписокЗначение.СписокВыбора.НайтиПоЗначению(ТекущиеДанные.Значение).Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные.Значение = "ОКВЭД" Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СдвинутьПолеГруппировки(Направление)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = Список.Индекс(Список.НайтиПоЗначению(ТекущиеДанные.Значение));
	
	КоличествоСтрок = Список.Количество();
	Если Направление = 1 И Индекс = КоличествоСтрок - 2 Тогда
		Возврат;
	КонецЕсли;
	Список.Сдвинуть(Индекс, Направление);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбора(СписокПолей, ВсеПоляГруппировок, ЭлементЗначения)
	
	РазностьМассивов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ВсеПоляГруппировок.ВыгрузитьЗначения(), СписокПолей);
	ЭлементЗначения.СписокВыбора.Очистить();
	Для Каждого Значение Из РазностьМассивов Цикл
		ЭлементЗначения.СписокВыбора.Добавить(Значение, ВсеПоляГруппировок.НайтиПоЗначению(Значение).Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НовыйПараметрыЗаполнения()

	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ДатаНачала", Дата(1, 1, 1));
	ПараметрыЗаполнения.Вставить("ДатаОкончания", Дата(1, 1, 1));
	ПараметрыЗаполнения.Вставить("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	Возврат ПараметрыЗаполнения;

КонецФункции

&НаСервереБезКонтекста
Функция НачатьЗаполнениеПолейГруппировки(ПараметрыФормы, ИдентификаторФормы)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Определение полей группировки'");

	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.КлассификацияВыручкиПоОКВЭД.ПоляГруппировкиПоУмолчанию", ПараметрыФормы);
	
КонецФункции

&НаКлиенте
Процедура ОжидатьПолучениеПолейГруппировки(ДлительнаяОперация)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьПоляГруппировки", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоляГруппировки(Результат, ДополнительныеПараметры) Экспорт 

	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Список = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Список.Добавить("Номенклатура", НСтр("ru = 'Номенклатура'"));
	Список.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

#КонецОбласти
