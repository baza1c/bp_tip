
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ДиаграммаАнализ.ТипДиаграммы = ТипДиаграммы.Кольцевая;
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Объект.Организация = Параметры.Организация;
	Иначе
		Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;

	Если ЗначениеЗаполнено(Параметры.ДатаНачала) И ЗначениеЗаполнено(Параметры.ДатаОкончания) Тогда
		Объект.ДатаНачала = Параметры.ДатаНачала;
		Объект.ДатаОкончания = Параметры.ДатаОкончания;
	Иначе
		ТекущаяДата = ТекущаяДатаСеанса();
		Если Месяц(ТекущаяДата) > 11 Тогда
			Объект.ДатаОкончания = КонецГода(ТекущаяДата);
		Иначе
			Объект.ДатаОкончания = НачалоГода(ТекущаяДата) - 1;
		КонецЕсли;
		Объект.ДатаНачала = НачалоГода(Объект.ДатаОкончания);
	КонецЕсли;
	
	ИмяОтчета = Параметры.ИДОтчета;
	
	ОткрытаИзСпискаЗадач = Параметры.ОткрытаИзСпискаЗадач;
	
	КлассификацияВыручкиПоОКВЭДНастройки = ХранилищеОбщихНастроек.Загрузить("КлассификацияВыручкиПоОКВЭДНастройки");
	Если ТипЗнч(КлассификацияВыручкиПоОКВЭДНастройки) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, КлассификацияВыручкиПоОКВЭДНастройки);
	КонецЕсли;
	
	ДоступноРедактированиеНастроек = ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.НастройкиКлассификацииВыручки);
	
	УправлениеФормой();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ПустаяСтрока(АдресРаспределениеВыручки) Тогда
		Результат = Новый Структура;
		Результат.Вставить("АдресРаспределениеВыручки", АдресРаспределениеВыручки);
		ОповеститьОВыборе(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Модифицированность Или ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЕГРЗаявление) И Модифицированность И ДоступноРедактированиеНастроек Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru='Сохранить данные распределения выручки?
			|В дальнейшем они могут быть использованы для заполнения отчетности в Росстат.'"),
			РежимДиалогаВопрос.ДаНетОтмена,
			,
			КодВозвратаДиалога.Да);
			
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВнесениеИзмененийЕГРнеЗавершено"
		Или ИмяСобытия = "ОбновитьСтатусЗаявленияНаГосРегистрацию"
		Или ИмяСобытия = "Запись_УведомлениеОСпецрежимахНалогообложения" Тогда
		УправлениеВидимостьюСменитьОКВЭД();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьДанные(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	Если Объект.ДатаНачала > Объект.ДатаОкончания
		Или Не ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Если ИмяОтчета = "РегламентированныйОтчетСтатистикаФормаП1" Тогда
			Объект.ДатаНачала = НачалоМесяца(Объект.ДатаОкончания);
		Иначе
			Объект.ДатаНачала = НачалоГода(Объект.ДатаОкончания);
		КонецЕсли;
	КонецЕсли;
	ЗаполнитьДанные(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	
	Если Объект.ДатаОкончания < Объект.ДатаНачала Тогда
		Если ИмяОтчета = "РегламентированныйОтчетСтатистикаФормаП1" Тогда
			Объект.ДатаОкончания = КонецМесяца(Объект.ДатаНачала);
		Иначе
			Объект.ДатаОкончания = КонецГода(Объект.ДатаНачала);
		КонецЕсли;
	КонецЕсли;
	ЗаполнитьДанные(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПолейГруппировокНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПоляГруппировок", ПоляГруппировок);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ДатаНачала", Объект.ДатаНачала);
	ПараметрыФормы.Вставить("ДатаОкончания", Объект.ДатаОкончания);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("НастройкиПолейГруппировокЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.КлассификацияВыручкиПоОКВЭД.Форма.ПоляГруппировки", ПараметрыФормы, ЭтотОбъект,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборФлагПриИзменении(Элемент)
	
	Если Не ОтборФлаг Тогда
		ОтборПоПолюГруппировкиСтрока = Неопределено;
	КонецЕсли;
	
	Если ОтборФлаг И Не ЗначениеЗаполнено(ОтборПоПолюГруппировкиСтрока) Тогда
		Возврат;
	КонецЕсли;
	УстановитьОтменитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоНоменклатуреПриИзменении(Элемент)
	
	УстановитьОтменитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнестиИзменениеВЕГРСсылкаНажатие(Элемент)
	
	ОткрытьПомощникВнесенияИзменений();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИзмененияВЕГРОшибкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Протокол" Тогда
		
		РегистрацияОрганизацииКлиент.ОткрытьПрикрепленныйФайлОтправки(
			ЕГРЗаявление,
			"Протокол",
			УникальныйИдентификатор);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ИсправлениеЗаявления" Тогда
		
		ОткрытьПомощникВнесенияИзменений(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоДанных

&НаКлиенте
Процедура ДеревоДанныхПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	Если Элементы.ДеревоДанных.ТекущиеДанные = Неопределено
		Или Элемент.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ДеревоДанныхКонтекстноеМенюОткрытьАнализСубконто.Доступность = ТекущиеДанные.ИндексУровня = ПоляГруппировок.Количество() - 1;
	
	Если Элемент.ТекущийЭлемент.Имя <> "ДеревоДанныхОКВЭД" И Не УжеВыделеноНаДиаграмме Тогда
		ВыделитьНаДиаграмме(ТекущиеДанные.ИндексУровня, ТекущиеДанные.ИндексСерии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДанныхОКВЭДНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ВыбратьВидДеятельностиОКВЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДанныхОКВЭДОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ДанныеВыбораОКВЭД(Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДанныхОКВЭДОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		ОКВЭДТекущийПослеИзменения = ВыбранноеЗначение;
		ВыбранноеЗначение = ОКВЭДТекущийПослеИзменения.Представление;
		ОКВЭДТекущийДоИзменения = Элементы.ДеревоДанных.ТекущиеДанные.ОКВЭД;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДанныхОКВЭДПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	ОбновитьДанныеПослеИзмененияОКВЭД(ОКВЭДТекущийПослеИзменения, ТекущиеДанные, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДанныхПередРазворачиванием(Элемент, Строка, Отказ)
	
	ТекущаяСтрока = ДеревоДанных.НайтиПоИдентификатору(Строка);
	Если ТекущаяСтрока <> Неопределено Тогда
		
		ПолеГруппировкиРодитель = Неопределено;
		СтрокаРодитель = ТекущаяСтрока.ПолучитьРодителя();
		Если СтрокаРодитель <> Неопределено Тогда
			ПолеГруппировкиРодитель = СтрокаРодитель.ПолеГруппировкиНоменклатуры;
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ПолеГруппировкиНоменклатуры", ТекущаяСтрока.ПолеГруппировкиНоменклатуры);
		ПараметрыОтбора.Вставить("ПолеГруппировкиРодитель", ПолеГруппировкиРодитель);
		НайденныеСтроки = РазвернутыеСтрокиДерева.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = РазвернутыеСтрокиДерева.Добавить();
			НоваяСтрока.ПолеГруппировкиНоменклатуры = ТекущаяСтрока.ПолеГруппировкиНоменклатуры;
			НоваяСтрока.ПолеГруппировкиРодитель = ПолеГруппировкиРодитель;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДанныхПередСворачиванием(Элемент, Строка, Отказ)
	
	ТекущаяСтрока = ДеревоДанных.НайтиПоИдентификатору(Строка);
	Если ТекущаяСтрока <> Неопределено Тогда
		
		ПолеГруппировкиРодитель = Неопределено;
		СтрокаРодитель = ТекущаяСтрока.ПолучитьРодителя();
		Если СтрокаРодитель <> Неопределено Тогда
			ПолеГруппировкиРодитель = СтрокаРодитель.ПолеГруппировкиНоменклатуры;
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ПолеГруппировкиНоменклатуры", ТекущаяСтрока.ПолеГруппировкиНоменклатуры);
		ПараметрыОтбора.Вставить("ПолеГруппировкиРодитель", ПолеГруппировкиРодитель);
		НайденныеСтроки = РазвернутыеСтрокиДерева.НайтиСтроки(ПараметрыОтбора);
		Для Каждого Строка Из НайденныеСтроки Цикл
			РазвернутыеСтрокиДерева.Удалить(Строка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийДиаграммы

&НаКлиенте
Процедура ДиаграммаАнализПриАктивизации(Элемент)
	
	МассивЗначенийДиаграммы = Элемент.ПолучитьВыделенныеЭлементы();
	Если МассивЗначенийДиаграммы.Количество() = 1 Тогда
		УжеВыделеноНаДиаграмме = Истина;
		ЗначениеДиаграммы = МассивЗначенийДиаграммы[0];
		
		Если ТипЗнч(ЗначениеДиаграммы) = Тип("ЗначениеДиаграммы") Тогда
			ИндексТочки = ДиаграммаАнализ.Точки.Индекс(ЗначениеДиаграммы.Точка);
			Если ИндексТочки <> 0 Тогда
				ИндексСерии = Строка(ДиаграммаАнализ.Серии.Индекс(ЗначениеДиаграммы.Серия));
				
				ИдентификаторВДереве = Неопределено;
				ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля(
					"ИндексСерии", ИдентификаторВДереве, ДеревоДанных.ПолучитьЭлементы(), ИндексСерии, Ложь);
				Элементы.ДеревоДанных.ТекущаяСтрока = ИдентификаторВДереве;
				
				Элементы.ДиаграммаАнализКонтекстноеМенюОткрытьАнализСубконто.Доступность = ИндексТочки = ПоляГруппировок.Количество();
				
				УжеВыделеноНаДиаграмме = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Элементы.ДиаграммаАнализКонтекстноеМеню.Видимость = ТипЗнч(ЗначениеДиаграммы) = Тип("ЗначениеДиаграммы") И ИндексТочки <> 0;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьОКВЭД(Команда)
	
	ВыбратьВидДеятельностиОКВЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтбор(Команда)
	
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	Если ПоляГруппировок.Количество() = ТекущиеДанные.ИндексУровня Тогда
		Возврат;
	КонецЕсли;
	
	ПоляОтбора = Новый Структура();
	Если Элементы.ДеревоДанных.ТекущийЭлемент.Имя = "ДеревоДанныхОКВЭД" Тогда
		ПоляОтбора.Вставить("ОКВЭД", ТекущиеДанные.ОКВЭД);
		ПоляОтбора.Вставить("ОКВЭДКод", ТекущиеДанные.ОКВЭДКод);
	КонецЕсли;
	
	ПоляОтбора.Вставить("ПолеГруппировкиНоменклатуры", ТекущиеДанные.ПолеГруппировкиНоменклатуры);
	СтрокаРодитель = ТекущиеДанные.ПолучитьРодителя();
	ПолеГруппировкиРодитель = Неопределено;
	Если СтрокаРодитель <> Неопределено Тогда
		ПолеГруппировкиРодитель = СтрокаРодитель.ПолеГруппировкиНоменклатуры;
	КонецЕсли;
	ПоляОтбора.Вставить("ПолеГруппировкиРодитель", ПолеГруппировкиРодитель);
	Если ТекущиеДанные.ИндексУровня + 1 = ПоляГруппировок.Количество() Тогда
		НаименованиеПоля = "Номенклатура";
	Иначе
		НаименованиеПоля = ПоляГруппировок[ТекущиеДанные.ИндексУровня + 1].Значение;
	КонецЕсли;
	ПоляОтбора.Вставить("НаименованиеПоля", НаименованиеПоля);
	
	ДлительнаяОперация = НачатьОтборДанных(ПоляОтбора);
	ОжидатьОтборДанных(ДлительнаяОперация, ПоляОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОтбор(Команда)
	
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	ДлительнаяОперация = НачатьОтборДанных(, Истина);
	
	ПоляОтбора = Новый Структура;
	ПоляОтбора.Вставить("ПолеГруппировкиНоменклатуры", ТекущиеДанные.ПолеГруппировкиНоменклатуры);
	СтрокаРодитель = ТекущиеДанные.ПолучитьРодителя();
	ПолеГруппировкиРодитель = Неопределено;
	Если СтрокаРодитель <> Неопределено Тогда
		ПолеГруппировкиРодитель = СтрокаРодитель.ПолеГруппировкиНоменклатуры;
	КонецЕсли;
	ПоляОтбора.Вставить("ПолеГруппировкиРодитель", ПолеГруппировкиРодитель);

	ОжидатьОтборДанных(ДлительнаяОперация, ПоляОтбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПолеГруппировки(Команда)
	
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ПолеГруппировкиНоменклатуры) Тогда
		ПоказатьЗначение( , ТекущиеДанные.ПолеГруппировкиНоменклатуры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОСВ(Команда)
	
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	ПолеГруппировкиНоменклатуры = ТекущиеДанные.ПолеГруппировкиНоменклатуры;
	
	РодительНоменклатурнаяГруппа = Неопределено;
	ОпределитьНоменклатурнуюГруппу(РодительНоменклатурнаяГруппа, ТекущиеДанные, ПолеГруппировкиНоменклатуры);
	
	Счет = ТекущиеДанные.Субсчет9001;
	ПараметрыФормы = ПараметрыФормыОСВПоСчету(
		Объект.Организация, Объект.ДатаНачала, Объект.ДатаОкончания, ПолеГруппировкиНоменклатуры, РодительНоменклатурнаяГруппа, Счет);
	
	ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету.Форма.ФормаОтчета",
		ПараметрыФормы, ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьАнализСубконто(Команда)
	
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	ПолеГруппировкиНоменклатуры = ТекущиеДанные.ПолеГруппировкиНоменклатуры;
	
	ПараметрыФормы = ПараметрыФормыАнализСубконто(Объект.Организация, Объект.ДатаНачала, Объект.ДатаОкончания, ПолеГруппировкиНоменклатуры);
	
	ОткрытьФорму("Отчет.АнализСубконто.Форма.ФормаОтчета",
		ПараметрыФормы, ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьВсе(Команда)
	
	СвернутьСтрокиДереваРекурсивно(ДеревоДанных.ПолучитьЭлементы());
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьВсе(Команда)
	
	РазвернутьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаДиаграммаСкрытьНажатие(Элемент)
	
	СкрытьДиаграмму();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаДиаграммаПоказатьНажатие(Элемент)
	
	ПоказатьДиаграмму();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыФормы = Новый Структура("НачалоПериода, КонецПериода", Объект.ДатаНачала, Объект.ДатаОкончания);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыФормы, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если ДоступноРедактированиеНастроек Тогда
		Модифицированность = Ложь;
		ПриЗакрытииНаСервере();
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура НаписатьРазработчикам(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнестиИзмененияВЕГР(Команда)
	
	ОткрытьПомощникВнесенияИзменений(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьДанные(ПерезаполнитьДанныеНоменклатуры = Ложь) Экспорт
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПустая;
	ДлительнаяОперация = НачатьЗаполнениеДанных(ПерезаполнитьДанныеНоменклатуры);
	ОжидатьЗаполнениеДанных(ДлительнаяОперация);
	
КонецПроцедуры

&НаСервере
Функция НачатьЗаполнениеДанных(ПерезаполнитьДанныеНоменклатуры = Ложь)

	ДанныеДерева = РеквизитФормыВзначение("ДеревоДанных");
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("Организация",                     Объект.Организация);
	ПараметрыОперации.Вставить("ДатаНачала",                      Объект.ДатаНачала);
	ПараметрыОперации.Вставить("ДатаОкончания",                   КонецДня(Объект.ДатаОкончания));
	ПараметрыОперации.Вставить("ИмяОтчета",                       ИмяОтчета);
	ПараметрыОперации.Вставить("ДанныеНоменклатуры",              ДанныеНоменклатуры.Выгрузить());
	ПараметрыОперации.Вставить("ПерезаполнитьДанныеНоменклатуры", ПерезаполнитьДанныеНоменклатуры);
	ПараметрыОперации.Вставить("ПоляГруппировок",                 ПоляГруппировок);
	ПараметрыОперации.Вставить("ДанныеДерева",                    ДанныеДерева);
	ПараметрыОперации.Вставить("ДиаграммаАнализ",                 ДиаграммаАнализ);
	ПараметрыОперации.Вставить("СоответствиеЦветов",              СоответствиеЦветов.Выгрузить());
	ПараметрыОперации.Вставить("ОтборПоПолюГруппировкиТаблица",   ОтборПоПолюГруппировкиТаблица.Выгрузить());
	ПараметрыОперации.Вставить("ОткрытаИзСпискаЗадач",            ОткрытаИзСпискаЗадач);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение данных распределения выручки по ОКВЭД'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Обработки.КлассификацияВыручкиПоОКВЭД.ДанныеРаспределенияВыручки",
		ПараметрыОперации);

КонецФункции

&НаКлиенте
Процедура ОжидатьЗаполнениеДанных(ДлительнаяОперация, ВыводитьОкноОжидания = Ложь)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатЗаполненияДанных", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗаполненияДанных(Результат, ДополнительныеПараметры) Экспорт 

	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	ЗагрузитьРезультат(Результат.АдресРезультата, ДополнительныеПараметры);
	
	КоличествоУровнейРазворачивать = ПоляГруппировок.Количество() - 2;
	РазвернутьДеревоПослеЗаполнения(ДеревоДанных, КоличествоУровнейРазворачивать);
	
	Если ДеревоДанных.ПолучитьЭлементы().Количество() = 0 Тогда
		ДиаграммаАнализ.Очистить();
	КонецЕсли;
	
	УстановитьУсловноеОформление();

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультат(АдресРезультата, ДополнительныеПараметры)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Результат.ДеревоДанных, "ДеревоДанных");
	ДиаграммаАнализ = Результат.ДиаграммаАнализ;
	
	ДанныеНоменклатуры.Загрузить(Результат.ДанныеНоменклатуры);
	ПоляГруппировок = Результат.ПоляГруппировок;
	СоответствиеЦветов.Загрузить(Результат.СоответствиеЦветов);
	
	Элементы.ОтборПоПолюГруппировкиСтрока.СписокВыбора.ЗагрузитьЗначения(Результат.СписокВыбораДляОтбора);
	Элементы.ДеревоДанныхПолеГруппировкиНоменклатуры.Заголовок = Результат.ЗаголовокПоляГруппировкиНоменклатуры;
	
	ЗаголовокОткрытьПолеГруппировки = СтрШаблон(НСтр("ru = 'Открыть %1'"), Результат.ЗаголовокПоляГруппировкиНоменклатуры);
	Элементы.ДеревоДанныхКонтекстноеМенюОткрытьПолеГруппировки.Заголовок = ЗаголовокОткрытьПолеГруппировки;
	Элементы.ДиаграммаАнализКонтекстноеМенюОткрытьПолеГруппировки.Заголовок = ЗаголовокОткрытьПолеГруппировки;
	
	ОтборПоПолюГруппировкиТаблица.Загрузить(Результат.ОтборПоПолюГруппировкиТаблица);
	СуммаВыручкиВсего = Результат.СуммаВыручки;
	ОКВЭДОрганизации = Результат.ОКВЭДОрганизации.Представление;
	ОКВЭДОрганизацииКод = Результат.ОКВЭДОрганизации.Код;
	
	ТекущийКод = Результат.ОКВЭДОрганизации.Код;
	
	Элементы.ДеревоДанных.ТекущийЭлемент = Элементы.ДеревоДанныхОКВЭД;
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРабочая;
	
	Если ОткрытаИзСпискаЗадач Тогда
		ПроверкаОсновногоКодаОКВЭД(Результат);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоПослеЗаполнения(ТекущийУзел, КоличествоУровнейРазворачивать)
	
	ЭлементыДерева = ТекущийУзел.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		Если ЭлементДерева.ИндексУровня < КоличествоУровнейРазворачивать Тогда
			Элементы.ДеревоДанных.Развернуть(ЭлементДерева.ПолучитьИдентификатор());
			РазвернутьДеревоПослеЗаполнения(ЭлементДерева, КоличествоУровнейРазворачивать);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	РаспределениеВыручки = Новый ТаблицаЗначений;
	РаспределениеВыручки.Колонки.Добавить("ОКВЭДКод", ОбщегоНазначения.ОписаниеТипаСтрока(8));
	РаспределениеВыручки.Колонки.Добавить("ОКВЭДНаименование", ОбщегоНазначения.ОписаниеТипаСтрока(500));
	РаспределениеВыручки.Колонки.Добавить("ПоляГруппировкиНоменклатуры", Новый ОписаниеТипов("Структура"));
	
	ДанныеДерева = РеквизитФормыВзначение("ДеревоДанных");
	Если ДанныеДерева.Строки.Количество() > 0 Тогда
		ОбойтиСтрокиПолейГруппировок(РаспределениеВыручки, ДанныеДерева);
		АдресРаспределениеВыручки = ПоместитьВоВременноеХранилище(РаспределениеВыручки, АдресРаспределениеВыручки);
		
		Если ОткрытаИзСпискаЗадач Тогда
			РегистрыСведений.НастройкиКлассификацииВыручки.ЗаполнитьРаспределениеВыручкиПоОКВЭД(АдресРаспределениеВыручки, Объект.Организация);
			Если Элементы.ГруппаБаннерУспех.Видимость Тогда
				РегистрыСведений.ЗадачиБухгалтера.ЗавершитьЗадачуПроверкиВидаДеятельности(Объект.Организация);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбойтиСтрокиПолейГруппировок(РаспределениеВыручки, СтрокаРодитель)
	
	// Обходим все строки дерева. На нижнем уровне, там где код ОКВЭД однозначно определен, сохраняем помимо поля группировки
	// для этого ОКВЭД и родительские поля группировок, объединяем эти условия в группу И (одновременное выполнение этих условий).
	// Этот же код ОКВЭД может быть указан и в других строках, там будет другая группа И.
	// Если у нас только один уровень группировки (например, Номенклатура), или код ОКВЭД однозначно определен уже для верхнего уровня,
	// то условия по полям группировки будут потом соединены в отборе компоновки в группу Или (достаточно выполнения одного из условий).
	
	ИндексУровня = 1;
	Если ТипЗнч(СтрокаРодитель) = Тип("СтрокаДереваЗначений") Тогда
		ИндексУровня = СтрокаРодитель.ИндексУровня + 2;
	КонецЕсли;
	
	Если ИндексУровня > ПоляГруппировок.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоля = ИмяПоля(ИндексУровня, ПоляГруппировок);
	
	Для Каждого СтрокаГруппировки Из СтрокаРодитель.Строки Цикл
		ПолеГруппировкиНоменклатуры = СтрокаГруппировки.ПолеГруппировкиНоменклатуры;
		ОКВЭД = СтрокаГруппировки.ОКВЭД;
		
		Если СтрНайти(ОКВЭД, ";") = 0 И ЗначениеЗаполнено(ОКВЭД) Тогда
			НайденныеСтроки = РаспределениеВыручки.НайтиСтроки(Новый Структура("ОКВЭДКод", СтрокаГруппировки.ОКВЭДКод));
			
			ПоляГруппировкиРодитель = Неопределено;
			ЗаполнитьПоляГруппировкиРодителяРекурсивно(ПоляГруппировкиРодитель, СтрокаРодитель, ИндексУровня - 1, ПолеГруппировкиНоменклатуры);
			Если НайденныеСтроки.Количество() = 0 Тогда
				СтрокаРаспределения = РаспределениеВыручки.Добавить();
				СтрокаРаспределения.ОКВЭДКод = СтрокаГруппировки.ОКВЭДКод;
				СтрокаРаспределения.ОКВЭДНаименование = СтрокаГруппировки.ОКВЭДНаименование;
					
				Если ЗначениеЗаполнено(ПоляГруппировкиРодитель) Тогда
					ЗаполнитьГруппуИ(СтрокаРаспределения.ПоляГруппировкиНоменклатуры, ПоляГруппировкиРодитель, ИмяПоля, ПолеГруппировкиНоменклатуры);
				Иначе
					СписокПолейГруппировок = Новый СписокЗначений;
					СписокПолейГруппировок.Добавить(ПолеГруппировкиНоменклатуры);
					СтрокаРаспределения.ПоляГруппировкиНоменклатуры.Вставить(ИмяПоля, СписокПолейГруппировок);
				КонецЕсли;
				
			Иначе
				СтрокаРаспределения = НайденныеСтроки[0];
				ПоляГруппировкиНоменклатуры = СтрокаРаспределения.ПоляГруппировкиНоменклатуры;
				Если ЗначениеЗаполнено(ПоляГруппировкиРодитель) Тогда
					ЗаполнитьГруппуИ(ПоляГруппировкиНоменклатуры, ПоляГруппировкиРодитель, ИмяПоля, ПолеГруппировкиНоменклатуры);
				Иначе
					Если ПоляГруппировкиНоменклатуры.Свойство(ИмяПоля) Тогда
						ПоляГруппировкиНоменклатуры[ИмяПоля].Добавить(ПолеГруппировкиНоменклатуры);
					Иначе
						СписокПолейГруппировок = Новый СписокЗначений;
						СписокПолейГруппировок.Добавить(ПолеГруппировкиНоменклатуры);
						ПоляГруппировкиНоменклатуры.Вставить(ИмяПоля, СписокПолейГруппировок);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ОбойтиСтрокиПолейГруппировок(РаспределениеВыручки, СтрокаГруппировки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьГруппуИ(ПоляГруппировкиНоменклатуры, ПоляГруппировкиРодитель, ИмяПоля, ПолеГруппировкиНоменклатуры)
	
	Если ПоляГруппировкиНоменклатуры.Свойство("ПорядокГруппыИ") Тогда
		ПорядокГруппыИ = ПоляГруппировкиНоменклатуры.ПорядокГруппыИ;
		ТекущаяГруппаИ = ПоляГруппировкиНоменклатуры["ГруппаИ" + ПорядокГруппыИ];
		ТекущаяГруппаИПодходит = Истина;
		Для Каждого ПолеГруппировки Из ПоляГруппировкиРодитель Цикл
			Если Не ТекущаяГруппаИ.Свойство(ПолеГруппировки.ИмяПоля)
				Или ТекущаяГруппаИ[ПолеГруппировки.ИмяПоля].НайтиПоЗначению(ПолеГруппировки.ЗначениеПоля) = Неопределено
				Или ТекущаяГруппаИ.Количество() <> ПоляГруппировкиРодитель.Количество() + 1 Тогда
				ТекущаяГруппаИПодходит = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ТекущаяГруппаИПодходит Тогда
			Если ТекущаяГруппаИ.Свойство(ИмяПоля) Тогда
				ТекущаяГруппаИ[ИмяПоля].Добавить(ПолеГруппировкиНоменклатуры);
			Иначе
				СписокПолейГруппировок = Новый СписокЗначений;
				СписокПолейГруппировок.Добавить(ПолеГруппировкиНоменклатуры);
				ТекущаяГруппаИ.Вставить(ИмяПоля, СписокПолейГруппировок);
			КонецЕсли;
		Иначе
			ПорядокГруппыИ = ПоляГруппировкиНоменклатуры.ПорядокГруппыИ + 1;
			ЗаполнитьНовуюГруппуИ(ПоляГруппировкиНоменклатуры, ПоляГруппировкиРодитель, ИмяПоля, ПолеГруппировкиНоменклатуры, ПорядокГруппыИ);
		КонецЕсли;
	Иначе
		
		ЗаполнитьНовуюГруппуИ(ПоляГруппировкиНоменклатуры, ПоляГруппировкиРодитель, ИмяПоля, ПолеГруппировкиНоменклатуры)
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНовуюГруппуИ(ПоляГруппировкиНоменклатуры, ПоляГруппировкиРодитель, ИмяПоля, ПолеГруппировкиНоменклатуры, ПорядокГруппыИ = 1)
	
	ИмяГруппы = "ГруппаИ" + ПорядокГруппыИ;
	ПоляГруппировкиНоменклатуры.Вставить("ПорядокГруппыИ", ПорядокГруппыИ);
	ПоляГруппировкиНоменклатуры.Вставить(ИмяГруппы, Новый Структура);
	Для Каждого ПолеГруппировки Из ПоляГруппировкиРодитель Цикл
		СписокПолейГруппировок = Новый СписокЗначений;
		СписокПолейГруппировок.Добавить(ПолеГруппировки.ЗначениеПоля);
		ПоляГруппировкиНоменклатуры[ИмяГруппы].Вставить(ПолеГруппировки.ИмяПоля, СписокПолейГруппировок);
	КонецЦикла;
	СписокПолейГруппировок = Новый СписокЗначений;
	СписокПолейГруппировок.Добавить(ПолеГруппировкиНоменклатуры);
	ПоляГруппировкиНоменклатуры[ИмяГруппы].Вставить(ИмяПоля, СписокПолейГруппировок);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляГруппировкиРодителяРекурсивно(ПоляГруппировкиРодитель, СтрокаРодитель, ИндексУровня, ПолеГруппировкиНоменклатуры)
	
	Если ИндексУровня = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Если ПоляГруппировкиРодитель = Неопределено Тогда
		ПоляГруппировкиРодитель = Новый ТаблицаЗначений;
		ПоляГруппировкиРодитель.Колонки.Добавить("ИмяПоля", ОбщегоНазначения.ОписаниеТипаСтрока(100));
		ТипыЗначенийПоля = Новый Массив;
		ТипыЗначенийПоля.Добавить(Тип("СправочникСсылка.НоменклатурныеГруппы"));
		ТипыЗначенийПоля.Добавить(Тип("СправочникСсылка.ВидыНоменклатуры"));
		ТипыЗначенийПоля.Добавить(Тип("СправочникСсылка.Номенклатура"));
		ПоляГруппировкиРодитель.Колонки.Добавить("ЗначениеПоля", Новый ОписаниеТипов(ТипыЗначенийПоля));
	КонецЕсли;
	
	// Для полей Номенклатура, ГруппаНоменклатурыВерхнегоУровня, ГруппаНоменклатурыВторогоУровня нет смысла сохранять группировки родителя,
	// ПолеГруппировкиНоменклатуры будет достаточно.
	Если ТипЗнч(СтрокаРодитель.ПолеГруппировкиНоменклатуры) <> ТипЗнч(ПолеГруппировкиНоменклатуры) Тогда
		ИмяПоля = ИмяПоля(ИндексУровня, ПоляГруппировок);
		
		НоваяСтрока = ПоляГруппировкиРодитель.Добавить();
		НоваяСтрока.ИмяПоля = ИмяПоля;
		НоваяСтрока.ЗначениеПоля = СтрокаРодитель.ПолеГруппировкиНоменклатуры;
	КонецЕсли;
	
	ЗаполнитьПоляГруппировкиРодителяРекурсивно(ПоляГруппировкиРодитель, СтрокаРодитель.Родитель, ИндексУровня - 1, ПолеГруппировкиНоменклатуры);
	
КонецПроцедуры

&НаСервере
Функция ИмяПоля(ИндексУровня, ПоляГруппировок)
	
	Если ИндексУровня = ПоляГруппировок.Количество() Тогда
		ИмяПоля = "Номенклатура";
	Иначе
		ИмяПоля = ПоляГруппировок[ИндексУровня].Значение;
		Если Лев(ИмяПоля, 18) = "ГруппаНоменклатуры" Тогда
			ИмяПоля = "ГруппаНоменклатуры";
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИмяПоля;
	
КонецФункции

&НаКлиенте
Процедура ВыбратьВидДеятельностиОКВЭД()
	
	ПараметрыФормы = Новый Структура("ТекущийКод, ТекущийРаздел, Организация", "", "", Неопределено);
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ОКВЭДКод) Тогда
		ПараметрыФормы.ТекущийКод = ТекущиеДанные.ОКВЭДКод;
	Иначе
		ПараметрыФормы.ТекущийКод = ТекущийКод;
	КонецЕсли;
	
	ПараметрыФормы.ТекущийРаздел = ТекущийРаздел;
	ПараметрыФормы.Организация = Объект.Организация;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборВидаДеятельностиОКВЭДЗавершение", ЭтотОбъект, Элементы.ДеревоДанных.ВыделенныеСтроки);
	ОткрытьФорму("ОбщаяФорма.ВыборВидаДеятельности", ПараметрыФормы, ЭтотОбъект, , , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборВидаДеятельностиОКВЭДЗавершение(Результат, ВыделенныеСтроки) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.Раздел) Тогда
		ТекущийРаздел = Результат.Раздел;
	КонецЕсли;
		
	ОКВЭД = Новый Структура;
	ОКВЭД.Вставить("Код", Результат.Код);
	ОКВЭД.Вставить("Наименование", Результат.Наименование);
	ОКВЭД.Вставить("Представление", Результат.Код + " " + Результат.Наименование);
	
	ОбновитьДанныеПослеИзмененияОКВЭД(ОКВЭД, ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПослеИзмененияОКВЭД(ОКВЭД, СтрокиДерева, СобытиеОКВЭДПриИзменении = Ложь)
	
	Если ЗначениеЗаполнено(ОКВЭД.Код) Тогда
		ТекущийКод = ОКВЭД.Код;
	КонецЕсли;
	
	ПоляПоискаГруппировкиНоменклатуры = Новый Массив;
	Если ТипЗнч(СтрокиДерева) = Тип("Массив") Тогда
		Для Каждого ИдентификаторСтроки Из СтрокиДерева Цикл
			СтрокаДерева = ДеревоДанных.НайтиПоИдентификатору(ИдентификаторСтроки);
			ДобавитьЭлементПоляПоиска(СтрокаДерева, ПоляПоискаГруппировкиНоменклатуры, СобытиеОКВЭДПриИзменении);
		КонецЦикла;
	Иначе
		ДобавитьЭлементПоляПоиска(СтрокиДерева, ПоляПоискаГруппировкиНоменклатуры, СобытиеОКВЭДПриИзменении);
	КонецЕсли;
	
	ДлительнаяОперация = НачатьОбновлениеДанных(ОКВЭД, ПоляПоискаГруппировкиНоменклатуры);
	ОжидатьОбновлениеДанных(ДлительнаяОперация, ОКВЭД.Представление, ПоляПоискаГруппировкиНоменклатуры[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭлементПоляПоиска(СтрокаДерева, ПоляПоискаГруппировкиНоменклатуры, СобытиеОКВЭДПриИзменении)
	
	ЭлементПолейПоиска = Новый Структура();
	ЭлементПолейПоиска.Вставить("ПолеГруппировкиНоменклатуры", СтрокаДерева.ПолеГруппировкиНоменклатуры);
	СтрокаРодитель = СтрокаДерева.ПолучитьРодителя();
	ПолеГруппировкиРодитель = Неопределено;
	Если СтрокаРодитель <> Неопределено Тогда
		ПолеГруппировкиРодитель = СтрокаРодитель.ПолеГруппировкиНоменклатуры;
	КонецЕсли;
	ЭлементПолейПоиска.Вставить("ПолеГруппировкиРодитель", ПолеГруппировкиРодитель);
	ЭлементПолейПоиска.Вставить("ИндексУровня", СтрокаДерева.ИндексУровня + 1);
	ЭлементПолейПоиска.Вставить("ОКВЭД", ?(СобытиеОКВЭДПриИзменении, ОКВЭДТекущийДоИзменения, СтрокаДерева.ОКВЭД));
	
	ПоляПоискаГруппировкиНоменклатуры.Добавить(ЭлементПолейПоиска);
	
КонецПроцедуры

&НаСервере
Функция НачатьОбновлениеДанных(ОКВЭД, ПоляПоискаГруппировкиНоменклатуры)

	ДанныеДерева = РеквизитФормыВзначение("ДеревоДанных");
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ОтборПоПолюГруппировкиСтрока",  ОтборПоПолюГруппировкиСтрока);
	ПараметрыОтбора.Вставить("ОтборПоПолюГруппировкиТаблица", ОтборПоПолюГруппировкиТаблица.Выгрузить());
	ПараметрыОтбора.Вставить("ОтборФлаг",                     ОтборФлаг);
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ПараметрыОтбора",                   ПараметрыОтбора);
	ПараметрыОперации.Вставить("ОКВЭД",                             ОКВЭД);
	ПараметрыОперации.Вставить("ПоляПоискаГруппировкиНоменклатуры", ПоляПоискаГруппировкиНоменклатуры);
	ПараметрыОперации.Вставить("ДанныеНоменклатуры",                ДанныеНоменклатуры.Выгрузить());
	ПараметрыОперации.Вставить("ПоляГруппировок",                   ПоляГруппировок);
	ПараметрыОперации.Вставить("ДанныеДерева",                      ДанныеДерева);
	ПараметрыОперации.Вставить("ДиаграммаАнализ",                   ДиаграммаАнализ);
	ПараметрыОперации.Вставить("СоответствиеЦветов",                СоответствиеЦветов.Выгрузить());
	ПараметрыОперации.Вставить("Организация",                       Объект.Организация);
	ПараметрыОперации.Вставить("ОткрытаИзСпискаЗадач",              ОткрытаИзСпискаЗадач);
	ПараметрыОперации.Вставить("ОКВЭДОрганизацииКод",               ОКВЭДОрганизацииКод);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление данных распределения выручки по ОКВЭД'");
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Обработки.КлассификацияВыручкиПоОКВЭД.ОбновитьДанныеРаспределенияВыручки",
		ПараметрыОперации);

КонецФункции

&НаКлиенте
Процедура ОжидатьОбновлениеДанных(ДлительнаяОперация, ОКВЭДПредставление, ПоляПоискаГруппировкиНоменклатуры)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОКВЭДПредставление", ОКВЭДПредставление);
	ДополнительныеПараметры.Вставить("ПоляПоискаГруппировкиНоменклатуры", ПоляПоискаГруппировкиНоменклатуры);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатОбновленияДанных", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатОбновленияДанных(Результат, ДополнительныеПараметры) Экспорт 

	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	ЗагрузитьРезультатОбновленияДанных(Результат.АдресРезультата, ДополнительныеПараметры);
	РазвернутьДеревоПослеОбновления(ДеревоДанных, ДополнительныеПараметры.ПоляПоискаГруппировкиНоменклатуры);
	Если ОткрытаИзСпискаЗадач Тогда
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатОбновленияДанных(АдресРезультата, ДополнительныеПараметры)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Результат.ДеревоДанных, "ДеревоДанных");
	ДиаграммаАнализ = Результат.ДиаграммаАнализ;
	
	ДанныеНоменклатуры.Загрузить(Результат.ДанныеНоменклатуры);
	СоответствиеЦветов.Загрузить(Результат.СоответствиеЦветов);
	
	Элементы.ОтборПоПолюГруппировкиСтрока.СписокВыбора.ЗагрузитьЗначения(Результат.СписокВыбораДляОтбора);
	ОтборПоПолюГруппировкиТаблица.Загрузить(Результат.ОтборПоПолюГруппировкиТаблица);
	ОтборПоПолюГруппировкиСтрока = Результат.ОтборПоПолюГруппировкиСтрока;
	
	Если ОткрытаИзСпискаЗадач Тогда
		ПроверкаОсновногоКодаОКВЭД(Результат);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СвернутьСтрокиДереваРекурсивно(СтрокиДерева)

	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		
		ПодчиненныеСтроки = СтрокаДерева.ПолучитьЭлементы();
		Если ПодчиненныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СвернутьСтрокиДереваРекурсивно(ПодчиненныеСтроки);
		Элементы.ДеревоДанных.Свернуть(СтрокаДерева.ПолучитьИдентификатор());
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево()
	
	Для Каждого СтрокаПервогоУровня Из ДеревоДанных.ПолучитьЭлементы() Цикл
		Элементы.ДеревоДанных.Развернуть(СтрокаПервогоУровня.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоПослеОбновления(ТекущийУзел, ПоляПоискаГруппировкиНоменклатуры)
	
	ЭлементыДерева = ТекущийУзел.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		ПолеГруппировкиРодитель = Неопределено;
		СтрокаРодитель = ЭлементДерева.ПолучитьРодителя();
		Если СтрокаРодитель <> Неопределено Тогда
			ПолеГруппировкиРодитель = СтрокаРодитель.ПолеГруппировкиНоменклатуры;
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ПолеГруппировкиНоменклатуры", ЭлементДерева.ПолеГруппировкиНоменклатуры);
		ПараметрыОтбора.Вставить("ПолеГруппировкиРодитель", ПолеГруппировкиРодитель);
		НайденныеСтроки = РазвернутыеСтрокиДерева.НайтиСтроки(ПараметрыОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Элементы.ДеревоДанных.Развернуть(ЭлементДерева.ПолучитьИдентификатор());
			РазвернутьДеревоПослеОбновления(ЭлементДерева, ПоляПоискаГруппировкиНоменклатуры);
		КонецЕсли;
		Если ЭлементДерева.ПолеГруппировкиНоменклатуры = ПоляПоискаГруппировкиНоменклатуры.ПолеГруппировкиНоменклатуры
			И ПолеГруппировкиРодитель = ПоляПоискаГруппировкиНоменклатуры.ПолеГруппировкиРодитель Тогда
			Элементы.ДеревоДанных.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПолейГруппировокЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("СписокЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	ПоляГруппировок = Результат;
	ДлительнаяОперация = НачатьЗаполнениеДанных();
	ОжидатьЗаполнениеДанных(ДлительнаяОперация, Истина);
	
	СохранитьНастройкиОбработки(, ПоляГруппировок);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()

	Элементы.ДеревоДанныхКонтекстноеМенюУстановитьОтбор.Доступность = Не ОтборФлаг;
	Элементы.ДеревоДанныхКонтекстноеМенюОтменитьОтбор.Доступность = ОтборФлаг;
	
	Элементы.ДиаграммаАнализКонтекстноеМенюУстановитьОтбор.Доступность = Не ОтборФлаг;
	Элементы.ДиаграммаАнализКонтекстноеМенюОтменитьОтбор.Доступность = ОтборФлаг;
	
	Элементы.ГруппаДиаграмма.Видимость = Не ДиаграммаСкрытаПользователем;
	Элементы.КартинкаДиаграммаПоказать.Видимость = ДиаграммаСкрытаПользователем;
	
	Если Не ДоступноРедактированиеНастроек Тогда
		Команды.Сохранить.Заголовок = "Закрыть";
		Элементы.ДеревоДанныхОКВЭД.Доступность = Ложь;
		Элементы.ДеревоДанныхКонтекстноеМенюИзменитьОКВЭД.Доступность = Ложь;
		Элементы.ДиаграммаАнализКонтекстноеМенюИзменитьОКВЭД.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДанныхОКВЭД");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДанных.ОКВЭД", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДанныхДоля");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДанных.Доля", ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '< 0.01'"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДанныхПолеГруппировкиНоменклатуры");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДанных.ПолеГруппировкиНоменклатуры", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));

	Если ПоляГруппировок.Количество() > 1 Тогда
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДанных");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ДеревоДанных.ИндексУровня", ВидСравненияКомпоновкиДанных.Равно, 0);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ДеревоДанных.ОКВЭД", ВидСравненияКомпоновкиДанных.Заполнено);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,, Истина));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьНаДиаграмме(ИндексУровня, ИндексСерии)
	
	Если ЗначениеЗаполнено(ИндексСерии) Тогда
		ВыделитьНаДиаграммеНаСервере(ИндексУровня, ИндексСерии);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыделитьНаДиаграммеНаСервере(ИндексУровня, ИндексСерии)

	ИндексТочки = ИндексУровня + 1;
	ТочкаДиаграммы = ДиаграммаАнализ.Точки[ИндексТочки];
	
	МассивЗначений = Новый Массив;
	ИндексыСерий = СтрРазделить(ИндексСерии, "; ", Ложь);
	Для Каждого Индекс Из ИндексыСерий Цикл
		СерияДиаграммы = ДиаграммаАнализ.Серии.Получить(Число(Индекс));
		ЗначениеДиаграммы = ДиаграммаАнализ.ПолучитьЗначение(ТочкаДиаграммы, СерияДиаграммы);
		МассивЗначений.Добавить(ЗначениеДиаграммы);
	КонецЦикла;
	Элементы.ДиаграммаАнализ.УстановитьВыделенныеЭлементы(МассивЗначений);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВыбораОКВЭД(СтрокаПоиска)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		ОКВЭД = Новый Структура;
		ОКВЭД.Вставить("Код", "");
		ОКВЭД.Вставить("Наименование", "");
		ОКВЭД.Вставить("Представление", "");
		ДанныеВыбора.Добавить(ОКВЭД, ""); 
		
		Возврат ДанныеВыбора;
	КонецЕсли;
	
	ТаблицаКлассификатор = КлассификаторОКВЭДБП.НайтиДанные(СтрокаПоиска, Истина);
	
	Для Каждого Строка Из ТаблицаКлассификатор Цикл
		Представление = Строка.Код + " " + Строка.Наименование;
		ОКВЭД = Новый Структура;
		ОКВЭД.Вставить("Код", Строка.Код);
		ОКВЭД.Вставить("Наименование", Строка.Наименование);
		ОКВЭД.Вставить("Представление", Представление);
		
		ДанныеВыбора.Добавить(ОКВЭД, Представление);
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтменитьОтбор()
	
	ТекущиеДанные = Элементы.ДеревоДанных.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоляОтбора = Новый Структура();
	ПоляОтбора.Вставить("ПолеГруппировкиНоменклатуры", ТекущиеДанные.ПолеГруппировкиНоменклатуры);
	СтрокаРодитель = ТекущиеДанные.ПолучитьРодителя();
	ПолеГруппировкиРодитель = Неопределено;
	Если СтрокаРодитель <> Неопределено Тогда
		ПолеГруппировкиРодитель = СтрокаРодитель.ПолеГруппировкиНоменклатуры;
	КонецЕсли;
	ПоляОтбора.Вставить("ПолеГруппировкиРодитель", ПолеГруппировкиРодитель);
	
	ДлительнаяОперация = НачатьОтборДанных();
	ОжидатьОтборДанных(ДлительнаяОперация, ПоляОтбора);
	
КонецПроцедуры

&НаСервере
Функция НачатьОтборДанных(ПоляОтбора = Неопределено, ОтменитьОтбор = Ложь)

	ДанныеДерева = РеквизитФормыВзначение("ДеревоДанных");
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ОтборПоПолюГруппировкиСтрока",  ОтборПоПолюГруппировкиСтрока);
	ПараметрыОтбора.Вставить("ОтборПоПолюГруппировкиТаблица", ОтборПоПолюГруппировкиТаблица.Выгрузить());
	ПараметрыОтбора.Вставить("ОтборФлаг",                     ОтборФлаг);
	ПараметрыОтбора.Вставить("ПоляОтбора",                    ПоляОтбора);
	ПараметрыОтбора.Вставить("ОтменитьОтбор",                 ОтменитьОтбор);
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ПараметрыОтбора",    ПараметрыОтбора);
	ПараметрыОперации.Вставить("ДанныеНоменклатуры", ДанныеНоменклатуры.Выгрузить());
	ПараметрыОперации.Вставить("ПоляГруппировок",    ПоляГруппировок);
	ПараметрыОперации.Вставить("ДанныеДерева",       ДанныеДерева);
	ПараметрыОперации.Вставить("ДиаграммаАнализ",    ДиаграммаАнализ);
	ПараметрыОперации.Вставить("СоответствиеЦветов", СоответствиеЦветов.Выгрузить());
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Установка / отмена отбора данных распределения выручки по ОКВЭД'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Обработки.КлассификацияВыручкиПоОКВЭД.ОтобратьДанныеРаспределенияВыручки",
		ПараметрыОперации);

КонецФункции

&НаКлиенте
Процедура ОжидатьОтборДанных(ДлительнаяОперация, ПоляОтбора)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПолеГруппировкиНоменклатуры", ПоляОтбора.ПолеГруппировкиНоменклатуры);
	ДополнительныеПараметры.Вставить("ПолеГруппировкиРодитель", ПоляОтбора.ПолеГруппировкиРодитель);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатОтбораДанных", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатОтбораДанных(Результат, ДополнительныеПараметры) Экспорт 

	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	ЗагрузитьРезультатОтбораДанных(Результат.АдресРезультата, ДополнительныеПараметры);
	РазвернутьДеревоПослеОбновления(ДеревоДанных, ДополнительныеПараметры);
	
	// Сбросим выделение на диаграмме
	Элементы.ДиаграммаАнализ.УстановитьВыделенныеЭлементы(Новый Массив);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатОтбораДанных(АдресРезультата, ДополнительныеПараметры)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ОтборПоПолюГруппировкиСтрока) 
		И Элементы.ОтборПоПолюГруппировкиСтрока.СписокВыбора.НайтиПоЗначению(Результат.ОтборПоПолюГруппировкиСтрока) = Неопределено Тогда
		Элементы.ОтборПоПолюГруппировкиСтрока.СписокВыбора.Добавить(Результат.ОтборПоПолюГруппировкиСтрока);
	КонецЕсли;
	ОтборПоПолюГруппировкиСтрока = Результат.ОтборПоПолюГруппировкиСтрока;
	ОтборПоПолюГруппировкиТаблица.Загрузить(Результат.ОтборПоПолюГруппировкиТаблица);
	ОтборФлаг = Результат.ОтборФлаг;
	
	ЗначениеВРеквизитФормы(Результат.ДеревоДанных, "ДеревоДанных");
	ДиаграммаАнализ = Результат.ДиаграммаАнализ;
	СуммаВыручкиВсего = Результат.СуммаВыручки;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля(ИмяПоля, ИдентификаторСтроки, КоллекцияЭлементовДерева, КлючСтроки, ПрекратитьПоиск) Экспорт
	
	Для Каждого СтрокаДерева Из КоллекцияЭлементовДерева Цикл
		
		Если ПрекратитьПоиск Тогда
			Возврат;
		КонецЕсли;
		
		ИндексыСерий = СтрРазделить(СтрокаДерева[ИмяПоля], "; ", Ложь);
		Если ИндексыСерий.Найти(КлючСтроки) <> Неопределено Тогда
			
			ИдентификаторСтроки = СтрокаДерева.ПолучитьИдентификатор();
			ПрекратитьПоиск = Истина;
			Возврат;
			
		КонецЕсли;
		
		КоллекцияЭлементов = СтрокаДерева.ПолучитьЭлементы();
		
		Если КоллекцияЭлементов.Количество() > 0 Тогда
			ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля(ИмяПоля, ИдентификаторСтроки, КоллекцияЭлементов, КлючСтроки, ПрекратитьПоиск);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьДиаграмму()
	
	ДиаграммаСкрытаПользователем = Истина;
	Элементы.ГруппаДиаграмма.Видимость = Ложь;
	Элементы.КартинкаДиаграммаПоказать.Видимость = Истина;
	СохранитьНастройкиОбработки(ДиаграммаСкрытаПользователем);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиаграмму()
	
	ДиаграммаСкрытаПользователем = Ложь;
	Элементы.ГруппаДиаграмма.Видимость = Истина;
	Элементы.КартинкаДиаграммаПоказать.Видимость = Ложь;
	СохранитьНастройкиОбработки(ДиаграммаСкрытаПользователем);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиОбработки(ДиаграммаСкрытаПользователем = Неопределено, ПоляГруппировок = Неопределено)
	
	КлассификацияВыручкиПоОКВЭДНастройки = Новый Структура;
	КлассификацияВыручкиПоОКВЭДНастройки.Вставить("ДиаграммаСкрытаПользователем", ДиаграммаСкрытаПользователем);
	КлассификацияВыручкиПоОКВЭДНастройки.Вставить("ПоляГруппировок", ПоляГруппировок);

	ХранилищеОбщихНастроек.Сохранить("КлассификацияВыручкиПоОКВЭДНастройки",, КлассификацияВыручкиПоОКВЭДНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДатаНачала    = РезультатВыбора.НачалоПериода;
	Объект.ДатаОкончания = РезультатВыбора.КонецПериода;
	
	ЗаполнитьДанные(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьНоменклатурнуюГруппу(РодительНоменклатурнаяГруппа, СтрокаДерева, ПолеГруппировкиНоменклатуры)
	
	Если ТипЗнч(СтрокаДерева.ПолеГруппировкиНоменклатуры) = Тип("СправочникСсылка.НоменклатурныеГруппы")
		И СтрокаДерева.ПолеГруппировкиНоменклатуры <> ПолеГруппировкиНоменклатуры Тогда
		РодительНоменклатурнаяГруппа = СтрокаДерева.ПолеГруппировкиНоменклатуры;
	Иначе
		СтрокаРодитель = СтрокаДерева.ПолучитьРодителя();
		Если СтрокаРодитель <> Неопределено Тогда
			ОпределитьНоменклатурнуюГруппу(РодительНоменклатурнаяГруппа, СтрокаРодитель, ПолеГруппировкиНоменклатуры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыФормыОСВПоСчету(Организация, ДатаНачала, ДатаОкончания, ПолеГруппировкиНоменклатуры, РодительНоменклатурнаяГруппа, Счет)
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("Организация",         Организация);
	ДополнительныеСвойства.Вставить("НачалоПериода",       ДатаНачала);
	ДополнительныеСвойства.Вставить("КонецПериода",        ДатаОкончания);
	ДополнительныеСвойства.Вставить("Счет",                Счет);
	ДополнительныеСвойства.Вставить("РежимРасшифровки",    Истина);
	ДополнительныеСвойства.Вставить("ПоказательБУ",        Истина);
	
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
	
	ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ДополнительныеСвойства.Счет);
	ДобавитьОтбор(ПолеГруппировкиНоменклатуры, ПользовательскиеОтборы, ДанныеСчета);
	Если РодительНоменклатурнаяГруппа <> Неопределено Тогда
		ДобавитьОтбор(РодительНоменклатурнаяГруппа, ПользовательскиеОтборы, ДанныеСчета);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	Возврат ПараметрыФормы;

КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыФормыАнализСубконто(Организация, ДатаНачала, ДатаОкончания, ПолеГруппировкиНоменклатуры)
	
	СписокВидовСубконто = Новый СписокЗначений;
	СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("Организация",         Организация);
	ДополнительныеСвойства.Вставить("НачалоПериода",       ДатаНачала);
	ДополнительныеСвойства.Вставить("КонецПериода",        ДатаОкончания);
	ДополнительныеСвойства.Вставить("СписокВидовСубконто", СписокВидовСубконто);
	ДополнительныеСвойства.Вставить("РежимРасшифровки",    Истина);
	ДополнительныеСвойства.Вставить("ПоказательБУ",        Истина);
	ДополнительныеСвойства.Вставить("ПоСубсчетам",         Истина);
	
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Субконто1", ПолеГруппировкиНоменклатуры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидРасшифровки", 2);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	Возврат ПараметрыФормы;

КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьОтбор(ЗначениеПоля, ПользовательскиеОтборы, ДанныеСчета)
	
	НомерСубконто = 0;
	ТипСубконто = ТипЗнч(ЗначениеПоля);
	Для Индекс = 1 По 3 Цикл
		ВидСубконтоТипЗначения = ДанныеСчета[СтрШаблон("ВидСубконто%1ТипЗначения", Индекс)];
		Если ТипЗнч(ВидСубконтоТипЗначения) = Тип("ОписаниеТипов") И ВидСубконтоТипЗначения.СодержитТип(ТипСубконто) Тогда
			НомерСубконто = Индекс;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ТипСубконто = Тип("СправочникСсылка.Номенклатура") И ЗначениеПоля.ЭтоГруппа Тогда
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
	Иначе
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Субконто" + НомерСубконто, ЗначениеПоля, ВидСравненияСКД);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = НСтр("ru = 'Отзыв о функциональности ""Классификация выручки по ОКВЭД""'");
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаОсновногоКодаОКВЭД(Результат)
	
	Элементы.ГруппаПроверкаОсновногоКода.Видимость = Истина;
	Если ЗначениеЗаполнено(Результат.ОсновнойКодОКВЭД) Тогда
		Элементы.ГруппаБаннерПодсказка.Видимость = Ложь;
		Элементы.ОКВЭДПредставление.Заголовок = НСтр("ru = ' Основной код ОКВЭД, указанный в организации'");
		
		Если ОКВЭДОрганизацииКод = Результат.ОсновнойКодОКВЭД.Код Тогда
			Элементы.ГруппаБаннерУспех.Видимость = Истина;
			Элементы.РезультатУспехКомментарийПроРаздел.Видимость = ЗначениеЗаполнено(Результат.КомментарийОсновнойКодОКВЭД);
			Элементы.РезультатУспехКомментарийПроРаздел.Заголовок = Результат.КомментарийОсновнойКодОКВЭД;
			Элементы.ГруппаБаннерСменитьОКВЭД.Видимость = Ложь;
		Иначе
			Элементы.ГруппаБаннерСменитьОКВЭД.Видимость = Истина;
			Элементы.СменитьОКВЭДКомментарийПроРаздел.Видимость = ЗначениеЗаполнено(Результат.КомментарийОсновнойКодОКВЭД);
			Элементы.СменитьОКВЭДКомментарийПроРаздел.Заголовок = Результат.КомментарийОсновнойКодОКВЭД;
			ОКВЭДОрганизацииНовый = Результат.ОсновнойКодОКВЭД.Представление;
			ОКВЭДОрганизацииНовыйКод = Результат.ОсновнойКодОКВЭД.Код;
			ОКВЭДОрганизацииНовыйНаименование = Результат.ОсновнойКодОКВЭД.Наименование;
			Элементы.ГруппаБаннерУспех.Видимость = Ложь;
			
			УправлениеВидимостьюСменитьОКВЭД();
		КонецЕсли;
		
	Иначе
		Элементы.ГруппаБаннерПодсказка.Видимость = ЗначениеЗаполнено(Результат.ДанныеНоменклатуры);
		Элементы.ГруппаБаннерСменитьОКВЭД.Видимость = Ложь;
		Элементы.ГруппаБаннерУспех.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюСменитьОКВЭД()
	
	Если Не Элементы.ГруппаБаннерСменитьОКВЭД.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	ЮридическоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ЮридическоеФизическоеЛицо");
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		ЗаголовокЕГР = НСтр("ru = 'ЕГРЮЛ'");
	ИначеЕсли ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ЗаголовокЕГР = НСтр("ru = 'ЕГРИП'");
	Иначе
		ЗаголовокЕГР = НСтр("ru = 'ЕГРЮЛ (ЕГРИП)'");
	КонецЕсли;
	
	ЕстьЕГРЗаявление = Ложь;
	СостояниеЗаявленияВЕГР = Обработки.РегистрацияОрганизации.СостояниеЗаявленияВЕГР(Объект.Организация);
	Если ЗначениеЗаполнено(СостояниеЗаявленияВЕГР.ЗаявлениеСсылка) Тогда
		ДанныеПомощникаЗаполнения = Обработки.РегистрацияОрганизации.ДанныеПомощникаЗаполнения(СостояниеЗаявленияВЕГР.ЗаявлениеСсылка);
		Если ДанныеПомощникаЗаполнения.Изменено.ОсновнойВидДеятельности Тогда
			ЕстьЕГРЗаявление = Истина;
			ЕГРЗаявление = СостояниеЗаявленияВЕГР.ЗаявлениеСсылка;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ВнестиИзмененияВЕГР.Видимость = Не ЕстьЕГРЗаявление;
	Элементы.ВнестиИзменениеВЕГРСсылка.Видимость = ЕстьЕГРЗаявление;
	Элементы.ВнестиИзменениеВЕГРСсылка.Заголовок = СтрШаблон(НСтр("ru = 'Внесение изменений в %1'"), ЗаголовокЕГР);
	Элементы.ВнестиИзмененияВЕГР.Заголовок = СтрШаблон(НСтр("ru = 'Изменить основной код ОКВЭД в %1'"), ЗаголовокЕГР);
	Элементы.ГруппаИзмененияВЕГР.Видимость = Ложь;
	Элементы.НадписьИзмененияВЕГРОшибка.Видимость = Ложь;
	
	Если ЕстьЕГРЗаявление Тогда
		Если СостояниеЗаявленияВЕГР.Ошибка Тогда
			Элементы.НадписьИзмененияВЕГРОшибка.Видимость = Истина;
			
			ТекстОшибки = СтрШаблон(НСтр("ru = 'При проверке заявления на изменение основного кода ОКВЭД в %1 обнаружены ошибки.
				|Исправьте их и отправьте заявление снова.'"), ЗаголовокЕГР);
			ШаблонСтроки =
				"%1
				|
				|<a href = ""%2"">%3</a>   <a href = ""%4"">%5</a>";
			
			Элементы.НадписьИзмененияВЕГРОшибка.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
				ШаблонСтроки,
				ТекстОшибки,
				"Протокол",
				НСтр("ru = 'Протокол'"),
				"ИсправлениеЗаявления",
				НСтр("ru = 'Внести исправления'"));
			
			Элементы.ВнестиИзмененияВЕГР.Видимость = Ложь;
			Элементы.ВнестиИзменениеВЕГРСсылка.Видимость = Ложь;
			
		ИначеЕсли СостояниеЗаявленияВЕГР.Документы Тогда
			Элементы.НадписьИзмененияВЕГР.Заголовок =
				СтрШаблон(НСтр("ru = 'Заявление на изменение основного кода ОКВЭД в %1 принято, изменения внесены в реестр.'"), ЗаголовокЕГР);
			Элементы.ГруппаИзмененияВЕГР.Видимость = Истина;
			Элементы.ВнестиИзмененияВЕГР.Видимость = Ложь;
			Элементы.ВнестиИзменениеВЕГРСсылка.Видимость = Ложь;
			
		ИначеЕсли СостояниеЗаявленияВЕГР.ОбновитьСтатус Тогда
			Элементы.НадписьИзмененияВЕГР.Заголовок =
				СтрШаблон(НСтр("ru = 'Заявление на изменение основного кода ОКВЭД в %1 находится на рассмотрении.'"), ЗаголовокЕГР);
			Элементы.ГруппаИзмененияВЕГР.Видимость = Истина;
			Элементы.ВнестиИзмененияВЕГР.Видимость = Ложь;
			Элементы.ВнестиИзменениеВЕГРСсылка.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникВнесенияИзменений(СоздатьПриОткрытии = Ложь)
	
	ПараметрыПомощника = РегистрацияОрганизацииКлиентСервер.НовыеПараметрыПомощникаВнесенияИзменений();
	ПараметрыПомощника.Организация = Объект.Организация;
	Если СоздатьПриОткрытии Тогда
		ПараметрыПомощника.СоздатьПриОткрытии = Истина;
	ИначеЕсли ЗначениеЗаполнено(ЕГРЗаявление) Тогда
		ПараметрыПомощника.Заявление = ЕГРЗаявление;
	КонецЕсли;
	ОКВЭД2ИзКлассификацииВыручки = Новый Структура;
	ОКВЭД2ИзКлассификацииВыручки.Вставить("ОсновнойКод", ОКВЭДОрганизацииНовыйКод);
	ОКВЭД2ИзКлассификацииВыручки.Вставить("ОсновнойКодНаименование", ОКВЭДОрганизацииНовыйНаименование);
	ПараметрыПомощника.ОКВЭД2ИзКлассификацииВыручки = ОКВЭД2ИзКлассификацииВыручки;
	
	РегистрацияОрганизацииКлиент.ОткрытьПомощникВнесенияИзменений(ПараметрыПомощника);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПриЗакрытииНаСервере();
	КонецЕсли;
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
