#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПолучитьДеревоПомеченныхНаУдалениеВФоне(Параметры, АдресРезультата) Экспорт
	
	КодПиктограммыСправочник = 4;
	КодПиктограммыДокумент = 13;
	
	ТекстЗапроса = "";
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	Для каждого Справочник Из Метаданные.Справочники Цикл
		Если РазделениеВключено И ЭтоОбщиеДанные(Справочник) Или Не ПравоДоступа("Чтение", Справочник) Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьГруппы = Справочник.Иерархический
			И Справочник.ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов;
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + СтрШаблон("ВЫБРАТЬ" + ?(ПустаяСтрока(ТекстЗапроса), " РАЗРЕШЕННЫЕ", "") + "
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Справочник%1.Ссылка) КАК Представление,
		|	Справочник%1.Ссылка КАК Ссылка,
		|	%2 КАК НомерКартинки,
		|	""%3"" КАК ВидОбъекта
		|ИЗ
		|	Справочник.%1 КАК Справочник%1
		|ГДЕ
		|	Справочник%1.ПометкаУдаления" + ?(ЕстьГруппы, " И НЕ Справочник%1.ЭтоГруппа", ""),
		Справочник.Имя, КодПиктограммыСправочник, СтрЗаменить(Справочник.Представление(), """", """"""));
	КонецЦикла;
	
	Для каждого Документ Из Метаданные.Документы Цикл
		Если РазделениеВключено И ЭтоОбщиеДанные(Документ) Или Не ПравоДоступа("Чтение", Документ) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + СтрШаблон("ВЫБРАТЬ ПРЕДСТАВЛЕНИЕССЫЛКИ(Документ%1.Ссылка) КАК Представление,
		|	Документ%1.Ссылка КАК Ссылка,
		|	%2 КАК НомерКартинки,
		|	""%3"" КАК ВидОбъекта
		|ИЗ
		|	Документ.%1 КАК Документ%1
		|ГДЕ
		|	Документ%1.ПометкаУдаления",
		Документ.Имя, КодПиктограммыСправочник, СтрЗаменить(Документ.Представление(), """", """"""));
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ИТОГИ ПО
	|	ВидОбъекта";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Дерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	Для каждого Строка Из Дерево.Строки Цикл
		Строка.Представление = СтрШаблон("%1 (%2)", Строка.ВидОбъекта, Строка.Строки.Количество());
	КонецЦикла;
	Дерево.Строки.Сортировать("Представление", Истина);
	
	ПоместитьВоВременноеХранилище(Дерево, АдресРезультата);
	
КонецПроцедуры

// Устанавливает видимость элементов формы панели настроек
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения  - форма панели настроек
//
Процедура ЗадатьВидимостьЭлементовФормы(Форма) Экспорт
	
	ЧастиПолногоИмениФормы = СтрРазделить(Форма.ИмяФормы, ".", Ложь);
	ИмяФормы = ЧастиПолногоИмениФормы[ЧастиПолногоИмениФормы.Количество() - 1];
	
	НастройкиВидимости = ВидимостьЭлементовФормы(ИмяФормы);
	
	Для Каждого ЭлементНастройкиВидимости Из НастройкиВидимости Цикл
		ЭлементФормы = Форма.Элементы.Найти(ЭлементНастройкиВидимости.Ключ);
		Если ЭлементФормы <> Неопределено Тогда
			ЭлементФормы.Видимость = ЭлементНастройкиВидимости.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НовыйТаблицаПоиска() Экспорт
	
	ТаблицаПоиска = Новый ТаблицаЗначений;
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ТипБулево = Новый ОписаниеТипов("Булево");
	
	// Ссылка для перехода к настройке
	ТаблицаПоиска.Колонки.Добавить("НавигационнаяСсылка", ТипСтрока);
	
	// Представление ссылки для вывода в результате поиска
	ТаблицаПоиска.Колонки.Добавить("ПредставлениеСсылки", ТипСтрока);
	
	// Текст по которому осуществляется поиск
	ТаблицаПоиска.Колонки.Добавить("ТекстДляПоиска", ТипСтрока);
	
	// Ключевые слова по тексту для поиска
	ТаблицаПоиска.Колонки.Добавить("КлючевыеСлова", ТипСтрока);
	
	// Признак доступности команды для пользователя
	ТаблицаПоиска.Колонки.Добавить("Доступна", ТипБулево);
	
	// Форма, в которой располагается команда перехода к настройке (для вычисления доступности)
	ТаблицаПоиска.Колонки.Добавить("ИмяФормы", ТипСтрока);
	
	// Элемент формы в Форме с командой перехода к настройке (для вычисления доступности)
	ТаблицаПоиска.Колонки.Добавить("ИмяЭлементаФормы", ТипСтрока);
	
	// Признак необходимости собрать тексты по заголовка и подсказкам формы с панелью настройки
	ТаблицаПоиска.Колонки.Добавить("СобратьТекстПоФорме", ТипБулево);
	
	// Имя формы панели настройки для сбора текстов по заголовкам и подсказкам
	ТаблицаПоиска.Колонки.Добавить("ИмяФормыДляСбораТекстов", ТипСтрока);
	
	// Признак, что тексты собраны по форме автоматически
	ТаблицаПоиска.Колонки.Добавить("ЗаполненоПоФорме", ТипБулево);
	
	Возврат ТаблицаПоиска;
	
КонецФункции

// Обновляет доступность элементов для поиска по настройкам видимости форм.
//  При первичном заполнении также заполняет таблицу текстами для поиска команд раздела.
//
// Параметры:
//  ТаблицаПоиска  - ТаблицаЗначений - таблица, выгруженная из реквизита ТаблицаПоиска формы ПанельНастройки
//  ПервичноеЗаполнение  - Булево - очистить таблицу и заполнить ее списком команд раздела Настройки и текстами для их поиска
//
// Возвращаемое значение:
//   ТаблицаПоиска  - ТаблицаЗначений - таблица для загрузки в реквизит ТаблицаПоиска формы ПанельНастройки
//
Функция ОбновитьТаблицуПоиска(ТаблицаПоиска, ПервичноеЗаполнение) Экспорт
	
	Если ПервичноеЗаполнение Тогда
		ЗаполнитьТаблицуПоиска(ТаблицаПоиска);
	КонецЕсли;
	
	УстановитьДоступностьВТаблицеПоискаПоФормам(ТаблицаПоиска);
	
	Возврат ТаблицаПоиска;
	
КонецФункции

#Область ПилотныйРазделБухгалтерия

Функция ПоказыватьПилотныйРазделБухгалтерия() Экспорт
	
	Результат = Не ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	Возврат Результат;
	
КонецФункции

Функция ИспользоватьПилотныйРазделБухгалтерия() Экспорт

	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПанелиПростойИнтерфейс",
		 "ИспользоватьПилотныйРазделБухгалтерия",
		 Истина);
	
КонецФункции

Процедура СохранитьНастройкуИспользоватьПилотныйРазделБухгалтерия(ИспользоватьПилотныйРаздел) Экспорт
	
	ЗаписатьСтатистикуПилотныйРазделБухгалтерия(ИспользоватьПилотныйРаздел);
		
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПанелиПростойИнтерфейс",
		"ИспользоватьПилотныйРазделБухгалтерия",
		ИспользоватьПилотныйРаздел);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоОбщиеДанные(ЭлементМетаданных)
	
	ОбластьДанныхОсновныеДанные = Метаданные.ОбщиеРеквизиты.ОбластьДанныхОсновныеДанные.Состав.Найти(ЭлементМетаданных);
	Если ОбластьДанныхОсновныеДанные <> Неопределено
		И ОбластьДанныхОсновныеДанные.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.НеИспользовать Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ВидимостьЭлементовФормы(ИмяФормы) 
	
	Результат = Новый Структура;
	
	Если ИмяФормы = "ПанельНастройки" Тогда
		ВидимостьКомандФормы_ПанельНастройки(Результат);
	ИначеЕсли ИмяФормы = "ПанельНастройкиНастройкиУчета" Тогда
		ВидимостьКомандФормы_ПанельНастройкиНастройкиУчета(Результат);
	ИначеЕсли ИмяФормы = "ПанельНастройкиИнтеграции" Тогда
		ВидимостьКомандФормы_ПанельНастройкиИнтеграции(Результат);
	ИначеЕсли ИмяФормы = "ПанельНастройкиИнструменты" Тогда
		ВидимостьКомандФормы_ПанельНастройкиИнструменты(Результат);
	ИначеЕсли ИмяФормы = "ПанельНастройкиСистема" Тогда
		ВидимостьКомандФормы_ПанельНастройкиСистема(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ВидимостьКомандФормы_ПанельНастройки(Результат)
	
	ФО_ВестиУчетПоДоговорам = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ФО_ИспользоватьДокументыРеализации = ПолучитьФункциональнуюОпцию("ИспользоватьДокументыРеализации");
	ФО_РазрешенУчетРегулярнойДеятельности = ПолучитьФункциональнуюОпцию("РазрешенУчетРегулярнойДеятельности");
	ФО_УстановкаМобильногоПриложенияПростойИнтерфейс = ПолучитьФункциональнуюОпцию("УстановкаМобильногоПриложенияПростойИнтерфейс");
	ФО_УстановкаМобильногоКлиентаБизнесСтарт = ПолучитьФункциональнуюОпцию("УстановкаМобильногоКлиентаБизнесСтарт");
	ФО_УстановкаМобильногоПриложенияСканированиеЧековПростойИнтерфейс = ПолучитьФункциональнуюОпцию("УстановкаМобильногоПриложенияСканированиеЧековПростойИнтерфейс");
	ФО_ТехнологияСервисаВЛокальномРежимеПростойИнтерфейс = ПолучитьФункциональнуюОпцию("ТехнологияСервисаВЛокальномРежимеПростойИнтерфейс");
	ФО_ТехнологияСервисаВМоделиСервисаПростойИнтерфейс = ПолучитьФункциональнуюОпцию("ТехнологияСервисаВМоделиСервисаПростойИнтерфейс");
	ФО_ПомеченныеНаУдалениеПростойИнтерфейс = ПолучитьФункциональнуюОпцию("ПомеченныеНаУдалениеПростойИнтерфейс");
	ФО_ИспользоватьУдалениеПомеченныхОбъектов = ПолучитьФункциональнуюОпцию("ИспользоватьУдалениеПомеченныхОбъектов");
	ФО_УдалениеПомеченныхОбъектовПростойИнтерфейс = ПолучитьФункциональнуюОпцию("УдалениеПомеченныхОбъектовПростойИнтерфейс");
	ФО_ИспользоватьРаспознаваниеДокументов = ПолучитьФункциональнуюОпцию("ИспользоватьРаспознаваниеДокументов");
	
	Доступ_ПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	Доступ_ПанельАдминистрированияБЭД = ПравоДоступа("Использование", Метаданные.Обработки.ПанельАдминистрированияБЭД);
	Доступ_ПанельАдминистрированияБПО = ПравоДоступа("Использование", Метаданные.Обработки.ПанельАдминистрированияБПО);
	Доступ_НастройкаКолонтитулов = ПравоДоступа("Просмотр", Метаданные.ОбщиеКоманды.НастройкаКолонтитулов);
	Доступ_УстановкаМобильногоПриложенияСканированиеЧеков = ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.ПомощникУстановкиМобильногоПриложенияСканированиеЧеков);
	
	Результат.Вставить("Интерфейс", ФО_ВестиУчетПоДоговорам Или ФО_ИспользоватьДокументыРеализации);
	Результат.Вставить("Обслуживание", Доступ_ПолноправныйПользователь И ФО_РазрешенУчетРегулярнойДеятельности);
	Результат.Вставить("Органайзер", Доступ_ПолноправныйПользователь И ФО_РазрешенУчетРегулярнойДеятельности);
	Результат.Вставить("ОбменЭлектроннымиДокументами", Доступ_ПанельАдминистрированияБЭД И ФО_РазрешенУчетРегулярнойДеятельности);
	Результат.Вставить("ПоддержкаОборудования", Доступ_ПанельАдминистрированияБПО);
	Результат.Вставить("ИнтернетПоддержкаИСервисы", Доступ_ПолноправныйПользователь И ФО_РазрешенУчетРегулярнойДеятельности); 
	Результат.Вставить("ПечатьАртикулов", ФО_ВестиУчетПоДоговорам Или ФО_ИспользоватьДокументыРеализации);
	Результат.Вставить("НастройкаКолонтитулов", Доступ_НастройкаКолонтитулов);
	Результат.Вставить("НастройкаЭлектроннойПочты", Доступ_ПолноправныйПользователь);
	Результат.Вставить("МобильныйКлиент", ФО_УстановкаМобильногоПриложенияПростойИнтерфейс);
	Результат.Вставить("МобильныйКлиентБизнесСтарт", ФО_УстановкаМобильногоКлиентаБизнесСтарт);
	Результат.Вставить("СканерЧеков", Доступ_УстановкаМобильногоПриложенияСканированиеЧеков
		И ФО_УстановкаМобильногоПриложенияСканированиеЧековПростойИнтерфейс);
	Результат.Вставить("СканерДокументов", Доступ_ПолноправныйПользователь
		И ФО_ИспользоватьРаспознаваниеДокументов);
	Результат.Вставить("ЗагрузкаДанныхИзСервиса", Доступ_ПолноправныйПользователь И ФО_ТехнологияСервисаВЛокальномРежимеПростойИнтерфейс);
	Результат.Вставить("ВыгрузитьДанныеДляПереходаВСервис", Доступ_ПолноправныйПользователь И ФО_ТехнологияСервисаВЛокальномРежимеПростойИнтерфейс);
	Результат.Вставить("ВыгрузитьДанныеВЛокальнуюВерсию", Доступ_ПолноправныйПользователь И ФО_ТехнологияСервисаВМоделиСервисаПростойИнтерфейс);
	Результат.Вставить("ПомеченныеНаУдаление", ФО_ПомеченныеНаУдалениеПростойИнтерфейс);    
	Результат.Вставить("УдалениеПомеченныхОбъектов", Доступ_ПолноправныйПользователь
		И (ФО_ИспользоватьУдалениеПомеченныхОбъектов Или ФО_УдалениеПомеченныхОбъектовПростойИнтерфейс));
	
КонецПроцедуры

Процедура ВидимостьКомандФормы_ПанельНастройкиНастройкиУчета(Результат)
	
	ФО_ФормированиеУставногоКапиталаПростойИнтерфейс = ПолучитьФункциональнуюОпцию("ФормированиеУставногоКапиталаПростойИнтерфейс");
	ФО_ИнтерфейсТаксиПростойИПиЮрЛица = ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростойИПиЮрЛица");
	ФО_ИспользоватьПлатежиПоРеестрам = ПолучитьФункциональнуюОпцию("ИспользоватьПлатежиПоРеестрам");
	ФО_ИспользуютсяПодарочныеСертификаты = ПолучитьФункциональнуюОпцию("ИспользуютсяПодарочныеСертификаты");
	ФО_ИспользоватьОплатуПоПлатежнымКартам = ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПоПлатежнымКартам");
	ФО_ОтображатьРазделТовары = ПолучитьФункциональнуюОпцию("ОтображатьРазделТовары");
	ФО_ОтображатьРазделыДокументыТоварыКонтрагенты = ПолучитьФункциональнуюОпцию("ОтображатьРазделыДокументыТоварыКонтрагенты");
	ФО_ВестиУчетРасчетовСКонтрагентами = ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСКонтрагентами");
	ФО_ИспользоватьДокументыПоступления = ПолучитьФункциональнуюОпцию("ИспользоватьДокументыПоступления");
	ФО_ОтображатьПодсистемуСотрудники = ПолучитьФункциональнуюОпцию("ОтображатьПодсистемуСотрудники");
	ФО_ВедетсяУчетЗарплатыКадров = ПолучитьФункциональнуюОпцию("ВедетсяУчетЗарплатыКадров");
	ФО_ИспользоватьНачислениеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
	
	Доступ_ВводНачальныхОстатков = ПравоДоступа("Просмотр", Метаданные.Обработки.ВводНачальныхОстатков);
	
	Результат.Вставить("НачальныеОстатки", Доступ_ВводНачальныхОстатков);
	Результат.Вставить("УставныйКапитал", ФО_ФормированиеУставногоКапиталаПростойИнтерфейс);
	Результат.Вставить("Подразделения", ФО_ИнтерфейсТаксиПростойИПиЮрЛица);
	Результат.Вставить("РегистрацииВНалоговыхОрганах", ФО_ИнтерфейсТаксиПростойИПиЮрЛица); 
	Результат.Вставить("ВидыОплат", ФО_ИспользоватьПлатежиПоРеестрам Или ФО_ИспользуютсяПодарочныеСертификаты
		Или ФО_ИспользоватьОплатуПоПлатежнымКартам);
	Результат.Вставить("ЛимитОстаткаКассы", ФО_ИнтерфейсТаксиПростойИПиЮрЛица);
	
	// Устанавливаем видимость не только для групп, но и для элементов групп для корректного заполнения таблицы поиска
	// в форме ПанельНастройки
	ГруппаТовары = ФО_ОтображатьРазделТовары Или ФО_ОтображатьРазделыДокументыТоварыКонтрагенты;
	Результат.Вставить("ГруппаТовары", ГруппаТовары);
	Результат.Вставить("Склады", ГруппаТовары И (ФО_ВестиУчетРасчетовСКонтрагентами Или ФО_ИспользоватьДокументыПоступления));
	Результат.Вставить("ТипыЦенНоменклатуры", ГруппаТовары);
	Результат.Вставить("ВидыНоменклатуры", ГруппаТовары);
	
	ГруппаСотрудники = ФО_ОтображатьПодсистемуСотрудники;
	Результат.Вставить("ГруппаСотрудники", ГруппаСотрудники);
	Результат.Вставить("НастройкиЗарплаты", ГруппаСотрудники И ФО_ВедетсяУчетЗарплатыКадров);
	Результат.Вставить("ФизическиеЛица", ГруппаСотрудники);
	Результат.Вставить("ЗарплатныеПроекты", ГруппаСотрудники И ФО_ИспользоватьНачислениеЗарплаты);
	
КонецПроцедуры

Процедура ВидимостьКомандФормы_ПанельНастройкиИнтеграции(Результат)
	
	ФО_ИспользоватьОбменСБанкамиБП = ПолучитьФункциональнуюОпцию("ИспользоватьОбменСБанкамиБП");
	ФО_РазрешенУчетРегулярнойДеятельности = ПолучитьФункциональнуюОпцию("РазрешенУчетРегулярнойДеятельности");
	ФО_ИспользуетсяАУСН = ПолучитьФункциональнуюОпцию("ИспользуетсяАУСН");
	ФО_ДоступнаИнтеграцияСПлатформойСамозанятые = ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые();
	ФО_ИспользоватьОбменСИнтернетМагазином = ПолучитьФункциональнуюОпцию("ИспользоватьОбменСИнтернетМагазином");
	ФИ_ИнтерфейсТаксиПростойИПиЮрЛица = ПолучитьФункциональнуюОпцию("ИнтерфейсТаксиПростойИПиЮрЛица");
	ФО_ИспользоватьМаркировку = ПолучитьФункциональнуюОпцию("ИспользоватьМаркировку");
	ФО_ИспользоватьДокументыРеализации = ПолучитьФункциональнуюОпцию("ИспользоватьДокументыРеализации");
		
	Доступ_ПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	Доступ_ОбменСИнтернетМагазином = ПравоДоступа("Использование", Метаданные.Обработки.ОбменСИнтернетМагазином);
	Доступ_ИнтеграцияSmartway = ПравоДоступа("Просмотр", Метаданные.Справочники.НастройкиЗагрузкиДанныхВнешнихСистем.Команды.Настройка);
	Доступ_ОбменИСМП = ПравоДоступа("Использование", Метаданные.Обработки.ПанельОбменИСМП);
	Доступ_ПодключениеАУСН = ПравоДоступа("Просмотр", Метаданные.Обработки.ПодключениеАУСН.Команды.ПодключениеАУСН);
	
	Результат.Вставить("ОбменСБанками", ФО_ИспользоватьОбменСБанкамиБП);
	Результат.Вставить("СинхронизацияДанных", Доступ_ПолноправныйПользователь И ФО_РазрешенУчетРегулярнойДеятельности);
	Результат.Вставить("ПодключениеАУСН", Доступ_ПодключениеАУСН И ФО_ИспользуетсяАУСН);
	Результат.Вставить("МойНалог", ФО_ДоступнаИнтеграцияСПлатформойСамозанятые);
	Результат.Вставить("ИнтернетМагазин", Доступ_ОбменСИнтернетМагазином И ФО_ИспользоватьОбменСИнтернетМагазином);
	Результат.Вставить("ИнтеграцияSmartway", Доступ_ИнтеграцияSmartway И ФИ_ИнтерфейсТаксиПростойИПиЮрЛица);
	Результат.Вставить("ОбменИСМП", Доступ_ОбменИСМП И ФО_ИспользоватьМаркировку);
	Результат.Вставить("УстановитьРасширениеМаг1С", ФО_ИспользоватьДокументыРеализации);
	
КонецПроцедуры

Процедура ВидимостьКомандФормы_ПанельНастройкиИнструменты(Результат)
	
	ФО_РаботаВЛокальномРежиме = ПолучитьФункциональнуюОпцию("РаботаВЛокальномРежиме");
	ФО_ВедетсяУчетОсновныхСредствПростойИнтерфейс = ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредствПростойИнтерфейс");
	ФО_ВестиУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров");
	ФО_ИспользоватьСкрытиеПерсональныхДанныхСубъектов = ПолучитьФункциональнуюОпцию("ИспользоватьСкрытиеПерсональныхДанныхСубъектов");
	ФО_ВозможенПереходНаУСН = ПолучитьФункциональнуюОпцию("ВозможенПереходНаУСН");
	ФО_ПрименяетсяУСН = ПолучитьФункциональнуюОпцию("ПрименяетсяУСН");
	ФО_ИспользоватьТипыЦенНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры");
	ФО_ОтображатьРазделТовары = ПолучитьФункциональнуюОпцию("ОтображатьРазделТовары");
	ФО_ОтображатьРазделыДокументыТоварыКонтрагенты = ПолучитьФункциональнуюОпцию("ОтображатьРазделыДокументыТоварыКонтрагенты");
	ФО_ИспользоватьСервисРаботаСНоменклатурой = ПолучитьФункциональнуюОпцию("ИспользоватьСервисРаботаСНоменклатурой");
	ФО_ИспользоватьОплатуСервисаБП = ПолучитьФункциональнуюОпцию("ИспользоватьОплатуСервисаБП");
	ФО_ОтображатьПодсистемуСотрудники = ПолучитьФункциональнуюОпцию("ОтображатьПодсистемуСотрудники");	
	ФО_УчетЗарплатыИКадровСредствамиБухгалтерии = ПолучитьФункциональнуюОпцию("УчетЗарплатыИКадровСредствамиБухгалтерии");
	ФО_ЗагрузкаИзТиС = ПолучитьФункциональнуюОпцию("ЗагрузкаИзТиС");
	ФО_РаботаВМоделиСервиса = ПолучитьФункциональнуюОпцию("РаботаВМоделиСервиса");
	ФО_РаботаВМоделиСервисаБТС = ПолучитьФункциональнуюОпцию("РаботаВМоделиСервисаБТС");
	ФО_ИспользуетсяОСНО = ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО");
	
	Доступ_ПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	Доступ_ТестированиеИИсправлениеОбластиДанных = ПравоДоступа("Просмотр", Метаданные.ОбщиеКоманды.ТестированиеИИсправлениеОбластиДанных);
	Доступ_НастройкиПроверкиРНПТ = ПравоДоступа("Просмотр", Метаданные.ОбщиеКоманды.НастройкиПроверкиРНПТ);
	Доступ_УничтожениеПерсональныхДанных = ПравоДоступа("Использование", Метаданные.Обработки.УничтожениеПерсональныхДанных);
	Доступ_ПомощникПоПереходуНаУСН = ПравоДоступа("Использование", Метаданные.Обработки.ПомощникПоПереходуНаУСН);
	Доступ_РаботаСНоменклатурой = ПравоДоступа("Использование", Метаданные.Обработки.РаботаСНоменклатурой);
	Доступ_Сервис1СКабинетСотрудника = ПравоДоступа("Просмотр", Метаданные.Обработки.ИнтеграцияКабинетСотрудника);
	Доступ_Подключение1ССверка20 = ПравоДоступа("Просмотр", Метаданные.ОбщиеКоманды.Подключение1ССверка20);
	Доступ_ЗагрузкаДанныхИзТиС = ПравоДоступа("Использование", Метаданные.Обработки.ЗагрузкаДанныхИзТиС);
	Доступ_ЗагрузкаИзДругойУчетнойСистемы = ПравоДоступа("Использование", Метаданные.Обработки.ЗагрузкаДанныхИзДругойУчетнойСистемы);
	Доступ_ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам = ПравоДоступа("Использование", Метаданные.Обработки.ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам);
	
	Результат.Вставить("СверткаИнформационнойБазы", Доступ_ПолноправныйПользователь И ФО_РаботаВЛокальномРежиме);
	Результат.Вставить("ПроверкаЗаписейРегистраБухгалтерии", Доступ_ПолноправныйПользователь);
	Результат.Вставить("ПроверкаПереходаНаФСБУ6", Доступ_ПолноправныйПользователь И ФО_ВедетсяУчетОсновныхСредствПростойИнтерфейс);
	Результат.Вставить("ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам", Доступ_ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам);
	Результат.Вставить("ПомощникИсключенияИзПрослеживаемости", Доступ_ПолноправныйПользователь И ФО_ВестиУчетПрослеживаемыхТоваров);
	Результат.Вставить("ТестированиеИИсправление", Доступ_ТестированиеИИсправлениеОбластиДанных И ФО_РаботаВМоделиСервисаБТС);
	Результат.Вставить("УничтожениеПерсональныхДанных", Доступ_УничтожениеПерсональныхДанных И ФО_ИспользоватьСкрытиеПерсональныхДанныхСубъектов);
	Результат.Вставить("ПомощникПоПереходуНаУСН", Доступ_ПомощникПоПереходуНаУСН И ФО_ВозможенПереходНаУСН);
	Результат.Вставить("ВводОстатковПриИзмененииОбъектаУСН", ФО_ПрименяетсяУСН); 
	Результат.Вставить("УстановкаЦенНоменклатуры", ФО_ИспользоватьТипыЦенНоменклатуры
		И (ФО_ОтображатьРазделТовары Или ФО_ОтображатьРазделыДокументыТоварыКонтрагенты));
	Результат.Вставить("НастройкиРаспознаванияДокументов", Доступ_ПолноправныйПользователь);
	Результат.Вставить("РаботаСНоменклатурой", Доступ_РаботаСНоменклатурой И ФО_ИспользоватьСервисРаботаСНоменклатурой);
	Результат.Вставить("НастройкиБиллинга", Доступ_ПолноправныйПользователь И ФО_ИспользоватьОплатуСервисаБП);
	Результат.Вставить("НастройкиПроверкиРНПТ", Доступ_НастройкиПроверкиРНПТ И ФО_ВестиУчетПрослеживаемыхТоваров);
	Результат.Вставить("Сервис1СКабинетСотрудника", Доступ_Сервис1СКабинетСотрудника И ФО_ОтображатьПодсистемуСотрудники
		И ФО_УчетЗарплатыИКадровСредствамиБухгалтерии);
	Результат.Вставить("Подключение1ССверка20", Доступ_Подключение1ССверка20);
	Результат.Вставить("ЗагрузкаИз1СПредприятия77", Доступ_ПолноправныйПользователь);
	Результат.Вставить("ЗагрузкаДанныхИзТиС", Доступ_ЗагрузкаДанныхИзТиС И ФО_ЗагрузкаИзТиС);
	Результат.Вставить("ЗагрузкаИзДругойУчетнойСистемы", Доступ_ЗагрузкаИзДругойУчетнойСистемы И ФО_РаботаВМоделиСервиса);
	Результат.Вставить("ИсправлениеРеквизитаБезЗакрывающих", Доступ_ПолноправныйПользователь);
	Результат.Вставить("ПомощникПереходаСУСННаОСНО", ФО_ИспользуетсяОСНО);
	
КонецПроцедуры

Процедура ВидимостьКомандФормы_ПанельНастройкиСистема(Результат)
	
	ФО_РазрешенУчетРегулярнойДеятельности = ПолучитьФункциональнуюОпцию("РазрешенУчетРегулярнойДеятельности");
	ФО_ВестиУчетПоДоговорам = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ФО_ИспользоватьДокументыРеализации = ПолучитьФункциональнуюОпцию("ИспользоватьДокументыРеализации");
	
	Доступ_ПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	Доступ_НастройкиРегистровУчета = ПравоДоступа("Просмотр", Метаданные.Обработки.ПанельАдминистрированияБП.Команды.НастройкиРегистровУчета);
	Доступ_ОткрытьНастройкиОтправкиPushУведомлений = ПравоДоступа("Просмотр", Метаданные.ОбщиеКоманды.ОткрытьНастройкиОтправкиPushУведомлений);
	
	Результат.Вставить("ОбщиеНастройки", Доступ_ПолноправныйПользователь И ФО_РазрешенУчетРегулярнойДеятельности);
	Результат.Вставить("ПараметрыУчета", ФО_ВестиУчетПоДоговорам Или ФО_ИспользоватьДокументыРеализации);
	Результат.Вставить("ПечатныеФормыОтчетыИОбработки", Доступ_ПолноправныйПользователь И ФО_РазрешенУчетРегулярнойДеятельности);
	Результат.Вставить("ПроведениеДокументов", ФО_ВестиУчетПоДоговорам Или ФО_ИспользоватьДокументыРеализации);
	Результат.Вставить("НастройкиРегистровУчета", Доступ_НастройкиРегистровУчета И (ФО_ВестиУчетПоДоговорам Или ФО_ИспользоватьДокументыРеализации));
	Результат.Вставить("НастройкиПользователейИПрав", Доступ_ПолноправныйПользователь И ФО_РазрешенУчетРегулярнойДеятельности);
	Результат.Вставить("НастройкиРаботыСФайлами", Доступ_ПолноправныйПользователь И ФО_РазрешенУчетРегулярнойДеятельности);
	Результат.Вставить("НастройкиОтправкиPushУведомлений", Доступ_ОткрытьНастройкиОтправкиPushУведомлений);
	Результат.Вставить("Производительность", Доступ_ПолноправныйПользователь И (ФО_ВестиУчетПоДоговорам Или ФО_ИспользоватьДокументыРеализации));
	
КонецПроцедуры

#Область ПоискНастроек

Процедура ЗаполнитьТаблицуПоиска(ТаблицаПоиска)
	
	ТаблицаПоиска.Очистить();
	
	ЗаполнитьТаблицуПоиска_Основное(ТаблицаПоиска);
	ЗаполнитьТаблицуПоиска_НастройкиУчета(ТаблицаПоиска);
	ЗаполнитьТаблицуПоиска_НастройкиИнтеграции(ТаблицаПоиска);
	ЗаполнитьТаблицуПоиска_НастройкиИнструменты(ТаблицаПоиска);
	ЗаполнитьТаблицуПоиска_НастройкиСистема(ТаблицаПоиска);
	ЗаполнитьТаблицуПоиска_Функциональность(ТаблицаПоиска);
	
	ДобавитьТекстыПоПредставлениямСсылок(ТаблицаПоиска);
	
	ТаблицаПоиска.Сортировать("НавигационнаяСсылка");
	
	ЗаполнитьКлючевыеСловаПоиска(ТаблицаПоиска);
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПоиска_Основное(ТаблицаПоиска)
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.ГлавноеПерсональныеНастройки",
		"ПанельНастройки", "ПерсональныеНастройки",
		НСтр("ru='Персональные настройки'"), НСтр("ru='Персональные настройки'"),
		"ОбщаяФорма.ПерсональныеНастройки");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.Интерфейс",
		"ПанельНастройки", "Интерфейс",
		НСтр("ru='Интерфейс'"), НСтр("ru='Интерфейс'"),
		"Обработка.ПанельАдминистрированияБП.Форма.Интерфейс");
		
	Текст = НСтр("ru='Контроль за состоянием программы, резервное копирование и восстановление, оптимизация быстродействия.'");
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.Обслуживание",
		"ПанельНастройки", "Обслуживание",
		НСтр("ru='Обслуживание'"), Текст,
		"Обработка.ПанельАдминистрированияБСП.Форма.Обслуживание");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.Органайзер",
		"ПанельНастройки", "Органайзер",
		НСтр("ru='Органайзер'"), НСтр("ru='Настройка электронной почты, заметок, напоминаний и бизнес-процессов.'"),
		"Обработка.ПанельАдминистрированияБСП.Форма.Органайзер");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБЭД.Команда.ОбменЭлектроннымиДокументами",
		"ПанельНастройки", "ОбменЭлектроннымиДокументами",
		НСтр("ru='Обмен электронными документами'"), НСтр("ru='Обмен электронными документами'"),
		"Обработка.ПанельАдминистрированияБЭД.Форма.ОбщиеНастройки");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБПО.Команда.ПоддержкаОборудования",
		"ПанельНастройки", "ПоддержкаОборудования",
		НСтр("ru='Поддержка оборудования'"), НСтр("ru='Поддержка оборудования'"),
		"Обработка.ПанельАдминистрированияБПО.Форма.Настройки");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.ИнтернетПоддержкаИСервисы",
		"ПанельНастройки", "ИнтернетПоддержкаИСервисы",
		НСтр("ru='Интернет-поддержка и сервисы'"), НСтр("ru='Интернет-сервисы и поддержка. Загрузка классификаторов.'"),
		"Обработка.ПанельАдминистрированияБСП.Форма.ИнтернетПоддержкаИСервисы");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБП.Команда.ПечатьАртикулов",
		"ПанельНастройки", "ПечатьАртикулов",
		НСтр("ru='Печать артикулов'"), НСтр("ru='Вывод артикулов номенклатуры в печатных формах.'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.НастройкаКолонтитулов",
		"ПанельНастройки", "НастройкаКолонтитулов",
		НСтр("ru='Настройка колонтитулов'"), НСтр("ru='Настройка колонтитулов'"));
	
	Текст = НСтр("ru='Открыть настройку электронной почты для рассылок уведомлений из программы'");
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.НастройкаСистемнойУчетнойЗаписиЭлектроннойПочты",
		"ПанельНастройки", "НастройкаЭлектроннойПочты",
		НСтр("ru='Настройка электронной почты'"), Текст);
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.МобильноеПриложение",
		"ПанельНастройки", "МобильнаяБухгалтерия",
		НСтр("ru='1С:Мобильная бухгалтерия'"), НСтр("ru='1С:Мобильная бухгалтерия'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.МобильноеПриложениеСканированиеЧеков",
		"ПанельНастройки", "СканерЧеков",
		НСтр("ru='1С:Сканер чеков'"), НСтр("ru='1С:Сканер чеков'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.ЗагрузкаДанныхИзСервиса",
		"ПанельНастройки", "ЗагрузкаДанныхИзСервиса",
		НСтр("ru='Загрузка данных из сервиса'"), НСтр("ru='Загрузка данных из сервиса'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.ВыгрузитьДанныеДляПереходаВСервис",
		"ПанельНастройки", "ВыгрузитьДанныеДляПереходаВСервис",
		НСтр("ru='Выгрузить данные для перехода в сервис'"), НСтр("ru='Выгрузить данные для перехода в сервис'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.ВыгрузитьДанныеВЛокальнуюВерсию",
		"ПанельНастройки", "ВыгрузитьДанныеВЛокальнуюВерсию",
		НСтр("ru='Выгрузить данные в локальную версию'"), НСтр("ru='Выгрузить данные в локальную версию'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанелиПростойИнтерфейс.Команда.ПомеченныеНаУдаление",
		"ПанельНастройки", "ПомеченныеНаУдаление",
		НСтр("ru='Помеченные на удаление'"), НСтр("ru='Помеченные на удаление'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.УдалениеПомеченныхОбъектов.Команда.УдалениеПомеченныхОбъектов",
		"ПанельНастройки", "УдалениеПомеченныхОбъектов",
		НСтр("ru='Удаление помеченных объектов'"), НСтр("ru='Поиск и удаление объектов, предварительно помеченных на удаление'"));
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПоиска_НастройкиУчета(ТаблицаПоиска)
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/РегистрСведений.УчетнаяПолитика.Команда.УчетнаяПолитикаПростойИнтерфейс",
		"ПанельНастройкиНастройкиУчета", "УчетнаяПолитика",
		НСтр("ru='Учетная политика'"), НСтр("ru='Учетная политика'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ВводНачальныхОстатков",
		"ПанельНастройкиНастройкиУчета", "НачальныеОстатки",
		НСтр("ru='Начальные остатки'"), НСтр("ru='Помощник ввода начальных остатков'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Документ.ФормированиеУставногоКапитала",
		"ПанельНастройкиНастройкиУчета", "УставныйКапитал",
		НСтр("ru='Уставный капитал'"), НСтр("ru='Уставный капитал'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.ПодразделенияОрганизаций",
		"ПанельНастройкиНастройкиУчета", "Подразделения",
		НСтр("ru='Подразделения'"), НСтр("ru='Подразделения'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.РегистрацииВНалоговомОргане",
		"ПанельНастройкиНастройкиУчета", "РегистрацииВНалоговыхОрганах",
		НСтр("ru='Регистрации в налоговых органах'"), НСтр("ru='Регистрации в налоговых органах'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.Валюты",
		"ПанельНастройкиНастройкиУчета", "Валюты",
		НСтр("ru='Валюты'"), НСтр("ru='Валюты'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.ВидыОплатОрганизаций",
		"ПанельНастройкиНастройкиУчета", "ВидыОплат",
		НСтр("ru='Виды оплат'"), НСтр("ru='Виды оплат'"));
	
	ПредставлениеСсылки = НСтр("ru='Банковские счета'");
	ПараметрыФормы = Новый Структура("БанковскиеСчетаОрганизаций", Истина);
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		ПолучитьНавигационнуюСсылку(Метаданные.Справочники.БанковскиеСчета, ПредставлениеСсылки, ПараметрыФормы),
		"ПанельНастройкиНастройкиУчета", "БанковскиеСчета",
		ПредставлениеСсылки, НСтр("ru='Банковские счета'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/РегистрСведений.ЛимитОстаткаКассыОрганизаций",
		"ПанельНастройкиНастройкиУчета", "ЛимитОстаткаКассы",
		НСтр("ru='Лимиты остатка кассы'"), НСтр("ru='Лимиты остатка кассы'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.Склады",
		"ПанельНастройкиНастройкиУчета", "Склады",
		НСтр("ru='Склады'"), НСтр("ru='Склады'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.ТипыЦенНоменклатуры",
		"ПанельНастройкиНастройкиУчета", "ТипыЦенНоменклатуры",
		НСтр("ru='Типы цен номенклатуры'"), НСтр("ru='Типы цен номенклатуры'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.ВидыНоменклатуры",
		"ПанельНастройкиНастройкиУчета", "ВидыНоменклатуры",
		НСтр("ru='Виды номенклатуры'"), НСтр("ru='Виды номенклатуры'"));
	
	Текст = НСтр("ru='Настройки учета заработной платы, учета НДФЛ и страховых взносов, а также кадрового учета.'");
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.НастройкиЗарплаты",
		"ПанельНастройкиНастройкиУчета", "НастройкиЗарплаты",
		НСтр("ru='Настройки зарплаты'"), Текст,
		"ОбщаяФорма.НастройкиЗарплаты");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.ФизическиеЛица",
		"ПанельНастройкиНастройкиУчета", "ФизическиеЛица",
		НСтр("ru='Физические лица'"), НСтр("ru='Физические лица'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.ЗарплатныеПроекты",
		"ПанельНастройкиНастройкиУчета", "ЗарплатныеПроекты",
		НСтр("ru='Зарплатные проекты'"), НСтр("ru='Зарплатные проекты'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/ПланСчетов.Хозрасчетный",
		"ПанельНастройкиНастройкиУчета", "ПланСчетов",
		НСтр("ru='План счетов'"), НСтр("ru='План счетов бухгалтерского учета'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.НоменклатурныеГруппы",
		"ПанельНастройкиНастройкиУчета", "НоменклатурныеГруппы",
		НСтр("ru='Номенклатурные группы'"), НСтр("ru='Номенклатурные группы'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.СтатьиЗатрат",
		"ПанельНастройкиНастройкиУчета", "СтатьиЗатрат",
		НСтр("ru='Статьи затрат'"), НСтр("ru='Статьи затрат'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.ПрочиеДоходыИРасходы",
		"ПанельНастройкиНастройкиУчета", "ПрочиеДоходыИРасходы",
		НСтр("ru='Прочие доходы и расходы'"), НСтр("ru='Прочие доходы и расходы'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.ДоходыБудущихПериодов",
		"ПанельНастройкиНастройкиУчета", "ДоходыБудущихПериодов",
		НСтр("ru='Доходы будущих периодов'"), НСтр("ru='Доходы будущих периодов'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Справочник.РасходыБудущихПериодов",
		"ПанельНастройкиНастройкиУчета", "РасходыБудущихПериодов",
		НСтр("ru='Расходы будущих периодов'"), НСтр("ru='Расходы будущих периодов'"));
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПоиска_НастройкиИнтеграции(ТаблицаПоиска)
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.НастройкиДиректБанкаОбменСБанкамиБП",
		"ПанельНастройкиИнтеграции", "ОбменСБанками",
		НСтр("ru='Обмен с банками'"), НСтр("ru='Обмен с банками'"));
	
	Текст = НСтр("ru='Синхронизация данных с другими программами и организация совместной работы в распределенной информационной базе'");
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.СинхронизацияДанных",
		"ПанельНастройкиИнтеграции", "СинхронизацияДанных",
		НСтр("ru='Синхронизация данных'"), Текст);
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПодключениеАУСН.Команда.ПодключениеАУСН",
		"ПанельНастройкиИнтеграции", "ПодключениеАУСН",
		НСтр("ru='Подключение к сервису АУСН'"), НСтр("ru='Подключение к сервису АУСН'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПомощникИнтеграцииСПлатформойСамозанятые.Команда.ПомощникИнтеграции",
		"ПанельНастройкиИнтеграции", "МойНалог",
		НСтр("ru='Подключение к сервису Мой налог'"), НСтр("ru='Подключение к сервису Мой налог'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ОбменСИнтернетМагазином.Команда.ОткрытьНастройкиОбменСИнтернетМагазином",
		"ПанельНастройкиИнтеграции", "ИнтернетМагазин",
		НСтр("ru='Обмен с интернет-магазином'"), НСтр("ru='Обмен с интернет-магазином'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Справочник.НастройкиЗагрузкиДанныхВнешнихСистем.Команда.Настройка",
		"ПанельНастройкиИнтеграции", "ИнтеграцияSmartway",
		НСтр("ru='Интеграция со Smartway'"), НСтр("ru='Интеграция со Smartway'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ПанельОбменИСМП",
		"ПанельНастройкиИнтеграции", "ОбменИСМП",
		НСтр("ru='Обмен с ИС МП (обувь, одежда, табак...)'"), НСтр("ru='Обмен с ИС МП (обувь, одежда, табак...)'"));
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПоиска_НастройкиИнструменты(ТаблицаПоиска)
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.СверткаИнформационнойБазы",
		"ПанельНастройкиИнструменты", "СверткаИнформационнойБазы",
		НСтр("ru='Свертка информационной базы'"), НСтр("ru='Свертка информационной базы'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ПроверкаЗаписейРегистраБухгалтерии",
		"ПанельНастройкиИнструменты", "ПроверкаЗаписейРегистраБухгалтерии",
		НСтр("ru='Проверка записей регистра бухгалтерии'"), НСтр("ru='Проверка записей регистра бухгалтерии'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ПроверкаПереходаНаФСБУ6",
		"ПанельНастройкиИнструменты", "ПроверкаПереходаНаФСБУ6",
		НСтр("ru='Проверка перехода на ФСБУ 6'"), НСтр("ru='Проверка перехода на ФСБУ 6'"));
		
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам",
		"ПанельНастройкиИнструменты", "ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам",
		НСтр("ru='Проверка дат изменения процентных ставок по займам'"), НСтр("ru='Проверка дат изменения процентных ставок по займам'"));	
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ПомощникИсключенияНоменклатурыИзПрослеживаемости",
		"ПанельНастройкиИнструменты", "ПомощникИсключенияИзПрослеживаемости",
		НСтр("ru='Помощник исключения из прослеживаемости'"), НСтр("ru='Помощник исключения из прослеживаемости'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.ТестированиеИИсправлениеОбластиДанных",
		"ПанельНастройкиИнструменты", "ТестированиеИИсправление",
		НСтр("ru='Тестирование и исправление'"), НСтр("ru='Тестирование и исправление'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.УничтожениеПерсональныхДанных",
		"ПанельНастройкиИнструменты", "УничтожениеПерсональныхДанных",
		НСтр("ru='Уничтожение персональных данных'"), НСтр("ru='Уничтожение персональных данных'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ПомощникПоПереходуНаУСН",
		"ПанельНастройкиИнструменты", "ПомощникПоПереходуНаУСН",
		НСтр("ru='Помощник по переходу на УСН'"), НСтр("ru='Помощник по переходу на УСН'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ВводОстатковПриИзмененииОбъектаУСН",
		"ПанельНастройкиИнструменты", "ВводОстатковПриИзмененииОбъектаУСН",
		НСтр("ru='Помощник ввода остатков при смене объекта УСН'"), НСтр("ru='Помощник ввода остатков при смене объекта УСН'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/list/Документ.УстановкаЦенНоменклатуры",
		"ПанельНастройкиИнструменты", "УстановкаЦенНоменклатуры",
		НСтр("ru='Установка цен номенклатуры'"), НСтр("ru='Установка цен номенклатуры'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБРД.Команда.НастройкиРаспознаванияДокументов",
		"ПанельНастройкиИнструменты", "НастройкиРаспознаванияДокументов",
		НСтр("ru='Настройки распознавания документов'"), НСтр("ru='Настройки распознавания документов'"),
		"Обработка.ПанельАдминистрированияБРД.Форма.НастройкиРаспознаванияДокументов");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.РаботаСНоменклатурой.Команда.РаботаСНоменклатурой",
		"ПанельНастройкиИнструменты", "РаботаСНоменклатурой",
		НСтр("ru='1С:Номенклатура'"), НСтр("ru='1С:Номенклатура'"),
		"Обработка.РаботаСНоменклатурой.Форма.ПанельАдминистрирования");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.НастройкиБиллинга.Команда.НастройкиБиллинга",
		"ПанельНастройкиИнструменты", "НастройкиБиллинга",
		НСтр("ru='Настройки биллинга'"), НСтр("ru='Настройки биллинга'"),
		"Обработка.НастройкиБиллинга.Форма.НастройкиБиллинга");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.НастройкиПроверкиРНПТ",
		"ПанельНастройкиИнструменты", "НастройкиПроверкиРНПТ",
		НСтр("ru='Настройки проверки РНПТ'"), НСтр("ru='Настройки проверки РНПТ'"),
		"ОбщаяФорма.ВключениеПроверкиРНПТ");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.ИнтеграцияССервисомКабинетСотрудника",
		"ПанельНастройкиИнструменты", "Сервис1СКабинетСотрудника",
		НСтр("ru='Сервис 1С:Кабинет сотрудника'"), НСтр("ru='Сервис 1С:Кабинет сотрудника'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.Подключение1ССверка20",
		"ПанельНастройкиИнструменты", "Подключение1ССверка20",
		НСтр("ru='1С:Сверка 2.0'"), НСтр("ru='1С:Сверка 2.0'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ПереносДанныхИзИнформационныхБаз1СПредприятия77",
		"ПанельНастройкиИнструменты", "ЗагрузкаИз1СПредприятия77",
		НСтр("ru='Загрузка из 1С:Предприятия 7.7'"), НСтр("ru='Загрузка из 1С:Предприятия 7.7'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ЗагрузкаДанныхИзТиС",
		"ПанельНастройкиИнструменты", "ЗагрузкаДанныхИзТиС",
		НСтр("ru='Загрузка из Торговля и Склад 7.7'"), НСтр("ru='Загрузка данных из конфигурации ""Торговля и Склад""'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ЗагрузкаДанныхИзДругойУчетнойСистемы",
		"ПанельНастройкиИнструменты", "ЗагрузкаИзДругойУчетнойСистемы",
		НСтр("ru='Загрузка из другой учетной системы'"), НСтр("ru='Загрузка из другой учетной системы'"));
		
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/app/Обработка.ИсправлениеРеквизитаБезЗакрывающихДокументов",
		"ПанельНастройкиИнструменты", "ИсправлениеРеквизитаБезЗакрывающихДокументов",
		НСтр("ru='Исправление реквизита ""Без закрывающих документов"" для УСН с НДС'"),
		НСтр("ru='Исправление реквизита ""Без закрывающих документов"" для УСН с НДС'"));

КонецПроцедуры

Процедура ЗаполнитьТаблицуПоиска_НастройкиСистема(ТаблицаПоиска)
	
	Текст = НСтр("ru='Изменение заголовка программы, настройка версионирования объектов, дополнительных реквизитов и других общих настроек.'");
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.ОбщиеНастройки",
		"ПанельНастройкиСистема", "ОбщиеНастройки",
		НСтр("ru='Общие настройки'"), Текст,
		"Обработка.ПанельАдминистрированияБСП.Форма.ОбщиеНастройки");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБП.Команда.ПараметрыУчета",
		"ПанельНастройкиСистема", "ПараметрыУчета",
		НСтр("ru='Параметры учета'"), НСтр("ru='Параметры учета'"),
		"Обработка.ПанельАдминистрированияБП.Форма.ПараметрыУчета");
	
	Текст = НСтр("ru='Настройка печатных форм, вариантов отчетов, рассылок отчетов, дополнительных отчетов и обработок.'");
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.ПечатныеФормыОтчетыИОбработки",
		"ПанельНастройкиСистема", "ПечатныеФормыОтчетыИОбработки",
		НСтр("ru='Печатные формы, отчеты и обработки'"), Текст,
		"Обработка.ПанельАдминистрированияБСП.Форма.ПечатныеФормыОтчетыИОбработки");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБП.Команда.ПроведениеДокументов",
		"ПанельНастройкиСистема", "ПроведениеДокументов",
		НСтр("ru='Проведение документов'"), НСтр("ru='Проведение документов'"),
		"Обработка.ПанельАдминистрированияБП.Форма.ПроведениеДокументов");
	
	Текст = НСтр("ru='Администрирование пользователей, настройка групп доступа, предоставление доступа для внешних пользователей, управление пользовательскими настройками.'");
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.НастройкиПользователейИПрав",
		"ПанельНастройкиСистема", "НастройкиПользователейИПрав",
		НСтр("ru='Настройки пользователей и прав'"), Текст,
		"Обработка.ПанельАдминистрированияБСП.Форма.НастройкиПользователейИПрав");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБСП.Команда.НастройкиРаботыСФайлами",
		"ПанельНастройкиСистема", "НастройкиРаботыСФайлами",
		НСтр("ru='Настройки работы с файлами'"), НСтр("ru='Настройка параметров загрузки, хранения и обработки файлов.'"),
		"Обработка.ПанельАдминистрированияБСП.Форма.НастройкиРаботыСФайлами");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБП.Команда.НастройкиРегистровУчета",
		"ПанельНастройкиСистема", "НастройкиРегистровУчета",
		НСтр("ru='Настройки регистров учета'"), НСтр("ru='Настройки регистров учета'"),
		"Обработка.ПанельАдминистрированияБП.Форма.НастройкиРегистровУчета");
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/ОбщаяКоманда.ОткрытьНастройкиОтправкиPushУведомлений",
		"ПанельНастройкиСистема", "НастройкиОтправкиPushУведомлений",
		НСтр("ru='Настройка отправки push-уведомлений'"), НСтр("ru='Настройка отправки push-уведомлений'"));
	
	ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска,
		"e1cib/command/Обработка.ПанельАдминистрированияБП.Команда.Производительность",
		"ПанельНастройкиСистема", "Производительность",
		НСтр("ru='Производительность'"), НСтр("ru='Производительность'"),
		"Обработка.ПанельАдминистрированияБП.Форма.Производительность");
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуПоиска_Функциональность(ТаблицаПоиска)
	
	ОписаниеФункциональности = Обработки.ФункциональностьПрограммы.ОписаниеФункциональности();
	ОписаниеФункциональности.Сортировать("Раздел");
	
	ТекРаздел = Неопределено;
	
	Для Каждого СтрокаОписания Из ОписаниеФункциональности Цикл
		
		Если ТекРаздел <> СтрокаОписания.Раздел Тогда
			ТекРаздел = СтрокаОписания.Раздел;
			Если ТекРаздел = "ЗарплатаКадры" Тогда
				СинонимРаздела = "Сотрудники";
			Иначе
				СинонимРаздела = ОбщегоНазначенияБПКлиентСервер.СинонимИзИмени(ТекРаздел);
			КонецЕсли; 
			ПредставлениеСсылки = СтрШаблон(НСтр("ru='Функциональность (%1)'"), СинонимРаздела);
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Метаданные.Обработки.ФункциональностьПрограммы,
				ПредставлениеСсылки, Новый Структура("ТекущаяСтраница", "Группа" + ТекРаздел));
		КонецЕсли;
		
		Если СтрокаОписания.Доступна Тогда
			ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска, НавигационнаяСсылка, , , ПредставлениеСсылки, СтрокаОписания.Заголовок);
			ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска, НавигационнаяСсылка, , , ПредставлениеСсылки, СтрокаОписания.Подсказка);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьСтрокуВТаблицуПоиска(ТаблицаПоиска, НавигационнаяСсылка, ИмяФормы, ИмяЭлементаФормы, ПредставлениеСсылки, ТекстДляПоиска, ИмяФормыДляСбораТекстов = "")
	
	Если ПустаяСтрока(ТекстДляПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	НовСтр = ТаблицаПоиска.Добавить();
	НовСтр.НавигационнаяСсылка = НавигационнаяСсылка;
	НовСтр.ИмяФормы = ИмяФормы;
	НовСтр.ИмяЭлементаФормы = ИмяЭлементаФормы;
	НовСтр.ПредставлениеСсылки = ПредставлениеСсылки;
	НовСтр.ТекстДляПоиска = ТекстДляПоиска;
	НовСтр.Доступна = Истина;
	
	Если ЗначениеЗаполнено(ИмяФормыДляСбораТекстов) Тогда
		НовСтр.СобратьТекстПоФорме = Истина; // Если задано имя формы, то тексты для поиска будут заполнены заголовками элементов указанной формы
		НовСтр.ИмяФормыДляСбораТекстов = ИмяФормыДляСбораТекстов;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДоступностьВТаблицеПоискаПоФормам(ТаблицаПоиска)
	
	УстановитьДоступностьВТаблицеПоискаПоФорме(ТаблицаПоиска, "ПанельНастройки");
	УстановитьДоступностьВТаблицеПоискаПоФорме(ТаблицаПоиска, "ПанельНастройкиНастройкиУчета");
	УстановитьДоступностьВТаблицеПоискаПоФорме(ТаблицаПоиска, "ПанельНастройкиИнтеграции");
	УстановитьДоступностьВТаблицеПоискаПоФорме(ТаблицаПоиска, "ПанельНастройкиИнструменты");
	УстановитьДоступностьВТаблицеПоискаПоФорме(ТаблицаПоиска, "ПанельНастройкиСистема");
	
КонецПроцедуры

Процедура УстановитьДоступностьВТаблицеПоискаПоФорме(ТаблицаПоиска, ИмяФормыНастройки)
	
	НастройкиВидимости = ВидимостьЭлементовФормы(ИмяФормыНастройки);
	
	СтрокиПоФорме = ТаблицаПоиска.НайтиСтроки(Новый Структура("ИмяФормы", ИмяФормыНастройки));
	Для Каждого СтрокаПоФорме Из СтрокиПоФорме Цикл
		Если НастройкиВидимости.Свойство(СтрокаПоФорме.ИмяЭлементаФормы) Тогда
			СтрокаПоФорме.Доступна = НастройкиВидимости[СтрокаПоФорме.ИмяЭлементаФормы];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКлючевыеСловаПоиска(ТаблицаПоиска)
	
	Для Каждого СтрокаТаблицыПоиска Из ТаблицаПоиска Цикл
		Если ПустаяСтрока(СтрокаТаблицыПоиска.КлючевыеСлова) Тогда
			СтрокаТаблицыПоиска.КлючевыеСлова = РаботаСТекстомКлиентСервер.ОбработатьСловаДляПоиска(СтрокаТаблицыПоиска.ТекстДляПоиска);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьТекстыПоПредставлениямСсылок(ТаблицаПоиска)
	
	Для Каждого СтрокаТаблицыПоиска Из ТаблицаПоиска Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицыПоиска.ПредставлениеСсылки)
			И СтрокаТаблицыПоиска.ПредставлениеСсылки <> СтрокаТаблицыПоиска.ТекстДляПоиска Тогда
			ПараметрыОтбора = Новый Структура("ПредставлениеСсылки, ТекстДляПоиска",
				СтрокаТаблицыПоиска.ПредставлениеСсылки, СтрокаТаблицыПоиска.ПредставлениеСсылки);
			СтрокиПоискаПоПредставлению = ТаблицаПоиска.НайтиСтроки(ПараметрыОтбора);
			Если СтрокиПоискаПоПредставлению.Количество() = 0 Тогда
				НоваяСтрока = ТаблицаПоиска.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыПоиска,
					"НавигационнаяСсылка, ПредставлениеСсылки, Доступна, ИмяФормы, ИмяЭлементаФормы");
				НоваяСтрока.ТекстДляПоиска = СтрокаТаблицыПоиска.ПредставлениеСсылки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПилотныйРазделБухгалтерия

Процедура ЗаписатьСтатистикуПилотныйРазделБухгалтерия(ИспользоватьПилотныйРаздел)
	
	ПредыдущаяНастройка = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПанелиПростойИнтерфейс",
		"ИспользоватьПилотныйРазделБухгалтерия");
	
	Если ПредыдущаяНастройка = Неопределено Тогда
		Действие = "ПервыйВход";
	ИначеЕсли ИспользоватьПилотныйРаздел Тогда
		Действие = "ПереходНаПилотныйРаздел";
	Иначе
		Действие = "ПереходНаКлассическийРаздел";
	КонецЕсли;
	
	ИмяКлючевойОперации = СтрШаблон("СтатистикаБП.ПилотныйРазделБухгалтерия.%1", Действие);
	СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(ИмяКлючевойОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли