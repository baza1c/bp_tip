#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Обработки.ПанелиПростойИнтерфейс.ЗадатьВидимостьЭлементовФормы(ЭтотОбъект);
	
	АдресХранилищаТаблицыПоиска = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ОбновитьТаблицуПоискаВФоне();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОжидатьОбработкуОбновленияТаблицыПоискаВФоне(ДлительнаяОперацияОбновитьТаблицуПоиска);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПростойИнтерфейс_ФункциональностьИзменение" Тогда
		Элементы.ГруппаПанели.ТекущаяСтраница = Элементы.ГруппаКолонки;
		ОбновитьТаблицуПоискаВФоне();
		ОжидатьОбработкуОбновленияТаблицыПоискаВФоне(ДлительнаяОперацияОбновитьТаблицуПоиска);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачальныеОстатки(Команда)
	
	ПараметрыФормы = Новый Структура("Организация", ОбщегоНазначенияБПВызовСервера.ОсновнаяОрганизация());
	
	ОткрытьФорму("Обработка.ВводНачальныхОстатков.Форма", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказатьНастройкиКакВПолномИнтерфейсеПриИзменении(Элемент)
	
	Если ПоказатьНастройкиКакВПолномИнтерфейсе Тогда
		Закрыть();
		ОткрытьФорму("Обработка.ПанелиПростойИнтерфейс.Форма.ПанельНастройкиАдминистрирование", , , , Окно);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтрокаПоиска = Текст;
	
	Элементы.ГруппаПанели.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	
	// Реагируем на изменение строки поиска не сразу по мере ввода, а выжидаем
	// 1 сек., чтобы дать пользователю возможность допечатать поисковую строку.
	ОтключитьОбработчикОжидания("Подключаемый_ИзменениеСтрокиПоиска");
	ПодключитьОбработчикОжидания("Подключаемый_ИзменениеСтрокиПоиска", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаОчистка(Элемент, СтандартнаяОбработка)
	
	Элементы.ГруппаПанели.ТекущаяСтраница = Элементы.ГруппаКолонки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьТаблицуПоискаВФоне()
	
	ПервичноеЗаполнение = Не ТаблицаПоискаПодготовлена;
	ТаблицаПоискаПодготовлена = Ложь;
	
	Если ПервичноеЗаполнение Тогда
		ТаблицаПоиска = Обработки.ПанелиПростойИнтерфейс.НовыйТаблицаПоиска();
	Иначе
		ТаблицаПоиска = ПолучитьИзВременногоХранилища(АдресХранилищаТаблицыПоиска);
		ОчиститьТаблицуПоискаОтЗаполненныхПоФормам(ТаблицаПоиска);
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.АдресРезультата = АдресХранилищаТаблицыПоиска;
	ДлительнаяОперацияОбновитьТаблицуПоиска = 
		ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"Обработки.ПанелиПростойИнтерфейс.ОбновитьТаблицуПоиска",
		ТаблицаПоиска, ПервичноеЗаполнение);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицуПоискаОтЗаполненныхПоФормам(ТаблицаПоиска)
	
	СтрокиЗаполненыПоФормам = ТаблицаПоиска.НайтиСтроки(Новый Структура("ЗаполненоПоФорме", Истина));
	Для Каждого СтрокаПоФорме Из СтрокиЗаполненыПоФормам Цикл
		ТаблицаПоиска.Удалить(СтрокаПоФорме);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьОбработкуОбновленияТаблицыПоискаВФоне(ДлительнаяОперация)
	
	Если ЗначениеЗаполнено(ДлительнаяОперация) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОжидатьОбработкуОбновленияТаблицыПоискаВФонеЗавершение", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьОбработкуОбновленияТаблицыПоискаВФонеЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		ДлительнаяОперацияОбновитьТаблицуПоиска = Неопределено;
		ТаблицаПоискаПодготовлена = Истина;
		ТребуетсяЗаполнениеТаблицыПоиска = Истина;
	Иначе
		ОповеститьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьТекстыПоФормамВТаблицуПоиска(АдресХранилищаТаблицыПоиска, ТекстыПоФормам)
	
	Если ТекстыПоФормам = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПоиска = ПолучитьИзВременногоХранилища(АдресХранилищаТаблицыПоиска);
	
	Для Каждого ДанныеПоФорме Из ТекстыПоФормам Цикл
		СтрокаТаблицыПоиска = ТаблицаПоиска.Найти(ДанныеПоФорме.ИмяФормыДляСбораТекстов, "ИмяФормыДляСбораТекстов");
		Для Каждого СобранныйТекст Из ДанныеПоФорме.СобранныеТексты Цикл
			НоваяСтрока = ТаблицаПоиска.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыПоиска, "НавигационнаяСсылка, ПредставлениеСсылки, Доступна");
			НоваяСтрока.ТекстДляПоиска = СобранныйТекст;
			НоваяСтрока.КлючевыеСлова = РаботаСТекстомКлиентСервер.ОбработатьСловаДляПоиска(СобранныйТекст);
			НоваяСтрока.ЗаполненоПоФорме = Истина;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаПоиска.Сортировать("НавигационнаяСсылка");
	
	ПоместитьВоВременноеХранилище(ТаблицаПоиска, АдресХранилищаТаблицыПоиска);
	
КонецПроцедуры

&НаКлиенте
Функция СобратьТекстыПоискаНастроекПоФормам(СписокФормДляСбораТекстов)
	
	ТекстыПоФормам = Новый Массив;
	
	Для Каждого ИмяФормыДляСбора Из СписокФормДляСбораТекстов Цикл
		СобранныеТексты = Новый Массив;
		ФормаДляСбора = ПолучитьФорму(ИмяФормыДляСбора);
		СобратьТекстыПоискаПодчиненныхЭлементов(СобранныеТексты, ФормаДляСбора.ПодчиненныеЭлементы);
		ДанныеПоФорме = Новый Структура("ИмяФормыДляСбораТекстов, СобранныеТексты", ИмяФормыДляСбора, СобранныеТексты);
		ТекстыПоФормам.Добавить(ДанныеПоФорме);
	КонецЦикла;
	
	Возврат ТекстыПоФормам;
	
КонецФункции

&НаКлиенте
Процедура СобратьТекстыПоискаПодчиненныхЭлементов(СобранныеТексты, ЭлементыФормы)
	
	Для Каждого ЭлементФормы Из ЭлементыФормы Цикл
		Если ЭлементФормы.Видимость Тогда
			Если ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") Тогда
				СобратьТекстыПоискаПодчиненныхЭлементов(СобранныеТексты, ЭлементФормы.ПодчиненныеЭлементы);
			Иначе
				ДобавитьСтрокуПоискаПоЭлементуФормы(СобранныеТексты, ЭлементФормы.Заголовок);
				ДобавитьСтрокуПоискаПоЭлементуФормы(СобранныеТексты, ЭлементФормы.РасширеннаяПодсказка.Заголовок);
				Если ТипЗнч(ЭлементФормы) <> Тип("КнопкаФормы") Тогда // Подсказки нет у кнопок
					ДобавитьСтрокуПоискаПоЭлементуФормы(СобранныеТексты, ЭлементФормы.Подсказка);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокуПоискаПоЭлементуФормы(СобранныеТексты, ТекстДляПоиска)
	
	Если Не ЗначениеЗаполнено(ТекстДляПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	СобранныеТексты.Добавить(ТекстДляПоиска);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполнитьПоискНаСервере(АдресХранилищаТаблицыПоиска, СтрокаПоиска)
	
	ТаблицаПоиска = ПолучитьИзВременногоХранилища(АдресХранилищаТаблицыПоиска);
	Шрифты = ШрифтыДляРезультатаПоиска();
	
	РезультатыПоиска = Новый ФорматированныйДокумент;
	
	СловаЗапроса = СтрРазделить(РаботаСТекстомКлиентСервер.ОбработатьСловаДляПоиска(СтрокаПоиска), " ", Ложь);
	
	МаксНайденных = 3;
	ВыведеноСтрокПоСсылке = 0;
	
	ТекСсылка = Неопределено;
	
	Для Каждого СтрокаТаблицыПоиска Из ТаблицаПоиска Цикл
		
		Если Не СтрокаТаблицыПоиска.Доступна Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВсеСловаНайдены(СловаЗапроса, СтрокаТаблицыПоиска.КлючевыеСлова) Тогда
			
			Если ТекСсылка = СтрокаТаблицыПоиска.НавигационнаяСсылка Тогда
				Если ВыведеноСтрокПоСсылке >= МаксНайденных Тогда
					Продолжить;
				КонецЕсли;
			Иначе
				Если ТекСсылка <> Неопределено Тогда
					ДобавитьПереводСтрокиКРезультатуПоиска(РезультатыПоиска);
				КонецЕсли;
				ТекСсылка = СтрокаТаблицыПоиска.НавигационнаяСсылка;
				ДобавитьТекстКРезультатуПоиска(РезультатыПоиска, СтрокаТаблицыПоиска.ПредставлениеСсылки,
					Шрифты.НавСсылка, СтрокаТаблицыПоиска.НавигационнаяСсылка);
				ДобавитьПереводСтрокиКРезультатуПоиска(РезультатыПоиска);
				ВыведеноСтрокПоСсылке = 0;
			КонецЕсли;
			
			Если СтрокаТаблицыПоиска.ТекстДляПоиска <> СтрокаТаблицыПоиска.ПредставлениеСсылки Тогда
				ВывестиТекстПоискаСВыделениемНайденныхСлов(РезультатыПоиска, СтрокаТаблицыПоиска.ТекстДляПоиска, СловаЗапроса);
				ДобавитьПереводСтрокиКРезультатуПоиска(РезультатыПоиска);
				ВыведеноСтрокПоСсылке = ВыведеноСтрокПоСсылке + 1;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если РезультатыПоиска.Элементы.Количество() = 0 Тогда
		
		ВывестиНичегоНеНайдено(РезультатыПоиска, СтрокаПоиска);
		
	КонецЕсли;
	
	Возврат РезультатыПоиска;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьТекстКРезультатуПоиска(РезультатыПоиска, Текст, Шрифт, НавигационнаяСсылка = Неопределено)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ЧастьТекста = РезультатыПоиска.Добавить(Текст);
		Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
			ЧастьТекста.НавигационнаяСсылка = НавигационнаяСсылка;
		КонецЕсли;
		ЧастьТекста.Шрифт = Шрифт;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьПереводСтрокиКРезультатуПоиска(РезультатыПоиска)
	
	РезультатыПоиска.Добавить(, Тип("ПереводСтрокиФорматированногоДокумента"));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ШрифтыДляРезультатаПоиска()
	
	БазовыйШрифт = ШрифтыСтиля.ОбычныйШрифтТекста;
	
	Обычный = Новый Шрифт(БазовыйШрифт, , , Ложь);
	Полужирный = Новый Шрифт(БазовыйШрифт, , , Истина);
	НавСсылка = Новый Шрифт(БазовыйШрифт, , , , , Истина, , 120);
	
	Шрифты = Новый Структура;
	Шрифты.Вставить("Обычный", Обычный);
	Шрифты.Вставить("Полужирный", Полужирный);
	Шрифты.Вставить("НавСсылка", НавСсылка);
	
	Возврат Шрифты;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВсеСловаНайдены(СловаЗапроса, КлючевыеСлова)
	
	КоличествоСовпадений = 0;
	Для каждого СловоЗапроса Из СловаЗапроса Цикл
		ЕстьСовпадение = СтрНайти(КлючевыеСлова, СловоЗапроса) <> 0;
		Если ЕстьСовпадение Тогда
			КоличествоСовпадений = КоличествоСовпадений + 1;
		КонецЕсли;
	КонецЦикла;
	
	Результат = КоличествоСовпадений = СловаЗапроса.Количество();
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ВывестиТекстПоискаСВыделениемНайденныхСлов(РезультатыПоиска, ТекстДляПоиска, СловаЗапроса)
	
	Шрифты = ШрифтыДляРезультатаПоиска();
	
	ТекПозиция = 1;
	ДлинаТекста = СтрДлина(ТекстДляПоиска);
	Пока ТекПозиция < ДлинаТекста Цикл
		
		ПозицияПервогоСлова = ДлинаТекста;
		Для Каждого СловоЗапроса Из СловаЗапроса Цикл
			ПозицияНайденногоСлова = СтрНайти(ВРег(ТекстДляПоиска), ВРег(СловоЗапроса), , ТекПозиция);
			Если ПозицияНайденногоСлова > 0 Тогда
				ПозицияПервогоСлова = Мин(ПозицияПервогоСлова, ПозицияНайденногоСлова);
			КонецЕсли;
		КонецЦикла;
		
		// Искомых слов не осталось
		Если ПозицияПервогоСлова = ДлинаТекста Тогда
			ТекстПослеНайденного = Сред(ТекстДляПоиска, ТекПозиция);
			ДобавитьТекстКРезультатуПоиска(РезультатыПоиска, ТекстПослеНайденного, Шрифты.Обычный);
			Прервать;
		КонецЕсли;
		
		НачалоСлова = НайтиНачалоСлова(ТекстДляПоиска, ПозицияПервогоСлова);
		КонецСлова = НайтиКонецСлова(ТекстДляПоиска, ПозицияПервогоСлова);
		
		Если НачалоСлова > ТекПозиция Тогда
			ТекстПередНайденным = Сред(ТекстДляПоиска, ТекПозиция, НачалоСлова - ТекПозиция);
			ДобавитьТекстКРезультатуПоиска(РезультатыПоиска, ТекстПередНайденным, Шрифты.Обычный);
		КонецЕсли;
		
		НайденноеСлово = Сред(ТекстДляПоиска, НачалоСлова, КонецСлова - НачалоСлова + 1);
		ДобавитьТекстКРезультатуПоиска(РезультатыПоиска, НайденноеСлово, Шрифты.Полужирный);
		
		ТекПозиция = КонецСлова + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиНачалоСлова(ТекстДляПоиска, Знач ПозицияПоиска)
	
	Пока ПозицияПоиска > 0 Цикл
		Если ПустаяСтрока(Сред(ТекстДляПоиска, ПозицияПоиска, 1)) Тогда
			Прервать;
		Иначе
			ПозицияПоиска = ПозицияПоиска - 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПозицияПоиска + 1;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиКонецСлова(ТекстДляПоиска, Знач ПозицияПоиска)
	
	ДлинаТекста = СтрДлина(ТекстДляПоиска);
	
	Пока ПозицияПоиска < ДлинаТекста Цикл
		Если ПустаяСтрока(Сред(ТекстДляПоиска, ПозицияПоиска, 1)) Тогда
			Прервать;
		Иначе
			ПозицияПоиска = ПозицияПоиска + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПозицияПоиска;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ВывестиНичегоНеНайдено(РезультатыПоиска, СтрокаПоиска)
	
	Шрифты = ШрифтыДляРезультатаПоиска();
	
	ДобавитьТекстКРезультатуПоиска(РезультатыПоиска, НСтр("ru='По запросу ""'"), Шрифты.Обычный);
	ДобавитьТекстКРезультатуПоиска(РезультатыПоиска, СтрокаПоиска, Шрифты.Полужирный);
	ДобавитьТекстКРезультатуПоиска(РезультатыПоиска, НСтр("ru='"" ничего не найдено'"), Шрифты.Обычный);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИзменениеСтрокиПоиска() Экспорт
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		Элементы.ГруппаПанели.ТекущаяСтраница = Элементы.ГруппаКолонки;
		Возврат;
	КонецЕсли;
	
	// Таблица поиска может быть еще не заполнена фоновым заданием
	Если Не ТаблицаПоискаПодготовлена Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ИзменениеСтрокиПоиска", 1, Истина);
		Возврат;
	КонецЕсли;
	
	// Заполняем таблицу поиска непосредственно перед поиском, чтобы пользователю не приходилось ждать,
	// пока соберутся данные для поиска, если он не будет использовать поиск
	Если ТребуетсяЗаполнениеТаблицыПоиска Тогда
		СписокФормДляСбораТекстов = СписокФормДляСбораТекстовПоиска(АдресХранилищаТаблицыПоиска);
		ТекстыПоФормам = СобратьТекстыПоискаНастроекПоФормам(СписокФормДляСбораТекстов);
		ДобавитьТекстыПоФормамВТаблицуПоиска(АдресХранилищаТаблицыПоиска, ТекстыПоФормам);
		ТребуетсяЗаполнениеТаблицыПоиска = Ложь;
	КонецЕсли;
	
	РезультатыПоиска = ВыполнитьПоискНаСервере(АдресХранилищаТаблицыПоиска, СтрокаПоиска);
	
	Элементы.ГруппаПанели.ТекущаяСтраница = Элементы.ГруппаРезультатыПоиска;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокФормДляСбораТекстовПоиска(АдресХранилищаТаблицыПоиска)
	
	ТаблицаПоиска = ПолучитьИзВременногоХранилища(АдресХранилищаТаблицыПоиска);
	
	ФормыДляСбораТекстов = Новый Массив;
	
	СтрокиТаблицыПоиска = ТаблицаПоиска.НайтиСтроки(Новый Структура("СобратьТекстПоФорме, Доступна", Истина, Истина));
	Для Каждого СтрокаТаблицыПоиска Из СтрокиТаблицыПоиска Цикл
		ФормыДляСбораТекстов.Добавить(СтрокаТаблицыПоиска.ИмяФормыДляСбораТекстов);
	КонецЦикла;
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(ФормыДляСбораТекстов);
	
КонецФункции

&НаКлиенте
Процедура ОповеститьОбОшибкеДлительнойОперации(ДлительнаяОперация)
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ВызватьИсключение ДлительнаяОперация.КраткоеПредставлениеОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаПанели.НастроитьФорму(ЭтотОбъект);
	Элементы.ПоказатьНастройкиКакВПолномИнтерфейсе.Видимость = Ложь;
	Элементы.ГруппаПоиск.Видимость = Ложь;
	Элементы.МобильныйКлиент.Видимость = Ложь;
	Элементы.МобильныйКлиентБизнесСтарт.Видимость = Ложь;
	Элементы.ГруппаПанели.Видимость = Ложь;
	
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаНастройкиПрограммы,
		"ПерсональныеНастройки,Органайзер,ПечатьАртикулов,НастройкаЭлектроннойПочты,ПомеченныеНаУдаление,Интерфейс");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаСервис,
		"ПомеченныеНаУдаление");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаМобильныеПриложения,
		"СканерЧеков,СканерДокументов");
	
КонецПроцедуры

#КонецОбласти