#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой(ЭтотОбъект);
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
			АктуализироватьМКШапкаФормы();
		Иначе
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗаписиКнигиДоходовИРасходовУСНОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок    = НСтр("ru = 'Записи книги доходов и расходов УСН'");
	ПараметрыОткрытия.ИмяФормы     = "Документ.ЗаписьКУДиР.ФормаСписка";
	ПараметрыОткрытия.Уникальность = "ФормаСписка_ЗаписьКУДиР";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор",     Новый Структура("ДеятельностьНаПатенте", Ложь));
	ПараметрыФормы.Вставить("Заголовок", ПараметрыОткрытия.Заголовок);
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписиКнигиУчетаДоходовПатентОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок    = НСтр("ru = 'Записи книги доходов по патенту'");
	ПараметрыОткрытия.ИмяФормы     = "Документ.ЗаписьКУДиР.ФормаСписка";
	ПараметрыОткрытия.Уникальность = "ФормаСписка_ЗаписьКнигиУчетаДоходовПатент";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор",     Новый Структура("ДеятельностьНаПатенте", Истина));
	ПараметрыФормы.Вставить("Заголовок", ПараметрыОткрытия.Заголовок);
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ОбщаяФорма.НалогиИОтчеты");
КонецПроцедуры

&НаКлиенте
Процедура КнигаУчетаДоходовРасходовУСНОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Отчет.КнигаУчетаДоходовИРасходов.Форма");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ФункциональныеОпции = ФункциональныеОпцииФормы();
	
	Элементы.ГруппаПСН.Видимость  = ФункциональныеОпции.ИспользуетсяУСНПатент
		И Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	Элементы.ГруппаУСН.Видимость  = ФункциональныеОпции.ИспользуетсяУСН;
	Элементы.ГруппаАУСН.Видимость = ФункциональныеОпции.ИспользуетсяАУСН
		И Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	Элементы.ГруппаНДС.Видимость  = ФункциональныеОпции.ИспользуетсяНДС
		И Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	Элементы.ГруппаИП.Видимость   = ФункциональныеОпции.ИспользуетсяНДФЛИП
		И Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	Элементы.ГруппаНПД.Видимость  = ФункциональныеОпции.ИспользуетсяНПД
		И Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	
	Элементы.СверкаРасчетаНалогаНаИмущество.Видимость    = ФункциональныеОпции.УплачиваетсяНалогНаИмуществоПоКадастровойСтоимости
		И Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	Элементы.СверкаРасчетаЗемельногоНалога.Видимость     = ФункциональныеОпции.УплачиваетсяЗемельныйНалог
		И Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	Элементы.СверкаРасчетаТранспортногоНалога.Видимость  = ФункциональныеОпции.УплачиваетсяТранспортныйНалог
		И Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
		
	Элементы.КалендарьОтчетности.Видимость = Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	Элементы.СравнениеНалоговыхРежимов.Видимость = Не ФункциональныеОпции.ЭтоИнтерфейсИнтеграцииСБанком;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФункциональныеОпцииФормы()
	
	ФункциональныеОпции = Новый Структура;
	ФункциональныеОпции.Вставить("ИспользуетсяУСН",
		ПолучитьФункциональнуюОпцию("ИспользуетсяУСН"));
	ФункциональныеОпции.Вставить("ИспользуетсяАУСН",
		ПолучитьФункциональнуюОпцию("ИспользуетсяАУСН"));
	ФункциональныеОпции.Вставить("ИспользуетсяУСНПатент",
		ПолучитьФункциональнуюОпцию("ИспользуетсяУСНПатент"));
	ФункциональныеОпции.Вставить("ИспользуетсяНПД",
		ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые());
	ФункциональныеОпции.Вставить("ИспользуетсяНДС",
		ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО") ИЛИ ПолучитьФункциональнуюОпцию("ИспользуетсяНДФЛИП")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользуетсяУСНСНДС"));
	ФункциональныеОпции.Вставить("ИспользуетсяОСНО",
		ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО"));
	ФункциональныеОпции.Вставить("ИспользуетсяНДФЛИП",
		ПолучитьФункциональнуюОпцию("ИспользуетсяНДФЛИП"));
	ФункциональныеОпции.Вставить("ВестиУчетПрослеживаемыхТоваров",
		ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров"));
	ФункциональныеОпции.Вставить("УплачиваетсяНалогНаИмуществоПоКадастровойСтоимости",
		ПолучитьФункциональнуюОпцию("УплачиваетсяНалогНаИмуществоПоКадастровойСтоимости"));
	ФункциональныеОпции.Вставить("УплачиваетсяТранспортныйНалог",
		ПолучитьФункциональнуюОпцию("УплачиваетсяТранспортныйНалог"));
	ФункциональныеОпции.Вставить("УплачиваетсяЗемельныйНалог",
		ПолучитьФункциональнуюОпцию("УплачиваетсяЗемельныйНалог"));
	ФункциональныеОпции.Вставить("ИспользуетсяСервисСверкиРасчетов",
		ПолучитьФункциональнуюОпцию("ИспользуетсяСервисСверкиРасчетов"));
	ФункциональныеОпции.Вставить("ЭтоИнтерфейсИнтеграцииСБанком",
		ОбщегоНазначенияБП.ЭтоИнтерфейсИнтеграцииСБанком());

	Возврат ФункциональныеОпции;
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаПанели.НастроитьФорму(ЭтотОбъект);
	
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаНалоги,
		"МониторНалоговОтчетности,СписокЗадач,Настройки");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаПСН,
		"Патенты,КнигаДоходовПСН");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаАУСН,
		"БанковскиеОперацииАУСН");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаУСН,
		"КнигаУчетаДоходовРасходовУСН");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаНПД,
		"СправкиПлательщикаНПД,ДоходыНПД,ПомощникУплатыНПД");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаИП,
		"КнигаДоходовПредпринимателя");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаЕдиныйНалоговыйСчет,
		"РасчетыПоЕНС,РасчетыПоНалогамНаЕНС");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.Группа1СОтчетность,
		"РегламентированныеОтчеты");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаНДС);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаСверкиСФНС);
	
КонецПроцедуры

&НаСервере
Процедура АктуализироватьМКШапкаФормы()
	
	УправлениеФормой(ЭтотОбъект);
	
	МобильныйКлиентБПФормаПанели.АктуализироватьСвойстваНедоступныхГрупп(ЭтотОбъект,
		"ГруппаПСН,ГруппаУСН,ГруппаАУСН,ГруппаНДС,ГруппаНПД");
	
КонецПроцедуры

#КонецОбласти
