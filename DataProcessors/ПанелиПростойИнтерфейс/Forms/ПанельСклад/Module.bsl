#Область ОписаниеПеременных

&НаКлиенте
Перем ОткрытыеФормы;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоПросмотрДокументыПоСкладу = ПравоДоступа("Просмотр", Метаданные.ЖурналыДокументов.СкладскиеДокументы);
	ЕстьПравоПросмотрПередачаСотрудникам = ПравоДоступа("Просмотр", Метаданные.ЖурналыДокументов.ПередачаМатериалов);
	ЕстьПравоПросмотрБезвозмезднаяПередача = ПравоДоступа("Просмотр", Метаданные.Документы.ПередачаТоваров);
	ЕстьПравоПросмотрКомплектацияНоменклатуры = ПравоДоступа("Просмотр", Метаданные.Документы.КомплектацияНоменклатуры);
	ЕстьПравоПросмотрИнвентаризацияТоваров = ПравоДоступа("Просмотр", Метаданные.Документы.ИнвентаризацияТоваровНаСкладе);

	Элементы.ДокументыПоСкладу.Видимость = ЕстьПравоПросмотрДокументыПоСкладу; 
	Элементы.ПередачаСотрудникам.Видимость = ЕстьПравоПросмотрПередачаСотрудникам;
	Элементы.БезвозмезднаяПередача.Видимость = ЕстьПравоПросмотрБезвозмезднаяПередача;
	Элементы.КомплектацияНоменклатуры.Видимость = ЕстьПравоПросмотрКомплектацияНоменклатуры;
	Элементы.ИнвентаризацияТоваров.Видимость = ЕстьПравоПросмотрИнвентаризацияТоваров;
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура ДокументыПоСкладу(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru='Документы по складу'"));
	
	ПростойИнтерфейсКлиент.ОткрытьФормуВОкнеФормы("ЖурналДокументов.СкладскиеДокументы.ФормаСписка",
		ЭтотОбъект, ПараметрыФормы, , ОткрытыеФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередачаСотрудникам(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru='Передача материалов сотрудникам'"));
	
	ПростойИнтерфейсКлиент.ОткрытьФормуВОкнеФормы("ЖурналДокументов.ПередачаМатериалов.ФормаСписка",
		ЭтотОбъект, ПараметрыФормы, , ОткрытыеФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура БезвозмезднаяПередача(Команда)
	
	Отбор = Новый Структура("ВидОперации",
		ПредопределенноеЗначение("Перечисление.ВидыОперацийПередачаТоваров.БезвозмезднаяПередача"));
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	
	ПростойИнтерфейсКлиент.ОткрытьФормуВОкнеФормы("Документ.ПередачаТоваров.Форма.ФормаСпискаБезвозмезднаяПередача",
		ЭтотОбъект, ПараметрыФормы, , ОткрытыеФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КомплектацияНоменклатуры(Команда)
	
	ПростойИнтерфейсКлиент.ОткрытьФормуВОкнеФормы("Документ.КомплектацияНоменклатуры.ФормаСписка",
		ЭтотОбъект, , , ОткрытыеФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвентаризацияТоваров(Команда)
	
	ПростойИнтерфейсКлиент.ОткрытьФормуВОкнеФормы("Документ.ИнвентаризацияТоваровНаСкладе.ФормаСписка",
		ЭтотОбъект, , , ОткрытыеФормы);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаПанели.НастроитьФорму(ЭтотОбъект);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанели(ЭтотОбъект,
		"ИнвентаризацияТоваров");
	
КонецПроцедуры

#КонецОбласти