#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
	Если Обработки.ПанелиПростойИнтерфейс.ПоказыватьПилотныйРазделБухгалтерия() Тогда
		Элементы.ГруппаПилот.Видимость = Истина;
		ПрежнийВидРаздела = Истина;
		Обработки.ПанелиПростойИнтерфейс.СохранитьНастройкуИспользоватьПилотныйРазделБухгалтерия(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
			АктуализироватьМКШапкаФормы();
		Иначе
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ЗаписатьСтатистикуПереходовПоСсылке(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПрежнийВидРазделаПриИзменении(Элемент)
	
	Если Не ПрежнийВидРаздела Тогда
		Закрыть();
		ОткрытьФорму("Обработка.ПанелиПростойИнтерфейс.Форма.ПанельБухгалтерияПилот", , , , Окно);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НаписатьРазработчикам(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ФункциональныеОпции = ФункциональныеОпцииФормы();
	
	Элементы.ЗаписиКнигиДоходовИРасходовУСН.Видимость = ФункциональныеОпции.ИспользуетсяУСН;
	Элементы.РегистрацияСуммыУбыткаУСН.Видимость      = ФункциональныеОпции.ИспользуетсяУСН;
	Элементы.АнализУчетаПоУСН.Видимость               = ФункциональныеОпции.ИспользуетсяУСН;
	Элементы.РасходыУСН.Видимость                     = ФункциональныеОпции.ИспользуетсяУСН;
	
	Элементы.РегистрыНалоговогоУчета.Видимость        = ФункциональныеОпции.ИспользуетсяОСНО;
	Элементы.АнализУчетаПоНалогуНаПрибыль.Видимость   = ФункциональныеОпции.ИспользуетсяОСНО;
	
	Элементы.ГруппаНДС.Видимость                      = ФункциональныеОпции.ИспользуетсяНДС;
	Элементы.РегламентныеОперацииНДС.Видимость        = ФункциональныеОпции.ИспользуетсяНДС;
	Элементы.АнализУчетаПоНДС.Видимость               = ФункциональныеОпции.ИспользуетсяНДС;
	Элементы.НаличиеСчетовФактур.Видимость            = ФункциональныеОпции.ИспользуетсяНДС;
	
	Элементы.ГруппаНПД.Видимость                      = ФункциональныеОпции.ВключенаИнтеграцияСПлатформойСамозанятые;
	
	Элементы.ЗаписьКнигиУчетаДоходовИРасходовИП.Видимость = ФункциональныеОпции.ИспользуетсяНДФЛИП;
	Элементы.ЗаписиКнигиУчетаДоходовПатент.Видимость      = ФункциональныеОпции.ИспользуетсяУСНПатент;
	
	Элементы.ГруппаТуристическийНалог.Видимость           = ФункциональныеОпции.УплачиваетсяТуристическийНалог;
	
	Элементы.ГруппаПрослеживаемость.Видимость = ФункциональныеОпции.ВестиУчетПрослеживаемыхТоваров;
	
	Элементы.СверкаРасчетаНалогаНаИмущество.Видимость    = ФункциональныеОпции.УплачиваетсяНалогНаИмуществоПоКадастровойСтоимости;
	Элементы.СверкаРасчетаЗемельногоНалога.Видимость     = ФункциональныеОпции.УплачиваетсяЗемельныйНалог;
	Элементы.СверкаРасчетаТранспортногоНалога.Видимость  = ФункциональныеОпции.УплачиваетсяТранспортныйНалог;
	Элементы.СверкаДокументовСКонтрагентами.Видимость    = ФункциональныеОпции.ИспользуетсяСервисСверкиРасчетов;
	
	Элементы.ИнвентаризацияОС.Видимость                  = ФункциональныеОпции.ВедетсяУчетОС;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФункциональныеОпцииФормы()
	
	ФункциональныеОпции = Новый Структура;
	ФункциональныеОпции.Вставить("ИспользуетсяУСН",
		ПолучитьФункциональнуюОпцию("ИспользуетсяУСН"));
	ФункциональныеОпции.Вставить("ИспользуетсяУСНПатент",
		ПолучитьФункциональнуюОпцию("ИспользуетсяУСНПатент"));
	ФункциональныеОпции.Вставить("ВключенаИнтеграцияСПлатформойСамозанятые",
		ПолучитьФункциональнуюОпцию("ВключенаИнтеграцияСПлатформойСамозанятые"));
	ФункциональныеОпции.Вставить("ИспользуетсяНДС",
		ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО") ИЛИ ПолучитьФункциональнуюОпцию("ИспользуетсяНДФЛИП")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользуетсяУСНСНДС"));
	ФункциональныеОпции.Вставить("ИспользуетсяОСНО",
		ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО"));
	ФункциональныеОпции.Вставить("ИспользуетсяНДФЛИП",
		ПолучитьФункциональнуюОпцию("ИспользуетсяНДФЛИП"));
	ФункциональныеОпции.Вставить("ВестиУчетПрослеживаемыхТоваров",
		ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров"));
	ФункциональныеОпции.Вставить("УплачиваетсяНалогНаИмуществоПоКадастровойСтоимости",
		ПолучитьФункциональнуюОпцию("УплачиваетсяНалогНаИмуществоПоКадастровойСтоимости"));
	ФункциональныеОпции.Вставить("УплачиваетсяТранспортныйНалог",
		ПолучитьФункциональнуюОпцию("УплачиваетсяТранспортныйНалог"));
	ФункциональныеОпции.Вставить("УплачиваетсяЗемельныйНалог",
		ПолучитьФункциональнуюОпцию("УплачиваетсяЗемельныйНалог"));
	ФункциональныеОпции.Вставить("ИспользуетсяСервисСверкиРасчетов",
		ПолучитьФункциональнуюОпцию("ИспользуетсяСервисСверкиРасчетов"));
	ФункциональныеОпции.Вставить("ВедетсяУчетОС",
		ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредствПростойИнтерфейс"));
	ФункциональныеОпции.Вставить("УплачиваетсяТуристическийНалог",
		ПолучитьФункциональнуюОпцию("УплачиваетсяТуристическийНалог"));
		
	Возврат ФункциональныеОпции;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикОткрытияФормы(ПараметрыФормы, ПараметрыВыполненияКоманды) Экспорт

	ВариантыОтчетовКлиент.ПоказатьПанельОтчетов(ПараметрыВыполненияКоманды.ПутьКПодсистеме, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = НСтр("ru = 'Отзыв о разделе Бухгалтерия'");
	
	ОтправкаПочтовыхСообщенийКлиент.ДополнитьПараметрыПисьмаДляОтправкиЛегкойПочтой(ПараметрыПисьма);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСтатистикуПереходовПоСсылке(НавигационнаяСсылка)
	Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
		ИмяКлючевойОперации = СтрШаблон("СтатистикаБП.ПанельБухгалтерия.ПереходПоСсылке.%1", НавигационнаяСсылка);
		СтатистикаПоПоказателямКлиентСервер.ДобавитьСобытие(ИмяКлючевойОперации);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаПанели.НастроитьФорму(ЭтотОбъект);
	
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаБухгалтерскийУчет,
		"ЖурналОпераций");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаОтчеты,
		"ОборотноСальдоваяВедомость,ОборотноСальдоваяВедомостьПоСчету,АнализСчета,КарточкаСчета,ОборотыСчета,ОтчетПоПроводкам");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.Группа1СОтчетность,
		"РегламентированныеОтчеты,УведомленияСообщенияИЗаявления,Письма,Сверки,ЛичныеКабинеты");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаУСН);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаНПД,
		"СправкиПлательщикаНПД,ДоходыНПД");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаИП);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаТуристическийНалог);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаАнализУчета);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаЕдиныйНалоговыйСчет,
		"РасчетыПоЕНС,РасчетыПоНалогамНаЕНС,Уведомления");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаЗакрытиеПериода,
		"ОбщаяКомандаЗакрытиеМесяца");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаДоходыИРасходы,
		"НоменклатурныеГруппы,СтатьиЗатрат,ПрочиеДоходыИРасходы,ДоходыБудущихПериодов,РасходыБудущихПериодов");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаИнвентаризация,
		"ИнвентаризацияТоваровНаСкладе");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаЛеваяКолонка);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаСредняяКолонка);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаПраваяКолонка);
	
КонецПроцедуры

&НаСервере
Процедура АктуализироватьМКШапкаФормы()
	
	УправлениеФормой(ЭтотОбъект);
	
	МобильныйКлиентБПФормаПанели.АктуализироватьСвойстваНедоступныхГрупп(ЭтотОбъект,
		"ГруппаНДС,ГруппаНПД,ГруппаПрослеживаемость");
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПланСчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ИмяФормы = "ПланСчетов.Хозрасчетный.ФормаСписка";

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
	ЗаписатьСтатистикуПереходовПоСсылке("e1cib/command/ОбщаяКоманда.ПланСчетов");
	
КонецПроцедуры

&НаКлиенте
Процедура КорреспонденцииСчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ИмяФормы = "РегистрСведений.КорреспонденцииСчетов.ФормаСписка";

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
	ЗаписатьСтатистикуПереходовПоСсылке("e1cib/list/РегистрСведений.КорреспонденцииСчетов");
	
КонецПроцедуры

&НаКлиенте
Процедура СправкиРасчетыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыВыполненияКоманды = Новый Структура("ПутьКПодсистеме", "Отчеты.СправкиРасчеты");
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ОбработчикОткрытияФормы = Новый ОписаниеОповещения("ОбработчикОткрытияФормы",
		ЭтотОбъект, ПараметрыВыполненияКоманды);

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
	ЗаписатьСтатистикуПереходовПоСсылке("e1cib/command/ОбщаяКоманда.ПанельОтчетовСправкиРасчеты");
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрыУчетаМалыхПредприятийОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыполненияКоманды = Новый Структура("ПутьКПодсистеме",
		"Отчеты.РегистрыБУСубъектовМалогоПредпринимательства");
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ОбработчикОткрытияФормы = Новый ОписаниеОповещения("ОбработчикОткрытияФормы",
		ЭтотОбъект, ПараметрыВыполненияКоманды);

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
	ЗаписатьСтатистикуПереходовПоСсылке("e1cib/command/ОбщаяКоманда.ПанельОтчетовРегистрыБУСубъектовМалогоПредпринимательства");

КонецПроцедуры

&НаКлиенте
Процедура РегистрыНалоговогоУчетаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыполненияКоманды = Новый Структура("ПутьКПодсистеме",
		"Отчеты.НалогНаПрибыль");
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок = Элемент.Заголовок;
	ПараметрыОткрытия.ОбработчикОткрытияФормы = Новый ОписаниеОповещения("ОбработчикОткрытияФормы",
		ЭтотОбъект, ПараметрыВыполненияКоманды);

	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия);
	
	ЗаписатьСтатистикуПереходовПоСсылке("e1cib/command/ОбщаяКоманда.ПанельОтчетовРегистрыНалоговогоУчета");
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаписиКнигиДоходовИРасходовУСНОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок    = НСтр("ru = 'Записи книги доходов и расходов УСН'");
	ПараметрыОткрытия.ИмяФормы     = "Документ.ЗаписьКУДиР.ФормаСписка";
	ПараметрыОткрытия.Уникальность = "ФормаСписка_ЗаписьКУДиР";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор",     Новый Структура("ДеятельностьНаПатенте", Ложь));
	ПараметрыФормы.Вставить("Заголовок", ПараметрыОткрытия.Заголовок);
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
	
	ЗаписатьСтатистикуПереходовПоСсылке("e1cib/list/Документ.ЗаписьКУДиР.УСН");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписиКнигиУчетаДоходовПатентОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок    = НСтр("ru = 'Записи книги доходов по патенту'");
	ПараметрыОткрытия.ИмяФормы     = "Документ.ЗаписьКУДиР.ФормаСписка";
	ПараметрыОткрытия.Уникальность = "ФормаСписка_ЗаписьКнигиУчетаДоходовПатент";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор",     Новый Структура("ДеятельностьНаПатенте", Истина));
	ПараметрыФормы.Вставить("Заголовок", ПараметрыОткрытия.Заголовок);
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
	
	ЗаписатьСтатистикуПереходовПоСсылке("e1cib/list/Документ.ЗаписьКУДиР.Патент");
	
КонецПроцедуры
