
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ГруппаНПД.Видимость = ИнтеграцияСПлатформойСамозанятыеПовтИсп.ДоступнаИнтеграцияСПлатформойСамозанятые();
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЛичныйКабинетЕНС(Команда)
	
	Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
		МобильныйКлиентБПКлиент.ПоказатьФормуНедоступнаяФункциональность(
			НСтр("ru = 'Получить сведения из личного кабинета ЕНС'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ЕдиныйНалоговыйСчетЛичныйКабинет.Форма.ФормаДлительнаяОперация",
			Новый Структура("РабочийРежим,ИмяФормы,", Истина, "Форма"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписиКнигиДоходовИРасходовУСН(Команда)
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок    = НСтр("ru = 'Записи книги доходов и расходов УСН'");
	ПараметрыОткрытия.ИмяФормы     = "Документ.ЗаписьКУДиР.ФормаСписка";
	ПараметрыОткрытия.Уникальность = "ФормаСписка_ЗаписьКУДиР";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор",     Новый Структура("ДеятельностьНаПатенте", Ложь));
	ПараметрыФормы.Вставить("Заголовок", ПараметрыОткрытия.Заголовок);
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписиКнигиУчетаДоходовПатент(Команда)
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.Заголовок    = НСтр("ru = 'Записи книги доходов по патенту'");
	ПараметрыОткрытия.ИмяФормы     = "Документ.ЗаписьКУДиР.ФормаСписка";
	ПараметрыОткрытия.Уникальность = "ФормаСписка_ЗаписьКнигиУчетаДоходовПатент";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор",     Новый Структура("ДеятельностьНаПатенте", Истина));
	ПараметрыФормы.Вставить("Заголовок", ПараметрыОткрытия.Заголовок);
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаПанели.НастроитьФорму(ЭтотОбъект);
	
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.Группа1СОтчетность,
		"РегламентированныеОтчеты,УведомленияСообщенияИЗаявления,Письма,Сверки,ЛичныеКабинеты");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаУСН);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаНПД,
		"СправкиПлательщикаНПД,ДоходыНПД");
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаИП);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаТуристическийНалог);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаЕдиныйНалоговыйСчет,
		"РасчетыПоЕНС,РасчетыПоНалогамНаЕНС,Уведомления");
	
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаЛеваяКолонка);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаСредняяКолонка);
	МобильныйКлиентБПФормаПанели.УстановитьДоступностьЭлементовПанелиПоГруппам(ЭтотОбъект,
		Элементы.ГруппаПраваяКолонка);
	
КонецПроцедуры

#КонецОбласти
