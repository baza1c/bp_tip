///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2026, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолноеИмяМетаданных = РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя();
	
	Для Каждого СервисИнтеграцииМетаданные Из Метаданные.СервисыИнтеграции Цикл
		СервисИнтеграции = СписокСервисовИнтеграции.Добавить();
		СервисИнтеграции.Имя = СервисИнтеграцииМетаданные.Имя;
		СервисИнтеграции.Представление = СервисИнтеграцииМетаданные.Представление();
		СервисИнтеграции.АдресВнешнегоСервисаИнтеграции = СервисИнтеграцииМетаданные.АдресВнешнегоСервисаИнтеграции;
	КонецЦикла;
	
	ОбновитьСостояние();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСервисыИнтеграции

&НаКлиенте
Процедура СервисыИнтеграцииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьНастройкиСервисаИнтеграции();
	
КонецПроцедуры

&НаКлиенте
Процедура СервисыИнтеграцииПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущийЭлемент.Имя = "СервисыИнтеграцииАктивен" Тогда
		Элемент.ТекущиеДанные.Активен = Не Элемент.ТекущиеДанные.Активен;
	Иначе
		Отказ = Истина;
		ОткрытьНастройкиСервисаИнтеграции();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СервисыИнтеграцииАктивенПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СервисыИнтеграции.ТекущиеДанные;

	УстановитьАктивностьУСлужбыИнтеграции(ТекущиеДанные.Имя, ТекущиеДанные.Активен);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьАктивность(Команда)
	
	УстановитьАктивностьНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьАктивность(Команда)
	
	УстановитьАктивностьНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНастройки(Команда)
	
	Если Элементы.СервисыИнтеграции.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьНастройкиСервисаИнтеграции();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСостояние()
	
	Для Каждого СервисИнтеграции Из СписокСервисовИнтеграции Цикл
        МенеджерСервисаИнтеграции = СервисыИнтеграции[СервисИнтеграции.Имя];
		МетаданныеСервисИнтеграции = Метаданные.СервисыИнтеграции[СервисИнтеграции.Имя];
		СервисИнтеграции.Активен = МенеджерСервисаИнтеграции.ПолучитьАктивность();
		НастройкиСервисаИнтеграции = МенеджерСервисаИнтеграции.ПолучитьНастройки();
		
		Если НастройкиСервисаИнтеграции.АдресВнешнегоСервисаИнтеграции = "" Тогда
			СервисИнтеграции.АдресВнешнегоСервисаИнтеграции = МетаданныеСервисИнтеграции.АдресВнешнегоСервисаИнтеграции; 
		Иначе
			СервисИнтеграции.АдресВнешнегоСервисаИнтеграции = НастройкиСервисаИнтеграции.АдресВнешнегоСервисаИнтеграции;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьАктивностьУСлужбыИнтеграции(Знач Имя, Знач Активность)
	
	МенеджерСервисаИнтеграции = СервисыИнтеграции[Имя];
	МенеджерСервисаИнтеграции.УстановитьАктивность(Активность);

КонецПроцедуры

&НаСервере
Процедура УстановитьАктивностьНаСервере(Знач Активность)

	Для Каждого СтрокаТаблицы Из СписокСервисовИнтеграции Цикл
		УстановитьАктивностьУСлужбыИнтеграции(СтрокаТаблицы.Имя, Активность);
	КонецЦикла;
	
	ОбновитьСостояние();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиСервисаИнтеграции()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяСервисаИнтеграции", Элементы.СервисыИнтеграции.ТекущиеДанные.Имя);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеЗакрытияФормы", ЭтотОбъект);
	ОткрытьФорму(ПолноеИмяМетаданных + ".Форма.ФормаНастроекСервисовИнтеграции", 
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЗакрытияФормы(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ОбновитьСостояние();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


