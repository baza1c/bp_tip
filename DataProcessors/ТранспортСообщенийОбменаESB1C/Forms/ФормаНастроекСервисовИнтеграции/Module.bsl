///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2026, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ИмяСервисаИнтеграции) Тогда
		
		ВызватьИсключение НСтр("ru = 'Эта форма не предназначена для непосредственного открытия.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
	КонецЕсли;
	
	МетаданныеСервисыИнтеграции = Метаданные.СервисыИнтеграции[Параметры.ИмяСервисаИнтеграции];
	НастройкиСервисаИнтеграции  = СервисыИнтеграции[Параметры.ИмяСервисаИнтеграции].ПолучитьНастройки();
	НаименованиеСервиса 		= МетаданныеСервисыИнтеграции.Представление();   
	
	Если НастройкиСервисаИнтеграции.АдресВнешнегоСервисаИнтеграции = "" Тогда
		АдресВнешнегоСервисаИнтеграции = МетаданныеСервисыИнтеграции.АдресВнешнегоСервисаИнтеграции;
	Иначе
		АдресВнешнегоСервисаИнтеграции = НастройкиСервисаИнтеграции.АдресВнешнегоСервисаИнтеграции;
	КонецЕсли;
	
	ИмяПользователяВнешнегоСервисаИнтеграции    = НастройкиСервисаИнтеграции.ИмяПользователяВнешнегоСервисаИнтеграции;
	ПарольПользователяВнешнегоСервисаИнтеграции = НастройкиСервисаИнтеграции.ПарольПользователяВнешнегоСервисаИнтеграции;
	ИмяПользователя 							= НастройкиСервисаИнтеграции.ИмяПользователя;
	
	Для Каждого Пользователь Из ПользователиИнформационнойБазы.ПолучитьПользователей() Цикл
		Элементы.ИмяПользователя.СписокВыбора.Добавить(Пользователь.Имя);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	СохранитьНастройкиНаСервере();
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройкиНаСервере() 
	
	НастройкиСервисаИнтеграции = Новый НастройкиСервисаИнтеграции;
	НастройкиСервисаИнтеграции.АдресВнешнегоСервисаИнтеграции              = АдресВнешнегоСервисаИнтеграции;
	НастройкиСервисаИнтеграции.ИмяПользователяВнешнегоСервисаИнтеграции    = ИмяПользователяВнешнегоСервисаИнтеграции;
	НастройкиСервисаИнтеграции.ПарольПользователяВнешнегоСервисаИнтеграции = ПарольПользователяВнешнегоСервисаИнтеграции;
	НастройкиСервисаИнтеграции.ИмяПользователя                             = ИмяПользователя;
	
	СервисыИнтеграции[Параметры.ИмяСервисаИнтеграции].УстановитьНастройки(НастройкиСервисаИнтеграции);
	
КонецПроцедуры

#КонецОбласти