#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет необходимость показа обработки пользователю (если есть договоры для обработки).
//
// Параметры:
//  Интерактивно - Булево - признак интерактивного открытия обработки пользователем.
// 
// Возвращаемое значение:
//  Булево - необходимость показывать обработку.
//
Функция НужноПоказатьОбработку(Интерактивно = Ложь) Экспорт
	
	// Проверяем по доступу к обработке.
	Если Не ПравоДоступа("Просмотр", Метаданные.Обработки.ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Если нет права на сохранение данных пользователя, не сможем сохранить флаг, что обработку не нужно открывать.
	Если Не ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	// Показываем только в главном узле.
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Показываем только если есть право изменения справочника "ДоговорыКонтрагентов".
	Если Не ПравоДоступа("Изменение", Метаданные.Справочники.ДоговорыКонтрагентов) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не Интерактивно Тогда
		НеПроверятьПриНачалеРаботы = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам", "НеПроверятьПриНачалеРаботы");
		Если НеПроверятьПриНачалеРаботы = Истина Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Показываем только если есть хоть один старый договор с датой изменения процентной ставки.
	Если Не ЕстьДоговорыСДатамиИзменения() Тогда
		НеПроверятьПриНачалеРаботы(Истина);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

// Записывает в хранилище настроек значение, которое проверяется при запуске обработки.
//
// Параметры:
//  Значение - Булево - необходимость проверки обработки при начале работы.
//
Процедура НеПроверятьПриНачалеРаботы(Значение) Экспорт
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам", "НеПроверятьПриНачалеРаботы", Значение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьДоговорыСДатамиИзменения()
	
	МассивОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДоговорыКонтрагентовИсторияПроцентныхСтавок.Ссылка КАК ДоговорКонтрагента
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов.ИсторияПроцентныхСтавок КАК ДоговорыКонтрагентовИсторияПроцентныхСтавок
	|ГДЕ
	|	НЕ ДоговорыКонтрагентовИсторияПроцентныхСтавок.УдалитьПериодСкорректирован
	|	И ДоговорыКонтрагентовИсторияПроцентныхСтавок.Период <> ДАТАВРЕМЯ(1, 1, 1)";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли