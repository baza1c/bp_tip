
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Обработки.ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам.НужноПоказатьОбработку(Истина) Тогда
		ФормаНеДоступна = Истина;
		Возврат;
	КонецЕсли;
	
	НастройкаНеПроверятьПриНачалеРаботы = ХранилищеОбщихНастроек.Загрузить(
		"ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам", "НеПроверятьПриНачалеРаботы");
	
	НеПроверятьПриНачалеРаботы = НастройкаНеПроверятьПриНачалеРаботы = Истина;
	
	ЗаполнитьТаблицуДоговоров();
	
	Если Не ПравоДоступа("Редактирование", Метаданные.Справочники.ДоговорыКонтрагентов) Тогда
		Элементы.НоваяДатаИзмененияСтавки.Видимость = Ложь;
		Элементы.Обновить.Видимость                 = Ложь;
		Элементы.ДекорацияРасположение.Видимость    = Ложь;
		Элементы.ДекорацияИсправление.Видимость     = Ложь;
	Иначе
		Элементы.ДекорацияНетПрав.Видимость         = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ФормаНеДоступна Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Договоры займа с датами изменения процентных ставок, требующие корректировок, не найдены!'"));
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура ТаблицаДоговоровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "НоваяДатаИзмененияСтавки" Тогда 
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ТаблицаДоговоров.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ОрганизацияДоговорКонтрагент) И ЗначениеЗаполнено(ТекущиеДанные.ДатаИзмененияСтавки) Тогда
		ПоказатьЗначение(, ТекущиеДанные.ПолучитьРодителя().ОрганизацияДоговорКонтрагент);
	Иначе
		ПоказатьЗначение(, ТекущиеДанные.ОрганизацияДоговорКонтрагент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПринятьИзменения(Команда)
	
	ЗаписатьИзменения();
	
	Если ТаблицаДоговоров.ПолучитьЭлементы().Количество() = 0 И НеПроверятьПриНачалеРаботы Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	
	Для Каждого ЭлементДерева Из ТаблицаДоговоров.ПолучитьЭлементы() Цикл
		Элементы.ТаблицаДоговоров.Развернуть(ЭлементДерева.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуДоговоров()
	
	ТаблицаДоговоров.ПолучитьЭлементы().Очистить();
	МассивОрганизаций = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Ложь);
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентовИсторияПроцентныхСтавок.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ДоговорыКонтрагентовИсторияПроцентныхСтавок.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ДоговорыКонтрагентовИсторияПроцентныхСтавок.Период КАК ДатаИзмененияСтавки,
	|	ДОБАВИТЬКДАТЕ(ДоговорыКонтрагентовИсторияПроцентныхСтавок.Период, ДЕНЬ, 1) КАК НоваяДатаИзмененияСтавки
	|ПОМЕСТИТЬ ИсторияПроцентныхСтавокДоговоров
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов.ИсторияПроцентныхСтавок КАК ДоговорыКонтрагентовИсторияПроцентныхСтавок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ДоговорыКонтрагентовИсторияПроцентныхСтавок.Ссылка = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	ДоговорыКонтрагентов.Организация В(&МассивОрганизаций)
	|	И ДоговорыКонтрагентовИсторияПроцентныхСтавок.Период <> ДАТАВРЕМЯ(1, 1, 1)
	|	И НЕ ДоговорыКонтрагентовИсторияПроцентныхСтавок.УдалитьПериодСкорректирован
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсторияПроцентныхСтавокДоговоров.Организация КАК Организация,
	|	ИсторияПроцентныхСтавокДоговоров.Договор КАК Договор,
	|	ИсторияПроцентныхСтавокДоговоров.Контрагент КАК Контрагент,
	|	ИсторияПроцентныхСтавокДоговоров.ТипПроцентнойСтавки КАК ТипПроцентнойСтавки,
	|	ИсторияПроцентныхСтавокДоговоров.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ИсторияПроцентныхСтавокДоговоров.ДатаИзмененияСтавки КАК ДатаИзмененияСтавки,
	|	ИсторияПроцентныхСтавокДоговоров.НоваяДатаИзмененияСтавки КАК НоваяДатаИзмененияСтавки
	|ИЗ
	|	ИсторияПроцентныхСтавокДоговоров КАК ИсторияПроцентныхСтавокДоговоров
	|ИТОГИ ПО
	|	Организация,
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсторияПроцентныхСтавокДоговоров.Договор КАК Договор
	|ИЗ
	|	ИсторияПроцентныхСтавокДоговоров КАК ИсторияПроцентныхСтавокДоговоров";
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	
	Если ПакетЗапроса[1].Пустой() Тогда
		НеПроверятьПриНачалеРаботы = Истина;
		Возврат;
	КонецЕсли;
	
	СписокДоговоровДляКорректировки.ЗагрузитьЗначения(ПакетЗапроса[2].Выгрузить().ВыгрузитьКолонку("Договор"));
	
	ВыборкаОрганизации = ПакетЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОрганизации.Следующий() Цикл
		СтрокаОрганизация = ТаблицаДоговоров.ПолучитьЭлементы().Добавить();
		СтрокаОрганизация.ОрганизацияДоговорКонтрагент = ВыборкаОрганизации.Организация;
		ВыборкаКонтрагенты = ВыборкаОрганизации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрагенты.Следующий() Цикл
			СтрокаКонтрагент = СтрокаОрганизация.ПолучитьЭлементы().Добавить();
			СтрокаКонтрагент.ОрганизацияДоговорКонтрагент = ВыборкаКонтрагенты.Контрагент;
			ВыборкаДоговоры = ВыборкаКонтрагенты.Выбрать();
			Пока ВыборкаДоговоры.Следующий() Цикл
				СтрокаДоговор = СтрокаКонтрагент.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДоговор, ВыборкаДоговоры);
				СтрокаДоговор.ОрганизацияДоговорКонтрагент = ВыборкаДоговоры.Договор;
			КонецЦикла;	
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменения()
	
	ТаблицаДанных = ДанныеФормыВЗначение(ТаблицаДоговоров, Тип("ДеревоЗначений"));
	
	ОтборПоДоговорам = Новый Структура("ОрганизацияДоговорКонтрагент");
	ОтборПоИстории = Новый Структура("Период, ПроцентнаяСтавка");
	
	Для Каждого Договор Из СписокДоговоровДляКорректировки Цикл 
		
		ДоговорОбъект = Договор.Значение.ПолучитьОбъект();
		
		НачатьТранзакцию();
		Попытка
			
			ОтборПоДоговорам.ОрганизацияДоговорКонтрагент = Договор.Значение;
			НайденныеСтроки = ТаблицаДанных.Строки.НайтиСтроки(ОтборПоДоговорам, Истина);
			Для Каждого СтрокаСНовойДатойСтавки Из НайденныеСтроки Цикл
				ОтборПоИстории.Период = СтрокаСНовойДатойСтавки.ДатаИзмененияСтавки;
				ОтборПоИстории.ПроцентнаяСтавка = СтрокаСНовойДатойСтавки.ПроцентнаяСтавка;
				СтрокиИсторииДоговора = ДоговорОбъект.ИсторияПроцентныхСтавок.НайтиСтроки(ОтборПоИстории);
				Если СтрокиИсторииДоговора.Количество() > 0 Тогда
					СтрокиИсторииДоговора[0].Период = СтрокаСНовойДатойСтавки.НоваяДатаИзмененияСтавки;
					СтрокиИсторииДоговора[0].УдалитьПериодСкорректирован = Истина;
				КонецЕсли;
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДоговорОбъект, Истина);
			ЗафиксироватьТранзакцию();

		Исключение
			ОтменитьТранзакцию();
			
			ШаблонСообщения = НСтр("ru = 'Не удалось записать договор %1:
				|%2'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Договор.Значение, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				,
				Договор.Значение,
				ТекстСообщения);
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
	
	КонецЦикла;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	ЗаполнитьТаблицуДоговоров();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	Обработки.ПроверкаДатИзмененияПроцентныхСтавокПоЗаймам.НеПроверятьПриНачалеРаботы(НеПроверятьПриНачалеРаботы);
	
КонецПроцедуры

#КонецОбласти

