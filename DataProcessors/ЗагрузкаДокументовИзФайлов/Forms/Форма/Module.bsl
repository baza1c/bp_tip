#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ШагиПомощника = ШагиПомощника();
	
	ИмяСоздаваемогоДокумента = Параметры.ИмяСоздаваемогоДокумента;
	ВидМаркетплейса = Параметры.ВидМаркетплейса;
	
	Если ЗначениеЗаполнено(ВидМаркетплейса) Тогда
		ЭтоЗагрузкаУслугМаркетплейса = Истина;
		ИмяСоздаваемогоДокумента = "ПоступлениеТоваровУслуг";
		ПоказыватьСписокКРаспознаванию = Истина;
	ИначеЕсли СоздаватьПлатежноеПоручение(ИмяСоздаваемогоДокумента)
		или СоздаватьСчетПоставщику(ИмяСоздаваемогоДокумента) Тогда
		ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.СчетНаОплату;
	ИначеЕсли СоздаватьПоступлениеТоваровУслуг(ИмяСоздаваемогоДокумента) Тогда
		ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.РеализацияТоваровИУслуг;
	КонецЕсли;
	
	РежимФормыЗагрузкаСКомпьютера = 
		(Параметры.ВариантПолученияФайлов = ЭлектронноеВзаимодействиеБПКлиентСервер.ВариантыПолученияФайлов().СКомпьютера);
	
	ДоступноРаспознавание = ПравоДоступа("Просмотр", Метаданные.Обработки.РаспознаваниеДокументов)
		И ПолучитьФункциональнуюОпцию("ИспользоватьРаспознаваниеДокументов");
	
	ПодключеноКСервисуРаспознавания = ПодключеноКСервисуРаспознавания();
	
	ЗаполнитьТекстПодсказки();
	
	Если ЭтоЗагрузкаУслугМаркетплейса Тогда
		Заголовок = НСтр("ru = 'Загрузка услуг маркетплейса из файла'");
	ИначеЕсли РежимФормыЗагрузкаСКомпьютера Тогда
		Заголовок = НСтр("ru = 'Загрузка документов из файлов'");
	Иначе
		Заголовок = НСтр("ru = 'Загрузка документов из почты'");
	КонецЕсли;
	
	Если РежимФормыЗагрузкаСКомпьютера Тогда
		
		Если ТипЗнч(Параметры.ФайлыКЗагрузке) = Тип("Массив")
			И ЗначениеЗаполнено(Параметры.ФайлыКЗагрузке) Тогда
			
			ПолучитьФайлыКЗагрузке();
			
			ПоказыватьСписокКРаспознаванию = Истина;
			Элементы.ВыборФайлов.ТекущаяСтраница = Элементы.Список;
			УстановитьДоступностьКнопкиДалее(ЭтотОбъект);
			
		КонецЕсли;
		
		ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML();
		
		УправлениеПредпросмотром(ЭтотОбъект);
		
		ТекущийШаг = ШагиПомощника.ПодборФайлов;
		
	Иначе
		
		Заголовок = НСтр("ru = 'Загрузка документов из почты'");
		
		ПоказыватьСписокКРаспознаванию = Истина;
		
		Если Не ЗначениеЗаполнено(ДатаОкончания) Тогда
			ДатаОкончания = НачалоДня(ТекущаяДата());
			ДатаНачала = ОбщегоНазначенияБПКлиентСервер.ДобавитьПериод(ДатаОкончания,
				Перечисления.Периодичность.День,
				- 7);
		КонецЕсли;
		
		УчетнаяЗапись = ЗагрузкаПочтовыхСообщений.УчетнаяЗаписьПользователяДляЗагрузкиСообщений();
		
		ТекущийШаг = ШагиПомощника.ЗагрузкаИзПочты;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	//Проверим по всем организациям
	ТребуетсяБаннерЭДО = Не ОбменСКонтрагентами.ОрганизацияПодключена(Неопределено);
	
	УстановитьБаннерНаФорме(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекстПодсказки()
	
	Если Не ДоступноРаспознавание Тогда
		ТекстРаспознавание = "";
	Иначе
		ТекстРаспознавание = "
		|● Изображения в форматах JPEG, PNG, BMP*;
		|● Одностраничные изображения в формате TIFF*;
		|● Документы в формате PDF, включая многостраничные*;
		|● Файлы в форматах MS Word и MS Excel*;
		|● Архивы в формате ZIP, содержащие вышеназванные файлы*;";
	КонецЕсли;
	
	МассивТекстовПодробно = Новый Массив;
	Если ЭтоЗагрузкаУслугМаркетплейса Тогда
		ТекстЗаголовкаПодсказки = "Загрузите Акт, УПД или Счет-фактуру в одном из следующий форматов:";
		МассивТекстовПодробно.Добавить("
		|● Документ в формате PDF или ZIP-архив из личного кабинета маркетплейса*;
		|● Электронный документ, полученный от маркетплейса через ЭДО.");
	ИначеЕсли СоздаватьПлатежноеПоручение(ИмяСоздаваемогоДокумента) Тогда
		ТекстЗаголовкаПодсказки = "Загрузите Платежное поручение из счета поставщика в одном из следующий форматов";
		МассивТекстовПодробно.Добавить(ТекстРаспознавание);
		МассивТекстовПодробно.Добавить("
		|● Электронный документ ""Счет на оплату"" в формате CML.");
	ИначеЕсли СоздаватьСчетПоставщику(ИмяСоздаваемогоДокумента) Тогда
		ТекстЗаголовкаПодсказки = "Загрузите счет от поставщика в одном из следующий форматов";
		МассивТекстовПодробно.Добавить(ТекстРаспознавание);
		МассивТекстовПодробно.Добавить("
		|● Электронный документ ""Счет на оплату""");
	ИначеЕсли СоздаватьПоступлениеТоваровУслуг(ИмяСоздаваемогоДокумента) Тогда
		ТекстЗаголовкаПодсказки = "Загрузите Акт, Накладную, УПД, УКД или Счет-фактуру поставщика в одном из следующий форматов:";
		МассивТекстовПодробно.Добавить(ТекстРаспознавание);
		МассивТекстовПодробно.Добавить("
		|● Электронные таблицы ""ТОРГ-12"" и ""УПД"", сформированные в 1С:Бухгалтерия 8, в формате MXL, XLS или XLSX;");
		МассивТекстовПодробно.Добавить("
		|● Электронные документы УПД, УКД или Счет-фактура");
	Иначе
		ТекстЗаголовкаПодсказки = "Загрузите Счет, Акт, Накладную, УПД, Счет-фактуру или Кассовый чек в одном из следующих форматов:";
		МассивТекстовПодробно.Добавить(ТекстРаспознавание);
		МассивТекстовПодробно.Добавить("
		|● Электронный документ ""Счет на оплату"";
		|● Электронные документы УПД, УКД или Счет-фактура");
	КонецЕсли;
	
	ТекстТребуетсяРаспознавание = "
	|* требуется подключение сервиса 1С:Распознавание документов";
	
	Элементы.ДекорацияПодсказка.Заголовок = Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(ТекстЗаголовкаПодсказки, Новый Шрифт(,,Истина)),
		МассивТекстовПодробно,
		ТекстТребуетсяРаспознавание);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьФайлыКЗагрузке()
	Для Каждого ФайлДокумента Из Параметры.ФайлыКЗагрузке Цикл
		ФайлИнфо = Новый Файл(ФайлДокумента.Имя);
		Расширение = ФайлИнфо.Расширение;
		
		НовыйФайл = Файлы.Добавить();
		НовыйФайл.Наименование = ФайлДокумента.Имя;
		НовыйФайл.Адрес = ФайлДокумента.Хранение;
		НовыйФайл.ИдентификаторФайла = Новый УникальныйИдентификатор;
		НовыйФайл.Расширение = Расширение;
		НовыйФайл.ИндексКартинки = РаботаСФайламиСлужебныйКлиентСервер.ИндексПиктограммыФайла(Расширение);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементыУсловногоОформления = УсловноеОформление.Элементы;
	ЭлементыУсловногоОформления.Очистить();
	
	ЭлементОформления = ЭлементыУсловногоОформления.Добавить();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
			"ДанныеЭлектронныхДокументов.Выполнено", ВидСравненияКомпоновкиДанных.Равно, Истина);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовСуммаДокумента");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовКонтрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовКомандаЗагрузить");
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);
	
	ЭлементОформления = ЭлементыУсловногоОформления.Добавить();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
			"ДанныеЭлектронныхДокументов.ЭтоОшибка", ВидСравненияКомпоновкиДанных.Равно, Истина);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовСуммаДокумента");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовКонтрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовКомандаЗагрузить");
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементОформления = ЭлементыУсловногоОформления.Добавить();
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
			"ДанныеЭлектронныхДокументов.ЭтоОшибка", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовНаименование");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементОформления.Поля, "ДанныеЭлектронныхДокументовОписаниеОшибки");
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не РежимФормыЗагрузкаСКомпьютера Тогда
	
		ПерерисоватьПоСостоянию();
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если НЕ ЗначениеЗаполнено(Настройки.Получить("УчетнаяЗапись")) Тогда
		Настройки.Вставить("УчетнаяЗапись", ЗагрузкаПочтовыхСообщений.УчетнаяЗаписьПользователяДляЗагрузкиСообщений());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДобавленыФайлыПеретаскиванием" Тогда
		Если Не ЗначениеЗаполнено(Параметр) Тогда
			Возврат;
		КонецЕсли;
		
		ОбновитьСостояниеФайлов(Параметр);
		
		ПодключитьОбработчикОжидания("ПерерисоватьПоСостоянию", 0.2, Истина);
		ПодключитьОбработчикОжидания("ЗагрузитьПревью", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если Не ЗавершениеРаботы И ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) Тогда
		
		ЗагрузкаДанныхОтменаНаСервере(ИдентификаторФоновогоЗадания);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредложениеПеретащитьИзображениеНажатие(Элемент)
	
	ВыполнитьЗагрузкуФайловИнтерактивно();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеПеретащитьИзображениеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеПеретащитьИзображениеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьФайлВСписок(ПараметрыПеретаскивания.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПросмотраДокументСформирован(Элемент)
	
	ДокументСформирован = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПолученияПочтыПриИзменении(Элемент)
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОкончаниеПриИзменении(Элемент)
	ПриИзмененииПериода();
КонецПроцедуры

&НаКлиенте
Процедура ПисьмаОтметкаПриИзменении(Элемент)
	УстановитьДоступностьКнопкиДалее(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		
	КонецЕсли;
	ПисьмаСДокументами.Очистить();
	УстановитьДоступностьКнопкиДалее(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФайлы

&НаКлиенте
Процедура ФайлыПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ЗагрузитьПревью", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Не ВыполняетсяЗагрузка Тогда
		ВыполнитьЗагрузкуФайловИнтерактивно();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПередУдалением(Элемент, Отказ)
	
	Если ВыполняетсяЗагрузка Тогда
		Отказ = Истина;
	Иначе
		УдалитьИзВременногоХранилищаФайлы(Элемент.ВыделенныеСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПослеУдаления(Элемент)
	
	ПерерисоватьПоСостоянию();
	ЗагрузитьПревью();
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если ВыполняетсяЗагрузка Тогда 
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.НеОбрабатывать;
	Иначе 
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	Если Не ВыполняетсяЗагрузка Тогда
		ДобавитьФайлВСписок(ПараметрыПеретаскивания.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеЭлектронныхДокументов

&НаКлиенте
Процедура ДанныеЭлектронныхДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = "ДанныеЭлектронныхДокументовКомандаЗагрузить" Тогда
		
		ТекДанные = ДанныеЭлектронныхДокументов.НайтиПоИдентификатору(ВыбраннаяСтрока);
		НачатьСозданиеЭлектронногоДокумента(ТекДанные.Адрес,
			ТекДанные.Наименование, ТекДанные.ЭтоЭлектроннаяТаблица);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписокПисем(Команда)
	
	Если НЕ УчетнаяЗаписьНастроенаНаПолучениеПочты(УчетнаяЗапись) Тогда
		ТекстСообщения = НСтр("ru = 'Для работы требуется настройка учетной записи электронной почты на получение сообщений.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			УчетнаяЗапись,
			"ИспользоватьДляПолучения");
		Возврат;
	КонецЕсли;

	НачатьЧтениеВложенийПисем();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ПисьмаСДокументами Цикл
		СтрокаТЧ.Отметка = Истина;
	КонецЦикла;
	
	УстановитьДоступностьКнопкиДалее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВсе(Команда)
	
	Для Каждого СтрокаТЧ Из ПисьмаСДокументами Цикл
		СтрокаТЧ.Отметка = Ложь;
	КонецЦикла;
	
	УстановитьДоступностьКнопкиДалее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)

	ПараметрыФормы = Новый Структура("НачалоПериода, КонецПериода", ДатаНачала, ДатаОкончания);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыФормы, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	Если ТекущийШаг = ШагиПомощника.ПодборФайлов
		Или ТекущийШаг = ШагиПомощника.ЗагрузкаИзПочты Тогда
		
		ШагНачальнойСтраницы = ТекущийШаг;
		
		НачатьЧтениеЭД();
		
	ИначеЕсли ТекущийШаг = ШагиПомощника.СозданиеИзЭлектронныхДокументов Тогда
		ОсталосьДокументов = КоличествоЭлектронныеДокументы(ЭтотОбъект);
		Если ОсталосьДокументов > 0 Тогда
			ТекстВопроса = НСтр("ru = 'В списке остались незагруженные электронные документы. 
			|Пропустить загрузку электронных документов?'");
			ОповещениеОтвет = Новый ОписаниеОповещения("ВопросПропуститьЭлектронныеДокументыЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОповещениеОтвет, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена,,КодВозвратаДиалога.Отмена);
		Иначе
			Если ПоказыватьСписокКРаспознаванию Тогда
				ВыполнитьШагПоказатьФайлыНаРаспознавание();
			Иначе
				ВыполнитьШагОтправитьНаРаспознавание();
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТекущийШаг = ШагиПомощника.ПроверитьПередРаспознаванием Тогда
		ВыполнитьШагОтправитьНаРаспознавание();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПропуститьЭлектронныеДокументыЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ДанныеЭлектронныхДокументов.Очистить();
		Если ПоказыватьСписокКРаспознаванию Тогда
			ВыполнитьШагПоказатьФайлыНаРаспознавание();
		Иначе
			ВыполнитьШагОтправитьНаРаспознавание();
		КонецЕсли;
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяВНачало(Команда)
	
	ВернутьсяВНачалоПомощника();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	ОсталосьДокументов = КоличествоЭлектронныеДокументы(ЭтотОбъект);
	Если ОсталосьДокументов > 0 Тогда
		ТекстВопроса = НСтр("ru = 'В списке остались незагруженные электронные документы. Закрыть форму?'");
		ОповещениеОтвет = Новый ОписаниеОповещения("ВопросЗакрытьФормуЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеОтвет, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена,,КодВозвратаДиалога.Отмена);
	Иначе
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗакрытьФормуЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредпросмотр(Команда)
	
	ПоказатьПредпросмотр = Не ПоказатьПредпросмотр;
	
	УправлениеПредпросмотром(ЭтотОбъект);
	
	Если ПоказатьПредпросмотр Тогда
		ЗагрузитьПревью();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗагрузкаИзПочты

&НаКлиенте
Процедура НачатьЧтениеВложенийПисем()
	
	ФоновоеЗадание = ЗапуститьФоновоеЧтениеВложенийПисем();
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаКоманднаяПанельПисьма.Видимость = Ложь;
	Элементы.ГруппаВыполняетсяЗагрузкаПисем.Видимость = Истина;
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	
	Обработчик = Новый ОписаниеОповещения("ПослеЧтенияПисем", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновоеЧтениеВложенийПисем()
	
	ПисьмаСДокументами.Очистить();
	
	ГлубинаПоиска = Окр(ОбщегоНазначенияБПКлиентСервер.РазностьДат(
		ДатаНачала,
		КонецДня(ТекущаяДата()),
		ПредопределенноеЗначение("Перечисление.Периодичность.День")));
		
	Отбор = Новый Структура;
	Отбор.Вставить("ДатаНачала"             , ДатаНачала);
	Отбор.Вставить("ДатаОкончания"          , КонецДня(ДатаОкончания));
	
	НастройкиЧтенияПочты = Новый Структура;
	НастройкиЧтенияПочты.Вставить("Отбор", Отбор);
	НастройкиЧтенияПочты.Вставить("ГлубинаПоиска"          , ГлубинаПоиска);
	НастройкиЧтенияПочты.Вставить("УчетнаяЗапись"          , УчетнаяЗапись);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Чтение вложений входящих писем'");
	
	АдресХранилищаПочтовыеВложения = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"Обработки.ЗагрузкаДокументовИзФайлов.ПрочитатьДокументыИзПочты",
		НастройкиЧтенияПочты,
		АдресХранилищаПочтовыеВложения);
		
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаКлиенте
Процедура ПослеЧтенияПисем(Задание, ДопПараметры) Экспорт
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Задание.Статус = "Выполнено" Тогда
		
		ЗагрузитьПрочитанныеПисьма();
		
		Если ПисьмаСДокументами.Количество() = 0 Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'За выбранный период нет писем с вложенными файлами'"));
		Иначе
			УстановитьДоступностьКнопкиДалее(ЭтотОбъект);
		КонецЕсли;
		
	Иначе
		ПоказатьПредупреждение(, Задание.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
	Элементы.ГруппаКоманднаяПанельПисьма.Видимость = Истина;
	Элементы.ГруппаВыполняетсяЗагрузкаПисем.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПрочитанныеПисьма()
	
	ПисьмаСДокументами.Очистить();
	
	ПисьмаСВложениями = ПолучитьИзВременногоХранилища(АдресХранилищаПочтовыеВложения);
	
	Для Каждого Письмо ИЗ ПисьмаСВложениями Цикл
		
		НоваяСтрока = ПисьмаСДокументами.Добавить();
		НоваяСтрока.Наименование = Письмо.Наименование;
		НоваяСтрока.Расширение = Письмо.Расширение;
		НоваяСтрока.Отметка = Истина;
		
		НоваяСтрока.ИдентификаторПисьма = Письмо.ИдентификаторПисьма;
		НоваяСтрока.ДатаПисьма = Письмо.Дата;
		НоваяСтрока.ОтправительПисьма = Письмо.Отправитель;
		
		НоваяСтрока.Адрес = ПоместитьВоВременноеХранилище(Письмо.ДанныеФайла, УникальныйИдентификатор);
		НоваяСтрока.ИндексКартинки = РаботаСФайламиСлужебныйКлиентСервер.ИндексПиктограммыФайла(НоваяСтрока.Расширение);
		
	КонецЦикла;
	
	Если ПисьмаСДокументами.Количество() > 0 Тогда
		ПисьмаСДокументами.Сортировать("ДатаПисьма УБЫВ");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаИзФайла

&НаКлиенте
Процедура НачатьЧтениеЭД()
	
	ФоновоеЗадание = ЗапуститьФоновоеЧтениеЭД();
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = истина;
	
	Обработчик = Новый ОписаниеОповещения("ПослеЧтенияЭД", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновоеЧтениеЭД()
	
	ДанныеЭлектронныхДокументов.Очистить();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Чтение электронных документов'");
	
	ИсточникДанных = ?(РежимФормыЗагрузкаСКомпьютера, Файлы, ПисьмаСДокументами);
	
	ФайлыДляОбработки = Новый ТаблицаЗначений;
	ФайлыДляОбработки.Колонки.Добавить("Расширение");
	ФайлыДляОбработки.Колонки.Добавить("Наименование");
	ФайлыДляОбработки.Колонки.Добавить("ДвоичныеДанныеФайла");
	ФайлыДляОбработки.Колонки.Добавить("ИсходныйАдрес");
	
	Отбор = Новый Структура();
	Если Не РежимФормыЗагрузкаСКомпьютера Тогда
		Отбор.Вставить("Отметка", Истина);
	КонецЕсли;

	Для каждого ДанныеФайла Из ИсточникДанных.НайтиСтроки(Отбор) Цикл
		НовыйФайл = ФайлыДляОбработки.Добавить();
		НовыйФайл.Наименование = ДанныеФайла.Наименование;
		//Передадим адрес. Оптимизация на случай если файл не изменится после обработки
		НовыйФайл.ИсходныйАдрес = ДанныеФайла.Адрес;
		НовыйФайл.ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла.Адрес);
		НовыйФайл.Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ДанныеФайла.Расширение);
	КонецЦикла;
	
	АдресХранилищаДанныеДокументов = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	//Обработаем особую ситуацию, когда пользователь привык загружать xls через ЭД
	РазрешеноЗагружатьЭлектронныеТаблицы =
		(ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.РеализацияТоваровИУслуг)
		Или Не ДоступноРаспознавание;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ФайлыДляОбработки", ФайлыДляОбработки);
	ПараметрыПроцедуры.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	ПараметрыПроцедуры.Вставить("РазрешеноЗагружатьЭлектронныеТаблицы",
		РазрешеноЗагружатьЭлектронныеТаблицы);
	ПараметрыПроцедуры.Вставить("ЭтоПлатежноеПоручение",
		СоздаватьПлатежноеПоручение(ИмяСоздаваемогоДокумента));
	
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"Обработки.ЗагрузкаДокументовИзФайлов.РазобратьДокументы",
		ПараметрыПроцедуры,
		АдресХранилищаДанныеДокументов);
		
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаКлиенте
Процедура ПослеЧтенияЭД(Задание, ДопПараметры) Экспорт
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Задание.Статус = "Выполнено" Тогда
		
		ЗагрузитьПрочитанныеДокументыЭД();
		
		КоличествоЭлектронныеДокументы = КоличествоЭлектронныеДокументы(ЭтотОбъект);
		КоличествоДокументыКРаспознаванию = КоличествоДокументыКРаспознаванию(ЭтотОбъект);
		
		//Поддержим старый формат работы
		РежимЕдиничногоЭлектронногоДокумента = 
			Не ЕстьДокументыСОшибками
			И КоличествоДокументыКРаспознаванию = 0
			И КоличествоЭлектронныеДокументы = 1
			И Не ПоказатьРекламуЭДО(ЭтотОбъект);
			
		РежимЕдиничногоДокументаРаспознавания = 
			Не ЕстьДокументыСОшибками
			И КоличествоДокументыКРаспознаванию = 1
			И КоличествоЭлектронныеДокументы = 0;
			
		Если РежимЕдиничногоЭлектронногоДокумента Тогда
			ПрочитатьЕдиничныйЭлектронныйДокумент();
		ИначеЕсли КоличествоЭлектронныеДокументы > 0 Тогда
			ВыполнитьШагПрочитатьЭлектронныеДокументы();
		ИначеЕсли РежимЕдиничногоДокументаРаспознавания Тогда
			ВыполнитьШагОтправитьНаРаспознавание();
		ИначеЕсли КоличествоДокументыКРаспознаванию > 0 Тогда
			Если Не ПоказыватьСписокКРаспознаванию Тогда
				ВыполнитьШагОтправитьНаРаспознавание();
			Иначе
				ВыполнитьШагПоказатьФайлыНаРаспознавание();
			КонецЕсли;
		ИначеЕсли КоличествоДокументыКРаспознаванию = 0 Тогда
			ПредупреждениеНетФайловЗавершение = Новый ОписаниеОповещения("ПредупреждениеНетФайловЗавершение", ЭтотОбъект);
			ПоказатьПредупреждение(ПредупреждениеНетФайловЗавершение,НСтр("ru = 'Выбранные документы не могут быть загружены'"));
		КонецЕсли;
		
	Иначе
		ПоказатьПредупреждение(, Задание.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
	Элементы.ГруппаКоманднаяПанельПисьма.Видимость = Истина;
	Элементы.ГруппаВыполняетсяЗагрузкаПисем.Видимость = Ложь;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупреждениеНетФайловЗавершение(ДопПараметры) Экспорт
	
	ВернутьсяВНачалоПомощника();
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяВНачалоПомощника()
	
	Если ШагНачальнойСтраницы = "" Тогда
		ВызватьИсключение "Неопределенное состояние";
	КонецЕсли;
	
	ТекущийШаг = ШагНачальнойСтраницы;
	ШагНачальнойСтраницы = "";
	ДанныеЭлектронныхДокументов.Очистить();
	ДокументыНаРаспознавание.Очистить();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПрочитанныеДокументыЭД()
	
	РасширенияЭлектронныТаблицДляРаспознавания = "xls;xlsx";
	
	ДанныеЭлектронныхДокументов.Очистить();
	ДокументыНаРаспознавание.Очистить();
	ПрочитанныеДанные = ПолучитьИзВременногоХранилища(АдресХранилищаДанныеДокументов);
	
	Для Каждого ДанныеФайла Из ПрочитанныеДанные Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеФайла.ДанныеЭД)
			И ДоступноРаспознавание
			И СтрНайти(РасширенияЭлектронныТаблицДляРаспознавания, НРег(ДанныеФайла.Расширение))> 0 Тогда
			//Подавим сообщение об ошибке - отправим файл на распознавание
			ДанныеФайла.ОписаниеОшибки = "";
		КонецЕсли;
		
		//Ошибки создает только попытка чтения ЭД.
		//При отключенном распознавании или бесправном пользователе - покажем ошибки в таблице электронных
		Если ЗначениеЗаполнено(ДанныеФайла.ДанныеЭД) 
			Или Не ПустаяСтрока(ДанныеФайла.ОписаниеОшибки)
			Или Не ДоступноРаспознавание Тогда
			
			СтрокаДанных = ДанныеЭлектронныхДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанных, ДанныеФайла);
			
			Если Не ПустаяСтрока(СтрокаДанных.ОписаниеОшибки) Тогда
				Если ЗначениеЗаполнено(ДанныеФайла.ИсходноеНаименование) Тогда
					СтрокаДанных.Наименование = ДанныеФайла.ИсходноеНаименование;
				КонецЕсли;
				СтрокаДанных.ЭтоОшибка = Истина;
				ЕстьДокументыСОшибками = Истина;
				Продолжить;
			ИначеЕсли Не ЗначениеЗаполнено(ДанныеФайла.ДанныеЭд)
				И Не ДоступноРаспознавание Тогда
				СтрокаДанных.ОписаниеОшибки = НСтр("ru = 'В файле отсутствуют данные для загрузки'");
				СтрокаДанных.ЭтоОшибка = Истина;
				ЕстьДокументыСОшибками = Истина;
				Продолжить;
			КонецЕсли;
			
			ТипДокумента = ДанныеФайла.ДанныеЭд.ТипДокумента;
			ЭтоЭлектроннаяТаблица = ДанныеФайла.ДанныеЭд.ЭтоЭлектроннаяТаблица;
			
			ОписаниеДокумента = ОписаниеСтрокиСозданияЭДПоТипу(ТипДокумента,
				ИмяСоздаваемогоДокумента,
				ЭтоЭлектроннаяТаблица);
			Если ОписаниеДокумента = Неопределено Тогда
				СтрокаДанных.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"Ошибка разбора ЭД %1. Причина: Не поддерживаемый тип ЭД %2",
					СтрокаДанных.Наименование,
					ТипДокумента);
				СтрокаДанных.ЭтоОшибка = Истина;
				ЕстьДокументыСОшибками = Истина;
				Продолжить;
			ИначеЕсли ЗначениеЗаполнено(ВариантЗагрузкиЭД)
				И ВариантЗагрузкиЭД <> ОписаниеДокумента.ВариантЗагрузкиЭД Тогда
				СтрокаДанных.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"Ошибка загрузки ЭД %1. Причина: вид ЭД отличается от ожидаемого %2",
					СтрокаДанных.Наименование,
					ВариантЗагрузкиЭД);
				СтрокаДанных.ЭтоОшибка = Истина;
				ЕстьДокументыСОшибками = Истина;
				Продолжить;
			КонецЕсли;
			
			СтрокаДанных.Документ = СтрШаблон(ОписаниеДокумента.ПредставлениеЭД,
				ДанныеФайла.ДанныеЭд.НомерДокумента,
				Формат(ДанныеФайла.ДанныеЭд.ДатаДокумента, "ДЛФ=D"));
			
			СтрокаДанных.КомандаЗагрузить = СтрШаблон(НСтр("ru = 'Создать
				|%1'"), ОписаниеДокумента.ПредставлениеДокумента);
			
			СтрокаДанных.Контрагент = ДанныеФайла.ДанныеЭд.КонтрагентПредставление;
			СтрокаДанных.СуммаДокумента = ДанныеФайла.ДанныеЭд.СуммаДокумента;
			
			СтрокаДанных.ЭтоЭлектроннаяТаблица = ЭтоЭлектроннаяТаблица;
			
		Иначе
			СтрокаДанных = ДокументыНаРаспознавание.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДанных, ДанныеФайла);
			СтрокаДанных.ИндексКартинки = РаботаСФайламиСлужебныйКлиентСервер.ИндексПиктограммыФайла(СтрокаДанных.Расширение);
		КонецЕсли;
		
		//Если файл не изменился - будем использовать тот же адрес
		Если ЗначениеЗаполнено(ДанныеФайла.ИсходныйАдрес) Тогда
			АдресФайла = ДанныеФайла.ИсходныйАдрес;
		Иначе
			АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла.ДвоичныеДанныеФайла, УникальныйИдентификатор);
		КонецЕСли;
		
		СтрокаДанных.Адрес = АдресФайла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеСтрокиСозданияЭДПоТипу(ТипДокумента, ИмяСоздаваемогоДокумента, ЭтоЭлектроннаяТаблица)
	
	ДоступныеТипы = ОбменСКонтрагентами.ТипыДокументов();
	
	ПредставлениеЭД =  НСтр("ru='Документ №%1 от %2'");
	ПредставлениеДокумента = "Документ";
	ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.РеализацияТоваровИУслуг;
	
	Если ТипДокумента = ДоступныеТипы.СчетНаОплату Тогда
		ВариантЗагрузкиЭД = Перечисления.ВариантыЗагрузкиЭД.СчетНаОплату;
		ПредставлениеЭД = НСтр("ru='Счет №%1 от %2'");
		Если СоздаватьПлатежноеПоручение(ИмяСоздаваемогоДокумента) Тогда
			ПредставлениеДокумента = НСтр("ru = 'Платежное поручение'");
		Иначе
			ПредставлениеДокумента = НСтр("ru = 'Счет от поставщика'");
		КонецЕсли;
	ИначеЕсли ТипДокумента = ДоступныеТипы.ТоварнаяНакладная Тогда
		//Для настоящих электронных документов - УПД 2
		Если ЭтоЭлектроннаяТаблица Тогда
			ПредставлениеЭД = НСтр("ru='Накладная №%1 от %2'");
		Иначе
			ПредставлениеЭД = НСтр("ru='УПД №%1 от %2'");
		КонецЕсли;
		ПредставлениеДокумента = НСтр("ru = 'Поступление товаров и услуг'");
	ИначеЕсли ТипДокумента = ДоступныеТипы.АктНаПередачуПрав
		Или ТипДокумента = ДоступныеТипы.АктВыполненныхРабот Тогда
		ПредставлениеЭД = НСтр("ru='УПД №%1 от %2'");
		ПредставлениеДокумента = НСтр("ru = 'Поступление услуг'");
	ИначеЕсли ТипДокумента = ДоступныеТипы.УПД Тогда
		ПредставлениеЭД = НСтр("ru='УПД №%1 от %2'");
		ПредставлениеДокумента = НСтр("ru = 'Поступление услуг и счет-фактура'");
	ИначеЕсли ТипДокумента = ДоступныеТипы.УКД Тогда
		ПредставлениеЭД = НСтр("ru='УКД №%1 от %2'");
		ПредставлениеДокумента = НСтр("ru = 'Корректировку поступления и счет-фактуры'");
	ИначеЕсли ТипДокумента = ДоступныеТипы.СчетФактура Тогда
		ПредставлениеЭД = НСтр("ru='Счет-фактура №%1 от %2'");
		ПредставлениеДокумента = НСтр("ru = 'Счет-фактура полученный'");
	ИначеЕсли ТипДокумента = ДоступныеТипы.КорректировочныйСчетФактура Тогда
		ПредставлениеЭД = НСтр("ru='Корр. счет-фактура №%1 от %2'");
		ПредставлениеДокумента = НСтр("ru = 'Счет-фактура полученный'");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура();
	Результат.Вставить("ПредставлениеЭД", ПредставлениеЭД);
	Результат.Вставить("ПредставлениеДокумента", ПредставлениеДокумента);
	Результат.Вставить("ВариантЗагрузкиЭД", ВариантЗагрузкиЭД);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ПоказатьПодсказку = Ложь;
	ДоступноНазад = Ложь;
	ДоступноЗакрыть = Ложь;
	ДоступноДалее = Ложь;
	
	Элементы = Форма.Элементы;
	ШагиПомощника = Форма.ШагиПомощника;
	
	Если Форма.ТекущийШаг = ШагиПомощника.ЗагрузкаИзПочты Тогда
		Элементы.ШагиОтправки.ТекущаяСтраница = Элементы.ЗагрузкаИзПочты;
		ПоказатьПодсказку = Истина;
		ДоступноДалее = Истина;
	ИначеЕсли Форма.ТекущийШаг = ШагиПомощника.ПодборФайлов Тогда
		Элементы.ШагиОтправки.ТекущаяСтраница = Элементы.ПодборФайлов;
		ПоказатьПодсказку = Истина;
		ДоступноДалее = Истина;
	ИначеЕсли Форма.ТекущийШаг = ШагиПомощника.СозданиеИзЭлектронныхДокументов Тогда
		Элементы.ШагиОтправки.ТекущаяСтраница = Элементы.ОбработкаФайловЭлектронно;
		ДоступноНазад = Истина;
		ДоступноДалее = Форма.КоличествоДокументыКРаспознаванию > 0;
		ДоступноЗакрыть = Форма.КоличествоДокументыКРаспознаванию = 0;
	ИначеЕсли Форма.ТекущийШаг = ШагиПомощника.ПроверитьПередРаспознаванием Тогда
		Элементы.ШагиОтправки.ТекущаяСтраница = Элементы.ОтправкаФайловНаРаспознавание;
		ДоступноДалее = Форма.КоличествоДокументыКРаспознаванию > 0;
		ДоступноНазад = Истина;
	ИначеЕсли Форма.ТекущийШаг = ШагиПомощника.ОтправитьНаРаспознавание Тогда
		Элементы.ШагиОтправки.ТекущаяСтраница = Элементы.ОжиданиеОтправкиНаРаспознавание;
		ДоступноНазад = Истина;
	КонецЕсли;
	
	Элементы.КнопкаДалее.Видимость = ДоступноДалее;
	Элементы.КнопкаНазад.Видимость = ДоступноНазад;
	Элементы.КнопкаЗакрыть.Видимость = ДоступноЗакрыть;
	Если ДоступноЗакрыть Тогда
		Элементы.КнопкаЗакрыть.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.КнопкаДалее.КнопкаПоУмолчанию = Истина;
	КонецЕСли;
		
	Элементы.ГруппаПодсказка.Видимость = ПоказатьПодсказку;
	
	УправлениеНавигацией(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКнопкиДалее(Форма)
	
	Доступность = Форма.ПисьмаСДокументами.НайтиСтроки(Новый Структура("Отметка", Истина)).Количество() > 0
			Или Форма.Файлы.Количество() > 0;
	
	Форма.Элементы.КнопкаДалее.Доступность = Доступность;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеНавигацией(Форма)
	
	Элементы = Форма.Элементы;
	ШагиПомощника = Форма.ШагиПомощника;
	ПоказыватьНавигацию = (Форма.ТекущийШаг = ШагиПомощника.СозданиеИзЭлектронныхДокументов
			Или Форма.ТекущийШаг = ШагиПомощника.ПроверитьПередРаспознаванием
			Или Форма.ТекущийШаг = ШагиПомощника.ОтправитьНаРаспознавание)
			И Форма.КоличествоДокументыКРаспознаванию > 0
			И Форма.КоличествоЭлектронныеДокументы > 0;
			
	Элементы.ГруппаНавигация.Видимость = ПоказыватьНавигацию;
	
	Если Не ПоказыватьНавигацию Тогда
		Возврат;
	КонецЕсли;
		
	Элементы.Шаг1.Заголовок = НСтр("ru = '1. Загрузка электронных документов'");
	Элементы.Шаг2.Заголовок = НСтр("ru = '2. Отправка на распознавание'");
	Элементы.Шаг1.Доступность = Форма.ТекущийШаг = ШагиПомощника.СозданиеИзЭлектронныхДокументов;
	Элементы.Шаг2.Доступность = (Форма.ТекущийШаг = ШагиПомощника.ПроверитьПередРаспознаванием)
		Или (Форма.ТекущийШаг = ШагиПомощника.ОтправитьНаРаспознавание);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеПредпросмотром(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.Предпросмотр.Видимость = Форма.ПоказатьПредпросмотр;
	
	Если Форма.ПоказатьПредпросмотр Тогда
		Элементы.ПоказатьПредпросмотр.Картинка = БиблиотекаКартинок.СтрелкаВправо;
	Иначе
		Элементы.ПоказатьПредпросмотр.Картинка = БиблиотекаКартинок.СтрелкаВлево;
	КонецЕсли;
	
	Элементы.ПоказатьПредпросмотр.Пометка = Форма.ПоказатьПредпросмотр;
	
	Форма.ДокументСформирован = Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КоличествоЭлектронныеДокументы(Форма)
	
	Отбор = Новый Структура("Выполнено",
		Ложь);
	
	Возврат Форма.ДанныеЭлектронныхДокументов.НайтиСтроки(Отбор).Количество();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоказатьРекламуЭДО(Форма)
	
	Отбор = Новый Структура("Расширение",
		"xml");
	
	Возврат Форма.ДанныеЭлектронныхДокументов.НайтиСтроки(Отбор).Количество() > 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КоличествоДокументыКРаспознаванию(Форма)
	
	Возврат Форма.ДокументыНаРаспознавание.Количество();
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьШагПрочитатьЭлектронныеДокументы()
	
	ТекущийШаг = ШагиПомощника.СозданиеИзЭлектронныхДокументов;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыполнитьШагПоказатьФайлыНаРаспознавание()
	ТекущийШаг = ШагиПомощника.ПроверитьПередРаспознаванием;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьШагОтправитьНаРаспознавание()
	
	ТекущийШаг = ШагиПомощника.ОтправитьНаРаспознавание;
	
	ТекстОписанияОтправки = НСтр("ru = 'Подготовка файлов к передаче...'");
	
	Если ПодключеноКСервисуРаспознавания Тогда
		ВыполнитьОтправкуФайлов();
	Иначе
		Обработчик = Новый ОписаниеОповещения("ПослеДобавленияФайловНеПодключенСервисРаспознавания", ЭтотОбъект);
		РаспознаваниеДокументовКлиент.ПоказатьДобавлениеФайлов(УникальныйИдентификатор, Обработчик);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеДобавленияФайловНеПодключенСервисРаспознавания(Результат, ДопПараметры) Экспорт
	
	ПодключеноКСервисуРаспознавания = ПодключеноКСервисуРаспознавания();
	
	Если ПодключеноКСервисуРаспознавания Тогда
		ВыполнитьОтправкуФайлов();
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодключеноКСервисуРаспознавания()
	
	ПытатьсяПодключитьсяПриПроверке = Ложь;
	ВыбрасыватьИсключение = Истина;
	
	Возврат РаспознаваниеДокументов.ПодключеноКСервисуРаспознавания(ПытатьсяПодключитьсяПриПроверке, ВыбрасыватьИсключение);
	
КонецФункции

#Область КомандыДействий

&НаКлиенте
Процедура ПерерисоватьПоСостоянию()
	
	ЕстьЗагруженныеФайлы = (Файлы.Количество() > 0);
	
	Если ВыполняетсяЗагрузка Или ЕстьЗагруженныеФайлы Тогда
		Элементы.ВыборФайлов.ТекущаяСтраница = Элементы.Список;
	Иначе
		Элементы.ВыборФайлов.ТекущаяСтраница = Элементы.ПредложениеПеретащить;
	КонецЕсли;
	
	Элементы.СостояниеЗагрузки.Видимость = ВыполняетсяЗагрузка;
	
	УстановитьДоступностьКнопкиДалее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПревью()
	
	Если Не ПоказатьПредпросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ПредпросмотрИзображение;
	
	ТекущиеДанные = Элементы.Файлы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТекстНевыбраннойКартинки = НСтр("ru = 'Выберите файл для предварительного просмотра'");
	ИначеЕсли ПустаяСтрока(ТекущиеДанные.Адрес) Тогда 
		ТекстНевыбраннойКартинки = НСтр("ru = 'Файл в процессе загрузки. Пожалуйста подождите...'");
	ИначеЕсли ЭтоРасширениеИзображения(ТекущиеДанные.Расширение) Тогда
		Элементы.ПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ПредпросмотрHTML;
		
		Если Не ДокументСформирован Тогда
			ПодключитьОбработчикОжидания("ЗагрузитьПревью", 0.5, Истина);
			Возврат;
		КонецЕсли;
		
		РаспознаваниеДокументовСлужебныйКлиент.ЗагрузитьКартинкуПоАдресу(Элементы.ПолеПросмотра, ТекущиеДанные.Адрес);
	ИначеЕсли ЭтоРасширениеPDF(ТекущиеДанные.Расширение) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ТекущиеДанные.Адрес);
		Если ДвоичныеДанные <> Неопределено Тогда
			ПолеПросмотраPDF.ПрочитатьАсинх(ДвоичныеДанные.ОткрытьПотокДляЧтения());
			Элементы.ПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ПредпросмотрPDF;
		КонецЕсли;
	Иначе
		ТекстНевыбраннойКартинки = НСтр("ru = 'Предварительный просмотр файлов данного типа не поддерживается'");
	КонецЕсли;
	
	Элементы.Изображение.ТекстНевыбраннойКартинки = ТекстНевыбраннойКартинки;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлВСписок(СсылкиНаФайлы)
	
	Если ТипЗнч(СсылкиНаФайлы) = Тип("СсылкаНаФайл") Тогда
		СсылкиНаФайлы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкиНаФайлы);
	КонецЕсли;
	
	Для Каждого СсылкаНаФайл Из СсылкиНаФайлы Цикл
		Файл = ДобавитьФайл(СсылкаНаФайл.Имя, СсылкаНаФайл.ИдентификаторФайла, СсылкаНаФайл.Расширение);
		
	КонецЦикла;
	
	ВыполнитьЗагрузкуФайлов(СсылкиНаФайлы);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьИзВременногоХранилищаФайлы(Знач ВыделенныеСтроки)
	
	Для Каждого Идентификатор Из ВыделенныеСтроки Цикл 
		
		Файл = Файлы.НайтиПоИдентификатору(Идентификатор);
		
		Если ЭтоАдресВременногоХранилища(Файл.Адрес) Тогда
			УдалитьИзВременногоХранилища(Файл.Адрес);
		КонецЕсли;
		
		Файл.Адрес = Неопределено;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеФайлов(ПомещенныеФайлы)
	
	Для Каждого ПомещенныйФайл Из ПомещенныеФайлы Цикл
		
		Файл = НайтиФайлПоИдентификатору(ПомещенныйФайл.СсылкаНаФайл.ИдентификаторФайла);
		
		Если Файл = Неопределено Тогда
			Файл = ДобавитьФайл(
				ПомещенныйФайл.СсылкаНаФайл.Имя,
				ПомещенныйФайл.СсылкаНаФайл.ИдентификаторФайла,
				ПомещенныйФайл.СсылкаНаФайл.Расширение
			);
		КонецЕсли;
		
		Файл.Адрес = ПомещенныйФайл.Адрес;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуФайловИнтерактивно()
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов(НСтр("ru = 'Выберите файлы для загрузки'"));
	ВыполнитьЗагрузкуФайлов(ПараметрыДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуФайлов(СсылкиНаФайлыИлиПараметрыДиалога)
	
	НачатьПомещениеФайловНаСервер(
		Новый ОписаниеОповещения("ПриЗавершенииПомещенияФайлов", ЭтотОбъект),
		Новый ОписаниеОповещения("ПриВыполненииПомещенияФайлов", ЭтотОбъект),
		Новый ОписаниеОповещения("ПередНачаломПомещенияФайлов", ЭтотОбъект),
		СсылкиНаФайлыИлиПараметрыДиалога,
		УникальныйИдентификатор
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтправкуФайлов()
	
	ФоновоеЗадание = ЗапуститьФоновуюОтправкуФайлов();
	
	ОбработчикПрогресса = Новый ОписаниеОповещения("ПриПрогрессеОтправкиФайлов", ЭтотОбъект);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
	НастройкиОжидания.ОповещениеОПрогрессеВыполнения = ОбработчикПрогресса;
	
	Обработчик = Новый ОписаниеОповещения("ПослеОтправкиФайлов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОбработчикиСобытийФайлы

&НаКлиенте
Процедура ПередНачаломПомещенияФайлов(ПомещаемыеФайлы, ОтказОтПомещенияВсехФайлов, Контекст) Экспорт
	
	ВыполняетсяЗагрузка = Истина;
	ПерерисоватьПоСостоянию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыполненииПомещенияФайлов(ПомещаемыйФайл, Помещено, ОтказОтПомещенияФайла, ПомещеноВсего, ОтказОтПомещенияВсехФайлов, Контекст) Экспорт
	
	Если Не ЭтотОбъект.Открыта() Тогда
		ОтказОтПомещенияВсехФайлов = Истина;
	КонецЕсли;
	
	ПрогрессЗагрузки = ПомещеноВсего;
	ТекстСообщенияЗагрузки = СтрШаблон(
		НСтр("ru = 'Загрузка %1 (%2)'"),
		ПомещаемыйФайл.Имя,
		Формат(Помещено, "ЧФ='Ч %'")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПомещенияФайлов(ПомещенныеФайлы, Контекст) Экспорт
	
	ВыполняетсяЗагрузка = Ложь;
	
	Если Не ЗначениеЗаполнено(ПомещенныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСостояниеФайлов(ПомещенныеФайлы);
	
	ПодключитьОбработчикОжидания("ПерерисоватьПоСостоянию", 0.2, Истина);
	ПодключитьОбработчикОжидания("ЗагрузитьПревью", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПрогрессеОтправкиФайлов(Результат, Контекст) Экспорт
	
	Если Результат.Статус = "Выполняется" Тогда
		Прогресс = ПрочитатьПрогресс(Результат.ИдентификаторЗадания);
		Если Прогресс <> Неопределено Тогда
			ТекстОписанияОтправки = Прогресс.Текст;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиФайлов(Задание, Контекст) Экспорт
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Задание.Статус = "Выполнено" Тогда
		
		ПослеДобавленияФайлов = Новый ОписаниеОповещения("ПослеДобавленияФайлов", РаспознаваниеДокументовКлиент, ЭтотОбъект);
		ВыполнитьОбработкуОповещения(ПослеДобавленияФайлов, ДокументыНаРаспознавание.Количество());
		Закрыть();
		
	Иначе
		ПослеДобавленияФайловОшибка = Новый ОписаниеОповещения("ПослеДобавленияФайловОшибка", ЭтотОбъект);
		ПоказатьПредупреждение(ПослеДобавленияФайловОшибка, Задание.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеДобавленияФайловОшибка(ДопПараметры) Экспорт
	ПослеДобавленияФайлов = Новый ОписаниеОповещения("ПослеДобавленияФайлов", РаспознаваниеДокументовКлиент, ЭтотОбъект);
	ВыполнитьОбработкуОповещения(ПослеДобавленияФайлов, ДокументыНаРаспознавание.Количество());
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОтправкаФайлов

&НаСервере
Функция ЗапуститьФоновуюОтправкуФайлов()
	
	ОтправляемыеФайлы = Новый Массив;
	Для каждого ДанныеФайла Из ДокументыНаРаспознавание Цикл
		
		ОписаниеФайла = Новый Структура;
		ОписаниеФайла.Вставить("ИмяФайла", ДанныеФайла.Наименование);
		ОписаниеФайла.Вставить("ДатаИзменения");
		ОписаниеФайла.Вставить("Каталог");
		ОписаниеФайла.Вставить("ДвоичныеДанные", ПолучитьИзВременногоХранилища(ДанныеФайла.Адрес));
		
		ОтправляемыеФайлы.Добавить(ОписаниеФайла);
	КонецЦикла;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка файлов в сервиса распознавания документов'");
	
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"Обработки.РаспознаваниеДокументов.ОтправитьФайлыНаРаспознавание",
		ОтправляемыеФайлы);
	
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПрочитатьПрогресс(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ПрочитатьПрогресс(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Функция ДобавитьФайл(Имя, ИдентификаторФайла, Расширение)
	
	НовыйФайл = Файлы.Добавить();
	НовыйФайл.Наименование = Имя;
	НовыйФайл.ИдентификаторФайла = ИдентификаторФайла;
	НовыйФайл.Расширение = Расширение;
	НовыйФайл.ИндексКартинки = РаботаСФайламиСлужебныйКлиентСервер.ИндексПиктограммыФайла(Расширение);
	
	Возврат НовыйФайл;
	
КонецФункции

&НаКлиенте
Функция НайтиФайлПоИдентификатору(ИдентификаторФайла)
	
	Для Каждого Файл Из Файлы Цикл
		
		Если Файл.ИдентификаторФайла = ИдентификаторФайла Тогда
			Возврат Файл;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоРасширениеИзображения(Знач РасширениеФайла)
	
	Возврат СтрНайти(
		".jpg;.jpeg;.jp2;.jpe;.bmp;.dib;.tif;.tiff;.gif;.gif;.png;",
		НормализоватьРасширениеФайла(РасширениеФайла)
	) <> 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоРасширениеPDF(Знач РасширениеФайла)
	
	Возврат СтрНайти(
		".pdf;",
		НормализоватьРасширениеФайла(РасширениеФайла)
	) <> 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НормализоватьРасширениеФайла(Знач РасширениеФайла)
	
	Если ТипЗнч(РасширениеФайла) <> Тип("Строка")
		Или ПустаяСтрока(РасширениеФайла) Тогда
		Возврат 0;
	КонецЕсли;
	
	РасширениеФайла = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(РасширениеФайла);
	
	Возврат "." + НРег(РасширениеФайла) + ";";
	
КонецФункции

#КонецОбласти

#Область СлужебныеОбработчикиСобытийФормы

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачала    = РезультатВыбора.НачалоПериода;
	ДатаОкончания = РезультатВыбора.КонецПериода;
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УчетнаяЗаписьНастроенаНаПолучениеПочты(Знач УчетнаяЗапись)
	
	Возврат ЗначениеЗаполнено(УчетнаяЗапись) И УчетнаяЗапись.ПолучитьОбъект() <> Неопределено 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ИспользоватьДляПолучения");
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииПериода()
	
	ПисьмаСДокументами.Очистить();
	
	УстановитьДоступностьКнопкиДалее(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Процедура ЗагрузкаДанныхОтменаНаСервере(Знач ИдентификаторФоновогоЗадания)
	
	Если ЗначениеЗаполнено(ИдентификаторФоновогоЗадания) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторФоновогоЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЕдиничныйЭлектронныйДокумент()
	
	ТекДанные = ДанныеЭлектронныхДокументов[0];
	
	НачатьСозданиеЭлектронногоДокумента(ТекДанные.Адрес,
		ТекДанные.Наименование, ТекДанные.ЭтоЭлектроннаяТаблица);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСозданиеЭлектронногоДокумента(АдресФайла, ПолноеИмяФайла, ЭтоЭлектроннаяТаблица)
	
	РасширениеФайла = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ПолноеИмяФайла);
	
	Если СоздаватьПлатежноеПоручение(ИмяСоздаваемогоДокумента) Тогда
		
		АдресаФайловXML = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(АдресФайла);
		РеквизитыСчета = ПрочитатьРеквизитыИзФайлаСчетаXMLНаСервере(АдресаФайловXML, УникальныйИдентификатор);
		Если Не ЗначениеЗаполнено(РеквизитыСчета) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", РеквизитыСчета);
		ДопПараметры = Элементы.ДанныеЭлектронныхДокументов.ТекущаяСтрока;
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗагрузкаПросмотрЭлектронногоДокументаЗакрытие",
		ЭтотОбъект,
		ДопПараметры);
		
		ОткрытьФорму("Документ.ПлатежноеПоручение.ФормаОбъекта", ПараметрыФормы,,,,,ОповещениеОЗакрытии);
		
	ИначеЕсли СоздаватьПоступлениеТоваровУслуг(ИмяСоздаваемогоДокумента)
		И ЭтоЭлектроннаяТаблица Тогда
		
		ОбработатьТабличныйДокумент(АдресФайла, РасширениеФайла);
		
	Иначе
		
		СтруктураОбмена = ИнтерфейсДокументовЭДОКлиент.НовыеПараметрыФормыПросмотраЗагрузкиЭлектронныхДокументов();
		// Для загрузки электронных документов в архиве zip использован механизм однократных сделок БЭД.
		СтруктураОбмена.НаправлениеЭД = ПредопределенноеЗначение("Перечисление.НаправленияЭДО.Входящий");
		СтруктураОбмена.УникальныйИдентификатор = УникальныйИдентификатор;
		
		СтруктураОбмена.ФайлАрхива = Ложь;
		СтруктураОбмена.ИмяФайла = ПолноеИмяФайла;
		СтруктураОбмена.АдресХранилищаФайла = АдресФайла;
		
		ДопПараметры = Элементы.ДанныеЭлектронныхДокументов.ТекущаяСтрока;
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗагрузкаПросмотрЭлектронногоДокументаЗакрытие",
			ЭтотОбъект,
			ДопПараметры);
		
		ПараметрыФормы = Новый Структура("СтруктураЭД", СтруктураОбмена);
		ОткрытьФорму("Обработка.ИнтерфейсДокументовЭДО.Форма.ЗагрузкаПросмотрЭлектронногоДокумента",
			ПараметрыФормы,
			ЭтотОбъект,
			СтруктураОбмена.УникальныйИдентификатор,
			,,
			ОповещениеОЗакрытии);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрочитатьРеквизитыИзФайлаСчетаXMLНаСервере(АдресаФайловXML, УникальныйИдентификатор)
	
	РеквизитыСчета = Документы.СчетНаОплатуПокупателю.РазобратьСчетаНаОплатуПокупателюXML(АдресаФайловXML);
	Если ЗначениеЗаполнено(РеквизитыСчета) Тогда
		
		ЕстьОшибки = Ложь;
		НайденныеРеквизиты = РеквизитыСчета[0].ДанныеДокумента;
		АдресФайлаДанных = ПоместитьВоВременноеХранилище(НайденныеРеквизиты.ФайлДанных, УникальныйИдентификатор);
		
		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("ДеревоРазбора", НайденныеРеквизиты.ДеревоРазбора);
		ДанныеДокумента.Вставить("СтрокаОбъекта", Неопределено);
		
		СтруктураЭД = ЭлектронноеВзаимодействиеБП.ПолучитьСтруктуруЭД(
			ДанныеДокумента,
			УникальныйИдентификатор,
			АдресФайлаДанных,
			РеквизитыСчета[0].Наименование);
		НайденныеРеквизиты.ДанныеРазбора = СтруктураЭД.ДанныеФайлаРазбора;
		ДополнитьСтруктуруРеквизитовСчета(НайденныеРеквизиты, ЕстьОшибки);
		
		НайденныеРеквизиты.ДеревоРазбора = Неопределено;
		
		Если ЕстьОшибки Тогда
			Возврат Неопределено;
		Иначе
			Возврат НайденныеРеквизиты;
		КонецЕсли;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДополнитьСтруктуруРеквизитовСчета(СтруктураРеквизитов, ЕстьОшибки) Экспорт
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(СтруктураРеквизитов.РеквизитыКонтрагента.КПП) Тогда
		КПП = СтруктураРеквизитов.РеквизитыКонтрагента.КПП;
	Иначе
		КПП = "";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИНН", СтруктураРеквизитов.РеквизитыКонтрагента.ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН = &ИНН
	|	И Контрагенты.КПП = &КПП";
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	НачатьТранзакцию();
	Если РезультатЗапроса.Следующий() Тогда
		СтруктураРеквизитов.Вставить("Контрагент", РезультатЗапроса.Ссылка);
	Иначе
		СоздатьКонтрагента(СтруктураРеквизитов, ЕстьОшибки);
	КонецЕсли;
	
	Если Не ЕстьОшибки Тогда
		БанковскийСчетКонтрагента = Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета",
			СтруктураРеквизитов.РеквизитыКонтрагента.НомерСчета, , СтруктураРеквизитов.Контрагент);
		
		Если ЗначениеЗаполнено(БанковскийСчетКонтрагента) Тогда
			СтруктураРеквизитов.Вставить("СчетКонтрагента", БанковскийСчетКонтрагента);
		Иначе
			СоздатьБанковскийСчетКонтрагента(СтруктураРеквизитов, ЕстьОшибки);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЕстьОшибки Тогда
		ЗафиксироватьТранзакцию();
		СтруктураРеквизитов.Вставить("СуммаДокумента"   , СтруктураРеквизитов.ШапкаДокумента.СуммаДокумента);
		СтруктураРеквизитов.Вставить("СуммаНДС"         , СтруктураРеквизитов.ШапкаДокумента.СуммаНДС);
		СтруктураРеквизитов.Вставить("СтавкаНДС"        , СтруктураРеквизитов.ШапкаДокумента.СтавкаНДС);
		СтруктураРеквизитов.Вставить("НазначениеПлатежа", СтруктураРеквизитов.ШапкаДокумента.НазначениеПлатежа);
	Иначе
		ОтменитьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьКонтрагента(СтруктураРеквизитов, ЕстьОшибки)
	
	РеквизитыКонтрагента = СтруктураРеквизитов.РеквизитыКонтрагента;
	РеквизитыКонтрагента.Вставить("ЭтоЭлектронныйДокумент", Истина);
	Контрагент = Справочники.Контрагенты.СоздатьЭлемент();
	Контрагент.Заполнить(РеквизитыКонтрагента);
	
	Попытка
		Контрагент.Записать();
		СтруктураРеквизитов.Вставить("Контрагент", Контрагент.Ссылка);
	Исключение
		ЕстьОшибки = Истина;
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Запись нового элемента справочника Контрагенты'"),
			УровеньЖурналаРегистрации.Ошибка,
			Контрагент.Метаданные(),
			Контрагент,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ТекстОшибки = НСтр("ru = 'Ошибка при записи нового элемента справочника Контрагенты
			|Подробности см. в Журнале регистрации.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьБанковскийСчетКонтрагента(СтруктураРеквизитов, ЕстьОшибки)
	
	Банки = Справочники.Банки.ПолучитьТаблицуБанковПоРеквизитам("Код", СтруктураРеквизитов.РеквизитыКонтрагента.БИК);
	Если Банки.Количество() = 0 Тогда
		ЕстьОшибки = Истина;
		ШаблонСообщения = НСтр("ru='Загрузка прервана. БИК %1 банка контрагента не найден в классификаторе банков РФ.'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения,
			СтруктураРеквизитов.РеквизитыКонтрагента.БИК);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	Иначе
		БанкКонтрагента = Банки[0].Ссылка;
	КонецЕсли;
	
	ДанныеРасчетногоСчета = Новый Структура;
	ДанныеРасчетногоСчета.Вставить("ЭтоЭлектронныйДокумент" , Истина);
	ДанныеРасчетногоСчета.Вставить("НомерСчета"             , СтруктураРеквизитов.РеквизитыКонтрагента.НомерСчета);
	ДанныеРасчетногоСчета.Вставить("Владелец"               , СтруктураРеквизитов.Контрагент);
	ДанныеРасчетногоСчета.Вставить("Банк"                   , БанкКонтрагента);
	ДанныеРасчетногоСчета.Вставить("ВалютаДенежныхСредств",
		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
		
	БанковскийСчетКонтрагента = Справочники.БанковскиеСчета.СоздатьЭлемент();
	БанковскийСчетКонтрагента.Заполнить(ДанныеРасчетногоСчета);
	
	Попытка
		БанковскийСчетКонтрагента.Записать();
		СтруктураРеквизитов.Вставить("СчетКонтрагента", БанковскийСчетКонтрагента.Ссылка);
	Исключение
		ЕстьОшибки = Истина;
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Запись нового элемента справочника Банковские счета'"),
			УровеньЖурналаРегистрации.Ошибка,
			БанковскийСчетКонтрагента.Метаданные(),
			БанковскийСчетКонтрагента,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ТекстОшибки = НСтр("ru = 'Ошибка при записи нового элемента справочника Банковские счета
			|Подробности см. в Журнале регистрации.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьТабличныйДокумент(АдресФайла, РасширениеФайла)
	
	ФоновоеЗадание = ЗапуститьФоновоеЧтениеТД(АдресФайла, РасширениеФайла);
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = истина;
	
	Обработчик = Новый ОписаниеОповещения("ПослеЧтенияТД", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, Обработчик, НастройкиОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновоеЧтениеТД(АдресФайла, РасширениеФайла) Экспорт

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Чтение табличного документа'");
	
	АдресХранилищаДанныеТабличногоДокумента = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		"ЭлектронноеВзаимодействиеБП.ПолучитьДанныеВФорматеБЭД",
		ПолучитьИзВременногоХранилища(АдресФайла),
		РасширениеФайла,
		АдресХранилищаДанныеТабличногоДокумента);
		
	Возврат ФоновоеЗадание;
	
КонецФункции

&НаКлиенте
Процедура ПослеЧтенияТД(Задание, ДопПараметры) Экспорт
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Задание.Статус = "Выполнено" Тогда
		
		НачатьЗагрузкуПоступлениеТоваровУслугИзЭД(АдресХранилищаДанныеТабличногоДокумента);
		
	Иначе
		ПоказатьПредупреждение(, Задание.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗагрузкуПоступлениеТоваровУслугИзЭД(АдресХранилища)
	
	ДанныеЭД = ЭлектронноеВзаимодействиеБПВызовСервера.РазобратьПолученныеДанные(АдресХранилища, УникальныйИдентификатор);
	Если ДанныеЭД.Свойство("ТекстОшибки") Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ДанныеЭД.ТекстОшибки);
		Возврат;
		
	КонецЕсли;
	
	АдресаФайловXML = Новый Массив;
	АдресаФайловXML.Добавить(ДанныеЭД.АдресХранилища);
	СтруктураЭД = ЭлектронноеВзаимодействиеБПВызовСервера.ПолучитьКонтрагентаИДанныеДокумента(АдресаФайловXML, УникальныйИдентификатор);
	Если ДанныеЭД.Свойство("НомерСФ") Тогда
		СтруктураЭД.Вставить("НомерСФ", ДанныеЭД.НомерСФ);
		СтруктураЭД.Вставить("ДатаСФ", ДанныеЭД.ДатаСФ);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтруктураЭД", СтруктураЭД);
	ОбработчикОповещения = Новый ОписаниеОповещения("СопоставитьПередЗаполнениемОповещение",
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	СписокНеСопоставленнойНоменклатуры = ЭлектронноеВзаимодействиеБПВызовСервера.СписокНеСопоставленнойНоменклатуры(
		СтруктураЭД.Контрагент, СтруктураЭД.ДанныеФайлаРазбора);
		
	Если СписокНеСопоставленнойНоменклатуры.Количество() > 0 Тогда
		СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьСопоставлениеНоменклатуры(
			СписокНеСопоставленнойНоменклатуры,, ОбработчикОповещения);
	Иначе
		СоздатьПоступлениеТовароУслугИзЭД(СтруктураЭД);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СопоставитьПередЗаполнениемОповещение(Результат, ДополнительныеПараметры) Экспорт
	
	СоздатьПоступлениеТовароУслугИзЭД(ДополнительныеПараметры.СтруктураЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоступлениеТовароУслугИзЭД(СтруктураЭД)
	
	СсылкаДокумента = ЭлектронноеВзаимодействиеБПВызовСервера.СоздатьДокументИзЭД(СтруктураЭД);
	Если Не ЗначениеЗаполнено(СсылкаДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураЭД.Свойство("НомерСФ") Тогда
		ПараметрыСозданияСчетаФактуры = УчетНДСКлиентСервер.НовыеПараметрыСозданияПолученногоСчетаФактуры();
		ПараметрыСозданияСчетаФактуры.Основание = СсылкаДокумента;
		ПараметрыСозданияСчетаФактуры.НомерСчетаФактурыПолученного = СтруктураЭД.НомерСФ;
		ПараметрыСозданияСчетаФактуры.ДатаСчетаФактурыПолученного  = СтруктураЭД.ДатаСФ;
		УчетНДСВызовСервера.СоздатьСчетФактуруПолученныйНаОсновании(ПараметрыСозданияСчетаФактуры);
	КонецЕсли;
	
	Если СоздаватьПоступлениеТоваровУслуг("ПоступлениеТоваровУслуг") Тогда
		Оповестить("ЗагруженДокументПоступления");
	Иначе
		ОповеститьОбИзменении(СсылкаДокумента);
	КонецЕсли;
	
	ПоказатьЗначение(, СсылкаДокумента);
	
	ДопПараметры = Элементы.ДанныеЭлектронныхДокументов.ТекущаяСтрока;
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗагрузкаПросмотрЭлектронногоДокументаЗакрытие", ЭтотОбъект, ДопПараметры);
	ВыполнитьОбработкуОповещения(ОповещениеОЗакрытии, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаПросмотрЭлектронногоДокументаЗакрытие(Результат, ВыбраннаяСтрока) Экспорт
	
	Если РежимЕдиничногоЭлектронногоДокумента Тогда
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
	Иначе
		Если ВыбраннаяСтрока <> Неопределено Тогда
			ДанныеСтроки = ДанныеЭлектронныхДокументов.НайтиПоИдентификатору(ВыбраннаяСтрока);
			ДанныеСтроки.Выполнено = Истина;
		КонецЕсли;
		КоличествоЭлектронныеДокументы = КоличествоЭлектронныеДокументы(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
	КонецЕСли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СоздаватьПлатежноеПоручение(ИмяСоздаваемогоДокумента)
	
	Возврат ВРег(ИмяСоздаваемогоДокумента) = ВРег("ПлатежноеПоручение");
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция СоздаватьСчетПоставщику(ИмяСоздаваемогоДокумента)
	
	Возврат ВРег(ИмяСоздаваемогоДокумента) = ВРег("СчетНаОплатуПоставщика");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СоздаватьПоступлениеТоваровУслуг(ИмяСоздаваемогоДокумента)
	
	Возврат ВРег(ИмяСоздаваемогоДокумента) = ВРег("ПоступлениеТоваровУслуг");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ШагиПомощника()
	
	ШагиПомощника = Новый Структура();
	ШагиПомощника.Вставить("ПодборФайлов", "ПодборФайлов");
	ШагиПомощника.Вставить("ЗагрузкаИзПочты", "ЗагрузкаИзПочты");
	ШагиПомощника.Вставить("СозданиеИзЭлектронныхДокументов", "СозданиеИзЭлектронныхДокументов");
	ШагиПомощника.Вставить("ПроверитьПередРаспознаванием", "ПроверитьПередРаспознаванием");
	ШагиПомощника.Вставить("ОтправитьНаРаспознавание", "ОтправитьНаРаспознавание");
	
	Возврат ШагиПомощника;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	
	ТС = Элементы.Файлы.ТекущаяСтрока;
	СтрокаФайлы = Файлы.НайтиПоИдентификатору(ТС);
	
	Если СтрокаФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьФайл(СтрокаФайлы.Адрес, , СтрокаФайлы.Наименование);
	
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьФайлИзПочты(Команда)
	ТС = Элементы.ПисьмаСДокументами.ТекущаяСтрока;
	СтрокаФайлы = ПисьмаСДокументами.НайтиПоИдентификатору(ТС);
	
	Если СтрокаФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьФайл(СтрокаФайлы.Адрес, , СтрокаФайлы.Наименование);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлРаспознавание(Команда)
	ТС = Элементы.ДокументыНаРаспознавание.ТекущаяСтрока;
	СтрокаФайлы = ДокументыНаРаспознавание.НайтиПоИдентификатору(ТС);
	
	Если СтрокаФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьФайл(СтрокаФайлы.Адрес, , СтрокаФайлы.Наименование);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьБаннерНаФорме(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Не Форма.ТребуетсяБаннерЭДО Тогда
		Элементы.Баннер.Видимость = Ложь;
	Иначе
		Элементы.Баннер.Видимость      = Истина;
		Элементы.ТекстБаннера.Шрифт    = Новый Шрифт(ШрифтыСтиля.ШрифтТекстаБаннера, , 2);
		Элементы.БаннерСФоном.ЦветФона = ЦветаСтиля.ЦветФонаБаннерЭДО;
		Элементы.КартинкаБаннера.Картинка   = БиблиотекаКартинок.ЛоготипЭДО;
		
		НавигационнаяСсылка = "https://edo.1c.ru/";
		
		ТекстЗаголовка = ПерсонализированныеПредложенияСервисов.НовыйЗаголовок(НСтр("ru='Устали загружать электронные документы вручную?'"));
	
		ТекстПодзаголовка = Новый ФорматированнаяСтрока(
			ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(НСтр("ru='Узнайте, как'")),
			ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(" "),
			ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(НСтр("ru='получать документы контрагентов автоматически'"), НавигационнаяСсылка));
		Форма.ТекстБаннера = ПерсонализированныеПредложенияСервисов.НовыйТекстБаннера(ТекстЗаголовка, ТекстПодзаголовка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
