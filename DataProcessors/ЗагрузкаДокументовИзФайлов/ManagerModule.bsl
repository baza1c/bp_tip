#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПрочитатьДокументыИзПочты(НастройкиЧтенияПочты, АдресХранилищаРезультата) Экспорт
	
	ПрочитанныеПисьма = ЗагрузкаПочтовыхСообщений.ПрочитатьПисьма(НастройкиЧтенияПочты);
	
	Если ПрочитанныеПисьма = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = НастройкиЧтенияПочты.Отбор;
	
	Письма = Новый Массив;
	Для Каждого Письмо ИЗ ПрочитанныеПисьма Цикл
		
		Если Письмо.ДатаОтправления >= Отбор.ДатаОкончания
			Или Письмо.ДатаОтправления < Отбор.ДатаНачала Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Вложение ИЗ Письмо.Вложения Цикл
			
			РасширениеИмениФайла = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(Вложение.Ключ);
			
			НоваяСтрока = НовыйОписаниеПисьма();
			НоваяСтрока.Наименование = Вложение.Ключ;
			НоваяСтрока.Расширение = РасширениеИмениФайла;
			НоваяСтрока.ИдентификаторПисьма = Письмо.ИдентификаторСообщения;
			НоваяСтрока.Дата = Письмо.ДатаОтправления;
			НоваяСтрока.Отправитель = Письмо.Отправитель;
			НоваяСтрока.ДанныеФайла = Вложение.Значение; //Двоичные данные
			
			Письма.Добавить(НоваяСтрока);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(Письма, АдресХранилищаРезультата);
	
КонецПроцедуры

Процедура РазобратьДокументы(ПараметрыРазбора, АдресХранилищаРезультата) Экспорт
	
	ФайлыДляОбработки = ПараметрыРазбора.ФайлыДляОбработки;
	ВидМаркетплейса = ПараметрыРазбора.ВидМаркетплейса;
	РазрешеноЗагружатьЭлектронныеТаблицы = ПараметрыРазбора.РазрешеноЗагружатьЭлектронныеТаблицы;
	//Для платежного поручения доступна только загрузка из файла контейнера Счета в формате 1С
	ЭтоПлатежноеПоручение = ПараметрыРазбора.ЭтоПлатежноеПоручение;
	
	РасширенияЭлектронныхТаблиц = "mxl"
		+ ?(РазрешеноЗагружатьЭлектронныеТаблицы, ";xls;xlsx", "");
	
	ФайлыДляОбработки.Колонки.Добавить("ИсходноеНаименование", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ФайлыДляОбработки.Колонки.Добавить("ОписаниеОшибки", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ФайлыДляОбработки.Колонки.Добавить("ДанныеЭД");
	
	СтрокиКУдалению = Новый Массив;
	Для Каждого Файл ИЗ ФайлыДляОбработки Цикл
		
		РасширениеИмениФайла = НРег(Файл.Расширение);
		ЭтоКонтейнерЭД = Ложь;
		
		РезультатОбработки = Неопределено;
		Если РасширениеИмениФайла = "zip" Тогда
			РезультатОбработки = ОбработатьАрхивZip(Файл, ВидМаркетплейса, ЭтоПлатежноеПоручение);
		
			Если ЗначениеЗаполнено(РезультатОбработки) Тогда
				Если ЗначениеЗаполнено(РезультатОбработки.ОписаниеОшибки) Тогда
					//Все ошибки покажем на странице электронных документов
					Файл.ОписаниеОшибки = РезультатОбработки.ОписаниеОшибки;
					Продолжить;
				ИначеЕсли РезультатОбработки.ЭтоКонтейнерЭД Тогда
					ЭтоКонтейнерЭД = Истина;
				ИначеЕсли ЗначениеЗаполнено(РезультатОбработки.АрхивРазобранФайлы) Тогда
					ДополнитьСведенияОФайлахПослеОбработки(ФайлыДляОбработки, Файл, РезультатОбработки, СтрокиКУдалению);
					//Расширение может измениться после разбора файла
					РасширениеИмениФайла = НРег(Файл.Расширение);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрНайти(РасширенияЭлектронныхТаблиц, РасширениеИмениФайла) > 0 Тогда
			ЗаполнитьДанныеЭДПоЭлектроннойТаблице(Файл);
		ИначеЕсли ЭтоКонтейнерЭД Или РасширениеИмениФайла = "xml" Тогда
			ЗаполнитьДанныеЭД(Файл, ЭтоКонтейнерЭД);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из СтрокиКУдалению Цикл
		ФайлыДляОбработки.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ФайлыДляОбработки, АдресХранилищаРезультата);
	
КонецПроцедуры

Процедура ЗаполнитьДанныеЭДПоЭлектроннойТаблице(Файл)
	
	ПолученныеДанные = ЭлектронноеВзаимодействиеБП.ПолучитьСодержимоеДокументаРеализацииВФорматеБЭД(Файл.ДвоичныеДанныеФайла, Файл.Расширение);
	
	Если ЗначениеЗаполнено(ПолученныеДанные.ОписаниеОшибки) Тогда
		Файл.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Ошибка загрузки ЭД %1. Причина: %2",
			Файл.Наименование,
			ПолученныеДанные.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
		
	Содержание = ПолученныеДанные.Содержание;
		
	ДанныеЭД = НовыйДанныеЭД();
	ДанныеЭД.ТипДокумента = Содержание.ТипДокумента;
	ДанныеЭД.КонтрагентПредставление = Содержание.ОтправительНаименование;
	ДанныеЭД.НомерДокумента = Содержание.НомерДокумента;
	ДанныеЭД.ДатаДокумента = Содержание.ДатаДокумента;
	ДанныеЭД.СуммаДокумента = Содержание.СуммаДокумента;
	ДанныеЭД.ЭтоЭлектроннаяТаблица = Истина;
	
	Файл.ДанныеЭД = ДанныеЭД;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеЭД(Файл, ЭтоКонтейнерЭД)
	
	ОписаниеФайла = РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла();
	ОписаниеФайла.ДвоичныеДанные = Файл.ДвоичныеДанныеФайла;
	ОписаниеФайла.ИмяФайла = Файл.Наименование;
	
	Если ЭтоКонтейнерЭД Тогда
		Результат = ПолучитьДанныеИзКонтейнераЭД(ОписаниеФайла);
		Если Не ЗначениеЗаполнено(Результат.Содержание) Тогда
			Файл.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"Ошибка загрузки ЭД %1. Причина: загрузка данного формата ЭД не поддерживается",
				Файл.Наименование);
			Возврат;
		ИначеЕсли ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда
			Файл.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"Ошибка загрузки ЭД %1. Причина: %2",
				Файл.Наименование,
				Результат.ОписаниеОшибки);
			Возврат;
		КонецЕсли;
		
		Содержание = Результат.Содержание;
		
	ИначеЕсли НРег(Файл.Расширение) = "xml" Тогда
		Содержание = ЭлектронныеДокументыЭДО.СодержаниеСообщения(ОписаниеФайла);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Содержание)
		И ЭлектронныеДокументыЭДО.ЗагрузкаФорматаПоддерживается(Содержание) Тогда
		ДанныеЭД = НовыйДанныеЭД();
		ДанныеЭД.ТипДокумента = Содержание.ТипДокумента;
		ДанныеЭД.КонтрагентПредставление = Содержание.Отправитель.Наименование;
		ДанныеЭД.НомерДокумента = Содержание.НомерДокумента;
		ДанныеЭД.ДатаДокумента = Содержание.ДатаДокумента;
		ДанныеЭД.СуммаДокумента = Содержание.СуммаДокумента;
		
		Файл.ДанныеЭД = ДанныеЭД;
	Иначе
		Файл.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Ошибка загрузки ЭД %1. Причина: загрузка данного формата ЭД не поддерживается",
			Файл.Наименование);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьСведенияОФайлахПослеОбработки(ФайлыДляОбработки, Файл, СодержимоеАрхива, СтрокиКУдалению)
	
	ИсходноеНаименование = Файл.Наименование;
	
	НовыхФайлов = СодержимоеАрхива.АрхивРазобранФайлы.Количество();
	Если НовыхФайлов = 1 Тогда
		//Затрём первым файлом, т.к. чаще файл один
		
		ЗаполнитьЗначенияСвойств(Файл, СодержимоеАрхива.АрхивРазобранФайлы[0]);
		Файл.ИсходныйАдрес = "";
		Файл.ИсходноеНаименование = ИсходноеНаименование;
	Иначе
		СтрокиКУдалению.Добавить(Файл);
		//Остальные файлы - в конец
		Для каждого ДобавленныйФайл Из СодержимоеАрхива.АрхивРазобранФайлы Цикл
			НоваяСтрока = ФайлыДляОбработки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДобавленныйФайл);
			НоваяСтрока.ИсходноеНаименование = ИсходноеНаименование;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция НовыйОписаниеРазобранногоФайла()
	
	ОписаниеРазобранногоФайла = Новый Структура;
	ОписаниеРазобранногоФайла.Вставить("ДвоичныеДанныеФайла");
	ОписаниеРазобранногоФайла.Вставить("Наименование");
	ОписаниеРазобранногоФайла.Вставить("Расширение");
	
	Возврат ОписаниеРазобранногоФайла;
	
КонецФункции

Процедура ЗакрытьЧтениеАрхива(РезультатЧтенияАрхива)
	
	Если Не ПустаяСтрока(РезультатЧтенияАрхива.ВременныйКаталог) Тогда
		ФайловаяСистема.УдалитьВременныйКаталог(РезультатЧтенияАрхива.ВременныйКаталог);
	КонецЕсли;
	Если РезультатЧтенияАрхива.ЧтениеАрхива <> Неопределено Тогда
		РезультатЧтенияАрхива.ЧтениеАрхива.Закрыть();
	КонецЕсли;
	Если РезультатЧтенияАрхива.ПотокЧтения <> Неопределено Тогда
		РезультатЧтенияАрхива.ПотокЧтения.Закрыть();
	КонецЕсли;
	
	РезультатЧтенияАрхива = НовыйРезультатЧтенияАрхива();
	
КонецПроцедуры

Функция ИзвлечьФайлИзАрхива(ЧтениеАрхива, Файл, ВременныйКаталог)
	
	ЧтениеАрхива.Извлечь(Файл, ВременныйКаталог, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	
	ИзвлеченныеФайлы = НайтиФайлы(ВременныйКаталог, Файл.Имя, Ложь);
	
	Для Каждого ИзвлеченныйФайл Из ИзвлеченныеФайлы Цикл
		Если Не ИзвлеченныйФайл.ЭтоФайл() Тогда
			Продолжить;
		КонецЕсли;
		РаспакованныйФайл = НовыйОписаниеРазобранногоФайла();
		РаспакованныйФайл.Наименование = ИзвлеченныйФайл.Имя;
		РаспакованныйФайл.ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИзвлеченныйФайл.ПолноеИмя);
		РаспакованныйФайл.Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ИзвлеченныйФайл.Расширение);
		
		Возврат РаспакованныйФайл;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция НовыйРезультатЧтенияАрхива()
	
	РезультатЧтения = Новый Структура();
	РезультатЧтения.Вставить("ЧтениеАрхива");
	РезультатЧтения.Вставить("ПотокЧтения");
	РезультатЧтения.Вставить("ВременныйКаталог", "");
	РезультатЧтения.Вставить("ОписаниеОшибки", "");
	
	Возврат РезультатЧтения;
	
КонецФункции

Функция ПрочитатьАрхив(ОписаниеФайла)
	
	РезультатЧтения = НовыйРезультатЧтенияАрхива();
	
	Попытка
		Поток = ОписаниеФайла.ДвоичныеДанныеФайла.ОткрытьПотокДляЧтения();
	Исключение
		ОписаниеОшибки = НСтр("ru = 'Не удалось прочитать вложенный ZIP-архив'");
		Возврат РезультатЧтения;
	КонецПопытки;
	РезультатЧтения.ПотокЧтения = Поток;
	
	ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог("bp_extract_zip");
	РезультатЧтения.ВременныйКаталог = ВременныйКаталог;
	
	Попытка
		ЧтениеАрхива = Новый ЧтениеZipФайла(Поток);
	Исключение
		РезультатЧтения.ПотокЧтения.Закрыть();
		РезультатЧтения.ОписаниеОшибки = НСтр("ru = 'Не удалось прочитать архив zip'");
		Возврат РезультатЧтения;
	КонецПопытки;
	
	РезультатЧтения.ЧтениеАрхива = ЧтениеАрхива;
	
	//Особо обработаем архив в архиве
	Если ЧтениеАрхива.Элементы.Количество() = 1
		И НРег(ЧтениеАрхива.Элементы[0].ИсходноеРасширение) = "zip" Тогда
		
		Если ЧтениеАрхива.Элементы[0].Зашифрован Тогда
			
			РезультатЧтения.ОписаниеОшибки = НСтр("ru = 'ZIP-архив не должен быть зашифрован'");
			Возврат РезультатЧтения;
			
		Иначе
			РаспакованныйФайл = ИзвлечьФайлИзАрхива(ЧтениеАрхива, ЧтениеАрхива.Элементы[0], ВременныйКаталог);
			
			Если РаспакованныйФайл = Неопределено Тогда
				РезультатЧтения.ОписаниеОшибки = НСтр("ru = 'Не удалось распакавать вложенный ZIP-архив'");
				Возврат РезультатЧтения;
			Иначе
				ЗакрытьЧтениеАрхива(РезультатЧтения);
				Возврат ПрочитатьАрхив(РаспакованныйФайл);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РезультатЧтения;
	
КонецФункции

Функция ОбработатьАрхивZip(ОписаниеФайла, ВидМаркетплейса, ЭтоПлатежноеПоручение)
	
	Результат = Новый Структура;
	Результат.Вставить("ОписаниеОшибки", "");
	Результат.Вставить("ЭтоКонтейнерЭД", Ложь);
	Результат.Вставить("АрхивРазобранФайлы", Новый Массив);
	
	РезультатЧтенияАрхива = ПрочитатьАрхив(ОписаниеФайла);
	
	Если Не ПустаяСтрока(РезультатЧтенияАрхива.ОписаниеОшибки) Тогда
		Результат.ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Ошибка: %1. При чтении файла: %2'"),
			РезультатЧтенияАрхива.ОписаниеОшибки,
			ОписаниеФайла.Наименование);
		ЗакрытьЧтениеАрхива(РезультатЧтенияАрхива);
		Возврат Результат;
	ИначеЕсли РезультатЧтенияАрхива.ЧтениеАрхива = Неопределено Тогда 
		ЗакрытьЧтениеАрхива(РезультатЧтенияАрхива);
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Необработанная ошибка при чтении архива %1'"),
				ОписаниеФайла.Наименование);
	КонецЕсли;
	
	ОписаниеАрхива = Новый Структура;
	ОписаниеАрхива.Вставить("ИмяАрхива", ОписаниеФайла.Наименование);
	ОписаниеАрхива.Вставить("ЕстьКарточкаКонтейнера", Ложь);
	ОписаниеАрхива.Вставить("ЕстьМетаданныеКонтейнера", Ложь);
	ОписаниеАрхива.Вставить("ЕстьФайлЭД", Ложь);
	ОписаниеАрхива.Вставить("ФайлЭД", Неопределено);
	ОписаниеАрхива.Вставить("ПечатныеФормыЭД", Новый Массив);
	
	//У ЯМаркет 2 файла в архиве из ЛК, с именами _act_ и _inv_
	ЭтоМаркетплейс = ЗначениеЗаполнено(ВидМаркетплейса);
	ЭтоЯндексМаркет = (ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	ЭтоOZON = (ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.OZON);
	
	Если РезультатЧтенияАрхива.ЧтениеАрхива.Элементы.Количество() > 20 Тогда
		ЗакрытьЧтениеАрхива(РезультатЧтенияАрхива);
		Возврат Результат;
	КонецЕсли;
	
	//На linux-based серверах имя файла OZON может быть прочтено в неправильной кодировке
	//Если архив содержит xlsx и это указано в имени, то получим визуальное представление из xlsx.
	ЗагружатьXLSX = Ложь;
	Если ЭтоOZON
		И СтрНайти(ОписаниеФайла.Наименование, "xlsx") > 0 Тогда
		ЗагружатьXLSX = Истина;
	КонецЕсли;
	
	Для Каждого ЭлементАрхива Из РезультатЧтенияАрхива.ЧтениеАрхива.Элементы Цикл
		
		Если Нрег(ЭлементАрхива.ИсходноеРасширение) = "xml" Тогда
			
			Дополнение = "";
			РегулярноеВыражение = "^(ON_NSCHFDOPPR|ON_NKORSCHFDOPPR@Дополнение)(.*)"; //УПД|УКД
			Если Не ЭтоПлатежноеПоручение Тогда
				Дополнение = "|ON_CHETOP|Schet\sna\soplatu";
			КонецЕсли;
			ПодстрокаПоиска = СтрЗаменить(РегулярноеВыражение, "@Дополнение", Дополнение);
			
			ЭтоПоддерживаемыйФорматЭД = СтрПодобнаПоРегулярномуВыражению(
				ЭлементАрхива.ИсходноеИмяБезРасширения,
				ПодстрокаПоиска,
				Истина); 
			Если ЭтоПоддерживаемыйФорматЭД Тогда
				ОписаниеАрхива.ФайлЭД = ЭлементАрхива;
				ОписаниеАрхива.ЕстьФайлЭД = Истина;
				Прервать;
			ИначеЕсли Нрег(Лев(ЭлементАрхива.ИсходноеИмяБезРасширения, 13)) = "card" Тогда //Файлы контейнера ЭД
				ОписаниеАрхива.ЕстьКарточкаКонтейнера = Истина;
				Продолжить;
			ИначеЕсли Нрег(Лев(ЭлементАрхива.ИсходноеИмяБезРасширения, 13)) = "meta" Тогда //Файлы контейнера ЭД
				ОписаниеАрхива.ЕстьМетаданныеКонтейнера = Истина;
				Продолжить;
			КонецЕсли;
			
		ИначеЕсли ЭтоМаркетплейс
			И Нрег(ЭлементАрхива.ИсходноеРасширение) = "pdf"
			И Не ЗагружатьXLSX Тогда
			
			Дополнение = "";
			РегулярноеВыражение = "(.*)(УПД|УКД|Печатная\sформа"
			    +"|Универсальный\sпередаточный\sдокумент"
				+"|Универсальный\sпередаточный\sдокумент"
				+"|Универсальный\sкорректировочный\sдокумент@Дополнение)(.*)";
			Если ЭтоЯндексМаркет Тогда
				Дополнение = "|_inv_";
			КонецЕсли;
			
			ПодстрокаПоиска = СтрЗаменить(РегулярноеВыражение, "@Дополнение", Дополнение);
			
			//Исключение: на linux-based серверах имя файла может быть прочтено в неправильной кодировке
			//Такие файлы не будут найдены и извлечены
			ЭтоВизуальноеПредставлениеДокумента = СтрПодобнаПоРегулярномуВыражению(
				ЭлементАрхива.ИсходноеИмяБезРасширения,
				ПодстрокаПоиска,
				Истина);
			Если ЭтоВизуальноеПредставлениеДокумента Тогда
				//Визуальные представления можно выделить из архива ЭД и распознать
				ОписаниеАрхива.ПечатныеФормыЭД.Добавить(ЭлементАрхива);
			КонецЕсли;
		ИначеЕсли ЗагружатьXLSX И Нрег(ЭлементАрхива.ИсходноеРасширение) = "xlsx" Тогда
			ОписаниеАрхива.ПечатныеФормыЭД.Добавить(ЭлементАрхива);
		КонецЕсли;
		
	КонецЦикла;
	
	//Обрабатыаем что получилось
	Если ОписаниеАрхива.ЕстьФайлЭД Тогда
		РаспакованныйФайл = ИзвлечьФайлИзАрхива(РезультатЧтенияАрхива.ЧтениеАрхива,
			ОписаниеАрхива.ФайлЭД,
			РезультатЧтенияАрхива.ВременныйКаталог);
		Если РаспакованныйФайл <> Неопределено Тогда
			Результат.АрхивРазобранФайлы.Добавить(РаспакованныйФайл);
		КонецЕсли;
	ИначеЕсли ЭтоПлатежноеПоручение И ОписаниеАрхива.ЕстьКарточкаКонтейнера И ОписаниеАрхива.ЕстьМетаданныеКонтейнера Тогда
		//Поддерживаем для контейнера Счета в формате 1С
		Результат.ЭтоКонтейнерЭД = Истина;
	ИначеЕсли ОписаниеАрхива.ПечатныеФормыЭД.Количество() > 0 Тогда
		
		Для Каждого ПечатнаяФормаЭД Из ОписаниеАрхива.ПечатныеФормыЭД Цикл
			
			РаспакованныйФайл = ИзвлечьФайлИзАрхива(РезультатЧтенияАрхива.ЧтениеАрхива,
				ПечатнаяФормаЭД,
				РезультатЧтенияАрхива.ВременныйКаталог);
			
			Если РаспакованныйФайл = Неопределено Тогда
				Результат.АрхивРазобранФайлы.Очистить();
				Прервать;
			КонецЕсли;
			
			Результат.АрхивРазобранФайлы.Добавить(РаспакованныйФайл);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗакрытьЧтениеАрхива(РезультатЧтенияАрхива);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ПолучитьДанныеИзКонтейнераЭД(Файл)
	
	Результат = Новый Структура;
	Результат.Вставить("Содержание");
	Результат.Вставить("ОписаниеОшибки", "");
	
	Попытка
		ДанныеКонтейнера = ОбменСКонтрагентами.ОписаниеКонтейнераЭлектронногоДокумента(Файл.ДвоичныеДанные);
		Результат.Содержание = ДанныеКонтейнера.Содержание;
		
	Исключение
		Результат.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Попытка разбора ЭД %1 завершилась неудачей. Структура данных отличается от ожидаемой.",
			Файл.ИмяФайла);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция НовыйОписаниеПисьма()
	
	Описание = Новый Структура;
	Описание.Вставить("Наименование", "");
	Описание.Вставить("Расширение", "");
	Описание.Вставить("ИдентификаторПисьма", "");
	Описание.Вставить("Дата","00010101");
	Описание.Вставить("Отправитель", "");
	Описание.Вставить("ДанныеФайла");
	
	Возврат Описание;
	
КонецФункции

Функция НовыйДанныеЭД()
	
	ДанныеЭД = Новый Структура;
	ДанныеЭД.Вставить("ТипДокумента");
	ДанныеЭД.Вставить("ЭтоЭлектроннаяТаблица", Ложь);
	ДанныеЭД.Вставить("ДатаДокумента", Дата(1,1,1));
	ДанныеЭД.Вставить("НомерДокумента", "");
	ДанныеЭД.Вставить("КонтрагентПредставление");
	ДанныеЭД.Вставить("СуммаДокумента", 0);

	Возврат ДанныеЭД;
	
КонецФункции

#КонецЕсли