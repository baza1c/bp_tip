#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПодборНМА", "");
	Если Настройки <> Неопределено Тогда
		Если Настройки.Свойство("ИсторияПоискаНМА") Тогда
			Элементы.СтрокаПоиска.СписокВыбора.ЗагрузитьЗначения(Настройки.ИсторияПоискаНМА);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Отбор) = Тип("Структура") Тогда
		
		Для Каждого ЭлементОтбора Из Параметры.Отбор Цикл
			ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ПодборТаблица,
				ЭлементОтбора.Ключ, ЭлементОтбора.Значение, Истина);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
	КонецЕсли;

	Если ЭтоАдресВременногоХранилища(Параметры.АдресВХранилище) Тогда
		НМА = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
		Объект.Корзина.Загрузить(НМА);
	КонецЕсли;

	СформироватьИнформационнуюНадпись(ЭтотОбъект);

	ТекущаяГруппа = Справочники.НематериальныеАктивы.ПустаяСсылка();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И ДобавленыНовые Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если НЕ ПеренестиВДокумент И ДобавленыНовые Тогда

		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ВопросПеренестиВДокументЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Подобранные нематериальные активы не перенесены в документ.
			|
			|Перенести?'"), РежимДиалогаВопрос.ДаНетОтмена);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ИсторияПоискаНМА", Элементы.СтрокаПоиска.СписокВыбора.ВыгрузитьЗначения());

	ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекСохранить("ПодборНМА", "", ПараметрыЗакрытия);

	Если ПеренестиВДокумент Тогда
		АдресВХранилище = ПоместитьВХранилище();
		Структура = Новый Структура("АдресВХранилище", АдресВХранилище);
		ПеренестиВДокумент = Истина;
		ОповеститьОВыборе(Структура);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаОчистка(Элемент, СтандартнаяОбработка)

	ПрименитьПоиск();

КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)

	ПрименитьПоиск();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИерархия

&НаКлиенте
Процедура ИерархияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	УстановитьОтборПоИерархии(ВыбраннаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ИерархияПриАктивизацииСтроки(Элемент)

	ТекущаяСтрока = Элементы.Иерархия.ТекущаяСтрока;
	Если ТекущаяСтрока <> ТекущаяГруппа Тогда
		ТекущаяГруппа = ТекущаяСтрока;
		УстановитьОтборПоИерархии(ТекущаяСтрока);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодборТаблица

&НаКлиенте
Процедура ПодборТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Если Элемент.ТекущиеДанные <> Неопределено И НЕ Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		СтруктураПараметры = Новый Структура;
		СтруктураПараметры.Вставить("НематериальныйАктив", Элемент.ТекущиеДанные.Ссылка);
		ДобавитьВКорзину(СтруктураПараметры, 1);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборТаблицаНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	// На практике, таблица формы предоставляет Массив.
	// При этом для платформенной логики проверки возможности перетаскивания важно, чтобы в Значение остался этот тип.
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	// Для того, чтобы элемент-получатель перетаскивания смог идентифицировать элемент-источник значения,
	// обернем значение в контейнер
	ЗначенияПеретаскивания = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПараметрыПеретаскивания.Значение);
	ПараметрыПеретаскивания.Значение.Очистить();
	
	КонтейнерЗначений = Новый Структура;
	КонтейнерЗначений.Вставить("Источник", Элемент.Имя);
	КонтейнерЗначений.Вставить("Значения", ЗначенияПеретаскивания);
	
	ПараметрыПеретаскивания.Значение.Добавить(КонтейнерЗначений);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКорзина

&НаКлиенте
Процедура КорзинаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
			// Ожидаем перетаскивание из таблицы формы "ПодборТаблица".
			// На практике, таблица формы предоставляет Массив.
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ПараметрыПеретаскивания.Значение) Тогда
		Возврат;
	КонецЕсли;

	КонтейнерЗначений = ПараметрыПеретаскивания.Значение[0];
	Если ТипЗнч(КонтейнерЗначений) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	Если Не КонтейнерЗначений.Свойство("Источник") Или Не КонтейнерЗначений.Свойство("Значения") Тогда
		Возврат;
	КонецЕсли;

	Если КонтейнерЗначений.Источник <> "ПодборТаблица" Тогда
		Возврат;
	КонецЕсли;

	Для Каждого ВыбранноеЗначение Из КонтейнерЗначений.Значения Цикл

		СтруктураПараметры = Новый Структура;
		СтруктураПараметры.Вставить("НематериальныйАктив", ВыбранноеЗначение);
		ДобавитьВКорзину(СтруктураПараметры, 1);

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура КорзинаПослеУдаления(Элемент)
	СформироватьИнформационнуюНадпись(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)

	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьИнформационнуюНадпись(Форма)

	ШаблонНадписи = НСтр("ru = 'Всего подобрано %1 позиции(ия)'");
	Форма.ИнформационнаяНадпись = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНадписи, Форма.Объект.Корзина.Количество());

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоИерархии(Группа)

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ПодборТаблица, "Ссылка",
		Группа, ЗначениеЗаполнено(Группа), ВидСравненияКомпоновкиДанных.ВИерархии);

КонецПроцедуры

&НаКлиенте
Процедура ПрименитьПоиск()

	Использование = ЗначениеЗаполнено(СтрокаПоиска);

	ГруппаОтбора = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ПодборТаблица.Отбор.Элементы, "ПоискПоПодстроке",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "Наименование", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "НаименованиеПолное", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "Код", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);

	СпискиВыбораКлиентСервер.ОбновитьСписокВыбора(Элементы.СтрокаПоиска.СписокВыбора, СтрокаПоиска);
	ОбновитьОтображениеДанных();

КонецПроцедуры

&НаСервере
Функция ПоместитьВХранилище()

	АдресВХранилище = ПоместитьВоВременноеХранилище(Объект.Корзина.Выгрузить(), УникальныйИдентификатор);

	Возврат АдресВХранилище;

КонецФункции

&НаКлиенте
Процедура ДобавитьВКорзину(Параметры, Количество)

	РезультатПоиска = Объект.Корзина.НайтиСтроки(Параметры);
	Если РезультатПоиска.Количество() = 0 Тогда
		ТекущаяСтрока = Объект.Корзина.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Параметры);

		ДобавленыНовые = Истина;
	Иначе
		ТекущаяСтрока = РезультатПоиска[0];
	КонецЕсли;

	// Активизируем текущую строку табличной части
	Элементы.Корзина.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();

	СформироватьИнформационнуюНадпись(ЭтотОбъект);

	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВопросПеренестиВДокументЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		ДобавленыНовые = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти