#Область ОбъявлениеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияАктуализации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Объект.Организация = Параметры.Организация;
	Иначе
		Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ТекущаяДата = ТекущаяДатаСеанса();
	Если Месяц(ТекущаяДата) > 10 Тогда
		Объект.Дата = КонецГода(ТекущаяДата);
	Иначе
		Объект.Дата = НачалоГода(ТекущаяДата) - 1;
	КонецЕсли;
	ЗаполнитьПодсказкуДата();
	
	АктуализацияВозможна = Обработки.ЗакрытиеМесяца.АктуализацияВозможна();
	ЕстьПравоАктуализации = Обработки.ЗакрытиеМесяца.ПравоИзмененияРегламентныхОпераций(Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьРазделыИнвентаризации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьФормуПомощникаИнвентаризации"
		И ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Организация") = Объект.Организация Тогда 
		ПараметрыОбновления = Новый Структура;
		ПараметрыОбновления.Вставить("РазделУчета", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "РазделУчета"));
		ПараметрыОбновления.Вставить("СкладПодраздела", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "СкладПодраздела"));
		ПараметрыОбновления.Вставить("СчетУчетаПодраздела", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "СчетУчетаПодраздела"));
		ПараметрыОбновления.Вставить("ПодразделениеПодраздела", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "ПодразделениеПодраздела"));
		ДлительнаяОперация = НачатьЗаполнениеРазделовИнвентаризации(ПараметрыОбновления);
		ОжидатьЗаполнениеРазделовИнвентаризации(ДлительнаяОперация, Ложь, ПараметрыОбновления);
		
	ИначеЕсли (ИмяСобытия = "АктуализацияЗавершенаУспешно"
		ИЛИ ИмяСобытия = "АктуализацияОтменена") 
		И Параметр.Свойство("Организация")
		И Объект.Организация = Параметр.Организация Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
		УправлениеФормойГруппаАктуализация(ЭтотОбъект, ИмяСобытия); 
		
	ИначеЕсли ИмяСобытия = "ТребуетсяАктуализация"
		И Параметр.Свойство("Организация")
		И Объект.Организация = Параметр.Организация 
		И Параметр.Свойство("ДатаАктуальности") Тогда
		
		ДатаАктуальности = Параметр.ДатаАктуальности;
		УправлениеФормойГруппаАктуализация(ЭтотОбъект, ИмяСобытия, ДатаАктуальности); 
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьРазделыИнвентаризации();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Объект.Дата = КонецМесяца(Объект.Дата);
	ЗаполнитьПодсказкуДата();
	ЗаполнитьРазделыИнвентаризации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналИнвентаризацияНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",  Объект.Организация);
	ОткрытьФорму("ЖурналДокументов.Инвентаризация.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТабличныхЧастей

&НаКлиенте
Процедура РазделыИнвентаризацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.РазделыИнвентаризации.ТекущиеДанные;
	
	Если Поле.Имя = "РазделыИнвентаризацииСуммаПоУчету" Тогда
		ОткрытьФормуРасшифровкиСуммы(ТекущаяСтрока);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущаяСтрока.СуммаПоИнвентаризации) Или ЗначениеЗаполнено(ТекущаяСтрока.ПолучитьЭлементы()) Тогда
		ИмяФормыДокумента = СтрШаблон("Документ.%1.ФормаСписка", ТекущаяСтрока.НаименованиеДокумента);
	Иначе
		Если ТекущаяСтрока.НаименованиеДокумента = "ИнвентаризацияОбъектовУчета" Тогда
			ИмяФормыДокумента = "Документ.ИнвентаризацияОбъектовУчета.Форма.ФормаДокумента";
		Иначе
			ИмяФормыДокумента = СтрШаблон("Документ.%1.ФормаОбъекта", ТекущаяСтрока.НаименованиеДокумента);
		КонецЕсли;
	КонецЕсли;
	ПараметрыФормы = ПараметрыФормы(ТекущаяСтрока.Раздел, ТекущаяСтрока.СкладПодраздела, ТекущаяСтрока.ПодразделениеПодраздела, ТекущаяСтрока.СчетУчетаПодраздела);
	ОткрытьФорму(ИмяФормыДокумента, ПараметрыФормы,ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиДерева = ДеревоРазделы.ПолучитьЭлементы();
	ДатаНачалаОС = НачалоДня(Объект.Дата);
	Если ЗначениеЗаполнено(СтрокиДерева) Тогда
		СтрокаОС = СтрокиДерева[0];
		Если СтрокаОС.Раздел = ПредопределенноеЗначение("Перечисление.РазделыУчетаДляИнвентаризации.ОсновныеСредства") Тогда
			ДатаНачалаОС = НачалоГода(СтрокаОС.ДатаОстатков);
		КонецЕсли;
	КонецЕсли;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Период", Объект.Дата);
	ДополнительныеПараметры.Вставить("ДатаНачалаОС", ДатаНачалаОС);
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПомощникИнвентаризации", "Реестр", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Организация), ЭтотОбъект, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура Актуализировать(Команда)
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	УправлениеФормойГруппаАктуализация(ЭтотОбъект, "ЗапущенаАктуализация");
	
	ПараметрыАктуализации = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыАктуализации();
	ПараметрыАктуализации.Организация                  = Объект.Организация;
	ПараметрыАктуализации.Период                       = КонецДня(Объект.Дата);
	ПараметрыАктуализации.ИдентификаторЗадания         = ИдентификаторЗаданияАктуализации;
	ПараметрыАктуализации.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыАктуализации.АктуализироватьВесьПериод	   = Истина; // актуализация до даты события
	
	РезультатВыполнения = ЗакрытиеМесяцаВызовСервера.АктуализироватьДанные(ПараметрыАктуализации);
	
	АдресХранилищаАктуализации = РезультатВыполнения.АдресРезультата;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ОбработатьРезультатАктуализации();
		ИдентификаторЗаданияАктуализации = Неопределено;
	Иначе
		ИдентификаторЗаданияАктуализации = РезультатВыполнения.ИдентификаторЗадания;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПроверитьВыполнениеАктуализации",
			ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал,
			Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьРазделыИнвентаризации();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

&НаКлиенте
Процедура ЗаполнитьРазделыИнвентаризации(ЗапуститьПроверкуАктуальности = Истина)
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПустая;
	ДлительнаяОперация = НачатьЗаполнениеРазделовИнвентаризации();
	ОжидатьЗаполнениеРазделовИнвентаризации(ДлительнаяОперация, ЗапуститьПроверкуАктуальности);
	
КонецПроцедуры

&НаСервере
Функция НачатьЗаполнениеРазделовИнвентаризации(ПараметрыОбновления = Неопределено)

	ДанныеДерева = РеквизитФормыВзначение("ДеревоРазделы");
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("Организация",             Объект.Организация);
	ПараметрыОперации.Вставить("Дата",                    КонецДня(Объект.Дата));
	ПараметрыОперации.Вставить("ДанныеДерева",            ДанныеДерева);
	ПараметрыОперации.Вставить("РазделУчета",             Неопределено);
	ПараметрыОперации.Вставить("СкладПодраздела",         Неопределено);
	ПараметрыОперации.Вставить("СчетУчетаПодраздела",     Неопределено);
	ПараметрыОперации.Вставить("ПодразделениеПодраздела", Неопределено);
	Если ПараметрыОбновления <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыОперации, ПараметрыОбновления);
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение разделов помощника инвентаризации'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Обработки.ПомощникИнвентаризации.РазделыИнвентаризации",
		ПараметрыОперации);

КонецФункции

&НаКлиенте
Процедура ОжидатьЗаполнениеРазделовИнвентаризации(ДлительнаяОперация, ЗапуститьПроверкуАктуальности, ПараметрыОбновления = Неопределено)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗапуститьПроверкуАктуальности", ЗапуститьПроверкуАктуальности);
	ДополнительныеПараметры.Вставить("РазделУчета",             Неопределено);
	ДополнительныеПараметры.Вставить("СкладПодраздела",         Неопределено);
	ДополнительныеПараметры.Вставить("СчетУчетаПодраздела",     Неопределено);
	ДополнительныеПараметры.Вставить("ПодразделениеПодраздела", Неопределено);
	Если ПараметрыОбновления <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ПараметрыОбновления);
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатЗаполненияРазделовИнвентаризации", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗаполненияРазделовИнвентаризации(Результат, ДополнительныеПараметры) Экспорт 

	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	ЗагрузитьРезультат(Результат.АдресРезультата, ДополнительныеПараметры);
	
	Если ДополнительныеПараметры.ЗапуститьПроверкуАктуальности Тогда
		ПодключитьПроверкуАктуальности();
	Иначе
		// Даже если не требуется актуализация, то может потребоваться обновить элементы формы, связанные с актуализацией
		УправлениеФормойГруппаАктуализация(ЭтотОбъект, ?(ТребуетсяАктуализация, "ТребуетсяАктуализация", "АктуализацияЗавершенаУспешно"), ДатаАктуальности);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультат(АдресРезультата, ДополнительныеПараметры)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ОбновитьРазделУчета Тогда
		ЗначениеВРеквизитФормы(Результат.ДанныеДерева, "ДеревоРазделы");
		УстановитьТекущуюСтроку(ДополнительныеПараметры);
	Иначе
		ДеревоРазделы.ПолучитьЭлементы().Очистить();
		ЗначениеВРеквизитФормы(Результат.ДанныеДерева, "ДеревоРазделы");
	КонецЕсли;
	ЗаполнитьНадписьРезультат();
	
	УправлениеФормой();

КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтроку(ДополнительныеПараметры)
	
	РазделУчета = ДополнительныеПараметры.РазделУчета;
	СчетУчетаПодраздела = ДополнительныеПараметры.СчетУчетаПодраздела;
	ПодразделениеПодраздела = ДополнительныеПараметры.ПодразделениеПодраздела;
	СкладПодраздела = ДополнительныеПараметры.СкладПодраздела;
	
	Разделы = ДеревоРазделы.ПолучитьЭлементы();
	Для Каждого СтрокаРаздела Из Разделы Цикл
		Если СтрокаРаздела.Раздел = РазделУчета Тогда
			Если ЗначениеЗаполнено(СчетУчетаПодраздела) Тогда
				СтрокиРаздела = СтрокаРаздела.ПолучитьЭлементы();
				Для Каждого СтрокаПодраздела Из СтрокиРаздела Цикл
					Если СтрокаПодраздела.СчетУчетаПодраздела = СчетУчетаПодраздела Тогда
						Если ПодразделениеПодраздела <> Неопределено Тогда
							СтрокиПодраздела = СтрокаПодраздела.ПолучитьЭлементы();
							Для Каждого СтрокаПодразделаПодразделение Из СтрокиПодраздела Цикл
								Если СтрокаПодразделаПодразделение.ПодразделениеПодраздела = ПодразделениеПодраздела Тогда
									Элементы.РазделыИнвентаризации.ТекущаяСтрока = СтрокаПодразделаПодразделение.ПолучитьИдентификатор();
									Прервать;
								КонецЕсли;
							КонецЦикла;
						Иначе
							Элементы.РазделыИнвентаризации.ТекущаяСтрока = СтрокаПодраздела.ПолучитьИдентификатор();
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли СкладПодраздела <> Неопределено Тогда
				СтрокиРаздела = СтрокаРаздела.ПолучитьЭлементы();
				Для Каждого СтрокаПодраздела Из СтрокиРаздела Цикл
					Если СтрокаПодраздела.СкладПодраздела = СкладПодраздела Тогда
						Элементы.РазделыИнвентаризации.ТекущаяСтрока = СтрокаПодраздела.ПолучитьИдентификатор();
						Прервать;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ПодразделениеПодраздела <> Неопределено Тогда
				СтрокиРаздела = СтрокаРаздела.ПолучитьЭлементы();
				Для Каждого СтрокаПодраздела Из СтрокиРаздела Цикл
					Если СтрокаПодраздела.ПодразделениеПодраздела = ПодразделениеПодраздела Тогда
						Элементы.РазделыИнвентаризации.ТекущаяСтрока = СтрокаПодраздела.ПолучитьИдентификатор();
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Элементы.РазделыИнвентаризации.ТекущаяСтрока = СтрокаРаздела.ПолучитьИдентификатор();
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНадписьРезультат()
	
	ЕстьНеВыполнено = 0;
	ЕстьВыполнено = 0;
	ЕстьРасхождения = 0;
	
	Разделы = ДеревоРазделы.ПолучитьЭлементы();
	Для Каждого СтрокаРаздела Из Разделы Цикл
		Если СтрокаРаздела.Статус = НСтр("ru = 'Выполнено'") Тогда
			ЕстьВыполнено = ЕстьВыполнено + 1;
		ИначеЕсли СтрокаРаздела.Статус = НСтр("ru = 'Есть расхождения'") Тогда
			ЕстьРасхождения = ЕстьРасхождения + 1;
		Иначе
			ЕстьНеВыполнено = ЕстьНеВыполнено + 1;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьНеВыполнено = 0 И ЕстьРасхождения = 0 Тогда
		Элементы.НадписьРезультат.Заголовок = НСтр("ru = 'Выполнено'");
		Элементы.НадписьРезультат.ЦветТекста = ОбщегоНазначенияВызовСервера.ЦветСтиля("ПомощникИнвентаризацииСостояниеВыполнено");
	ИначеЕсли ЕстьНеВыполнено = 0 И ЕстьРасхождения > 0 Тогда
		Элементы.НадписьРезультат.Заголовок = НСтр("ru = 'Есть расхождения'");
		Элементы.НадписьРезультат.ЦветТекста = ОбщегоНазначенияВызовСервера.ЦветСтиля("ПоясняющийОшибкуТекст");
	Иначе
		Элементы.НадписьРезультат.Заголовок = НСтр("ru = 'Не выполнено'");
		Элементы.НадписьРезультат.ЦветТекста = ОбщегоНазначенияВызовСервера.ЦветСтиля("ПомощникИнвентаризацииСостояниеНеВыполнено");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()

	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРабочая;
	УстановитьУсловноеОформление();

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "РазделыИнвентаризацииИмяРаздела");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ДеревоРазделы.ВерхнийУровень", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(ШрифтыСтиля.ШрифтИтоговМонитораРуководителя));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "РазделыИнвентаризацииОписание");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ДеревоРазделы.Описание", ВидСравненияКомпоновкиДанных.Равно, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "РазделыИнвентаризацииПереченьСчетов");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ДеревоРазделы.Описание", ВидСравненияКомпоновкиДанных.Равно, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "РазделыИнвентаризацииСуммаПоИнвентаризации");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ДеревоРазделы.ЕстьРасхождения", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);

КонецПроцедуры

&НаСервере
Функция ПараметрыФормы(Раздел, СкладПодраздела, ПодразделениеПодраздела, СчетУчетаПодраздела)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗаполнениеИзПомощника", Истина);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("Дата", КонецДня(Объект.Дата));
	ПараметрыФормы.Вставить("РазделУчета", Раздел);
	ПараметрыФормы.Вставить("СкладПодраздела", СкладПодраздела);
	ПараметрыФормы.Вставить("ПодразделениеОрганизации", ПодразделениеПодраздела);
	ПараметрыФормы.Вставить("СчетУчетаПодраздела", СчетУчетаПодраздела);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПодсказкуДата()
	
	Элементы.Дата.Подсказка = СтрШаблон(
		НСтр("ru = 'По разделу ""Основные средства"" годовая инвентаризация может проводиться один раз в три года или ежегодно начиная с 1 октября %1 г.
		|По разделам: ""Нематериальные активы"", ""Запасы"" и ""Капитальные вложения"" допустимо проводить годовую инвентаризацию начиная с 1 октября %1 г.
		|Сумма по учету будет определяться на последнюю дату документов по разделу. По остальным разделам сумма по учету определяется на %2 г.'"),
		Формат(Объект.Дата, "ДФ=yyyy"), Формат(Объект.Дата, "ДФ=dd.MM.yyyy"));
	
КонецПроцедуры

#Область РасшифровкаСуммы

&НаКлиенте
Процедура ОткрытьФормуРасшифровкиСуммы(ТекущаяСтрока)
	
	Раздел = ТекущаяСтрока.Раздел;
	ДатаОстатков = ТекущаяСтрока.ДатаОстатков;
	ПодразделениеПодраздела = ТекущаяСтрока.ПодразделениеПодраздела;
	СкладПодраздела = ТекущаяСтрока.СкладПодраздела;
	
	Если ЗначениеЗаполнено(ТекущаяСтрока.СчетУчетаПодраздела) Тогда
		ПараметрыФормы = ПараметрыФормыОСВПоСчету(Объект.Организация, Раздел, ТекущаяСтрока.СчетУчетаПодраздела, ДатаОстатков, ПодразделениеПодраздела);
		ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету.Форма.ФормаОтчета",
			ПараметрыФормы, ЭтотОбъект, Истина);
	ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.СкладПодраздела)
		И ТекущаяСтрока.Раздел = ПредопределенноеЗначение("Перечисление.РазделыУчетаДляИнвентаризации.Запасы") Тогда
		ПараметрыФормы = ПараметрыФормыОстаткиТоваров(Объект.Организация, Раздел, СкладПодраздела, ДатаОстатков);
		ОткрытьФорму("Отчет.ОстаткиТоваров.Форма.ФормаОтчета",
			ПараметрыФормы, ЭтотОбъект, Истина);
	Иначе
		ИсключитьСчетаСубконтоСклад = ТекущаяСтрока.Раздел = ПредопределенноеЗначение("Перечисление.РазделыУчетаДляИнвентаризации.Запасы")
			И Не ТекущаяСтрока.ВерхнийУровень И Не ЗначениеЗаполнено(ТекущаяСтрока.СкладПодраздела);
		ПараметрыФормы = ПараметрыФормыОСВ(
			Объект.Организация, Раздел, ДатаОстатков, ПодразделениеПодраздела, ИсключитьСчетаСубконтоСклад, ТекущаяСтрока.ПрочиеСчетаНепредопределенные);
		ОткрытьФорму("Отчет.ОборотноСальдоваяВедомость.Форма.ФормаОтчета",
			ПараметрыФормы, ЭтотОбъект, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыФормыОСВ(Организация, Раздел, ДатаОстатков, ПодразделениеПодраздела, ИсключитьСчетаСубконтоСклад, ПрочиеСчетаНепредопределенные)
	
	ПараметрыОтчета = Отчеты.ОборотноСальдоваяВедомость.ПустыеПараметрыКомпоновкиОтчета();
	ЗаполнитьОбщиеПараметрыКомпоновкиОтчета(ПараметрыОтчета); 
	Если Раздел = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами Тогда
		ЗаполнитьРазвернутоеСальдоОтчета(ПараметрыОтчета);
	КонецЕсли;
	Если Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
		ПараметрыОтчета.ВыводитьЗабалансовыеСчета = Истина;
	КонецЕсли;
	ЗаполнитьОтборыОтчета(ПараметрыОтчета, Организация, Раздел, ДатаОстатков, ПодразделениеПодраздела, ИсключитьСчетаСубконтоСклад, ПрочиеСчетаНепредопределенные);
	ПараметрыОтчета.Организация   = Организация;
	ПараметрыОтчета.НачалоПериода = ДатаОстатков;
	ПараметрыОтчета.КонецПериода  = ДатаОстатков;
	Возврат ПараметрыФормыОтчета(ПараметрыОтчета);

КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыФормыОСВПоСчету(Организация, Раздел, СчетУчетаПодраздела, ДатаОстатков, ПодразделениеПодраздела)
	
	ПараметрыОтчета = Отчеты.ОборотноСальдоваяВедомостьПоСчету.ПустыеПараметрыКомпоновкиОтчета();
	ЗаполнитьОбщиеПараметрыКомпоновкиОтчета(ПараметрыОтчета);
	
	ПараметрыОтчета.Организация   = Организация;
	ПараметрыОтчета.НачалоПериода = ДатаОстатков;
	ПараметрыОтчета.КонецПериода  = ДатаОстатков;
	ПараметрыОтчета.Счет          = СчетУчетаПодраздела;
	
	Если ЗначениеЗаполнено(ПодразделениеПодраздела) Тогда
		ПользовательскиеНастройки = ПользовательскиеНастройкиОтчета(ПараметрыОтчета);
		ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.Равно;
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Подразделение", ПодразделениеПодраздела, ВидСравненияСКД);
	КонецЕсли;
	
	Возврат ПараметрыФормыОтчета(ПараметрыОтчета);

КонецФункции

&НаСервере
Функция ПараметрыФормыОстаткиТоваров(Организация, Раздел, СкладПодраздела, ДатаОстатков)
	
	ПараметрыОтчета = Отчеты.ОстаткиТоваров.ПустыеПараметрыКомпоновкиОтчета();
	ЗаполнитьОбщиеПараметрыКомпоновкиОтчета(ПараметрыОтчета);
	
	ПараметрыОтчета.Организация   = Организация;
	ПараметрыОтчета.НачалоПериода = ДатаОстатков;
	ПараметрыОтчета.КонецПериода  = ДатаОстатков;
	
	ПользовательскиеНастройки = ПользовательскиеНастройкиОтчета(ПараметрыОтчета);
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Склад", СкладПодраздела, ВидСравненияКомпоновкиДанных.Равно);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Количество", 0, ВидСравненияКомпоновкиДанных.Больше);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Сумма", 0, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
	
	ПараметрыОтчета.Вставить("ОчищатьДополнительныеПоля", Истина);

	Возврат ПараметрыФормыОтчета(ПараметрыОтчета);

КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыФормыОтчета(ПараметрыОтчета)
	
	// Подготавливаем структуру для отбора необходимых параметров.
	СлужебныеПараметры = БухгалтерскиеОтчеты.СлужебныеПустыеПараметрыКомпоновкиОтчета();
	
	// Через ПользовательскиеНастройки могут быть переданы отборы и другие настройки, подготовленные в формате СКД.
	Если ПараметрыОтчета.Свойство("ПользовательскиеНастройки") Тогда
		ПользовательскиеНастройки = ПараметрыОтчета.ПользовательскиеНастройки;
		// Добавим пользовательские настройки в качестве служебных параметров, в форму они передаются явным параметром.
		СлужебныеПараметры.Вставить("ПользовательскиеНастройки"); 
	Иначе
		ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	КонецЕсли;
	
	ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ЗаполняемыеНастройки = Новый Структура();
	// Показатели заполнены исходя из параметров отчета, их не требуется заполнять по умолчанию.
	ЗаполняемыеНастройки.Вставить("Показатели", Ложь);
	
	// Преобразуем параметры отчета при необходимости.
	Для каждого КлючИЗначение Из ПараметрыОтчета Цикл
		ИмяПараметра      = КлючИЗначение.Ключ;
		ЗначениеПараметра = КлючИЗначение.Значение;
		Если СлужебныеПараметры.Свойство(ИмяПараметра) Тогда
			// Служебные параметры не передаем в форму, они будут заполнены при инициализации формы.
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ЗначениеПараметра) = Тип("ТаблицаЗначений") Тогда
			// Таблицы значений на клиенте не доступны, их требуется преобразовывать в массивы структур.
			ЗначениеПараметра = ОбщегоНазначения.ТаблицаЗначенийВМассив(ЗначениеПараметра);
			ЗаполняемыеНастройки.Вставить(ИмяПараметра, Ложь); // Не требуется менять настройки, которые явно установлены в ПараметрыОтчета.
		КонецЕсли;
		ДополнительныеСвойства.Вставить(ИмяПараметра, ЗначениеПараметра);
	КонецЦикла;
	
	ПараметрыФормы = НовыйПараметрыФормы();
	ПараметрыФормы.Вставить("ВидРасшифровки",            2); // Из пользовательских настроек
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьОбщиеПараметрыКомпоновкиОтчета(ПараметрыОтчета)

	// Заполняем параметры, которые одинаковы для всех формируемых отчетов.
	Если ПараметрыОтчета.Свойство("РежимРасшифровки") Тогда
		ПараметрыОтчета.РежимРасшифровки  = Истина;
	КонецЕсли;
	Если ПараметрыОтчета.Свойство("ВыводитьЗаголовок") Тогда
		ПараметрыОтчета.ВыводитьЗаголовок = Истина;
	КонецЕсли;
	Если ПараметрыОтчета.Свойство("ПоказательБУ") Тогда
		ПараметрыОтчета.ПоказательБУ = Истина;
	КонецЕсли;
	Если ПараметрыОтчета.Свойство("ПоСубсчетам") Тогда
		ПараметрыОтчета.ПоСубсчетам = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьОтборыОтчета(ПараметрыОтчета, Организация, Раздел, ДатаОстатков, ПодразделениеПодраздела, ИсключитьСчетаСубконтоСклад, ПрочиеСчетаНепредопределенные)

	ПользовательскиеНастройки = ПользовательскиеНастройкиОтчета(ПараметрыОтчета);
	ПользовательскиеОтборы = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
	
	СчетаДляИнвентаризации = ИнвентаризацияАктивовОбязательств.СчетаДляИнвентаризации(Раздел, ДатаОстатков, Организация, ИсключитьСчетаСубконтоСклад);
	СчетаУчета = СчетаДляИнвентаризации.СчетаУчета;
	Если Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета
		И ЗначениеЗаполнено(ПрочиеСчетаНепредопределенные) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаУчета, ПрочиеСчетаНепредопределенные.ВыгрузитьЗначения());
	КонецЕсли;
	ИсключаемыеСчета = СчетаДляИнвентаризации.ИсключаемыеСчета;
	
	Если Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства
		Или Раздел = Перечисления.РазделыУчетаДляИнвентаризации.НематериальныеАктивы
		Или Раздел = Перечисления.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство Тогда
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВСписке;
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Счет", СчетаУчета, ВидСравненияСКД);
		
	ИначеЕсли Раздел = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения
		Или Раздел = Перечисления.РазделыУчетаДляИнвентаризации.Запасы
		Или Раздел = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами Тогда
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Счет", СчетаУчета, ВидСравненияСКД);
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.НеВСписке;
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Счет", ИсключаемыеСчета, ВидСравненияСКД);
		
	Иначе
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Счет", СчетаУчета, ВидСравненияСКД);
	КонецЕсли;
	
	Если ПодразделениеПодраздела <> Неопределено Тогда
		Если ПодразделениеПодраздела = Справочники.ПодразделенияОрганизаций.ПустаяСсылка() Тогда
			СписокПодразделений = Справочники.ПодразделенияОрганизаций.ОбособленныеПодразделенияНеВыделенныеНаОтдельныйБаланс(Организация);
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.НеВСписке;
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Подразделение", СписокПодразделений, ВидСравненияСКД);
		Иначе
			ВидСравненияСКД = ВидСравненияКомпоновкиДанных.Равно;
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Подразделение", ПодразделениеПодраздела, ВидСравненияСКД);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьРазвернутоеСальдоОтчета(ПараметрыОтчета)
	
	// Сч 60
	ЗаполнитьСтрокуНастроек(ПараметрыОтчета.РазвернутоеСальдо.Добавить(),
		БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками),
		Истина,
		Новый Структура("Субконто1, Субконто2, Субконто3", Ложь, Ложь, Ложь));
		
	// Сч 62
	ЗаполнитьСтрокуНастроек(ПараметрыОтчета.РазвернутоеСальдо.Добавить(),
		БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками),
		Истина,
		Новый Структура("Субконто1, Субконто2, Субконто3", Ложь, Ложь, Ложь));
	
	// Сч 68
	ЗаполнитьСтрокуНастроек(ПараметрыОтчета.РазвернутоеСальдо.Добавить(),
		БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам),
		Истина,
		Новый Структура("Субконто1, Субконто2, Субконто3", Истина, Ложь, Ложь));
	
	// Сч 69
	ЗаполнитьСтрокуНастроек(ПараметрыОтчета.РазвернутоеСальдо.Добавить(),
		БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию),
		Истина,
		Новый Структура("Субконто1, Субконто2, Субконто3", Истина, Ложь, Ложь));
	
	Если НЕ БухгалтерскийУчетПереопределяемый.ВедетсяУчетРасчетовПоЗарплатеСводно() Тогда
		// Сч 70 по субконто "Работники организаций".
		ЗаполнитьСтрокуНастроек(ПараметрыОтчета.РазвернутоеСальдо.Добавить(),
		БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда),
		Истина,
		Новый Структура("Субконто1, Субконто2, Субконто3", Истина, Ложь, Ложь));
	КонецЕсли;
	
	// Сч 71
	ЗаполнитьСтрокуНастроек(ПараметрыОтчета.РазвернутоеСальдо.Добавить(),
		БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами_),
		Истина,
		Новый Структура("Субконто1, Субконто2, Субконто3", Истина, Ложь, Ложь));
	
	// Сч 73
	ЗаполнитьСтрокуНастроек(ПараметрыОтчета.РазвернутоеСальдо.Добавить(),
		БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоПрочимОперациям),
		Истина,
		Новый Структура("Субконто1, Субконто2, Субконто3", Истина, Ложь, Ложь));
		
	// Сч 76
	ЗаполнитьСтрокуНастроек(ПараметрыОтчета.РазвернутоеСальдо.Добавить(),
		БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами),
		Истина,
		Новый Структура("Субконто1, Субконто2, Субконто3", Истина, Ложь, Ложь));
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСтрокуНастроек(СтрокаНастроек, ДанныеСчета, ПоСубсчетам = Истина, ПоСубконто = Неопределено)

	СтрокаПоСубконто = "";
	СтрокаПредставление = "";
	
	Если ПоСубконто <> Неопределено Тогда
		Для НомерСубконто = 1 По 3 Цикл
			
			Если ДанныеСчета["ВидСубконто" + НомерСубконто] = Неопределено Тогда
				
				Прервать;
				
			ИначеЕсли ПоСубконто["Субконто" + НомерСубконто] Тогда
				
				СтрокаПоСубконто = СтрокаПоСубконто + "+" + НомерСубконто;
				СтрокаПредставление = СтрокаПредставление + ДанныеСчета["ВидСубконто" + НомерСубконто + "Наименование"] + ", ";
			Иначе
				
				СтрокаПоСубконто = СтрокаПоСубконто + "-" + НомерСубконто;
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	СтрокаНастроек.Использование = Истина;
	СтрокаНастроек.Счет          = ДанныеСчета.Ссылка;
	СтрокаНастроек.ПоСубсчетам   = ПоСубсчетам;
	СтрокаНастроек.Представление = Лев(СтрокаПредставление, СтрДлина(СтрокаПредставление) - 2);
	СтрокаНастроек.ПоСубконто    = СтрокаПоСубконто;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПользовательскиеНастройкиОтчета(ПараметрыОтчета)

	Если ПараметрыОтчета.Свойство("ПользовательскиеНастройки") Тогда
		Результат = ПараметрыОтчета.ПользовательскиеНастройки;
	КонецЕсли;
	
	Если ТипЗнч(Результат) <> Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		Результат = Новый ПользовательскиеНастройкиКомпоновкиДанных;
		ПараметрыОтчета.Вставить("ПользовательскиеНастройки", Результат);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Функция НовыйПараметрыФормы()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("РежимРасшифровки",        Истина);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции

#КонецОбласти

#Область ПроверкаАктуальности 

&НаКлиенте
Процедура ПодключитьПроверкуАктуальности()
	
	Если Не АктуализацияВозможна Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьАктуальность", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьАктуальность()
	
	ЗаданиеАктуализации = ЗакрытиеМесяцаВызовСервера.НайтиФоновоеЗаданиеАктуализацииПоОрганизации(Объект.Организация, УникальныйИдентификатор);
	
	Если ЗаданиеАктуализации = Неопределено Или Не ЗначениеЗаполнено(ЗаданиеАктуализации.УникальныйИдентификатор) Тогда
		ПроверитьАктуальностьДанных();
	Иначе
		
		Если ИдентификаторЗаданияАктуализации <> ЗаданиеАктуализации.УникальныйИдентификатор Тогда
			// Задание запущено в другой форме
			ИдентификаторЗаданияАктуализации = ЗаданиеАктуализации.УникальныйИдентификатор;
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеАктуализации",
				ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал,
				Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьАктуальностьДанных()
	
	УправлениеФормойГруппаАктуализация(ЭтотОбъект, "ЗапущенаПроверкаАктуальности");
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	
	ИдентификаторЗаданияАктуализации = Неопределено;
	
	УИДЗамера = ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Ложь, "АктуализацияДанныхТребуетсяАктуализацияЗаПериод");
	
	ПараметрыПроверки = ЗакрытиеМесяцаКлиентСервер.НовыеПараметрыПроверкиАктуальности();
	ПараметрыПроверки.Организация                  = Объект.Организация;
	ПараметрыПроверки.Период                       = Объект.Дата;
	ПараметрыПроверки.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыПроверки.УИДЗамера                    = УИДЗамера;
	ПараметрыПроверки.АктуализироватьВесьПериод    = Истина;
	
	РезультатВыполнения = ЗакрытиеМесяцаВызовСервера.ПроверитьАктуальность(ПараметрыПроверки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиАктуальности", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеАктуализации()
	
	Если ЗакрытиеМесяцаВызовСервера.ЗаданиеВыполнено(ИдентификаторЗаданияАктуализации) Тогда
		ОбработатьРезультатАктуализации();
		ИдентификаторЗаданияАктуализации = Неопределено;
	Иначе
		ОбновитьПроцентПрогресса();
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияАктуализации);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации",
			ПараметрыОбработчикаОжиданияАктуализации.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПроверкиАктуальности(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если РезультатПроверки.УИДЗамера <> Неопределено Тогда
		ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(РезультатПроверки.УИДЗамера);
	КонецЕсли;
	
	Если НЕ РезультатПроверки.ТребуетсяАктуализация Тогда
		ТребуетсяАктуализация = Ложь;
		Оповестить("АктуализацияЗавершенаУспешно", Новый Структура("Организация", Объект.Организация));
	Иначе
		ДатаАктуальности = РезультатПроверки.ДатаАктуальности;
		ПараметрыОповещения = Новый Структура("Организация, ДатаАктуальности", 
			Объект.Организация, ДатаАктуальности);
		ТребуетсяАктуализация = Истина;
		Оповестить("ТребуетсяАктуализация", ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатАктуализации()
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеАктуализации");
	ПараметрыОповещения = Новый Структура("Организация", Объект.Организация);
	
	Если Не ЭтоАдресВременногоХранилища(АдресХранилищаАктуализации) Тогда
		Оповестить("АктуализацияОтменена", ПараметрыОповещения);
		Возврат;
	КонецЕсли;
	
	РезультатАктуализации = ПолучитьИзВременногоХранилища(АдресХранилищаАктуализации);
	УдалитьИзВременногоХранилища(АдресХранилищаАктуализации);
	АдресХранилищаАктуализации = "";
	
	Если РезультатАктуализации.Выполнено Тогда
		ТребуетсяАктуализация = Ложь;
		Оповестить("АктуализацияЗавершенаУспешно", ПараметрыОповещения);
		// Запускаем обновление разделов
		ЗаполнитьРазделыИнвентаризации(Ложь);
	Иначе
		ЗакрытиеМесяцаКлиент.ПоказатьОшибкиАктуализации(ЭтотОбъект, РезультатАктуализации);
		Оповестить("АктуализацияОтменена", ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПроцентПрогресса()
	
	Прогресс = ЗакрытиеМесяцаВызовСервера.ПрочитатьПрогресс(ИдентификаторЗаданияАктуализации);
	
	Если ТипЗнч(Прогресс) = Тип("Структура") И Прогресс.Свойство("Процент") Тогда
		Процент = Мин(Прогресс.Процент, 99);
		ПрогрессорАктуализации = Строка(Процент) + "%.";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормойГруппаАктуализация(Форма, Событие, ДатаАктуальностиДанных = '00010101')
	
	Элементы = Форма.Элементы;
	
	// Если автоматическая актуализация невозможна, то вся группа актуализации скрыта.
	ОтображатьАктуализацию = Форма.АктуализацияВозможна
		И (Событие = "ТребуетсяАктуализация" Или Событие = "ЗапущенаАктуализация");
	
	Элементы.Актуализация.Видимость = ОтображатьАктуализацию;
	Элементы.Актуализировать.Видимость = Форма.ЕстьПравоАктуализации;
	
	Если Не ОтображатьАктуализацию Тогда
		Возврат;
	КонецЕсли;
		
	Элементы.ИдетАктуализация.Видимость         = (Событие = "ЗапущенаАктуализация");
	Элементы.ИдетПроверкаАктуальности.Видимость = (Событие = "ЗапущенаПроверкаАктуальности");
	
	Если Событие = "ТребуетсяАктуализация" Тогда
		Элементы.ТребуетсяАктуализация.Видимость = Истина;
		Элементы.ДекорацияАктуальность.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные учета неактуальны с %1.'"), Формат(ДатаАктуальностиДанных, "ДФ=dd.MM.yyyy"));
	Иначе
		Элементы.ТребуетсяАктуализация.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
