#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные разделов инвентаризации.
//
// Параметры:
//  Параметры - Структура:
//    ДанныеДерева - ДеревоЗначений
//    Дата - Дата
//    Организация - СправочникСсылка.Организации
//    РазделУчета - ПеречислениеСсылка.РазделыУчетаДляИнвентаризации
//    ПодразделениеПодраздела - СправочникСсылка.ПодразделенияОрганизаций
//    СкладПодраздела - СправочникСсылка.Склады
//    СчетУчетаПодраздела - ПланСчетовСсылка.Хозрасчетный
// 
// Возвращаемое значение:
//  Структура:
//    ДанныеДерева - ДеревоЗначений
//    ОбновитьРазделУчета - Булево
//
Функция РазделыИнвентаризации(Параметры) Экспорт
	
	РезультатВыполнения = Новый Структура();
	РезультатВыполнения.Вставить("ДанныеДерева",        Неопределено);
	РезультатВыполнения.Вставить("ОбновитьРазделУчета", Ложь);
		
	ДанныеДерева = Параметры.ДанныеДерева;
	Если Параметры.РазделУчета = Неопределено Тогда
		ДанныеДерева.Строки.Очистить();
	Иначе
		СтрокаДерева = ДанныеДерева.Строки.Найти(Параметры.РазделУчета, "Раздел");
		Если СтрокаДерева <> Неопределено Тогда
			ЗаполнитьСтрокуРаздела(СтрокаДерева, Параметры);
			Если Параметры.РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.Запасы
				И ЗначениеЗаполнено(Параметры.СкладПодраздела) Тогда
				// Могут быть забалансовые счета в документе "Инвентаризация товаров".
				РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета;
				СтрокаДерева = ДанныеДерева.Строки.Найти(РазделУчета, "Раздел");
				Если СтрокаДерева <> Неопределено Тогда
					ЗаполнитьСтрокуРаздела(СтрокаДерева, Параметры);
				КонецЕсли;
			КонецЕсли;
			РезультатВыполнения.ОбновитьРазделУчета = Истина;
			РезультатВыполнения.ДанныеДерева = ДанныеДерева;
			Возврат РезультатВыполнения;
		КонецЕсли;
	КонецЕсли;
	
	СписокИспользуемыхРазделов = Перечисления.РазделыУчетаДляИнвентаризации.ДанныеВыбораПоФункциональнымОпциям();
	ДанныеДерева.Строки.Очистить();
	Для Каждого Раздел Из СписокИспользуемыхРазделов Цикл
		СтрокаПервогоУровня = ДанныеДерева.Строки.Добавить();
		СтрокаПервогоУровня.Раздел = Раздел.Значение;
		СтрокаПервогоУровня.ИмяРаздела = Раздел.Представление;
		СтрокаПервогоУровня.ВерхнийУровень = Истина;
		ЗаполнитьСтрокуРаздела(СтрокаПервогоУровня, Параметры);
		Если СтрокаПервогоУровня.СуммаПоИнвентаризации = 0 И СтрокаПервогоУровня.СуммаПоУчету = 0 Тогда
			ДанныеДерева.Строки.Удалить(ДанныеДерева.Строки.Индекс(СтрокаПервогоУровня));
		КонецЕсли;
	КонецЦикла;
	
	РезультатВыполнения.ДанныеДерева = ДанныеДерева;
	Возврат РезультатВыполнения;
	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив Из СправочникСсылка.Организации - ссылки на организации, по которым нужно распечатать документы инвентаризации;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//
// Выходные параметры:
//  КоллекцияПечатныхФорм - см. УправлениеПечатью.ПодготовитьКоллекциюПечатныхФорм;
//  ОбъектыПечати        - СписокЗначений  - значение - СправочникСсылка.Организации, ссылка на объект;
//                                            представление - имя области в которой был выведен объект;
//  ПараметрыВывода      - Структура      - дополнительные параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ИмяПечатнойФормы = "Реестр";
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяПечатнойФормы) Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, ИмяПечатнойФормы, НСтр("ru = 'Реестр'"), ПечатьРеестр(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,
			"Обработка.ПомощникИнвентаризации.ПФ_MXL_Реестр", НСтр("ru = 'Реестр документов по инвентаризации'"));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьСтрокуРаздела(Строка, Параметры)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Если КонецГода(Параметры.Дата) = Параметры.Дата
		И Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства Тогда
		// ОС можно инвентаризировать 1 раз в 3 года
		Запрос.УстановитьПараметр("ДатаНачала", ДобавитьМесяц(Параметры.Дата + 1, -3 * 12));
	ИначеЕсли КонецГода(Параметры.Дата) = Параметры.Дата
		И (Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.НематериальныеАктивы 
		Или Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.Запасы
		Или Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения) Тогда
		// С 1 октября признается инвентаризация по разделам: НМА, Капитальные вложения, Запасы.
		Запрос.УстановитьПараметр("ДатаНачала", ДобавитьМесяц(НачалоМесяца(Параметры.Дата), -2));
	Иначе
		// Остальные на отчетную дату.
		Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Параметры.Дата));
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаОкончания", Параметры.Дата);
	Строка.СуммаПоИнвентаризации = 0;
	Строка.СуммаПоУчету = 0;
	Строка.ДатаОстатков = Параметры.Дата;
	
	СчетаДляИнвентаризации = ИнвентаризацияАктивовОбязательств.СчетаДляИнвентаризации(Строка.Раздел, Запрос.Параметры.ДатаОкончания, Параметры.Организация);
	СчетаУчета = СчетаДляИнвентаризации.СчетаУчета;
	Строка.ПереченьСчетов = ПереченьСчетов(СчетаУчета);
	
	Если Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ОсновныеСредства Тогда
		Строка.Описание = НСтр("ru='документ ""Инвентаризация ОС""'");
		Строка.ПереченьСчетов = НСтр("ru='счета учета: 01, 03'");
		Строка.НаименованиеДокумента = "ИнвентаризацияОС";
		ОпределитьДокументыПоИнвентаризацииИДатуОстатковПоОС(Строка.НаименованиеДокумента, Запрос, Строка.ДатаОстатков);
		Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
		Запрос.УстановитьПараметр("ДатаОстатков", Строка.ДатаОстатков+1);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ЕСТЬNULL(ИнвентаризацияОСОС.СтоимостьПоРезультатамИнвентаризации, 0)) КАК СуммаПоИнвентаризации
		|ИЗ
		|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОС.ОС КАК ИнвентаризацияОСОС
		|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияОСОС.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаПоУчету
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&СчетаУчета), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация = &Организация) КАК ХозрасчетныйОстатки";
		ЗаполнитьСуммы(Строка, Запрос);
	
	ИначеЕсли Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.НематериальныеАктивы Тогда
		Строка.Описание = НСтр("ru='документ ""Инвентаризация НМА""'");
		Строка.ПереченьСчетов = НСтр("ru='счета учета: 04'");
		Строка.НаименованиеДокумента = "ИнвентаризацияНМА";
		ОпределитьДокументыПоИнвентаризацииИДатуОстатков(Строка.НаименованиеДокумента, Запрос, Строка.ДатаОстатков);
		Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
		Запрос.УстановитьПараметр("ДатаОстатков", Строка.ДатаОстатков+1);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ЕСТЬNULL(ИнвентаризацияНМАОбъектыУчета.Стоимость, 0)) КАК СуммаПоИнвентаризации
		|ИЗ
		|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНМА.ОбъектыУчета КАК ИнвентаризацияНМАОбъектыУчета
		|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияНМАОбъектыУчета.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаПоУчету
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&СчетаУчета), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы), Организация = &Организация) КАК ХозрасчетныйОстатки";
		ЗаполнитьСуммы(Строка, Запрос);
		
	ИначеЕсли Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.Запасы Тогда
		ЗаполнитьСтрокуЗапасы(Строка, СчетаДляИнвентаризации, Параметры.СкладПодраздела, Параметры.СчетУчетаПодраздела, Запрос);
		
	ИначеЕсли Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство Тогда
		ЗаполнитьСтрокуНЗП(Строка, СчетаУчета, Параметры.СчетУчетаПодраздела, Параметры.ПодразделениеПодраздела, Запрос);
		
	ИначеЕсли Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.Касса Тогда
		ЗаполнитьСтрокуКасса(Строка, СчетаУчета, Параметры.ПодразделениеПодраздела, Запрос);
		
	ИначеЕсли Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами Тогда
		ЗаполнитьСтрокуРасчетыСКонтрагентами(Строка, СчетаДляИнвентаризации, Запрос);
		
	ИначеЕсли Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения Тогда
		ИсключаемыеСчета = СчетаДляИнвентаризации.ИсключаемыеСчета;
		ЗаполнитьСтрокуИнвентаризацияОбъектовУчета(Строка, Запрос, СчетаУчета, ИсключаемыеСчета);
		
	ИначеЕсли Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям
		Или Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.БанковскиеСчета
		Или Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ФинансовыеВложения
		Или Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.РасходыБудущихПериодов Тогда
		ЗаполнитьСтрокуИнвентаризацияОбъектовУчета(Строка, Запрос, СчетаУчета);
		
	ИначеЕсли Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ОтложенныеНалоговыеАктивыОбязательства
		Или Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ОценочныеОбязательства
		Или Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ДоходыБудущихПериодов Тогда
		ЗаполнитьСтрокуИнвентаризацияОбъектовУчета(Строка, Запрос, СчетаУчета,, Истина);
		
	Иначе
		ЗаполнитьСтрокуПрочиеЗабалансовые(Строка, СчетаДляИнвентаризации, Параметры.СчетУчетаПодраздела, Запрос);
	КонецЕсли;
	
	ЗаполнитьКартинкуСтатус(Строка);

КонецПроцедуры

Процедура ЗаполнитьСтрокуИнвентаризацияОбъектовУчета(Строка, Запрос, СчетаУчета, ИсключаемыеСчета = Неопределено, НуженОбходПоСчетам = Ложь)
	
	Строка.Описание = НСтр("ru='документ ""Инвентаризация объектов учета""'");
	Строка.НаименованиеДокумента = "ИнвентаризацияОбъектовУчета";
	Запрос.УстановитьПараметр("РазделУчета", Строка.Раздел);
	
	Если Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения Тогда
		ОпределитьДокументыПоИнвентаризацииИДатуОстатков(Строка.НаименованиеДокумента, Запрос, Строка.ДатаОстатков, Строка.Раздел);
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.ИспользоватьВПроводках = Истина;
		МассивСчетов = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчета, УсловиеОтбора);
		СчетаУчетаСубсчета =  ОбщегоНазначенияКлиентСервер.РазностьМассивов(МассивСчетов, ИсключаемыеСчета);
	Иначе
		ОпределитьДокументыПоИнвентаризации(Строка.НаименованиеДокумента, Запрос, Строка.Раздел);
		УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
		УсловиеОтбора.ИспользоватьВПроводках = Истина;
		СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчета, УсловиеОтбора);
	КонецЕсли;
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчетаСубсчета);
	Запрос.УстановитьПараметр("ДатаОстатков", Строка.ДатаОстатков+1);
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ
	|	СУММА(ИнвентаризацияОбъектовУчетаОбъектыУчета.Сумма) КАК СуммаПоИнвентаризации
	|ИЗ
	|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ИнвентаризацияОбъектовУчетаОбъектыУчета
	|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка");
	
	Если НуженОбходПоСчетам Тогда
		
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Счет КАК Счет,
		|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаОстаток,
		|	ХозрасчетныйОстатки.Счет.Вид КАК СчетВид
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&СчетаУчета), , Организация = &Организация) КАК ХозрасчетныйОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстатки.Счет,
		|	ХозрасчетныйОстатки.Счет.Вид");
		
	Иначе
		
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаПоУчету
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&СчетаУчета), , Организация = &Организация) КАК ХозрасчетныйОстатки");
		
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	ЗаполнитьСуммы(Строка, Запрос, НуженОбходПоСчетам);
	
КонецПроцедуры

Процедура ЗаполнитьСуммы(Строка, Запрос, НуженОбходПоСчетам = Ложь)
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатыЗапроса[0].Выбрать();
	Если Выборка.Следующий() Тогда
		Строка.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
	КонецЕсли;
	Выборка = РезультатыЗапроса[1].Выбрать();
	Если НуженОбходПоСчетам Тогда
		Пока Выборка.Следующий() Цикл
			Если Выборка.СчетВид <> ВидСчета.Активный Тогда
				Строка.СуммаПоУчету = Строка.СуммаПоУчету + ?(Выборка.СуммаОстаток > 0, Выборка.СуммаОстаток, -Выборка.СуммаОстаток);
			Иначе
				Строка.СуммаПоУчету = Строка.СуммаПоУчету + Выборка.СуммаОстаток;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если Выборка.Следующий() Тогда
			Строка.СуммаПоУчету = Выборка.СуммаПоУчету;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуЗапасы(Строка, СчетаДляИнвентаризации, СкладПодраздела, СчетУчетаПодраздела, Запрос)
	
	Строка.Описание = НСтр("ru='документ ""Инвентаризация товаров""'");
	Строка.НаименованиеДокумента = "ИнвентаризацияТоваровНаСкладе";
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка КАК Ссылка,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата КАК Дата,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Склад КАК Склад
	|ПОМЕСТИТЬ ДокументыПоИнвентаризации
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
	|ГДЕ
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Проведен
	|	И ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Организация = &Организация
	|	И ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Забалансовый
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПоИнвентаризации.Дата КАК Дата
	|ИЗ
	|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";

	РезультатыЗапроса = Запрос.Выполнить();
	Выборка = РезультатыЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Строка.ДатаОстатков = Выборка.Дата;
	КонецЕсли;
	ВестиСкладскойУчетБУ = БухгалтерскийУчет.ВедетсяУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары);
	ВестиСуммовойУчетПоСкладамБУ = ВестиСкладскойУчетБУ
	И БухгалтерскийУчет.ВедетсяСуммовойУчетПоСкладам(ПланыСчетов.Хозрасчетный.Товары);
	ПорядокСубконтоКоличество = Новый Массив;
	ПорядокСубконтоКоличество.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Если ВестиСкладскойУчетБУ Тогда
		ПорядокСубконтоКоличество.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	ПорядокСубконтоЦена = Новый Массив();
	ПорядокСубконтоЦена.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Если ВестиСуммовойУчетПоСкладамБУ Тогда
		ПорядокСубконтоЦена.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	МассивДополнительныхСчетов = Новый Массив;
	Запрос.УстановитьПараметр("ИсклСчета", СчетаДляИнвентаризации.ИсключаемыеСчета);
	Запрос.УстановитьПараметр("ПорядокСубконтоКоличество", ПорядокСубконтоКоличество);
	Запрос.УстановитьПараметр("ПорядокСубконтоЦена", ПорядокСубконтоЦена);
	Запрос.УстановитьПараметр("ДатаОстатков", Строка.ДатаОстатков+1);
	// КоличествоОстаток > 0 как в документе "Инвентаризация товаров", отрицательные суммы не учитываются
	Если ВестиСкладскойУчетБУ Тогда
		МассивДополнительныхСчетов = Новый Массив;
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТопливоВБаке);                     //10.03.2
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку);  //10.07
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья); //20.02
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.ПокупныеТоварыОтгруженные);        //45.01
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная);      //45.02
		МассивДополнительныхСчетов.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеТоварыОтгруженные);          //45.03
		Запрос.УстановитьПараметр("ДопСчета", МассивДополнительныхСчетов);
		Запрос.УстановитьПараметр("СубконтоНомеклатура", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		Если ВестиСуммовойУчетПоСкладамБУ Тогда
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДокументыПоИнвентаризации.Склад КАК Склад,
			|	СУММА(ИнвентаризацияТоваровТовары.Сумма) КАК СуммаПоИнвентаризации
			|ПОМЕСТИТЬ ДокументыПоСкладам
			|ИЗ
			|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровТовары
			|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияТоваровТовары.Ссылка
			|			И (НЕ ИнвентаризацияТоваровТовары.СчетУчета.Забалансовый)
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументыПоИнвентаризации.Склад
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Склад
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХозрасчетныйОстатки.Счет КАК Счет,
			|	ХозрасчетныйОстатки.Субконто1 КАК Номенклатура,
			|	ХозрасчетныйОстатки.Субконто2 КАК Склад,
			|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток,
			|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток,
			|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто2 КАК Справочник.Склады).Наименование КАК СкладНаименование
			|ПОМЕСТИТЬ ХозрасчетныйОстаткиКоличество
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, НЕ Счет В (&ИсклСчета), &ПорядокСубконтоКоличество, Организация = &Организация) КАК ХозрасчетныйОстатки
			|ГДЕ
			|	ХозрасчетныйОстатки.КоличествоОстаток > 0
			|	И ХозрасчетныйОстатки.СуммаОстаток > 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ХозрасчетныйОстатки.Счет,
			|	ХозрасчетныйОстатки.Субконто1,
			|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
			|	ХозрасчетныйОстатки.КоличествоОстаток,
			|	ХозрасчетныйОстатки.СуммаОстаток,
			|	""""
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&ДопСчета), &СубконтоНомеклатура, Организация = &Организация) КАК ХозрасчетныйОстатки
			|ГДЕ
			|	ХозрасчетныйОстатки.КоличествоОстаток > 0
			|	И ХозрасчетныйОстатки.СуммаОстаток > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХозрасчетныйОстаткиКоличество.Склад КАК Склад,
			|	СУММА(ХозрасчетныйОстаткиКоличество.СуммаОстаток) КАК СуммаОстаток,
			|	ХозрасчетныйОстаткиКоличество.СкладНаименование КАК СкладНаименование,
			|	ВЫБОР
			|		КОГДА ХозрасчетныйОстаткиКоличество.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Порядок
			|ПОМЕСТИТЬ ОстаткиПоСкладам
			|ИЗ
			|	ХозрасчетныйОстаткиКоличество КАК ХозрасчетныйОстаткиКоличество
			|
			|СГРУППИРОВАТЬ ПО
			|	ХозрасчетныйОстаткиКоличество.Склад,
			|	ХозрасчетныйОстаткиКоличество.СкладНаименование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(ОстаткиПоСкладам.Склад, ДокументыПоСкладам.Склад) КАК Склад,
			|	ЕСТЬNULL(ОстаткиПоСкладам.СуммаОстаток, 0) КАК СуммаПоУчету,
			|	ЕСТЬNULL(ДокументыПоСкладам.СуммаПоИнвентаризации, 0) КАК СуммаПоИнвентаризации
			|ИЗ
			|	ОстаткиПоСкладам КАК ОстаткиПоСкладам
			|		ПОЛНОЕ СОЕДИНЕНИЕ ДокументыПоСкладам КАК ДокументыПоСкладам
			|		ПО ОстаткиПоСкладам.Склад = ДокументыПоСкладам.Склад
			|
			|УПОРЯДОЧИТЬ ПО
			|	ОстаткиПоСкладам.Порядок,
			|	ОстаткиПоСкладам.СкладНаименование
			|ИТОГИ
			|	СУММА(СуммаПоУчету),
			|	СУММА(СуммаПоИнвентаризации)
			|ПО
			|	ОБЩИЕ";
		Иначе
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДокументыПоИнвентаризации.Склад КАК Склад,
			|	СУММА(ИнвентаризацияТоваровТовары.Сумма) КАК СуммаПоИнвентаризации
			|ПОМЕСТИТЬ ДокументыПоСкладам
			|ИЗ
			|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровТовары
			|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияТоваровТовары.Ссылка
			|			И (НЕ ИнвентаризацияТоваровТовары.СчетУчета.Забалансовый)
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументыПоИнвентаризации.Склад
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Склад
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХозрасчетныйОстатки.Счет КАК Счет,
			|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
			|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
			|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток
			|ПОМЕСТИТЬ ХозрасчетныйОстаткиКоличество
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, НЕ Счет В (&ИсклСчета), &ПорядокСубконтоКоличество, Организация = &Организация) КАК ХозрасчетныйОстатки
			|ГДЕ
			|	ХозрасчетныйОстатки.КоличествоОстаток > 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ХозрасчетныйОстатки.Счет,
			|	ХозрасчетныйОстатки.Субконто1,
			|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
			|	ХозрасчетныйОстатки.КоличествоОстаток
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&ДопСчета), &СубконтоНомеклатура, Организация = &Организация) КАК ХозрасчетныйОстатки
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Счет,
			|	Субконто1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХозрасчетныйОстатки.Счет КАК Счет,
			|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
			|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток,
			|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток
			|ПОМЕСТИТЬ ХозрасчетныйОстаткиСумма
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, НЕ Счет В (&ИсклСчета), &ПорядокСубконтоЦена, Организация = &Организация) КАК ХозрасчетныйОстатки
			|ГДЕ
			|	ХозрасчетныйОстатки.СуммаОстаток > 0
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Счет,
			|	Субконто1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ХозрасчетныйОстаткиКоличество.Субконто2 КАК Склад,
			|	СУММА(ВЫБОР
			|			КОГДА ХозрасчетныйОстаткиКоличество.КоличествоОстаток = ХозрасчетныйОстаткиСумма.КоличествоОстаток
			|				ТОГДА ХозрасчетныйОстаткиСумма.СуммаОстаток
			|			ИНАЧЕ ХозрасчетныйОстаткиКоличество.КоличествоОстаток * ХозрасчетныйОстаткиСумма.СуммаОстаток / ХозрасчетныйОстаткиСумма.КоличествоОстаток
			|		КОНЕЦ) КАК СуммаОстаток,
			|	ВЫРАЗИТЬ(ХозрасчетныйОстаткиКоличество.Субконто2 КАК Справочник.Склады).Наименование КАК СкладНаименование,
			|	ВЫБОР
			|		КОГДА ХозрасчетныйОстаткиКоличество.Субконто2 = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Порядок
			|ПОМЕСТИТЬ ОстаткиПоСкладам
			|ИЗ
			|	ХозрасчетныйОстаткиСумма КАК ХозрасчетныйОстаткиСумма
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиКоличество КАК ХозрасчетныйОстаткиКоличество
			|		ПО ХозрасчетныйОстаткиСумма.Счет = ХозрасчетныйОстаткиКоличество.Счет
			|			И ХозрасчетныйОстаткиСумма.Субконто1 = ХозрасчетныйОстаткиКоличество.Субконто1
			|
			|СГРУППИРОВАТЬ ПО
			|	ХозрасчетныйОстаткиКоличество.Субконто2,
			|	ВЫРАЗИТЬ(ХозрасчетныйОстаткиКоличество.Субконто2 КАК Справочник.Склады).Наименование
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(ОстаткиПоСкладам.Склад, ДокументыПоСкладам.Склад) КАК Склад,
			|	ЕСТЬNULL(ОстаткиПоСкладам.СуммаОстаток, 0) КАК СуммаПоУчету,
			|	ЕСТЬNULL(ДокументыПоСкладам.СуммаПоИнвентаризации, 0) КАК СуммаПоИнвентаризации
			|ИЗ
			|	ОстаткиПоСкладам КАК ОстаткиПоСкладам
			|		ПОЛНОЕ СОЕДИНЕНИЕ ДокументыПоСкладам КАК ДокументыПоСкладам
			|		ПО ОстаткиПоСкладам.Склад = ДокументыПоСкладам.Склад
			|
			|УПОРЯДОЧИТЬ ПО
			|	ОстаткиПоСкладам.Порядок,
			|	ОстаткиПоСкладам.СкладНаименование
			|ИТОГИ
			|	СУММА(СуммаПоУчету),
			|	СУММА(СуммаПоИнвентаризации)
			|ПО
			|	ОБЩИЕ";
		КонецЕсли;
		РезультатЗапроса = Запрос.Выполнить();
		СтрокаПодразделаПоСкладу = Неопределено;
		Если СкладПодраздела <> Неопределено Тогда
			СтрокаПодразделаПоСкладу = Строка.Строки.Найти(СкладПодраздела, "СкладПодраздела");
		КонецЕсли;
		Если СтрокаПодразделаПоСкладу = Неопределено Тогда
			Строка.Строки.Очистить();
		КонецЕсли;
		ВыборкаИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаИтоги.Следующий() Тогда
			Строка.СуммаПоУчету = ВыборкаИтоги.СуммаПоУчету;
			Строка.СуммаПоИнвентаризации = ВыборкаИтоги.СуммаПоИнвентаризации;
			Выборка = ВыборкаИтоги.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если СтрокаПодразделаПоСкладу <> Неопределено Тогда
					Если Выборка.Склад = СтрокаПодразделаПоСкладу.СкладПодраздела Тогда
						СтрокаПодразделаПоСкладу.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
						СтрокаПодразделаПоСкладу.СуммаПоУчету = Выборка.СуммаПоУчету;
						ЗаполнитьКартинкуСтатус(СтрокаПодразделаПоСкладу);
					КонецЕсли
				Иначе
					СтрокаПодраздела = Строка.Строки.Добавить();
					СтрокаПодраздела.Раздел = Строка.Раздел;
					СтрокаПодраздела.НаименованиеДокумента = "ИнвентаризацияТоваровНаСкладе";
					Если ЗначениеЗаполнено(Выборка.Склад) Тогда
						СтрокаПодраздела.ИмяРаздела = СтрШаблон(НСтр("ru='Склад:  %1'"),Выборка.Склад);
					Иначе
						СтрокаПодраздела.ИмяРаздела = НСтр("ru='Склад: <пустое значение>'");
					КонецЕсли;
					СтрокаПодраздела.ВерхнийУровень = Ложь;
					СтрокаПодраздела.СуммаПоУчету = Выборка.СуммаПоУчету;
					СтрокаПодраздела.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
					СтрокаПодраздела.СкладПодраздела = Выборка.Склад;
					СтрокаПодраздела.ДатаОстатков = Строка.ДатаОстатков;
					ЗаполнитьКартинкуСтатус(СтрокаПодраздела);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ИнвентаризацияТоваровТовары.Сумма) КАК СуммаПоИнвентаризации
		|ИЗ
		|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровТовары
		|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияТоваровТовары.Ссылка
		|			И (НЕ ИнвентаризацияТоваровТовары.СчетУчета.Забалансовый)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаПоУчету
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, НЕ Счет В (&ИсклСчета), &ПорядокСубконтоКоличество, Организация = &Организация) КАК ХозрасчетныйОстатки
		|ГДЕ
		|	ХозрасчетныйОстатки.КоличествоОстаток > 0";
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		Выборка = РезультатыЗапроса[0].Выбрать();
		Если Выборка.Следующий() Тогда
			Строка.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
		КонецЕсли;
		Выборка = РезультатыЗапроса[1].Выбрать();
		Если Выборка.Следующий() Тогда
			Строка.СуммаПоУчету = Выборка.СуммаПоУчету;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗаполнитьСтрокуНЗП(Строка, СчетаУчета, СчетУчетаПодраздела, ПодразделениеПодраздела, Запрос)
	
	Строка.Описание = НСтр("ru='документы ""Инвентаризация НЗП"", ""Инвентаризация объектов учета""'");
	Строка.ПереченьСчетов = НСтр("ru='счета учета: 20, 23'");
	Строка.НаименованиеДокумента = "ИнвентаризацияНЗП";
	СтрокаПодразделаПоСчетуУчета = Неопределено;
	СтрокаПодразделаПоПодразделению = Неопределено;
	ВестиУчетЗатратПоПодразделениям = ПолучитьФункциональнуюОпцию("ВестиУчетЗатратПоПодразделениям");
	ОчиститьСтроки = Ложь;
	Если СчетУчетаПодраздела <> Неопределено Тогда
		СтрокаПодразделаПоСчетуУчета = Строка.Строки.Найти(СчетУчетаПодраздела, "СчетУчетаПодраздела");
		Если ВестиУчетЗатратПоПодразделениям И ПодразделениеПодраздела <> Неопределено И СтрокаПодразделаПоСчетуУчета <> Неопределено Тогда
			СтрокаПодразделаПоПодразделению = СтрокаПодразделаПоСчетуУчета.Строки.Найти(ПодразделениеПодраздела, "ПодразделениеПодраздела");
			Если СтрокаПодразделаПоПодразделению = Неопределено Тогда
				ОчиститьСтроки = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если СтрокаПодразделаПоСчетуУчета = Неопределено Или ОчиститьСтроки Тогда
		Строка.Строки.Очистить();
	КонецЕсли;
	
	НастройкиУчетаЗатрат = Справочники.НастройкиУчетаЗатрат.БазовыеНастройки(Запрос.Параметры.ДатаОкончания, Запрос.Параметры.Организация);
	СуммаПоИнвентаризацииИтого = 0;
	СуммаПоУчетуИтого = 0;
	
	Для Каждого Счет Из СчетаУчета Цикл
		НастройкиЗакрытия = НастройкиУчетаЗатрат.Закрытие[Счет];
		ВидОперацииУчетнойПолитики = Справочники.НастройкиУчетаЗатрат.ИспользованиеДокументаИнвентаризацияНезавершенногоПроизводства(
			Запрос.Параметры.ДатаОкончания, Счет, Запрос.Параметры.Организация);
		СуммаПоИнвентаризации = 0;
		СуммаПоУчету = 0;
		Запрос.УстановитьПараметр("СчетУчета", Счет);
		Запрос.УстановитьПараметр("ДатаОстатков", Строка.ДатаОстатков+1);
		
		Если Не ЗначениеЗаполнено(ВидОперацииУчетнойПолитики)
			Или НастройкиЗакрытия.НезавершенноеПроизводство = Перечисления.ВариантыУчетаНезавершенногоПроизводства.ВедетсяБезИнвентаризации Тогда
			Запрос.УстановитьПараметр("РазделУчета", Строка.Раздел);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИнвентаризацияОбъектовУчета.Ссылка КАК Ссылка,
			|	СУММА(ИнвентаризацияОбъектовУчетаОбъектыУчета.Сумма) КАК СуммаПоИнвентаризации
			|ИЗ
			|	Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ИнвентаризацияОбъектовУчетаОбъектыУчета
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета КАК ИнвентаризацияОбъектовУчета
			|		ПО ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка = ИнвентаризацияОбъектовУчета.Ссылка
			|ГДЕ
			|	ИнвентаризацияОбъектовУчета.Проведен
			|	И ИнвентаризацияОбъектовУчета.Организация = &Организация
			|	И ИнвентаризацияОбъектовУчета.РазделУчета = &РазделУчета
			|	И ИнвентаризацияОбъектовУчета.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета = &СчетУчета
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнвентаризацияОбъектовУчета.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаПоУчету
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет = &СчетУчета, , Организация = &Организация) КАК ХозрасчетныйОстатки";
			
			РезультатыЗапроса = Запрос.ВыполнитьПакет();
			Выборка = РезультатыЗапроса[0].Выбрать();
			Пока Выборка.Следующий() Цикл
				СуммаПоИнвентаризации = СуммаПоИнвентаризации + Выборка.СуммаПоИнвентаризации;
			КонецЦикла;
			Выборка = РезультатыЗапроса[1].Выбрать();
			Если Выборка.Следующий() Тогда
				СуммаПоУчету = Выборка.СуммаПоУчету;
			КонецЕсли;
			Если СуммаПоИнвентаризации <> 0 Или СуммаПоУчету <> 0 Тогда
				Если СтрокаПодразделаПоСчетуУчета <> Неопределено Тогда
					Если Счет = СтрокаПодразделаПоСчетуУчета.СчетУчетаПодраздела Тогда
						СтрокаПодразделаПоСчетуУчета.СуммаПоИнвентаризации = СуммаПоИнвентаризации;
						СтрокаПодразделаПоСчетуУчета.СуммаПоУчету = СуммаПоУчету;
						ЗаполнитьКартинкуСтатус(СтрокаПодразделаПоСчетуУчета);
					КонецЕсли
				Иначе
					СтрокаПодраздела = Строка.Строки.Добавить();
					СтрокаПодраздела.Раздел = Строка.Раздел;
					СтрокаПодраздела.НаименованиеДокумента = "ИнвентаризацияОбъектовУчета";
					РеквизитыСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Счет, "Код, Наименование");
					СтрокаПодраздела.ИмяРаздела = СтрШаблон(НСтр("ru='Счет %1 ""%2""'"), РеквизитыСчета.Код, РеквизитыСчета.Наименование);
					СтрокаПодраздела.ВерхнийУровень = Ложь;
					СтрокаПодраздела.СуммаПоИнвентаризации = СуммаПоИнвентаризации;
					СтрокаПодраздела.СуммаПоУчету = СуммаПоУчету;
					СтрокаПодраздела.СчетУчетаПодраздела = Счет;
					СтрокаПодраздела.ДатаОстатков = Строка.ДатаОстатков;
					ЗаполнитьКартинкуСтатус(СтрокаПодраздела);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИнвентаризацияНЗП.ПодразделениеОрганизации КАК Подразделение,
			|	ИнвентаризацияНЗП.ПодразделениеОрганизации.Наименование КАК НаименованиеПодразделения,
			|	СУММА(ЕСТЬNULL(ИнвентаризацияНЗПСостав.Количество, 0)) КАК КоличествоПоИнвентаризации
			|ПОМЕСТИТЬ ДокументыПоСчету
			|ИЗ
			|	Документ.ИнвентаризацияНЗП КАК ИнвентаризацияНЗП
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияНЗП.СоставПоПродукции КАК ИнвентаризацияНЗПСостав
			|		ПО ИнвентаризацияНЗП.Ссылка = ИнвентаризацияНЗПСостав.Ссылка
			|ГДЕ
			|	ИнвентаризацияНЗП.Проведен
			|	И ИнвентаризацияНЗП.Организация = &Организация
			|	И ИнвентаризацияНЗП.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И ИнвентаризацияНЗП.СчетЗатрат = &СчетУчета
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнвентаризацияНЗП.ПодразделениеОрганизации,
			|	ИнвентаризацияНЗП.ПодразделениеОрганизации.Наименование
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Подразделение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаПоУчету,
			|	ЕСТЬNULL(ХозрасчетныйОстатки.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК Подразделение,
			|	ХозрасчетныйОстатки.Подразделение.Наименование КАК НаименованиеПодразделения
			|ПОМЕСТИТЬ ОстаткиПоСчету
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет = &СчетУчета, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы), Организация = &Организация) КАК ХозрасчетныйОстатки
			|
			|СГРУППИРОВАТЬ ПО
			|	ХозрасчетныйОстатки.Подразделение.Наименование,
			|	ЕСТЬNULL(ХозрасчетныйОстатки.Подразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Подразделение
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(ДокументыПоСчету.КоличествоПоИнвентаризации, 0) КАК КоличествоПоИнвентаризации,
			|	ЕСТЬNULL(ОстаткиПоСчету.СуммаПоУчету, 0) КАК СуммаПоУчету,
			|	ЕСТЬNULL(ОстаткиПоСчету.Подразделение, ДокументыПоСчету.Подразделение) КАК Подразделение,
			|	ЕСТЬNULL(ОстаткиПоСчету.НаименованиеПодразделения, ДокументыПоСчету.НаименованиеПодразделения) КАК НаименованиеПодразделения
			|ИЗ
			|	ДокументыПоСчету КАК ДокументыПоСчету
			|		ПОЛНОЕ СОЕДИНЕНИЕ ОстаткиПоСчету КАК ОстаткиПоСчету
			|		ПО ДокументыПоСчету.Подразделение = ОстаткиПоСчету.Подразделение
			|
			|УПОРЯДОЧИТЬ ПО
			|	НаименованиеПодразделения
			|ИТОГИ
			|	СУММА(КоличествоПоИнвентаризации),
			|	СУММА(СуммаПоУчету)
			|ПО
			|	ОБЩИЕ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ДокументыПоСчету
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ОстаткиПоСчету";
			
			Если ВидОперацииУчетнойПолитики = Перечисления.ВидыОперацийИнвентаризацияНЗП.СуммоваяОценка Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "СоставПоПродукции", "Состав");
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Количество", "Сумма");
			КонецЕсли;
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаИтоги.Следующий() Цикл
				Если ВидОперацииУчетнойПолитики = Перечисления.ВидыОперацийИнвентаризацияНЗП.СуммоваяОценка Тогда
					СуммаПоИнвентаризации = ВыборкаИтоги.СуммаПоИнвентаризации;
				Иначе
					// Для НЗП с видом операции по количеству примем допущение, что сумма по инвентаризации равна сумме по учету,
					// т.к. в документе у нас есть данные только по количеству.
					СуммаПоИнвентаризации = ?(ВыборкаИтоги.КоличествоПоИнвентаризации = 0, 0, ВыборкаИтоги.СуммаПоУчету);
				КонецЕсли;
				СуммаПоУчету = ВыборкаИтоги.СуммаПоУчету;
				Если СуммаПоИнвентаризации <> 0 Или СуммаПоУчету <> 0 Тогда
					Если СтрокаПодразделаПоСчетуУчета <> Неопределено 
						И ?(ВестиУчетЗатратПоПодразделениям, СтрокаПодразделаПоПодразделению <> Неопределено, Истина) Тогда
						Если Счет = СтрокаПодразделаПоСчетуУчета.СчетУчетаПодраздела Тогда
							СтрокаПодразделаПоСчетуУчета.СуммаПоИнвентаризации = СуммаПоИнвентаризации;
							СтрокаПодразделаПоСчетуУчета.СуммаПоУчету = СуммаПоУчету;
							ЗаполнитьКартинкуСтатус(СтрокаПодразделаПоСчетуУчета);
						КонецЕсли
					Иначе
						СтрокаПодраздела = Строка.Строки.Добавить();
						СтрокаПодраздела.Раздел = Строка.Раздел;
						СтрокаПодраздела.НаименованиеДокумента = "ИнвентаризацияНЗП";
						РеквизитыСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Счет, "Код, Наименование");
						СтрокаПодраздела.ИмяРаздела = СтрШаблон(НСтр("ru='Счет %1 ""%2""'"), РеквизитыСчета.Код, РеквизитыСчета.Наименование);
						СтрокаПодраздела.ВерхнийУровень = Ложь;
						СтрокаПодраздела.СуммаПоИнвентаризации = СуммаПоИнвентаризации;
						СтрокаПодраздела.СуммаПоУчету = СуммаПоУчету;
						СтрокаПодраздела.СчетУчетаПодраздела = Счет;
						СтрокаПодраздела.ДатаОстатков = Строка.ДатаОстатков;
						ЗаполнитьКартинкуСтатус(СтрокаПодраздела);
					КонецЕсли;
					Если Не ВестиУчетЗатратПоПодразделениям Тогда
						Продолжить;
					КонецЕсли;
					Выборка = ВыборкаИтоги.Выбрать();
					Пока Выборка.Следующий() Цикл
						Если ВидОперацииУчетнойПолитики = Перечисления.ВидыОперацийИнвентаризацияНЗП.СуммоваяОценка Тогда
							СуммаПоИнвентаризацииПодразделения = Выборка.СуммаПоИнвентаризации;
						Иначе
							СуммаПоИнвентаризацииПодразделения = ?(Выборка.КоличествоПоИнвентаризации = 0, 0, Выборка.СуммаПоУчету);
						КонецЕсли;
						СуммаПоУчетуПодразделения = Выборка.СуммаПоУчету;
						Если СтрокаПодразделаПоСчетуУчета <> Неопределено И СтрокаПодразделаПоПодразделению <> Неопределено Тогда
							Если Выборка.Подразделение = СтрокаПодразделаПоПодразделению.ПодразделениеПодраздела И Счет = СтрокаПодразделаПоСчетуУчета.СчетУчетаПодраздела Тогда
								СтрокаПодразделаПоПодразделению.СуммаПоИнвентаризации = СуммаПоИнвентаризацииПодразделения;
								СтрокаПодразделаПоПодразделению.СуммаПоУчету = СуммаПоУчетуПодразделения;
								ЗаполнитьКартинкуСтатус(СтрокаПодразделаПоПодразделению);
							КонецЕсли;
						Иначе
							СтрокаПодразделаПодразделение = СтрокаПодраздела.Строки.Добавить();
							СтрокаПодразделаПодразделение.Раздел = Строка.Раздел;
							СтрокаПодразделаПодразделение.НаименованиеДокумента = "ИнвентаризацияНЗП";
							СтрокаПодразделаПодразделение.ИмяРаздела = СтрШаблон(НСтр("ru='Подразделение:  %1'"),Выборка.Подразделение);
							СтрокаПодразделаПодразделение.ВерхнийУровень = Ложь;
							СтрокаПодразделаПодразделение.СуммаПоИнвентаризации = СуммаПоИнвентаризацииПодразделения;
							СтрокаПодразделаПодразделение.СуммаПоУчету = СуммаПоУчетуПодразделения;
							СтрокаПодразделаПодразделение.СчетУчетаПодраздела = Счет;
							СтрокаПодразделаПодразделение.ПодразделениеПодраздела = Выборка.Подразделение;
							СтрокаПодразделаПодразделение.ДатаОстатков = Строка.ДатаОстатков;
							ЗаполнитьКартинкуСтатус(СтрокаПодразделаПодразделение);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		СуммаПоИнвентаризацииИтого = СуммаПоИнвентаризацииИтого + СуммаПоИнвентаризации;
		СуммаПоУчетуИтого = СуммаПоУчетуИтого + СуммаПоУчету;
	КонецЦикла;
	Строка.СуммаПоИнвентаризации = СуммаПоИнвентаризацииИтого;
	Строка.СуммаПоУчету = СуммаПоУчетуИтого;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуКасса(Строка, СчетаУчета, ПодразделениеПодраздела, Запрос)
	
	Строка.Описание = НСтр("ru='документ ""Инвентаризация кассы""'");
	Строка.НаименованиеДокумента = "ИнвентаризацияКассы";
	УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловиеОтбора.ИспользоватьВПроводках = Истина;
	СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаУчета, УсловиеОтбора);
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчетаСубсчета);
	Запрос.УстановитьПараметр("ДатаОстатков", Строка.ДатаОстатков+1);
	ФормироватьПоПодразделению = Справочники.ПодразделенияОрганизаций.ИспользуютсяОбособленныеПодразделенияНеВыделенныеНаОтдельныйБаланс(Запрос.Параметры.Организация);
	
	Если ФормироватьПоПодразделению Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ИнвентаризацияКассы.ФормироватьПоПодразделению
		|			ТОГДА ИнвентаризацияКассы.ПодразделениеОрганизации
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|	КОНЕЦ КАК Подразделение,
		|	СУММА(ЕСТЬNULL(ИнвентаризацияКассыЦенности.Сумма, 0)) КАК СуммаПоИнвентаризации
		|ПОМЕСТИТЬ ДокументыПоПодразделениям
		|ИЗ
		|	Документ.ИнвентаризацияКассы КАК ИнвентаризацияКассы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияКассы.Ценности КАК ИнвентаризацияКассыЦенности
		|		ПО ИнвентаризацияКассы.Ссылка = ИнвентаризацияКассыЦенности.Ссылка
		|ГДЕ
		|	ИнвентаризацияКассы.Проведен
		|	И ИнвентаризацияКассы.Организация = &Организация
		|	И ИнвентаризацияКассы.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ИнвентаризацияКассы.ФормироватьПоПодразделению
		|			ТОГДА ИнвентаризацияКассы.ПодразделениеОрганизации
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|	КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОстатки.Подразделение.ОбособленноеПодразделение
		|			ТОГДА ХозрасчетныйОстатки.Подразделение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|	КОНЕЦ КАК Подразделение,
		|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаПоУчету
		|ПОМЕСТИТЬ ОстаткиПоПодразделениям
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&СчетаУчета), , Организация = &Организация) КАК ХозрасчетныйОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОстатки.Подразделение.ОбособленноеПодразделение
		|			ТОГДА ХозрасчетныйОстатки.Подразделение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|	КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ОстаткиПоПодразделениям.Подразделение, ДокументыПоПодразделениям.Подразделение) КАК Подразделение,
		|	ЕСТЬNULL(ДокументыПоПодразделениям.СуммаПоИнвентаризации, 0) КАК СуммаПоИнвентаризации,
		|	ЕСТЬNULL(ОстаткиПоПодразделениям.СуммаПоУчету, 0) КАК СуммаПоУчету
		|ИЗ
		|	ОстаткиПоПодразделениям КАК ОстаткиПоПодразделениям
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДокументыПоПодразделениям КАК ДокументыПоПодразделениям
		|		ПО ОстаткиПоПодразделениям.Подразделение = ДокументыПоПодразделениям.Подразделение
		|
		|УПОРЯДОЧИТЬ ПО
		|	Подразделение
		|ИТОГИ
		|	СУММА(СуммаПоИнвентаризации),
		|	СУММА(СуммаПоУчету)
		|ПО
		|	ОБЩИЕ
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		РезультатЗапроса = Запрос.Выполнить();
		СтрокаПодразделаПоПодразделению = Неопределено;
		Если ПодразделениеПодраздела <> Неопределено Тогда
			СтрокаПодразделаПоПодразделению = Строка.Строки.Найти(ПодразделениеПодраздела, "ПодразделениеПодраздела");
		КонецЕсли;
		Если СтрокаПодразделаПоПодразделению = Неопределено Тогда
			Строка.Строки.Очистить();
		КонецЕсли;
		ВыборкаИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаИтоги.Следующий() Тогда
			Строка.СуммаПоУчету = ВыборкаИтоги.СуммаПоУчету;
			Строка.СуммаПоИнвентаризации = ВыборкаИтоги.СуммаПоИнвентаризации;
			Выборка = ВыборкаИтоги.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если СтрокаПодразделаПоПодразделению <> Неопределено Тогда
					Если Выборка.Подразделение = СтрокаПодразделаПоПодразделению.ПодразделениеПодраздела Тогда
						СтрокаПодразделаПоПодразделению.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
						СтрокаПодразделаПоПодразделению.СуммаПоУчету = Выборка.СуммаПоУчету;
						ЗаполнитьКартинкуСтатус(СтрокаПодразделаПоПодразделению);
					КонецЕсли
				Иначе
					СтрокаПодраздела = Строка.Строки.Добавить();
					СтрокаПодраздела.Раздел = Строка.Раздел;
					СтрокаПодраздела.НаименованиеДокумента = "ИнвентаризацияКассы";
					СтрокаПодраздела.ДатаОстатков = Строка.ДатаОстатков;
					Если ЗначениеЗаполнено(Выборка.Подразделение) Тогда
						СтрокаПодраздела.ИмяРаздела = Выборка.Подразделение;
					Иначе
						СтрокаПодраздела.ИмяРаздела = НСтр("ru='По головному подразделению'");
					КонецЕсли;
					СтрокаПодраздела.ВерхнийУровень = Ложь;
					СтрокаПодраздела.СуммаПоУчету = Выборка.СуммаПоУчету;
					СтрокаПодраздела.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
					СтрокаПодраздела.ПодразделениеПодраздела = Выборка.Подразделение;
					ЗаполнитьКартинкуСтатус(СтрокаПодраздела);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		ОпределитьДокументыПоИнвентаризации(Строка.НаименованиеДокумента, Запрос);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ЕСТЬNULL(ИнвентаризацияКассыЦенности.Сумма, 0)) КАК СуммаПоИнвентаризации
		|ИЗ
		|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияКассы.Ценности КАК ИнвентаризацияКассыЦенности
		|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияКассыЦенности.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК СуммаПоУчету,
		|	ХозрасчетныйОстатки.Счет КАК Счет
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&СчетаУчета), , Организация = &Организация) КАК ХозрасчетныйОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстатки.Счет
		|ИТОГИ
		|	СУММА(СуммаПоУчету)
		|ПО
		|	ОБЩИЕ";
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		Выборка = РезультатыЗапроса[0].Выбрать();
		Если Выборка.Следующий() Тогда
			Строка.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
		КонецЕсли;
		Выборка = РезультатыЗапроса[1].Выбрать();
		Если Выборка.Следующий() Тогда
			Строка.СуммаПоУчету = Выборка.СуммаПоУчету;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуРасчетыСКонтрагентами(Строка, СчетаДляИнвентаризации, Запрос)
	
	Строка.Описание = НСтр("ru='документ ""Акт инвентаризации расчетов""'");
	Строка.НаименованиеДокумента = "ИнвентаризацияРасчетовСКонтрагентами";
	ОпределитьДокументыПоИнвентаризации(Строка.НаименованиеДокумента, Запрос);
	УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловиеОтбора.ИспользоватьВПроводках = Истина;
	СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаДляИнвентаризации.СчетаУчета, УсловиеОтбора);
	ИсключаемыеСчета = СчетаДляИнвентаризации.ИсключаемыеСчета;
	Запрос.УстановитьПараметр("СчетаУчета", ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаУчетаСубсчета, ИсключаемыеСчета));
	Запрос.УстановитьПараметр("ДатаОстатков", Строка.ДатаОстатков+1);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ЕСТЬNULL(ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Подтверждено, 0)) КАК СуммаПоИнвентаризации,
	|	ЕСТЬNULL(ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.ВидЗадолженности, ЗНАЧЕНИЕ(Перечисление.ВидыЗадолженности.ПустаяСсылка)) КАК ВидЗадолженности
	|ИЗ
	|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияРасчетовСКонтрагентами.Контрагенты КАК ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты
	|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.ВидЗадолженности
	|ИТОГИ
	|	СУММА(СуммаПоИнвентаризации)
	|ПО
	|	ОБЩИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	СУММА(ХозрасчетныйОстатки.СуммаРазвернутыйОстатокДт) КАК СуммаРазвернутыйОстатокДт,
	|	СУММА(ХозрасчетныйОстатки.СуммаРазвернутыйОстатокКт) КАК СуммаРазвернутыйОстатокКт,
	|	СУММА(ХозрасчетныйОстатки.СуммаОстатокДт) КАК СуммаОстатокДт,
	|	СУММА(ХозрасчетныйОстатки.СуммаОстатокКт) КАК СуммаОстатокКт,
	|	ХозрасчетныйОстатки.Счет.Вид КАК СчетВид,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ХозрасчетныйОстатки.Субконто1) КАК Субконто1,
	|	ХозрасчетныйОстатки.Счет.Код КАК СчетКод
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&СчетаУчета), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Счет,
	|	ХозрасчетныйОстатки.Счет.Вид,
	|	ХозрасчетныйОстатки.Счет.Код
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетКод";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатыЗапроса[0].Выбрать();
	Если Выборка.Следующий() Тогда
		Строка.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
	КонецЕсли;
	Выборка = РезультатыЗапроса[1].Выбрать();
	СуммаДебиторская = 0;
	СуммаКредиторская = 0;
	Пока Выборка.Следующий() Цикл
		Если Выборка.СчетВид = ВидСчета.Активный Тогда
			СуммаДебиторская = СуммаДебиторская + Выборка.СуммаРазвернутыйОстатокДт;
		ИначеЕсли Выборка.СчетВид = ВидСчета.Пассивный Тогда
			СуммаКредиторская = СуммаКредиторская + Выборка.СуммаРазвернутыйОстатокКт;
		Иначе
			СуммаДебиторская = СуммаДебиторская + Выборка.СуммаРазвернутыйОстатокДт;
			СуммаКредиторская = СуммаКредиторская + Выборка.СуммаРазвернутыйОстатокКт;
		КонецЕсли;
	КонецЦикла;
	Строка.СуммаПоУчету = СуммаДебиторская + СуммаКредиторская;
	
КонецПроцедуры

Процедура ЗаполнитьСтрокуПрочиеЗабалансовые(Строка, СчетаДляИнвентаризации, СчетУчетаПодраздела, Запрос)
	
	Строка.ПереченьСчетов = "";
	Строка.НаименованиеДокумента = "ИнвентаризацияОбъектовУчета";
	Запрос.УстановитьПараметр("РазделУчета", Строка.Раздел);
	ОпределитьДокументыПоИнвентаризации(Строка.НаименованиеДокумента, Запрос, Строка.Раздел);
	УсловиеОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловиеОтбора.ИспользоватьВПроводках = Истина;
	СчетаУчетаСубсчета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаДляИнвентаризации.СчетаУчета, УсловиеОтбора);
	Если Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета Тогда
		Строка.Описание = НСтр("ru='документ ""Инвентаризация объектов учета"" по прочим счетам'");
		// В документе "Инвентаризация объектов учета" есть возможность выбрать счет не из типового списка по разделу,
		// нужно добавить эти счета в список счетов.
		ДополнитьПрочиеСчета(СчетаУчетаСубсчета, Запрос, Строка.ПрочиеСчетаНепредопределенные);
	КонецЕсли;
	Если Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
		Строка.Описание = НСтр("ru='документ ""Инвентаризация объектов учета"" по забалансовым счетам'");
	КонецЕсли;
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчетаСубсчета);
	Запрос.УстановитьПараметр("ДатаОстатков", Строка.ДатаОстатков+1);
	
	ТекстыЗапроса = Новый Массив;
	Если Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	СУММА(ИнвентаризацияОбъектовУчетаОбъектыУчета.Сумма) КАК СуммаПоИнвентаризации,
		|	ВЫБОР
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель
		|		ИНАЧЕ ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель.Родитель
		|	КОНЕЦ КАК СчетУчетаПодраздела
		|ПОМЕСТИТЬ ДокументыПоСчетам
		|ИЗ
		|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ИнвентаризацияОбъектовУчетаОбъектыУчета
		|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель
		|		ИНАЧЕ ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель.Родитель
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ИнвентаризацияТоваровНаСкладеТовары.Сумма),
		|	ВЫБОР
		|		КОГДА ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияТоваровНаСкладеТовары.СчетУчета
		|		КОГДА ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Родитель
		|		ИНАЧЕ ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Родитель.Родитель
		|	КОНЕЦ
		|ИЗ
		|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
		|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияТоваровНаСкладеТовары.Ссылка
		|ГДЕ
		|	ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Забалансовый
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияТоваровНаСкладеТовары.СчетУчета
		|		КОГДА ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Родитель
		|		ИНАЧЕ ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Родитель.Родитель
		|	КОНЕЦ");
	Иначе
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	СУММА(ИнвентаризацияОбъектовУчетаОбъектыУчета.Сумма) КАК СуммаПоИнвентаризации,
		|	ВЫБОР
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыполнениеРемонтовОС)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель
		|		ИНАЧЕ ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель.Родитель
		|	КОНЕЦ КАК СчетУчетаПодраздела
		|ПОМЕСТИТЬ ДокументыПоСчетам
		|ИЗ
		|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ИнвентаризацияОбъектовУчетаОбъектыУчета
		|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыполнениеРемонтовОС)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета
		|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
		|			ТОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель
		|		ИНАЧЕ ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета.Родитель.Родитель
		|	КОНЕЦ");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет.Вид <> ЗНАЧЕНИЕ(ВидСчета.Активный)
	|					И ХозрасчетныйОстатки.СуммаОстаток < 0
	|				ТОГДА -ХозрасчетныйОстатки.СуммаОстаток
	|			ИНАЧЕ ХозрасчетныйОстатки.СуммаОстаток
	|		КОНЕЦ) КАК СуммаОстаток,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости)
	|			ТОГДА ХозрасчетныйОстатки.Счет
	|		КОГДА ХозрасчетныйОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыполнениеРемонтовОС)
	|			ТОГДА ХозрасчетныйОстатки.Счет
	|		КОГДА ХозрасчетныйОстатки.Счет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Счет
	|		КОГДА ХозрасчетныйОстатки.Счет.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Счет.Родитель
	|		ИНАЧЕ ХозрасчетныйОстатки.Счет.Родитель.Родитель
	|	КОНЕЦ КАК Счет
	|ПОМЕСТИТЬ ОстаткиПоСчетам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&СчетаУчета), , Организация = &Организация) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости)
	|			ТОГДА ХозрасчетныйОстатки.Счет
	|		КОГДА ХозрасчетныйОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыполнениеРемонтовОС)
	|			ТОГДА ХозрасчетныйОстатки.Счет
	|		КОГДА ХозрасчетныйОстатки.Счет.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Счет
	|		КОГДА ХозрасчетныйОстатки.Счет.Родитель.Родитель = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			ТОГДА ХозрасчетныйОстатки.Счет.Родитель
	|		ИНАЧЕ ХозрасчетныйОстатки.Счет.Родитель.Родитель
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ОстаткиПоСчетам.Счет, ДокументыПоСчетам.СчетУчетаПодраздела) КАК Счет,
	|	ЕСТЬNULL(ОстаткиПоСчетам.СуммаОстаток, 0) КАК СуммаПоУчету,
	|	ЕСТЬNULL(ДокументыПоСчетам.СуммаПоИнвентаризации, 0) КАК СуммаПоИнвентаризации,
	|	ЕСТЬNULL(ОстаткиПоСчетам.Счет.Вид, ДокументыПоСчетам.СчетУчетаПодраздела.Вид) КАК СчетВид,
	|	ЕСТЬNULL(ОстаткиПоСчетам.Счет.Код, ДокументыПоСчетам.СчетУчетаПодраздела.Код) КАК СчетКод,
	|	ЕСТЬNULL(ОстаткиПоСчетам.Счет.Наименование, ДокументыПоСчетам.СчетУчетаПодраздела.Наименование) КАК СчетНаименование,
	|	ЕСТЬNULL(ОстаткиПоСчетам.Счет.Порядок, ДокументыПоСчетам.СчетУчетаПодраздела.Порядок) КАК СчетПорядок
	|ИЗ
	|	ОстаткиПоСчетам КАК ОстаткиПоСчетам
	|		ПОЛНОЕ СОЕДИНЕНИЕ ДокументыПоСчетам КАК ДокументыПоСчетам
	|		ПО ОстаткиПоСчетам.Счет = ДокументыПоСчетам.СчетУчетаПодраздела
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетПорядок
	|ИТОГИ
	|	СУММА(СуммаПоУчету),
	|	СУММА(СуммаПоИнвентаризации)
	|ПО
	|	ОБЩИЕ");
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	СтрокаПодразделаПоСчетуУчета = Неопределено;
	Если СчетУчетаПодраздела <> Неопределено Тогда
		СтрокаПодразделаПоСчетуУчета = Строка.Строки.Найти(СчетУчетаПодраздела, "СчетУчетаПодраздела");
	КонецЕсли;
	Если СтрокаПодразделаПоСчетуУчета = Неопределено Тогда
		Строка.Строки.Очистить();
	КонецЕсли;
	Если ВыборкаИтоги.Следующий() Тогда
		Строка.СуммаПоУчету = ВыборкаИтоги.СуммаПоУчету;
		Строка.СуммаПоИнвентаризации = ВыборкаИтоги.СуммаПоИнвентаризации;
		Выборка = ВыборкаИтоги.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если СтрокаПодразделаПоСчетуУчета <> Неопределено Тогда
				Если Выборка.Счет = СтрокаПодразделаПоСчетуУчета.СчетУчетаПодраздела Тогда
					СтрокаПодразделаПоСчетуУчета.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
					СтрокаПодразделаПоСчетуУчета.СуммаПоУчету = Выборка.СуммаПоУчету;
					ЗаполнитьКартинкуСтатус(СтрокаПодразделаПоСчетуУчета);
				КонецЕсли
			Иначе
				СтрокаПодраздела = Строка.Строки.Добавить();
				СтрокаПодраздела.Раздел = Строка.Раздел;
				СтрокаПодраздела.НаименованиеДокумента = "ИнвентаризацияОбъектовУчета";
				СтрокаПодраздела.ИмяРаздела = СтрШаблон(НСтр("ru='Счет %1 ""%2""'"),Выборка.СчетКод, Выборка.СчетНаименование);
				СтрокаПодраздела.ВерхнийУровень = Ложь;
				СтрокаПодраздела.СуммаПоИнвентаризации = Выборка.СуммаПоИнвентаризации;
				СтрокаПодраздела.СуммаПоУчету = Выборка.СуммаПоУчету;
				СтрокаПодраздела.СчетУчетаПодраздела = Выборка.Счет;
				СтрокаПодраздела.ДатаОстатков = Строка.ДатаОстатков;
				Если Строка.Раздел = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
					Если СчетаДляИнвентаризации.ИсключаемыеСчета.Найти(Выборка.Счет) <> Неопределено Тогда
						СтрокаПодраздела.Описание = "документ ""Инвентаризация товаров""";
						СтрокаПодраздела.НаименованиеДокумента = "ИнвентаризацияТоваровНаСкладе";
					КонецЕсли;
				КонецЕсли;
				ЗаполнитьКартинкуСтатус(СтрокаПодраздела);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьДокументыПоИнвентаризацииИДатуОстатковПоОС(НаименованиеДокумента, Запрос, ДатаОстатков);

	ОпределитьДокументыПоИнвентаризацииИДатуОстатков(НаименованиеДокумента, Запрос, ДатаОстатков);
	
	Если Год(ДатаОстатков) <> Год(Запрос.Параметры.ДатаНачала) Тогда
		// Если есть свежие документы по инвентаризации ОС, то выкинем за старые года
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДокументыПоИнвентаризации.Ссылка КАК Ссылка,
		|	ДокументыПоИнвентаризации.Дата КАК Дата
		|ПОМЕСТИТЬ ДокументыПоИнвентаризацииОС
		|ИЗ
		|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
		|ГДЕ
		|	ДокументыПоИнвентаризации.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДокументыПоИнвентаризации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыПоИнвентаризацииОС.Ссылка КАК Ссылка,
		|	ДокументыПоИнвентаризацииОС.Дата КАК Дата
		|ПОМЕСТИТЬ ДокументыПоИнвентаризации
		|ИЗ
		|	ДокументыПоИнвентаризацииОС КАК ДокументыПоИнвентаризацииОС";
		
		Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(ДатаОстатков));
		Запрос.Выполнить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьДокументыПоИнвентаризацииИДатуОстатков(НаименованиеДокумента, Запрос, ДатаОстатков, РазделУчета = Неопределено);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИнвентаризацияДок.Ссылка КАК Ссылка,
	|	ИнвентаризацияДок.Дата КАК Дата
	|ПОМЕСТИТЬ ДокументыПоИнвентаризации
	|ИЗ
	|	&НаименованиеДокумента КАК ИнвентаризацияДок
	|ГДЕ
	|	ИнвентаризацияДок.Проведен
	|	И ИнвентаризацияДок.Организация = &Организация
	|	И ИнвентаризацияДок.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И &УсловиеРазделУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПоИнвентаризации.Дата КАК Дата
	|ИЗ
	|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&НаименованиеДокумента", СтрШаблон("Документ.%1", НаименованиеДокумента));
	Если ЗначениеЗаполнено(РазделУчета) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРазделУчета", "ИнвентаризацияДок.РазделУчета = &РазделУчета");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРазделУчета", "Истина");
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ДатаОстатков = Выборка.Дата;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьДокументыПоИнвентаризации(НаименованиеДокумента, Запрос, РазделУчета = Неопределено)
	
	Если РазделУчета = Перечисления.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнвентаризацияОбъектовУчета.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДокументыПоИнвентаризации
		|ИЗ
		|	Документ.ИнвентаризацияОбъектовУчета КАК ИнвентаризацияОбъектовУчета
		|ГДЕ
		|	ИнвентаризацияОбъектовУчета.Организация = &Организация
		|	И ИнвентаризацияОбъектовУчета.РазделУчета = &РазделУчета
		|	И ИнвентаризацияОбъектовУчета.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ИнвентаризацияОбъектовУчета.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка
		|ИЗ
		|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
		|ГДЕ
		|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Проведен
		|	И ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Организация = &Организация
		|	И ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Забалансовый
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИнвентаризацияДок.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДокументыПоИнвентаризации
		|ИЗ
		|	&НаименованиеДокумента КАК ИнвентаризацияДок
		|ГДЕ
		|	ИнвентаризацияДок.Проведен
		|	И ИнвентаризацияДок.Организация = &Организация
		|	И ИнвентаризацияДок.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И &УсловиеРазделУчета
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&НаименованиеДокумента", СтрШаблон("Документ.%1", НаименованиеДокумента));
	Если ЗначениеЗаполнено(РазделУчета) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРазделУчета", "ИнвентаризацияДок.РазделУчета = &РазделУчета");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеРазделУчета", "Истина");
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаполнитьКартинкуСтатус(Строка)
	
	Строка.ЕстьРасхождения = Ложь;
	Если Строка.СуммаПоИнвентаризации = 0 Тогда
		Строка.Картинка = БиблиотекаКартинок.СерыйШар;
		Строка.Статус = НСтр("ru = 'Не выполнено'");
	ИначеЕсли Строка.СуммаПоИнвентаризации <> Строка.СуммаПоУчету Тогда
		Если Строка.Строки.Количество() = 0 Тогда
			Строка.Картинка = БиблиотекаКартинок.ОперацияВыполненаСОшибками;
			Строка.ЕстьРасхождения = Истина;
			Строка.Статус = НСтр("ru = 'Есть расхождения'");
		Иначе
			Для Каждого СтрокаПодраздела Из Строка.Строки Цикл
				Если СтрокаПодраздела.СуммаПоИнвентаризации = 0 Тогда
					Строка.Картинка = БиблиотекаКартинок.СерыйШар;
					Строка.Статус = НСтр("ru = 'Не выполнено'");
					Прервать;
				ИначеЕсли СтрокаПодраздела.СуммаПоИнвентаризации <> СтрокаПодраздела.СуммаПоУчету Тогда
					Строка.Картинка = БиблиотекаКартинок.ОперацияВыполненаСОшибками;
					Строка.ЕстьРасхождения = Истина;
					Строка.Статус = НСтр("ru = 'Есть расхождения'");
					Прервать;
				Иначе
					Строка.Картинка = БиблиотекаКартинок.ИзмененияСохранены;
					Строка.Статус = НСтр("ru = 'Выполнено'");
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Строка.Картинка = БиблиотекаКартинок.ИзмененияСохранены;
		Строка.Статус = НСтр("ru = 'Выполнено'");
	КонецЕсли;
	
КонецПроцедуры

Функция ПереченьСчетов(СчетаУчета)
	
	СчетаУчаствующиеВРасчетах = Новый Массив;
	Для Каждого Счет Из СчетаУчета Цикл
		СчетаУчаствующиеВРасчетах.Добавить(Счет);
	КонецЦикла;
	
	Возврат СтрШаблон(НСтр("ru = 'счета учета: %1'"), СтрСоединить(СчетаУчаствующиеВРасчетах, ", "));

КонецФункции

Процедура ДополнитьПрочиеСчета(СчетаУчета, Запрос, ПрочиеСчетаНепредопределенные)
	
	Дата = Запрос.Параметры.ДатаОкончания;
	Организация = Запрос.Параметры.Организация;
	
	СчетаПоУмолчанию = ИнвентаризацияАктивовОбязательств.СчетаДляИнвентаризации(, Дата, Организация).СчетаУчета;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаПоУмолчанию, СчетаУчета);
	УсловияОтбора = БухгалтерскийУчет.НовыеУсловияОтбораСубсчетов();
	УсловияОтбора.ИспользоватьВПроводках = Истина;
	УсловияОтбора.Забалансовый = Ложь;
	СчетаПоУмолчаниюССубсчетами = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(СчетаПоУмолчанию, УсловияОтбора);
	УсловияОтбора.СчетаИсключения = СчетаПоУмолчаниюССубсчетами;
	Счета = БухгалтерскийУчет.СформироватьМассивСубсчетовПоОтбору(, УсловияОтбора);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета КАК СчетУчета
	|ИЗ
	|	ДокументыПоИнвентаризации КАК ДокументыПоИнвентаризации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ИнвентаризацияОбъектовУчетаОбъектыУчета
	|		ПО ДокументыПоИнвентаризации.Ссылка = ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка
	|ГДЕ
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета В(&СчетаУчета)";
	
	Запрос.УстановитьПараметр("СчетаУчета", Счета);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СчетаУчета.Добавить(Выборка.СчетУчета);
		ПрочиеСчетаНепредопределенные.Добавить(Выборка.СчетУчета);
	КонецЦикла;
	
КонецПроцедуры

Функция ПечатьРеестр(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	Результат = Новый ТабличныйДокумент;
	
	Результат.АвтоМасштаб          = Истина;
	Результат.ОриентацияСтраницы   = ОриентацияСтраницы.Ландшафт;
	Результат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПомощникИнвентаризации_Реестр";
	
	ПервыйДокумент = Истина;
	
	Для Каждого Организация Из МассивОбъектов Цикл
		
		Если ОбъектыПечати.НайтиПоЗначению(Организация) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ПервыйДокумент Тогда
			Результат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НачалоДокумента = Результат.ВысотаТаблицы + 1;
		
		ВывестиРеестрПоОрганизации(Результат, Организация, ПараметрыПечати);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			Результат,
			НачалоДокумента,
			ОбъектыПечати,
			Организация);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ВывестиРеестрПоОрганизации(Результат, Организация, ПараметрыПечати)
	
	Период = ПараметрыПечати.Период;
	Данные = ДанныеПечатиРеестра(Организация, КонецДня(Период), ПараметрыПечати.ДатаНачалаОС);
	
	Если Не ЗначениеЗаполнено(Данные) Тогда
		Возврат;
	КонецЕсли;
	
	// Собственно вывод реестра
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПомощникИнвентаризации.ПФ_MXL_Реестр");
	
	ДанныеОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Период);
	НаименованиеДляПечатныхФорм = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(ДанныеОрганизации, "НаименованиеДляПечатныхФорм");
	
	ПараметрыОрганизации = Новый Структура("Организация", НаименованиеДляПечатныхФорм);
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(Результат, Шапка, ПараметрыОрганизации);
	
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	Результат.Вывести(ЗаголовокТаблицы);
	
	СтрокаТаблицы = Макет.ПолучитьОбласть("Строка");
	ОбластьПроверкиВывода = Новый Массив;
	Если СтрокаТаблицы <> Неопределено Тогда
		ОбластьПроверкиВывода.Добавить(СтрокаТаблицы);
	КонецЕсли;
	НомерСтроки = 0;
	СуммаИтого = 0;
	Для Каждого Строка Из Данные Цикл
		НомерСтроки = НомерСтроки + 1;
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаТаблицы.Параметры.Заполнить(Новый Структура("НомерСтроки", НомерСтроки));
			НазваниеДокумента = Строка.Ссылка.Метаданные().Синоним;
			ЗаголовокДокумента = СтрШаблон(НСтр("ru = '%1 %2 от %3'"), НазваниеДокумента, Строка.Номер, Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
			СтрокаТаблицы.Параметры.Заполнить(Новый Структура("Документ", ЗаголовокДокумента));
		КонецЕсли;
		СтрокаТаблицы.Параметры.Расшифровка = ПолучитьНавигационнуюСсылку(Строка.Ссылка);
		Если ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(Результат, ОбластьПроверкиВывода) Тогда
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(Результат, СтрокаТаблицы, Строка);
		Иначе
			Результат.ВывестиГоризонтальныйРазделительСтраниц();
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(Результат, ЗаголовокТаблицы);
			ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(Результат, СтрокаТаблицы, Строка);
		КонецЕсли;
		СуммаИтого = СуммаИтого + Строка.Сумма;
	КонецЦикла;
	
	Итог = ИнвентаризацияАктивовОбязательств.ОбластьМакета(Макет, "Итог");
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(Результат, Итог, Новый Структура("Сумма", СуммаИтого));
	
	Подпись = Макет.ПолучитьОбласть("Подпись");
	
	ПараметрыПодписи = Новый Структура;
	ПараметрыПодписи.Вставить("ДолжностьПодписавшегоЛица", "");
	ПараметрыПодписи.Вставить("ДокументПодписал",          "");

	Пользователь = Пользователи.ТекущийПользователь();
	ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ФизическоеЛицо");
	Полномочия = ОтветственныеЛицаБП.ПолномочияОтветственного(ФизическоеЛицо, Организация, Период);
	ПараметрыПодписи.ДолжностьПодписавшегоЛица = Полномочия.ДолжностьПредставление;
	ПараметрыПодписи.ДокументПодписал          = Полномочия.ФИО.Представление;
	
	ИнвентаризацияАктивовОбязательств.ЗаполнитьПараметрыИВывестиОбласть(Результат, Подпись, ПараметрыПодписи);
	
КонецПроцедуры

Функция ДанныеПечатиРеестра(Организация, Период, ДатаНачалаОС)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.ОсновныеСредства) КАК Раздел,
	|	ИнвентаризацияОСОС.Ссылка КАК Ссылка,
	|	ИнвентаризацияОСОС.Ссылка.ПодразделениеОрганизации КАК Подразделение,
	|	ИнвентаризацияОСОС.Ссылка.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЕСТЬNULL(СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОСвОрганизации)) КАК Счет,
	|	СУММА(ИнвентаризацияОСОС.СтоимостьПоРезультатамИнвентаризации) КАК Сумма,
	|	0 КАК ПорядокРаздела,
	|	ИнвентаризацияОСОС.Ссылка.Номер КАК Номер,
	|	ИнвентаризацияОСОС.Ссылка.Дата КАК Дата
	|ИЗ
	|	Документ.ИнвентаризацияОС.ОС КАК ИнвентаризацияОСОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(&ДатаОкончания, Организация = &Организация) КАК СчетаБухгалтерскогоУчетаОССрезПоследних
	|		ПО ИнвентаризацияОСОС.ОсновноеСредство = СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство
	|ГДЕ
	|	ИнвентаризацияОСОС.Ссылка.Проведен
	|	И ИнвентаризацияОСОС.Ссылка.Организация = &Организация
	|	И ИнвентаризацияОСОС.Ссылка.Дата МЕЖДУ &ДатаНачалаОС И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияОСОС.Ссылка.ПодразделениеОрганизации,
	|	ЕСТЬNULL(СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОСвОрганизации)),
	|	ИнвентаризацияОСОС.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияОСОС.Ссылка,
	|	ИнвентаризацияОСОС.Ссылка.Номер,
	|	ИнвентаризацияОСОС.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.НематериальныеАктивы),
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка.ОтветственноеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	ЕСТЬNULL(СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НематериальныеАктивыОрганизации)),
	|	СУММА(ИнвентаризацияНМАОбъектыУчета.Стоимость),
	|	1,
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка.Номер,
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка.Дата
	|ИЗ
	|	Документ.ИнвентаризацияНМА.ОбъектыУчета КАК ИнвентаризацияНМАОбъектыУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(&ДатаОкончания, Организация = &Организация) КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|		ПО ИнвентаризацияНМАОбъектыУчета.Ссылка = СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив
	|ГДЕ
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка.Проведен
	|	И ИнвентаризацияНМАОбъектыУчета.Ссылка.Организация = &Организация
	|	И ИнвентаризацияНМАОбъектыУчета.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка,
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка.ОтветственноеЛицо,
	|	ЕСТЬNULL(СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НематериальныеАктивыОрганизации)),
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка.Номер,
	|	ИнвентаризацияНМАОбъектыУчета.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.Запасы),
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Склад,
	|	ИнвентаризацияТоваровНаСкладеТовары.СчетУчета,
	|	СУММА(ИнвентаризацияТоваровНаСкладеТовары.Сумма),
	|	2,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Номер,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
	|ГДЕ
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Проведен
	|	И ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Организация = &Организация
	|	И ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Забалансовый
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Склад,
	|	ИнвентаризацияТоваровНаСкладеТовары.СчетУчета,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Номер,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.ПодразделениеОрганизации,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.ОтветственноеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета,
	|	СУММА(ИнвентаризацияОбъектовУчетаОбъектыУчета.Сумма),
	|	3,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Номер,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Дата
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ИнвентаризацияОбъектовУчетаОбъектыУчета
	|ГДЕ
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Проведен
	|	И ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Организация = &Организация
	|	И ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = &РазделУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.ПодразделениеОрганизации,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Номер,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокРаздела,
	|	Подразделение,
	|	Склад,
	|	ОтветственноеЛицо,
	|	Счет
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Если КонецГода(Период) = Период Тогда
		Запрос.УстановитьПараметр("ДатаНачала", ДобавитьМесяц(НачалоМесяца(Период), -2));
		Запрос.УстановитьПараметр("ДатаНачалаОС", ДатаНачалаОС);
	Иначе
		Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Период));
		Запрос.УстановитьПараметр("ДатаНачалаОС", НачалоДня(Период));
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаОкончания", Период);
	Запрос.УстановитьПараметр("РазделУчета", Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения);

	РезультатЗапроса = Запрос.Выполнить();
	ДанныеСОктября = РезультатЗапроса.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета КАК Раздел,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка КАК Ссылка,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.ПодразделениеОрганизации КАК Подразделение,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.ОтветственноеЛицо КАК ОтветственноеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета КАК Счет,
	|	СУММА(ИнвентаризацияОбъектовУчетаОбъектыУчета.Сумма) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство)
	|			ТОГДА 0
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.НДСПоПриобретеннымЦенностям)
	|			ТОГДА 1
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.БанковскиеСчета)
	|			ТОГДА 3
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.ФинансовыеВложения)
	|			ТОГДА 4
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.ОтложенныеНалоговыеАктивыОбязательства)
	|			ТОГДА 6
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.ОценочныеОбязательства)
	|			ТОГДА 7
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.РасходыБудущихПериодов)
	|			ТОГДА 8
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.ДоходыБудущихПериодов)
	|			ТОГДА 9
	|		КОГДА ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.ПрочиеСчетаБухгалтерскогоУчета)
	|			ТОГДА 10
	|		ИНАЧЕ 11
	|	КОНЕЦ КАК ПорядокРаздела,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Номер КАК Номер,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Дата КАК Дата
	|ИЗ
	|	Документ.ИнвентаризацияОбъектовУчета.ОбъектыУчета КАК ИнвентаризацияОбъектовУчетаОбъектыУчета
	|ГДЕ
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Проведен
	|	И ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Организация = &Организация
	|	И ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета <> &РазделУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.ПодразделениеОрганизации,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.РазделУчета,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.СчетУчета,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Номер,
	|	ИнвентаризацияОбъектовУчетаОбъектыУчета.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство),
	|	ИнвентаризацияНЗПСостав.Ссылка,
	|	ИнвентаризацияНЗПСостав.Ссылка.ПодразделениеОрганизации,
	|	ИнвентаризацияНЗПСостав.Ссылка.ОтветственноеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	ИнвентаризацияНЗПСостав.Ссылка.СчетЗатрат,
	|	СУММА(ИнвентаризацияНЗПСостав.Сумма),
	|	0,
	|	ИнвентаризацияНЗПСостав.Ссылка.Номер,
	|	ИнвентаризацияНЗПСостав.Ссылка.Дата
	|ИЗ
	|	Документ.ИнвентаризацияНЗП.Состав КАК ИнвентаризацияНЗПСостав
	|ГДЕ
	|	ИнвентаризацияНЗПСостав.Ссылка.Проведен
	|	И ИнвентаризацияНЗПСостав.Ссылка.Организация = &Организация
	|	И ИнвентаризацияНЗПСостав.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияНЗПСостав.Ссылка,
	|	ИнвентаризацияНЗПСостав.Ссылка.СчетЗатрат,
	|	ИнвентаризацияНЗПСостав.Ссылка.ПодразделениеОрганизации,
	|	ИнвентаризацияНЗПСостав.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияНЗПСостав.Ссылка.Номер,
	|	ИнвентаризацияНЗПСостав.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.НезавершенноеПроизводство),
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Ссылка,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.ПодразделениеОрганизации,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.ОтветственноеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.СчетЗатрат,
	|	0,
	|	0,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Номер,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Дата
	|ИЗ
	|	Документ.ИнвентаризацияНЗП.СоставПоПродукции КАК ИнвентаризацияНЗПСоставПоПродукции
	|ГДЕ
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Проведен
	|	И ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Организация = &Организация
	|	И ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Ссылка,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.ПодразделениеОрганизации,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.СчетЗатрат,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Номер,
	|	ИнвентаризацияНЗПСоставПоПродукции.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.Касса),
	|	ИнвентаризацияКассыЦенности.Ссылка,
	|	ИнвентаризацияКассыЦенности.Ссылка.ПодразделениеОрганизации,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	ИнвентаризацияКассыЦенности.СчетУчета,
	|	СУММА(ИнвентаризацияКассыЦенности.Сумма),
	|	2,
	|	ИнвентаризацияКассыЦенности.Ссылка.Номер,
	|	ИнвентаризацияКассыЦенности.Ссылка.Дата
	|ИЗ
	|	Документ.ИнвентаризацияКассы.Ценности КАК ИнвентаризацияКассыЦенности
	|ГДЕ
	|	ИнвентаризацияКассыЦенности.Ссылка.Проведен
	|	И ИнвентаризацияКассыЦенности.Ссылка.Организация = &Организация
	|	И ИнвентаризацияКассыЦенности.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияКассыЦенности.Ссылка,
	|	ИнвентаризацияКассыЦенности.Ссылка.ПодразделениеОрганизации,
	|	ИнвентаризацияКассыЦенности.СчетУчета,
	|	ИнвентаризацияКассыЦенности.Ссылка.Номер,
	|	ИнвентаризацияКассыЦенности.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.РасчетыСКонтрагентами),
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.СчетРасчетов,
	|	СУММА(ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Подтверждено),
	|	5,
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка.Номер,
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка.Дата
	|ИЗ
	|	Документ.ИнвентаризацияРасчетовСКонтрагентами.Контрагенты КАК ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты
	|ГДЕ
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка.Проведен
	|	И ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка.Организация = &Организация
	|	И ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.СчетРасчетов,
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка,
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка.Номер,
	|	ИнвентаризацияРасчетовСКонтрагентамиКонтрагенты.Ссылка.Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаДляИнвентаризации.ЗабалансовыеСчета),
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Склад,
	|	ИнвентаризацияТоваровНаСкладеТовары.СчетУчета,
	|	СУММА(ИнвентаризацияТоваровНаСкладеТовары.Сумма),
	|	11,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Номер,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе.Товары КАК ИнвентаризацияТоваровНаСкладеТовары
	|ГДЕ
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Проведен
	|	И ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Организация = &Организация
	|	И ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ИнвентаризацияТоваровНаСкладеТовары.СчетУчета.Забалансовый
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.ОтветственноеЛицо,
	|	ИнвентаризацияТоваровНаСкладеТовары.СчетУчета,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Склад,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Номер,
	|	ИнвентаризацияТоваровНаСкладеТовары.Ссылка.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокРаздела,
	|	Подразделение,
	|	ОтветственноеЛицо,
	|	Счет
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", Период);
	Запрос.УстановитьПараметр("РазделУчета", Перечисления.РазделыУчетаДляИнвентаризации.КапитальныеВложения);

	РезультатЗапроса = Запрос.Выполнить();
	ДанныеНаОтчетнуюДату = РезультатЗапроса.Выгрузить();
	
	Данные = НовыйДанные();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеСОктября, Данные);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеНаОтчетнуюДату, Данные);

	Возврат Данные;
	
КонецФункции

Функция НовыйДанные() Экспорт
	
	КвалификаторЧисла = Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный);
	ТипСтрока = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(30));
	ТипДата = Новый ОписаниеТипов("Дата");
	
	СписокТипов = Новый Массив;
	СписокТипов.Добавить(Тип("ДокументСсылка.ИнвентаризацияОС"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ИнвентаризацияНМА"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ИнвентаризацияНЗП"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ИнвентаризацияКассы"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ИнвентаризацияРасчетовСКонтрагентами"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ИнвентаризацияТоваровНаСкладе"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ИнвентаризацияОбъектовУчета"));
	ТипДокумент = Новый ОписаниеТипов(СписокТипов);
	
	Данные = Новый ТаблицаЗначений();
	Данные.Колонки.Добавить("Раздел", Новый ОписаниеТипов("ПеречислениеСсылка.РазделыУчетаДляИнвентаризации"));
	Данные.Колонки.Добавить("Ссылка", ТипДокумент);
	Данные.Колонки.Добавить("Номер", ТипСтрока);
	Данные.Колонки.Добавить("Дата", ТипДата);
	Данные.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Данные.Колонки.Добавить("ОтветственноеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Данные.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	Данные.Колонки.Добавить("Счет", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	Данные.Колонки.Добавить("Сумма", БухгалтерскийУчетКлиентСервер.ТипСумма());
	Данные.Колонки.Добавить("ПорядокРаздела", Новый ОписаниеТипов("Число", КвалификаторЧисла));
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти

#КонецЕсли