#Область ПеременныеФормы

&НаКлиенте
Перем ВыполняетсяПовторноеРаспознаваниеДанныхИзБанка;

&НаКлиенте
Перем СтруктураПоискаКонтрагента;

&НаКлиенте
Перем ДокументыКИмпорту_ТекущаяСтрока;

// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
&НаКлиенте
Перем ОбменСБанкамиДепозиты_ПараметрыПредложений;
// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВключитьВыгрузкуПлатежныхТребований = Параметры.ВключитьВыгрузкуПлатежныхТребований;
	
	РасширениеРаботыСФайламиПодключено = Не ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	Если Параметры.Свойство("РежимЗагрузки") Тогда
		ЭтоЦифровойСчет = (Параметры.РежимЗагрузки = "ЦифровойСчет");
	КонецЕсли;
	Если НЕ (Параметры.Свойство("НачалоПериода") И Параметры.Свойство("КонецПериода")) Тогда
		Объект.НачалоПериода = ОбщегоНазначения.ТекущаяДатаПользователя();
		Объект.КонецПериода  = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	Если Параметры.Свойство("РежимПоУмолчанию") Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы[Параметры.РежимПоУмолчанию];
	КонецЕсли;
	
	Если Параметры.Свойство("СписокДокументов") Тогда
		СписокДокументов = Параметры.СписокДокументов;
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ИспользоватьСтатьиДДС          = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиДвиженияДенежныхСредств"); 
	
	ИспользуетсяСогласованиеПлатежей = УчетДенежныхСредствБП.ДоступностьСогласованияПлатежейПоБанковскомуСчету(Объект.БанковскийСчет);
		
	Если НЕ ЭтоМобильныйКлиент Тогда
		ИспользуетсяСогласованиеПлатежей = ПолучитьФункциональнуюОпцию("ИспользоватьСогласованиеПлатежей");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		ЭтоЦифровойСчет = Справочники.БанковскиеСчета.ЭтоЦифровойСчет(Объект.БанковскийСчет);
	КонецЕсли;
	
	ЦветОшибки = ЦветаСтиля.ПоясняющийПроблемуТекст;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыгрузка Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
		ДоступенПлатежОбменСЦифровымКошельком = ЦифровойРубль.ДоступенПлатежОбменСЦифровымКошельком(Период);
		Если ДоступенПлатежОбменСЦифровымКошельком Тогда
			УчетДенежныхСредствБП.УстановитьБанковскийСчет(
				Объект.БанковскийСчет, Объект.Организация, ВалютаРегламентированногоУчета,,,,, ЭтоЦифровойСчет);
		Иначе
			УчетДенежныхСредствБП.УстановитьБанковскийСчет(
				Объект.БанковскийСчет, Объект.Организация, ВалютаРегламентированногоУчета);
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаДокументов.Загрузить(Обработки.КлиентБанк.ВидыДокументовКлиентаБанка());
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		Объект.СоздаватьНенайденныеЭлементы = Истина;
	КонецЕсли;
	
	ВыпискиБанка.Очистить();
	
	ЭлектронныеВыпискиБанка = Новый Массив;
	ПереданоНесколькоВыписок = Ложь;
	// Разбор выписки при открытии КлиентБанка из формы электронного документа
	Если Параметры.Свойство("ЭлектроннаяВыпискаБанка")
		И ЗначениеЗаполнено(Параметры.ЭлектроннаяВыпискаБанка) Тогда
		Если ТипЗнч(Параметры.ЭлектроннаяВыпискаБанка) = Тип("ДокументСсылка.СообщениеОбменСБанками") Тогда
			СообщениеБанка = Параметры.ЭлектроннаяВыпискаБанка;
			ВыпискиБанка.Добавить(СообщениеБанка);
		ИначеЕсли ТипЗнч(Параметры.ЭлектроннаяВыпискаБанка) = Тип("СписокЗначений") Тогда
			ЭлектронныеВыпискиБанка = Параметры.ЭлектроннаяВыпискаБанка.ВыгрузитьЗначения();
			СообщениеБанка = ЭлектронныеВыпискиБанка[0];
			ВыпискиБанка.ЗагрузитьЗначения(ЭлектронныеВыпискиБанка);
			
			Если Не ЗначениеЗаполнено(Объект.БанковскийСчет)
				И ЭлектронныеВыпискиБанка.Количество() > 1
				И ИспользоватьНесколькоБанковскихСчетов() Тогда
				ПереданоНесколькоВыписок = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СообщениеБанка) Тогда
			Объект.НачалоПериода = СообщениеБанка.ДатаНачала;
			Объект.КонецПериода = ?(ЗначениеЗаполнено(СообщениеБанка.ДатаОкончания),
				СообщениеБанка.ДатаОкончания, СообщениеБанка.ДатаНачала);
			ЭлектроннаяВыпискаБанка = ТекстСсылкиНаЭлектронныеВыписки(Объект.НачалоПериода, Объект.КонецПериода);
		КонецЕсли;
		
		// должно идти раньше ЗагрузитьНастройкиДляБанковскогоСчета()
		АдресВыписки      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "АдресВыписки");
		АдресФайлаВыписки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "АдресФайлаВыписки");
	КонецЕсли;
	
	УстановитьОтборБанкиПрямогоОбмена();
	ЗагрузитьНастройкиДляБанковскогоСчета();
	
	Если ПереданоНесколькоВыписок Тогда
		ЭтоДиректБанкПоБанку = Ложь;
		НастройкаОбмена = Неопределено;
		НомерСчета = "";
		ДанныеВыписок = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ЭлектронныеВыпискиБанка,
			"НастройкаОбмена, НомерСчета, ДатаНачала, ДатаОкончания");
		Для Каждого КлючИЗначение Из ДанныеВыписок Цикл
			ДанныеВыписки = КлючИЗначение.Значение;
			Если ЗначениеЗаполнено(НастройкаОбмена) И НастройкаОбмена <> ДанныеВыписки.НастройкаОбмена Тогда
				ЭтоДиректБанкПоБанку = Ложь;
				Прервать;
			КонецЕсли;
			
			Объект.КонецПериода = Макс(Объект.КонецПериода, ДанныеВыписки.ДатаОкончания);
			Если ЗначениеЗаполнено(Объект.НачалоПериода) Тогда
				Объект.НачалоПериода = Мин(Объект.НачалоПериода, ДанныеВыписки.ДатаНачала);
			Иначе
				Объект.НачалоПериода = ДанныеВыписки.ДатаНачала;
			КонецЕсли;
			
			НастройкаОбмена = ДанныеВыписки.НастройкаОбмена;
			
			Если ЗначениеЗаполнено(НомерСчета) И НомерСчета <> ДанныеВыписки.НомерСчета Тогда
				ЭтоДиректБанкПоБанку = Истина;
			КонецЕсли;
			
			НомерСчета = ДанныеВыписки.НомерСчета;
		КонецЦикла;
		
		ЭлектроннаяВыпискаБанка = ТекстСсылкиНаЭлектронныеВыписки(Объект.НачалоПериода, Объект.КонецПериода);
		
		Если ЗначениеЗаполнено(НастройкаОбмена) Тогда
			Банк = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "Банк");
			Если ЭтоДиректБанкПоБанку Тогда
				СпособЗагрузки = "ДиректБанкПоБанку";
			ИначеЕсли Не ЗначениеЗаполнено(Объект.БанковскийСчет) И ЗначениеЗаполнено(Объект.Организация)
				И ЗначениеЗаполнено(НомерСчета) Тогда
				Объект.БанковскийСчет = Справочники.БанковскиеСчета.БанковскийСчетПоРеквизитам(
					Объект.Организация, Банк, НомерСчета);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("АдресХранилищаРаспознанныеДанныеИзБанка") Тогда
		ВыполненаЗагрузка = Ложь;
		УстановитьУсловноеОформление(); // должно идти раньше РазместитьНаФормеРаспознанныеДанныеИзБанка()
		РазместитьНаФормеРаспознанныеДанныеИзБанка(Параметры.АдресХранилищаРаспознанныеДанныеИзБанка);
		Если Объект.СоздаватьНенайденныеЭлементы И ЕстьОшибкиВДокументыКИмпорту() Тогда
			ПоказатьОшибкиНаСервере(Истина);
			ПоказатьОшибкиИмпорта = Истина;
			Элементы.ДокументыКИмпортуПоказатьОшибки.Пометка = Истина;
		КонецЕсли;
		Для Каждого СообщениеПользователю Из Параметры.СообщенияПользователюВФормеОбменСБанком Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю);
		КонецЦикла;
		ПереданыРаспознанныеДанныеИзБанка = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("ФайлЗагрузки") Тогда
		Объект.ФайлЗагрузки = Параметры.ФайлЗагрузки;
	КонецЕсли;
	
	Если Параметры.Свойство("УплатаНалогов") Тогда
		УплатаНалогов = Параметры.УплатаНалогов;
	КонецЕсли;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыгрузка Тогда
		ДлительнаяОперацияЗаполненияДокументовНаЭкспортПриОткрытии =
			ОбновитьДокументыНаЭкспортВФоне(Объект.ВыгружатьПлатежноеПоручение, Объект.ВыгружатьПлатежноеТребование);
	КонецЕсли;
	
	УстановитьУсловноеОформление(); // должно идти раньше УправлениеФормой()
	УправлениеФормой(ЭтотОбъект);
	
	ИзменитьПараметрыВыбораБанковскогоСчета(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Обработка.КлиентБанк",
		"Форма",
		НСтр("ru = 'Новости: Обмен с банком'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	Если ОбменСБанкамиДепозиты.ДоступенОбменДепозитами() И Не ЭтоМобильныйКлиент Тогда
		// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
		ДополнительныеПараметры = ОбменСБанкамиДепозиты.ПараметрыДепозитногоКалькулятора();
		ДополнительныеПараметры.ИмяГруппыРазмещения = "ГруппаДепозитыРасчеты";
		ДополнительныеПараметры.ПутьКРеквизитуОрганизация = "Объект.Организация";
		ДополнительныеПараметры.ИспользоватьКомандуДепозиты = Ложь;
		ДополнительныеПараметры.ВыводитьЗаголовокПанелиРазмещения = Ложь;
		ДополнительныеПараметры.ИмяЭлементаРеквизитаОрганизация = "ОрганизацияПрямойОбмен";
		ОбменСБанкамиДепозиты.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
		// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	Иначе
		Элементы.ПоказатьПанельИнформации.Видимость = Ложь;
	КонецЕсли;
	
	Если ЭтоМобильныйКлиент Тогда
		Если Параметры.Свойство("ДвоичныеДанныеФайла") Тогда
			СпособЗагрузки = "Файл";
			ЗагрузитьБанковскуюВыпискуИзФайлаМК(Параметры.ДвоичныеДанныеФайла);
		КонецЕсли;
		
		МобильныйКлиентАдаптацияФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	НастройкиЗагрузки(); // Превентивно прочитаем настройки, чтобы при необходимости они были сконвертированы
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВерсияФормата = "";
	
	ВыполняетсяПовторноеРаспознаваниеДанныхИзБанка = Ложь;
	ДокументыКИмпорту_ТекущаяСтрока = 0;
	
	Если Не ПустаяСтрока(АдресВыписки) И Не ПереданыРаспознанныеДанныеИзБанка Тогда
		ПрочитатьФайлВыпискиНаКлиенте();
	ИначеЕсли Не ПустаяСтрока(АдресФайлаВыписки) Тогда
		ПрочитатьФайлВыпискиНаКлиенте(Новый ОписаниеПередаваемогоФайла(, АдресФайлаВыписки));
	ИначеЕсли Не ПустаяСтрока(ИмяФайлаЗагрузкиМК) Тогда
		ПрочитатьФайлВыпискиНаКлиенте(Новый ОписаниеПередаваемогоФайла(ИмяФайлаЗагрузкиМК));
	КонецЕсли;
	
	Если ДлительнаяОперацияЗаполненияДокументовНаЭкспортПриОткрытии <> Неопределено Тогда
		ЗапуститьПроверкуФоновогоЗаданияОбновитьСписокДокументовНаЭкспорт(ДлительнаяОперацияЗаполненияДокументовНаЭкспортПриОткрытии);
		ДлительнаяОперацияЗаполненияДокументовНаЭкспортПриОткрытии = Неопределено;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПодключитьРасширениеРаботыСФайламиИПрочитатьФайл", 0.1, Истина);
	
	УправлениеФормой(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	ОбменСБанкамиДепозитыКлиент.ПриОткрытии(ЭтотОбъект);
	НастроитьОтображениеКомандыПоказатьПанельИнформации(Элементы.ГруппаДепозитыРасчеты.Видимость);
	//Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЭтотОбъект Тогда
		ОбновитьСписокДокументовНаЭкспорт();
	ИначеЕсли ИмяСобытия = "ОбновитьСостояниеОбменСБанками" Тогда
		ОбновитьСписокДокументовНаЭкспорт(Ложь);
	ИначеЕсли ИмяСобытия = "ИзменениеВыписки" И Источник <> ЭтотОбъект
		И Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗагрузка Тогда
		Если НЕ ВыполняетсяПовторноеРаспознаваниеДанныхИзБанка И ДокументыКИмпорту.Количество() > 0 Тогда
			ВыполняетсяПовторноеРаспознаваниеДанныхИзБанка = Истина;
			ПодключитьОбработчикОжидания("Подключаемый_РаспознатьДанныеБанкаПовторно", 1, Истина);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИзмененБанковскийСчет" И Параметр.Владелец = Объект.Организация Тогда
		Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
			ПриИзмененииБанковскийСчет();
		Иначе
			ПриИзмененииОрганизацияСервер();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "КлиентБанкОбновитьФорму" Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	Если ИмяСобытия = "ИзмененаНастройкаОбмена" Тогда
		ВидимостьЭлементовДиректБанк = ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(
			Объект.Организация, Банк);
		
		ПоказатьЭлементыДиректБанк(ЭтотОбъект);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	ОбменСБанкамиДепозитыКлиент.ОбработатьВыборУсловия(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора,
		ОбменСБанкамиДепозиты_ПараметрыПредложений);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	Если Не ЗавершениеРаботы Тогда
		ОбменСБанкамиДепозитыКлиент.ПередЗакрытием(ЭтотОбъект);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область КомандыВыгрузки

&НаКлиенте
Процедура КомандаВыгрузить(Команда)
	
	ОчиститьСообщения();
	Если Объект.ПлатежныеДокументы.Количество() > 0 И КоличествоКВыгрузке > 0 Тогда
		
		АдресВоВременномХранилище = ВыгрузитьДокументы();
		
		Если РасширениеРаботыСФайламиПодключено Тогда
			
			// Вариант для установленного расширения для работы с файлами
			
			Если НЕ ЗначениеЗаполнено(Объект.ФайлВыгрузки) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Не указан файл данных для выгрузки документов в банк'")
					,, "Объект.ФайлВыгрузки");
				Возврат;
			КонецЕсли;
			
			ВыгрузитьФайлНаКлиенте(Объект.ФайлВыгрузки, АдресВоВременномХранилище);
			
		Иначе
			// Веб клиент без расширения для работы с файлами
			Попытка
				
				ПолучитьФайл(АдресВоВременномХранилище, "1c_to_kl.txt", Истина);
				ДополнительныеПараметры = Новый Структура("ИмяФайла, АдресВоВременномХранилище",
					"", АдресВоВременномХранилище);
				ЗавершениеВыгрузкиФайла(ДополнительныеПараметры);
				
			Исключение
				ОписаниеОшибки = ИнформацияОбОшибке();
				ШаблонСообщения = НСтр("ru = 'При записи файла возникла ошибка
					|%1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
					КраткоеПредставлениеОшибки(ОписаниеОшибки));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				
				ЗаписатьОшибкуВЖурнал(ТекстСообщения, ОписаниеОшибки.Описание);
				
			КонецПопытки;
			
		КонецЕсли;
		
	Иначе
		
		Если Объект.ПлатежныеДокументы.Количество() = 0 Тогда
			Если Объект.НачалоПериода = Объект.КонецПериода Тогда
				ТекстПериода = Формат(Объект.НачалоПериода, "ДФ=dd.MM.yyyy");
			Иначе
				ТекстПериода = НСтр("ru = 'период с %1 по %2'");
				ТекстПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПериода,
					Формат(Объект.НачалоПериода, "ДФ=dd.MM.yyyy"),
					Формат(Объект.КонецПериода, "ДФ=dd.MM.yyyy"));
			КонецЕсли;
			
			ТекстСообщения = НСтр("ru = 'Отсутствуют платежные поручения за %1
				|по счету %2
				|(%3).
				|
				|Попробуйте изменить период или указать другой банковский счет.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения,
				ТекстПериода,
				Объект.БанковскийСчет,
				Объект.Организация);
		Иначе
			ТекстСообщения = НСтр("ru = 'Отсутствуют платежные поручения, отмеченные к выгрузке.
				|
				|Отметьте документы и повторите выгрузку.'");
		КонецЕсли;
		
		ПоказатьПредупреждение(, ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправить(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		ОтправитьЭД();
	Иначе
		ОтправитьВБанкМобильныйКлиент();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаНовыйПлатеж(Команда)
	
	ОповещениеЗакрытияФормыДокумента = Новый ОписаниеОповещения("ОбновитьПослеЗакрытияФормыНовогоДокумента", ЭтотОбъект);
	
	ОткрытьФорму("Документ.ПлатежноеПоручение.Форма.ФормаДокумента",, ЭтотОбъект,,,, ОповещениеЗакрытияФормыДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтчетПоВыгрузке(Команда)
	
	ПолеОтчета = Новый ТабличныйДокумент;
	ПолучитьОтчетОВыгрузке(ПолеОтчета);
	ПолеОтчета.ТолькоПросмотр = Истина;
	ПолеОтчета.АвтоМасштаб    = Истина;
	ПолеОтчета.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтчетПоВыгрузке";
	ПолеОтчета.Показать(НСтр("ru = 'Отчет о выгруженных платежных документах'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьВыгруженныйФайл(Элемент)
	
	ОбменСБанкомКлиент.ОткрытьФайлДляПросмотра(
		ЭтотОбъект,
		Новый Структура("ТекстРедактирования, Имя", Объект.ФайлВыгрузки, "ФайлВыгрузки"),
		?(ЭтоЦифровойСчет, "UTF-8", Объект.Кодировка),
		НСтр("ru = 'Файл выгрузки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДокументыНаЭкспорт(Команда)
	
	ОбновитьСписокДокументовНаЭкспорт();
	
КонецПроцедуры

// Команда устанавливает флажки во всех строках таблицы.
//
&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	ПлатежныеДокументыУстановитьОтметку(Истина);
	
КонецПроцедуры

// Команда снимает флажки во всех строках таблицы.
//
&НаКлиенте
Процедура СнятьОтметкуСоВсех(Команда)
	
	ПлатежныеДокументыУстановитьОтметку(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗагрузки

// Команда считывает данные из текстового файла.
//
&НаКлиенте
Процедура КомандаОбновитьИзВыписки(Команда)
	
	ОбновитьИзВыписки();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗапроситьЭлектроннуюВыпискуБанка(Команда)
	
	Если ЭтоМобильныйКлиент Тогда
		ОчиститьСообщения();
	КонецЕсли;
	
	ЗапроситьЭлектроннуюВыпискуБанка();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузить(Команда)
	
	ЗагрузитьВыпискуНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьЗагружаемыйФайл(Элемент)
	
	ОбменСБанкомКлиент.ОткрытьФайлДляПросмотра(
		ЭтотОбъект,
		Новый Структура("ТекстРедактирования, Имя", Объект.ФайлЗагрузки, "ФайлЗагрузки"),
		?(ЭтоЦифровойСчет, "UTF-8", Объект.Кодировка),
		НСтр("ru = 'Файл загрузки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтчетПоЗагрузке(Команда)
	
	ПолеОтчета = Новый ТабличныйДокумент;
	ПолучитьОтчетОЗагрузке(ПолеОтчета);
	ПолеОтчета.ТолькоПросмотр = Истина;
	ПолеОтчета.Показать(НСтр("ru = 'Отчет о загруженных документах'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНенайденное(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	АдресХранилищаКонтрагентов = АдресВременногоХранилищаДереваКонтрагентов();
	ПараметрыФормы.Вставить("АдресХранилищаКонтрагентов",              АдресХранилищаКонтрагентов);
	ПараметрыФормы.Вставить("АдресХранилищаРаспознанныеДанныеИзБанка", АдресХранилищаРаспознанныеДанныеИзБанка);
	ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьНенайденноеЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.КлиентБанк.Форма.ФормаСозданиеНенайденных", ПараметрыФормы,,,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДокументыКИмпортуКонтекстноеМенюКнопкаИзменить(Команда)
	
	Если НЕ ЗначениеЗаполнено(ДокументыКИмпорту) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Истина;
	ДокументыКИмпортуВыбор(Элементы.ДокументыКИмпорту, Элементы.ДокументыКИмпорту.ТекущаяСтрока,
		Элементы.ДокументыКИмпорту.ТекущийЭлемент, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		Элементы.ДокументыКИмпорту.ИзменитьСтроку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьИПрочитатьФайл(Команда)
	
	ОбновитьИзВыписки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибки(Команда)
	
	Элементы.ДокументыКИмпортуПоказатьОшибки.Пометка = НЕ Элементы.ДокументыКИмпортуПоказатьОшибки.Пометка;
	
	ПоказатьОшибкиИмпорта = Элементы.ДокументыКИмпортуПоказатьОшибки.Пометка;
	ПоказатьОшибкиНаСервере(ПоказатьОшибкиИмпорта);
	
КонецПроцедуры

// Команда устанавливает флажки во всех строках таблицы.
//
&НаКлиенте
Процедура ДокументыКИмпортуОтметитьВсе(Команда)
	
	ДокументыКИмпортуУстановитьОтметку(Истина);
	
КонецПроцедуры

// Команда снимает флажки во всех строках таблицы.
//
&НаКлиенте
Процедура ДокументыКИмпортуСнятьОтметкуСоВсех(Команда)
	
	ДокументыКИмпортуУстановитьОтметку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	Если ЭтоЦифровойСчет Тогда
		ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект,, "ЦифровойСчет");
	Иначе
		ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект, Объект.Кодировка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзСервисаАУСН(Команда)
	
	ИнтеграцияАУСНКлиент.ВыполнитьОбменССервисом(ЭтотОбъект, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбщиеКоманды

&НаКлиенте
Процедура РезультатПроведения(Команда)
	
	ТекущиеДанные = Элементы.ДокументыКИмпорту.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		ПараметрыФормы = Новый Структура("ДокументДвижений", ТекущиеДанные.Документ);
		ОткрытьФорму("Обработка.КорректировкаДвижений.Форма",
			ПараметрыФормы,
			Команда,
			Команда);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", Объект.НачалоПериода, Объект.КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора,,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастройка(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Организация");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.Организация");
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",             Объект.Организация);
	ПараметрыФормы.Вставить("БанковскийСчет",          Объект.БанковскийСчет);
	ПараметрыФормы.Вставить("Кодировка",               Объект.Кодировка);
	ПараметрыФормы.Вставить("ВозможностьВыбораФайлов", РасширениеРаботыСФайламиПодключено);
	ПараметрыФормы.Вставить("КешНастроекЗагрузки",     КешНастроекЗагрузки);
	
	Если Не ЗначениеЗаполнено(Объект.БанковскийСчет) И ЗначениеЗаполнено(Объект.СоглашениеПрямогоОбменаСБанками) Тогда
		ПараметрыФормы.Вставить("СоглашениеПрямогоОбменаСБанками", Объект.СоглашениеПрямогоОбменаСБанками);
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыполнитьНастройкуКлиентБанкаЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.КлиентБанк.Форма.ФормаНастройкиЗаполнения", ПараметрыФормы, ЭтотОбъект,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСогласованиеПлатежей(Команда)
	
	ПараметрыОткрытия = Новый Структура;   
	ПараметрыОткрытия.Вставить("Организация", Объект.Организация); 
	ПараметрыОткрытия.Вставить("БанковскийСчет", Объект.БанковскийСчет);  
	
	ОткрытьФорму("Обработка.СогласованиеПлатежей.Форма.Форма", ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПоказатьПанельИнформации(Команда)
	
	ИзменитьВидимостьПанелиИнформации();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПанельИнформации(Команда)
	
	ИзменитьВидимостьПанелиИнформации();
	
КонецПроцедуры

// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыВыполнитьКоманду(Команда)
	
	ОбменСБанкамиДепозитыКлиент.ВыполнитьКоманду(ЭтотОбъект, ОбменСБанкамиДепозиты_ПараметрыПредложений, Команда);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НачПериодаЗагрузкиПриИзменении(Элемент)
	
	ПроверитьИзменитьДатуОкончанияЗапросаВыписки();
	
	ОчиститьЭлектронныеВыписки();
	
КонецПроцедуры

&НаКлиенте
Процедура КонПериодаЗагрузкиПриИзменении(Элемент)
	
	ОчиститьЭлектронныеВыписки();
	
КонецПроцедуры

&НаКлиенте
Процедура НачПериодаПриИзменении(Элемент)
	
	ОбновитьСписокДокументовНаЭкспорт();
	
КонецПроцедуры

&НаКлиенте
Процедура КонПериодаПриИзменении(Элемент)
	
	ОбновитьСписокДокументовНаЭкспорт();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	// Запомним предыдущий путь к файлу
	ТекущийФайлЗагрузки = Объект.ФайлЗагрузки;
	ПриИзмененииОрганизацияСервер();
	
	ОчиститьЭлектронныеВыписки();
	ОбновитьСпискиДокументов();
	
	// Если файл не изменился, проверим возможно ничего перечитывать не требуется
	Если РасширениеРаботыСФайламиПодключено И ТекущийФайлЗагрузки = Объект.ФайлЗагрузки Тогда
		Если ЗначениеЗаполнено(Объект.БанковскийСчет) И
			СоответствиеВыпискиОтбору(АдресХранилищаРаспознанныеДанныеИзБанка, Объект.БанковскийСчет).Соответствует Тогда
			
			РазместитьНаФормеРаспознанныеДанныеИзБанка(АдресХранилищаРаспознанныеДанныеИзБанка, Ложь);
			
		КонецЕсли;
	Иначе // Загрузим новый файл
		ПодключитьОбработчикОжидания("Подключаемый_ПодключитьРасширениеРаботыСФайламиИПрочитатьФайл", 0.1, Истина);
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	
	ОбменСБанкамиДепозитыКлиент.ОбновитьПредложенияПриИзмененииОрганизации(ЭтотОбъект,
		ОбменСБанкамиДепозиты_ПараметрыПредложений);
	
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	
	ПриИзмененииБанковскийСчет();
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетЗагрузкаФайлОчистка(Элемент, СтандартнаяОбработка)
	
	ЭтоЦифровойСчет = Ложь;
	ПриИзмененииБанковскийСчет();
	
КонецПроцедуры

&НаКлиенте
Процедура БанкПрямойОбменПриИзменении(Элемент)
	
	ПриИзмененииБанк();
	
КонецПроцедуры

&НаКлиенте
Процедура БанкПрямойОбменОчистка(Элемент, СтандартнаяОбработка)
	
	ПриИзмененииБанк();
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыборФайлаДляВыгрузки(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлВыгрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСБанкомКлиент.ОткрытьФайлДляПросмотра(
		ЭтотОбъект,
		Новый Структура("ТекстРедактирования, Имя", Объект.ФайлВыгрузки, "ФайлВыгрузки"),
		?(ЭтоЦифровойСчет, "UTF-8", Объект.Кодировка),
		НСтр("ru = 'Файл выгрузки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЭтоЗагрузкаУниверсальная = Ложь;
	Если Не ЭтоЦифровойСчет
		И ЗначениеЗаполнено(Объект.Организация) И Не ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		ЭтоЗагрузкаУниверсальная = Истина;
	КонецЕсли;
	
	Если ЭтоЦифровойСчет Тогда
		ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект,, "ЦифровойСчет");
	ИначеЕсли ЭтоЗагрузкаУниверсальная Тогда
		ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект,, "Универсальная");;
	Иначе
		ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект, Объект.Кодировка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСБанкомКлиент.ОткрытьФайлДляПросмотра(
		ЭтотОбъект,
		Новый Структура("ТекстРедактирования, Имя", Объект.ФайлЗагрузки, "ФайлЗагрузки"),
		?(ЭтоЦифровойСчет, "UTF-8", Объект.Кодировка),
		НСтр("ru = 'Файл загрузки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлЗагрузкиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если НЕ ПустаяСтрока(Текст) Тогда
		Объект.ФайлЗагрузки = СокрЛП(Текст);
		ОбновитьСпискиДокументов();
		РежимЗагрузки = "";
		Если ЭтоЦифровойСчет Тогда
			РежимЗагрузки = "ЦифровойСчет";
		ИначеЕсли Не ЗначениеЗаполнено(Объект.БанковскийСчет) И ЗначениеЗаполнено(Объект.Организация) Тогда
			РежимЗагрузки = "Универсальная";
		КонецЕсли;
		ОбменСБанкомКлиент.ЗагрузитьВыбранныйФайл(Объект.ФайлЗагрузки, ЭтотОбъект, Объект.Кодировка, РежимЗагрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектроннаяВыпискаБанкаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыпискиБанка.Количество() > 1 Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВыпискиБанка", ВыпискиБанка);
		ПараметрыФормы.Вставить("НачалоПериода", Объект.НачалоПериода);
		ПараметрыФормы.Вставить("КонецПериода", Объект.КонецПериода);
		ПараметрыФормы.Вставить("НастройкаОбмена", Объект.СоглашениеПрямогоОбменаСБанками);
		ПараметрыФормы.Вставить("ЭтоДиректБанкПоБанку", СпособЗагрузки = "ДиректБанкПоБанку");
		
		ОткрытьФорму("Обработка.КлиентБанк.Форма.ФормаСпискаВыписок", ПараметрыФормы);
		
	ИначеЕсли ВыпискиБанка.Количество() = 1 Тогда
		
		ПараметрыФормы = Новый Структура("СообщениеОбмена", ВыпискиБанка[0].Значение);
		ОбменСБанкамиКлиент.ОткрытьФормуПросмотраЭлектронногоДокумента(ВыпискиБанка[0].Значение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ГруппаВыгрузка Тогда
		ОбновитьСписокДокументовНаЭкспорт();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыМКПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОчиститьСообщения();
	
	Элементы.ФормаНовыйПлатеж.Видимость = (ТекущаяСтраница = Элементы.ВыгрузкаМК);
	Элементы.КомандаОтправитьЭД.Видимость = (ТекущаяСтраница = Элементы.ВыгрузкаМК);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатуснаяСтрокаОшибкиПриВыгрузке2ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "e1cib/app/Документ.ПлатежноеПоручение.Форма.ФормаПроверкиПлатежныхРеквизитов" Тогда
		
		ТекущиеДанные = Элементы.ПлатежныеДокументы.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ПлатежныеРеквизиты = Новый Структура();
			ПлатежныеРеквизиты.Вставить("ИНН", ТекущиеДанные.КонтрагентИНН);
			ПлатежныеРеквизиты.Вставить("КПП", ТекущиеДанные.КонтрагентКПП);
			ПлатежныеРеквизиты.Вставить("ПолучательПлатежа", ТекущиеДанные.ТекстПолучателя);
			ПлатежныеРеквизиты.Вставить("РасчетныйСчет",     ТекущиеДанные.КонтрагентНомерСчета);
			ПлатежныеРеквизиты.Вставить("БИК",               ТекущиеДанные.КонтрагентБИКБанка);
			ПлатежныеРеквизиты.Вставить("КоррСчет",          СокрЛП(ТекущиеДанные.КонтрагентРасчСчет));
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("ПлатежныеРеквизиты", ПлатежныеРеквизиты);
			ПараметрыФормы.Вставить("БанковскийСчет",     ТекущиеДанные.КонтрагентСчет);
			ОписаниеОповещения = Новый ОписаниеОповещения("ИсправитьСчетаКонтрагентовЗавершение", ЭтотОбъект);
			ОткрытьФорму("Документ.ПлатежноеПоручение.Форма.ФормаПроверкиПлатежныхРеквизитов",
				ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,,, ОписаниеОповещения);
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияТекстРекламыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПодключитьДиректБанк" Тогда
		СтандартнаяОбработка = Ложь;
		ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(Объект.Организация, Банк, НомерСчета);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСкрытьРекламуДиректБанкНажатие(Элемент)
	
	ЭлектронноеВзаимодействиеБПВызовСервера.СохранитьНастройкуВидимостиРекламыДиректБанк(Банк, Ложь);
	ВидимостьЭлементовДиректБанк = "ПоказатьГиперссылку";
	ПоказатьЭлементыДиректБанк(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьДиректБанкНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСБанкамиКлиент.ОткрытьСоздатьНастройкуОбмена(Объект.Организация, Банк, НомерСчета);
	
КонецПроцедуры

#Область СогласованиеПлатежей

&НаКлиенте
Процедура ДекорацияСкрытьПодсказкуДиректБанкНажатие(Элемент)
	
	СохранитьНастройкиВидимостиПодсказкиОСогласовании();
	
	Элементы.ГруппаПодсказкаСогласование.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеПриИзменении(Элемент)
	
	Если ОтборСостояние = 0 Тогда
		Элементы.ПлатежныеДокументы.ОтборСтрок = Неопределено;
	ИначеЕсли ОтборСостояние = 1 Тогда
		Элементы.ПлатежныеДокументы.ОтборСтрок = Новый ФиксированнаяСтруктура(
			"Состояние", ПредопределенноеЗначение("Перечисление.СостоянияБанковскихДокументов.Согласовано"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодсказкаСогласованиеТекстОбработкаНавигационнойСсылки(Элемент,
																		  НавигационнаяСсылкаФорматированнойСтроки,
																		  СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьФормуФункциональности" Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ТекущаяСтраница", "ГруппаБанкКасса");
		ОткрытьФорму("Обработка.ФункциональностьПрограммы.Форма.ФормаФункциональностьПрограммы", ПараметрыОткрытия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыПриИзменении(Элемент)
	
	ОбменСБанкамиДепозитыКлиент.ДепозитыПриИзменении(ЭтотОбъект, Элемент, ОбменСБанкамиДепозиты_ПараметрыПредложений);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	ОбменСБанкамиДепозитыКлиент.ОбработкаВыбора(ЭтотОбъект, ОбменСБанкамиДепозиты_ПараметрыПредложений,
		Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ОбменСБанкамиДепозитыКлиент.ЗаполнитьДанныеВыбора(ЭтотОбъект, ОбменСБанкамиДепозиты_ПараметрыПредложений, 
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетВыгрузкаОчистка(Элемент, СтандартнаяОбработка)
	
	ЭтоЦифровойСчет = Ложь;
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыПлатежныеДокументы

&НаКлиенте
Процедура ПлатежныеДокументыПриАктивизацииСтроки(Элемент)
	
	Если НЕ ЕстьСтрокиЭкспортаСОшибками Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПлатежныеДокументыПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПлатежныеДокументыПриАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.ПлатежныеДокументы.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если СтрНайти(ТекущиеДанные.ОписаниеОшибок, "НеВерноУказаныПлатежныеРеквизиты") <> 0 Тогда
			СтатуснаяСтрокаОшибкиПриВыгрузке = РезультатПроверкиСчетаКонтрагентаОшибка();
		ИначеЕсли СтрНайти(ТекущиеДанные.ОписаниеОшибок, "РекомендуетсяОбновитьРеквизиты") <> 0 Тогда
			СтатуснаяСтрокаОшибкиПриВыгрузке = РезультатПроверкиСчетаКонтрагентаРекомендация();
		ИначеЕсли ТекущиеДанные.Готовность = 0 Тогда
			СтатуснаяСтрокаОшибкиПриВыгрузке = Новый ФорматированнаяСтрока(ТекущиеДанные.ОписаниеОшибок);
		Иначе
			СтатуснаяСтрокаОшибкиПриВыгрузке = Новый ФорматированнаяСтрока(ТекущиеДанные.ОписаниеОшибок, , ЦветОшибки);
		КонецЕсли;
		Элементы.ГруппаСообщенияОбОшибкеПриВыгрузкеСтраницы.ТекущаяСтраница = ?(ТекущиеДанные.Готовность = 0,
			Элементы.ГруппаСтраницаСтатусНетОшибки, Элементы.ГруппаСтраницаСтатусЕстьОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция РезультатПроверкиСчетаКонтрагентаОшибка()
	
	Подстроки = Новый Массив;
	
	Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Возможно, платежные реквизиты уплаты налога указаны неверно'"),
		, ЦветОшибки));
	Подстроки.Добавить("  ");
	Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Исправить'")
		,,,, "e1cib/app/Документ.ПлатежноеПоручение.Форма.ФормаПроверкиПлатежныхРеквизитов"));
	
	Возврат Новый ФорматированнаяСтрока(Подстроки);
	
КонецФункции

&НаКлиенте
Функция РезультатПроверкиСчетаКонтрагентаРекомендация()
	
	Подстроки = Новый Массив;
	
	Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'С 01.01.21 рекомендуются новые платежные реквизиты для уплаты налога'"),
		, ЦветОшибки));
	Подстроки.Добавить("  ");
	Подстроки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Исправить'")
		,,,, "e1cib/app/Документ.ПлатежноеПоручение.Форма.ФормаПроверкиПлатежныхРеквизитов"));
	
	Возврат Новый ФорматированнаяСтрока(Подстроки);
	
КонецФункции

&НаКлиенте
Процедура ПлатежныеДокументыВыгружатьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПлатежныеДокументы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Знак = ?(ТекущиеДанные.Выгружать, 1, -1);
	
	КоличествоКВыгрузке     = КоличествоКВыгрузке     + 1 * Знак;
	СуммаДокументаКВыгрузке = СуммаДокументаКВыгрузке + ТекущиеДанные.СуммаДокумента * Знак;
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.ПредставлениеИтоговВыгрузкиМК.Заголовок = СтрШаблон(НСтр("ru = 'К отправке: %1 На сумму: %2'"), 
														Строка(КоличествоКВыгрузке), Формат(СуммаДокументаКВыгрузке, "ЧДЦ=2"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = "ПлатежныеДокументыОписаниеОшибок" Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Ошибки в документе'"),
			ПолучитьНавигационнуюСсылку(Элемент.ТекущиеДанные.Документ),
			Элемент.ТекущиеДанные.ОписаниеОшибок);
	ИначеЕсли Поле.Имя = "ПлатежныеДокументыНазначениеПлатежа" Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыСообщения = Новый Структура;
		ПараметрыСообщения.Вставить("Заголовок", НСтр("ru = 'Назначение платежа'"));
		ПараметрыСообщения.Вставить("Сообщение", Элемент.ТекущиеДанные.НазначениеПлатежа);
		Если Элемент.ТекущиеДанные.Документ <> Неопределено Тогда
			ПараметрыСообщения.Вставить("ГиперссылкаТекст",
				ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(Элемент.ТекущиеДанные.Документ));
			ПараметрыСообщения.Вставить("ГиперссылкаНавигация", ПолучитьНавигационнуюСсылку(Элемент.ТекущиеДанные.Документ));
		КонецЕсли;
		
		ОбщегоНазначенияБПКлиент.ПоказатьСообщениеВФорме(ПараметрыСообщения);
		
	Иначе
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Документ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныеДокументыПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущийЭлемент.Имя <> "ПлатежныеДокументыВыгружать" И Элемент.Имя <> "ПлатежныеДокументы" Тогда
		Отказ = Истина;
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Документ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныеДокументыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежныеДокументыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыДокументыКИмпорту

// Событие вызывается при двойном щелчке мыши или нажатии Enter
//
&НаКлиенте
Процедура ДокументыКИмпортуВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = ДокументыКИмпорту.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если ЭтоМобильныйКлиент Тогда
		СтандартнаяОбработка = Ложь;
		
		Если Поле.Имя = "ДокументыКИмпортуДокумент" Тогда
			ПоказатьЗначение(, СтрокаТаблицы.Документ);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ДокументыКИмпортуЗагружать" Тогда
		СтрокаТаблицы.Загружать = НЕ СтрокаТаблицы.Загружать;
	ИначеЕсли Поле.Имя = "ДокументыКИмпортуОписаниеОшибок" Тогда
		СтандартнаяОбработка = Ложь;
		Если Элемент.ТекущиеДанные.Документ = Неопределено Тогда
			СсылкаДляПерехода = Неопределено;
		Иначе
			СсылкаДляПерехода = ПолучитьНавигационнуюСсылку(СтрокаТаблицы.Документ);
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Ошибки в документе'"), СсылкаДляПерехода, СтрокаТаблицы.ОписаниеОшибок);
	ИначеЕсли Поле.Имя = "ДокументыКИмпортуНазначениеПлатежа" Тогда
		СтандартнаяОбработка = Ложь;
		
		ПараметрыСообщения = Новый Структура;
		ПараметрыСообщения.Вставить("Заголовок", НСтр("ru = 'Назначение платежа'"));
		ПараметрыСообщения.Вставить("Сообщение", СтрокаТаблицы.НазначениеПлатежа);
		Если СтрокаТаблицы.Документ <> Неопределено Тогда
			ПараметрыСообщения.Вставить("ГиперссылкаТекст",
				ОбщегоНазначенияБПВызовСервера.СформироватьЗаголовокДокумента(СтрокаТаблицы.Документ));
			ПараметрыСообщения.Вставить("ГиперссылкаНавигация", ПолучитьНавигационнуюСсылку(СтрокаТаблицы.Документ));
		КонецЕсли;
		
		ОбщегоНазначенияБПКлиент.ПоказатьСообщениеВФорме(ПараметрыСообщения);
		
	ИначеЕсли Поле.Имя = "ДокументыКИмпортуДокумент" Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(СтрокаТаблицы.Документ) Тогда
			
			Если ТипЗнч(СтрокаТаблицы.Документ) = Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет") Тогда
				КлючеваяОперация = "ОткрытиеФормыПоступлениеНаРасчетныйСчет";
			ИначеЕсли ТипЗнч(СтрокаТаблицы.Документ) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
				КлючеваяОперация = "ОткрытиеФормыСписаниеСРасчетногоСчета";
			КонецЕсли;
			
			ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
			
			ПоказатьЗначение(, СтрокаТаблицы.Документ);
		Иначе
			// не реагируем
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицы.Документ) И ВыполненаЗагрузка Тогда
		Если Поле.Имя = "ДокументыКИмпортуКонтрагент" Тогда 
			СтандартнаяОбработка = Ложь;
			Если ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
				ПоказатьЗначение(, СтрокаТаблицы.Контрагент);
			КонецЕсли;
		ИначеЕсли Поле.Имя = "ДокументыКИмпортуСчетКонтрагента" Тогда 
			СтандартнаяОбработка = Ложь;
			Если ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагента) Тогда
				ПоказатьЗначение(, СтрокаТаблицы.СчетКонтрагента);
			КонецЕсли;
		ИначеЕсли Поле.Имя = "ДокументыКИмпортуНомерСтроки" ИЛИ 
			Поле.Имя = "ДокументыКИмпортуДокумент" ИЛИ 
			Поле.Имя = "ДокументыКИмпортуНомерДок" ИЛИ 
			Поле.Имя = "ДокументыКИмпортуДатаПроведения" ИЛИ
			Поле.Имя = "ДокументыКИмпортуСуммаСписано" ИЛИ
			Поле.Имя = "ДокументыКИмпортуСуммаПоступило" ИЛИ 
			Поле.Имя = "ДокументыКИмпортуВидОперации" ИЛИ 
			Поле.Имя = "ДокументыКИмпортуДоговор" Тогда
			
			Если ТипЗнч(СтрокаТаблицы.Документ) = Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет") Тогда
				КлючеваяОперация = "ОткрытиеФормыПоступлениеНаРасчетныйСчет";
			ИначеЕсли ТипЗнч(СтрокаТаблицы.Документ) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
				КлючеваяОперация = "ОткрытиеФормыСписаниеСРасчетногоСчета";
			КонецЕсли;
			
			ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
			
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение(, СтрокаТаблицы.Документ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент = Элементы.ДокументыКИмпортуКонтрагент Тогда 
		МассивПараметрыВыбора = Новый Массив;
		Если ТекущиеДанные.ВидОперации = 
			ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ВзносВУставныйКапитал") Тогда 
			НовыйПараметр = Новый ПараметрВыбора("Отбор.ЮридическоеФизическоеЛицо",
				ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
			МассивПараметрыВыбора.Добавить(НовыйПараметр);
			Элемент.ТекущийЭлемент.ВыбиратьТип = Истина;
			Элемент.ТекущийЭлемент.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Контрагенты,СправочникСсылка.ФизическиеЛица");
		Иначе
			Элемент.ТекущийЭлемент.ВыбиратьТип = Ложь;
			ТипыКонтрагента = Новый Массив;
			ТипыКонтрагента.Добавить(УчетДенежныхСредствКлиентСервер.ТипКонтрагентаПоВидуОперации(ТекущиеДанные.ВидОперации));
			Элемент.ТекущийЭлемент.ОграничениеТипа = Новый ОписаниеТипов(ТипыКонтрагента);
		КонецЕсли;
		Элемент.ТекущийЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметрыВыбора);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент.Имя = "ДокументыКИмпортуКонтрагент" Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТипЗнч(ТекущиеДанные.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
			Элемент.ТекущийЭлемент.ПодсказкаВвода = ТекущиеДанные.РеквизитыКонтрагента.НаименованиеПолное;
		ИначеЕсли ТипЗнч(ТекущиеДанные.Контрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			Элемент.ТекущийЭлемент.ПодсказкаВвода = ТекущиеДанные.РеквизитыКонтрагента.Наименование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуКонтрагентПриИзменении(Элемент)
	
	ДокументыКИмпортуКонтрагентПриИзмененииНаСервере(Элементы.ДокументыКИмпорту.ТекущаяСтрока, СтруктураПоискаКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	// Определим структуру поиска контрагента.
	// Может потребоваться если пользователь захочет изменить этого контрагента и в других строках
	ТекущиеДанные = Элементы.ДокументыКИмпорту.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Контрагент) Тогда
		СтруктураПоискаКонтрагента = Новый Структура("Контрагент", ТекущиеДанные.Контрагент);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.КонтрагентСсылкаДляНового) Тогда
		СтруктураПоискаКонтрагента = Новый Структура("КонтрагентСсылкаДляНового", ТекущиеДанные.КонтрагентСсылкаДляНового);
	Иначе
		СтруктураПоискаКонтрагента = Новый Структура;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуКонтрагентСоздание(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДокументыКИмпорту.ТекущиеДанные;
	Если ПустаяСтрока(Элемент.ТекстРедактирования) И ЗначениеЗаполнено(ТекущиеДанные.РеквизитыКонтрагента) Тогда
		СтандартнаяОбработка = Ложь;
		
		ДанныеЗаполнения = Новый Структура("Ключ, Родитель, Наименование, НаименованиеПолное, ИНН, КПП, ЮридическоеФизическоеЛицо");
		ДанныеЗаполнения.Ключ = ТекущиеДанные.КонтрагентСсылкаДляНового;
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ТекущиеДанные.РеквизитыКонтрагента);
		
		Если ТипЗнч(ТекущиеДанные.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
			ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ДанныеЗаполнения), ЭтотОбъект);
		ИначеЕсли ТипЗнч(ТекущиеДанные.Контрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ДанныеЗаполнения), ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуСчетКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДокументыКИмпорту.ТекущиеДанные;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Владелец",    ТекущиеДанные.Контрагент);
	ПараметрыОтбора.Вставить("Организация", Объект.Организация);
	ПараметрыОтбора.Вставить("Валютный",    ТекущиеДанные.ВалютаСчетаОрганизации <> ВалютаРегламентированногоУчета);
	Если ПараметрыОтбора.Валютный Тогда
		ПараметрыОтбора.Вставить("ВалютаДенежныхСредств", ТекущиеДанные.ВалютаСчетаОрганизации);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("Справочник.БанковскиеСчета.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДокументыКИмпорту.ТекущиеДанные;
	
	ВидДоговораВзаиморасчетов = ЗаполнитьВидыДоговоров(ТекущиеДанные.ВидОперации);
	Если ВидДоговораВзаиморасчетов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Владелец",      ТекущиеДанные.Контрагент);
	ПараметрыОтбора.Вставить("Организация",   Объект.Организация);
	ПараметрыОтбора.Вставить("ВидДоговора",   Новый ФиксированныйМассив(ВидДоговораВзаиморасчетов));
	ПараметрыОтбора.Вставить("ОплатаВВалюте", (ВалютаДенежныхСредств <> ВалютаРегламентированногоУчета));
	Если ПараметрыОтбора.ОплатаВВалюте Тогда
		ПараметрыОтбора.Вставить("ВалютаВзаиморасчетов", ВалютаДенежныхСредств);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

// СтруктураПоискаКонтрагента может быть модифицирован
&НаСервере
Процедура ДокументыКИмпортуКонтрагентПриИзмененииНаСервере(ИдентификаторСтроки, СтруктураПоискаКонтрагента)
	
	ТекущиеДанные = ДокументыКИмпорту.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	УточнитьХозяйственнуюОперациюДаннымиВведеннымиПользователем(ТекущиеДанные, "Контрагент");
		
	Если Не ЗначениеЗаполнено(СтруктураПоискаКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	// Не изменяем контрагента, если он уже существует
	// Общая замена происходит только для новых контрагентов.
	Если Не СтруктураПоискаКонтрагента.Свойство("КонтрагентСсылкаДляНового") Тогда
		Возврат;
	КонецЕсли;
	
	// Поищем контрагента по другим строкам, возможно придется изменять контрагента и в остальных строках;
	ОстальныеСтроки = ДокументыКИмпорту.НайтиСтроки(СтруктураПоискаКонтрагента);
	Если Не ЗначениеЗаполнено(ОстальныеСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ОстальныеСтроки Цикл
		СтрокаТаблицы.Контрагент = ТекущиеДанные.Контрагент;
		УточнитьХозяйственнуюОперациюДаннымиВведеннымиПользователем(СтрокаТаблицы, "Контрагент");
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ДокументыКИмпортуСчетКонтрагентаПриИзмененииНаСервере(ИдентификаторСтроки)
	
	УточнитьХозяйственнуюОперациюДаннымиВведеннымиПользователем(
		ДокументыКИмпорту.НайтиПоИдентификатору(ИдентификаторСтроки),
		"СчетКонтрагента");
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуСчетКонтрагентаПриИзменении(Элемент)
	
	ДокументыКИмпортуСчетКонтрагентаПриИзмененииНаСервере(Элементы.ДокументыКИмпорту.ТекущаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуДоговорПриИзменении(Элемент)
	
	ДокументыКИмпортуДоговорПриИзмененииНаСервере(Элементы.ДокументыКИмпорту.ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ДокументыКИмпортуДоговорПриИзмененииНаСервере(ИдентификаторСтроки)

	УточнитьХозяйственнуюОперациюДаннымиВведеннымиПользователем(
		ДокументыКИмпорту.НайтиПоИдентификатору(ИдентификаторСтроки),
		"Договор");
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуЗагружатьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДокументыКИмпорту.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Знак = ?(ТекущиеДанные.Загружать, 1, -1);
	
	КоличествоКЗагрузке     = КоличествоКЗагрузке     + 1 * Знак;
	СуммаПоступилоКЗагрузке = СуммаПоступилоКЗагрузке + ТекущиеДанные.СуммаПоступило * Знак;
	СуммаСписаноКЗагрузке   = СуммаСписаноКЗагрузке   + ТекущиеДанные.СуммаСписано   * Знак;
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.ПредставлениеИтоговЗагрузкиМК.Заголовок = СтрШаблон(НСтр("ru = 'К загрузке: %1 Поступило: %2 Списано: %3'"), 
											Строка(КоличествоКЗагрузке), Формат(СуммаПоступилоКЗагрузке, "ЧДЦ=2"),Формат(СуммаСписаноКЗагрузке, "ЧДЦ=2"));
	КонецЕсли;
	
	УстановитьДоступностьКнопкиЗагрузить(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуВидОперацииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДокументыКИмпорту.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ДокументОснование) Тогда
		ДокументыКИмпортуВидОперацииПриИзмененииНаСервере(Элементы.ДокументыКИмпорту.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДокументыКИмпортуВидОперацииПриИзмененииНаСервере(ИдентификаторСтроки)
	
	УточнитьХозяйственнуюОперациюДаннымиВведеннымиПользователем(
		ДокументыКИмпорту.НайтиПоИдентификатору(ИдентификаторСтроки),
		"ВидОперации");
	
КонецПроцедуры

&НаСервере
Процедура ДокументыКИмпортуСтатьяДДСПриИзмененииНаСервере(ИдентификаторСтроки)
	
	УточнитьХозяйственнуюОперациюДаннымиВведеннымиПользователем(
		ДокументыКИмпорту.НайтиПоИдентификатору(ИдентификаторСтроки),
		"СтатьяДДС");
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыКИмпортуСтатьяДДСПриИзменении(Элемент)
	
	ДокументыКИмпортуСтатьяДДСПриИзмененииНаСервере(Элементы.ДокументыКИмпорту.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьБанковскуюВыпискуИзФайлаМК(ДвоичныеДанныеФайла)
	
	ИмяФайлаЗагрузкиМК = ПолучитьИмяВременногоФайла("txt");
	
	ДвоичныеДанныеФайла.Записать(ИмяФайлаЗагрузкиМК);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПослеЗакрытияФормыНовогоДокумента(Результат, ДопПараметры) Экспорт
	
	ОбновитьСписокДокументовНаЭкспорт();
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьДоступностьДиректбанкаМК()
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьОбменСБанками") Тогда
		ДиректБанкЗагрузкаДоступенМК = Ложь;
		ДиректБанкОтправкаДоступенМК = Ложь;
		
		Возврат;
	КонецЕсли;
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(НастройкиОбменаОтправка.Ссылка, НЕОПРЕДЕЛЕНО) КАК НастройкаОбменаОтправка,
		|	ЕСТЬNULL(НастройкиОбменаЗагрузка.Ссылка, НЕОПРЕДЕЛЕНО) КАК НастройкаОбменаЗагрузка,
		|	БанковскиеСчета.Ссылка КАК Счет
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменаОтправка
		|		ПО БанковскиеСчета.Банк = НастройкиОбменаОтправка.Ссылка.Банк
		|			И (НЕ НастройкиОбменаОтправка.Ссылка.Недействительна)
		|			И (НастройкиОбменаОтправка.ИсходящийДокумент = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение))
		|			И (НастройкиОбменаОтправка.СпособПодтвержденияДокумента = ЗНАЧЕНИЕ(Перечисление.СпособыПодтвержденияДокументовОбменСБанками.SMS)
		|				ИЛИ НастройкиОбменаОтправка.СпособПодтвержденияДокумента = ЗНАЧЕНИЕ(Перечисление.СпособыПодтвержденияДокументовОбменСБанками.ВЛичномКабинетеБанка))
		|			И (НЕ БанковскиеСчета.Банк.Наименование ПОДОБНО ""%Сбербанк%"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиОбменСБанками КАК НастройкиОбменаЗагрузка
		|		ПО БанковскиеСчета.Банк = НастройкиОбменаЗагрузка.Банк
		|			И (НЕ НастройкиОбменаЗагрузка.Недействительна)
		|			И (НЕ БанковскиеСчета.Банк.Наименование ПОДОБНО ""%Сбербанк%"")
		|ГДЕ
		|	БанковскиеСчета.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.БанковскийСчет);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДиректБанкЗагрузкаДоступенМК = Выборка.НастройкаОбменаЗагрузка <> Неопределено;
		ДиректБанкОтправкаДоступенМК = Выборка.НастройкаОбменаОтправка <> Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВБанкМобильныйКлиент()
	
	Если НЕ КоличествоКВыгрузке > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ДиректБанкОтправкаДоступенМК Тогда
		ОтправитьЭД();
	Иначе
		ПоделитьсяЭД();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЭД()
	
	ОчиститьСообщения();
	Если Объект.ПлатежныеДокументы.Количество() > 0 Тогда
		
		МассивДокументов = Новый Массив;
		
		Для каждого Строка Из Объект.ПлатежныеДокументы Цикл
			Если Строка.Выгружать = Истина Тогда
				МассивДокументов.Добавить(Строка.Документ);
			КонецЕсли;
		КонецЦикла;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьВБанкЗавершение", ЭтотОбъект);
		ЭлектронноеВзаимодействиеБПКлиент.ВыполнитьПроверкуПроведенияДокументов(
			МассивДокументов, ОписаниеОповещения, ЭтотОбъект);
		
	Иначе
		
		Если Объект.НачалоПериода = Объект.КонецПериода Тогда
			ТекстПериода = Формат(Объект.НачалоПериода, "ДФ=dd.MM.yyyy");
		Иначе
			ТекстПериода = НСтр("ru = 'период с %1 по %2'");
			ТекстПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПериода,
				Формат(Объект.НачалоПериода, "ДФ=dd.MM.yyyy"), 
				Формат(Объект.КонецПериода, "ДФ=dd.MM.yyyy"));
		КонецЕсли;
		
		ТекстСообщения = НСтр("ru = 'Отсутствуют платежные поручения за %1
			|по счету %2
			|(%3).
			|
			|Попробуйте изменить период или указать другой банковский счет.'");
			
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ТекстПериода,
			Объект.БанковскийСчет,
			Объект.Организация);
			
		ПоказатьПредупреждение(, ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоделитьсяЭД()
	
	АдресВоВременномХранилище = ВыгрузитьДокументы();
	
	Каталог = МобильныйКлиентБПКлиент.ПолучитьВременныйКаталогДокументов();
	
	ПолноеИмяФайла = ОбщегоНазначенияКлиентСервер.ПолучитьПолноеИмяФайла(Каталог, "1c_to_kl.txt");
	
	ПолучитьФайл(АдресВоВременномХранилище, ПолноеИмяФайла, Ложь);
	
	ДополнительныеПараметры = Новый Структура("ИмяФайла, АдресВоВременномХранилище",
		"", АдресВоВременномХранилище);
	
	МобильныйКлиентБПКлиент.ПоделитьсяФайлами(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПолноеИмяФайла));
	
	ЗавершениеВыгрузкиФайла(ДополнительныеПараметры);
	
	ОбновитьСписокДокументовНаЭкспорт();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЭлектронныеВыписки()
	
	ВыпискиБанка.Очистить();
	ЭлектроннаяВыпискаБанка = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИзменитьДатуОкончанияЗапросаВыписки()
	
	Если ПрямойОбменСбербанк Тогда
		Если ЗначениеЗаполнено(Объект.НачалоПериода) Тогда
			МаксимальнаяДата = Мин(ТекущаяДата(),
				Объект.НачалоПериода + УчетДенежныхСредствКлиентСервер.МаксимальныйПериодВыпискиСбербанк());
			Если МаксимальнаяДата < Объект.КонецПериода Или Объект.НачалоПериода > Объект.КонецПериода Тогда
				Объект.КонецПериода = МаксимальнаяДата;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиДляБанковскогоСчета(БанковскийСчетУстановленИзФайла = Ложь, ЭтоИзменениеБанкСчета = Ложь)
	
	ПодключенаИнтеграцияСБанком = Ложь;
	Объект.Кодировка = "";
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчет, "Владелец");
	КонецЕсли;
	
	ИспользуетсяСогласованиеПлатежей = УчетДенежныхСредствБП.ДоступностьСогласованияПлатежейПоБанковскомуСчету(Объект.БанковскийСчет);
	
	БанкИНомерСчета(Объект.БанковскийСчет, Банк, НомерСчета);
	ИспользоватьНесколькоБанковскихСчетовОрганизации = ИспользоватьНесколькоБанковскихСчетов();
	
	ВидимостьЭлементовДиректБанк = ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(
		Объект.Организация, Объект.БанковскийСчет);
	
	ПрямойОбменСбербанк = Ложь;
	ПрямойОбменСБанками = Ложь;
	ПрямойОбменТолькоЗагрузка   = Ложь;
	ПодключенаИнтеграцияСБанком = Ложь;
	ЭтоЦифровойСчет = Ложь;
	Объект.СоглашениеПрямогоОбменаСБанками = Неопределено;
	ВалютаДенежныхСредств = ВалютаРегламентированногоУчета;
	
	ЕстьПрямойОбмен = Ложь;
	СохранятьТекущуюНастройку = Ложь;
	ФайлЗагрузки = "";
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Настройки = Обработки.КлиентБанк.ПолучитьНастройкиПрограммыКлиентаБанка(
			Объект.Организация, Объект.БанковскийСчет, , Истина);
		Если Настройки.Свойство("ПрограммаБанка") Тогда
			ПрямойОбменСбербанк = Настройки.ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн;
		КонецЕсли;
		
		Объект.СоглашениеПрямогоОбменаСБанками = Настройки.СоглашениеПрямогоОбменаСБанками;
		ПрямойОбменТолькоЗагрузка              = Настройки.ПрямойОбменТолькоЗагрузка;
		Объект.Программа                       = Настройки.Программа;
		Объект.Кодировка                       = Настройки.Кодировка;
		Объект.ФайлВыгрузки                    = Настройки.ФайлВыгрузки;
		ПодключенСервисАУСН                    = Настройки.ПодключенСервисАУСН;
		ЭтоЦифровойСчет                        = Настройки.ЭтоЦифровойСчет;
		
		ПрямойОбменСБанками = ЗначениеЗаполнено(Настройки.СоглашениеПрямогоОбменаСБанками)
			Или ЗначениеЗаполнено(АдресВыписки);
		
		Объект.ВыгружатьПлатежноеПоручение = Настройки.Платежное_Поручение;
		
		Если ВключитьВыгрузкуПлатежныхТребований Тогда
			Объект.ВыгружатьПлатежноеТребование = Истина;
		Иначе
			Объект.ВыгружатьПлатежноеТребование = Настройки.Платежное_Требование;
		КонецЕсли;
		
		ВыгружатьПлатежноеТребование(ЭтотОбъект);
		
		Объект.КонтролироватьНекорректныеСимволыВНомере = Настройки.КонтролироватьНекорректныеСимволыВНомере;
		Объект.СоздаватьНенайденныеЭлементы             = Настройки.СоздаватьНенайденныеЭлементы;
		Объект.КонтролироватьБезопасностьОбменаСБанком  = Настройки.КонтролироватьБезопасностьОбменаСБанком;
		
		ЗагружатьВыпискуПоВсемСчетамБанка = Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗагрузка
			И Не ЭтоИзменениеБанкСчета
			И ЗначениеЗаполнено(Настройки.БанкДляПрямогоОбмена) И Настройки.БанковскийСчет = Неопределено;
		
		БанкиИНастройкиСПрямымОбменом = Справочники.БанковскиеСчета.БанкиИНастройкиСПрямымОбменом(Объект.Организация);
		
		Если (ЗначениеЗаполнено(Объект.БанковскийСчет) Или ЗначениеЗаполнено(АдресВыписки))
			И Не ЗагружатьВыпискуПоВсемСчетамБанка Тогда
			ПодключенаИнтеграцияСБанком = ИнтеграцияСБанкамиПовтИсп.ИнтеграцияВключена(Объект.БанковскийСчет);
			
			ЗагружатьФайлИзНастройки = Не БанковскийСчетУстановленИзФайла;
			СоответствиеВыписки = СоответствиеВыпискиОтбору(
				АдресХранилищаРаспознанныеДанныеИзБанка, Объект.БанковскийСчет, Объект.Организация);
			СохранятьТекущуюНастройку = СоответствиеВыписки.Соответствует И ЗначениеЗаполнено(ФайлЗагрузки);
			Если ЗагружатьФайлИзНастройки И Не СохранятьТекущуюНастройку И Не ПустаяСтрока(Настройки.ФайлЗагрузки) Тогда
				ФайлЗагрузки = Настройки.ФайлЗагрузки;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
				РеквизитыСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
					Объект.БанковскийСчет, "ВалютаДенежныхСредств, ЦифровойСчет");
				ВалютаДенежныхСредств = РеквизитыСчета.ВалютаДенежныхСредств;
				Если РеквизитыСчета.ЦифровойСчет Или ВалютаДенежныхСредств <> ВалютаРегламентированногоУчета Тогда
					ЕстьПрямойОбмен = БанкиИНастройкиСПрямымОбменом.Количество() <> 0 И ПрямойОбменСБанками;
					СпособЗагрузки = "Файл";
					СохранятьТекущуюНастройку = Истина;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(Настройки.БанкДляПрямогоОбмена) Тогда
			Если ИспользоватьНесколькоБанковскихСчетовОрганизации Тогда
				Объект.БанковскийСчет = Неопределено;
				Банк = Настройки.БанкДляПрямогоОбмена;
				СпособЗагрузки = "ДиректБанкПоБанку";
			Иначе
				СпособЗагрузки = "ДиректБанк";
			КонецЕсли;
			
			СохранятьТекущуюНастройку = Истина;
		Иначе
			// Организация заполнена, а банковский счет (или банк для прямого обмена) нет.
			// Проверим, что у организации есть прямой обмен с банком.
			Если БанкиИНастройкиСПрямымОбменом.Количество() <> 0 Тогда
				// Этот вариант будет срабатывать только в том случае, если у организации одновременно есть счета
				// с прямым обменом и счета без подключенного обмена, для которых выписка загружается через файл.
				// Поэтому "по умолчанию" выберем режим загрузки из файла.
				СпособЗагрузки = "Файл";
				СохранятьТекущуюНастройку = Истина;
				ЕстьПрямойОбмен = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПроверкаКорректностиБанковскогоСчета();
	
	ЗаполнитьСпособЗагрузки(СохранятьТекущуюНастройку, ЕстьПрямойОбмен);
	Если СпособЗагрузки = "Файл" И Не ПустаяСтрока(ФайлЗагрузки) Тогда
		Объект.ФайлЗагрузки = ФайлЗагрузки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиДляБанка()
	
	Объект.Кодировка = "";
	
	ИспользоватьНесколькоБанковскихСчетовОрганизации = ИспользоватьНесколькоБанковскихСчетов();
	
	ПрямойОбменСбербанк = Ложь;
	ПрямойОбменСБанками = Ложь;
	ПрямойОбменТолькоЗагрузка   = Ложь;
	ПодключенаИнтеграцияСБанком = Ложь;
	ЭтоЦифровойСчет = Ложь;
	Объект.СоглашениеПрямогоОбменаСБанками = Неопределено;
	ВалютаДенежныхСредств = ВалютаРегламентированногоУчета;
	
	ЕстьПрямойОбмен = Ложь;
	ФайлЗагрузки    = "";
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Настройки = Обработки.КлиентБанк.ПолучитьНастройкиПрограммыКлиентаБанка(Объект.Организация, , Банк);
		Если Настройки.Свойство("ПрограммаБанка") Тогда
			ПрямойОбменСбербанк = Настройки.ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн;
		КонецЕсли;
		
		Объект.СоглашениеПрямогоОбменаСБанками = Настройки.СоглашениеПрямогоОбменаСБанками;
		ПрямойОбменТолькоЗагрузка              = Настройки.ПрямойОбменТолькоЗагрузка;
		Объект.Программа                       = Настройки.Программа;
		Объект.Кодировка                       = Настройки.Кодировка;
		ПодключенСервисАУСН                    = Настройки.ПодключенСервисАУСН;
		ЭтоЦифровойСчет                        = Настройки.ЭтоЦифровойСчет;
		
		ПрямойОбменСБанками = ЗначениеЗаполнено(Настройки.СоглашениеПрямогоОбменаСБанками)
			Или ЗначениеЗаполнено(АдресВыписки);
		
		Если ЗначениеЗаполнено(Настройки.БанкДляПрямогоОбмена) Тогда
			Банк = Настройки.БанкДляПрямогоОбмена;
			СпособЗагрузки = "ДиректБанкПоБанку";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВыгружатьПлатежноеТребование(Форма)
	
	СтрокиПлатТребование = Форма.ТаблицаДокументов.НайтиСтроки(Новый Структура("Документ", "ПлатежноеТребование"));
	Если СтрокиПлатТребование.Количество() = 1 Тогда
		СтрокиПлатТребование[0].Пометка = Форма.Объект.ВыгружатьПлатежноеТребование И СтрокиПлатТребование[0].ФО;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НастройкиЗагрузки()
	
	Если ПустаяСтрока(КешНастроекЗагрузки) Тогда
		НастройкиЗагрузки = Неопределено;
	Иначе
		НастройкиЗагрузки = ПолучитьИзВременногоХранилища(КешНастроекЗагрузки);
	КонецЕсли;
	
	Если НастройкиЗагрузки = Неопределено Тогда
		НастройкиЗагрузки = Обработки.КлиентБанк.НастройкиЗагрузки();
		КешНастроекЗагрузки = ПоместитьВоВременноеХранилище(НастройкиЗагрузки, УникальныйИдентификатор);
	ИначеЕсли НастройкиЗагрузки.Кодировка <> Объект.Кодировка Тогда
		НастройкиЗагрузки.Кодировка = Объект.Кодировка;
		КешНастроекЗагрузки = ПоместитьВоВременноеХранилище(НастройкиЗагрузки, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат НастройкиЗагрузки;
	
КонецФункции

// Процедура формирует отчет о выгрузке
//
&НаСервере
Процедура ПолучитьОтчетОВыгрузке(ПолеОтчета)
	
	Обработки.КлиентБанк.ПечатьОтчетаОВыгруженныхПлатежныхДокументах(ПолеОтчета,
																	Объект.ПлатежныеДокументы,
																	Объект.Организация,
																	Объект.БанковскийСчет,
																	Объект.НачалоПериода,
																	Объект.КонецПериода);
	
КонецПроцедуры

// Процедура формирует отчет о загрузке
//
&НаСервере
Процедура ПолучитьОтчетОЗагрузке(ПолеОтчета)
	
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("Организация",                  Объект.Организация);
	ДанныеФормы.Вставить("БанковскийСчет",               Объект.БанковскийСчет);
	ДанныеФормы.Вставить("СоздаватьНенайденныеЭлементы", Объект.СоздаватьНенайденныеЭлементы);
	ДанныеФормы.Вставить("ДокументыКИмпорту",            ДокументыКИмпорту.Выгрузить());
	
	Обработки.КлиентБанк.ПечатьОтчетаОЗагруженныхПлатежныхДокументах(ПолеОтчета,
		ДанныеФормы,
		АдресХранилищаРаспознанныеДанныеИзБанка);
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаКорректностиБанковскогоСчета()
	
	Ошибки = Новый Массив;
	ЕстьОшибки = Ложь;
	
	Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		РезультатПроверкиСчета = БанковскиеСчетаСервер.ПроверитьКорректностьБанковскогоСчета(Объект.БанковскийСчет);
		ЕстьОшибки = РезультатПроверкиСчета.ЕстьОшибки;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		ПризнакСчета = БанковскиеСчетаСервер.НовыйПризнакСчета();
		ПризнакСчета.СчетОрганизации = Истина;
		ТекстОшибки = БанковскиеСчетаСервер.ТекстОшибкиБанковскогоСчета(РезультатПроверкиСчета, ПризнакСчета);
		Ошибки.Добавить(ТекстОшибки);
		Элементы.ГруппаБанковскийСчетПроверка.ЦветФона = ЦветаСтиля.ЦветОшибкиБанковскогоСчета;
	Иначе
		Элементы.ГруппаБанковскийСчетПроверка.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
	КонецЕсли;
	
	Если Ошибки.Количество() > 0 Тогда
		Элементы.БанковскийСчетВыгрузка.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(Ошибки);
		Элементы.БанковскийСчетВыгрузка.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элементы.БанковскийСчетВыгрузка.РасширеннаяПодсказка.Заголовок = "";
		Элементы.БанковскийСчетВыгрузка.ОтображениеПодсказки = ОтображениеПодсказки.Авто;
	КонецЕсли;
	
КонецПроцедуры

#Область УправлениеФормой

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ОткрытаСтраницаВыгрузки = Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыгрузка;
	
	ПрямойОбменВыгрузка = Форма.ПрямойОбменСБанками И Не Форма.ПрямойОбменТолькоЗагрузка;
	
	// Выгрузка
	Элементы.ДекорацияОтчетОВыгрузке.Доступность  = Форма.ЭкспортПроизведен;
	Элементы.ДекорацияВыгруженныйФайл.Доступность = Форма.ЭкспортПроизведен
		И Не Объект.КонтролироватьБезопасностьОбменаСБанком;
	
	Элементы.БанковскийСчетВыгрузка.Видимость         = Форма.ИспользоватьНесколькоБанковскихСчетовОрганизации;
	Элементы.ГруппаВыгрузкиВыпискиЧерезЭД.Видимость   = ПрямойОбменВыгрузка;
	Элементы.ГруппаВыгрузкаВыпискиВФайл.Видимость     = Не ПрямойОбменВыгрузка;
	
	// Установим кнопку по умолчанию
	Если ПрямойОбменВыгрузка Тогда
		Элементы.КомандаОтправитьЭД.КнопкаПоУмолчанию = ОткрытаСтраницаВыгрузки;
	Иначе
		Элементы.КомандаВыгрузить.КнопкаПоУмолчанию   = ОткрытаСтраницаВыгрузки;
	КонецЕсли;
	
	// Загрузка
	Элементы.ДекорацияЗагружаемыйФайл.Доступность = ЗначениеЗаполнено(Объект.ФайлЗагрузки);
	УстановитьДоступностьКнопкиЗагрузить(Форма);
	
	// Установим кнопку по умолчанию
	Элементы.КомандаЗагрузить.КнопкаПоУмолчанию   = Не ОткрытаСтраницаВыгрузки;
	Элементы.БанковскийСчетЗагрузкаФайл.Видимость = Форма.ИспользоватьНесколькоБанковскихСчетовОрганизации;
	
	Если НЕ Форма.ЭтоМобильныйКлиент Тогда
		Элементы.ДокументыКИмпортуПоказатьОшибки.Видимость     = Объект.СоздаватьНенайденныеЭлементы;
		Элементы.ДокументыКИмпортуСоздатьНенайденное.Видимость = Не Объект.СоздаватьНенайденныеЭлементы;
	КонецЕсли;
	
	ПоказатьЭлементыДиректБанк(Форма);
	
	НастроитьЭлементыДляИнтеграцииСБанком(Форма);
	
	Элементы.ГруппаСпособЗагрузки.Видимость = Элементы.СпособЗагрузки.СписокВыбора.Количество() > 1;
	
	Если Не ОткрытаСтраницаВыгрузки Тогда
		СпособЗагрузки = Форма.СпособЗагрузки;
		Если СпособЗагрузки = "ДиректБанк" Или СпособЗагрузки = "ДиректБанкПоБанку" Тогда
			Элементы.СтраницыВыборВыписки.ТекущаяСтраница = Элементы.ВыпискаПоПрямомуОбмену;
		ИначеЕсли СпособЗагрузки = "Файл" Тогда
			Если Форма.РасширениеРаботыСФайламиПодключено Тогда
				Элементы.СтраницыВыборВыписки.ТекущаяСтраница = Элементы.ВыпискаИзФайлаСРасширением;
			Иначе
				Элементы.СтраницыВыборВыписки.ТекущаяСтраница = Элементы.ВыпискаИзФайлаБезРасширения;
			КонецЕсли;
		Иначе
			Элементы.СтраницыВыборВыписки.ТекущаяСтраница = Элементы.ВыпискаИзСервисаАУСН;
			Элементы.ЗагрузитьИзСервисаАУСН.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		
		Если СпособЗагрузки = "ДиректБанкПоБанку" Тогда
			Элементы.ГруппаБанкОрганизацияПрямойОбмен.Видимость = Истина;
			Элементы.ГруппаБанковскийСчетаОрганизация.Видимость = Ложь;
		Иначе
			Элементы.ГруппаБанкОрганизацияПрямойОбмен.Видимость = Ложь;
			Элементы.ГруппаБанковскийСчетаОрганизация.Видимость = Истина;
		КонецЕсли;
		
		Элементы.ВыпискаПоПрямомуОбмену.Видимость = СпособЗагрузки = "ДиректБанк" Или СпособЗагрузки = "ДиректБанкПоБанку";
		Элементы.ГруппаТаблицаЗагрузки.Видимость = СпособЗагрузки <> "СервисАУСН";
		Элементы.ГруппаКомандЗагрузки.Видимость = СпособЗагрузки <> "СервисАУСН";
		Элементы.ФайлВыгрузки.Видимость = Форма.РасширениеРаботыСФайламиПодключено;
		Элементы.ДекорацияВыгруженныйФайл.Видимость = Форма.РасширениеРаботыСФайламиПодключено;
		Элементы.ЭлектроннаяВыпискаБанка.Видимость = Форма.ПрямойОбменСБанками;
	КонецЕсли;
	
	ОбновитьУсловноеОформление(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗагрузкаУстановитьВидимостьЭлементовВыбораФайлов(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.ФайлВыгрузки.Видимость             = Форма.РасширениеРаботыСФайламиПодключено;
	Элементы.ДекорацияВыгруженныйФайл.Видимость = Форма.РасширениеРаботыСФайламиПодключено;
	Элементы.ЭлектроннаяВыпискаБанка.Видимость  = Форма.ПрямойОбменСБанками;
	Если Форма.ПрямойОбменСБанками Тогда
		Элементы.СтраницыВыборВыписки.ТекущаяСтраница = Элементы.ВыпискаПоПрямомуОбмену;
	ИначеЕсли Форма.РасширениеРаботыСФайламиПодключено Тогда
		Элементы.СтраницыВыборВыписки.ТекущаяСтраница = Элементы.ВыпискаИзФайлаСРасширением;
	Иначе
		Элементы.СтраницыВыборВыписки.ТекущаяСтраница = Элементы.ВыпискаИзФайлаБезРасширения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКнопкиЗагрузить(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.КомандаЗагрузить.Доступность = (Форма.КоличествоКЗагрузке > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСпискиДокументов()
	
	Если ЭтоМобильныйКлиент Тогда
		ОбновитьСписокДокументовНаЭкспорт(Ложь);
		
		Возврат;
	КонецЕсли;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыгрузка Тогда
		ОбновитьСписокДокументовНаЭкспорт();
	Иначе
		ОчиститьСообщения();
		ДокументыКИмпорту.Очистить();
		
		КоличествоКЗагрузке     = 0;
		СуммаПоступилоКЗагрузке = 0;
		СуммаСписаноКЗагрузке   = 0;
		
		Элементы.ДокументыКИмпортуСуммаПоступило.ТекстПодвала    = "";
		Элементы.ДокументыКИмпортуСуммаСписано.ТекстПодвала      = "";
		
		СброситьСостояниеКомандИмпорта();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьСостояниеКомандИмпорта()
	
	Элементы.КомандаЗагрузить.Доступность                    = Ложь;
	Элементы.ДокументыКИмпортуСоздатьНенайденное.Доступность = Ложь;
	Элементы.ДокументыКИмпортуПоказатьОшибки.Доступность     = Ложь;
	Элементы.ДокументыКИмпортуПоказатьОшибки.Пометка         = Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ГотовностьСтроки(Знач ДанныеСтроки, Знач СоздаватьНенайденныеЭлементы)
	
	// Если есть описание ошибки, то строка не готова
	Если НЕ ПустаяСтрока(ДанныеСтроки.ОписаниеОшибок) Тогда
		Готовность = Ложь;
	// Если требуется создавать контрагентов вручную, тогда проверим нужно ли что-то создавать
	ИначеЕсли НЕ СоздаватьНенайденныеЭлементы Тогда
		Готовность = НЕ ТребуетсяСозданиеНенайденныхВСтроке(ДанныеСтроки);
	// Во всех остальных случаях строка готова к загрузке
	Иначе
		Готовность = Истина;
	КонецЕсли;
	
	Возврат Готовность;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииОрганизацияСервер(ПоВсемСчетам = Ложь)
	
	АдресВыписки = "";
	СпособЗагрузкиЗаполнен = Ложь;
	Если Не ПоВсемСчетам И УчетДенежныхСредствБП.УстановитьБанковскийСчет(
		Объект.БанковскийСчет, Объект.Организация, ВалютаРегламентированногоУчета) Тогда
		ЗагрузитьНастройкиДляБанковскогоСчета();
		СпособЗагрузкиЗаполнен = Истина;
	Иначе
		ИспользоватьНесколькоБанковскихСчетовОрганизации = ИспользоватьНесколькоБанковскихСчетов();
	КонецЕсли;
	
	БанкИНомерСчета(Объект.БанковскийСчет, Банк, НомерСчета);
	
	ВидимостьЭлементовДиректБанк = ЭлектронноеВзаимодействиеБПВызовСервера.ВидимостьЭлементовДиректБанк(
		Объект.Организация, Объект.БанковскийСчет);
	
	Если Не СпособЗагрузкиЗаполнен Тогда
		ЗаполнитьСпособЗагрузки();
	КонецЕсли;
	
	УстановитьОтборБанкиПрямогоОбмена();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииБанковскийСчет(БанковскийСчетУстановленИзФайла = Ложь)
	
	// Запомним предыдущий путь к файлу
	ТекущийФайлЗагрузки = Объект.ФайлЗагрузки;
	БылПрямойОбменСБанками = ПрямойОбменСБанками;
	ЗагрузитьНастройкиДляБанковскогоСчета(БанковскийСчетУстановленИзФайла, Истина);
	
	ОчиститьЭлектронныеВыписки();
	ОбновитьСпискиДокументов();
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыгрузка Тогда
		Если БылПрямойОбменСБанками И Не ПрямойОбменСБанками Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ПодключитьРасширениеРаботыСФайламиИПрочитатьФайл", 0.1, Истина);
		КонецЕсли;
		
		ЭтоФайлJSON = СтрЗаканчиваетсяНа(НРег(Объект.ФайлВыгрузки), ".json");
		Если ЗначениеЗаполнено(Объект.БанковскийСчет)
			И (ЭтоЦифровойСчет И Не ЭтоФайлJSON
				Или Не ЭтоЦифровойСчет И ЭтоФайлJSON) Тогда
			Объект.ФайлВыгрузки = "";
		КонецЕсли;
	Иначе
		ПроверитьИзменитьДатуОкончанияЗапросаВыписки();
		
		Если Не РасширениеРаботыСФайламиПодключено И БылПрямойОбменСБанками И Не ПрямойОбменСБанками Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ПодключитьРасширениеРаботыСФайламиИПрочитатьФайл", 0.1, Истина);
			Возврат;
		КонецЕсли;
		
		ЭтоФайлXML = СтрЗаканчиваетсяНа(НРег(Объект.ФайлЗагрузки), ".xml");
		Если ЗначениеЗаполнено(Объект.БанковскийСчет)
			И (ЭтоЦифровойСчет И Не ЭтоФайлXML
				Или Не ЭтоЦифровойСчет И ЭтоФайлXML) Тогда
			Объект.ФайлЗагрузки = "";
		КонецЕсли;
		
		// Если файл не изменился, проверим возможно ничего перечитывать не требуется
		Если ТекущийФайлЗагрузки = Объект.ФайлЗагрузки Тогда
			
			Если Не БанковскийСчетУстановленИзФайла // данные будут размещены в ОбработатьРезультатПрочитаннойВыпискиЗавершение()
				И СоответствиеВыпискиОтбору(АдресХранилищаРаспознанныеДанныеИзБанка, Объект.БанковскийСчет).Соответствует Тогда
				РазместитьНаФормеРаспознанныеДанныеИзБанка(АдресХранилищаРаспознанныеДанныеИзБанка, Ложь);
			КонецЕсли;
			
		Иначе // Загрузим новый файл
			ПодключитьОбработчикОжидания("Подключаемый_ПодключитьРасширениеРаботыСФайламиИПрочитатьФайл", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	Если ЭтоМобильныйКлиент Тогда
		ОпределитьДоступностьДиректбанкаМК();
		
		Если НЕ ДиректБанкОтправкаДоступенМК Тогда
			Элементы.КомандаОтправитьЭД.Заголовок = "Поделиться";
		Иначе
			Элементы.КомандаОтправитьЭД.Заголовок = "Отправить в банк";
		КонецЕсли;
		
		Элементы.ГруппаПериодЗагрузкаМК.Видимость                = ДиректБанкЗагрузкаДоступенМК ИЛИ (НЕ ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК));
		Элементы.ГруппаДокументыКИмпортуМК.Видимость             = ДиректБанкЗагрузкаДоступенМК ИЛИ (ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК));
		Элементы.ГруппаКомандЗагрузкиМК.Видимость                = ДиректБанкЗагрузкаДоступенМК ИЛИ (ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК));
		Элементы.НадписьНедоступенДиректбанкЗагрузкаМК.Видимость = НЕ ДиректБанкЗагрузкаДоступенМК И (НЕ ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК));
		
		Если ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК) Тогда
			Элементы.БанковскийСчетЗагрузкаФайл.Доступность = Ложь;
			Элементы.ОрганизацияПрямойОбмен.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииБанк()
	
	ЗагрузитьНастройкиДляБанка();
	
	ОчиститьЭлектронныеВыписки();
	ОбновитьСпискиДокументов();
	
	ПроверитьИзменитьДатуОкончанияЗапросаВыписки();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборБанкиПрямогоОбмена()
	
	БанкиПрямогоОбмена = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		БанкиИНастройкиСПрямымОбменом = Справочники.БанковскиеСчета.БанкиИНастройкиСПрямымОбменом(Объект.Организация);
		Для Каждого КлючИЗначение Из БанкиИНастройкиСПрямымОбменом Цикл
			БанкиПрямогоОбмена.Добавить(КлючИЗначение.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(БанкиПрямогоОбмена)));
	
	Элементы.БанкПрямойОбмен.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ДокументыКИмпортуОбновитьИтогиВПодвале()
	
	ДокументыКЗагрузке = ДокументыКИмпорту.Выгрузить(Новый Структура("Загружать", Истина));
	
	КоличествоКЗагрузке     = ДокументыКЗагрузке.Количество();
	СуммаПоступилоКЗагрузке = ДокументыКЗагрузке.Итог("СуммаПоступило");
	СуммаСписаноКЗагрузке   = ДокументыКЗагрузке.Итог("СуммаСписано");
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.ПредставлениеИтоговЗагрузкиМК.Заголовок = СтрШаблон(НСтр("ru = 'К загрузке: %1 Поступило: %2 Списано: %3'"), 
											Строка(КоличествоКЗагрузке), Формат(СуммаПоступилоКЗагрузке, "ЧДЦ=2"), Формат(СуммаСписаноКЗагрузке, "ЧДЦ=2"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПлатежныеДокументыОбновитьИтогиВПодвале()
	
	Сумма      = 0;
	Количество = 0;
	
	ОтборСтрокКВыгрузке = Новый Структура("Выгружать", Истина);
	СтрокиКВыгрузке     = Объект.ПлатежныеДокументы.НайтиСтроки(ОтборСтрокКВыгрузке);
	Для каждого Строка Из СтрокиКВыгрузке Цикл
		Сумма      = Сумма      + Строка.СуммаДокумента;
		Количество = Количество + 1;
	КонецЦикла;
	
	КоличествоКВыгрузке     = Количество;
	СуммаДокументаКВыгрузке = Сумма;
	
	Если ЭтоМобильныйКлиент Тогда
		Если Элементы.Найти("ПредставлениеИтоговВыгрузкиМК") <> Неопределено Тогда
			Элементы.ПредставлениеИтоговВыгрузкиМК.Заголовок = СтрШаблон(НСтр("ru = 'К отправке: %1 На сумму: %2'"), Строка(КоличествоКВыгрузке), 
														Формат(СуммаДокументаКВыгрузке, "ЧДЦ=2"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область УсловноеОформление

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Если НастройкиУсловногоОформления <> Неопределено Тогда
		// Условное оформление уже инициализировано.
		Возврат;
	КонецЕсли;
	
	НастройкиУсловногоОформления = Новый Структура();
	
	УсловноеОформление.Элементы.Очистить();
	
	// Условное оформление, связанное с видимостью, устанавливаем сразу для всех колонок.
	УстановитьУсловноеОформлениеВидимость();
	
	// Условное оформление для полей, расположенных на страницах
	ОбновитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьУсловноеОформление(Форма)
	
	Элементы = Форма.Элементы;
	
	Если НЕ Форма.НастройкиУсловногоОформления.Свойство("ВыгрузкаПроинициализировано")
		И Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыгрузка Тогда
		
		Форма.УстановитьУсловноеОформлениеВыгрузка();
		
	ИначеЕсли НЕ Форма.НастройкиУсловногоОформления.Свойство("ЗагрузкаПроинициализировано")
		И Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗагрузка Тогда
		
		Форма.УстановитьУсловноеОформлениеЗагрузка();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеВидимость()
	
	// ДокументыКИмпортуДокументНеЗагружен
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокументНеЗагружен");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Документ",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ДокументыКИмпортуДокумент
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокумент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Документ",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ДокументыКИмпортуСтатьяДДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСтатьяДДС");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ИспользоватьСтатьиДДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ДокументыКИмпортуОписаниеОшибок
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуОписаниеОшибок");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ДокументыКИмпорту.ОписаниеОшибок",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ПоказатьОшибкиИмпорта",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеВыгрузка() Экспорт
	
	НастройкиУсловногоОформления.Вставить("ВыгрузкаПроинициализировано", Истина);
	
	// ПлатежныеДокументы
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПлатежныеДокументы");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.ПлатежныеДокументы.Готовность",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийПроблемуТекст);   
	
	// ПлатежныеДокументыКонтрагентИдентификаторЦифровогоСчета
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля,
		"ПлатежныеДокументыКонтрагентИдентификаторЦифровогоСчета");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.ПлатежныеДокументы.КонтрагентИдентификаторЦифровогоСчета",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	
	Если ИспользуетсяСогласованиеПлатежей Тогда
		
		СписокСостояний = Новый СписокЗначений;
		СписокСостояний.Добавить(Перечисления.СостоянияБанковскихДокументов.Подготовлено);
		СписокСостояний.Добавить(Перечисления.СостоянияБанковскихДокументов.ПустаяСсылка());
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПлатежныеДокументыСостояние");
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ЭлементУО.Отбор,
			"Объект.ПлатежныеДокументы.Состояние",
			ВидСравненияКомпоновкиДанных.ВСписке,
			СписокСостояний);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не согласовано'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеЗагрузка() Экспорт
	
	НастройкиУсловногоОформления.Вставить("ЗагрузкаПроинициализировано", Истина);
	
	// ДокументыКИмпортуДокументНеЗагружен
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокументНеЗагружен");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Документ",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не загружен'"));
	
	// ДокументыКИмпортуДокумент, ДокументыКИмпортуДокументНеЗагружен
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокументНеЗагружен");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Готовность",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Документ",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);
	
	// ДокументыКИмпортуДокумент, ДокументыКИмпортуДокументНеЗагружен
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокументНеЗагружен");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Готовность",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Документ",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	// ДокументыКИмпортуДокумент, ДокументыКИмпортуДокументНеЗагружен
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокументНеЗагружен");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Готовность", 
		ВидСравненияКомпоновкиДанных.Равно, 
		Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийПроблемуТекст);
	
	// ДокументыКИмпортуКонтрагент
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуКонтрагент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.КонтрагентОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Содержит,
		НСтр("ru = 'Не найден'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийПроблемуТекст);
	
	// ДокументыКИмпортуКонтрагент
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуКонтрагент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.КонтрагентОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Содержит,
		НСтр("ru = 'Не загружен'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	// ДокументыКИмпортуКонтрагент
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуКонтрагент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Готовность",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.КонтрагентОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Содержит,
		НСтр("ru = 'Не указаны реквизиты контрагента'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийПроблемуТекст);
	
	// ДокументыКИмпортуКонтрагент
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуКонтрагент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Контрагент",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.КонтрагентОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Заполнено);
			
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Документ",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста",  ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	// ДокументыКИмпортуКонтрагент
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуКонтрагент");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Контрагент",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.КонтрагентОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ДокументыКИмпорту.КонтрагентОтображениеНенайденного"));
	
	// ДокументыКИмпортуКонтрагент
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуКонтрагент");
	
	ПереводыНаДругойСчет = Новый СписокЗначений;
	ПереводыНаДругойСчет.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПереводСДругогоСчета);
	ПереводыНаДругойСчет.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет);
	ПереводыНаДругойСчет.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.Депозит);
	ПереводыНаДругойСчет.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.Депозит);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.ВидОперации",
		ВидСравненияКомпоновкиДанных.ВСписке,
		ПереводыНаДругойСчет);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ДокументыКИмпортуСчетКонтрагента
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСчетКонтрагента");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.СчетКонтрагента",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.СчетКонтрагентаОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ДокументыКИмпорту.СчетКонтрагентаОтображениеНенайденного"));
	
	// ДокументыКИмпортуСчетКонтрагента
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСчетКонтрагента");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.СчетКонтрагентаОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Содержит,
		НСтр("ru = 'Не найден'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийПроблемуТекст);
	
	// ДокументыКИмпортуСчетКонтрагента
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСчетКонтрагента");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.СчетКонтрагентаОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Содержит,
		НСтр("ru = 'Не загружен'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСчетКонтрагента");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.СчетКонтрагентаОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Равно,
		НСтр("ru = 'Не указаны реквизиты расчетного счета'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийПроблемуТекст);
	
	// ДокументыКИмпортуСчетКонтрагента
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСчетКонтрагента");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.СчетКонтрагента",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.СчетКонтрагентаОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Заполнено);
			
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Документ",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста",  ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	// ДокументыКИмпортуСчетКонтрагента
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСчетКонтрагента");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Контрагент",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ДокументыКИмпортуДоговор
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДоговор");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Готовность",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ДокументыКИмпорту.ДоговорОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Равно,
		НСтр("ru = 'Не найден'"));
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ДокументыКИмпорту.ДоговорОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Равно,
		НСтр("ru = 'Не создан'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийПроблемуТекст);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// ДокументыКИмпортуДоговор
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДоговор");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Договор",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.ДоговорОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ДокументыКИмпорту.ДоговорОтображениеНенайденного"));
	
	// ДокументыКИмпортуДоговор
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДоговор");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Готовность",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ДокументыКИмпорту.ДоговорОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Равно,
		НСтр("ru = 'Не найден'"));
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ДокументыКИмпорту.ДоговорОтображениеНенайденного",
		ВидСравненияКомпоновкиДанных.Равно,
		НСтр("ru = 'Не создан'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	// ДокументыКИмпортуДоговор
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДоговор");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Контрагент",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ДокументыКИмпортуДоговор
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДоговор");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ВыполненаЗагрузка",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	// Виды операций "без договора".
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалога);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеНалогаЗаТретьихЛиц);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПрочееСписание);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.Депозит);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ЛичныеСредстваПредпринимателя);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.КомиссияБанка);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДепонентов);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПодотчетномуЛицу);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникуПоДоговоруПодряда);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеСотрудникамПоДоговорамПодряда);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийСписаниеДенежныхСредств.ВыдачаЗаймаРаботнику);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.Инкассация);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВзносНаличными);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПрочееПоступление);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ПереводСДругогоСчета);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.Депозит);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ЛичныеСредстваПредпринимателя);
	СписокЗначений.Добавить(Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ВозвратЗаймаРаботником);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ДокументыКИмпорту.ВидОперации",
		ВидСравненияКомпоновкиДанных.ВСписке,
		СписокЗначений);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ДокументыКИмпортуНомерДок
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуНомерДок");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.Готовность",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.НомерДокументаНекорректен",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийПроблемуТекст);
	
	// ДокументыКИмпортуВидОперации
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуВидОперации");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуКонтрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСчетКонтрагента");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ДокументыКИмпорту.ДокументОснование",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"ВыполненаЗагрузка",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДоговор");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ДокументыКИмпорту.ДоговорРазрешеноИзменение",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ЭкспортДокументов

&НаСервере
Функция ОбновитьДокументыНаЭкспортВФоне(Знач ВыгружатьПлатПоручения, Знач ВыгружатьПлатТребования)
	
	ВыгрузкаЗапрещена = Ложь;
	Если ЭтоЦифровойСчет Тогда
		Период = ОбщегоНазначения.ТекущаяДатаПользователя();
		ДоступенПлатежОбменСЦифровымКошельком = ЦифровойРубль.ДоступенПлатежОбменСЦифровымКошельком(Период);
		ВыгрузкаЗапрещена = Не ДоступенПлатежОбменСЦифровымКошельком;
	КонецЕсли;
	
	Если ПодключенаИнтеграцияСБанком Или ВыгрузкаЗапрещена Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение платежных документов для выгрузки'");
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Организация",        Объект.Организация);
	ПараметрыВыполнения.Вставить("ПлатежныеДокументы", Объект.ПлатежныеДокументы.Выгрузить());
	ПараметрыВыполнения.Вставить("НачалоПериода",      Объект.НачалоПериода);
	ПараметрыВыполнения.Вставить("КонецПериода",       Объект.КонецПериода);
	ПараметрыВыполнения.Вставить("БанковскийСчет",     Объект.БанковскийСчет);
	ПараметрыВыполнения.Вставить("СписокДокументов",   СписокДокументов);
	ПараметрыВыполнения.Вставить("ВыгружатьПлатПоручения",  ВыгружатьПлатПоручения);
	ПараметрыВыполнения.Вставить("ВыгружатьПлатТребования", ВыгружатьПлатТребования);
	ПараметрыВыполнения.Вставить("КонтролироватьНекорректныеСимволыВНомере", Объект.КонтролироватьНекорректныеСимволыВНомере);
	Если ПрямойОбменТолькоЗагрузка Тогда
		ПараметрыВыполнения.Вставить("СоглашениеПрямогоОбменаСБанками", Справочники.НастройкиОбменСБанками.ПустаяСсылка());
	Иначе
		ПараметрыВыполнения.Вставить("СоглашениеПрямогоОбменаСБанками", Объект.СоглашениеПрямогоОбменаСБанками);
	КонецЕсли;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне("Обработки.КлиентБанк.ЗаполнитьДокументыНаЭкспортВФоне",
		ПараметрыВыполнения, ПараметрыЗапуска);
	
	Если ДлительнаяОперация <> Неопределено И ДлительнаяОперация.Статус = "Выполнено" Тогда
		ЗагрузитьПлатежныеДокументыПослеОбновленияВФоне(ДлительнаяОперация);
		Возврат Неопределено;
	Иначе
		УстановитьСостояниеПлатежныхДокументовДляВыгрузки(Ложь);
		Возврат ДлительнаяОперация;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПлатежныеДокументыПослеОбновленияВФоне(Результат)
	
	Если Результат = Неопределено Тогда // задание было отменено
		Возврат;
	КонецЕсли;
	
	УстановитьСостояниеПлатежныхДокументовДляВыгрузки(Истина);
	
	ТекущийДокументДоЗагрузки = ТекущийДокумент();
	
	ДокументыСИзмененнымФлагомВыгрузки = Обработки.КлиентБанк.ДокументыСИзмененнымФлагомВыгрузки(Объект.ПлатежныеДокументы, ИспользуетсяСогласованиеПлатежей);
	
	ПлатежныеДокументы = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Объект.ПлатежныеДокументы.Очистить();
	Объект.ПлатежныеДокументы.Загрузить(ПлатежныеДокументы);
	
	Если ЭтоМобильныйКлиент Тогда
		Для каждого Строка Из Объект.ПлатежныеДокументы Цикл
			Строка.КраткоеПредставлениеДокументаМК = СтрШаблон(НСтр("ru = '%1 от %2'"), Строка.Номер, Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
		КонецЦикла;
	КонецЕсли;
	
	УстановитьТекущийДокумент(ТекущийДокументДоЗагрузки);
	
	Если ДокументыСИзмененнымФлагомВыгрузки.Количество() > 0 Тогда
		ВосстановитьИзмененияФлаговВыгрузки(ДокументыСИзмененнымФлагомВыгрузки);
	КонецЕсли;
	
	ЕстьСтрокиЭкспортаСОшибками      = Ложь;
	СтатуснаяСтрокаОшибкиПриВыгрузке = "";
	Обработки.КлиентБанк.ПроверитьЗаполнениеТаблицыДокументов(Объект, Объект.ПлатежныеДокументы, ЕстьСтрокиЭкспортаСОшибками);
	
	Элементы.ГруппаСообщенияОбОшибкеПриВыгрузкеСтраницы.Видимость = ЕстьСтрокиЭкспортаСОшибками;
	
	ПлатежныеДокументыОбновитьИтогиВПодвале();     
	
	УстановитьЭлементыСогласованиеПлатежей(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ТекущийДокумент()
	
	ТекущийДокумент = Неопределено;
	Если Элементы.ПлатежныеДокументы.ТекущаяСтрока <> Неопределено Тогда
		
		ТекущиеДанные = Объект.ПлатежныеДокументы.НайтиПоИдентификатору(Элементы.ПлатежныеДокументы.ТекущаяСтрока);
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущийДокумент = ТекущиеДанные.Документ;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекущийДокумент;
	
КонецФункции

&НаСервере
Процедура УстановитьТекущийДокумент(ТекущийДокумент)
	
	Если ТекущийДокумент <> Неопределено Тогда
		СтрокиТекущегоДокумента = Объект.ПлатежныеДокументы.НайтиСтроки(Новый Структура("Документ", ТекущийДокумент));
		Если СтрокиТекущегоДокумента.Количество() > 0 Тогда
			Элементы.ПлатежныеДокументы.ТекущаяСтрока = СтрокиТекущегоДокумента[0].ПолучитьИдентификатор()
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьИзмененияФлаговВыгрузки(ДокументыСИзмененнымФлагомВыгрузки)
	
	Для Каждого ПлатежныйДокумент Из Объект.ПлатежныеДокументы Цикл
		СохраненноеЗначениеВыгружать = ДокументыСИзмененнымФлагомВыгрузки.Получить(ПлатежныйДокумент.Документ);
		Если СохраненноеЗначениеВыгружать <> Неопределено Тогда
			ПлатежныйДокумент.Выгружать = СохраненноеЗначениеВыгружать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуОбновленияДокументовНаЭкспортВФоне(ДлительнаяОперация)
	
	УстановитьСостояниеПлатежныхДокументовДляВыгрузки(Истина);
	
	Текст = НСтр("ru = 'Ошибка при заполнении списка документов для выгрузки:'");
	Текст = Текст + Символы.ПС + ДлительнаяОперация.КраткоеПредставлениеОшибки;
	ВызватьИсключение Текст;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеПлатежныхДокументовДляВыгрузки(ПлатежныеДокументыПодготовлены)
	
	Элементы.ПлатежныеДокументы.Видимость = ПлатежныеДокументыПодготовлены;
	Элементы.ГруппаОжиданиеЗаполненияПлатежныхДокументов.Видимость = НЕ ПлатежныеДокументыПодготовлены;
	Элементы.КомандаВыгрузить.Доступность = ПлатежныеДокументыПодготовлены;
	Элементы.ГруппаСообщенияОбОшибкеПриВыгрузкеСтраницы.Видимость = ПлатежныеДокументыПодготовлены;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокДокументовНаЭкспорт(Очищать = Истина)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> Элементы.ГруппаВыгрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Очищать Тогда
		ОчиститьСообщения();
		Объект.ПлатежныеДокументы.Очистить();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Объект.ВыгружатьПлатежноеПоручение И НЕ Объект.ВыгружатьПлатежноеТребование Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо отметить хотя бы один из видов платежных документов.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Банковский счет");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.БанковскийСчет");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.КонецПериода) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения("Поле", "Заполнение", "Конец периода выгрузки");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.КонецПериода");
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперацияЗаполненияДокументовНаЭкспорт = ОбновитьДокументыНаЭкспортВФоне(Объект.ВыгружатьПлатежноеПоручение, Объект.ВыгружатьПлатежноеТребование);
	
	ЗапуститьПроверкуФоновогоЗаданияОбновитьСписокДокументовНаЭкспорт(ДлительнаяОперацияЗаполненияДокументовНаЭкспорт);
	
КонецПроцедуры

&НаСервере
Процедура ПлатежныеДокументыУстановитьОтметку(Отметка)
	
	Для каждого СтрокаДокумента Из Объект.ПлатежныеДокументы Цикл
		СтрокаДокумента.Выгружать = Отметка;
	КонецЦикла;
	
	ПлатежныеДокументыОбновитьИтогиВПодвале();
	
КонецПроцедуры

// Функция вызывает функцию выгрузки документов из модуля объекта.
//
&НаСервере
Функция ВыгрузитьДокументы()
	
	Возврат Обработки.КлиентБанк.Выгрузить(ТаблицаДокументов.Выгрузить(),
											Объект.ПлатежныеДокументы,
											?(ЭтоЦифровойСчет, "UTF", Объект.Кодировка),
											Объект.Программа,
											Объект.НачалоПериода,
											Объект.КонецПериода,
											Объект.БанковскийСчет,
											ВерсияФормата,
											УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьВидыДоговоров(Знач ВидОперации)
	
	Возврат РаботаСДоговорамиКонтрагентовБПВызовСервера.ВидыДоговоровДокумента(ВидОперации);
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСписокДокументовНаЭкспортЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда // задание было отменено
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		ПоказатьОшибкуОбновленияДокументовНаЭкспортВФоне(ДлительнаяОперация);
	Иначе
		ЗагрузитьПлатежныеДокументыПослеОбновленияВФоне(ДлительнаяОперация);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПроверкуФоновогоЗаданияОбновитьСписокДокументовНаЭкспорт(ДлительнаяОперация)
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьСписокДокументовНаЭкспортЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИсправитьСчетаКонтрагентовИОбновитьВФоне(РасчетныйСчетДоИсправления, КонтрагентПослеИсправления, РасчетныйСчетПослеИсправления)
	
	Для Каждого ПлатежныйДокумент Из Объект.ПлатежныеДокументы Цикл
		
		Если ПлатежныйДокумент.КонтрагентСчет = РасчетныйСчетДоИсправления Тогда
			
			УстановитьНовыйРасчетныйСчетКонтрагента(ПлатежныйДокумент.Документ, КонтрагентПослеИсправления, РасчетныйСчетПослеИсправления);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОбновитьДокументыНаЭкспортВФоне(Объект.ВыгружатьПлатежноеПоручение, Объект.ВыгружатьПлатежноеТребование);
	
КонецФункции

&НаСервере
Процедура УстановитьНовыйРасчетныйСчетКонтрагента(Документ, Контрагент, СчетКонтрагента)
	
	ДокументОбъект = Документ.ПолучитьОбъект();
	
	ДокументОбъект.Контрагент      = Контрагент;
	ДокументОбъект.СчетКонтрагента = СчетКонтрагента;
	
	АвтоЗначенияРеквизитов = УчетДенежныхСредствБП.СформироватьАвтоЗначенияРеквизитовПлательщикаПолучателя(
		ДокументОбъект.Организация,
		ДокументОбъект.СчетОрганизации,
		ДокументОбъект.Контрагент,
		ДокументОбъект.СчетКонтрагента,
		ДокументОбъект.ПеречислениеВБюджет,
		ДокументОбъект.Дата);
	
	Если ДокументОбъект.КПППолучателя <> АвтоЗначенияРеквизитов.КПППолучателя Тогда
		ЗаполнитьЗначенияСвойств(ДокументОбъект, АвтоЗначенияРеквизитов, "ТекстПолучателя, ИННПолучателя, КПППолучателя", "");
	Иначе
		ЗаполнитьЗначенияСвойств(ДокументОбъект, АвтоЗначенияРеквизитов, "ТекстПолучателя");
	КонецЕсли;
	
	ДокументОбъект.Записать(?(ДокументОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
	
КонецПроцедуры

&НаКлиенте
Процедура ИсправитьСчетаКонтрагентовЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") 
		И РезультатВыполнения.Свойство("ПерезаполнитьКонтрагента") Тогда
		
		ТекущиеДанные = Элементы.ПлатежныеДокументы.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			
			РасчетныйСчетДоИсправления    = ТекущиеДанные.КонтрагентСчет;
			РасчетныйСчетПослеИсправления = РезультатВыполнения.СчетКонтрагента;
			
			ДлительнаяОперация = ИсправитьСчетаКонтрагентовИОбновитьВФоне(РасчетныйСчетДоИсправления,
				РезультатВыполнения.Контрагент, РасчетныйСчетПослеИсправления);
			
			Если ДлительнаяОперация <> Неопределено Тогда
				ЗапуститьПроверкуФоновогоЗаданияОбновитьСписокДокументовНаЭкспорт(ДлительнаяОперация);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаИзБанка

#Область ПолучениеВыписки

&НаКлиенте
Процедура ПрочитатьФайлВыпискиНаКлиенте(ОписаниеФайла   = Неопределено,
										Кодировка       = Неопределено,
										РежимЗагрузки = "") Экспорт
	
	Если СпособЗагрузки = "Файл" И ОписаниеФайла <> Неопределено Тогда
		Если РасширениеРаботыСФайламиПодключено
			И ТипЗнч(ОписаниеФайла) = Тип("ОписаниеПереданногоФайла")
			И ЗначениеЗаполнено(ОписаниеФайла.ПолноеИмя) Тогда
			Объект.ФайлЗагрузки = ОписаниеФайла.ПолноеИмя;
		Иначе
			Объект.ФайлЗагрузки = ОписаниеФайла.Имя;
		КонецЕсли;
		
		ОбновитьСпискиДокументов();
	КонецЕсли;
	
	Если РежимЗагрузки = "ЦифровойСчет" Тогда
		ДлительнаяОперация = ПрочитатьФайлВыпискиНаСервере(ОписаниеФайла, "UTF-8", "ЦифровойСчет");
	Иначе
		ДлительнаяОперация = ПрочитатьФайлВыпискиНаСервере(ОписаниеФайла, Кодировка);
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	ИначеЕсли ДлительнаяОперация.Статус = "ОшибкаЗагрузкиЦР" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация, Истина);
	ИначеЕсли ДлительнаяОперация.Статус = "ЗапроситьВыпискуЗаново" Тогда
		ЗапроситьЭлектроннуюВыпискуБанка();
	Иначе
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ТекстСообщения = НСтр("ru = 'Чтение данных из банка'");
		
		Обработчик = Новый ОписаниеОповещения("ПослеЧтенияБанковскойВыпискиВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЧтенияБанковскойВыпискиВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ОбработатьРезультатПрочитаннойВыписки(ДлительнаяОперация.АдресРезультата);
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПрочитаннойВыписки(АдресХранилища)
	
	АдресХранилищаРаспознанныеДанныеИзБанка = АдресХранилища;
	СоответствиеВыпискиОтбору = СоответствиеВыпискиОтбору(АдресХранилища, Объект.БанковскийСчет, Объект.Организация);
	ВыполненаЗагрузка = Ложь;
	
	Если СоответствиеВыпискиОтбору.НетСчетовОрганизации Тогда
		ТекстПредупреждения = СтрШаблон(
			НСтр("ru = 'Перед загрузкой банковской выписки укажите основной банковский счет в реквизитах организации %1.'"),
			СоответствиеВыпискиОтбору.Организация);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
		ИЛИ ЗначениеЗаполнено(Объект.БанковскийСчет) И СоответствиеВыпискиОтбору.Соответствует И СоответствиеВыпискиОтбору.БанковскиеСчетаВыписки.Количество() = 1 Тогда
		
		Объект.БанковскийСчет  = СоответствиеВыпискиОтбору.БанковскийСчет;
		Если СоответствиеВыпискиОтбору.Свойство("Организация")
			И ЗначениеЗаполнено(СоответствиеВыпискиОтбору.Организация)
			И (Объект.Организация <> СоответствиеВыпискиОтбору.Организация) Тогда
			Объект.Организация = СоответствиеВыпискиОтбору.Организация;
		КонецЕсли;
		РазместитьНаФормеРаспознанныеДанныеИзБанка(АдресХранилища);
		
	ИначеЕсли СоответствиеВыпискиОтбору.БанковскиеСчетаВыписки.Количество() > 0 Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьРезультатПрочитаннойВыпискиЗавершение", ЭтотОбъект, СоответствиеВыпискиОтбору);
		
		КолВоСчетов = СоответствиеВыпискиОтбору.БанковскиеСчетаВыписки.Количество();
		СтруктураПараметровТекста = Новый Структура;
		СтруктураПараметровТекста.Вставить("ШаблонДругимДругому",
			?(КолВоСчетов = 1, НСтр("ru = 'другому счету'"), НСтр("ru = 'другим счетам'")));
		СтруктураПараметровТекста.Вставить("ПереченьСчетов",
			СтрСоединить(Новый ФиксированныйМассив(СоответствиеВыпискиОтбору.БанковскиеСчетаВыписки), Символы.ПС));
		СтруктураПараметровТекста.Вставить("ВыбранныйСчет", Объект.БанковскийСчет);
		
		Если СоответствиеВыпискиОтбору.Соответствует Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
				НСтр("ru = 'В файле содержатся выписки по нескольким счетам:
				|
				|[ПереченьСчетов]
				|
				|Для загрузки выбран счет [ВыбранныйСчет].
				|По каким счетам показать операции?'"),
				СтруктураПараметровТекста);
			СписокКнопок = Новый СписокЗначений;
			СписокКнопок.Добавить(КодВозвратаДиалога.Да,     "По всем");
			СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, "По выбранному");
		Иначе
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
				НСтр("ru = 'Для загрузки выбран счет [ВыбранныйСчет]
				|
				|В файле содержатся выписки по [ШаблонДругимДругому]:
				|
				|[ПереченьСчетов]'"),
				СтруктураПараметровТекста);
			
			СписокКнопок = Новый СписокЗначений;
			СписокКнопок.Добавить(КодВозвратаДиалога.Да,     "Прочитать");
			СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
		КонецЕсли;
		
		ПоказатьВопрос(
			ОписаниеОповещения,
			ТекстВопроса,
			СписокКнопок);
	ИначеЕсли Не СоответствиеВыпискиОтбору.Соответствует И СоответствиеВыпискиОтбору.ЭтоСчетДепозита Тогда
		ОписаниеОповещения =
			Новый ОписаниеОповещения("ОбработатьРезультатПрочитаннойВыпискиЗавершение", ЭтотОбъект, СоответствиеВыпискиОтбору);
		ТекстВопроса = НСтр("ru = 'В файле содержится выписка по другому счету.'");
		
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(КодВозвратаДиалога.Да,     "Прочитать");
		СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
		ПоказатьВопрос(
			ОписаниеОповещения,
			ТекстВопроса,
			СписокКнопок);
	Иначе
		РазместитьНаФормеРаспознанныеДанныеИзБанка(АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПрочитаннойВыпискиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИзменилсяБанковскийСчет = Ложь;
	ИзмениласьОрганизация   = Ложь;
	Если Результат = КодВозвратаДиалога.Да Тогда
		ИзменилсяБанковскийСчет = ЗначениеЗаполнено(Объект.БанковскийСчет)
			И Объект.БанковскийСчет <> ДополнительныеПараметры.БанковскийСчет;
		ИзмениласьОрганизация = Объект.Организация <> ДополнительныеПараметры.Организация;
		Объект.БанковскийСчет = ДополнительныеПараметры.БанковскийСчет;
		Объект.Организация    = ДополнительныеПараметры.Организация;
		АдресВыписки = ?(ИзменилсяБанковскийСчет Или ИзмениласьОрганизация, "", АдресВыписки);
	КонецЕсли;
	
	Если ИзменилсяБанковскийСчет Тогда
		ПриИзмененииБанковскийСчет(Истина);
	ИначеЕсли ИзмениласьОрганизация Тогда
		ПоВсемСчетам = Не ЗначениеЗаполнено(Объект.БанковскийСчет) И ИспользоватьНесколькоБанковскихСчетов();
		ПриИзмененииОрганизацияСервер(ПоВсемСчетам);
	КонецЕсли;
	
	Если ДополнительныеПараметры.Соответствует ИЛИ Результат = КодВозвратаДиалога.Да Тогда
		РазместитьНаФормеРаспознанныеДанныеИзБанка(АдресХранилищаРаспознанныеДанныеИзБанка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьТекущуюСтроку()
	
	Если ДокументыКИмпорту_ТекущаяСтрока > 0 И ДокументыКИмпорту.Количество() > ДокументыКИмпорту_ТекущаяСтрока Тогда
		ТекущаяСтрока = ДокументыКИмпорту[ДокументыКИмпорту_ТекущаяСтрока];
		Если ТекущаяСтрока <> Неопределено Тогда
			Элементы.ДокументыКИмпорту.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	ДокументыКИмпорту_ТекущаяСтрока = 0;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НовыйРезультатЧтенияВыписки()
	
	Результат = Новый Структура;
	Результат.Вставить("Статус",                       "");
	Результат.Вставить("КраткоеПредставлениеОшибки",   "");
	Результат.Вставить("ПодробноеПредставлениеОшибки", "");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПрочитатьФайлВыпискиНаСервере(ОписаниеФайла, Кодировка, РежимЗагрузки = "")
	
	Если СпособЗагрузки = "ДиректБанк" Или СпособЗагрузки = "ДиректБанкПоБанку" Тогда
		Если ОписаниеФайла = Неопределено Тогда
			Результат = НовыйРезультатЧтенияВыписки();
			Если Не ЗначениеЗаполнено(АдресВыписки) Тогда
				Результат.Статус = "Ошибка";
				Возврат Результат;
			КонецЕсли;
			
			ВыпискаБанка = ПолучитьИзВременногоХранилища(АдресВыписки);
			Если ВыпискаБанка = Неопределено Тогда
				Результат.Статус = "ЗапроситьВыпискуЗаново";
				АдресВыписки = "";
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ОписаниеФайла = Неопределено Тогда
		// Некорректное состояние обмена - нет файла и нет электронной выписки.
		Результат = НовыйРезультатЧтенияВыписки();
		Результат.Статус = "Ошибка";
		Возврат Результат;
	Иначе
		Если РасширениеРаботыСФайламиПодключено
			И ТипЗнч(ОписаниеФайла) = Тип("ОписаниеПереданногоФайла")
			И ЗначениеЗаполнено(ОписаниеФайла.ПолноеИмя) Тогда
			ФайлЗагрузки = ОписаниеФайла.ПолноеИмя;
		Иначе
			ФайлЗагрузки = ОписаниеФайла.Имя;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Настройки", НастройкиЗагрузки());
	СтруктураПараметров.Вставить("ФайлЗагрузки", ФайлЗагрузки);
	СтруктураПараметров.Настройки.Кодировка = Кодировка;
	Если ЗначениеЗаполнено(ВыпискаБанка) Тогда
		СтруктураПараметров.Вставить("ВыпискаБанка", ВыпискаБанка);
	ИначеЕсли ОписаниеФайла <> Неопределено И Не ПустаяСтрока(ОписаниеФайла.Хранение) Тогда
		СтруктураПараметров.Вставить("ДвоичныеДанные", ПолучитьИзВременногоХранилища(ОписаниеФайла.Хранение));
	КонецЕсли;
	
	СведенияОВладельцеБанковскогоСчетаИзКонтекста = Новый Структура;
	СведенияОВладельцеБанковскогоСчетаИзКонтекста.Вставить("Организация",               Новый Массив);
	СведенияОВладельцеБанковскогоСчетаИзКонтекста.Вставить("БанковскийСчетОрганизации", Новый Массив);
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		СведенияОВладельцеБанковскогоСчетаИзКонтекста.Организация.Добавить(Объект.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		СведенияОВладельцеБанковскогоСчетаИзКонтекста.БанковскийСчетОрганизации.Добавить(Объект.БанковскийСчет);
	ИначеЕсли РежимЗагрузки = "ЦифровойСчет" Тогда
		ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		БанковскиеСчетаЦифровые = Справочники.БанковскиеСчета.БанковскиеСчетаОрганизации(
			Объект.Организация, ВалютаРеглУчета, , Истина);
		Если БанковскиеСчетаЦифровые.Количество() > 0 Тогда
			СчетЦифровогоРубля = БанковскиеСчетаЦифровые[0].Ссылка;
			СведенияОВладельцеБанковскогоСчетаИзКонтекста.БанковскийСчетОрганизации.Добавить(СчетЦифровогоРубля);
		Иначе
			Результат = Новый Структура("Статус, КраткоеПредставлениеОшибки");
			Результат.Статус = "ОшибкаЗагрузкиЦР";
			Результат.КраткоеПредставлениеОшибки = НСтр("ru = 'У организации нет счета цифрового рубля'");
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПараметров.Вставить("СведенияОВладельцеБанковскогоСчетаИзКонтекста", СведенияОВладельцеБанковскогоСчетаИзКонтекста);
	
	СтруктураПараметров.Вставить("РежимЗагрузки", РежимЗагрузки);
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Чтение данных из банка'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.КлючФоновогоЗадания = Обработки.КлиентБанк.КлючФоновогоЗаданияЗагрузкаВыписки();
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.КлиентБанк.ФоноваяРаспознатьДанныеИзБанка",
		СтруктураПараметров,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьИзВыписки()
	
	ОбновитьСпискиДокументов();
	
	Если СпособЗагрузки = "ДиректБанк"
		Или СпособЗагрузки = "ДиректБанкПоБанку" Тогда
		ЗапроситьЭлектроннуюВыпискуБанка(Истина);
	Иначе
		ОбновитьВыпискуЧерезФайл();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьЭлектроннуюВыпискуБанка(Обновить = Ложь)
	
	Если Не ПрямойОбменСБанками Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСпискиДокументов();
	
	Если СпособЗагрузки = "ДиректБанк" И Не ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Нстр("ru = 'Не выбран банковский счет'")
			,, "Объект.БанковскийСчет");
		Возврат;
	ИначеЕсли СпособЗагрузки = "ДиректБанкПоБанку" Тогда
		ЕстьПредупреждение = Ложь;
		Если Не ЗначениеЗаполнено(Банк) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Нстр("ru = 'Не выбран банк'")
				,, "Банк",, ЕстьПредупреждение);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Нстр("ru = 'Не выбрана организация'")
				,, "Объект.Организация",, ЕстьПредупреждение);
		КонецЕсли;
		
		Если ЕстьПредупреждение Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Проверим период
	Если НЕ ЗначениеЗаполнено(Объект.НачалоПериода) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Нстр("ru = 'Не заполнена дата начала периода'")
			,, "Объект.НачалоПериода");
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.КонецПериода) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Нстр("ru = 'Не заполнена дата окончания периода'")
			,, "Объект.КонецПериода");
		Возврат;
	КонецЕсли;
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	Если Объект.КонецПериода > КонецДня(ТекущаяДатаСеанса) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дата окончания периода не может быть больше текущей даты'")
			,, "Объект.КонецПериода");
		Возврат;
	ИначеЕсли Объект.НачалоПериода > Объект.КонецПериода Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Дата начала не может быть больше даты окончания периода'")
			,, "Объект.НачалоПериода");
		Возврат;
	КонецЕсли;
	
	Если Обновить И ВыпискиБанка.Количество() > 0 И ЗначениеЗаполнено(АдресВыписки) И ЭтоАдресВременногоХранилища(АдресВыписки) Тогда
		ПрочитатьФайлВыпискиНаКлиенте();
	Иначе
		ДополнительныеПараметры = Новый Структура;
		Оповещение = Новый ОписаниеОповещения("ЗапроситьЭлектроннуюВыпискуБанкаЗавершение",
			ЭтотОбъект, ДополнительныеПараметры);
		
		Если СпособЗагрузки = "ДиректБанкПоБанку" Тогда
			ЗапрашиваемыеРеквизиты = Новый Структура("ПрограммаБанка", Неопределено);
			ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
				Объект.СоглашениеПрямогоОбменаСБанками, ЗапрашиваемыеРеквизиты);
			Если ЗапрашиваемыеРеквизиты.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.SberAPI") Тогда
				ДополнительныеПараметры.Вставить("Форма", ЭтотОбъект);
				ДополнительныеПараметры.Вставить("ОповещениеИзОбработки", Оповещение);
				Оповещение = Новый ОписаниеОповещения("ПолучитьВыпискуБанкаЗавершение",
					БанкИКассаФормыКлиент, ДополнительныеПараметры);
			КонецЕсли;
			ОбменСБанкамиКлиент.ПолучитьВыпискуПоНастройкеОбмена(
				Оповещение, Объект.СоглашениеПрямогоОбменаСБанками, Объект.НачалоПериода, Объект.КонецПериода);
		Иначе
			ОбменСБанкамиКлиент.ПолучитьВыписку(Оповещение, Объект.БанковскийСчет, Объект.НачалоПериода, Объект.КонецПериода);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьЭлектроннуюВыпискуБанкаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат)Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Результат.Успех Тогда
		
		ЗапрашиваемыеРеквизиты = Новый Структура("ПрограммаБанка", Неопределено);
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			Объект.СоглашениеПрямогоОбменаСБанками, ЗапрашиваемыеРеквизиты);
		Если Не ДополнительныеПараметры.Свойство("ОкноУжеПоказано")
			И ЗапрашиваемыеРеквизиты.ПрограммаБанка <> ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
				ДополнительныеПараметры.Вставить("ОкноУжеПоказано", Истина);
				ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаЗагрузитьВыпискуИзФайла",
					ЭтотОбъект, ДополнительныеПараметры);
				СписокКнопок       = Новый СписокЗначений;
				СписокКнопок.Добавить(КодВозвратаДиалога.Да,     НСтр("ru = 'Загрузить выписку из файла'"));
				СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Попробовать позже'"));
				ПоказатьВопрос(
					ОписаниеОповещения,
					НСтр("ru = 'При запросе выписки через 1С:ДиректБанк вы отменили загрузку или она завершилась с ошибкой.
						|Хотите загрузить выписку из файла?'"),
					СписокКнопок,
					,
					КодВозвратаДиалога.Да,
					НСтр("ru = 'Загрузка банковской выписки'"));
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ОчиститьЭлектронныеВыписки();
	Если Результат.Выписки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВыпискиБанка.ЗагрузитьЗначения(Результат.Выписки);
	
	АдресВыписки = ЭлектронноеВзаимодействиеБПВызовСервера.ДанныеВыписокБанкаВКоллекцию(Результат.Выписки);
	ПрочитатьФайлВыпискиНаКлиенте();
	
	ЭлектроннаяВыпискаБанка = ТекстСсылкиНаЭлектронныеВыписки(Объект.НачалоПериода, Объект.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаЗагрузитьВыпискуИзФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ЭтоЦифровойСчет Тогда
			ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект,, "ЦифровойСчет");
		Иначе
			ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект, Объект.Кодировка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекстСсылкиНаЭлектронныеВыписки(НачалоПериода, КонецПериода)
	
	ШаблонСтроки = НСтр("ru = 'Выписка банка за период с %1 по %2'");
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки,
		Формат(НачалоПериода, "ДФ=dd.MM.yyyy"), Формат(КонецПериода, "ДФ=dd.MM.yyyy"));
	
КонецФункции

&НаКлиенте
Процедура ОбновитьВыпискуЧерезФайл()
	
	РежимЗагрузки = "";
	Если ЭтоЦифровойСчет Тогда
		РежимЗагрузки = "ЦифровойСчет";
	ИначеЕсли Не ЗначениеЗаполнено(Объект.БанковскийСчет) И ЗначениеЗаполнено(Объект.Организация) Тогда
		РежимЗагрузки = "Универсальная";
	КонецЕсли;
	
	Если РасширениеРаботыСФайламиПодключено И ЗначениеЗаполнено(Объект.ФайлЗагрузки) Тогда
		// Файл определен - можно начинаем чтение
		ОбменСБанкомКлиент.ЗагрузитьВыбранныйФайл(
			Объект.ФайлЗагрузки, ЭтотОбъект, Объект.Кодировка, РежимЗагрузки);
	Иначе
		// Файл не определен - запросим файл у пользователя
		Если РежимЗагрузки = "" Тогда
			ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект, Объект.Кодировка);
		Иначе
			ОбменСБанкомКлиент.ВыбратьИЗагрузитьФайл(ЭтотОбъект,, РежимЗагрузки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РазмещениеВыпискиНаФорме

&НаСервере
Процедура РазместитьНаФормеРаспознанныеДанныеИзБанка(АдресРезультата, ВыводитьСообщения = Истина)
	
	Если ПустаяСтрока(АдресРезультата) Тогда
		Возврат;
	КонецЕсли;
	
	РаспознанныеДанныеИзБанка = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если РаспознанныеДанныеИзБанка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Разместим прочитанные данные на форме
	ЗаполнитьДокументыКИмпортуПоРаспознаннымДаннымИзБанка(
		ДокументыКИмпорту, РаспознанныеДанныеИзБанка, ВыводитьСообщения);
	
	АдресХранилищаРаспознанныеДанныеИзБанка = ПоместитьВоВременноеХранилище(
		РаспознанныеДанныеИзБанка, УникальныйИдентификатор);
	
	// Обновим связанные данные формы
	ПоказатьОшибкиНаСервере(Ложь); // Сбросим отбор по ошибкам
	ДокументыКИмпортуОбновитьИтогиВПодвале();
	Элементы.ДокументыКИмпортуСуммаПоступило.ТекстПодвала = Формат(
		ДокументыКИмпорту.Итог("СуммаПоступило"), "ЧЦ=15; ЧДЦ=2");
	Элементы.ДокументыКИмпортуСуммаСписано.ТекстПодвала   = Формат(
		ДокументыКИмпорту.Итог("СуммаСписано"), "ЧЦ=15; ЧДЦ=2");
	Элементы.ДекорацияЗагружаемыйФайл.Доступность = Истина;
	// Если отбор по банковскому счету не установлен, то покажем колонку в таблице
	Элементы.ДокументыКИмпортуБанковскийСчет.Видимость = Не ЗначениеЗаполнено(Объект.БанковскийСчет);
	УстановитьДоступностьКнопкиЗагрузить(ЭтотОбъект);
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		Если Объект.СоздаватьНенайденныеЭлементы Тогда
			Элементы.ДокументыКИмпортуПоказатьОшибки.Видимость       = Истина;
			Элементы.ДокументыКИмпортуПоказатьОшибки.Доступность     = ЕстьОшибкиВДокументыКИмпорту();
			Элементы.ДокументыКИмпортуСоздатьНенайденное.Видимость   = Ложь;
			ПоказатьОшибкиИмпорта = Ложь;
		Иначе
			Элементы.ДокументыКИмпортуСоздатьНенайденное.Видимость   = Истина;
			Элементы.ДокументыКИмпортуСоздатьНенайденное.Доступность = ТребуетсяСозданиеНенайденныхВТаблице(ДокументыКИмпорту);
			Элементы.ДокументыКИмпортуПоказатьОшибки.Видимость       = Ложь;
			ПоказатьОшибкиИмпорта = Истина;
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура РазместитьПредупреждение(СтрокаТаблицы, ПредупрежденияРаспознавания = Неопределено, РезультатСозданияДокументов = Неопределено)
	
	МассивПредупреждений = Новый Массив;
	Если ПредупрежденияРаспознавания <> Неопределено Тогда
		Для Каждого Предупреждение Из ПредупрежденияРаспознавания Цикл
			Если Предупреждение.КодПроблемы = "СлучайныйВыбор" Тогда
				МассивПредупреждений.Добавить(Предупреждение.Текст);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если РезультатСозданияДокументов <> Неопределено Тогда
		РезультатСозданияДокумента = РезультатСозданияДокументов[СтрокаТаблицы.ИдентификаторОперации];
		Если РезультатСозданияДокумента <> Неопределено И НЕ РезультатСозданияДокумента.Успешно Тогда
			МассивПредупреждений.Добавить(РезультатСозданияДокумента.КраткоеПредставлениеОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(СтрокаТаблицы.ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийСписаниеДенежныхСредств") Тогда
		ТребуетсяКонтрагент = Документы.СписаниеСРасчетногоСчета.КонтрагентДолженБытьЗаполнен(СтрокаТаблицы.ВидОперации);
	Иначе
		ТребуетсяКонтрагент = Документы.ПоступлениеНаРасчетныйСчет.КонтрагентДолженБытьЗаполнен(СтрокаТаблицы.ВидОперации);
	КонецЕсли;
	
	Если ТребуетсяКонтрагент И НЕ ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) И НЕ ЗначениеЗаполнено(СтрокаТаблицы.КонтрагентСсылкаДляНового) Тогда
		МассивПредупреждений.Добавить(НСтр("ru='Не заполнены реквизиты контрагента'"));
	КонецЕсли;
	
	Если СтрокаТаблицы.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет")
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагента) 
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового) Тогда
		МассивПредупреждений.Добавить(НСтр("ru='Не заполнен счет получателя'"));
	КонецЕсли;
	Если СтрокаТаблицы.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ПереводСДругогоСчета")
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагента) 
		И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового) Тогда
		МассивПредупреждений.Добавить(НСтр("ru='Не заполнен счет отправителя'"));
	КонецЕсли;
	
	Если МассивПредупреждений.Количество() > 0 Тогда
		СтрокаТаблицы.ОписаниеОшибок = СтрСоединить(МассивПредупреждений, "; ");
		СтрокаТаблицы.ЕстьОшибка     = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументыКИмпортуПоРаспознаннымДаннымИзБанка(ДокументыКИмпорту, РаспознанныеДанныеИзБанка, ВыводитьСообщения)
	
	ДокументыКИмпорту.Очистить();
	
	Если ВыводитьСообщения Тогда
		// Сообщим пользователю об ошибках в выписке
		Для Каждого ОшибкаГруппировкиВыписки Из РаспознанныеДанныеИзБанка.ПротоколГруппировкиВыписки Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОшибкаГруппировкиВыписки.ПолноеОписание);
		КонецЦикла;
	КонецЕсли;
	
	НомерСтроки = 1;
	Для Каждого КлючИЗначение Из РаспознанныеДанныеИзБанка.Выписки Цикл
		
		ИдентификаторВыписки = КлючИЗначение.Ключ;
		Выписка              = КлючИЗначение.Значение;
		РаспознаннаяВыписка  = РаспознанныеДанныеИзБанка.РаспознанныеВыписки[ИдентификаторВыписки];
		Если РаспознаннаяВыписка = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПредупреждениеНеНайденБанковскийСчет = РаспознаннаяВыписка.Предупреждения.Найти("БанковскийСчетНеНайден", "КодПроблемы");
		Если ПредупреждениеНеНайденБанковскийСчет <> Неопределено Тогда
			Если ВыводитьСообщения Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ПредупреждениеНеНайденБанковскийСчет.Текст);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		// Ограничим загружаемую выписку
		БанковскийСчет = РаспознаннаяВыписка.ВладелецСчета.БанковскийСчет;
		Если ЗначениеЗаполнено(Объект.БанковскийСчет) И БанковскийСчет <> Объект.БанковскийСчет Тогда
			Продолжить;
		КонецЕсли;
		
		ВалютаСчета = РаспознаннаяВыписка.ВладелецСчета.ВалютаСчета;
		
		ДанныеДокументов = ДанныеДокументов(Выписка, РаспознаннаяВыписка);
		
		Для Каждого Операция Из Выписка.Операции Цикл
			
			СтрокаТаблицы                           = ДокументыКИмпорту.Добавить();
			СтрокаТаблицы.ИдентификаторВыписки      = ИдентификаторВыписки;
			СтрокаТаблицы.Загружать                 = Истина;
			СтрокаТаблицы.НомерСтроки               = НомерСтроки;
			СтрокаТаблицы.БанковскийСчетОрганизации = БанковскийСчет;
			СтрокаТаблицы.ВалютаСчетаОрганизации    = ВалютаСчета;
			НомерСтроки                             = НомерСтроки + 1;
			
			// Заполним по строкам выписки
			СтрокаТаблицы.ДатаПроведения            = Операция.ДатаИсполнения;
			СтрокаТаблицы.НомерДок                  = Операция.НомерДокумента;
			СтрокаТаблицы.НазначениеПлатежа         = Операция.НазначениеПлатежа;
			СтрокаТаблицы.ИдентификаторОперации     = Операция.Идентификатор;
			Если Операция.ПоступилоСписано = "Поступило" Тогда
				СтрокаТаблицы.СуммаПоступило = Операция.Сумма;
			Иначе
				СтрокаТаблицы.СуммаСписано   = Операция.Сумма;
			КонецЕсли;
			
			// Заполним по строкам распознанной выписки
			РаспознаннаяОперация = РаспознаннаяВыписка.Операции.Найти(Операция.Идентификатор, "Идентификатор");
			Если РаспознаннаяОперация = Неопределено Тогда 
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицы.ДокументОснование  = РаспознаннаяОперация.ПлатежноеПоручение;
			СтрокаТаблицы.Документ           = РаспознаннаяОперация.Ссылка;
			
			ХозяйственнаяОперация = РаспознаннаяОперация.ХозяйственнаяОперация;
			ДанныеДокумента       = ДанныеДокументов[Операция.Идентификатор];
			Если ХозяйственнаяОперация <> Неопределено Тогда
				ЗаполнитьСтрокуПоХозяйственнойОперации(РаспознанныеДанныеИзБанка, СтрокаТаблицы, ХозяйственнаяОперация);
			ИначеЕсли ДанныеДокумента <> Неопределено Тогда
				ЗаполнитьСтрокуПоДаннымДокумента(СтрокаТаблицы, ДанныеДокумента);
			КонецЕсли;
			
			СтрокаТаблицы.ДоговорРазрешеноИзменение =
				Не (ЗначениеЗаполнено(СтрокаТаблицы.ДокументОснование) И ЗначениеЗаполнено(СтрокаТаблицы.Договор));
			
			// Покажем ошибки создания документов если они есть
			Если РаспознанныеДанныеИзБанка.Свойство("СозданиеДокументов") Тогда
				РезультатСозданияДокументов = РаспознанныеДанныеИзБанка.СозданиеДокументов[ИдентификаторВыписки];
			Иначе
				РезультатСозданияДокументов = Неопределено;
			КонецЕсли;
			
			РазместитьПредупреждение(СтрокаТаблицы, РаспознаннаяОперация.Предупреждения, РезультатСозданияДокументов);
			ПодготовитьОтображениеПредупреждений(СтрокаТаблицы, Объект.СоздаватьНенайденныеЭлементы);
			
			Если ЭтоМобильныйКлиент Тогда
				СтрокаТаблицы.СуммаМК = Операция.Сумма;
				
				Если ЗначениеЗаполнено(СтрокаТаблицы.Документ) Тогда
					СтрокаТаблицы.Загружать = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуПоХозяйственнойОперации(РаспознанныеДанныеИзБанка, СтрокаТаблицы, ХозяйственнаяОперация)
	
	ШаблоныТекстов = Новый Структура;
	ШаблоныТекстов.Вставить("КонтрагентРасчетныйСчет");
	ШаблоныТекстов.Вставить("Договор");
	Если Объект.СоздаватьНенайденныеЭлементы Тогда
		ШаблоныТекстов.КонтрагентРасчетныйСчет = НСтр("ru = 'Не загружен (%1)'");
		ШаблоныТекстов.Договор                 = НСтр("ru = 'Не создан'");
	Иначе
		ШаблоныТекстов.КонтрагентРасчетныйСчет = НСтр("ru = 'Не найден (%1)'");
		ШаблоныТекстов.Договор                 = НСтр("ru = 'Не найден'");
	КонецЕсли;
	
	Если ХозяйственнаяОперация.Свойство("ВидОперацииДокумента") Тогда
		СтрокаТаблицы.ВидОперации = ХозяйственнаяОперация.ВидОперацииДокумента;
	КонецЕсли;
	Если ХозяйственнаяОперация.Свойство("СтатьяДвиженияДенежныхСредств") Тогда
		СтрокаТаблицы.СтатьяДДС   = ХозяйственнаяОперация.СтатьяДвиженияДенежныхСредств;
	КонецЕсли;
	
	// Контрагент
	Если ТипЗнч(СтрокаТаблицы.ВидОперации) = Тип("ПеречислениеСсылка.ВидыОперацийСписаниеДенежныхСредств") Тогда
		ТребуетсяКонтрагент = Документы.СписаниеСРасчетногоСчета.КонтрагентДолженБытьЗаполнен(СтрокаТаблицы.ВидОперации);
	Иначе
		ТребуетсяКонтрагент = Документы.ПоступлениеНаРасчетныйСчет.КонтрагентДолженБытьЗаполнен(СтрокаТаблицы.ВидОперации);
	КонецЕсли;
	Если ХозяйственнаяОперация.Свойство("Контрагент") Тогда
		РаспознанныйКонтрагент = ХозяйственнаяОперация.Контрагент;
		НовыйКонтрагент = ИдентификацияУчастниковБанковскихОпераций.ОписаниеНовогоОбъекта(
			РаспознанныйКонтрагент,
			РаспознанныеДанныеИзБанка.УчастникиОпераций);
		
		Если ЗначениеЗаполнено(РаспознанныйКонтрагент) Тогда
			Если НовыйКонтрагент = Неопределено Тогда
				СтрокаТаблицы.Контрагент = РаспознанныйКонтрагент;
				СтрокаТаблицы.КонтрагентОтображениеНенайденного = "";
				СтрокаТаблицы.РеквизитыКонтрагента = НовыйОписаниеКонтрагента();
			Иначе
				СтрокаТаблицы.КонтрагентСсылкаДляНового = РаспознанныйКонтрагент;
				ТипСсылкиКонтрагенты = Тип("СправочникСсылка.Контрагенты");
				Если ТипЗнч(РаспознанныйКонтрагент) = ТипСсылкиКонтрагенты Тогда
					СтрокаТаблицы.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
					СтрокаТаблицы.КонтрагентОтображениеНенайденного = СтрШаблон(ШаблоныТекстов.КонтрагентРасчетныйСчет, НовыйКонтрагент.НаименованиеПолное);
					
					НовыйКонтрагентОписаниеСсылки = ИдентификацияУчастниковБанковскихОпераций.ОписаниеСсылки(
						РаспознанныйКонтрагент, РаспознанныеДанныеИзБанка.УчастникиОпераций);
					СтрокаТаблицы.РеквизитыКонтрагента = НовыйОписаниеКонтрагента();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы.РеквизитыКонтрагента, НовыйКонтрагент);
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы.РеквизитыКонтрагента, НовыйКонтрагентОписаниеСсылки);
					
					НастройкиЗаполнения = РаспознанныеДанныеИзБанка.Настройки.ЗаполнениеНовыхЭлементов[ТипСсылкиКонтрагенты];
					Если НастройкиЗаполнения <> Неопределено Тогда
						ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтрокаТаблицы.РеквизитыКонтрагента, НастройкиЗаполнения, Истина);
					КонецЕсли;
				ИначеЕсли ТипЗнч(РаспознанныйКонтрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
					Наименование = СокрЛП(СтрЗаменить(НовыйКонтрагент.Наименование, НовыйКонтрагент.ИНН, ""));
					СтрокаТаблицы.Контрагент = Справочники.ФизическиеЛица.ПустаяСсылка();
					СтрокаТаблицы.КонтрагентОтображениеНенайденного = СтрШаблон(ШаблоныТекстов.КонтрагентРасчетныйСчет, Наименование);
					СтрокаТаблицы.РеквизитыКонтрагента = НовыйОписаниеКонтрагента();
					СтрокаТаблицы.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
					СтрокаТаблицы.ДоговорОтображениеНенайденного = "";
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы.РеквизитыКонтрагента, НовыйКонтрагент);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТребуетсяКонтрагент Тогда
		СтрокаТаблицы.Контрагент = Новый(УчетДенежныхСредствКлиентСервер.ТипКонтрагентаПоВидуОперации(СтрокаТаблицы.ВидОперации));
		СтрокаТаблицы.КонтрагентОтображениеНенайденного = НСтр("ru = 'Не указаны реквизиты контрагента'");
		СтрокаТаблицы.РеквизитыКонтрагента = НовыйОписаниеКонтрагента();
	КонецЕсли;
	
	// Счет контрагента
	Если ХозяйственнаяОперация.Свойство("БанковскийСчетКонтрагента") Тогда
		РаспознанныйСчетКонтрагента = ХозяйственнаяОперация.БанковскийСчетКонтрагента;
		Если ЗначениеЗаполнено(РаспознанныйСчетКонтрагента) Тогда
			НовыйСчетКонтрагента = ИдентификацияУчастниковБанковскихОпераций.ОписаниеНовогоОбъекта(
				РаспознанныйСчетКонтрагента,
				РаспознанныеДанныеИзБанка.УчастникиОпераций);
			Если НовыйСчетКонтрагента = Неопределено Тогда
				СтрокаТаблицы.СчетКонтрагента = РаспознанныйСчетКонтрагента;
			Иначе
				СтрокаТаблицы.СчетКонтрагента = Справочники.БанковскиеСчета.ПустаяСсылка();
				СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового = РаспознанныйСчетКонтрагента;
				Если НовыйСчетКонтрагента.ЦифровойСчет Тогда
					СтрокаТаблицы.СчетКонтрагентаОтображениеНенайденного =
						СтрШаблон(ШаблоныТекстов.КонтрагентРасчетныйСчет, НовыйСчетКонтрагента.ИдентификаторЦифровогоСчета);
				Иначе
					СтрокаТаблицы.СчетКонтрагентаОтображениеНенайденного =
						СтрШаблон(ШаблоныТекстов.КонтрагентРасчетныйСчет, НовыйСчетКонтрагента.НомерСчета);
				КонецЕсли;
			КонецЕсли;
		Иначе
			СтрокаТаблицы.СчетКонтрагента = Справочники.БанковскиеСчета.ПустаяСсылка();
			СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового = РаспознанныйСчетКонтрагента;
			СтрокаТаблицы.СчетКонтрагентаОтображениеНенайденного = НСтр("ru = 'Не найден'");
		КонецЕсли;
	ИначеЕсли ТребуетсяКонтрагент Тогда
		СтрокаТаблицы.СчетКонтрагентаОтображениеНенайденного = НСтр("ru = 'Не указаны реквизиты расчетного счета'");
	КонецЕсли;
	
	// Договор
	Если ХозяйственнаяОперация.Свойство("ДоговорКонтрагента") Тогда
		РаспознанныйДоговор = ХозяйственнаяОперация.ДоговорКонтрагента;
		Если ЗначениеЗаполнено(РаспознанныйДоговор) Тогда
			НовыйДоговор = ИдентификацияУчастниковБанковскихОпераций.ОписаниеНовогоОбъекта(
				РаспознанныйДоговор,
				РаспознанныеДанныеИзБанка.УчастникиОпераций);
			Если НовыйДоговор = Неопределено Тогда
				СтрокаТаблицы.Договор = РаспознанныйДоговор;
			Иначе
				СтрокаТаблицы.ДоговорСсылкаДляНового = РаспознанныйДоговор;
				СтрокаТаблицы.ДоговорОтображениеНенайденного = ШаблоныТекстов.Договор;
			КонецЕсли;
		Иначе
			СтрокаТаблицы.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
			СтрокаТаблицы.ДоговорСсылкаДляНового = РаспознанныйДоговор;
			СтрокаТаблицы.ДоговорОтображениеНенайденного = ШаблоныТекстов.Договор;
		КонецЕсли;
	ИначеЕсли НЕ ТребуетсяКонтрагент Тогда
		СтрокаТаблицы.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		СтрокаТаблицы.ДоговорОтображениеНенайденного = "";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Документ) Тогда
		СтрокаТаблицы.СостояниеДокумента = 3;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НовыйОписаниеКонтрагента()
	
	ОписаниеКонтрагента = Новый Структура;
	ОписаниеКонтрагента.Вставить("Наименование",              "");
	ОписаниеКонтрагента.Вставить("НаименованиеПолное",        "");
	ОписаниеКонтрагента.Вставить("ИНН",                       "");
	ОписаниеКонтрагента.Вставить("КПП",                       "");
	ОписаниеКонтрагента.Вставить("ГосударственныйОрган",      Ложь);
	ОписаниеКонтрагента.Вставить("ВидГосударственногоОргана", Перечисления.ВидыГосударственныхОрганов.ПустаяСсылка());
	ОписаниеКонтрагента.Вставить("ЮридическоеФизическоеЛицо", Перечисления.ЮридическоеФизическоеЛицо.ПустаяСсылка());
	
	Возврат ОписаниеКонтрагента;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСтрокуПоДаннымДокумента(СтрокаТаблицы, ДанныеДокумента)
	
	СтрокаТаблицы.ВидОперации        = ДанныеДокумента.ВидОперацииДокумента;
	СтрокаТаблицы.СтатьяДДС          = ДанныеДокумента.СтатьяДвиженияДенежныхСредств;
	СтрокаТаблицы.Контрагент         = ДанныеДокумента.Контрагент;
	СтрокаТаблицы.СчетКонтрагента    = ДанныеДокумента.БанковскийСчетКонтрагента;
	СтрокаТаблицы.Договор            = ДанныеДокумента.ДоговорКонтрагента;
	СтрокаТаблицы.СостояниеДокумента = ДанныеДокумента.СостояниеДокумента;
	СтрокаТаблицы.НазначениеПлатежа  = ДанныеДокумента.НазначениеПлатежа;
	
	// Если в документе не заполнен контрагент, то подставим информацию из файла
	Если НЕ ЗначениеЗаполнено(ДанныеДокумента.Контрагент) И ДанныеДокумента.Свойство("РеквизитыКонтрагента") Тогда
		РеквизитыКонтрагента = ДанныеДокумента.РеквизитыКонтрагента;
		МассивРеквизитовКонтрагента = Новый Массив;
		// Выводим либо детальное наименование
		
		Если РеквизитыКонтрагента["НаименованиеДетально"] <> Неопределено Тогда
			МассивРеквизитовКонтрагента.Добавить(РеквизитыКонтрагента["НаименованиеДетально"]);
		// Либо просто наименование
		ИначеЕсли РеквизитыКонтрагента["Наименование"] <> Неопределено Тогда
			 МассивРеквизитовКонтрагента.Добавить(РеквизитыКонтрагента["Наименование"]);
		КонецЕсли;
		Если РеквизитыКонтрагента["ИНН"] <> Неопределено Тогда
			МассивРеквизитовКонтрагента.Добавить(СтрШаблон(НСтр("ru='ИНН %1'"), РеквизитыКонтрагента["ИНН"]));
		КонецЕсли;
		Если РеквизитыКонтрагента["КПП"] <> Неопределено Тогда
			МассивРеквизитовКонтрагента.Добавить(СтрШаблон(НСтр("ru='КПП %1'"), РеквизитыКонтрагента["КПП"]));
		КонецЕсли;
		
		СтрокаТаблицы.КонтрагентОтображениеНенайденного = СтрСоединить(МассивРеквизитовКонтрагента, ", ");
	КонецЕсли;
	// Если в документе не заполнен БанковскийСчетКонтрагента, то подставим информацию из файла
	Если НЕ ЗначениеЗаполнено(ДанныеДокумента.БанковскийСчетКонтрагента) И ДанныеДокумента.Свойство("РеквизитыКонтрагента") Тогда
		РеквизитыКонтрагента = ДанныеДокумента.РеквизитыКонтрагента;
		МассивРеквизитовКонтрагента = Новый Массив;
		Если РеквизитыКонтрагента["РасчСчет"] <> Неопределено Тогда
			МассивРеквизитовКонтрагента.Добавить(РеквизитыКонтрагента["РасчСчет"]);
		КонецЕсли;
		Если РеквизитыКонтрагента["БИК"] <> Неопределено Тогда
			МассивРеквизитовКонтрагента.Добавить(СтрШаблон(НСтр("ru='БИК %1'"), РеквизитыКонтрагента["БИК"]));
		КонецЕсли;
		
		СтрокаТаблицы.СчетКонтрагентаОтображениеНенайденного = СтрСоединить(МассивРеквизитовКонтрагента, ", ");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ДанныеДокументов(Выписка, РаспознаннаяВыписка)
	
	ДанныеДокументов = Новый Соответствие;
	
	ЗаполнитьДанныеПлатежныхПоручений(ДанныеДокументов, РаспознаннаяВыписка);
	ЗаполнитьДанныеРанееСозданныхДокументов(ДанныеДокументов, РаспознаннаяВыписка);
	
	Возврат ДанныеДокументов;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеПлатежныхПоручений(ДанныеДокументов, РаспознаннаяВыписка)
	
	ИндексСсылок = Новый Соответствие;
	Для Каждого РаспознаннаяОперация Из РаспознаннаяВыписка.Операции Цикл
		ОперацииСсылки = ИндексСсылок[РаспознаннаяОперация.ПлатежноеПоручение];
		Если ОперацииСсылки = Неопределено Тогда
			ОперацииСсылки = Новый Массив;
			ИндексСсылок.Вставить(РаспознаннаяОперация.ПлатежноеПоручение, ОперацииСсылки);
		КонецЕсли;
		ОперацииСсылки.Добавить(РаспознаннаяОперация);
	КонецЦикла;
	
	Ссылки = Новый Массив;
	Для Каждого КлючИЗначение Из ИндексСсылок Цикл
		Ссылки.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручение.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет)
	|				ИЛИ ПлатежноеПоручение.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.Депозит)
	|			ТОГДА ПлатежноеПоручение.Организация
	|		ИНАЧЕ ПлатежноеПоручение.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ПлатежноеПоручение.СчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ПлатежноеПоручение.ПодразделениеОрганизации КАК Подразделение,
	|	ПлатежноеПоручение.СуммаДокумента КАК Сумма,
	|	ПлатежноеПоручение.СтавкаНДС КАК СтавкаНДС,
	|	ПлатежноеПоручение.СуммаНДС КАК СуммаНДС,
	|	ПлатежноеПоручение.ВидОперации КАК ВидОперацииДокумента,
	|	ПлатежноеПоручение.ПлатежнаяВедомость КАК ВедомостьЗаработнойПлаты,
	|	ПлатежноеПоручение.СуммаПроцентов КАК СуммаПроцентов,
	|	ПлатежноеПоручение.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ПлатежноеПоручение.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ПлатежноеПоручение.НазначениеПлатежа КАК НазначениеПлатежа,
	|	3 КАК СостояниеДокумента
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Ссылка В(&Ссылки)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Исключения = Новый Структура("Ссылка");
	
	Пока Выборка.Следующий() Цикл
		
		ОперацииСсылки = ИндексСсылок[Выборка.Ссылка];
		
		ДанныеДокумента = ЭлементВыборкиЗапросаДанныхДокументовВСтруктуру(Выборка, Исключения);
		
		Для Каждого РаспознаннаяОперация Из ОперацииСсылки Цикл
			
			Если ЗначениеЗаполнено(ДанныеДокумента) Тогда
				ДанныеДокументов.Вставить(РаспознаннаяОперация.Идентификатор, ДанныеДокумента);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеРанееСозданныхДокументов(ДанныеДокументов, РаспознаннаяВыписка)
	
	Ссылки       = Новый Массив;
	ИндексСсылок = Новый Соответствие;
	Для Каждого РаспознаннаяОперация Из РаспознаннаяВыписка.Операции Цикл
		ИндексСсылок.Вставить(РаспознаннаяОперация.Ссылка, РаспознаннаяОперация);// Одна ссылка может быть только в одной строке выписки
		Ссылки.Добавить(РаспознаннаяОперация.Ссылка);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеСРасчетногоСчета.Ссылка КАК Ссылка,
	|	СписаниеСРасчетногоСчета.ВидОперации КАК ВидОперацииДокумента,
	|	ВЫБОР
	|		КОГДА СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет)
	|				ИЛИ СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.Депозит)
	|			ТОГДА СписаниеСРасчетногоСчета.Организация
	|		ИНАЧЕ СписаниеСРасчетногоСчета.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	СписаниеСРасчетногоСчета.СчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	СписаниеСРасчетногоСчета.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СписаниеСРасчетногоСчета.ПодразделениеОрганизации КАК Подразделение,
	|	СписаниеСРасчетногоСчета.СуммаДокумента КАК Сумма,
	|	СписаниеСРасчетногоСчета.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	СписаниеСРасчетногоСчета.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ВЫБОР
	|		КОГДА СписаниеСРасчетногоСчета.Проведен
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СостояниеДокумента,
	|	СписаниеСРасчетногоСчета.РасшифровкаПлатежа.(
	|		СтавкаНДС КАК СтавкаНДС,
	|		СуммаНДС КАК СуммаНДС,
	|		СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|		КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|		СуммаВзаиморасчетов КАК СуммаВзаиморасчетов
	|	) КАК РасшифровкаПлатежа,
	|	СписаниеСРасчетногоСчета.РеквизитыКонтрагента.(
	|		Реквизит КАК Реквизит,
	|		Значение КАК Значение
	|	) КАК РеквизитыКонтрагента
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|ГДЕ
	|	СписаниеСРасчетногоСчета.Ссылка В(&Ссылки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеНаРасчетныйСчет.Ссылка,
	|	ПоступлениеНаРасчетныйСчет.ВидОперации,
	|	ВЫБОР
	|		КОГДА ПоступлениеНаРасчетныйСчет.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ПереводСДругогоСчета)
	|				ИЛИ ПоступлениеНаРасчетныйСчет.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеДенежныхСредств.Депозит)
	|			ТОГДА ПоступлениеНаРасчетныйСчет.Организация
	|		ИНАЧЕ ПоступлениеНаРасчетныйСчет.Контрагент
	|	КОНЕЦ,
	|	ПоступлениеНаРасчетныйСчет.СчетКонтрагента,
	|	ПоступлениеНаРасчетныйСчет.ДоговорКонтрагента,
	|	ПоступлениеНаРасчетныйСчет.ПодразделениеОрганизации,
	|	ПоступлениеНаРасчетныйСчет.СуммаДокумента,
	|	ПоступлениеНаРасчетныйСчет.СтатьяДвиженияДенежныхСредств,
	|	ПоступлениеНаРасчетныйСчет.НазначениеПлатежа,
	|	ВЫБОР
	|		КОГДА ПоступлениеНаРасчетныйСчет.Проведен
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ПоступлениеНаРасчетныйСчет.РасшифровкаПлатежа.(
	|		СтавкаНДС,
	|		СуммаНДС,
	|		СтатьяДвиженияДенежныхСредств,
	|		ДоговорКонтрагента.ВалютаВзаиморасчетов,
	|		КурсВзаиморасчетов,
	|		КратностьВзаиморасчетов,
	|		СуммаВзаиморасчетов
	|	),
	|	ПоступлениеНаРасчетныйСчет.РеквизитыКонтрагента.(
	|		Реквизит,
	|		Значение
	|	)
	|ИЗ
	|	Документ.ПоступлениеНаРасчетныйСчет КАК ПоступлениеНаРасчетныйСчет
	|ГДЕ
	|	ПоступлениеНаРасчетныйСчет.Ссылка В(&Ссылки)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СлужебныеКолонки = Новый Структура("РасшифровкаПлатежа, РеквизитыКонтрагента");// Их не надо помещать в ДанныеДокумента
	
	Пока Выборка.Следующий() Цикл
		
		РаспознаннаяОперация = ИндексСсылок[Выборка.Ссылка];
		
		ДанныеДокумента = ЭлементВыборкиЗапросаДанныхДокументовВСтруктуру(Выборка, СлужебныеКолонки);
		
		ВыборкаДетальныхЗаписей = Выборка.РасшифровкаПлатежа.Выбрать();
		Если ВыборкаДетальныхЗаписей.Количество() = 1 Тогда
			ВыборкаДетальныхЗаписей.Следующий();
			ДанныеРасшифровки = ЭлементВыборкиЗапросаДанныхДокументовВСтруктуру(ВыборкаДетальныхЗаписей, СлужебныеКолонки);
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеДокумента, ДанныеРасшифровки, Истина);
		КонецЕсли;
		
		// Дополним данные документа, реквизитами контрагента, которые записываются из файла
		Если НЕ Выборка.РеквизитыКонтрагента.Пустой() Тогда
			ДанныеДокумента.Вставить("РеквизитыКонтрагента", Новый Соответствие);
			ВыборкаРеквизитыКонтрагента = Выборка.РеквизитыКонтрагента.Выбрать();
			Пока ВыборкаРеквизитыКонтрагента.Следующий() Цикл
				Если НЕ ПустаяСтрока(ВыборкаРеквизитыКонтрагента.Реквизит) Тогда
					ДанныеДокумента.РеквизитыКонтрагента.Вставить(ВыборкаРеквизитыКонтрагента.Реквизит, ВыборкаРеквизитыКонтрагента.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеДокумента) Тогда
			ДанныеДокументов.Вставить(РаспознаннаяОперация.Идентификатор, ДанныеДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЭлементВыборкиЗапросаДанныхДокументовВСтруктуру(Выборка, Исключения)
	
	Структура = Новый Структура;
	Для Каждого Колонка Из Выборка.Владелец().Колонки Цикл
		
		Если Исключения.Свойство(Колонка.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		Значение = Выборка[Колонка.Имя];
		Структура.Вставить(Колонка.Имя, Значение);
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

&НаСервереБезКонтекста
Функция СоответствиеВыпискиОтбору(Знач АдресРезультата, Знач БанковскийСчет, Знач Организация = Неопределено)
	
	СоответствиеВыпискиОтбору = Новый Структура;
	СоответствиеВыпискиОтбору.Вставить("Соответствует",        Ложь);
	СоответствиеВыпискиОтбору.Вставить("ЭтоСчетДепозита",      Ложь);
	СоответствиеВыпискиОтбору.Вставить("НетСчетовОрганизации", Ложь);
	СоответствиеВыпискиОтбору.Вставить("БанковскийСчет", БанковскийСчет);
	СоответствиеВыпискиОтбору.Вставить("БанковскиеСчетаВыписки", Новый Массив);
	
	Если ПустаяСтрока(АдресРезультата) Или Не ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		Возврат СоответствиеВыпискиОтбору;
	КонецЕсли;
	
	РаспознанныеДанныеИзБанка = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если РаспознанныеДанныеИзБанка = Неопределено Тогда
		Возврат СоответствиеВыпискиОтбору;
	КонецЕсли;
	
	СоответствиеВыпискиОтбору.Соответствует = Истина;
	
	БанковскиеСчетаВыписки = Новый Массив;
	ОрганизацииВыписки     = Новый Массив;
	
	Для Каждого КлючИЗначение Из РаспознанныеДанныеИзБанка.РаспознанныеВыписки Цикл
		РаспознаннаяВыписка = КлючИЗначение.Значение;
		// Не учитываем счета, не найденные в информационной базе
		ПредупреждениеНеНайденБанковскийСчет = РаспознаннаяВыписка.Предупреждения.Найти("БанковскийСчетНеНайден", "КодПроблемы");
		СчетСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РаспознаннаяВыписка.ВладелецСчета.БанковскийСчет, "Ссылка");
		Если ЗначениеЗаполнено(РаспознаннаяВыписка.ВладелецСчета.Организация)
			И Не ЗначениеЗаполнено(РаспознаннаяВыписка.ВладелецСчета.КоличествоСчетовОрганизации) Тогда
			СоответствиеВыпискиОтбору.НетСчетовОрганизации = Истина;
			ОрганизацииВыписки.Добавить(РаспознаннаяВыписка.ВладелецСчета.Организация);
		ИначеЕсли ПредупреждениеНеНайденБанковскийСчет = Неопределено И СчетСсылка <> Неопределено Тогда
			БанковскиеСчетаВыписки.Добавить(РаспознаннаяВыписка.ВладелецСчета.БанковскийСчет);
			ОрганизацииВыписки.Добавить(РаспознаннаяВыписка.ВладелецСчета.Организация);
		ИначеЕсли ПредупреждениеНеНайденБанковскийСчет = Неопределено И СчетСсылка = Неопределено Тогда
			СоответствиеВыпискиОтбору.ЭтоСчетДепозита = Истина;
			Если ЗначениеЗаполнено(РаспознаннаяВыписка.ВладелецСчета.Организация) Тогда
				ОрганизацииВыписки.Добавить(РаспознаннаяВыписка.ВладелецСчета.Организация);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Оставим только уникальные значения
	БанковскиеСчетаВыписки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(БанковскиеСчетаВыписки);
	ОрганизацииВыписки     = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОрганизацииВыписки);
	
	// Установим организацию
	СоответствиеВыпискиОтбору.Вставить("Организация", Неопределено);
	Если ОрганизацииВыписки.Количество() = 1 Тогда
		СоответствиеВыпискиОтбору.Организация = ОрганизацииВыписки[0];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		НайденныйБанковскийСчет = БанковскиеСчетаВыписки.Найти(БанковскийСчет);
		// В файле нет выписок, соответствующих отбору
		Если НайденныйБанковскийСчет = Неопределено Тогда
			СоответствиеВыпискиОтбору.Соответствует = Ложь;
		Иначе
			// Если в файле есть счет, соответствующий отбору, то установим его первым
			БанковскиеСчетаВыписки.Удалить(НайденныйБанковскийСчет);
			БанковскиеСчетаВыписки.Вставить(0, БанковскийСчет);
		КонецЕсли;
		
		// Подготовим новый отбор по счетам банковской выписки
		Если БанковскиеСчетаВыписки.Количество() = 1 Тогда
			СоответствиеВыпискиОтбору.БанковскийСчет = БанковскиеСчетаВыписки[0];
		Иначе
			СоответствиеВыпискиОтбору.БанковскийСчет = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	// Преобразуем массив в строки и добавим организацию если требуется
	ТребуетсяУказыватьОрганизацию = (ЗначениеЗаполнено(Организация) И СоответствиеВыпискиОтбору.Организация <> Организация);
	
	Для Каждого БанковскийСчет Из БанковскиеСчетаВыписки Цикл
		БанковскийСчетСтрокой = Строка(БанковскийСчет) + ?(ТребуетсяУказыватьОрганизацию, " ("+Строка(СоответствиеВыпискиОтбору.Организация)+")", "");
		СоответствиеВыпискиОтбору.БанковскиеСчетаВыписки.Добавить(БанковскийСчетСтрокой);
	КонецЦикла;
	
	Возврат СоответствиеВыпискиОтбору;
	
КонецФункции

&НаСервере
Функция ЕстьОшибкиВДокументыКИмпорту()

	Возврат (ДокументыКИмпорту.НайтиСтроки(Новый Структура("ЕстьОшибка", Истина)).Количество() > 0);

КонецФункции

#КонецОбласти

#Область ОбработкаСобытийДокументыКИмпорту

&НаСервере
Процедура ПоказатьОшибкиНаСервере(Включить)
	
	Если НЕ Объект.СоздаватьНенайденныеЭлементы Тогда
		Возврат;
	КонецЕсли;
	
	Если Включить Тогда
		ОтборОшибок = Новый Структура("ЕстьОшибка", Истина);
		Элементы.ДокументыКИмпорту.ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборОшибок);
	Иначе
		Элементы.ДокументыКИмпорту.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДокументыКИмпортуУстановитьОтметку(Отметка)
	
	Для каждого СтрокаДокумента Из ДокументыКИмпорту Цикл
		СтрокаДокумента.Загружать = Отметка;
	КонецЦикла;
	
	ДокументыКИмпортуОбновитьИтогиВПодвале();
	УстановитьДоступностьКнопкиЗагрузить(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПодготовитьОтображениеПредупреждений(СтрокаТаблицы, СоздаватьНенайденныеЭлементы)
	
	// Если пользователь выбрал объекты вручную, то очистим ссылки для новых
	Если ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
		СтрокаТаблицы.КонтрагентСсылкаДляНового              = Неопределено;
		СтрокаТаблицы.КонтрагентОтображениеНенайденного      = "";
		СтрокаТаблицы.РеквизитыКонтрагента = НовыйОписаниеКонтрагента();
	КонецЕсли;
	Если ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагента) Тогда
		СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового         = Неопределено;
		СтрокаТаблицы.СчетКонтрагентаОтображениеНенайденного = "";
	КонецЕсли;
	Если ЗначениеЗаполнено(СтрокаТаблицы.Договор) Тогда
		СтрокаТаблицы.ДоговорСсылкаДляНового                 = Неопределено;
		СтрокаТаблицы.ДоговорОтображениеНенайденного         = "";
	КонецЕсли;
	
	// Пользователь мог перевыбрать контрагентов, в этом случае очистим неактуальные предупреждения
	МассивПредупреждений = СтрРазделить(СтрокаТаблицы.ОписаниеОшибок, ";", Ложь);
	АктуальныйМассивПредупреждений = Новый Массив;
	
	Для Каждого Предупреждение Из МассивПредупреждений Цикл
		
		ТекстПредупреждения = СокрЛП(Предупреждение);
		Если ТекстПредупреждения = НСтр("ru='Не заполнены реквизиты контрагента'") И (ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) ИЛИ ЗначениеЗаполнено(СтрокаТаблицы.КонтрагентСсылкаДляНового)) Тогда
			Продолжить;
		ИначеЕсли ТекстПредупреждения = НСтр("ru='Не заполнен расчетный счет контрагента'") И (ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагента) ИЛИ ЗначениеЗаполнено(СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового)) Тогда
			Продолжить;
		КонецЕсли;
		
		АктуальныйМассивПредупреждений.Добавить(ТекстПредупреждения);
		
	КонецЦикла;
	
	СтрокаТаблицы.ЕстьОшибка     = (АктуальныйМассивПредупреждений.Количество() > 0);
	СтрокаТаблицы.ОписаниеОшибок = СтрСоединить(АктуальныйМассивПредупреждений, "; ");
	СтрокаТаблицы.Готовность     = ГотовностьСтроки(СтрокаТаблицы, СоздаватьНенайденныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура УточнитьХозяйственнуюОперациюДаннымиВведеннымиПользователем(СтрокаДокументыКИмпорту, ИменаВведенныхДанных)
	
	Если ЗначениеЗаполнено(СтрокаДокументыКИмпорту.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаДокументыКИмпорту.Документ) Тогда
		// Если есть документ, то хозяйственная операция не определена,
		// поэтому создадим и заполним её полностью.
		// Это нужно для того, чтобы при загрузке документ перезаполнился
		ВведенныеДанные = Новый Структура("Контрагент,ВидОперации,СчетКонтрагента,Договор,СтатьяДДС");
	Иначе
		ВведенныеДанные = Новый Структура(ИменаВведенныхДанных);
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ВведенныеДанные, СтрокаДокументыКИмпорту);
	
	// Приведем ВведенныеДанные в соответствие ХозяйственнаяОперация
	Если ВведенныеДанные.Свойство("ВидОперации") Тогда
		ВидОперацииДокумента = ВведенныеДанные.ВидОперации;
		ВведенныеДанные.Удалить("ВидОперации");
		ВведенныеДанные.Вставить("ВидОперацииДокумента", ВидОперацииДокумента);
	КонецЕсли;
	Если ВведенныеДанные.Свойство("Договор") Тогда
		ДоговорДокумента = ВведенныеДанные.Договор;
		ВведенныеДанные.Удалить("Договор");
		ВведенныеДанные.Вставить("ДоговорКонтрагента", ДоговорДокумента);
	КонецЕсли;
	Если ВведенныеДанные.Свойство("СчетКонтрагента") Тогда
		СчетКонтрагентаДокумента = ВведенныеДанные.СчетКонтрагента;
		ВведенныеДанные.Удалить("СчетКонтрагента");
		ВведенныеДанные.Вставить("БанковскийСчетКонтрагента", СчетКонтрагентаДокумента);
	КонецЕсли;
	Если ВведенныеДанные.Свойство("СтатьяДДС") Тогда
		СтатьяДДСДокумента = ВведенныеДанные.СтатьяДДС;
		ВведенныеДанные.Удалить("СтатьяДДС");
		ВведенныеДанные.Вставить("СтатьяДвиженияДенежныхСредств", СтатьяДДСДокумента);
	КонецЕсли;
	
	РаспознанныеДанныеИзБанка = ПолучитьИзВременногоХранилища(АдресХранилищаРаспознанныеДанныеИзБанка);
	
	ХозяйственнаяОперация = ЗагрузкаВыпискиПоБанковскомуСчету.УточнитьХозяйственнуюОперациюДаннымиВведеннымиПользователем(
		ВведенныеДанные,
		СтрокаДокументыКИмпорту.ИдентификаторВыписки,  // Идентифицируют выписку в РаспознанныеДанныеИзБанка
		СтрокаДокументыКИмпорту.ИдентификаторОперации, // Идентифицируют операцию в РаспознанныеДанныеИзБанка
		РаспознанныеДанныеИзБанка,
		НастройкиЗагрузки());
	
	АдресХранилищаРаспознанныеДанныеИзБанка = ПоместитьВоВременноеХранилище(РаспознанныеДанныеИзБанка, АдресХранилищаРаспознанныеДанныеИзБанка);
	
	ЗаполнитьСтрокуПоХозяйственнойОперации(РаспознанныеДанныеИзБанка, СтрокаДокументыКИмпорту, ХозяйственнаяОперация);
	
	РазместитьПредупреждение(СтрокаДокументыКИмпорту);
	ПодготовитьОтображениеПредупреждений(СтрокаДокументыКИмпорту, Объект.СоздаватьНенайденныеЭлементы);
	
КонецПроцедуры

&НаСервере
Функция РаспознатьДанныеИзБанкаПовторноНаСервере()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("РаспознанныеДанныеИзБанка", ПолучитьИзВременногоХранилища(АдресХранилищаРаспознанныеДанныеИзБанка));
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Повторное чтение данных из банка'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.КлючФоновогоЗадания = Обработки.КлиентБанк.КлючФоновогоЗаданияЗагрузкаВыписки();
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.КлиентБанк.ФоноваяРаспознатьДанныеИзБанкаПовторно",
		СтруктураПараметров,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СозданиеНенайденного

&НаСервере
Функция АдресВременногоХранилищаДереваКонтрагентов()
	
	РазобранныеДанныеИзБанка = ПолучитьИзВременногоХранилища(АдресХранилищаРаспознанныеДанныеИзБанка);
	УчастникиОпераций        = РазобранныеДанныеИзБанка.УчастникиОпераций;
	ДеревоКонтрагентов       = НовыйДеревоКонтрагентов();
	
	Для Каждого СтрокаИмпорта Из ДокументыКИмпорту Цикл
		
		// Не создаем элементы для строк, которые пользователь не выбирал
		Если НЕ СтрокаИмпорта.Загружать Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ТребуетсяСозданиеНенайденныхВСтроке(СтрокаИмпорта) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаИмпорта.КонтрагентСсылкаДляНового) Тогда
			КонтрагентДляСозданиеНенайденного = СтрокаИмпорта.КонтрагентСсылкаДляНового;
		Иначе
			КонтрагентДляСозданиеНенайденного = СтрокаИмпорта.Контрагент;
		КонецЕсли;
		
		СчетКонтрагентаСсылкаДляНового = СтрокаИмпорта.СчетКонтрагентаСсылкаДляНового;
		ДоговорСсылкаДляНового         = СтрокаИмпорта.ДоговорСсылкаДляНового;
		
		СтрокаКонтрагент = ДеревоКонтрагентов.Строки.Найти(КонтрагентДляСозданиеНенайденного, "Ссылка");
		// Добавим нового контрагента
		Если СтрокаКонтрагент = Неопределено Тогда
			СтрокаКонтрагент = ДеревоКонтрагентов.Строки.Добавить();
			
			ДобавитьКонтрагентаВДеревоКонтрагентов(СтрокаКонтрагент, КонтрагентДляСозданиеНенайденного, УчастникиОпераций);
			ДобавитьРасчетныйСчетВДеревоКонтрагентов(СтрокаКонтрагент, СчетКонтрагентаСсылкаДляНового, УчастникиОпераций);
			ДобавитьДоговорВДеревоКонтрагентов(СтрокаКонтрагент, ДоговорСсылкаДляНового, УчастникиОпераций);
			
		// Такой контрагент уже есть, следует добавить только расчетный счет и договор
		Иначе
			
			// Добавим расчетный счет
			СтрокаРасчетныйСчет = СтрокаКонтрагент.Строки.Найти(СчетКонтрагентаСсылкаДляНового, "Ссылка");
			Если СтрокаРасчетныйСчет = Неопределено Тогда
				ДобавитьРасчетныйСчетВДеревоКонтрагентов(СтрокаКонтрагент, СчетКонтрагентаСсылкаДляНового, УчастникиОпераций);
			КонецЕсли;
			
			// Добавим договор
			СтрокаДоговор = СтрокаКонтрагент.Строки.Найти(ДоговорСсылкаДляНового, "Ссылка");
			Если СтрокаДоговор = Неопределено Тогда
				ДобавитьДоговорВДеревоКонтрагентов(СтрокаКонтрагент, ДоговорСсылкаДляНового, УчастникиОпераций);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(ДеревоКонтрагентов, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура СоздатьНенайденноеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	АдресХранилищаРаспознанныеДанныеИзБанка = Результат.АдресХранилищаРаспознанныеДанныеИзБанка;
	
	Для Каждого ОшибкаСоздания Из Результат.МассивСообщений Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОшибкаСоздания);
	КонецЦикла;
	
	ОтобразитьСозданныеЭлементы(Результат.СсылкиНаСозданныеОбъекты);
	
	// Обновим доступность кнопки создать ненайденное
	Элементы.ДокументыКИмпортуСоздатьНенайденное.Доступность = ТребуетсяСозданиеНенайденныхВТаблице(ДокументыКИмпорту);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСозданныеЭлементы(СсылкиНаСозданныеОбъекты)
	
	ИзмененныеСтроки = Новый Соответствие;
	
	// Одна ссылка может использоваться в нескольких строках,
	// поэтому ищем использование данной ссылки в таблице 
	// и устанавливаем новое значение для отображение реквизита
	Для Каждого Ссылка Из СсылкиНаСозданныеОбъекты Цикл
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Контрагенты") ИЛИ ТипЗнч(Ссылка) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("КонтрагентСсылкаДляНового", Ссылка);
			СтрокиДокументыКИмпорту = ДокументыКИмпорту.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаДокументыКИмпорту Из СтрокиДокументыКИмпорту Цикл
				СтрокаДокументыКИмпорту.Контрагент                = Ссылка;
				СтрокаДокументыКИмпорту.КонтрагентСсылкаДляНового = Неопределено;
				СтрокаДокументыКИмпорту.КонтрагентОтображениеНенайденного = "";
				СтрокаДокументыКИмпорту.РеквизитыКонтрагента = НовыйОписаниеКонтрагента();
				ИзмененныеСтроки.Вставить(СтрокаДокументыКИмпорту, Истина);
			КонецЦикла;
		ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("СчетКонтрагентаСсылкаДляНового", Ссылка);
			СтрокиДокументыКИмпорту = ДокументыКИмпорту.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаДокументыКИмпорту Из СтрокиДокументыКИмпорту Цикл
				СтрокаДокументыКИмпорту.СчетКонтрагента                = Ссылка;
				СтрокаДокументыКИмпорту.СчетКонтрагентаСсылкаДляНового = Неопределено;
				СтрокаДокументыКИмпорту.СчетКонтрагентаОтображениеНенайденного = "";
				ИзмененныеСтроки.Вставить(СтрокаДокументыКИмпорту, Истина);
			КонецЦикла;
		ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("ДоговорСсылкаДляНового", Ссылка);
			СтрокиДокументыКИмпорту = ДокументыКИмпорту.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаДокументыКИмпорту Из СтрокиДокументыКИмпорту Цикл
				СтрокаДокументыКИмпорту.Договор = Ссылка;
				СтрокаДокументыКИмпорту.ДоговорСсылкаДляНового = Неопределено;
				СтрокаДокументыКИмпорту.ДоговорОтображениеНенайденного = "";
				ИзмененныеСтроки.Вставить(СтрокаДокументыКИмпорту, Истина);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	// Установить готовность для измененных строк
	Для Каждого КлючИЗначение Из ИзмененныеСтроки Цикл
		СтрокаТаблицы = КлючИЗначение.Ключ;
		СтрокаТаблицы.Готовность = ГотовностьСтроки(СтрокаТаблицы, Объект.СоздаватьНенайденныеЭлементы);
	КонецЦикла;
	
	Если СсылкиНаСозданныеОбъекты.Количество() > 0 Тогда
		ОповеститьОбИзменении(Тип("СправочникСсылка.Контрагенты"));
		ОповеститьОбИзменении(Тип("СправочникСсылка.ДоговорыКонтрагентов"));
		ОповеститьОбИзменении(Тип("СправочникСсылка.БанковскиеСчета"));
		ОповеститьОбИзменении(Тип("СправочникСсылка.ФизическиеЛица"));
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Добавлены контрагенты, банковские счета и договоры'"));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ДобавитьКонтрагентаВДеревоКонтрагентов(СтрокаКонтрагент, КонтрагентДляСозданиеНенайденного, УчастникиОпераций)
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Наименование");
	СтруктураРеквизитов.Вставить("ИНН");
	
	ЭтоОрганизация = Ложь;
	
	// Определим коллекцию
	Если ТипЗнч(КонтрагентДляСозданиеНенайденного) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		КоллекцияКонтрагенты = УчастникиОпераций.НовыеОбъекты[Тип("СправочникСсылка.ФизическиеЛица")];
		ВидКонтрагента       = "Сотрудник";
	ИначеЕсли ТипЗнч(КонтрагентДляСозданиеНенайденного) = Тип("СправочникСсылка.Организации") Тогда
		КоллекцияКонтрагенты = УчастникиОпераций.Ссылки[Тип("СправочникСсылка.Организации")];
		ВидКонтрагента       = "Организация";
		ЭтоОрганизация       = Истина;
		СтруктураРеквизитов.Вставить("НаименованиеПолное");
		СтруктураРеквизитов.Вставить("КПП");
	Иначе
		КоллекцияКонтрагенты = УчастникиОпераций.НовыеОбъекты[Тип("СправочникСсылка.Контрагенты")];
		ВидКонтрагента       = "Контрагент";
		СтруктураРеквизитов.Вставить("НаименованиеПолное");
		СтруктураРеквизитов.Вставить("КПП")
	КонецЕсли;
	
	НовыйКонтрагент = КоллекцияКонтрагенты.Найти(КонтрагентДляСозданиеНенайденного);
	Если НовыйКонтрагент = Неопределено Тогда
		// Контрагент уже создан, берем данные из ИБ
		СтруктураРеквизитов       = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КонтрагентДляСозданиеНенайденного, СтруктураРеквизитов);
		СтрокаКонтрагент.Реквизит = КонтрагентДляСозданиеНенайденного;
	ИначеЕсли ЭтоОрганизация Тогда
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НовыйКонтрагент.Ссылка,
			"ИНН, КПП, Наименование, НаименованиеПолное");
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, РеквизитыОрганизации);
	Иначе
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, НовыйКонтрагент);
	КонецЕсли;
	
	СтрокаКонтрагент.Представление = СтруктураРеквизитов.Наименование;
	СтрокаКонтрагент.Значение      = ВидКонтрагента;
	СтрокаКонтрагент.Ссылка        = КонтрагентДляСозданиеНенайденного;
	СтрокаКонтрагент.Пометка       = Истина;
	СтрокаКонтрагент.ЭтоРодитель   = Истина;
	
	// Заполним реквизиты
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		СтрокаРеквизит               = СтрокаКонтрагент.Строки.Добавить();
		СтрокаРеквизит.Представление = КлючИЗначение.Ключ;
		СтрокаРеквизит.Значение      = КлючИЗначение.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРасчетныйСчетВДеревоКонтрагентов(СтрокаКонтрагент, СчетКонтрагентаСсылкаДляНового, УчастникиОпераций);
	
	КоллекцияБанковскиеСчета = УчастникиОпераций.НовыеОбъекты[Тип("СправочникСсылка.БанковскиеСчета")];
	НовыйБанковскийСчет      = КоллекцияБанковскиеСчета.Найти(СчетКонтрагентаСсылкаДляНового);
	Если НовыйБанковскийСчет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НомерСчета          = НовыйБанковскийСчет.НомерСчета;
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Банк",  НовыйБанковскийСчет.НаименованиеБанка);
	СтруктураРеквизитов.Вставить("Город", НовыйБанковскийСчет.ГородБанка);
	СтруктураРеквизитов.Вставить("БИК",   НовыйБанковскийСчет.БИК);
	СтруктураРеквизитов.Вставить("КоррСчетБанка", НовыйБанковскийСчет.КоррСчетБанка);
	
	// Добавим расчетный счет
	СтрокаРасчетныйСчет = СтрокаКонтрагент.Строки.Вставить(3);
	СтрокаРасчетныйСчет.Представление = "Р/счет";
	СтрокаРасчетныйСчет.Значение      = НомерСчета;
	СтрокаРасчетныйСчет.Пометка       = Истина;
	СтрокаРасчетныйСчет.ЭтоРодитель   = Истина;
	СтрокаРасчетныйСчет.Ссылка        = СчетКонтрагентаСсылкаДляНового;
	
	Если КоллекцияБанковскиеСчета.Колонки.Найти("ИдентификаторЦифровогоСчета") <> Неопределено
		И ЗначениеЗаполнено(НовыйБанковскийСчет.ИдентификаторЦифровогоСчета) Тогда
		// Добавим расчетный счет
		СтрокаРеквизит = СтрокаРасчетныйСчет.Строки.Добавить();
		СтрокаРеквизит.Представление = "Номер счета ЦР";
		СтрокаРеквизит.Значение = НовыйБанковскийСчет.ИдентификаторЦифровогоСчета;
	КонецЕсли;
	
	// Заполним реквизиты
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		СтрокаРеквизит               = СтрокаРасчетныйСчет.Строки.Добавить();
		СтрокаРеквизит.Представление = КлючИЗначение.Ключ;
		СтрокаРеквизит.Значение      = КлючИЗначение.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДоговорВДеревоКонтрагентов(СтрокаКонтрагент, ДоговорСсылкаДляНового, УчастникиОпераций);
	
	КоллекцияДоговоры = УчастникиОпераций.НовыеОбъекты[Тип("СправочникСсылка.ДоговорыКонтрагентов")];
	НовыйДоговор      = КоллекцияДоговоры.Найти(ДоговорСсылкаДляНового);
	Если НовыйДоговор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСсылки      = ИдентификацияУчастниковБанковскихОпераций.ОписаниеСсылки(ДоговорСсылкаДляНового, УчастникиОпераций);
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Наименование", НовыйДоговор.Наименование);
	СтруктураРеквизитов.Вставить("Вид",          ОписаниеСсылки.ВидДоговора);
	
	// Добавим договор
	СтрокаДоговор = СтрокаКонтрагент.Строки.Добавить();
	СтрокаДоговор.Представление = "Договор";
	СтрокаДоговор.Значение      = СтруктураРеквизитов.Вид;
	СтрокаДоговор.Пометка       = Истина;
	СтрокаДоговор.ЭтоРодитель   = Истина;
	СтрокаДоговор.Ссылка        = ДоговорСсылкаДляНового;
	
	// Заполним реквизиты
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		СтрокаРеквизит               = СтрокаДоговор.Строки.Добавить();
		СтрокаРеквизит.Представление = КлючИЗначение.Ключ;
		СтрокаРеквизит.Значение      = КлючИЗначение.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НовыйДеревоКонтрагентов()

	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка");
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Организации"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ДоговорыКонтрагентов"));
	МассивТипов.Добавить(Тип("СправочникСсылка.БанковскиеСчета"));
	ОписаниеТиповСсылка = Новый ОписаниеТипов(МассивТипов);
	
	ДеревоКонтрагентов = Новый ДеревоЗначений;
	ДеревоКонтрагентов.Колонки.Добавить("Представление", ОписаниеТиповСтрока);
	ДеревоКонтрагентов.Колонки.Добавить("Значение",      ОписаниеТиповСтрока);
	ДеревоКонтрагентов.Колонки.Добавить("Реквизит",      ОписаниеТиповСсылка);
	ДеревоКонтрагентов.Колонки.Добавить("Пометка",       ОписаниеТиповБулево);
	ДеревоКонтрагентов.Колонки.Добавить("ЭтоРодитель",   ОписаниеТиповБулево);
	ДеревоКонтрагентов.Колонки.Добавить("Ссылка",        ОписаниеТиповСсылка);
	
	Возврат ДеревоКонтрагентов;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТребуетсяСозданиеНенайденныхВТаблице(Знач ДокументыКИмпорту)
	
	ТаблицаНеГотовыхСтрок = ДокументыКИмпорту.Выгрузить(Новый Структура("Готовность", Ложь));
	
	Если ТаблицаНеГотовыхСтрок.Количество() = 0 Тогда
		Возврат Ложь;
	Иначе
		ТребуетсяСозданиеНенайденных = Ложь;
		Для Каждого НеГотоваяСтрока Из ТаблицаНеГотовыхСтрок Цикл
			Если ТребуетсяСозданиеНенайденныхВСтроке(НеГотоваяСтрока) Тогда
				ТребуетсяСозданиеНенайденных = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Возврат ТребуетсяСозданиеНенайденных;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяСозданиеНенайденныхВСтроке(Знач ДанныеСтроки)
	
	Возврат ЗначениеЗаполнено(ДанныеСтроки.КонтрагентСсылкаДляНового)
			Или ЗначениеЗаполнено(ДанныеСтроки.СчетКонтрагентаСсылкаДляНового)
			Или ЗначениеЗаполнено(ДанныеСтроки.ДоговорСсылкаДляНового);
	
КонецФункции

#КонецОбласти

#Область СозданиеДокументов

&НаСервере
Функция ЗагрузкаВыпискиНаСервере()
	
	ПараметрыЗагрузкиДокументов = ПараметрыЗагрузкиДокументовНаСервере();
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка банковской выписки'");
	НастройкиЗапуска.ЗапуститьВФоне = Истина;
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	НастройкиЗапуска.КлючФоновогоЗадания = Обработки.КлиентБанк.КлючФоновогоЗаданияЗагрузкаВыписки();
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.КлиентБанк.ФоноваяЗагрузкаДокументовКИмпортуИзКлиентБанка",
		ПараметрыЗагрузкиДокументов,
		НастройкиЗапуска);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПараметрыЗагрузкиДокументовНаСервере()
	
	ПараметрыЗагрузкиДокументов = Новый Структура;
	ПараметрыЗагрузкиДокументов.Вставить("СоздаватьНенайденныеЭлементы", Объект.СоздаватьНенайденныеЭлементы);
	// Если требуется, то передадим ссылки для создания ненайденных элементов
	Если Объект.СоздаватьНенайденныеЭлементы Тогда
		ПараметрыЗагрузкиДокументов.Вставить("ОтборСсылкиКСозданию", ОтборСсылкиКСозданию());
	КонецЕсли;
	
	ДокументовКСозданию       = ДокументыКИмпорту.Выгрузить(Новый Структура("Загружать", Истина));
	РаспознанныеДанныеИзБанка = ПолучитьИзВременногоХранилища(АдресХранилищаРаспознанныеДанныеИзБанка);
	
	// Передадим отбор по документам, которые требуется создать
	ПараметрыЗагрузкиДокументов.Вставить("ОтборДокументовКСозданию",                ОтборДокументовКСозданию(ДокументовКСозданию, РаспознанныеДанныеИзБанка));
	ПараметрыЗагрузкиДокументов.Вставить("РаспознанныеДанныеИзБанка",               РаспознанныеДанныеИзБанка);
	ПараметрыЗагрузкиДокументов.Вставить("АдресХранилищаРаспознанныеДанныеИзБанка", АдресХранилищаРаспознанныеДанныеИзБанка);
	
	Возврат ПараметрыЗагрузкиДокументов;
	
КонецФункции

&НаСервере
Функция ОтборСсылкиКСозданию()
	
	БанковскийСчетаСсылкиНового = Новый Соответствие;
	
	ОтборСсылки = Новый Соответствие;
	ОтборСсылки.Вставить(Тип("СправочникСсылка.Контрагенты"),          Новый Соответствие);
	ОтборСсылки.Вставить(Тип("СправочникСсылка.ФизическиеЛица"),       Новый Соответствие);
	ОтборСсылки.Вставить(Тип("СправочникСсылка.ДоговорыКонтрагентов"), Новый Соответствие);
	ОтборСсылки.Вставить(Тип("СправочникСсылка.БанковскиеСчета"),      Новый Соответствие);
	
	Для Каждого СтрокаТаблицы Из ДокументыКИмпорту Цикл
		ЭтоСсылкаНовогоБанковскийСчет = Ложь;
		БанковскийСчетОрганизации = СтрокаТаблицы.БанковскийСчетОрганизации;
		Если ЗначениеЗаполнено(БанковскийСчетОрганизации) Тогда
			Если БанковскийСчетаСсылкиНового[БанковскийСчетОрганизации] = Неопределено Тогда
				ЭтоСсылкаНовогоБанковскийСчет = ЗначениеЗаполнено(БанковскийСчетОрганизации)
					И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчетОрганизации, "Ссылка") = Неопределено;
				БанковскийСчетаСсылкиНового.Вставить(БанковскийСчетОрганизации, ЭтоСсылкаНовогоБанковскийСчет);
			Иначе
				ЭтоСсылкаНовогоБанковскийСчет = БанковскийСчетаСсылкиНового[БанковскийСчетОрганизации];
			КонецЕсли;
		КонецЕсли;
		
		Контрагент = СтрокаТаблицы.КонтрагентСсылкаДляНового;
		Если ЗначениеЗаполнено(Контрагент) Тогда
			Если ОтборСсылки[ТипЗнч(Контрагент)][Контрагент] <> Неопределено Тогда
				ОтборСсылки[ТипЗнч(Контрагент)][Контрагент] =
					СтрокаТаблицы.Загружать Или ОтборСсылки[ТипЗнч(Контрагент)][Контрагент];
			Иначе
				ОтборСсылки[ТипЗнч(Контрагент)].Вставить(Контрагент, СтрокаТаблицы.Загружать);
			КонецЕсли;
		КонецЕсли;
		
		СчетКонтрагента = СтрокаТаблицы.СчетКонтрагентаСсылкаДляНового;
		Если ЗначениеЗаполнено(СчетКонтрагента) Тогда
			Если ОтборСсылки[ТипЗнч(СчетКонтрагента)][СчетКонтрагента] <> Неопределено Тогда
				ОтборСсылки[ТипЗнч(СчетКонтрагента)][СчетКонтрагента] =
					СтрокаТаблицы.Загружать Или ОтборСсылки[ТипЗнч(СчетКонтрагента)][СчетКонтрагента];
			Иначе
				ОтборСсылки[ТипЗнч(СчетКонтрагента)].Вставить(СчетКонтрагента, СтрокаТаблицы.Загружать);
			КонецЕсли;
		КонецЕсли;
		
		Договор = СтрокаТаблицы.ДоговорСсылкаДляНового;
		Если ЗначениеЗаполнено(Договор) Тогда
			Если ОтборСсылки[ТипЗнч(Договор)][Договор] <> Неопределено Тогда
				ОтборСсылки[ТипЗнч(Договор)][Договор] =
					СтрокаТаблицы.Загружать Или ОтборСсылки[ТипЗнч(Договор)][Договор];
			Иначе
				ОтборСсылки[ТипЗнч(Договор)].Вставить(Договор, СтрокаТаблицы.Загружать);
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоСсылкаНовогоБанковскийСчет Тогда
			Если ОтборСсылки[ТипЗнч(БанковскийСчетОрганизации)][БанковскийСчетОрганизации] <> Неопределено Тогда
				ОтборСсылки[ТипЗнч(БанковскийСчетОрганизации)][БанковскийСчетОрганизации] =
					СтрокаТаблицы.Загружать Или ОтборСсылки[ТипЗнч(БанковскийСчетОрганизации)][БанковскийСчетОрганизации];
			Иначе
				ОтборСсылки[ТипЗнч(БанковскийСчетОрганизации)].Вставить(БанковскийСчетОрганизации, СтрокаТаблицы.Загружать);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОтборСсылки;
	
КонецФункции

&НаСервере
Функция ОтборДокументовКСозданию(ДокументовКСозданию, РаспознанныеДанныеИзБанка)
	
	ОтборДокументовКСозданию = Новый Соответствие; // Ключ выписка, значение массив идентификаторов операции
	
	// Подготовим все выписки
	Для Каждого Выписка Из РаспознанныеДанныеИзБанка.РаспознанныеВыписки Цикл
		ОтборДокументовКСозданию.Вставить(Выписка.Ключ, Новый Массив);
	КонецЦикла;
	// Заполним документы, которые требуется создать
	Для Каждого СтрокаДокументаКСозданию Из ДокументовКСозданию Цикл
		ОтборДокумента = ОтборДокументовКСозданию[СтрокаДокументаКСозданию.ИдентификаторВыписки];
		Если ОтборДокумента <> Неопределено Тогда
			ОтборДокумента.Добавить(СтрокаДокументаКСозданию.ИдентификаторОперации);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОтборДокументовКСозданию;

КонецФункции

&НаКлиенте
Процедура АктуализироватьДокументыКИмпорту(ПодготовленныеДанные)
	
	Если ПодготовленныеДанные.Свойство("СсылкиНаСозданныеОбъекты") Тогда
		ОтобразитьСозданныеЭлементы(ПодготовленныеДанные.СсылкиНаСозданныеОбъекты);
	КонецЕсли;
	Если ПодготовленныеДанные.Свойство("МассивСообщений") Тогда
		Для Каждого ТекстСообщения Из ПодготовленныеДанные.МассивСообщений Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	КонецЕсли;
	
	Если ПодготовленныеДанные.Свойство("РезультатСозданияДокументов") Тогда
		
		РезультатСозданияДокументов = ПодготовленныеДанные.РезультатСозданияДокументов;
		
		Если НЕ РезультатСозданияДокументов.Свойство("КоличествоЗагружено") Тогда
			ТекстОповещения = НСтр("ru='Новых документов для загрузки не обнаружено'");
			ТекстПояснения = "";
			СсылкаДляПерехода = Неопределено;
		Иначе
			ТекстОповещения = НСтр("ru='Выполнена загрузка документов'");
			ШаблонТекста = НСтр("ru = 'Загружено: [КоличествоДокументов]. 
				|Поступило: [СуммаПоступило] 
				|Списано: [СуммаСписано]'");
			
			СтрокаКоличествоДокументов = СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(РезультатСозданияДокументов.КоличествоЗагружено, "документ, документа, документов");
			СтрокаСуммаПоступило       = ОбщегоНазначенияБПВызовСервера.ФорматСумм(РезультатСозданияДокументов.СуммаПоступило, , "0,00");
			СтрокаСуммаСписано         = ОбщегоНазначенияБПВызовСервера.ФорматСумм(РезультатСозданияДокументов.СуммаСписано, , "0,00");
			
			ВставляемыеЗначения = Новый Структура;
			ВставляемыеЗначения.Вставить("КоличествоДокументов", СтрокаКоличествоДокументов);
			ВставляемыеЗначения.Вставить("СуммаПоступило",       СтрокаСуммаПоступило);
			ВставляемыеЗначения.Вставить("СуммаСписано",         СтрокаСуммаСписано);
			ТекстПояснения = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ВставляемыеЗначения);
			СсылкаДляПерехода = "e1cib/list/ЖурналДокументов.Деньги.Форма.БанковскиеВыписки";
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			ТекстОповещения,
			СсылкаДляПерехода,
			ТекстПояснения);
		
	КонецЕсли;
	
	АктуализироватьДанныеДокументаНаСервере();
	
	ОповеститьОбИзменении(Тип("ДокументСсылка.ПоступлениеНаРасчетныйСчет"));
	ОповеститьОбИзменении(Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплатуПокупателю"));
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.ЗадачиБухгалтера"));
	
	Оповестить("ИзменениеВыписки",, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АктуализироватьДанныеДокументаНаСервере()
	
	Если ПустаяСтрока(АдресХранилищаРаспознанныеДанныеИзБанка) Тогда
		Возврат;
	КонецЕсли;
	
	РаспознанныеДанныеИзБанка = ПолучитьИзВременногоХранилища(АдресХранилищаРаспознанныеДанныеИзБанка);
	РаспознанныеВыписки       = РаспознанныеДанныеИзБанка.РаспознанныеВыписки;
	
	Для Каждого КлючИЗначение Из РаспознанныеДанныеИзБанка.Выписки Цикл
		
		ИдентификаторВыписки = КлючИЗначение.Ключ;
		Выписка              = КлючИЗначение.Значение;
		РаспознаннаяВыписка  = РаспознанныеДанныеИзБанка.РаспознанныеВыписки[ИдентификаторВыписки];
		
		ДанныеДокументов = ДанныеДокументов(Выписка, РаспознаннаяВыписка);
		Для Каждого КлючИЗначение ИЗ ДанныеДокументов Цикл
			ДанныеДокумента = КлючИЗначение.Значение;
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("ИдентификаторВыписки", ИдентификаторВыписки);
			СтруктураПоиска.Вставить("ИдентификаторОперации", КлючИЗначение.Ключ);
			СтрокиТаблицы = ДокументыКИмпорту.НайтиСтроки(СтруктураПоиска);
			Если СтрокиТаблицы.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТаблицы = СтрокиТаблицы[0];
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Документ) И ДанныеДокумента.Свойство("Ссылка") Тогда
				СтрокаТаблицы.Документ = ДанныеДокумента.Ссылка;
			КонецЕсли;
			ЗаполнитьСтрокуПоДаннымДокумента(СтрокаТаблицы, ДанныеДокумента);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВыпискуНаКлиенте()
	
	СброситьСостояниеКомандИмпорта();
	
	ДлительнаяОперация = ЗагрузкаВыпискиНаСервере();
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	Иначе
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ТекстСообщения = НСтр("ru = 'Загрузка банковской выписки'");
		
		Обработчик = Новый ОписаниеОповещения("ПослеЗагрузкиБанковскойВыпискиВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗагрузкиБанковскойВыпискиВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда // задание было отменено
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ПодготовленныеДанные = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		АктуализироватьДокументыКИмпорту(ПодготовленныеДанные);
		ВыполненаЗагрузка = Истина;
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область РаботаСФайлами

#Область РаботаСФайламиВыгрузка

&НаКлиенте
Процедура ВыборФайлаДляВыгрузки(Элемент) Экспорт
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	Если ЭтоЦифровойСчет Тогда
		ДиалогВыбора.Фильтр              = НСтр("ru = 'Текстовый файл'") + " (*.json)|*.json";
		ДиалогВыбора.Расширение          = "txt";
	Иначе
		ДиалогВыбора.Фильтр              = НСтр("ru = 'Текстовый файл'") + " (*.txt)|*.txt";
		ДиалогВыбора.Расширение          = "json";
	КонецЕсли;
	
	ДиалогВыбора.Заголовок               = НСтр("ru = 'Выберите папку для выгрузки данных из 1C'");
	ДиалогВыбора.ПредварительныйПросмотр = Ложь;
	ДиалогВыбора.ИндексФильтра           = 0;
	ДиалогВыбора.ПолноеИмяФайла          = ?(ПустаяСтрока(Элемент.ТекстРедактирования),
		ИмяФайлаВыгрузки(), Элемент.ТекстРедактирования);
	ДиалогВыбора.ПроверятьСуществованиеФайла = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаДляВыгрузкиЗавершение", ЭтотОбъект);
	ДиалогВыбора.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Функция ИмяФайлаВыгрузки()
	
	Если ЭтоЦифровойСчет Тогда
		ИмяФайла = СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "") + ".json";
	Иначе
		ИмяФайла = СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "") + ".txt";
	КонецЕсли;
	
	Возврат ИмяФайла;
	
КонецФункции

&НаКлиенте
Процедура ВыборФайлаДляВыгрузкиЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено
		И ВыбранныеФайлы.Количество()>0 Тогда
		
		Объект.ФайлВыгрузки = ВыбранныеФайлы.Получить(0);
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлНаКлиенте(ИмяФайла, АдресВоВременномХранилище)
	
	ДополнительныеПараметры = Новый Структура("ИмяФайла, АдресВоВременномХранилище", ИмяФайла, АдресВоВременномХранилище);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьФайлНаКлиентеСозданиеФайла", ЭтотОбъект, ДополнительныеПараметры);
	
	Файл = Новый Файл();
	Файл.НачатьИнициализацию(ОписаниеОповещения, ИмяФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлНаКлиентеЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	ТекстЗаголовка = НСтр("ru = 'Данные успешно выгружены в файл'");
	Если Объект.КонтролироватьБезопасностьОбменаСБанком Тогда
		ПоказатьОповещениеПользователя(ТекстЗаголовка);
	Иначе
		#Если НЕ ВебКлиент Тогда
		ПоказатьОповещениеПользователя(
			ТекстЗаголовка, "file:///" + СтрЗаменить(СокрЛП(ДополнительныеПараметры.ИмяФайла), "\", "/"),
			ДополнительныеПараметры.ИмяФайла);
		#Иначе
		Если НЕ ЗначениеЗаполнено(ПолученныеФайлы) Тогда
			Возврат;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(
			ТекстЗаголовка,,
			ДополнительныеПараметры.ИмяФайла);
		#КонецЕсли
	КонецЕсли;
	
	ЗавершениеВыгрузкиФайла(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлНаКлиентеПроверкаНаКаталог(ЭтоКаталог, ДополнительныеПараметры) Экспорт
	
	Если Прав(СокрЛП(ДополнительныеПараметры.ИмяФайла), 1) = "\"
		ИЛИ Прав(СокрЛП(ДополнительныеПараметры.ИмяФайла), 1) = "/"
		ИЛИ ЭтоКаталог Тогда
		ТекстСообщения = НСтр("ru = 'Файл данных для выгрузки документов в банк некорректен - выбран ""каталог"".
				|Выберите файл выгрузки'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения
			,, "Объект.ФайлВыгрузки");
		
		Возврат;
	КонецЕсли;
	
	ПередаваемыеФайлы = Новый Массив;
	
	ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(
		ДополнительныеПараметры.ИмяФайла, ДополнительныеПараметры.АдресВоВременномХранилище);
	ПередаваемыеФайлы.Добавить(ОписаниеФайла);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьФайлНаКлиентеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	НачатьПолучениеФайлов(ОписаниеОповещения, ПередаваемыеФайлы,ДополнительныеПараметры.ИмяФайла, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлНаКлиентеПроверкаСуществования(Существует, ДополнительныеПараметры) Экспорт
	
	Файл = ДополнительныеПараметры.Файл;
	ДополнительныеПараметры.Вставить("ФайлСуществует", Существует);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьФайлНаКлиентеПроверкаНаКаталог", ЭтотОбъект, ДополнительныеПараметры);
	Если Существует Тогда
		Файл.НачатьПроверкуЭтоКаталог(ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлНаКлиентеСозданиеФайла(Файл, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("Файл", Файл);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьФайлНаКлиентеПроверкаСуществования", ЭтотОбъект, ДополнительныеПараметры);
	Файл.НачатьПроверкуСуществования(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыгрузкиФайла(ДополнительныеПараметры)
	
	УстановитьСостояниеОтправленоСервер();
	ОповеститьОбИзменении(Тип("ДокументСсылка.ПлатежноеПоручение"));
	ОповеститьОбИзменении(Тип("ДокументСсылка.ПлатежноеТребование"));
	
	Если УплатаНалогов
		И ЗначениеЗаполнено(СписокДокументов)
		И ТипЗнч(СписокДокументов) = Тип("СписокЗначений") Тогда
		ПлатежноеПоручениеСписка = СписокДокументов[0].Значение;
		РеквизитыПлатежногоПоручения = РеквизитыПлатежногоПорученияНаУплатуНалога(ПлатежноеПоручениеСписка);
		Если РеквизитыПлатежногоПоручения <> Неопределено Тогда
			ПлатежиВБюджетКлиент.ОповеститьОЗаписиПлатежаНаУплатуНалога(
				РеквизитыПлатежногоПоручения, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЭтоМобильныйКлиент Тогда
		ПоказатьФормуКонтроляБезопасности(ДополнительныеПараметры.ИмяФайла, ДополнительныеПараметры.АдресВоВременномХранилище);
	КонецЕсли;
	
	ЭкспортПроизведен = Истина;
	ПлатежныеДокументыОбновитьИтогиВПодвале();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФормуКонтроляБезопасности(ИмяФайла, АдресВоВременномХранилище)
	
	Если Объект.КонтролироватьБезопасностьОбменаСБанком И Не ПрямойОбменСБанками Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("АдресИсходныхДанных", АдресВоВременномХранилище);
		ПараметрыФормы.Вставить("ПутьКФайлу", ИмяФайла);
		ПараметрыФормы.Вставить("Кодировка", Объект.Кодировка);
		ОткрытьФорму("Обработка.КлиентБанк.Форма.КонтрольБезопасностиОбменаСБанком", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыПлатежногоПорученияНаУплатуНалога(ПлатежноеПоручение)
	
	Если ТипЗнч(ПлатежноеПоручение) = Тип("ДокументСсылка.ПлатежноеПоручение") Тогда

		РеквизитыПлатежногоПоручения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПлатежноеПоручение,
			"Организация,Налог,ДокументОснование,ПоказательПериода,КодБК,КодОКАТО,Ссылка,СуммаДокумента");
		Возврат РеквизитыПлатежногоПоручения;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

&НаСервереБезКонтекста
Функция ТекстовыйДокументИзВременногоХранилищаФайла(АдресФайла, Кодировка, Расширение = "txt") Экспорт
	
	ИмяВременногоФайла  = ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
	ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);
	Текст = Новый ТекстовыйДокумент();
	Если Кодировка = "DOS" Тогда
		Кодир = "cp866";
	ИначеЕсли Кодировка = "UTF-8" Тогда
		Кодир = "UTF-8";
	Иначе
		Кодир = "windows-1251";
	КонецЕсли;
	
	Текст.Прочитать(ИмяВременногоФайла, Кодир);
	УдалитьФайлы(ИмяВременногоФайла); // Удалим временный файл, после его обработки.
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти

#Область РегламентныеЗаданияИДлительныеОперации

&НаСервере
Процедура ЗаписатьОшибкуВЖурнал(Знач ТекстСообщения, Знач ОписаниеОшибки)
	
	ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация, ОшибкаЗагрузкиЦР = Ложь)
	
	ШаблонСообщения = НСтр("ru = 'При выполнении операции произошла ошибка:
							|%1
							|Подробности в журнале регистрации.'");
	Если ОшибкаЗагрузкиЦР Тогда
		СообщениеОбОшибке = ДлительнаяОперация.КраткоеПредставлениеОшибки;
	ИначеЕсли ЗначениеЗаполнено(ДлительнаяОперация.КраткоеПредставлениеОшибки) Тогда
		СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, ДлительнаяОперация.КраткоеПредставлениеОшибки);
	ИначеЕсли ЗначениеЗаполнено(ДлительнаяОперация.ПодробноеПредставлениеОшибки) Тогда
		СообщениеОбОшибке = СтрШаблон(ШаблонСообщения, ДлительнаяОперация.ПодробноеПредставлениеОшибки);
	Иначе
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеОбработчики

&НаКлиенте
Процедура Подключаемый_ПодключитьРасширениеРаботыСФайламиИПрочитатьФайл() Экспорт
	
	// Для прямого обмена с банками не предлагаем установку расширения работы с файлами
	Если СпособЗагрузки <> "Файл" Тогда
		РасширениеРаботыСФайламиПодключено = Ложь;
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПрочитатьФайлЗавершение", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьВопросОбУстановкеРасширенияРаботыСФайлами(ОписаниеОповещения);
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РаспознатьДанныеБанкаПовторно()
	
	// запомним текущую строку, чтобы после обновления (перезаполнения) данных в таблице,
	// вернуться ней
	Если Элементы.ДокументыКИмпорту.ТекущаяСтрока <> Неопределено Тогда
		ДокументыКИмпорту_ТекущаяСтрока =
			ДокументыКИмпорту.Индекс(ДокументыКИмпорту.НайтиПоИдентификатору(Элементы.ДокументыКИмпорту.ТекущаяСтрока));
	КонецЕсли;
	
	ДлительнаяОперация = РаспознатьДанныеИзБанкаПовторноНаСервере();
	
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	ИначеЕсли ДлительнаяОперация.Статус = "ОшибкаЗагрузкиЦР" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация, Истина);
	Иначе
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		Обработчик = Новый ОписаниеОповещения("ПослеПовторногоРаспознаванияДанныхИзБанкаВФоне", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПовторногоРаспознаванияДанныхИзБанкаВФоне(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда // задание было отменено
		Возврат;
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		АдресХранилищаРаспознанныеДанныеИзБанка = ДлительнаяОперация.АдресРезультата;
		РазместитьНаФормеРаспознанныеДанныеИзБанка(АдресХранилищаРаспознанныеДанныеИзБанка, Ложь);
		ВыполняетсяПовторноеРаспознаваниеДанныхИзБанка = Ложь;
		ВосстановитьТекущуюСтроку();
	ИначеЕсли ДлительнаяОперация.Статус = "Ошибка" Тогда
		СообщитьОбОшибкеДлительнойОперации(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте
Процедура ПрочитатьФайлЗавершение(ПодключеноРасширениеРаботыСФайлами, ДополнительныеПараметры) Экспорт
	
	РасширениеРаботыСФайламиПодключено = ПодключеноРасширениеРаботыСФайлами;
	ЗагрузкаУстановитьВидимостьЭлементовВыбораФайлов(ЭтотОбъект);
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗагрузка
		И РасширениеРаботыСФайламиПодключено
		И НЕ ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() // не запускаем чтение файла в веб-клиенте, так как требуется разрешение от пользователя
		И НЕ ПереданыРаспознанныеДанныеИзБанка Тогда
		Если НЕ ПустаяСтрока(Объект.ФайлЗагрузки) Тогда
			РежимЗагрузки = "";
			Если ЭтоЦифровойСчет Тогда
				РежимЗагрузки = "ЦифровойСчет";
			ИначеЕсли Не ЗначениеЗаполнено(Объект.БанковскийСчет) И ЗначениеЗаполнено(Объект.Организация) Тогда
				РежимЗагрузки = "Универсальная";
			КонецЕсли;
			ОбменСБанкомКлиент.ЗагрузитьВыбранныйФайл(Объект.ФайлЗагрузки, ЭтотОбъект, Объект.Кодировка, РежимЗагрузки);
		КонецЕсли;
	КонецЕсли;
	
	ПереданыРаспознанныеДанныеИзБанка = Ложь;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьНастройкуКлиентБанкаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(Объект, РезультатЗакрытия,, "ФайлЗагрузки");
		ВыгружатьПлатежноеТребование(ЭтотОбъект);
		
		УправлениеФормой(ЭтотОбъект);
		
		Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаЗагрузка Тогда
			Если НЕ ПустаяСтрока(РезультатЗакрытия.ФайлЗагрузки) И Объект.ФайлЗагрузки <> РезультатЗакрытия.ФайлЗагрузки И РасширениеРаботыСФайламиПодключено Тогда
				Объект.ФайлЗагрузки = РезультатЗакрытия.ФайлЗагрузки;
				ОбновитьСпискиДокументов();
				РежимЗагрузки = "";
				Если ЭтоЦифровойСчет Тогда
					РежимЗагрузки = "ЦифровойСчет";
				ИначеЕсли Не ЗначениеЗаполнено(Объект.БанковскийСчет) И ЗначениеЗаполнено(Объект.Организация) Тогда
					РежимЗагрузки = "Универсальная";
				КонецЕсли;
				ОбменСБанкомКлиент.ЗагрузитьВыбранныйФайл(Объект.ФайлЗагрузки, ЭтотОбъект, Объект.Кодировка, РежимЗагрузки);
			ИначеЕсли СоответствиеВыпискиОтбору(АдресХранилищаРаспознанныеДанныеИзБанка, Объект.БанковскийСчет).Соответствует Тогда
				// Настройки могли повлиять на форму, повторно разместим данные на форме.
				РазместитьНаФормеРаспознанныеДанныеИзБанка(АдресХранилищаРаспознанныеДанныеИзБанка, Ложь);
			Иначе
				Объект.ФайлЗагрузки = РезультатЗакрытия.ФайлЗагрузки;
				ОбновитьСпискиДокументов();
			КонецЕсли;
		Иначе
			Объект.ФайлЗагрузки = РезультатЗакрытия.ФайлЗагрузки;
			ОбновитьСпискиДокументов();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВБанкЗавершение(МассивДокументов, ДополнительныеПараметры) Экспорт
	
	ОбменСБанкамиКлиент.СформироватьПодписатьОтправитьЭД(МассивДокументов);
	ЭкспортПроизведен = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.НачалоПериода = РезультатВыбора.НачалоПериода;
	Объект.КонецПериода  = РезультатВыбора.КонецПериода;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыгрузка Тогда
		ОбновитьСписокДокументовНаЭкспорт();
	Иначе
		ОчиститьЭлектронныеВыписки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьЭлементыДиректБанк(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ВидимостьБаннера     = Ложь;
	ВидимостьГиперссылки = Ложь;
	
	Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		ВидимостьБаннера     = Форма.ВидимостьЭлементовДиректБанк = "ПоказатьБаннер";
		ВидимостьГиперссылки = Форма.ВидимостьЭлементовДиректБанк = "ПоказатьГиперссылку";
	КонецЕсли;
	
	Элементы.ГруппаРекламаДиректБанк.Видимость = ВидимостьБаннера
		И Не Форма.ПодключенаИнтеграцияСБанком И Не Форма.ЭтоЦифровойСчет;
	
	Если ВидимостьГиперссылки Тогда
		Элементы.ПодключитьДиректБанк.Гиперссылка = Истина;
		Форма.ПодключитьДиректБанк = НСтр("ru = 'Подключить 1С:ДиректБанк'");
	Иначе
		Элементы.ПодключитьДиректБанк.Гиперссылка = Ложь;
		Форма.ПодключитьДиректБанк = "";
	КонецЕсли;
	
	Элементы.ДекорацияПояснениеПериод.Видимость = Форма.ПрямойОбменСбербанк;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура БанкИНомерСчета(БанковскийСчет, Банк, НомерСчета)
	
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		Банк = Неопределено;
		Возврат;
	КонецЕсли;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(БанковскийСчет, "Банк, НомерСчета");
	Банк = ЗначенияРеквизитов.Банк;
	НомерСчета = ЗначенияРеквизитов.НомерСчета;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеОтправленоСервер()
	
	Для Каждого Строка Из Объект.ПлатежныеДокументы Цикл
		
		Если Не Строка.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		
		РегистрыСведений.СостоянияБанковскихДокументов.УстановитьСостояниеДокумента(
			Строка.Документ, Перечисления.СостоянияБанковскихДокументов.Отправлено);
		Строка.Состояние = Перечисления.СостоянияБанковскихДокументов.Отправлено;
		Строка.Выгружать = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыДляИнтеграцииСБанком(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаПодсказкаИнтеграцииВыгрузка.Видимость = Форма.ПодключенаИнтеграцияСБанком;
	Элементы.ГруппаПериоды.Видимость         = Не Форма.ПодключенаИнтеграцияСБанком;
	Элементы.ГруппаТаблицаВыгрузки.Видимость = Не Форма.ПодключенаИнтеграцияСБанком;
	Элементы.ГруппаВыгрузкаВыписки.Видимость = Не Форма.ПодключенаИнтеграцияСБанком;
	
	Элементы.ГруппаПодсказкаИнтеграцииЗагрузка.Видимость = Форма.ПодключенаИнтеграцияСБанком;
	Элементы.СтраницыВыборВыписки.Видимость  = Не Форма.ПодключенаИнтеграцияСБанком;
	Элементы.ГруппаТаблицаЗагрузки.Видимость = Не Форма.ПодключенаИнтеграцияСБанком;
	Элементы.ГруппаКомандЗагрузки.Видимость  = Не Форма.ПодключенаИнтеграцияСБанком;
	
КонецПроцедуры

&НаСервере
Функция ИспользоватьНесколькоБанковскихСчетов()
	
	Результат = Истина;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Результат =
			Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Объект.Организация);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СпособЗагрузкиПриИзменении(Элемент)
	
	Если СпособЗагрузки = "Файл" Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПодключитьРасширениеРаботыСФайламиИПрочитатьФайл", 0.1, Истина);
	ИначеЕсли СпособЗагрузки = "ДиректБанк" И ЭтоЦифровойСчет Тогда
		Объект.БанковскийСчет = Неопределено;
		ЭтоЦифровойСчет       = Ложь;
		УправлениеФормой(ЭтотОбъект);
	Иначе
		Если СпособЗагрузки = "ДиректБанкПоБанку" Тогда
			Объект.БанковскийСчет = Неопределено;
			ЭтоЦифровойСчет       = Ложь;
			ОчиститьЭлектронныеВыписки();
		КонецЕсли;
		
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
	ИзменитьПараметрыВыбораБанковскогоСчета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпособЗагрузки(СохранятьТекущуюНастройку = Ложь, ЕстьПрямойОбмен = Ложь)
	
	Элементы.СпособЗагрузки.СписокВыбора.Очистить();
	
	ТекущийСпособЗагрузки = СпособЗагрузки;
	СпособЗагрузки = "";
	
	Если ПодключенСервисАУСН Тогда
		Элементы.СпособЗагрузки.СписокВыбора.Добавить("СервисАУСН", НСтр("ru = 'Сервис АУСН'"));
		Если Не СохранятьТекущуюНастройку Или ТекущийСпособЗагрузки = "СервисАУСН" Тогда
			СпособЗагрузки = "СервисАУСН";
		КонецЕсли;
	КонецЕсли;
	
	Если ПрямойОбменСБанками Или ЕстьПрямойОбмен Тогда
		Если ИспользоватьНесколькоБанковскихСчетовОрганизации Тогда
			Элементы.СпособЗагрузки.СписокВыбора.Добавить("ДиректБанк", НСтр("ru = '1С:ДиректБанк (по одному счету)'"));
		Иначе
			Элементы.СпособЗагрузки.СписокВыбора.Добавить("ДиректБанк", НСтр("ru = '1С:ДиректБанк'"));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СпособЗагрузки)
			И (Не СохранятьТекущуюНастройку Или ТекущийСпособЗагрузки = "ДиректБанк") Тогда
			СпособЗагрузки = "ДиректБанк";
		КонецЕсли;
		
		Если ИспользоватьНесколькоБанковскихСчетовОрганизации Тогда
			Элементы.СпособЗагрузки.СписокВыбора.Добавить("ДиректБанкПоБанку", НСтр("ru = '1С:ДиректБанк (по всем счетам в банке)'"));
			Если Не ЗначениеЗаполнено(СпособЗагрузки)
				И (Не СохранятьТекущуюНастройку Или ТекущийСпособЗагрузки = "ДиректБанкПоБанку") Тогда
				СпособЗагрузки = "ДиректБанкПоБанку";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.СпособЗагрузки.СписокВыбора.Добавить("Файл", НСтр("ru = 'Файл'"));
	Если Не ЗначениеЗаполнено(СпособЗагрузки) Тогда
		СпособЗагрузки = "Файл";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораБанковскогоСчета(Форма)
	
	Элементы = Форма.Элементы;
	
	МассивПараметров = Новый Массив;
	Если Форма.СпособЗагрузки = "ДиректБанк" Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ЦифровойСчет", Ложь));
	КонецЕсли;
	
	Элементы.БанковскийСчетЗагрузкаФайл.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	
КонецПроцедуры

#Область СогласованиеПлатежей

&НаСервереБезКонтекста
Процедура УстановитьЭлементыСогласованиеПлатежей(Форма) 
	
	Объект = Форма.Объект; 
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаПодсказкаСогласование.Видимость = Ложь;     
	Элементы.ГруппаКомандыСогласования.Видимость = Ложь;
	
	Если Не Форма.ИспользуетсяСогласованиеПлатежей Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Или Не ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		Возврат;
	КонецЕсли;   
	
	Элементы.ГруппаКомандыСогласования.Видимость = Истина;
	
	НастройкиВидимостиПодсказки = НастройкиВидимостиПодсказкиОСогласованииПоУмолчанию();  
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;	
	СохраненныеНастройки =  
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("СохраненныеНастройкиПодсказкиОСогласовании",
		"НастройкиВидимостиПодсказкиОСогласовании",,,ИмяПользователя); 	
	
	Если СохраненныеНастройки <> Неопределено Тогда  
		ЗаполнитьЗначенияСвойств(НастройкиВидимостиПодсказки, СохраненныеНастройки); 
	КонецЕсли;
	            
	Если Не НастройкиВидимостиПодсказки.ПоказыватьПодсказку Тогда    
		Возврат;
	КонецЕсли;  		
		
	ПоказатьПодсказкуСогласованиеПлатежей(Форма);    
	
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура ПоказатьПодсказкуСогласованиеПлатежей(Форма)  
		
	Элементы = Форма.Элементы;    
	Объект = Форма.Объект; 
	
	Элементы.ГруппаПодсказкаСогласование.Видимость = Ложь; 
		
	ВсегоНесогласовано = НесогласованныеПлатежи(Объект.Организация, Объект.БанковскийСчет, Объект.НачалоПериода, Объект.КонецПериода); 
	Если ВсегоНесогласовано = 0 Тогда
		Возврат;
	КонецЕсли;    
	
	Элементы.ГруппаПодсказкаСогласование.Видимость = Истина; 
	
	НавигационнаяСсылка = "ОткрытьФормуФункциональности";
	СсылкаНаСтатью = "https://buh.ru/news/samoe-novoe-v-1s-bukhgalterii-8-coglasovanie-platezhey-kompanii.html";   
	
	Элементы.ДекорацияПодсказкаСогласованиеЗаголовок.Заголовок = 
		ПерсонализированныеПредложенияСервисов.НовыйЗаголовок(НСтр("ru='Перед отправкой в банк нужно согласовать документы'"));
		
	Элементы.ДекорацияПодсказкаСогласованиеТекст.Заголовок = Новый ФорматированнаяСтрока(
		ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(НСтр("ru='Если согласование платежей не требуется, отключите в'")),
		ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(" "),
		ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(НСтр("ru='настройках функциональности'"), НавигационнаяСсылка),
		ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(". "),
		ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера("Подробнее в"),
		ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(" "),
		ПерсонализированныеПредложенияСервисов.НовыйСтрокаБаннера(НСтр("ru='статье'"), СсылкаНаСтатью));
		
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура СохранитьНастройкиВидимостиПодсказкиОСогласовании()  
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;	
	
	ПараметрыСохранения = Новый Структура;
	ПараметрыСохранения.Вставить("ПоказыватьПодсказку", Ложь);     
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("СохраненныеНастройкиПодсказкиОСогласовании",
		"НастройкиВидимостиПодсказкиОСогласовании", ПараметрыСохранения,,ИмяПользователя);  
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция НастройкиВидимостиПодсказкиОСогласованииПоУмолчанию()
	
	ПараметрыСохранения = Новый Структура;
	ПараметрыСохранения.Вставить("ПоказыватьПодсказку", Истина);
	
	Возврат ПараметрыСохранения; 
	
КонецФункции

&НаСервереБезКонтекста
Функция НесогласованныеПлатежи(Организация, БанковскийСчет, НачалоПериода, КонецПериода)
	
	Текст =
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК ПлатежноеПоручение,
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) КАК Состояние
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияБанковскихДокументов КАК СостоянияБанковскихДокументов
	|		ПО ПлатежноеПоручение.Организация = СостоянияБанковскихДокументов.Организация
	|			И ПлатежноеПоручение.Ссылка = СостоянияБанковскихДокументов.СсылкаНаОбъект
	|ГДЕ
	|	ЕСТЬNULL(СостоянияБанковскихДокументов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)) = ЗНАЧЕНИЕ(Перечисление.СостоянияБанковскихДокументов.Подготовлено)
	|	И ПлатежноеПоручение.Дата <= &КонецПериода
	|	И ПлатежноеПоручение.Дата >= &НачалоПериода
	|	И НЕ ПлатежноеПоручение.ПометкаУдаления
	|	И ПлатежноеПоручение.Организация = &Организация
	|	И ПлатежноеПоручение.СчетОрганизации = &СчетОрганизации";
	
	Запрос = Новый Запрос;
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетОрганизации", БанковскийСчет);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выбрать().Количество();
	
КонецФункции

#КонецОбласти

// ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

&НаКлиенте
Процедура Подключаемый_ОбменСБанкамиДепозитыПроверитьАктуальностьПредложений() Экспорт
	
	ОбменСБанкамиДепозитыКлиент.ПроверитьАктуальностьИОбновитьДанныеДепозитныхПродуктов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменСБанкамиДепозитыОбновитьУсловияПродукта() Экспорт
	
	ОбменСБанкамиДепозитыКлиент.ПроверитьОбновитьРасчетПоУсловиямТекущегоПродукта(
		ЭтотОбъект, ОбменСБанкамиДепозиты_ПараметрыПредложений);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменСБанкамиДепозитыВыполнитьИнициализацию() Экспорт
	
	РезультатИнициализации = ОбменСБанкамиДепозитыЗавершитьИнициализацию();
	ОбменСБанкамиДепозитыКлиент.УстановитьПредложенияБанковПослеИнициализации(ЭтотОбъект,
		ОбменСБанкамиДепозиты_ПараметрыПредложений, РезультатИнициализации);
	ЕстьПредложенияБанков = РезультатИнициализации.ЕстьПредложенияБанков;
	НастроитьОтображениеКомандыПоказатьПанельИнформации(ЕстьПредложенияБанков);
	
КонецПроцедуры

&НаСервере
Функция ОбменСБанкамиДепозитыЗавершитьИнициализацию()
	Возврат ОбменСБанкамиДепозиты.ПроверитьПродуктыБанковИЗавершитьИнициализацию(ЭтотОбъект);
КонецФункции

&НаКлиенте
Процедура ИзменитьВидимостьПанелиИнформации()
	
	ИмяКоманды = ОбменСБанкамиДепозитыКлиентСервер.ИмяКоманды("ПоказатьРасчетДепозитов");
	ОбменСБанкамиДепозитыКлиент.ВыполнитьКоманду(ЭтотОбъект,
		ОбменСБанкамиДепозиты_ПараметрыПредложений, Неопределено, ИмяКоманды);
		
	Если Не Элементы.ГруппаДепозитыРасчеты.Видимость Тогда
		ОбменСБанкамиДепозитыВызовСервера.ОбработатьЗакрытиеПанелиРасчетаДепозитов();
	КонецЕсли;
	
	НастроитьОтображениеКомандыПоказатьПанельИнформации(Элементы.ГруппаДепозитыРасчеты.Видимость);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтображениеКомандыПоказатьПанельИнформации(РасчетыВидимость)
	
	Если РасчетыВидимость Тогда
		Элементы.ПоказатьПанельИнформации.Картинка = БиблиотекаКартинок.СтрелкаВправо;
	Иначе
		Элементы.ПоказатьПанельИнформации.Картинка = БиблиотекаКартинок.СтрелкаВлево;
	КонецЕсли;
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСБанками.Депозиты

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	ОпределитьДоступностьДиректбанкаМК();
	
	ДобавитьЭлементыФормыМК();
	
	НастроитьОтображениеЭлементовМК();
	
	НастроитьПоложениеЭлементовМК();
	
	УстановитьУсловноеОформлениеЗагрузка();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеЭлементовМК()

#Область ОтправкаПлатежей
	
	МассивЭлементовКСкрытию = Новый Массив;
	
	МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаСтраницы);
	
	Если ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК) Тогда
		Элементы.СтраницыМК.ТекущаяСтраница = Элементы.ЗагрузкаМК;
	КонецЕсли;
	
	//Шапка
	Элементы.БанковскийСчетВыгрузка.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.ОрганизацияВыгрузка.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	//Платежные документы
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыОбновитьДокументыНаЭкспорт);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыСортироватьСписокПоВозрастанию);
	МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаНайти);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыСортироватьСписокПоУбыванию);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыНомерСтроки);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыДата);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыНомер);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыКонтрагентНомерСчета);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыНазначениеПлатежа);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыВидДокумента);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыВидОперации);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыДокумент);
	МассивЭлементовКСкрытию.Добавить(Элементы.ПлатежныеДокументыОписаниеОшибок);
	
	Для каждого ЭлементФормы Из Элементы.ПлатежныеДокументы.КонтекстноеМеню.ПодчиненныеЭлементы Цикл
		МассивЭлементовКСкрытию.Добавить(ЭлементФормы);
	КонецЦикла;
	
	Элементы.ПлатежныеДокументы.ВариантУправленияВысотой = ВариантУправленияВысотойТаблицы.ВСтрокахФормы;
	Элементы.ПлатежныеДокументы.Высота = 20;
	Элементы.ПлатежныеДокументы.ПутьКДаннымКартинкиСтроки = "";
	Элементы.ПлатежныеДокументы.Шапка = Ложь;
	Элементы.ПлатежныеДокументы.Подвал = Ложь;
	
	Элементы.ПлатежныеДокументыОтметитьВсе.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Элементы.ПлатежныеДокументыОтметитьВсе.Заголовок = НСтр("ru = 'Выбрать'");
	
	Элементы.ПлатежныеДокументыСнятьОтметкуСоВсех.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Элементы.ПлатежныеДокументыСнятьОтметкуСоВсех.Заголовок = НСтр("ru = 'Очистить'");
	
	Для каждого Строка Из Объект.ПлатежныеДокументы Цикл
		Строка.КраткоеПредставлениеДокументаМК = СтрШаблон(НСтр("ru = '%1 от %2'"), Строка.Номер, Формат(Строка.Дата, "ДФ=dd.MM.yyyy"));
	КонецЦикла;
	
	Элементы.ПредставлениеИтоговВыгрузкиМК.Заголовок = СтрШаблон(НСтр("ru = 'К отправке: %1 На сумму: %2'"), Строка(КоличествоКВыгрузке), Формат(СуммаДокументаКВыгрузке, "ЧДЦ=2"));
	
	//Команда отправить
	Если НЕ ДиректБанкОтправкаДоступенМК Тогда
		Элементы.КомандаОтправитьЭД.Заголовок = НСтр("ru = 'Поделиться'");
	КонецЕсли;
	
	Элементы.КомандаОтправитьЭД.Отображение = ОтображениеКнопки.Текст;
	Элементы.КомандаОтправитьЭД.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.КомандаОтправитьЭД.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.КомандаОтправитьЭД.РастягиватьПоВертикали = Ложь;
	Элементы.КомандаОтправитьЭД.РастягиватьПоГоризонтали = Истина;
	Элементы.КомандаОтправитьЭД.ЦветТекста = ЦветаСтиля.ЦветФонаКнопки;
	Элементы.КомандаОтправитьЭД.ЦветРамки = ЦветаСтиля.ЦветРамкиКнопки;
	Элементы.КомандаОтправитьЭД.Высота = 2;
	Элементы.КомандаОтправитьЭД.Фигура = ФигураКнопки.Овал;
	Элементы.КомандаОтправитьЭД.ЦветФона = ЦветаСтиля.ЦветТекстаКнопки;
	
#КонецОбласти

#Область ЗагрузкаВыписки
	
	//Шапка
	Элементы.БанковскийСчетЗагрузкаФайл.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	Элементы.БанковскийСчетЗагрузкаФайл.КнопкаОчистки = Ложь;
	Элементы.БанковскийСчетЗагрузкаФайл.ПодсказкаВвода = "";
	
	Элементы.ОрганизацияПрямойОбмен.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	Для каждого ПодиненныйЭлемент Из Элементы.ДокументыКИмпортуГруппа2.ПодчиненныеЭлементы Цикл
		ПодиненныйЭлемент.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	КонецЦикла;
	
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуГруппаГлавныеКоменды);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуПоказатьОшибки);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуСоздатьНенайденное);
	МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаДокументыКИмпортуРезультатПроведения);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуГруппаПоиск);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуСортироватьСписокПоВозрастанию);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуСортироватьСписокПоУбыванию);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуКонтекстноеМенюКнопкаИзменить);
	МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаКнопокДокументыКИмпортуКонтекстноеМенюПеремещения);
	МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаКнопокДокументыКИмпортуКонтекстноеМенюРезультатПроведения);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуКонтекстноеМенюКнопкаСортироватьПоВозр);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуКонтекстноеМенюКнопкаСортироватьПоУбыв);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуГруппа1);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуГруппа2);
	МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаСуммКЗагрузке);
	МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаКомандЗагрузки);
	МассивЭлементовКСкрытию.Добавить(Элементы.ДокументыКИмпортуНомерДок);
	
	Элементы.ДокументыКИмпорту.Шапка = Ложь;
	Элементы.ДокументыКИмпорту.Подвал = Ложь;
	Элементы.ДокументыКИмпорту.ПутьКДаннымКартинкиСтроки = "";
	Элементы.ДокументыКИмпорту.ВариантУправленияВысотой = ВариантУправленияВысотойТаблицы.ВСтрокахФормы;
	Элементы.ДокументыКИмпорту.Высота = 20;
	
	Элементы.ДокументыКИмпортуСуммаМК.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	
	Элементы.ДокументыКИмпортуДокументыКИмпортуОтметитьВсе.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Элементы.ДокументыКИмпортуДокументыКИмпортуОтметитьВсе.Заголовок = НСтр("ru = 'Выбрать'");
	
	Элементы.ДокументыКИмпортуДокументыКИмпортуСнятьОтметкуСоВсех.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Элементы.ДокументыКИмпортуДокументыКИмпортуСнятьОтметкуСоВсех.Заголовок = НСтр("ru = 'Очистить'");
	
	Элементы.ДокументыКИмпорту.ВажностьПриОтображении = ВажностьПриОтображении.Высокая;
	
	Элементы.ДокументыКИмпортуКонтрагент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	Элементы.ДокументыКИмпортуКонтрагент.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Лево;
	
	Элементы.КомандаЗагрузить.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	Элементы.КомандаЗагрузить.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	Элементы.КомандаЗагрузить.РастягиватьПоВертикали = Ложь;
	Элементы.КомандаЗагрузить.РастягиватьПоГоризонтали = Истина;
	Элементы.КомандаЗагрузить.ЦветТекста = ЦветаСтиля.ЦветФонаКнопки;
	Элементы.КомандаЗагрузить.ЦветРамки = ЦветаСтиля.ЦветРамкиКнопки;
	Элементы.КомандаЗагрузить.Высота = 2;
	Элементы.КомандаЗагрузить.Фигура = ФигураКнопки.Овал;
	Элементы.КомандаЗагрузить.ЦветФона = ЦветаСтиля.ЦветТекстаКнопки;
	
	Если ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК) Тогда
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуДокументНеЗагружен");
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор, "ДокументыКИмпорту.Документ",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не загружен'"));
	КонецЕсли;
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСуммаМК");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДокументыКИмпорту.СуммаСписано", ВидСравненияКомпоновкиДанных.Больше, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтрицательногоЗначения);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДокументыКИмпортуСуммаМК");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДокументыКИмпорту.СуммаПоступило", ВидСравненияКомпоновкиДанных.Больше, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПоложительногоЗначения);
	
	Элементы.ДокументыКИмпортуВидОперации.ЦветТекста = ЦветаСтиля.ЦветТекстаСтрокаСпискаМК;
	Элементы.ДокументыКИмпортуНазначениеПлатежа.ЦветТекста = ЦветаСтиля.ЦветТекстаСтрокаСпискаМК;
	
#КонецОбласти
	
	Если ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК) Тогда
		МассивЭлементовКСкрытию.Добавить(Элементы.КомандаОтправитьЭД);
		МассивЭлементовКСкрытию.Добавить(Элементы.ФормаНовыйПлатеж);
		МассивЭлементовКСкрытию.Добавить(Элементы.КомандаЗапроситьЭлектроннуюВыпискуБанка);
		МассивЭлементовКСкрытию.Добавить(Элементы.ВыгрузкаМК);
	КонецЕсли;
	
	Если НЕ ДиректБанкЗагрузкаДоступенМК И НЕ ЗначениеЗаполнено(ИмяФайлаЗагрузкиМК) Тогда
		МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаДокументыКИмпортуМК);
		МассивЭлементовКСкрытию.Добавить(Элементы.ГруппаКомандЗагрузкиМК);
		
		Элементы.НадписьНедоступенДиректбанкЗагрузкаМК.Видимость = Истина;
	КонецЕсли;
	
	ОтключитьВидимостьМассиваЭлементовМК(МассивЭлементовКСкрытию);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьВидимостьМассиваЭлементовМК(МассивЭлементов)
	
	Для каждого Элемент Из МассивЭлементов Цикл
		Элемент.Видимость = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыФормыМК()
	
	НовыйЭлемент = Элементы.Добавить("СтраницыМК", Тип("ГруппаФормы"));
	НовыйЭлемент.Вид = ВидГруппыФормы.Страницы;
	НовыйЭлемент.УстановитьДействие("ПриСменеСтраницы", "СтраницыМКПриСменеСтраницы");
	
	НовыйЭлемент = Элементы.Добавить("ВыгрузкаМК", Тип("ГруппаФормы"), Элементы.СтраницыМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.Страница;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.Заголовок = "Выгрузка платежей";
	
	НовыйЭлемент = Элементы.Добавить("ЗагрузкаМК", Тип("ГруппаФормы"), Элементы.СтраницыМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.Страница;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.Заголовок = "Загрузка выписки";
	
#Область ОтправкаПлатежей
	
	НоваяКоманда = Команды.Добавить("НовыйПлатеж");
	НоваяКоманда.Действие = "Подключаемый_КомандаНовыйПлатеж";
	НоваяКоманда.Заголовок = НСтр("ru = 'Новый платеж'");
	
	НовыйЭлемент = Элементы.Добавить("ФормаНовыйПлатеж", Тип("КнопкаФормы"), КоманднаяПанель);
	НовыйЭлемент.ИмяКоманды = "НовыйПлатеж";
	НовыйЭлемент.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
	
	//Шапка выгрузка
	НовыйЭлемент = Элементы.Добавить("ГруппаОтборВыгрузкаМК", Тип("ГруппаФормы"), Элементы.ВыгрузкаМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаПериодВыгрузкаМК", Тип("ГруппаФормы"), Элементы.ГруппаОтборВыгрузкаМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ВыгрузкаПериодНачалоМК", Тип("ПолеФормы"), Элементы.ГруппаПериодВыгрузкаМК);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.НачалоПериода";
	НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
	НовыйЭлемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "НачПериодаПриИзменении");
	
	НовыйЭлемент = Элементы.Добавить("ВыгрузкаДекорацияТиреМК", Тип("ДекорацияФормы"), Элементы.ГруппаПериодВыгрузкаМК);
	НовыйЭлемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок = НСтр("ru = '-'");
	
	НовыйЭлемент = Элементы.Добавить("ВыгрузкаПериодКонецМК", Тип("ПолеФормы"), Элементы.ГруппаПериодВыгрузкаМК);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.ПутьКДанным = "Объект.КонецПериода";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "КонПериодаПриИзменении");
	
	НовыйЭлемент = Элементы.Добавить("ВыгрузкаВыбратьПериодМК", Тип("КнопкаФормы"), Элементы.ГруппаПериодВыгрузкаМК);
	НовыйЭлемент.ИмяКоманды = "ВыбратьПериод";
	
	//Платежные документы
	НовыйЭлемент = Элементы.Добавить("ГруппаВыгрузкаМКПлатежныеДокументы", Тип("ГруппаФормы"), Элементы.ВыгрузкаМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ПлатежноеПоручениеСтрока", Тип("ГруппаФормы"), Элементы.ПлатежныеДокументы);
	НовыйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок;
	НовыйЭлемент.Группировка = ГруппировкаКолонок.Вертикальная;
	
	НовыйЭлемент = Элементы.Добавить("ПлатежныеДокументыСтатусМК", Тип("ГруппаФормы"), Элементы.ПлатежноеПоручениеСтрока);
	НовыйЭлемент.Группировка = ГруппировкаКолонок.Горизонтальная;
	НовыйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок;
	
	НовыйЭлемент = Элементы.Добавить("КраткоеПредставлениеПлатежногоПорученияЗагрузкаМК", Тип("ПолеФормы"), Элементы.ПлатежноеПоручениеСтрока);
	НовыйЭлемент.ПутьКДанным = "Объект.ПлатежныеДокументы.КраткоеПредставлениеДокументаМК";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	
	//Подвал выгрузка
	НовыйЭлемент = Элементы.Добавить("ПредставлениеИтоговВыгрузкиМК", Тип("ДекорацияФормы"), Элементы.ГруппаВыгрузкаМКПлатежныеДокументы);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок = НСтр("ru = 'К отправке: 0 На сумму: 0'");
	
	//Нижняя командная панель выгрузки
	НовыйЭлемент = Элементы.Добавить("ГруппаКомандВыгрузкиМК", Тип("ГруппаФормы"), Элементы.ВыгрузкаМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	НовыйЭлемент.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Центр;
	
#КонецОбласти

#Область ЗагрузкаВыписки
	
	//Шапка загрузка
	НовыйЭлемент = Элементы.Добавить("ГруппаОтборЗагрузкаМК", Тип("ГруппаФормы"), Элементы.ЗагрузкаМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ВажностьПриОтображении = ВажностьПриОтображении.Высокая;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ГруппаПериодЗагрузкаМК", Тип("ГруппаФормы"), Элементы.ГруппаОтборЗагрузкаМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	НовыйЭлемент.ВажностьПриОтображении = ВажностьПриОтображении.Высокая;
	НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ЗагрузкаПериодНачалоМК", Тип("ПолеФормы"), Элементы.ГруппаПериодЗагрузкаМК);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект.НачалоПериода";
	НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
	НовыйЭлемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "НачПериодаПриИзменении");
	
	НовыйЭлемент = Элементы.Добавить("ЗагрузкаДекорацияТиреМК", Тип("ДекорацияФормы"), Элементы.ГруппаПериодЗагрузкаМК);
	НовыйЭлемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок = НСтр("ru = '-'");
	
	НовыйЭлемент = Элементы.Добавить("ЗагрузкаПериодКонецМК", Тип("ПолеФормы"), Элементы.ГруппаПериодЗагрузкаМК);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.ПутьКДанным = "Объект.КонецПериода";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "КонПериодаПриИзменении");
	
	НовыйЭлемент = Элементы.Добавить("ЗагрузкаВыбратьПериодМК", Тип("КнопкаФормы"), Элементы.ГруппаПериодЗагрузкаМК);
	НовыйЭлемент.ИмяКоманды = "ВыбратьПериод";
	
	//Документы к импорту
	НовыйЭлемент = Элементы.Добавить("ГруппаДокументыКИмпортуМК", Тип("ГруппаФормы"), Элементы.ЗагрузкаМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("ДокументыКИмпортуСтрока", Тип("ГруппаФормы"), Элементы.ДокументыКИмпорту);
	НовыйЭлемент.Группировка = ГруппировкаКолонок.Вертикальная;
	НовыйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок;
	
	НовыйЭлемент = Элементы.Добавить("ДокументыКИмпортуДатаНомер", Тип("ГруппаФормы"), Элементы.ДокументыКИмпортуСтрока);
	НовыйЭлемент.Группировка = ГруппировкаКолонок.Горизонтальная;
	НовыйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок;
	
	НовыйЭлемент = Элементы.Добавить("ДокументыКИмпортуСуммаМК", Тип("ПолеФормы"), Элементы.ДокументыКИмпортуДатаНомер);
	НовыйЭлемент.ПутьКДанным = "ДокументыКИмпорту.СуммаМК";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	
	//Подвал загрузка
	НовыйЭлемент = Элементы.Добавить("ПредставлениеИтоговЗагрузкиМК", Тип("ДекорацияФормы"), Элементы.ГруппаДокументыКИмпортуМК);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок = НСтр("ru = 'К загрузке: 0 Поступило: 0 Списано: 0'");
	
	//Нижняя командная панель загрузки
	НовыйЭлемент = Элементы.Добавить("ГруппаКомандЗагрузкиМК", Тип("ГруппаФормы"), Элементы.ЗагрузкаМК);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Центр;
	НовыйЭлемент.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Центр;
	
	НовыйЭлемент = Элементы.Добавить("НадписьНедоступенДиректбанкЗагрузкаМК", Тип("ДекорацияФормы"), Элементы.ЗагрузкаМК);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.Заголовок = "Для выбранного банковского счета недоступна загрузка через ДиректБанк. 
						|''Поделитесь'' файлом обмена из мобильного приложения банка или файловой системы телефона в мобильное приложение 1С.";
	НовыйЭлемент.Видимость = Ложь;
	
#КонецОбласти

КонецПроцедуры

&НаСервере
Процедура НастроитьПоложениеЭлементовМК()
	
#Область ОтправкаПлатежей
	
	Элементы.Переместить(Элементы.КомандаОтправитьЭД, Элементы.ГруппаКомандВыгрузкиМК);
	
	//Шапка выгрузка
	Элементы.Переместить(Элементы.БанковскийСчетВыгрузка, Элементы.ГруппаОтборВыгрузкаМК);
	Элементы.Переместить(Элементы.ОрганизацияВыгрузка, Элементы.ГруппаОтборВыгрузкаМК);
	
	//Платежные документы
	Элементы.Переместить(Элементы.ПлатежныеДокументы, Элементы.ГруппаВыгрузкаМКПлатежныеДокументы, Элементы.ПредставлениеИтоговВыгрузкиМК);
	Элементы.Переместить(Элементы.ПлатежныеДокументыВыгружать, Элементы.ПлатежноеПоручениеСтрока);
	Элементы.Переместить(Элементы.ПлатежныеДокументыСостояние, Элементы.ПлатежноеПоручениеСтрока);
	Элементы.Переместить(Элементы.ПлатежныеДокументыКонтрагент, Элементы.ПлатежноеПоручениеСтрока);
	Элементы.Переместить(Элементы.ПлатежныеДокументыСтатусМК, Элементы.ПлатежноеПоручениеСтрока, Элементы.ПлатежныеДокументыКонтрагент);
	Элементы.Переместить(Элементы.ПлатежныеДокументыСостояние, Элементы.ПлатежныеДокументыСтатусМК);
	Элементы.Переместить(Элементы.ПлатежныеДокументыСуммаДокумента, Элементы.ПлатежныеДокументыСтатусМК);
	
#КонецОбласти

#Область ЗагрузкаВыписки
	
	Элементы.Переместить(Элементы.КомандаЗапроситьЭлектроннуюВыпискуБанка, Элементы.ДокументыКИмпорту.КоманднаяПанель);
	Элементы.Переместить(Элементы.КомандаЗагрузить, Элементы.ГруппаКомандЗагрузкиМК);
	
	//Шапка загрузка
	Элементы.Переместить(Элементы.БанковскийСчетЗагрузкаФайл, Элементы.ГруппаОтборЗагрузкаМК);
	Элементы.Переместить(Элементы.ОрганизацияПрямойОбмен, Элементы.ГруппаОтборЗагрузкаМК);
	
	//Документы к импорту
	Элементы.Переместить(Элементы.ДокументыКИмпорту, Элементы.ГруппаДокументыКИмпортуМК, Элементы.ПредставлениеИтоговЗагрузкиМК);
	Элементы.Переместить(Элементы.ДокументыКИмпортуЗагружать, Элементы.ДокументыКИмпорту, Элементы.ДокументыКИмпортуСтрока);
	Элементы.Переместить(Элементы.ДокументыКИмпортуДатаПроведения, Элементы.ДокументыКИмпортуДатаНомер, Элементы.ДокументыКИмпортуСуммаМК);
	Элементы.Переместить(Элементы.ДокументыКИмпортуКонтрагент, Элементы.ДокументыКИмпортуСтрока);
	Элементы.Переместить(Элементы.ДокументыКИмпортуГруппаДокументГиперссылка, Элементы.ДокументыКИмпортуСтрока);
	Элементы.Переместить(Элементы.ДокументыКИмпортуНазначениеПлатежа, Элементы.ДокументыКИмпортуСтрока);
	Элементы.Переместить(Элементы.ДокументыКИмпортуВидОперации, Элементы.ДокументыКИмпортуСтрока, Элементы.ДокументыКИмпортуНазначениеПлатежа);
	
#КонецОбласти

КонецПроцедуры

#КонецОбласти
