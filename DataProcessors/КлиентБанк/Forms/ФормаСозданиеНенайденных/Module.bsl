
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресХранилищаРаспознанныеДанныеИзБанка = Параметры.АдресХранилищаРаспознанныеДанныеИзБанка;
	ДеревоКонтрагентов                      = ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаКонтрагентов);
	
	ЗначениеВРеквизитФормы(ДеревоКонтрагентов, "ТаблицаКонтрагентов");
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	РезультатСоздания = СоздатьНенайденное();
	
	Если РезультатСоздания.Статус = "Выполняется" Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВернутьРезультатОбработкиИЗакрытьФорму", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатСоздания, ОповещениеОЗавершении, ПараметрыОжидания);
	Иначе
		ВернутьРезультатОбработкиИЗакрытьФорму(РезультатСоздания, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыДеревоКонтрагентов

&НаКлиенте
Процедура СнятьВсеПометки(Команда)
	
	УстановитьПометки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсеПометки(Команда)
	
	УстановитьПометки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКонтрагентовПометкаПриИзменении(Элемент)
	
	// количество элементов в ветви дерева заведомо небольшое, поэтому операцию
	// выполняем на клиенте
	
	ТекущийЭлементДерева = ТаблицаКонтрагентов.НайтиПоИдентификатору(Элементы.ДеревоКонтрагентов.ТекущаяСтрока);
	
	РодительЭлемента = ТекущийЭлементДерева.ПолучитьРодителя();
	Если РодительЭлемента <> Неопределено Тогда
		Если ТекущийЭлементДерева.Пометка Тогда
			РодительЭлемента.Пометка = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ПодчиненныеТекущемуЭлементы = ТекущийЭлементДерева.ПолучитьЭлементы();
	
	Для каждого ПодчиненныйЭлемент Из ПодчиненныеТекущемуЭлементы Цикл
		Если ТекущийЭлементДерева.ЭтоРодитель Тогда
			ПодчиненныйЭлемент.Пометка = ТекущийЭлементДерева.Пометка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьНенайденное()
	
	ПараметрыЗадания = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыЗадания.ЗапуститьВФоне = Истина;
	ПараметрыЗадания.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание контрагентов, договоров, банковских счетов'");
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("РаспознанныеДанныеИзБанка", ПолучитьИзВременногоХранилища(АдресХранилищаРаспознанныеДанныеИзБанка));
	ПараметрыСоздания.Вставить("ОтборСсылкиКСозданию", ОтборСсылкиКСозданию());
	ПараметрыСоздания.Вставить("АдресХранилищаРаспознанныеДанныеИзБанка", АдресХранилищаРаспознанныеДанныеИзБанка);
	
	ОписаниеЗаданияСоздания = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыЗадания, "Обработки.КлиентБанк.ФоноваяСозданиеНовыхОбъектов", ПараметрыСоздания);
	
	Возврат ОписаниеЗаданияСоздания;
	
КонецФункции

&НаСервере
Функция ОтборСсылкиКСозданию()
	
	ДеревоКонтрагентов = РеквизитФормыВЗначение("ТаблицаКонтрагентов");
	
	ОтборСсылкиКСозданию = Новый Соответствие;
	ПорядокТипов = ИдентификацияУчастниковБанковскихОпераций.СвязиМеждуТипамиОбъектовИнформационнойБазы();
	Для Каждого Тип Из ПорядокТипов Цикл
		ОтборСсылкиКСозданию.Вставить(Тип, Новый Соответствие);
	КонецЦикла;
	
	РаспознанныеДанныеИзБанка = ПолучитьИзВременногоХранилища(АдресХранилищаРаспознанныеДанныеИзБанка);
	Для Каждого ТипСсылки Из ПорядокТипов Цикл
		// Инициализируем все новые объекты. По умолчанию отбор по ним будет выключен.
		НовыеОбъектыПоТипу = РаспознанныеДанныеИзБанка.УчастникиОпераций.НовыеОбъекты[ТипСсылки];
		Если ЗначениеЗаполнено(НовыеОбъектыПоТипу) Тогда
			Для Каждого СтрокаТаблицы Из НовыеОбъектыПоТипу Цикл
				ОтборСсылкиКСозданию[ТипСсылки].Вставить(СтрокаТаблицы.Ссылка, Ложь);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаКонтрагент Из ДеревоКонтрагентов.Строки Цикл
		ДобавитьСсылкуВОтбор(ОтборСсылкиКСозданию, СтрокаКонтрагент);
	КонецЦикла;
	
	Возврат ОтборСсылкиКСозданию;
	
КонецФункции

&НаСервере
Процедура ДобавитьСсылкуВОтбор(ОтборСсылкиКСозданию, СтрокаДерева, Использование = Истина)
	
	Если НЕ СтрокаДерева.Пометка ИЛИ НЕ ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаДерева.Реквизит) Тогда
		Ссылка = СтрокаДерева.Ссылка;
		ОтборСсылкиКСозданию[ТипЗнч(Ссылка)].Вставить(Ссылка, Истина);
	КонецЕсли;
	
	Для Каждого ПодстрокаДерева Из СтрокаДерева.Строки Цикл
		ДобавитьСсылкуВОтбор(ОтборСсылкиКСозданию, ПодстрокаДерева, Использование);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// ДеревоКонтрагентовПометка
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоКонтрагентовПометка");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ТаблицаКонтрагентов.ЭтоРодитель", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Флаг)
	
	Для каждого Строка Из ТаблицаКонтрагентов.ПолучитьЭлементы() Цикл
		Если Строка.ЭтоРодитель Тогда
			Строка.Пометка = Флаг;
		КонецЕсли;
		
		Для Каждого Подстрока Из Строка.ПолучитьЭлементы() Цикл
			Если Подстрока.ЭтоРодитель Тогда
				Подстрока.Пометка = Флаг;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВернутьРезультатОбработкиИЗакрытьФорму(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовленныеДанные = ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	Закрыть(ПодготовленныеДанные);
	
КонецПроцедуры

#КонецОбласти
