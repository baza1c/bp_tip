
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыпискиБанка = Параметры.ВыпискиБанка;
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПечатьВыписки(Команда)
	
	МассивОбъектовПечати = МассивОбъектовПечати();
	
	НоваяКоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм("ЭД");
	НоваяКоллекцияПечатныхФорм[0].ТабличныйДокумент =
		ЭлектронноеВзаимодействиеБПВызовСервера.ФайлДанныхЭД(МассивОбъектовПечати, , Истина);
	
	УправлениеПечатьюКлиент.ПечатьДокументов(НоваяКоллекцияПечатныхФорм, МассивОбъектовПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьКомплектаДокументов(Команда)
	
	МассивОбъектовПечати = МассивОбъектовПечати();
	
	НоваяКоллекцияПечатныхФорм = УправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм("ЭД");
	НоваяКоллекцияПечатныхФорм[0].ТабличныйДокумент =
		ЭлектронноеВзаимодействиеБПВызовСервера.ФайлДанныхЭД(МассивОбъектовПечати, "КомплектДокументов", Истина);
	
	УправлениеПечатьюКлиент.ПечатьДокументов(НоваяКоллекцияПечатныхФорм, МассивОбъектовПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлаги(Команда)
	
	Для Каждого Строка ИЗ ТаблицаВыписки Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлаги(Команда)
	
	Для Каждого Строка ИЗ ТаблицаВыписки Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВыпискуБанка(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаВыписки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТаблицаВыписки

&НаКлиенте
Процедура ТаблицаВыпискиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = ТаблицаВыписки.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если ЗначениеЗаполнено(СтрокаТаблицы.Ссылка) И Поле.Имя = "ТаблицаВыпискиРеквизитыВыписки" Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, СтрокаТаблицы.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыпискиБанкаПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_ВыпискиБанкаПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	РеквизитыОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.НастройкаОбмена, "Банк, ПрограммаБанка");
	
	// Выписка по дням есть только в Сбербанке, поэтому вывод периода выписки имеет смысл только для него.
	ПоказыватьПериод = ОбменСБанкамиСлужебныйКлиентСервер.ЭтоПрограммаСбербанка(РеквизитыОбмена.ПрограммаБанка)
		И Параметры.НачалоПериода <> Параметры.КонецПериода; // выводим период, только если выписка не за 1 день
	
	// Период выписки выведем в финансовом формате.
	ФинансовыйПериод = ОбщегоНазначенияБПКлиентСервер.ПредставлениеФинансовогоПериода(
		Параметры.НачалоПериода, Параметры.КонецПериода);
	ШаблонСтроки = НСтр("ru = 'Выписки банка %1 %2'");
	Заголовок = СтрШаблон(ШаблонСтроки, РеквизитыОбмена.Банк, ФинансовыйПериод);
	
	ОбменПоНесколькимСчетам = Ложь;
	НомерСчета = "";
	РеквизитыВыписок = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ВыпискиБанка.ВыгрузитьЗначения(), "ДатаНачала, НомерСчета");
	Для Каждого Элемент ИЗ ВыпискиБанка Цикл
		НоваяСтрока = ТаблицаВыписки.Добавить();
		НоваяСтрока.Ссылка = Элемент.Значение;
		Для Каждого КлючИЗначение Из РеквизитыВыписок[Элемент.Значение] Цикл
			НоваяСтрока[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
		КонецЦикла;
		
		ОбменПоНесколькимСчетам = ОбменПоНесколькимСчетам Или Параметры.ЭтоДиректБанкПоБанку // имеет смысл только если обмен по всем счетам банка
			И ЗначениеЗаполнено(НомерСчета) И НомерСчета <> НоваяСтрока.НомерСчета;
		НомерСчета = НоваяСтрока.НомерСчета;
		
		НоваяСтрока.Пометка = Истина;
	КонецЦикла;
	
	Если ПоказыватьПериод И ОбменПоНесколькимСчетам Тогда
		Элементы.ТаблицаВыпискиРеквизитыВыписки.Заголовок = НСтр("ru = 'Дата выписки, номер счета'");
	ИначеЕсли ОбменПоНесколькимСчетам Тогда
		Элементы.ТаблицаВыпискиРеквизитыВыписки.Заголовок = НСтр("ru = 'Номер счета'");
	Иначе
		Элементы.ТаблицаВыпискиРеквизитыВыписки.Заголовок = НСтр("ru = 'Дата выписки'");
	КонецЕсли;
	
	Для Каждого СтрокаВыписки Из ТаблицаВыписки Цикл
		Если ПоказыватьПериод И ОбменПоНесколькимСчетам Тогда
			ДанныеВыписки = Новый Массив;
			// Для компактности в этом режиме выводим период в сокращенном формате
			ДанныеВыписки.Добавить(Формат(СтрокаВыписки.ДатаНачала, "ДФ=dd.MM.yy"));
			ДанныеВыписки.Добавить(СтрокаВыписки.НомерСчета);
			СтрокаВыписки.РеквизитыВыписки = СтрСоединить(ДанныеВыписки, ", ");
		ИначеЕсли ОбменПоНесколькимСчетам Тогда
			СтрокаВыписки.РеквизитыВыписки = СтрокаВыписки.НомерСчета;
		Иначе
			СтрокаВыписки.РеквизитыВыписки = Формат(СтрокаВыписки.ДатаНачала, "ДЛФ=D");
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаВыписки.Сортировать("НомерСчета, ДатаНачала");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыпискиБанкаПриАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.ТаблицаВыписки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабличныйДокумент = ЭлектронноеВзаимодействиеБПВызовСервера.ФайлДанныхЭД(МассивОбъектовПечати(ТекущиеДанные.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Функция МассивОбъектовПечати(Параметр = Неопределено)
	
	МассивОбъектов = Новый Массив;
	Если Параметр = Неопределено Тогда
		Для Каждого Строка ИЗ ТаблицаВыписки Цикл
			Если Строка.Пометка Тогда
				МассивОбъектов.Добавить(Строка.Ссылка);
			КонецЕсли;
		КонецЦикла;
	Иначе
		МассивОбъектов.Добавить(Параметр);
	КонецЕсли;
	
	Возврат МассивОбъектов;
	
КонецФункции

#КонецОбласти
