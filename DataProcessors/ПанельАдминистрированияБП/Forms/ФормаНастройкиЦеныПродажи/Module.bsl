#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДоступныПлановыеЦены = ПравоДоступа("Просмотр", Метаданные.Константы.ТипЦенПлановойСебестоимостиНоменклатуры);
	ДоступныЦеныПокупки  = ПравоДоступа("Просмотр", Метаданные.Константы.НастройкаЗаполненияЦеныПокупки);
	ДоступныЦеныПродажи  = ПравоДоступа("Просмотр", Метаданные.Константы.НастройкаЗаполненияЦеныПродажи);
	
	РазрешеноИзменениеНастройкаЦеныПродажи = ПравоДоступа("Изменение", Метаданные.Константы.НастройкаЗаполненияЦеныПродажи);
	РазрешеноИзменениеНастройкаЦеныПокупки = ПравоДоступа("Изменение", Метаданные.Константы.НастройкаЗаполненияЦеныПокупки);
	РазрешеноИзменениеТипаЦенПродажи       = ПравоДоступа("Изменение", Метаданные.Константы.ОсновнойТипЦеныПродажи);
	РазрешеноИзменениеТипаЦенПокупки       = ПравоДоступа("Изменение", Метаданные.Константы.ОсновнойТипЦеныПокупки); 
	РазрешеноИзменениеПлановыхЦен          = ПравоДоступа("Изменение", Метаданные.Константы.ТипЦенПлановойСебестоимостиНоменклатуры);
	
	Если ДоступныЦеныПродажи Тогда
		Элементы.ИзПредыдущегоДокумента.ТолькоПросмотр = НЕ РазрешеноИзменениеНастройкаЦеныПродажи;
		Элементы.ИзКарточкиНоменклатуры.ТолькоПросмотр = НЕ РазрешеноИзменениеНастройкаЦеныПродажи;
		Элементы.НеПодставлять.ТолькоПросмотр          = НЕ РазрешеноИзменениеНастройкаЦеныПродажи;
		Элементы.ПоТипуЦен.ТолькоПросмотр              = НЕ РазрешеноИзменениеНастройкаЦеныПродажи;
		Элементы.ОсновнойТипЦенПродажи.ТолькоПросмотр  = НЕ РазрешеноИзменениеТипаЦенПродажи;
		
		НастройкаЦеныПродажи  = Константы.НастройкаЗаполненияЦеныПродажи.Получить();
		ОсновнойТипЦенПродажи = Константы.ОсновнойТипЦеныПродажи.Получить();
	Иначе
		Элементы.ГруппаПродажи.Видимость = Ложь;
	КонецЕсли;
	
	Если ДоступныЦеныПокупки Тогда
		Элементы.НеПодставлятьПокупки.ТолькоПросмотр          = НЕ РазрешеноИзменениеНастройкаЦеныПокупки;
		Элементы.ИзПредыдущегоДокументаПокупки.ТолькоПросмотр = НЕ РазрешеноИзменениеНастройкаЦеныПокупки;
		Элементы.ПоТипуЦенПокупки.ТолькоПросмотр              = НЕ РазрешеноИзменениеНастройкаЦеныПокупки;
		Элементы.ОсновнойТипЦенПокупки.ТолькоПросмотр         = НЕ РазрешеноИзменениеТипаЦенПокупки;
		
		НастройкаЦеныПокупки  = Константы.НастройкаЗаполненияЦеныПокупки.Получить();
		ОсновнойТипЦенПокупки = Константы.ОсновнойТипЦеныПокупки.Получить();
	Иначе
		Элементы.ГруппаПокупки.Видимость = Ложь;
	КонецЕсли;
	
	Если ДоступныПлановыеЦены И ПолучитьФункциональнуюОпцию("ИспользоватьПлановуюСебестоимость") Тогда
		Элементы.ТипЦенПлановойСебестоимостиНоменклатуры.ТолькоПросмотр = НЕ РазрешеноИзменениеПлановыхЦен;
		ТипЦенПлановойСебестоимостиНоменклатуры = Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Получить();
	Иначе
		Элементы.ГруппаПлановыеЦены.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ПодсказкаОНастройкахФормы.Видимость = Параметры.ОткрытаИзКарточкиНоменклатуры;
	
	Элементы.ОсновнойТипЦенПродажи.Доступность = (НастройкаЦеныПродажи = Перечисления.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен);
	Элементы.ОсновнойТипЦенПродажи.АвтоОтметкаНезаполненного = Элементы.ОсновнойТипЦенПродажи.Доступность;
	
	Элементы.ОсновнойТипЦенПокупки.Доступность = (НастройкаЦеныПокупки = Перечисления.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен);
	Элементы.ОсновнойТипЦенПокупки.АвтоОтметкаНезаполненного = Элементы.ОсновнойТипЦенПокупки.Доступность;
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НастройкаЦеныПокупки = ПредопределенноеЗначение("Перечисление.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен")
		И ОсновнойТипЦенПокупки = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка") Тогда
		Отказ = Истина;

		ТекстСообщения = НСтр("ru = 'Не указан основной тип цен покупки'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	Если НастройкаЦеныПродажи = ПредопределенноеЗначение("Перечисление.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен")
		И ОсновнойТипЦенПродажи = ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка") Тогда
		Отказ = Истина;

		ТекстСообщения = НСтр("ru = 'Не указан основной тип цен продажи'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда
		УстановитьЗначенияКонстант();
		Оповестить("ФормаНастройкаЦеныПокупкиИзменена");
		Оповестить("ФормаНастройкаЦеныПродажиИзменена");
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервереБезКонтекста
Процедура НастройкаЦеныПродажиПриИзмененииНаСервере(НастройкаЦеныПродажи)
	
	Константы.НастройкаЗаполненияЦеныПродажи.Установить(НастройкаЦеныПродажи);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаЦеныПродажиПриИзменении(Элемент)
	
	Элементы.ОсновнойТипЦенПродажи.Доступность = (НастройкаЦеныПродажи = ПредопределенноеЗначение("Перечисление.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен"));
	Элементы.ОсновнойТипЦенПродажи.АвтоОтметкаНезаполненного = Элементы.ОсновнойТипЦенПродажи.Доступность;
	
	Если НастройкаЦеныПродажи <> ПредопределенноеЗначение("Перечисление.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен") Тогда
		Если ЗначениеЗаполнено(ОсновнойТипЦенПродажи) Тогда
			ОсновнойТипЦенПродажи =  ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
			ОсновнойТипЦенПродажиПриИзмененииНаСервере(); 
		Иначе
			НастройкаЦеныПродажиПриИзмененииНаСервере(НастройкаЦеныПродажи);
		КонецЕсли;
		Оповестить("ФормаНастройкаЦеныПродажиИзменена");
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НастройкаЦеныПокупкиПриИзмененииНаСервере(НастройкаЦеныПокупки)
	
	Константы.НастройкаЗаполненияЦеныПокупки.Установить(НастройкаЦеныПокупки);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаЦеныПокупкиПриИзменении(Элемент)
	
	Элементы.ОсновнойТипЦенПокупки.Доступность = (НастройкаЦеныПокупки = ПредопределенноеЗначение("Перечисление.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен"));
	Элементы.ОсновнойТипЦенПокупки.АвтоОтметкаНезаполненного = Элементы.ОсновнойТипЦенПокупки.Доступность; 
	
	Если НастройкаЦеныПокупки <> ПредопределенноеЗначение("Перечисление.НастройкаЗаполненияЦеныПродажи.ПоТипуЦен") Тогда
		Если ЗначениеЗаполнено(ОсновнойТипЦенПокупки) Тогда
			ОсновнойТипЦенПокупки =  ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
			ОсновнойТипЦенПокупкиПриИзмененииНаСервере();
		Иначе
			НастройкаЦеныПокупкиПриИзмененииНаСервере(НастройкаЦеныПокупки);
		КонецЕсли;
		Оповестить("ФормаНастройкаЦеныПродажиИзменена");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОсновнойТипЦенПокупкиПриИзмененииНаСервере()
	Константы.НастройкаЗаполненияЦеныПокупки.Установить(НастройкаЦеныПокупки);
	Константы.ОсновнойТипЦеныПокупки.Установить(ОсновнойТипЦенПокупки);
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойТипЦенПокупкиПриИзменении(Элемент)
	ОсновнойТипЦенПокупкиПриИзмененииНаСервере();
	Оповестить("ФормаНастройкаЦеныПродажиИзменена");
КонецПроцедуры

&НаСервере
Процедура ОсновнойТипЦенПродажиПриИзмененииНаСервере()
	Константы.НастройкаЗаполненияЦеныПродажи.Установить(НастройкаЦеныПродажи);
	Константы.ОсновнойТипЦеныПродажи.Установить(ОсновнойТипЦенПродажи);
КонецПроцедуры

&НаКлиенте
Процедура ОсновнойТипЦенПродажиПриИзменении(Элемент)
	ОсновнойТипЦенПродажиПриИзмененииНаСервере();
	Оповестить("ФормаНастройкаЦеныПродажиИзменена");
КонецПроцедуры

&НаСервере
Процедура ТипЦенПлановойСебестоимостиНоменклатурыПриИзмененииНаСервере()
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПлановуюСебестоимость") Тогда
		Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Установить(ТипЦенПлановойСебестоимостиНоменклатуры);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТипЦенПлановойСебестоимостиНоменклатурыПриИзменении(Элемент)
	ТипЦенПлановойСебестоимостиНоменклатурыПриИзмененииНаСервере();
	Оповестить("ФормаНастройкаЦеныПродажиИзменена");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

&НаСервере
Процедура УстановитьЗначенияКонстант()
	
	Если ДоступныЦеныПродажи И Константы.НастройкаЗаполненияЦеныПродажи.Получить() <> НастройкаЦеныПродажи Тогда
		Константы.НастройкаЗаполненияЦеныПродажи.Установить(НастройкаЦеныПродажи);
	КонецЕсли;
	Если ДоступныЦеныПокупки И Константы.НастройкаЗаполненияЦеныПокупки.Получить() <> НастройкаЦеныПокупки Тогда
		Константы.НастройкаЗаполненияЦеныПокупки.Установить(НастройкаЦеныПокупки);
	Конецесли;
	Если ДоступныЦеныПокупки И Константы.ОсновнойТипЦеныПокупки.Получить() <> ОсновнойТипЦенПокупки Тогда
		Константы.ОсновнойТипЦеныПокупки.Установить(ОсновнойТипЦенПокупки);
	Конецесли;
	Если ДоступныЦеныПродажи И Константы.ОсновнойТипЦеныПродажи.Получить() <> ОсновнойТипЦенПродажи Тогда
		Константы.ОсновнойТипЦеныПродажи.Установить(ОсновнойТипЦенПродажи);
	Конецесли;
	Если ДоступныПлановыеЦены И ПолучитьФункциональнуюОпцию("ИспользоватьПлановуюСебестоимость")
		И Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Получить() <> ТипЦенПлановойСебестоимостиНоменклатуры Тогда
		Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Установить(ТипЦенПлановойСебестоимостиНоменклатуры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ОсновнойТипЦенПродажи.ПодсказкаВвода = НСтр("ru = 'Укажите тип цены'");
	Элементы.ОсновнойТипЦенПокупки.ПодсказкаВвода = НСтр("ru = 'Укажите тип цены'");
	Элементы.ТипЦенПлановойСебестоимостиНоменклатуры.ПодсказкаВвода = НСтр("ru = 'Укажите тип цены'");
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтступ", "Видимость", Ложь);
	
КонецПроцедуры

#КонецОбласти

