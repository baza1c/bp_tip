#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Обработчики служебных событий базовой функциональности

// Заполняет переименования тех объектов метаданных, которые невозможно
// автоматически найти по типу, но ссылки на которые требуется сохранять
// в базе данных (например: подсистемы, роли).
//
// Подробнее: см. ОбщегоНазначения.ДобавитьПереименование().
//
Процедура ПриДобавленииПереименованийОбъектовМетаданных(Итог) Экспорт
	
	Библиотека = "СтандартныеПодсистемы";
	
	ОбщегоНазначения.ДобавитьПереименование(Итог,
		"2.2.1.12",
		"Подсистема.НастройкаИАдминистрирование",
		"Подсистема.Администрирование",
		Библиотека);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов

// Определяет разделы, в которых доступна панель отчетов.
//   Подробнее - см. описание процедуры ИспользуемыеРазделы
//   общего модуля ВариантыОтчетов.
//
Процедура ПриОпределенииРазделовСВариантамиОтчетов(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.Администрирование, НСтр("ru = 'Отчеты администратора'"));
	
КонецПроцедуры

// Определяет разделы, в которых доступна команда вызова дополнительных отчетов.
//   Подробнее - см. описание функции РазделыДополнительныхОтчетов
//   общего модуля ДополнительныеОтчетыИОбработки.
//
Процедура ПриОпределенииРазделовСДополнительнымиОтчетами(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.Администрирование);
	
КонецПроцедуры

// Определяет разделы, в которых доступна команда вызова дополнительных обработок.
//   Подробнее - см. описание функции РазделыДополнительныхОбработок
//   общего модуля ДополнительныеОтчетыИОбработки.
//
Процедура ПриОпределенииРазделовСДополнительнымиОбработками(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.Администрирование);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Процедура УстановитьЗначениеЦеныПродажиПоУмолчанию() Экспорт
	
	КонстантаОбъект = Константы.НастройкаЗаполненияЦеныПродажи.СоздатьМенеджерЗначения();
	КонстантаОбъект.Значение = Перечисления.НастройкаЗаполненияЦеныПродажи.ПредыдущийДокумент;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(КонстантаОбъект, Ложь, Ложь);
			
	                            
КонецПроцедуры

// Перенос значений констант УдалитьОтображатьОстаткиИЦеныВСпискеТовары
// и УдалитьОтображатьОстаткиПоВзаиморасчетамВСпискеКонтрагентов в константы
// ОтображатьОстаткиИЦеныВСпискеТоваров и ОтображатьДополнительнуюИнформациюВСпискеКонтрагентов.
//
Процедура ПеренестиНастройкиПроизводительностиВФормахТоварыИКонтрагенты() Экспорт
	
	// Переносим настройки только если используется Простой интерфейс, для Полного - заполняем настройками по умолчанию.
	Если Не ОбщегоНазначенияБП.ЭтоПростойИнтерфейс() Тогда
		Возврат;
	КонецЕсли;
	
	КонстантаОбъект = Константы.ОтображатьОстаткиИЦеныВСпискеТоваров.СоздатьМенеджерЗначения();
	Если Константы.УдалитьОтображатьОстаткиИЦеныВСпискеТовары.Получить() Тогда
		КонстантаОбъект.Значение = Перечисления.ВариантыОтображенияДополнительнойИнформацииВСписках.ВКолонках;
	Иначе
		КонстантаОбъект.Значение = Перечисления.ВариантыОтображенияДополнительнойИнформацииВСписках.ВПодвале;
	КонецЕсли;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(КонстантаОбъект);
	
	КонстантаОбъект = Константы.ОтображатьДополнительнуюИнформациюВСпискеКонтрагентов.СоздатьМенеджерЗначения();
	Если Константы.УдалитьОтображатьОстаткиПоВзаиморасчетамВСпискеКонтрагентов.Получить() Тогда
		КонстантаОбъект.Значение = Перечисления.ВариантыОтображенияДополнительнойИнформацииВСписках.ВКолонках;
	Иначе
		КонстантаОбъект.Значение = Перечисления.ВариантыОтображенияДополнительнойИнформацииВСписках.ВПодвале;
	КонецЕсли;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(КонстантаОбъект);
	
КонецПроцедуры

// Начальное заполнение значениями по умолчанию констант ОтображатьОстаткиИЦеныВСпискеТоваров и
// ОтображатьДополнительнуюИнформациюВСпискеКонтрагентов.
//
Процедура НачальноеЗаполнениеНастроекПроизводительностиВФормахТоварыИКонтрагенты() Экспорт
	
	Если ВариантыПриложений.ЭтоБазоваяВерсия() Тогда
		ЗначениеПоУмолчанию = Перечисления.ВариантыОтображенияДополнительнойИнформацииВСписках.ВКолонках;
	Иначе
		ЗначениеПоУмолчанию = Перечисления.ВариантыОтображенияДополнительнойИнформацииВСписках.ВПодвале;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Константы.ОтображатьОстаткиИЦеныВСпискеТоваров.Получить()) Тогда
		КонстантаОбъект = Константы.ОтображатьОстаткиИЦеныВСпискеТоваров.СоздатьМенеджерЗначения();
		КонстантаОбъект.Значение = ЗначениеПоУмолчанию;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(КонстантаОбъект);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Константы.ОтображатьДополнительнуюИнформациюВСпискеКонтрагентов.Получить()) Тогда
		КонстантаОбъект = Константы.ОтображатьДополнительнуюИнформациюВСпискеКонтрагентов.СоздатьМенеджерЗначения();
		КонстантаОбъект.Значение = ЗначениеПоУмолчанию;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(КонстантаОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
