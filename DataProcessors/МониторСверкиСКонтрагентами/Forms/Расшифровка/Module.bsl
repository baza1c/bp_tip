# Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Организация         = Параметры.Организация;
	Объект.ДатаНачала          = Параметры.ДатаНачала;
	Объект.ДатаОкончания       = Параметры.ДатаОкончания;
	Контрагент                 = Параметры.Контрагент;
	ИдентификаторРасшифровки   = Параметры.ИдентификаторРасшифровки;
	
	Параметры.Свойство("Поставщики", Поставщики);
	Параметры.Свойство("Покупатели", Покупатели);
	Параметры.Свойство("СчетаРасчетовСПоставщиками", СчетаРасчетовСПоставщиками);
	Параметры.Свойство("СчетаРасчетовСПокупателями", СчетаРасчетовСПокупателями);
	Параметры.Свойство("ИдентификаторЭДО", ИдентификаторЭДО);
	Параметры.Свойство("ОтправлятьПисьмаВФорматеHTML", ОтправлятьПисьмаВФорматеHTML);
	
	УстановитьЗаголовок();
	СформироватьРасшифровкуНаСервере();
	
КонецПроцедуры

# КонецОбласти

# Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РезультатВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если СтрНачинаетсяС(Область.Имя, "ПерейтиВСервисСверки") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОтчета = Новый Структура;
		ПараметрыОтчета.Вставить("РежимРасшифровки", Истина);
		ПараметрыОтчета.Вставить("Организация",      Объект.Организация);
		ПараметрыОтчета.Вставить("Контрагент",       Контрагент);
		ПараметрыОтчета.Вставить("НачалоПериода",    Объект.ДатаНачала);
		ПараметрыОтчета.Вставить("КонецПериода",     Объект.ДатаОкончания);
		
		ОткрытьФорму("Отчет.СверкаРасчетовСКонтрагентами.Форма.ФормаОтчета", ПараметрыОтчета);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначениеРасшифровки = ПолучитьЗначениеДляОткрытия(Расшифровка, АдресХранилища);
	Если ЗначениеРасшифровки <> Неопределено Тогда
		ПоказатьЗначение(, ЗначениеРасшифровки);
	КонецЕсли;
	
КонецПроцедуры

# КонецОбласти

# Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьЗапросНаОригиналы(Команда)
	
	ПараметрыОтправкиПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ИдентификаторЗапроса = "ЗапросОригиналов";
	
	ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ИдентификаторЗапроса);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыОтправкиПисьма);
	
КонецПроцедуры

# КонецОбласти

# Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовок()
	
	Заголовок = Обработки.МониторСверкиСКонтрагентами.ПолучитьЗаголовокРасшифровки(ИдентификаторРасшифровки);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыРасшифровки()

	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ИдентификаторРасшифровки",   ИдентификаторРасшифровки);
	ПараметрыОтчета.Вставить("Контрагент",                 Контрагент);
	ПараметрыОтчета.Вставить("Организация",                Объект.Организация);
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	ПараметрыОтчета.Вставить("НачалоПериода",              НачалоДня(Объект.ДатаНачала));
	ПараметрыОтчета.Вставить("КонецПериода",               КонецДня(Объект.ДатаОкончания));
	ПараметрыОтчета.Вставить("УникальныйИдентификатор",    УникальныйИдентификатор);
	
	Если ИдентификаторРасшифровки = "РасшифровкаАктыСверки" Тогда
		ПараметрыОтчета.Вставить("Поставщики",                 Поставщики);
		ПараметрыОтчета.Вставить("Покупатели",                 Покупатели);
		ПараметрыОтчета.Вставить("СчетаРасчетовСПоставщиками", СчетаРасчетовСПоставщиками);
		ПараметрыОтчета.Вставить("СчетаРасчетовСПокупателями", СчетаРасчетовСПокупателями);
	КонецЕсли;
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЗначениеДляОткрытия(Расшифровка, АдресХранилища)

	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(АдресХранилища);
	ЭлементРасшифровки = ДанныеРасшифровки.Элементы[Расшифровка];
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПоляРасшифровки = ЭлементРасшифровки.ПолучитьПоля();
	Если ПоляРасшифровки.Количество() > 0 Тогда
		ЗначениеРасшифровки = ПоляРасшифровки[0].Значение;
		Если ЗначениеРасшифровки = Null
			Или ЗначениеРасшифровки = Неопределено
			Или ТипЗнч(ЗначениеРасшифровки) = Тип("Число")
			Или ТипЗнч(ЗначениеРасшифровки) = Тип("Строка")
			Или ТипЗнч(ЗначениеРасшифровки) = Тип("Булево") Тогда
			Возврат Неопределено;
		Иначе
			Возврат ЗначениеРасшифровки;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СформироватьРасшифровкуНаСервере()
	
	ПараметрыОтчета = ПолучитьПараметрыРасшифровки();
	
	РезультатРасшифровки = Обработки.МониторСверкиСКонтрагентами.СформироватьРасшифровку(ПараметрыОтчета);
	
	Результат      = РезультатРасшифровки.Результат;
	АдресХранилища = РезультатРасшифровки.АдресХранилища;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ИдентификаторЗапроса)
	
	ДанныеДляЗаполнения = Обработки.МониторСверкиСКонтрагентами.НовыйДанныеДляЗаполнения();
	ДанныеДляЗаполнения.ИдентификаторЗапроса = "ЗапросОригиналов";
	ДанныеДляЗаполнения.ДатаНачала = Объект.ДатаНачала;
	ДанныеДляЗаполнения.ДатаОкончания = Объект.ДатаОкончания;
	ДанныеДляЗаполнения.Организация = Объект.Организация;
	ДанныеДляЗаполнения.ИдентификаторЭДО = ИдентификаторЭДО;
	ДанныеДляЗаполнения.Контрагент = Параметры.Контрагент;
	ДанныеДляЗаполнения.КонтактноеЛицо = Параметры.КонтактноеЛицо;
	ДанныеДляЗаполнения.ЭДО = Параметры.ЭДО;
	ДанныеДляЗаполнения.ОтправлятьПисьмаВФорматеHTML = ОтправлятьПисьмаВФорматеHTML;
	ДанныеДляЗаполнения.НетОригиналовКоличество = Параметры.НетОригиналовКоличество;
	ДанныеДляЗаполнения.НетОригиналовКоличествоЭДО = Параметры.НетОригиналовКоличествоЭДО;
	ДанныеДляЗаполнения.УникальныйИдентификатор = УникальныйИдентификатор;
	
	Обработки.МониторСверкиСКонтрагентами.ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения);
	
КонецПроцедуры

# КонецОбласти