#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Объект.Организация = Параметры.Организация;
	Иначе
		Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	ПредставлениеОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "НаименованиеСокращенное");
	
	// Заполним настройки по умолчанию. Далее могут быть заменены из сохраненных настроек.
	
	// Период по умолчанию - квартал (недавно завершенный или текущий)
	ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
	Если НачалоМесяца(ТекущаяДата) = НачалоКвартала(ТекущаяДата) Тогда
		// Сейчас 1-й месяц квартала - смотрим прошлый квартал
		Объект.ДатаНачала = НачалоКвартала(ДобавитьМесяц(ТекущаяДата, -3));;
		Объект.ДатаОкончания = КонецКвартала(Объект.ДатаНачала);
	Иначе
		// Смотрим текущий квартал
		Объект.ДатаНачала = НачалоКвартала(ТекущаяДата);;
		Объект.ДатаОкончания = КонецКвартала(ТекущаяДата);
	КонецЕсли;
	
	Периодичность = Перечисления.Периодичность.Квартал;
	СчетаРасчетовСПоставщиками.ЗагрузитьЗначения(Обработки.МониторСверкиСКонтрагентами.СчетаРасчетовСПоставщиками());
	СчетаРасчетовСПокупателями.ЗагрузитьЗначения(Обработки.МониторСверкиСКонтрагентами.СчетаРасчетовСПокупателями());
	Поставщики = Истина;
	Покупатели = Истина;
	КонтрагентыСРасчетамиИОстатками = Истина;
	КонтрагентыСРасчетамиБезОстатков = Истина;
	ЗаполнятьСписокПриИзмененииНастроек = Истина;
	
	ИтогиПоСтатусамСверки = Новый ФиксированноеСоответствие(Новый Соответствие);
	ЗаполнитьОтборПоСтатусамСверки();
	
	// Прочитаем ФО и настройки пользователя
	ИспользуетсяСервисСверкиРасчетов = СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов();
	ИспользуютсяОтветственные = 
		(ОбщегоНазначенияБП.ЭтоПолныйИнтерфейс() И ПолучитьФункциональнуюОпцию("ИспользоватьЛидыПолныйИнтерфейс")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьЛидыПростойИнтерфейс"))
		И ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОтветственных");
	ПользовательУправляетСчетамиУчета = СчетаУчетаВДокументахВызовСервераПовтИсп.ПользовательУправляетСчетамиУчета();
	
	ОтправлятьПисьмаВФорматеHTML = ПолучитьФункциональнуюОпцию("ОтправлятьПисьмаВФорматеHTML");
	
	СписокИдентификаторов = УчетныеЗаписиЭДО.СписокИдентификаторовУчетныхЗаписейОрганизацииСервисаЭДО(Объект.Организация);
	Если СписокИдентификаторов.Количество() = 1 Тогда
		ИдентификаторЭДО = СписокИдентификаторов[0].Значение;
	КонецЕсли;
	
	ИнициализироватьКомпоновщикНастроек();
	
	ПодготовитьФормуНаСервере();
	
	УправлениеФормой(ЭтотОбъект);
	
	ОбщегоНазначенияБП.ЗаписатьОперациюБизнесСтатистики("СтатистикаБП.МониторСверкиСКонтрагентами.ОткрытМонитор");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗаполнятьСписокПриИзмененииНастроек Тогда
		ЗаполнитьДаннымиБухгалтерскогоУчетаНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	// Период
	// Для квартальной периодичности период уже установлен. В иных случаях устанавливаем здесь.
	Если Периодичность = Перечисления.Периодичность.Месяц Тогда
		ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
		// Смотрим прошлый месяц
		Объект.ДатаНачала = НачалоМесяца(ДобавитьМесяц(ТекущаяДата, -1));
		Объект.ДатаОкончания = КонецМесяца(Объект.ДатаНачала);
	ИначеЕсли Периодичность = Перечисления.Периодичность.Год Тогда
		ТекущаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
		// В декабре смотрим текущий год, а иначе - предшествующие 12 месяцев
		Если Месяц(ТекущаяДата) = 12 Тогда
			Объект.ДатаНачала = НачалоГода(ТекущаяДата);
			Объект.ДатаОкончания = КонецГода(ТекущаяДата);
		Иначе
			Объект.ДатаНачала = НачалоМесяца(ДобавитьМесяц(ТекущаяДата, -12));
			Объект.ДатаОкончания = КонецМесяца(ДобавитьМесяц(ТекущаяДата, -1));
		КонецЕсли;
	КонецЕсли;
	
	// Сортировка
	Если Не ЗначениеЗаполнено(Сортировка) Или Элементы.Сортировка.СписокВыбора.НайтиПоЗначению(Сортировка) = Неопределено Тогда
		Сортировка = Элементы.Сортировка.СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_АктСверкиВзаиморасчетов" Тогда
		Контрагент = КонтрагентДляОбновленияПослеЗаписиАктаСверки(Источник);
		Если ЗначениеЗаполнено(Контрагент) Тогда
			ЗаполнитьДаннымиБухгалтерскогоУчетаНаКлиенте(, Контрагент);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_СостояниеСверкиСКонтрагентами" Тогда
		// Обновляем комментарий
		Если Параметр.Организация = Объект.Организация Тогда
			СтрокиКонтрагента = СверкаСКонтрагентами.НайтиСтроки(Новый Структура("Контрагент", Параметр.Контрагент));
			Если СтрокиКонтрагента.Количество() > 0 Тогда
				СтрокиКонтрагента[0].Комментарий = Параметр.Комментарий;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	ПриИзмененииПериода();
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	ПриИзмененииПериода();
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ПриИзмененииНастройки();
	ПриИзмененииОранизацииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыФормы = Новый Структура("НачалоПериода, КонецПериода", Объект.ДатаНачала, Объект.ДатаОкончания);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыФормы, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СверкаСКонтрагентамиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СверкаСКонтрагентами.ТекущиеДанные;
	СписокВыбора = Новый СписокЗначений;
	
	Если Поле.Имя = "Контрагент" Тогда
		
		ПоказатьЗначение(, ТекущиеДанные.Контрагент);
		
	ИначеЕсли Поле.Имя = "ДебиторскаяЗадолженность" Или Поле.Имя = "КредиторскаяЗадолженность" Тогда
		
		// Находим счета, по которым есть расчеты
		СчетаРасчетовКонтрагента = СчетаРасчетов.НайтиСтроки(Новый Структура("Контрагент", ТекущиеДанные.Контрагент));
		
		// Для каждого счета отдельная расшифровка через ОСВ
		Для Каждого СтрокаСчета Из СчетаРасчетовКонтрагента Цикл
			ДействиеРасшифровки = НовыйОписаниеДействияРасшифровки();
			ДействиеРасшифровки.Действие = "ОборотноСальдоваяВедомостьПоСчету";
			ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("Контрагент", ТекущиеДанные.Контрагент);
			Если СтрокаСчета.СчетКоличество > 1 Тогда
				// Расчеты на нескольких субсчетах - расшифровываем счет верхнего уровня и детализируем до субсчетов
				ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("Счет", СтрокаСчета.СчетВерхнегоУровня);
				ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("ПоСубсчетам", Истина);
			Иначе
				// Расчеты на одном субсчете - открываем расшифровку именно по нему
				ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("Счет", СтрокаСчета.Счет);
				ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("ПоСубсчетам", Ложь);
			КонецЕсли;
			СписокВыбора.Добавить(
				ДействиеРасшифровки,
				СтрШаблон(НСтр("ru='ОСВ по счету %1'"), ДействиеРасшифровки.ПараметрыРасшифровки.Счет));
		КонецЦикла;
		
	ИначеЕсли Поле.Имя = "СостояниеСверки" Или Поле.Имя = "СтатусСверки" Тогда
		
		Если ТекущиеДанные.АктСверкиКоличество = 1
			 И ЗначениеЗаполнено(ТекущиеДанные.АктСверки)
			 И Не (ТекущиеДанные.СервисСверкиИспользуется
			 И ТекущиеДанные.СервисСверкиДанныеАктуальны) Тогда
			
			// Открыть текущий акт сверки
			ДействиеРасшифровки = НовыйОписаниеДействияРасшифровки();
			ДействиеРасшифровки.Действие = "ОткрытьФорму";
			ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("ИмяФормы", "Документ.АктСверкиВзаиморасчетов.ФормаОбъекта");
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.АктСверки);
			
			ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("ПараметрыФормы", ПараметрыФормы);
			
			СписокВыбора.Добавить(ДействиеРасшифровки);
			
		ИначеЕсли ТекущиеДанные.АктСверкиКоличество > 0 Тогда
			// Показать расшифровку актов сверки за период
			ДействиеРасшифровки = НовыйОписаниеДействияРасшифровки();
			ДействиеРасшифровки.Действие = "ОткрытьФорму";
			ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("ИмяФормы", "Обработка.МониторСверкиСКонтрагентами.Форма.Расшифровка");
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация",   Объект.Организация);
			ПараметрыФормы.Вставить("ДатаНачала",    Объект.ДатаНачала);
			ПараметрыФормы.Вставить("ДатаОкончания", Объект.ДатаОкончания);
			ПараметрыФормы.Вставить("Контрагент"   , ТекущиеДанные.Контрагент);
			ПараметрыФормы.Вставить("Поставщики"   , Поставщики);
			ПараметрыФормы.Вставить("Покупатели"   , Покупатели);
			ПараметрыФормы.Вставить("СчетаРасчетовСПоставщиками", СчетаРасчетовСПоставщиками);
			ПараметрыФормы.Вставить("СчетаРасчетовСПокупателями", СчетаРасчетовСПокупателями);
			ПараметрыФормы.Вставить("ИдентификаторРасшифровки", "РасшифровкаАктыСверки");
			
			ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("ПараметрыФормы", ПараметрыФормы);
			
			СписокВыбора.Добавить(ДействиеРасшифровки);
			
		ИначеЕсли ТекущиеДанные.СервисСверкиИспользуется И ТекущиеДанные.СервисСверкиДанныеАктуальны Тогда
			// Нет актов, но есть актуальные данные в сервисе сверки - открываем отчет по данным сервиса
			ДействиеРасшифровки = НовыйОписаниеДействияРасшифровки();
			ДействиеРасшифровки.Действие = "РасшифровкаСервисаСверки";
			ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("Контрагент", ТекущиеДанные.Контрагент);
			
			СписокВыбора.Добавить(ДействиеРасшифровки, ТекущиеДанные.СостояниеСверки);
		КонецЕсли;
		
	ИначеЕсли Поле.Имя = "КомментарийКартинка" Или Поле.Имя = "Комментарий" Тогда
		
		ВвестиКомментарийПоКонтрагенту();
		
	ИначеЕсли Поле.Имя = "НетОригиналов" Тогда
		
		ДействиеРасшифровки = НовыйОписаниеДействияРасшифровки();
		ДействиеРасшифровки.Действие = "ОткрытьФорму";
		ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("ИмяФормы", "Обработка.МониторСверкиСКонтрагентами.Форма.Расшифровка");
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ПараметрыФормы.Вставить("ДатаНачала", Объект.ДатаНачала);
		ПараметрыФормы.Вставить("ДатаОкончания", Объект.ДатаОкончания);
		ПараметрыФормы.Вставить("Контрагент" , ТекущиеДанные.Контрагент);
		ПараметрыФормы.Вставить("ИдентификаторРасшифровки", "РасшифровкаНетОригиналов");
		ПараметрыФормы.Вставить("КонтактноеЛицо", ТекущиеДанные.КонтактноеЛицо);
		ПараметрыФормы.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
		ПараметрыФормы.Вставить("ЭДО", ТекущиеДанные.ЭДО);
		ПараметрыФормы.Вставить("ОтправлятьПисьмаВФорматеHTML", ОтправлятьПисьмаВФорматеHTML);
		ПараметрыФормы.Вставить("НетОригиналовКоличество", ТекущиеДанные.НетОригиналовКоличество);
		ПараметрыФормы.Вставить("НетОригиналовКоличествоЭДО", ТекущиеДанные.НетОригиналовКоличествоЭДО);
		
		ДействиеРасшифровки.ПараметрыРасшифровки.Вставить("ПараметрыФормы", ПараметрыФормы);
		
		СписокВыбора.Добавить(ДействиеРасшифровки, ТекущиеДанные.НетОригиналовПредставление);
		
	КонецЕсли;
	
	ОповещениеПриВыборе = Новый ОписаниеОповещения("РасшифровкаПослеВыбораИзСписка", ЭтотОбъект);
	
	Если СписокВыбора.Количество() = 1 Тогда
		ВыполнитьОбработкуОповещения(ОповещениеПриВыборе, СписокВыбора[0]);
	ИначеЕсли СписокВыбора.Количество() > 1 Тогда
		ПоказатьВыборИзМеню(ОповещениеПриВыборе, СписокВыбора, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Баннер1ССверкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СервисСверкиРасчетов" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.ИнтеграцияССервисомСверкиРасчетов");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗакрытьБаннер1ССверка()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ВидимостьБаннера1ССверка",
		"МониторСверкиСКонтрагентами",
		Ложь);
		
	Элементы.ГруппаБаннер.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Сортировка) Тогда
		Возврат;
	КонецЕсли;
	
	СверкаСКонтрагентами.Сортировать(Сортировка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусыСверкиПриИзменении(Элемент)
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ОтборИспользованиеПриИзменении(Элемент)
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидСравненияПриИзменении(Элемент)
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ОтборПравоеЗначениеПриИзменении(Элемент)
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикиПриИзменении(Элемент)
	ПриИзмененииНастройки();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаРасчетовСПоставщикамиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.СчетаРасчетовСПоставщиками.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И Не ЗначениеЗаполнено(ТекущиеДанные.Значение) Или ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаРасчетовСПоставщикамиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаРасчетовСПоставщикамиПослеУдаления(Элемент)
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ПокупателиПриИзменении(Элемент)
	ПриИзмененииНастройки();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СчетаРасчетовСПокупателямиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.СчетаРасчетовСПокупателями.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И Не ЗначениеЗаполнено(ТекущиеДанные.Значение) Или ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаРасчетовСПокупателямиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаРасчетовСПокупателямиПослеУдаления(Элемент)
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыСРасчетамиБезОстатковПриИзменении(Элемент)
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыСОстаткамиПрошлыхПериодовПриИзменении(Элемент)
	ПриИзмененииНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнятьСписокПриИзмененииНастроекПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьДаннымиБухгалтерскогоУчетаНаКлиенте(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБыстрыеНастройки(Команда)
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализироватьКомпоновщикНастроек();
	КонецЕсли;
	
	ПереключитьВидимостьБыстрыхНастроек(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьАктСверки(Команда)
	
	ТекущиеДанные = Элементы.СверкаСКонтрагентами.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура);
	ЗначенияЗаполнения = ПараметрыФормы.ЗначенияЗаполнения;
	ЗначенияЗаполнения.Вставить("Организация",   Объект.Организация);
	ЗначенияЗаполнения.Вставить("ДатаНачала",    Объект.ДатаНачала);
	ЗначенияЗаполнения.Вставить("ДатаОкончания", Объект.ДатаОкончания);
	ЗначенияЗаполнения.Вставить("Контрагент"   , ТекущиеДанные.Контрагент);
	ЗначенияЗаполнения.Вставить("ЭтоЭлектронныйДокумент", Истина);
	
	СчетаУчетаРасчетов = СчетаРасчетовСПоставщиками.ВыгрузитьЗначения();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СчетаУчетаРасчетов,
		СчетаРасчетовСПокупателями.ВыгрузитьЗначения(),
		Истина);
	
	ЗначенияЗаполнения.Вставить("СчетаУчетаРасчетов", СчетаУчетаРасчетов);
	
	ОткрытьФорму("Документ.АктСверкиВзаиморасчетов.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНаСверку(Команда)
	
	ИдентификаторТекущейСтроки = Элементы.СверкаСКонтрагентами.ТекущаяСтрока;
	Если ИдентификаторТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗапроса = "ЗапросСверки";
	ПараметрыОтправкиПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	
	ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ИдентификаторТекущейСтроки, ИдентификаторЗапроса);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыОтправкиПисьма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНаОригиналы(Команда)
	
	ИдентификаторТекущейСтроки = Элементы.СверкаСКонтрагентами.ТекущаяСтрока;
	Если ИдентификаторТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗапроса = "ЗапросОригиналов";
	ПараметрыОтправкиПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	
	ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ИдентификаторТекущейСтроки, ИдентификаторЗапроса);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыОтправкиПисьма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНаСверкуИОригиналы(Команда)
	
	ИдентификаторТекущейСтроки = Элементы.СверкаСКонтрагентами.ТекущаяСтрока;
	Если ИдентификаторТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗапроса = "ЗапросСверкиИОригиналов";
	ПараметрыОтправкиПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	
	ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ИдентификаторТекущейСтроки, ИдентификаторЗапроса);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыОтправкиПисьма);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДляПечати(Команда)
	
	ПараметрыОткрытия = Новый Структура; 
	ПараметрыОткрытия.Вставить("ТабличныйДокумент", ПодготовитьСписокДляПечатиНаСервере());
	ОткрытьФорму("Обработка.МониторСверкиСКонтрагентами.Форма.ПечатьСписка", ПараметрыОткрытия);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПоСсылке(Команда)
	ИнтеграцияShareКлиент.ОткрытьФормуЗагрузитьДокументПоСсылке();
КонецПроцедуры

&НаКлиенте
Процедура ВвестиКомментарий(Команда)
	
	ВвестиКомментарийПоКонтрагенту();
	
КонецПроцедуры

&НаКлиенте
Процедура НаписатьРазработчикам(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаСчетаРасчетовСПоставщиками.Доступность = Форма.Поставщики;
	Элементы.ГруппаСчетаРасчетовСПокупателями.Доступность = Форма.Покупатели;
	Элементы.КонтрагентыСОстаткамиПрошлыхПериодов.Доступность = Форма.Поставщики Или Форма.Покупатели;
	
	Элементы.ЗаполнитьВБыстрыхНастройках.Видимость = Не Форма.ЗаполнятьСписокПриИзмененииНастроек;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Элементы.ГруппаБаннер.Видимость = ВидимостьБаннера1ССверка(ИспользуетсяСервисСверкиРасчетов);
	ДанныеБаннера = Новый Структура();
	ДанныеБаннера.Вставить("ПрименяетсяОСНО", ПолучитьФункциональнуюОпцию("ИспользуетсяОСНО") 
		Или ПолучитьФункциональнуюОпцию("ИспользуетсяНДФЛИП"));
	Элементы.Баннер1ССверка.Заголовок = ПерсонализированныеПредложенияСервисов.ТекстБаннераСервисСверкиРасчетов(
		"СервисСверкиРасчетов", ДанныеБаннера);

	Элементы.Ответственный.Видимость = ИспользуютсяОтветственные;
	
	Элементы.ГруппаСчетаРасчетовСПоставщиками.Видимость = ПользовательУправляетСчетамиУчета;
	Элементы.ГруппаСчетаРасчетовСПокупателями.Видимость = ПользовательУправляетСчетамиУчета;
	
	Если ПользовательУправляетСчетамиУчета Тогда
		Элементы.ЗадолженностьОстаток.Заголовок = ЗаголовокНаДату(НСтр("ru='Остаток'"), Объект.ДатаОкончания);
		Элементы.ДебиторскаяЗадолженность.Заголовок =  НСтр("ru='Дебет'");
		Элементы.КредиторскаяЗадолженность.Заголовок = НСтр("ru='Кредит'");
		Элементы.КредиторскаяЗадолженностьНетОпераций.Заголовок = НСтр("ru='Кредит'");
		Элементы.ПроцентПодтвержденнойДЗ.Заголовок = НСтр("ru='по дебиторской задолженности'");
		Элементы.ПроцентПодтвержденнойКЗ.Заголовок = НСтр("ru='по кредиторской задолженности'");
	Иначе
		Элементы.ЗадолженностьОстаток.Заголовок = ЗаголовокНаДату(НСтр("ru='Долг'"), Объект.ДатаОкончания);
		Элементы.ДебиторскаяЗадолженность.Заголовок =  НСтр("ru='Контрагента'");
		Элементы.КредиторскаяЗадолженность.Заголовок = НСтр("ru='Организации'");
		Элементы.КредиторскаяЗадолженностьНетОпераций.Заголовок = НСтр("ru='Организации'");
		Элементы.ПроцентПодтвержденнойДЗ.Заголовок = НСтр("ru='по долгу контрагентов'");
		Элементы.ПроцентПодтвержденнойКЗ.Заголовок = НСтр("ru='по долгу организации'");
	КонецЕсли;
	
	// Во всех вариантах дополнительно сортируем по ссылке контрагента, чтобы порядок записей был более стабильный
	Элементы.Сортировка.СписокВыбора.Добавить("ДебиторскаяЗадолженность Убыв,Контрагент", 
		?(ПользовательУправляетСчетамиУчета, НСтр("ru='По убыванию дебиторской задолженности'"), НСтр("ru='По убыванию долга контрагента'")));
	Элементы.Сортировка.СписокВыбора.Добавить("КредиторскаяЗадолженность Убыв,Контрагент",
		?(ПользовательУправляетСчетамиУчета, НСтр("ru='По убыванию кредиторской задолженности'"), НСтр("ru='По убыванию долга организации'")));
	Элементы.Сортировка.СписокВыбора.Добавить("АктСверкиКоличество,Контрагент", НСтр("ru='Сначала без актов сверки'"));
	Элементы.Сортировка.СписокВыбора.Добавить("НетОригиналовКоличество Убыв,Контрагент", НСтр("ru='Сначала с неполученными оригиналами'"));
	Элементы.Сортировка.СписокВыбора.Добавить("НаименованиеКонтрагента,Контрагент", НСтр("ru='По наименованию'"));
	
	Если Не ЗначениеЗаполнено(Сортировка) Или Элементы.Сортировка.СписокВыбора.НайтиПоЗначению(Сортировка) = Неопределено Тогда
		Сортировка = Элементы.Сортировка.СписокВыбора[0].Значение;
	КонецЕсли;
	
	// Показываем быстрые настройки
	ПереключитьВидимостьБыстрыхНастроек(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПослеУспешнойЗагрузкиДанных()
	
	// Расчетные итоги
	ЗаполнитьОтборПоСтатусамСверки(); // расчет процента по каждому статусу
	Если ВсегоДЗ <> 0 Тогда
		ПроцентПодтвержденнойДЗ = (ПодтвержденнаяДЗ / ВсегоДЗ) * 100;
	Иначе
		ПроцентПодтвержденнойДЗ = 0;
	КонецЕсли;
	
	Если ВсегоКЗ <> 0 Тогда
		ПроцентПодтвержденнойКЗ = (ПодтвержденнаяКЗ / ВсегоКЗ) * 100;
	Иначе
		ПроцентПодтвержденнойКЗ = 0;
	КонецЕсли;
	
	// При успешной загрузке данных обновим указание на дату остатков
	Если ПользовательУправляетСчетамиУчета Тогда
		Элементы.ЗадолженностьОстаток.Заголовок = ЗаголовокНаДату(НСтр("ru='Остаток'"), Объект.ДатаОкончания);
	Иначе
		Элементы.ЗадолженностьОстаток.Заголовок = ЗаголовокНаДату(НСтр("ru='Долг'"), Объект.ДатаОкончания);
	КонецЕсли;
	
	Если ВсегоКонтрагентов > 0 Тогда
		Элементы.ОтборСтатусыСверки.Заголовок = СтрШаблон(
			НСтр("ru='Состояние сверки (из %1)'"),
			ПолучитьСклоненияСтрокиПоЧислу(
				"контрагент",
				ВсегоКонтрагентов,
				"",
				"Л= ru_RU; ЧС=Количественное",
				"ПД=Родительный;ПЧ=Число")[0]);
	Иначе
		Элементы.ОтборСтатусыСверки.Заголовок = НСтр("ru='Состояние сверки'");
	КонецЕсли;
	
	// Для корректного отображения заголовка в шапке таблицы служебная колонка скрыта при начальной загрузке формы.
	// Здесь отображаем колонку, т.к. ее видимость будет управляться условным оформлением.
	Элементы.КредиторскаяЗадолженностьНетОпераций.Видимость = (СверкаСКонтрагентами.Количество() > 0);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	// КредиторскаяЗадолженность
	
	// Если были остатки или обороты (СчетКоличество > 0), но сальдо нулевое, то выведем 0,
	// чтобы можно было открыть отчет по ссылке
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КредиторскаяЗадолженность");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.ДебиторскаяЗадолженность", ВидСравненияКомпоновкиДанных.Равно, 0);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.КредиторскаяЗадолженность", ВидСравненияКомпоновкиДанных.Равно, 0);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.СчетКоличество", ВидСравненияКомпоновкиДанных.Больше, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "0");
	
	// Если сальдо нулевое и не было оборотов (СчетКоличество = 0), то выведем просто подсказку, что не было операций.
	// В этом случае скроем гиперссылку КредиторскаяЗадолженность, а вместо этого выведем колонку с текстом пояснения.
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КредиторскаяЗадолженность");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.СчетКоличество", ВидСравненияКомпоновкиДанных.Равно, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КредиторскаяЗадолженностьНетОпераций");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.СчетКоличество", ВидСравненияКомпоновкиДанных.Равно, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "Нет операций");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КредиторскаяЗадолженностьНетОпераций");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.СчетКоличество", ВидСравненияКомпоновкиДанных.Больше, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//СостояниеСверки
	
	// Если нет актов и нет данных сервиса сверки, то сверка не выполнялась.
	// В этом случае скроем гиперссылку СостояниеСверки, а вместо этого выведем колонку с текстом пояснения.
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СостояниеСверкиНеВыполнялась");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.АктСверкиКоличество", ВидСравненияКомпоновкиДанных.Равно, 0);
	ГруппаИли = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"СверкаСКонтрагентами.СервисСверкиИспользуется", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"СверкаСКонтрагентами.СервисСверкиДанныеАктуальны", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "Не выполнялась");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СтатусСверки");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СостояниеСверки");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.АктСверкиКоличество", ВидСравненияКомпоновкиДанных.Равно, 0);
	ГруппаИли = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"СверкаСКонтрагентами.СервисСверкиИспользуется", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"СверкаСКонтрагентами.СервисСверкиДанныеАктуальны", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СтатусСверкиНеВыполнялась");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СостояниеСверкиНеВыполнялась");
	ГруппаИли = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"СверкаСКонтрагентами.АктСверкиКоличество", ВидСравненияКомпоновкиДанных.Больше, 0);
	ГруппаИВторогоУровня = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаИли.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИВторогоУровня,
		"СверкаСКонтрагентами.СервисСверкиИспользуется", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИВторогоУровня,
		"СверкаСКонтрагентами.СервисСверкиДанныеАктуальны", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//Комментарий
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КомментарийКартинка");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Комментарий");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СверкаСКонтрагентами.Комментарий", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	КомпоновщикИнициализирован = Истина;
	
	Схема = Обработки.МониторСверкиСКонтрагентами.ПолучитьМакет("СхемаКомпоновкиДанных");
	СхемаКомпоновкиДанных = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
	
	ВариантНастроек = Схема.ВариантыНастроек.Найти("Основной");
	
	Если ВариантНастроек <> Неопределено Тогда
		Настройки = ВариантНастроек.Настройки;
	Иначе
		Настройки = Схема.НастройкиПоУмолчанию;
	КонецЕсли;
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	
	ПолеКонтрагент = Новый ПолеКомпоновкиДанных("Контрагент");
	ПолеОтветственный = Новый ПолеКомпоновкиДанных("Ответственный");
	
	Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		Если Не ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭлементОтбора.ЛевоеЗначение = ПолеКонтрагент
			 Или ЭлементОтбора.ЛевоеЗначение = ПолеОтветственный И ИспользуютсяОтветственные Тогда
			 
			ИндексСтроки = КомпоновщикНастроек.Настройки.Отбор.Элементы.Индекс(ЭлементОтбора);
			
			Если ИндексСтроки = 0 Тогда
				// Элемент формы уже есть - только включим видимость и настроим заголовок ниже
				Элементы.ГруппаОтбор0.Видимость = Истина;
			Иначе
				СкопироватьЭлементФормыРекурсивно(Элементы.ГруппаОтбор0, Элементы.БыстрыеОтборы, ИндексСтроки);
			КонецЕсли;
			
			Элементы["Отбор" + ИндексСтроки + "Заголовок"].Заголовок = ЭлементОтбора.Представление;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьЭлементФормыРекурсивно(Знач ЭлементИсточник, Родитель, ИндексСтроки)
	
	// ЭлементИсточник - это всегда элемент с 0 в имени, который означает связь со строкой источника с индексом 0.
	ИмяНовогоЭлемента = СтрЗаменить(ЭлементИсточник.Имя, "0", ИндексСтроки);
	НовыйЭлемент = СкопироватьЭлементФормы(ЭлементИсточник, Родитель, ИмяНовогоЭлемента, ИндексСтроки);
	
	Если ТипЗнч(ЭлементИсточник) = Тип("ГруппаФормы") Тогда
		Для каждого ПодчиненныйЭлемент Из ЭлементИсточник.ПодчиненныеЭлементы Цикл
			СкопироватьЭлементФормыРекурсивно(ПодчиненныйЭлемент, НовыйЭлемент, ИндексСтроки);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СкопироватьЭлементФормы(ЭлементИсточник, Родитель, ИмяНовогоЭлемента, ИндексСтроки)

	ТипИсточника = ТипЗнч(ЭлементИсточник);
	НовыйЭлемент = Элементы.Добавить(ИмяНовогоЭлемента, ТипИсточника, Родитель);
	НовыйЭлемент.Вид = ЭлементИсточник.Вид;

	Если ТипИсточника = Тип("ПолеФормы") Тогда
		// Игнорируем свойства, которые вызывают ошибки копирования или устанавливаются однократно 
		НеКопируемыеСвойства = Новый Массив;
		НеКопируемыеСвойства.Добавить("ПутьКДанным");
		Если НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода Тогда
			НеКопируемыеСвойства.Добавить("ВыделенныйТекст");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементИсточник,, СтрСоединить(НеКопируемыеСвойства, ", "));
		НовыйЭлемент.ПутьКДанным = СтрЗаменить(ЭлементИсточник.ПутьКДанным, "0", ИндексСтроки);
		// Копируем обработчик события "ПриИзменении" (другие не используются)
		ДействиеПриИзменении = ЭлементИсточник.ПолучитьДействие("ПриИзменении");
		Если ДействиеПриИзменении <> "" Тогда
			НовыйЭлемент.УстановитьДействие("ПриИзменении", ДействиеПриИзменении);
		КонецЕсли;
	ИначеЕсли ТипИсточника = Тип("ГруппаФормы") Тогда
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементИсточник,, "ПутьКДаннымЗаголовка");
	Иначе
		// Например, декорация формы
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементИсточник);
	КонецЕсли;
	
	НовыйЭлемент.Видимость = Истина;

	Возврат НовыйЭлемент;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПереключитьВидимостьБыстрыхНастроек(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ПоказыватьБыстрыеНастройки = Не Форма.ПоказыватьБыстрыеНастройки;

	Элементы.БыстрыеНастройки.Видимость = Форма.ПоказыватьБыстрыеНастройки;
	Элементы.ПоказатьБыстрыеНастройки.Пометка = Форма.ПоказыватьБыстрыеНастройки;
	
	Если Форма.ПоказыватьБыстрыеНастройки Тогда
		Элементы.ПоказатьБыстрыеНастройки.Картинка = БиблиотекаКартинок.СтрелкаВправо;
	Иначе
		Элементы.ПоказатьБыстрыеНастройки.Картинка = БиблиотекаКартинок.СтрелкаВлево;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПоДаннымБухгалтерскогоУчета(ТихийРежим = Истина, Контрагент = Неопределено)
	
	// Проверим заполненность обязательных реквизитов
	Если Не ПроверитьЗаполнение() Тогда
		Если ТихийРежим Тогда
			// Скроем сообщения об ошибках заполенения
			ПолучитьСообщенияПользователю(Истина);
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
	ИдентификаторЗадания = Неопределено;
	
	СтруктураПараметров = Новый Структура;
	
	СтруктураПараметров.Вставить("ДатаНачала",    Объект.ДатаНачала);
	СтруктураПараметров.Вставить("ДатаОкончания", КонецДня(Объект.ДатаОкончания));
	СтруктураПараметров.Вставить("Организация",   Объект.Организация);
	СтруктураПараметров.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		СтруктураПараметров.Вставить("Контрагент", Контрагент);
	КонецЕсли;
	
	ВыбранныеСтатусыСверки = Новый СписокЗначений;
	Для Каждого Элемент Из ОтборСтатусыСверки Цикл
		Если Не Элемент.Пометка Тогда
			Продолжить;
		КонецЕсли;
		ВыбранныеСтатусыСверки.Добавить(Элемент.Значение);
	КонецЦикла;
	
	СтруктураПараметров.Вставить("Сортировка", Сортировка);
	
	СтруктураПараметров.Вставить("ОтбиратьПоСтатусуСверки",
		ВыбранныеСтатусыСверки.Количество() > 0 И ОтборСтатусыСверки.Количество() <> ВыбранныеСтатусыСверки.Количество());
	СтруктураПараметров.Вставить("СтатусыСверки", ВыбранныеСтатусыСверки);
	
	СтруктураПараметров.Вставить("Поставщики", Поставщики);
	СтруктураПараметров.Вставить("Покупатели", Покупатели);
	
	СчетаРасчетовСПоставщикамиМассив = СчетаРасчетовСПоставщиками.ВыгрузитьЗначения();
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(СчетаРасчетовСПоставщикамиМассив , ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	СтруктураПараметров.Вставить("СчетаРасчетовСПоставщиками", СчетаРасчетовСПоставщикамиМассив);
	
	СчетаРасчетовСПокупателямиМассив = СчетаРасчетовСПокупателями.ВыгрузитьЗначения();
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(СчетаРасчетовСПокупателямиМассив , ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	// Во избежание дубирования данных о расчетах уберем из массива счетов покупателей те, что уже есть в счетах поставщиков
	Если Поставщики Тогда
		ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			СчетаРасчетовСПокупателямиМассив, СчетаРасчетовСПоставщикамиМассив);
	КонецЕсли;
	СтруктураПараметров.Вставить("СчетаРасчетовСПокупателями", СчетаРасчетовСПокупателямиМассив);
	
	СтруктураПараметров.Вставить("КонтрагентыСОстаткамиПрошлыхПериодов", КонтрагентыСОстаткамиПрошлыхПериодов);
	СтруктураПараметров.Вставить("КонтрагентыСРасчетамиБезОстатков", КонтрагентыСРасчетамиБезОстатков);
	СтруктураПараметров.Вставить("СхемаКомпоновкиДанных", ПолучитьИзВременногоХранилища(СхемаКомпоновкиДанных));
	СтруктураПараметров.Вставить("НастройкиКомпоновкиДанных", КомпоновщикНастроек.ПолучитьНастройки());
	
	Если ЭтоАдресВременногоХранилища(АдресХранилищаСОшибками) Тогда
		УдалитьИзВременногоХранилища(АдресХранилищаСОшибками);
		АдресХранилищаСОшибками = "";
	КонецЕсли;
	
	АдресХранилищаСОшибками = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	СтруктураПараметров.Вставить("АдресХранилищаСОшибками", АдресХранилищаСОшибками);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = "ЗаполнитьКонтрагентовДляСверки";
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.МониторСверкиСКонтрагентами.ПолучитьДанныеСверкиСКонтрагентами",
		СтруктураПараметров,
		ПараметрыВыполнения);
		
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьДаннымиБухгалтерскогоУчетаНаКлиенте(ТихийРежим = Истина, Контрагент = Неопределено)
	
	Результат = ЗаполнитьПоДаннымБухгалтерскогоУчета(ТихийРежим, Контрагент);
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ЗаполнитьПоДаннымБухгалтерскогоУчетаЗавершение",
		ЭтотОбъект,
		Контрагент);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДаннымиБухгалтерскогоУчетаНаКлиентеСОжиданием() Экспорт
	ЗаполнитьДаннымиБухгалтерскогоУчетаНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДаннымБухгалтерскогоУчетаЗавершение(Результат, Контрагент) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыполнения = ЗагрузитьПодготовленныеДанные(Результат, Контрагент);
	
	Если Не РезультатВыполнения.Успешно Тогда
		ОбщегоНазначенияБПКлиент.ОткрытьФормуОшибокПерепроведения(ЭтотОбъект, АдресХранилищаСОшибками);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьПодготовленныеДанные(Результат, Контрагент)
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	РезультатВыполнения = Новый Структура;
	РезультатВыполнения.Вставить("ЗаданиеВыполнено", Истина);
	РезультатВыполнения.Вставить("Успешно", Ложь);
	
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат РезультатВыполнения;
	КонецЕсли;
	
	РезультатВыполнения.Успешно = СтруктураДанных.Успешно;
	
	Если СтруктураДанных.Успешно Тогда
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			// При "точечном" обновлении данных (по контрагенту) обновляем или дописываем только одну строку
			СтрокиКонтрагента = СверкаСКонтрагентами.НайтиСтроки(Новый Структура("Контрагент", Контрагент));
			СтрокаКонтрагента = Неопределено;
			
			СтатусыСверкиАктПодписан = Обработки.МониторСверкиСКонтрагентами.СтатусыСверки().АктСверкиПодписан.Статус;
			ИтогиПоСтатусамСверкиВрем = Новый Соответствие(ИтогиПоСтатусамСверки); // для редактирования
			
			Если СтрокиКонтрагента.Количество() > 0 Тогда
				
				// В таблице только одна строка по контрагенту
				СтрокаКонтрагента = СтрокиКонтрагента[0];
				
				//Здесь вычтем показатели контрагента из общей статистики, а после загрузки данных добавим изменные значения
				
				// Итоги по статусам сверки
				КоличествоКонтрагентов = ИтогиПоСтатусамСверкиВрем.Получить(СтрокаКонтрагента.СтатусСверки);
				Если КоличествоКонтрагентов <> Неопределено Тогда
					ИтогиПоСтатусамСверкиВрем[СтрокаКонтрагента.СтатусСверки] = КоличествоКонтрагентов - 1;
				КонецЕсли;
				
				// Суммы задолженности
				ВсегоДЗ = ВсегоДЗ - СтрокаКонтрагента.ДебиторскаяЗадолженность;
				ВсегоКЗ = ВсегоКЗ - СтрокаКонтрагента.КредиторскаяЗадолженность;
				
				Если СтрокаКонтрагента.СтатусСверки = СтатусыСверкиАктПодписан Тогда
					ПодтвержденнаяДЗ = ПодтвержденнаяДЗ - СтрокаКонтрагента.ДебиторскаяЗадолженность;
					ПодтвержденнаяКЗ = ПодтвержденнаяКЗ - СтрокаКонтрагента.КредиторскаяЗадолженность;
				КонецЕсли;
				
			КонецЕсли;
			
			Если СтрокиКонтрагента.Количество() > 0 И СтруктураДанных.ТаблицаСостояниеСверки.Количество() = 0 Тогда
				// Если контрагент более не соответствует критериям отбора, то удалим строку
				ВсегоКонтрагентов = ВсегоКонтрагентов - 1;
				СверкаСКонтрагентами.Удалить(СтрокиКонтрагента[0]);
				СтрокаКонтрагента = Неопределено;
			ИначеЕсли СтрокиКонтрагента.Количество() = 0 И СтруктураДанных.ТаблицаСостояниеСверки.Количество() > 0 Тогда
				// Контрагента ранее не было в таблице - добавляем
				СтрокаКонтрагента = СверкаСКонтрагентами.Добавить();
			КонецЕсли;
			
			Если СтрокаКонтрагента <> Неопределено Тогда
				
				ЗаполнитьЗначенияСвойств(СтрокаКонтрагента, СтруктураДанных.ТаблицаСостояниеСверки[0]);
				
				// Обновим порядок строк исходя из новых данных
				СверкаСКонтрагентами.Сортировать(Сортировка);
				
				// Обновим строки по контрагенту во вспомогательной таблице
				Для Каждого СтрокаКУдалению Из СчетаРасчетов.НайтиСтроки(Новый Структура("Контрагент", Контрагент)) Цикл
					СчетаРасчетов.Удалить(СтрокаКУдалению);
				КонецЦикла;
				
				Для Каждого СтрокаСчетаРасчетов Из СтруктураДанных.СчетаРасчетов Цикл
					ЗаполнитьЗначенияСвойств(СчетаРасчетов.Добавить(), СтрокаСчетаРасчетов);
				КонецЦикла;
				
				//Здесь добавим показатели контрагента в общую статистику
				
				// Итоги по статусам сверки
				КоличествоКонтрагентов = ИтогиПоСтатусамСверкиВрем.Получить(СтрокаКонтрагента.СтатусСверки);
				Если КоличествоКонтрагентов <> Неопределено Тогда
					ИтогиПоСтатусамСверкиВрем[СтрокаКонтрагента.СтатусСверки] = КоличествоКонтрагентов + 1;
				КонецЕсли;
				
				// Суммы задолженности
				ВсегоДЗ = ВсегоДЗ + СтрокаКонтрагента.ДебиторскаяЗадолженность;
				ВсегоКЗ = ВсегоКЗ + СтрокаКонтрагента.КредиторскаяЗадолженность;
				
				Если СтрокаКонтрагента.СтатусСверки = СтатусыСверкиАктПодписан Тогда
					ПодтвержденнаяДЗ = ПодтвержденнаяДЗ + СтрокаКонтрагента.ДебиторскаяЗадолженность;
					ПодтвержденнаяКЗ = ПодтвержденнаяКЗ + СтрокаКонтрагента.КредиторскаяЗадолженность;
				КонецЕсли;
				
			КонецЕсли;
			
			ИтогиПоСтатусамСверки = Новый ФиксированноеСоответствие(ИтогиПоСтатусамСверкиВрем);
			
		Иначе
			
			СверкаСКонтрагентами.Загрузить(СтруктураДанных.ТаблицаСостояниеСверки);
			СчетаРасчетов.Загрузить(СтруктураДанных.СчетаРасчетов);
			
			// Итоговые суммы
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДанных, , "СчетаРасчетов");
			
		КонецЕсли;
		
		НастроитьФормуПослеУспешнойЗагрузкиДанных();
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьОтборПоСтатусамСверки()
	
	// Прочитаем возможные статусы сверки.
	// Если есть статистика, то пишем ее в наименовании статуса.
	Для Каждого ОписаниеСтатуса Из Обработки.МониторСверкиСКонтрагентами.СтатусыСверки() Цикл
		
		КоличествоКонтрагентов = ИтогиПоСтатусамСверки.Получить(ОписаниеСтатуса.Значение.Статус);
		
		Если ЗначениеЗаполнено(КоличествоКонтрагентов) Тогда
			ПредставлениеСтатуса = СтрШаблон(
				НСтр("ru='%1 (%2)'"),
				ОписаниеСтатуса.Значение.Представление,
				КоличествоКонтрагентов);
		Иначе
			ПредставлениеСтатуса = ОписаниеСтатуса.Значение.Представление;
		КонецЕсли;
		
		ЭлементСписка = ОтборСтатусыСверки.НайтиПоЗначению(ОписаниеСтатуса.Значение.Статус);
		
		Если ЭлементСписка = Неопределено Тогда
			ОтборСтатусыСверки.Добавить(
				ОписаниеСтатуса.Значение.Статус,
				ПредставлениеСтатуса,
				,
				ОписаниеСтатуса.Значение.Картинка);
		Иначе
			ЭлементСписка.Представление = ПредставлениеСтатуса;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ДатаНачала    = РезультатВыбора.НачалоПериода;
	Объект.ДатаОкончания = РезультатВыбора.КонецПериода;
	
	ПриИзмененииПериода();
	ПриИзмененииНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПериода()
	
	Если НачалоМесяца(Объект.ДатаНачала) = НачалоМесяца(Объект.ДатаОкончания) Тогда
		Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц");
	ИначеЕсли НачалоМесяца(Объект.ДатаНачала) = ДобавитьМесяц(НачалоМесяца(Объект.ДатаОкончания), -11) Тогда
		Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год");
	Иначе
		// В иных случаях считаем, что периодичность квартал
		Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНастройки()
	
	Если ЗаполнятьСписокПриИзмененииНастроек Тогда
		ОтключитьОбработчикОжидания("ЗаполнитьДаннымиБухгалтерскогоУчетаНаКлиентеСОжиданием");
		ПодключитьОбработчикОжидания("ЗаполнитьДаннымиБухгалтерскогоУчетаНаКлиентеСОжиданием", 1, Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОранизацииСервер()
	ПредставлениеОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "НаименованиеСокращенное");
КонецПроцедуры

&НаКлиенте
Процедура ВвестиКомментарийПоКонтрагенту()
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.СверкаСКонтрагентами.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КлючЗаписи = КлючЗаписиСостояниеСверкиСКонтрагентом(Объект.Организация, ТекущиеДанные.Контрагент);
	
	ПараметрыФормы = Новый Структура;
	Если КлючЗаписи = Неопределено Тогда
		// Передаем значения для заполнения
		ЗначенияЗаполнения = Новый Структура("Организация, Контрагент", Объект.Организация, ТекущиеДанные.Контрагент);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	Иначе
		// Открываем существующую запись
		ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.СостояниеСверкиСКонтрагентами.ФормаЗаписи", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КлючЗаписиСостояниеСверкиСКонтрагентом(Организация, Контрагент)
	
	Возврат РегистрыСведений.СостояниеСверкиСКонтрагентами.КлючЗаписиСостояниеСверкиСКонтрагентом(Организация, Контрагент);
	
КонецФункции

&НаКлиенте
Процедура КартинкаЗакрытьБаннерНажатие(Элемент)
	ЗакрытьБаннер1ССверка();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидимостьБаннера1ССверка(ИспользуетсяСервисСверкиРасчетов)
	
	Возврат Не ИспользуетсяСервисСверкиРасчетов
		И ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ВидимостьБаннера1ССверка",
			"МониторСверкиСКонтрагентами",
			Истина);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокНаДату(ТекстЗаголовкаБезДаты, НаДату)
	
	Если Не ЗначениеЗаполнено(НаДату) Тогда
		Возврат ТекстЗаголовкаБезДаты;
	КонецЕсли;
	
	Возврат СтрШаблон(НСтр("ru='%1 на %2'"), ТекстЗаголовкаБезДаты, Формат(НаДату, "ДЛФ=D"));
	
КонецФункции

&НаКлиенте
Функция НовыйОписаниеДействияРасшифровки()
	
	ОписаниеДействия = Новый Структура;
	
	ОписаниеДействия.Вставить("Действие", "");
	ОписаниеДействия.Вставить("ПараметрыРасшифровки", Новый Структура);
	
	Возврат ОписаниеДействия;
	
КонецФункции

&НаКлиенте
Процедура РасшифровкаПослеВыбораИзСписка(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт 
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеВыбора = ВыбранноеЗначение.Значение;
	
	Если ЗначениеВыбора.Действие = "ОборотноСальдоваяВедомостьПоСчету" Тогда
		
		Счет = ЗначениеВыбора.ПараметрыРасшифровки.Счет;
		Контрагент = ЗначениеВыбора.ПараметрыРасшифровки.Контрагент;
		
		ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
		ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
		ДополнительныеСвойства.Вставить("Организация",      Объект.Организация);
		ДополнительныеСвойства.Вставить("НачалоПериода",    Объект.ДатаНачала);
		ДополнительныеСвойства.Вставить("КонецПериода",     Объект.ДатаОкончания);
		ДополнительныеСвойства.Вставить("Счет",             Счет);
		ДополнительныеСвойства.Вставить("ПоказательБУ",     Истина);
		ДополнительныеСвойства.Вставить("РежимРасшифровки", Истина);
		ДополнительныеСвойства.Вставить("ПоСубсчетам",      ЗначениеВыбора.ПараметрыРасшифровки.ПоСубсчетам);
		
		// Инициализируем отборы
		ПользовательскиеОтборыДляСчета = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
		ПользовательскиеОтборыДляСчета.ИдентификаторПользовательскойНастройки = "Отбор";
		
		ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
		
		// Группировка передается в отчет как массив структур.
		// Элемент массива содержит структуру описывающую строку таблицы "Группировка" стандартного отчета.
		Группировки = Новый Массив;
		
		Для НомерСубконто = 1 По ДанныеСчета.КоличествоСубконто Цикл
			
			// Отбираем по контрагенту, а по остальным субконто группируем
			ВидСубконто = ДанныеСчета["ВидСубконто" + НомерСубконто];
			Если ВидСубконто = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты") Тогда
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборыДляСчета, "Субконто" + НомерСубконто, Контрагент);
			Иначе
				ГруппировкаСубконто = Новый Структура;
				ГруппировкаСубконто.Вставить("Поле",           "Субконто" + НомерСубконто);
				ГруппировкаСубконто.Вставить("Представление",  ДанныеСчета["ВидСубконто" + НомерСубконто + "Наименование"]);
				ГруппировкаСубконто.Вставить("Использование",  Истина);
				ГруппировкаСубконто.Вставить("ТипГруппировки", 0);
				Группировки.Добавить(ГруппировкаСубконто);
			КонецЕсли;
			
		КонецЦикла;
		
		ДополнительныеСвойства.Вставить("Группировка", Группировки);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВидРасшифровки", 2);
		ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
		
		ОткрытьФорму("Отчет.ОборотноСальдоваяВедомостьПоСчету.Форма.ФормаОтчета", ПараметрыФормы, , Истина);
		
	ИначеЕсли ЗначениеВыбора.Действие = "ОткрытьФорму" Тогда
		
		ОткрытьФорму(ЗначениеВыбора.ПараметрыРасшифровки.ИмяФормы, ЗначениеВыбора.ПараметрыРасшифровки.ПараметрыФормы, ЭтотОбъект);
		
	ИначеЕсли ЗначениеВыбора.Действие = "РасшифровкаСервисаСверки" Тогда
		
		ПараметрыОтчета = Новый Структура;
		ПараметрыОтчета.Вставить("РежимРасшифровки", Истина);
		ПараметрыОтчета.Вставить("Организация",   Объект.Организация);
		ПараметрыОтчета.Вставить("Контрагент",    ЗначениеВыбора.ПараметрыРасшифровки.Контрагент);
		ПараметрыОтчета.Вставить("НачалоПериода", Объект.ДатаНачала);
		ПараметрыОтчета.Вставить("КонецПериода",  Объект.ДатаОкончания);
		
		ОткрытьФорму("Отчет.СверкаРасчетовСКонтрагентами.Форма.ФормаОтчета", ПараметрыОтчета);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = НСтр("ru = 'Отзыв о функциональности ""Монитор сверки с контрагентами""'");
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

&НаСервере
Функция КонтрагентДляОбновленияПослеЗаписиАктаСверки(АктСверки)
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		АктСверки,
		"Организация, Контрагент, ДатаНачала, ДатаОкончания");
	
	Если РеквизитыДокумента.Организация = Объект.Организация
		 И РеквизитыДокумента.ДатаНачала <= Объект.ДатаОкончания
		 И РеквизитыДокумента.ДатаОкончания >= Объект.ДатаНачала Тогда
		Возврат РеквизитыДокумента.Контрагент;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция ПодготовитьСписокДляПечатиНаСервере()
	
	ТабличныйДокумент = Новый ТабличныйДокумент;

	Макет = Обработки.МониторСверкиСКонтрагентами.ПолучитьМакет("ПФ_MXL_СостояниеСверкиСКонтрагентами");
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт; 
	
	ОбластьПечати = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПечати.Параметры.ДатаОкончания = Формат(Объект.ДатаОкончания, "ДЛФ=DD");
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	ОбластьПечати = Макет.ПолучитьОбласть("РеквизитыОрганизации");
	ОбластьПечати.Параметры.ПредставлениеОрганизации = ПредставлениеОрганизации;
	ТабличныйДокумент.Вывести(ОбластьПечати); 
	
	ОбластьПечати = Макет.ПолучитьОбласть("ШапкаТаблицы");
	Если ПользовательУправляетСчетамиУчета Тогда
		ОбластьПечати.Параметры.ОстатокЗаголовок = ЗаголовокНаДату(НСтр("ru='Остаток'"), Объект.ДатаОкончания);
		ОбластьПечати.Параметры.ДебиторскаяЗадолженностьЗаголовок =  НСтр("ru='Дебет'");
		ОбластьПечати.Параметры.КредиторскаяЗадолженностьЗаголовок = НСтр("ru='Кредит'");
	Иначе
		ОбластьПечати.Параметры.ОстатокЗаголовок = ЗаголовокНаДату(НСтр("ru='Долг'"), Объект.ДатаОкончания);
		ОбластьПечати.Параметры.ДебиторскаяЗадолженностьЗаголовок =  НСтр("ru='Контрагента'");
		ОбластьПечати.Параметры.КредиторскаяЗадолженностьЗаголовок = НСтр("ru='Организации'");
	КонецЕсли;
	ТабличныйДокумент.Вывести(ОбластьПечати);
	
	НомерСтроки = 0;
	
	Для Каждого СтрокаСверки Из СверкаСКонтрагентами Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		ОбластьПечати = Макет.ПолучитьОбласть("СтрокаТаблицы");
		
		ОбластьПечати.Параметры.НомерСтроки = НомерСтроки;
		ОбластьПечати.Параметры.Заполнить(СтрокаСверки);
		
		Если Не ЗначениеЗаполнено(СтрокаСверки.СостояниеСверки) Тогда
			ОбластьПечати.Параметры.СостояниеСверки = НСтр("ru='Не выполнялась'");
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьПечати);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции 

&НаСервере
Процедура ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ИдентификаторТекущейСтроки, ИдентификаторЗапроса)
	
	ТекущиеДанные = СверкаСКонтрагентами.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	
	ДанныеДляЗаполнения = Обработки.МониторСверкиСКонтрагентами.НовыйДанныеДляЗаполнения();
	ДанныеДляЗаполнения.ИдентификаторЗапроса = ИдентификаторЗапроса;
	ДанныеДляЗаполнения.ДатаНачала = Объект.ДатаНачала;
	ДанныеДляЗаполнения.ДатаОкончания = Объект.ДатаОкончания;
	ДанныеДляЗаполнения.Организация = Объект.Организация;
	ДанныеДляЗаполнения.ИдентификаторЭДО = ИдентификаторЭДО;
	ДанныеДляЗаполнения.Контрагент = ТекущиеДанные.Контрагент;
	ДанныеДляЗаполнения.КонтактноеЛицо = ТекущиеДанные.КонтактноеЛицо;
	ДанныеДляЗаполнения.ЭДО = ТекущиеДанные.ЭДО;
	ДанныеДляЗаполнения.ОтправлятьПисьмаВФорматеHTML = ОтправлятьПисьмаВФорматеHTML;
	ДанныеДляЗаполнения.НетОригиналовКоличество = ТекущиеДанные.НетОригиналовКоличество;
	ДанныеДляЗаполнения.НетОригиналовКоличествоЭДО = ТекущиеДанные.НетОригиналовКоличествоЭДО;
	ДанныеДляЗаполнения.УникальныйИдентификатор = УникальныйИдентификатор;
	
	Обработки.МониторСверкиСКонтрагентами.ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения);
	
КонецПроцедуры

#КонецОбласти
