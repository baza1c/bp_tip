#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура подготавливает данные для таблиц СверкаСКонтрагентами, СчетаРасчетов в форме состояния сверки
// 
// Параметры:
//    СтруктураПараметров - Структура - Структура с параметрами для заполнения.
//    АдресХранилища - Строка - Адрес во временном хранилище, куда будет помещен результат выполнения процедуры.
//
Процедура ПолучитьДанныеСверкиСКонтрагентами(СтруктураПараметров, АдресХранилища) Экспорт
	
	СтруктураДанныхЗаполнения = Новый Структура;
	СтруктураДанныхЗаполнения.Вставить("Успешно", Ложь);
	
	Если Не ПроверитьОтложенныеРасчеты(СтруктураПараметров) Тогда
		ПоместитьВоВременноеХранилище(СтруктураДанныхЗаполнения, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	СхемаКомпоновкиДанных = СтруктураПараметров.СхемаКомпоновкиДанных;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.ЗагрузитьНастройки(СтруктураПараметров.НастройкиКомпоновкиДанных);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "НачалоПериода", СтруктураПараметров.ДатаНачала);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "КонецПериода", КонецДня(СтруктураПараметров.ДатаОкончания));
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "Период", Новый Граница(КонецДня(СтруктураПараметров.ДатаОкончания), ВидГраницы.Включая));
	
	ДатаПолученияДанныхСервисаСверкиРасчетов = РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов.ДатаАктуальностиРасхождений(
		СтруктураПараметров.Организация, СтруктураПараметров.ДатаНачала, СтруктураПараметров.ДатаОкончания);
		
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "ДатаПолученияДанныхСервисаСверкиРасчетов", ДатаПолученияДанныхСервисаСверкиРасчетов);
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
		КомпоновщикНастроек, "ИспользуетсяСервисСверкиРасчетов", СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов(), ВидСравненияКомпоновкиДанных.Равно);
	
	СтатусыСверки = СтатусыСверки();
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "СтатусыСверкиНеВыполнялась", СтатусыСверки.СверкаНеВыполнялась.Статус);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "СтатусыСверкиНеЗавершена", СтатусыСверки.СверкаНеЗавершена.Статус);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "СтатусыСверкиРасхождения", СтатусыСверки.РасхожденияВСверке.Статус);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "СтатусыСверкиАктСверкиГотовКПодписанию", СтатусыСверки.АктСверкиГотовКПодписанию.Статус);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек, "СтатусыСверкиАктСверкиПодписан", СтатусыСверки.АктСверкиПодписан.Статус);
	
	Если Не СтруктураПараметров.Поставщики Тогда
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, "Поставщики", Ложь, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если Не СтруктураПараметров.Покупатели Тогда
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, "Покупатели", Ложь, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
		КомпоновщикНастроек, "СчетаРасчетовСПоставщиками", СтруктураПараметров.СчетаРасчетовСПоставщиками, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии);
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
		КомпоновщикНастроек, "СчетаРасчетовСПокупателями", СтруктураПараметров.СчетаРасчетовСПокупателями, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии);
	
	Если Не СтруктураПараметров.КонтрагентыСОстаткамиПрошлыхПериодов Тогда
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, "ТолькоКонтрагентыСОборотами", Истина, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если Не СтруктураПараметров.КонтрагентыСРасчетамиБезОстатков Тогда
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, "ТолькоКонтрагентыСОстатками", Истина, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Контрагент") Тогда
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, "Контрагент", СтруктураПараметров.Контрагент, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(СтруктураПараметров, КомпоновщикНастроек);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	НастройкиДляКомпоновкиМакета = КомпоновщикНастроек.ПолучитьНастройки();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
		НастройкиДляКомпоновкиМакета,
		,
		,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
	// Для сохранения данных временных таблиц и удобства дополнительной обработки строк используем обычный запрос.
	// Текст запроса берем из скомпонованного макета СКД.
	Запрос = Новый Запрос;
	Запрос.Текст = МакетКомпоновки.НаборыДанных.НаборДанных1.Запрос;
	
	// Установим сортировку из параметра
	Сортировка = "";
	СтруктураПараметров.Свойство("Сортировка", Сортировка);
	Если ЗначениеЗаполнено(Сортировка) Тогда
		
		ВыраженияСортировки = СтрРазделить(Сортировка, ",", Ложь);
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		ПоследнийЗапросПакета = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество()-1];
		ПорядокВЗапросе = ПоследнийЗапросПакета.Порядок;
		ПорядокВЗапросе.Очистить();
		
		Для Каждого ВыражениеСортировки Из ВыраженияСортировки Цикл
			ПоУбыванию = (СтрНайти(ВыражениеСортировки, " Убыв") <> 0);
			ИмяКолонки = СтрЗаменить(ВыражениеСортировки, " Убыв", "");
			ИмяКолонки = СтрЗаменить(ИмяКолонки, " Возр", "");
			КолонкаСортировки = ПоследнийЗапросПакета.Колонки.Найти(ИмяКолонки);
			ВыражениеПорядка = ПорядокВЗапросе.Добавить(
				Новый ВыражениеСхемыЗапроса(Строка(КолонкаСортировки.Поля[0])));
			ВыражениеПорядка.Направление = ?(
				ПоУбыванию,
				НаправлениеПорядкаСхемыЗапроса.ПоУбыванию,
				НаправлениеПорядкаСхемыЗапроса.ПоВозрастанию);
		КонецЦикла;
		
		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		
	КонецЕсли;
	
	Для Каждого Параметр Из МакетКомпоновки.ЗначенияПараметров Цикл
		Запрос.УстановитьПараметр(Параметр.Имя, Параметр.Значение);
	КонецЦикла;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; // для дальнейшего получения данных временных таблиц
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаСостояниеСверки = Новый ТаблицаЗначений;
	
	Для Каждого КолонкаРезультата Из РезультатЗапроса.Колонки Цикл
		ТаблицаСостояниеСверки.Колонки.Добавить(КолонкаРезультата.Имя, КолонкаРезультата.ТипЗначения);
	КонецЦикла;
	
	// Колонки с текстовой информацией
	ТаблицаСостояниеСверки.Колонки.Добавить("СостояниеСверки",            ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаСостояниеСверки.Колонки.Добавить("НетОригиналовПредставление", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// Заполним текстовые поля построчно и одновременно посчитаем итоги
	ВсегоКонтрагентов = 0;
	ИтогиПоСтатусамСверки = Новый Соответствие; // ключ - числовой статус сверки, значение - количество контрагентов
	ВсегоДЗ = 0;
	ВсегоКЗ = 0;
	ПодтвержденнаяДЗ = 0;
	ПодтвержденнаяКЗ = 0;
	
	Для Каждого ОписаниеСтатуса Из СтатусыСверки Цикл
		ИтогиПоСтатусамСверки.Вставить(ОписаниеСтатуса.Значение.Статус, 0);
	КонецЦикла;
	
	Пока Выборка.Следующий() Цикл
		
		// Чтобы посчитать статистику в общей массе контрагентов, не отбираем по статусам сразу в запросе.
		// Контрагентов, не проходящих по статусу сверки, ниже не добавляем в итоговую таблицу.
		ВсегоКонтрагентов = ВсегоКонтрагентов + 1;
		ИтогиПоСтатусамСверки[Выборка.СтатусСверки] = ИтогиПоСтатусамСверки[Выборка.СтатусСверки] + 1;
		ВсегоДЗ = ВсегоДЗ + Выборка.ДебиторскаяЗадолженность;
		ВсегоКЗ = ВсегоКЗ + Выборка.КредиторскаяЗадолженность;
		Если Выборка.СтатусСверки = СтатусыСверки.АктСверкиПодписан.Статус Тогда
			ПодтвержденнаяДЗ = ПодтвержденнаяДЗ + Выборка.ДебиторскаяЗадолженность;
			ПодтвержденнаяКЗ = ПодтвержденнаяКЗ + Выборка.КредиторскаяЗадолженность;
		КонецЕсли;
		
		Если СтруктураПараметров.ОтбиратьПоСтатусуСверки 
			 И СтруктураПараметров.СтатусыСверки.НайтиПоЗначению(Выборка.СтатусСверки) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаСверки = ТаблицаСостояниеСверки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСверки, Выборка);
		ЗаполнитьПредставлениеПолейСверки(НоваяСтрокаСверки, СтатусыСверки);
		
	КонецЦикла;
	
	// Дополним данные по счетам расчетов.
	// Получим счета верхнего уровня и дополнительную информацию для того, чтобы минимизировать количество
	// расшифровочных отчетов.
	СчетаРасчетов = МенеджерВременныхТаблиц.Таблицы.Найти("ДанныеПоСчетамРасчетов").ПолучитьДанные().Выгрузить();
	СчетаРасчетов.Колонки.Добавить("СчетВерхнегоУровня", Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	КэшПоиска = Новый Соответствие;
	Для Каждого СтрокаСчета Из СчетаРасчетов Цикл
		
		СчетИзКэша = КэшПоиска[СтрокаСчета.Счет];
		Если СчетИзКэша <> Неопределено Тогда
			СтрокаСчета.СчетВерхнегоУровня = СчетИзКэша;
			Продолжить;
		КонецЕсли;
		
		СтрокаСчета.СчетВерхнегоУровня = БухгалтерскийУчетПовтИсп.СчетВерхнегоУровня(СтрокаСчета.Счет);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаРасчетов.Контрагент,
	|	СчетаРасчетов.Счет,
	|	СчетаРасчетов.СчетВерхнегоУровня
	|ПОМЕСТИТЬ СчетаРасчетов
	|ИЗ
	|	&СчетаРасчетов КАК СчетаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаРасчетов.Контрагент КАК Контрагент,
	|	СчетаРасчетов.СчетВерхнегоУровня КАК СчетВерхнегоУровня,
	|	МАКСИМУМ(СчетаРасчетов.Счет) КАК Счет,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СчетаРасчетов.Счет) КАК СчетКоличество
	|ИЗ
	|	СчетаРасчетов КАК СчетаРасчетов
	|СГРУППИРОВАТЬ ПО
	|	СчетаРасчетов.Контрагент,
	|	СчетаРасчетов.СчетВерхнегоУровня";
	
	Запрос.УстановитьПараметр("СчетаРасчетов", СчетаРасчетов);
	
	СчетаРасчетов = Запрос.Выполнить().Выгрузить();
	
	// Заполним возвращаемую структуру
	СтруктураДанныхЗаполнения.Вставить("ТаблицаСостояниеСверки", ТаблицаСостояниеСверки);
	СтруктураДанныхЗаполнения.Вставить("СчетаРасчетов", СчетаРасчетов);
	
	СтруктураДанныхЗаполнения.Вставить("ВсегоКонтрагентов", ВсегоКонтрагентов);
	СтруктураДанныхЗаполнения.Вставить("ИтогиПоСтатусамСверки", Новый ФиксированноеСоответствие(ИтогиПоСтатусамСверки));
	СтруктураДанныхЗаполнения.Вставить("ВсегоДЗ", ВсегоДЗ);
	СтруктураДанныхЗаполнения.Вставить("ВсегоКЗ", ВсегоКЗ);
	СтруктураДанныхЗаполнения.Вставить("ПодтвержденнаяДЗ", ПодтвержденнаяДЗ);
	СтруктураДанныхЗаполнения.Вставить("ПодтвержденнаяКЗ", ПодтвержденнаяКЗ);
	
	СтруктураДанныхЗаполнения.Успешно = Истина;
	
	ПоместитьВоВременноеХранилище(СтруктураДанныхЗаполнения, АдресХранилища);
	
КонецПроцедуры

Функция СчетаРасчетовСПоставщиками() Экспорт

	Счета = Новый Массив;

	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);              // 60
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПоставщикамиИПодрядчиками);       // 76.05
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами);    // 76.09
	Если БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет() Тогда
		Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПоставщикамиИПодрядчикамиВал);    // 76.25
		Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиВал); // 76.29
		Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПоставщикамиИПодрядчикамиУЕ);     // 76.35
		Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ);  // 76.39
	КонецЕсли;

	Возврат Счета;

КонецФункции

Функция СчетаРасчетовСПокупателями() Экспорт

	Счета = Новый Массив;

	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);               // 62
	Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчиками);        // 76.06
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами);    // 76.09
	Если БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет() Тогда
		Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчикамиВал);     // 76.26
		Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиВал); // 76.29
		Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчикамиУЕ);      // 76.36
		Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ);  // 76.39
	КонецЕсли;

	Возврат Счета;

КонецФункции

// Функция - конструктор структуры со статусами сверки по контрагенту. Ключ - строчный ключ статуса,
// значение - структура с числовым значением статуса, представлением и картинкой для вывода пользователю.
// 
// Возвращаемое значение:
//  Структура
//
Функция СтатусыСверки() Экспорт
	
	Статусы = Новый Структура;
	
	Статусы.Вставить("СверкаНеВыполнялась",       ОписаниеСтатусаСверки(0, НСтр("ru='Не выполнялась'"), БиблиотекаКартинок.СветофорСерый));
	Статусы.Вставить("СверкаНеЗавершена",         ОписаниеСтатусаСверки(1, НСтр("ru='Не завершена'"), БиблиотекаКартинок.СветофорЖелтый));
	Статусы.Вставить("РасхожденияВСверке",        ОписаниеСтатусаСверки(2, НСтр("ru='Есть расхождения'"), БиблиотекаКартинок.СветофорКрасный));
	Статусы.Вставить("АктСверкиГотовКПодписанию", ОписаниеСтатусаСверки(3, НСтр("ru='Готово к подписанию акта сверки'"), БиблиотекаКартинок.СветофорЗеленый));
	Статусы.Вставить("АктСверкиПодписан",         ОписаниеСтатусаСверки(4, НСтр("ru='Подписан акт сверки'"), БиблиотекаКартинок.СветофорГалка));
	
	Возврат Статусы;
	
КонецФункции

#Область Расшифровки

Функция СформироватьРасшифровку(ПараметрыОтчета) Экспорт
	
	ИдентификаторРасшифровки = ПараметрыОтчета.ИдентификаторРасшифровки;
	
	СхемаКомпоновкиДанных = ПолучитьМакет(ИдентификаторРасшифровки);
	
	ВариантыНастроек = СхемаКомпоновкиДанных.ВариантыНастроек.Найти("Основной");
	ВариантНастройки = ВариантыНастроек.Настройки;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроек.ЗагрузитьНастройки(ВариантНастройки);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"НачалоПериода", ПараметрыОтчета.НачалоПериода, Истина);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"КонецПериода", ПараметрыОтчета.КонецПериода, Истина);
	УстановитьПараметрыПредставленияДокументов(КомпоновщикНастроек);
	УстановитьПараметрыПредставленияПричиныОтсутствияДокумента(КомпоновщикНастроек);
	
	БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
		КомпоновщикНастроек, "Контрагент", ПараметрыОтчета.Контрагент, ВидСравненияКомпоновкиДанных.Равно);
	
	Если ИдентификаторРасшифровки = "РасшифровкаАктыСверки" Тогда
		
		ДатаПолученияДанныхСервисаСверкиРасчетов = РегистрыСведений.СервисСверкиРасчетовОчередьЗапросов.ДатаАктуальностиРасхождений(
			ПараметрыОтчета.Организация, ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
		
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "ДатаПолученияДанныхСервисаСверкиРасчетов", ДатаПолученияДанныхСервисаСверкиРасчетов);
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, "ИспользуетсяСервисСверкиРасчетов", СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов(), ВидСравненияКомпоновкиДанных.Равно);
		
		Если Не ПараметрыОтчета.Поставщики Тогда
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
				КомпоновщикНастроек, "Поставщики", Ложь, ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
		
		Если Не ПараметрыОтчета.Покупатели Тогда
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
				КомпоновщикНастроек, "Покупатели", Ложь, ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
		
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, "СчетаРасчетовСПоставщиками", ПараметрыОтчета.СчетаРасчетовСПоставщиками, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии);
		
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, "СчетаРасчетовСПокупателями", ПараметрыОтчета.СчетаРасчетовСПокупателями, ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии);
		
	КонецЕсли;
	
	НастройкиДляКомпоновкиМакета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиДляКомпоновкиМакета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет,, ДанныеРасшифровки, Истина);
	
	Результат = Новый ТабличныйДокумент;
	
	ПриВыводеЗаголовкаРасшифровки(ИдентификаторРасшифровки, ПараметрыОтчета, Результат);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// Установим свойства областей гиперссылки
	ОбластьГиперссылки = Результат.НайтиТекст("Подробнее");
	Сч = 0;
	Пока ОбластьГиперссылки <> Неопределено Цикл
		ОбластьГиперссылки.Имя = "ПерейтиВСервисСверки" + Сч;
		ОбластьГиперссылки.Гиперссылка = Истина;
		ОбластьГиперссылки = Результат.НайтиТекст("Подробнее", ОбластьГиперссылки);
		Сч = Сч + 1;
	КонецЦикла;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, ПараметрыОтчета.УникальныйИдентификатор);
	
	РезультатРасшифровка = Новый Структура("Результат, АдресХранилища", Результат, АдресХранилища);
	
	Возврат РезультатРасшифровка;
	
КонецФункции

Функция ПолучитьЗаголовокРасшифровки(ИдентификаторРасшифровки) Экспорт

	Если ИдентификаторРасшифровки = "РасшифровкаНетОригиналов" Тогда
		Возврат НСтр("ru='Документы без оригиналов и с незавершенным ЭДО'");
	ИначеЕсли ИдентификаторРасшифровки = "РасшифровкаАктыСверки" Тогда
		Возврат НСтр("ru='Состояние сверки по контрагенту'");
	КонецЕсли;

КонецФункции

Процедура ПриВыводеЗаголовкаРасшифровки(ИдентификаторРасшифровки, ПараметрыОтчета, Результат) Экспорт
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	ОбластьЗаголовок        = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьОписаниеНастроек = Макет.ПолучитьОбласть("ОписаниеНастроек");
	
	ПредставлениеПериодаОтчета = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		ПараметрыОтчета.НачалоПериода,
		ПараметрыОтчета.КонецПериода,
		Истина);
	ИмяНастроекОтчетаПериод = НСтр("ru='Период: '");
	ТекстОрганизация = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(ПараметрыОтчета.Организация,
		ПараметрыОтчета.ВключатьОбособленныеПодразделения,
		ПараметрыОтчета.КонецПериода);
	ИмяНастроекОтчетаОрганизация = НСтр("ru='Организация: '");
	СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ПараметрыОтчета.Контрагент,
		ПараметрыОтчета.КонецПериода);
	ТекстКонтрагент = СведенияОКонтрагенте.Представление;
	ИмяНастроекОтчетаКонтрагент = НСтр("ru='Контрагент: '");
	
	// Заголовок
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = "" + ПолучитьТекстЗаголовкаРасшифровки(ИдентификаторРасшифровки, ПараметрыОтчета);
	Результат.Вывести(ОбластьЗаголовок);
	
	// Настройки
	ОбластьОписаниеНастроек.Параметры.ИмяНастроекОтчета = ИмяНастроекОтчетаОрганизация;
	ОбластьОписаниеНастроек.Параметры.ОписаниеНастроекОтчета = ТекстОрганизация;
	Результат.Вывести(ОбластьОписаниеНастроек);
	
	ОбластьОписаниеНастроек.Параметры.ИмяНастроекОтчета = ИмяНастроекОтчетаКонтрагент;
	ОбластьОписаниеНастроек.Параметры.ОписаниеНастроекОтчета = ТекстКонтрагент;
	Результат.Вывести(ОбластьОписаниеНастроек);
	
	ОбластьОписаниеНастроек.Параметры.ИмяНастроекОтчета = ИмяНастроекОтчетаПериод;
	ОбластьОписаниеНастроек.Параметры.ОписаниеНастроекОтчета = ПредставлениеПериодаОтчета;
	Результат.Вывести(ОбластьОписаниеНастроек);
	
	Результат.Область("R1:R" + Результат.ВысотаТаблицы).Имя = "Заголовок";
	
КонецПроцедуры

Функция ПолучитьТекстЗаголовкаРасшифровки(ИдентификаторРасшифровки, ПараметрыОтчета)
	
	ПредставлениеПериодаОтчета = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
		ПараметрыОтчета.НачалоПериода,
		ПараметрыОтчета.КонецПериода,
		Ложь);
		
	Если ИдентификаторРасшифровки = "РасшифровкаНетОригиналов" Тогда
		ТекстЗаголовка = НСтр("ru = 'Документы без оригиналов и с незавершенным ЭДО'");
	Иначе
		// РасшифровкаАктыСверки
		ШаблонЗаголовка = НСтр("ru = 'Состояние сверки по %1 %2'");
		ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
			ПараметрыОтчета.Контрагент,
			СокрЛП(ПредставлениеПериодаОтчета));
	КонецЕсли;
		
	Возврат ТекстЗаголовка;
	
КонецФункции

#КонецОбласти

// Процедура заполняет параметры отправки письма на основании данных для заполнения.
// 
// Параметры:
//  ПараметрыОтправкиПисьма - Структура - см. РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма().
//  ДанныеДляЗаполнения - Структура - см. НовыйДанныеДляЗаполнения
// 
Процедура ЗаполнитьПараметрыОтправкиПисьма(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения) Экспорт
	
	Если ДанныеДляЗаполнения.ИдентификаторЗапроса = "ЗапросСверки" Тогда
		ЗаполнитьПараметрыОтправкиПисьмаПоСверке(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения);
	ИначеЕсли ДанныеДляЗаполнения.ИдентификаторЗапроса = "ЗапросОригиналов" Тогда
		ЗаполнитьПараметрыОтправкиПисьмаПоОригиналам(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения);
	ИначеЕсли ДанныеДляЗаполнения.ИдентификаторЗапроса = "ЗапросСверкиИОригиналов" Тогда
		ЗаполнитьПараметрыОтправкиПисьмаПоСверкеИОригиналам(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

// Конструктор параметра ДанныеДляЗаполнения процедуры ЗаполнитьПараметрыОтправкиПисьма
//
// Возвращаемое значение:
//  Структура - структура со свойствами:
//   * ИдентификаторЗапроса - Строка - идентификатор запроса.
//   * ДатаНачала - Дата - дата начала обрабатываемого периода.
//   * ДатаОкончания - Дата - дата окончания обрабатываемого периода.
//   * Организация - СправочникСсылка.Организации - организация, для которой заполняются параметры.
//   * ИдентификаторЭДО - строка - идентификатор ЭДО организации, для которой заполняются параметры.
//   * Контрагент - СправочникСсылка.Контрагенты - контрагент, по которому заполняются параметры.
//   * КонтактноеЛицо - СправочникСсылка.КонтактныеЛица - контактное лицо контрагента.
//   * ЭДО - Число - параметр ЭДО.
//   * ОтправлятьПисьмаВФорматеHTML - Булево - признак отправки писем в формате HTML.
//   * НетОригиналовКоличество - Число - количество документов без оригиналов.
//   * НетОригиналовКоличествоЭДО - Число - количество документов с незавершенным ЭДО.
//   * УникальныйИдентификатор - Строка - уникальный идентификатор.
//
Функция НовыйДанныеДляЗаполнения() Экспорт
	
	ДанныеДляЗаполнения = Новый Структура;
	
	ДанныеДляЗаполнения.Вставить("ИдентификаторЗапроса", "");
	ДанныеДляЗаполнения.Вставить("ДатаНачала", '0001-01-01');
	ДанныеДляЗаполнения.Вставить("ДатаОкончания", '0001-01-01');
	ДанныеДляЗаполнения.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ДанныеДляЗаполнения.Вставить("ИдентификаторЭДО", "");
	ДанныеДляЗаполнения.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	ДанныеДляЗаполнения.Вставить("КонтактноеЛицо", Справочники.КонтактныеЛица.ПустаяСсылка());
	ДанныеДляЗаполнения.Вставить("ЭДО", 0);
	ДанныеДляЗаполнения.Вставить("ОтправлятьПисьмаВФорматеHTML", Ложь);
	ДанныеДляЗаполнения.Вставить("НетОригиналовКоличество", 0);
	ДанныеДляЗаполнения.Вставить("НетОригиналовКоличествоЭДО", 0);
	ДанныеДляЗаполнения.Вставить("УникальныйИдентификатор", "");
	
	Возврат ДанныеДляЗаполнения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПроверитьОтложенныеРасчеты(СтруктураПараметров)

	ДатаОкончания = СтруктураПараметров.ДатаОкончания;
	Если ТипЗнч(ДатаОкончания) = Тип("Граница") Тогда
		ДатаОкончания = ДатаОкончания.Значение;
	КонецЕсли;
	
	ПараметрыРасчета = УчетВзаиморасчетовОтложенноеПроведение.НовыеПараметрыРасчета();
	ЗаполнитьЗначенияСвойств(ПараметрыРасчета, СтруктураПараметров);
	ПараметрыРасчета.ДатаОкончания = ДатаОкончания;
	
	Результат = УчетВзаиморасчетовОтложенноеПроведение.ВыполнитьОтложенныеРасчеты(ПараметрыРасчета);
	
	Возврат (Результат.КоличествоДоговоровСОшибками = 0);

КонецФункции

Функция ОписаниеСтатусаСверки(Статус, Представление, Картинка)
	
	ОписаниеСтатуса = Новый Структура;
	ОписаниеСтатуса.Вставить("Статус",        Статус);
	ОписаниеСтатуса.Вставить("Представление", Представление);
	ОписаниеСтатуса.Вставить("Картинка",      Картинка);
	
	Возврат ОписаниеСтатуса;
	
КонецФункции

Процедура УстановитьПараметрыПредставленияДокументов(КомпоновщикНастроек)
	
	СписокПредставленийДокументов = ОбщегоНазначенияБПКлиентСервер.ПредставленияДокументов();
	Для Каждого Элемент Из СписокПредставленийДокументов Цикл
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
			Элемент.Значение,
			Элемент.Представление,
			Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьПараметрыПредставленияПричиныОтсутствияДокумента(КомпоновщикНастроек)
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ПредставлениеНеЗавершенЭДО", НСтр("ru='Не завершен ЭДО'"), Истина);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек,
		"ПредставлениеНетОригиналовНаБумажномНосителе", НСтр("ru='Нет оригиналов на бумажном носителе'"), Истина);
	
КонецПроцедуры

Функция ШаблонПисьмаНетОригиналов()
	
	ИмяМакета = "ШаблонПисьмаЗапросОригиналов";
	
	ДокументHTML = Обработки.МониторСверкиСКонтрагентами.ПолучитьМакет(ИмяМакета).ПолучитьДокументHTML();
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	
	Возврат ЗаписьHTML.Закрыть();
	
КонецФункции

Функция ШаблонПисьмаЗапросНаСверку()
	
	ИмяМакета = "ШаблонПисьмаЗапросНаСверку_НетЭДО";
	Если НастройкиЭДО.ИспользуетсяОбменЭлектроннымиДокументами() Тогда
		ИмяМакета = "ШаблонПисьмаЗапросНаСверку";
	КонецЕсли;
	
	ДокументHTML = Обработки.МониторСверкиСКонтрагентами.ПолучитьМакет(ИмяМакета).ПолучитьДокументHTML();
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	
	Возврат ЗаписьHTML.Закрыть();
	
КонецФункции

Процедура ЗаполнитьПараметрыОтправкиПисьмаПоСверке(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения)
	
	ИдентификаторЭДО = ДанныеДляЗаполнения.ИдентификаторЭДО;
	Контрагент = ДанныеДляЗаполнения.Контрагент;
	ЭДО = ДанныеДляЗаполнения.ЭДО;
	ОтправлятьПисьмаВФорматеHTML = ДанныеДляЗаполнения.ОтправлятьПисьмаВФорматеHTML;
	
	ПараметрыТекста = ПолучитьПараметрыТекста(ДанныеДляЗаполнения);
	
	Получатели = ОтправкаПочтовыхСообщений.АдресаЭлектроннойПочты(Контрагент);
	
	ПараметрыОтправкиПисьма.Получатель = Получатели;
	ПараметрыОтправкиПисьма.Тема = СтрШаблон(
		НСтр("ru='Сверка расчетов между %1 и %2'"),
		ПараметрыТекста.ПредставлениеОрганизации,
		ПараметрыТекста.ПредставлениеКонтрагента);
	
	ШаблонHTML = ШаблонПисьмаЗапросНаСверку();
	
	ДобавитьПараметрТекстаТребуетсяНастроитьОбменПоЭДО(ПараметрыТекста, ЭДО, ИдентификаторЭДО);
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
		ШаблонHTML,
		ПараметрыТекста);
	
	Если ОтправлятьПисьмаВФорматеHTML Тогда
		ТекстСообщения = Новый Структура("ТекстHTML, СтруктураВложений",
			ТекстСообщения, Новый Структура());
	КонецЕсли;
	
	ПараметрыОтправкиПисьма.Текст = ТекстСообщения;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыОтправкиПисьмаПоОригиналам(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения)
	
	Контрагент = ДанныеДляЗаполнения.Контрагент;
	ОтправлятьПисьмаВФорматеHTML = ДанныеДляЗаполнения.ОтправлятьПисьмаВФорматеHTML;
	НетОригиналовКоличество = ДанныеДляЗаполнения.НетОригиналовКоличество;
	НетОригиналовКоличествоЭДО = ДанныеДляЗаполнения.НетОригиналовКоличествоЭДО;
	
	НетОригиналовКоличествоБумага = НетОригиналовКоличество - НетОригиналовКоличествоЭДО;
	
	ПараметрыТекста = ПолучитьПараметрыТекста(ДанныеДляЗаполнения);
	
	Получатели = ОтправкаПочтовыхСообщений.АдресаЭлектроннойПочты(Контрагент);
	
	ПараметрыОтправкиПисьма.Получатель = Получатели;
	ПараметрыОтправкиПисьма.Тема = СтрШаблон(НСтр("ru='Запрос оригиналов документов для %1 от %2'"),
		ПараметрыТекста.ПредставлениеОрганизации,
		ПараметрыТекста.ПредставлениеКонтрагента);
	
	ШаблонHTML = ШаблонПисьмаНетОригиналов();
	
	Если НетОригиналовКоличествоБумага > 0 И НетОригиналовКоличествоЭДО > 0 Тогда
		ШаблонТекст = СтрШаблон(
			НСтр("ru='Просим передать оригиналы и завершить ЭДО с %1 по документам согласно перечню в приложении.'"),
			ПараметрыТекста.ПредставлениеОрганизации);
	ИначеЕсли НетОригиналовКоличествоЭДО > 0 Тогда
		ШаблонТекст = СтрШаблон(
			НСтр("ru='Просим завершить ЭДО с %1 по документам согласно перечню в приложении.'"),
			ПараметрыТекста.ПредставлениеОрганизации);
	Иначе
		ШаблонТекст = СтрШаблон(
			НСтр("ru='Просим передать оригиналы документов по %1 согласно перечню в приложении.'"),
			ПараметрыТекста.ПредставлениеОрганизации);
	КонецЕсли;
	ПараметрыТекста.Вставить("ШаблонТекст", ШаблонТекст);
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
		ШаблонHTML,
		ПараметрыТекста);
	
	Если ОтправлятьПисьмаВФорматеHTML Тогда
		ТекстСообщения = Новый Структура("ТекстHTML, СтруктураВложений",
			ТекстСообщения, Новый Структура());
	КонецЕсли;
	
	ПараметрыОтправкиПисьма.Текст = ТекстСообщения;
	
	ПараметрыОтправкиПисьма.Вложения = СформироватьВложения(ПараметрыТекста, ДанныеДляЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыОтправкиПисьмаПоСверкеИОригиналам(ПараметрыОтправкиПисьма, ДанныеДляЗаполнения)
	
	ИдентификаторЭДО = ДанныеДляЗаполнения.ИдентификаторЭДО;
	Контрагент = ДанныеДляЗаполнения.Контрагент;
	ЭДО = ДанныеДляЗаполнения.ЭДО;
	ОтправлятьПисьмаВФорматеHTML = ДанныеДляЗаполнения.ОтправлятьПисьмаВФорматеHTML;
	НетОригиналовКоличество = ДанныеДляЗаполнения.НетОригиналовКоличество;
	НетОригиналовКоличествоЭДО = ДанныеДляЗаполнения.НетОригиналовКоличествоЭДО;
	
	НетОригиналовКоличествоБумага = НетОригиналовКоличество - НетОригиналовКоличествоЭДО;
	
	ПараметрыТекста = ПолучитьПараметрыТекста(ДанныеДляЗаполнения);
	
	Получатели = ОтправкаПочтовыхСообщений.АдресаЭлектроннойПочты(Контрагент);
	
	ПараметрыОтправкиПисьма.Получатель = Получатели;
	ПараметрыОтправкиПисьма.Тема = СтрШаблон(
		НСтр("ru='Сверка расчетов между %1 и %2'"),
		ПараметрыТекста.ПредставлениеОрганизации,
		ПараметрыТекста.ПредставлениеКонтрагента);
	
	ШаблонHTML = ШаблонПисьмаЗапросНаСверку();
	
	ДобавитьПараметрТекстаТребуетсяНастроитьОбменПоЭДО(ПараметрыТекста, ЭДО, ИдентификаторЭДО);
	
	Если НетОригиналовКоличествоБумага > 0 И НетОригиналовКоличествоЭДО > 0 Тогда
		ШаблонТекст = СтрШаблон(
			НСтр("ru=', а также передать оригиналы и завершить ЭДО с %1 по документам согласно перечню в приложении'"),
			ПараметрыТекста.ПредставлениеОрганизации);
	ИначеЕсли НетОригиналовКоличествоЭДО > 0 Тогда
		ШаблонТекст = СтрШаблон(
			НСтр("ru=', а также завершить ЭДО с %1 по документам согласно перечню в приложении'"),
			ПараметрыТекста.ПредставлениеОрганизации);
	Иначе
		ШаблонТекст = СтрШаблон(
			НСтр("ru=', а также передать оригиналы документов по %1 согласно перечню в приложении'"),
			ПараметрыТекста.ПредставлениеОрганизации);
	КонецЕсли;
	ПараметрыТекста.Вставить("ЗапросОригиналов", ШаблонТекст);
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
		ШаблонHTML,
		ПараметрыТекста);
	
	Если ОтправлятьПисьмаВФорматеHTML Тогда
		ТекстСообщения = Новый Структура("ТекстHTML, СтруктураВложений",
			ТекстСообщения, Новый Структура());
	КонецЕсли;
	
	ПараметрыОтправкиПисьма.Текст = ТекстСообщения;
	
	ПараметрыОтправкиПисьма.Вложения = СформироватьВложения(ПараметрыТекста, ДанныеДляЗаполнения);
	
КонецПроцедуры

Функция ПолучитьПараметрыРасшифровки(Параметры)
	
	ПараметрыРасшифровки = Новый Структура;
	ПараметрыРасшифровки.Вставить("ИдентификаторРасшифровки", "РасшифровкаНетОригиналов");
	ПараметрыРасшифровки.Вставить("Контрагент", Параметры.Контрагент);
	ПараметрыРасшифровки.Вставить("Организация", Параметры.Организация);
	ПараметрыРасшифровки.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	ПараметрыРасшифровки.Вставить("НачалоПериода", НачалоДня(Параметры.ДатаНачала));
	ПараметрыРасшифровки.Вставить("КонецПериода", КонецДня(Параметры.ДатаОкончания));
	ПараметрыРасшифровки.Вставить("УникальныйИдентификатор", Параметры.УникальныйИдентификатор);
	
	Возврат ПараметрыРасшифровки;
	
КонецФункции

Функция ПолучитьАдресТабличногоДокументаРасшифровки(Знач Параметры)
	
	ПараметрыРасшифровки = ПолучитьПараметрыРасшифровки(Параметры);
	Расшифровка = СформироватьРасшифровку(ПараметрыРасшифровки);
	ТабличныйДокумент = Расшифровка.Результат;
	
	ПотокВПамяти = Новый ПотокВПамяти;
	ТабличныйДокумент.Записать(ПотокВПамяти, ТипФайлаТабличногоДокумента.PDF);
	
	Возврат ПоместитьВоВременноеХранилище(ТабличныйДокумент,
		Параметры.УникальныйИдентификатор);
	
	КонецФункции

Функция СформироватьВложения(ПараметрыТекста, ДанныеДляЗаполнения)
	
	Вложения = Новый Массив;
	ИмяФайла = СтрШаблон(НСтр("ru='Запрос оригиналов для %1 от %2 за период с %3 по %4'"),
		ПараметрыТекста.ПредставлениеОрганизации,
		ПараметрыТекста.ПредставлениеКонтрагента,
		ПараметрыТекста.ДатаНачала,
		ПараметрыТекста.ДатаОкончания);
	ОписаниеВложения = Новый Структура("Представление, АдресВоВременномХранилище",
		ИмяФайла,
		ПолучитьАдресТабличногоДокументаРасшифровки(ДанныеДляЗаполнения));
	Вложения.Добавить(ОписаниеВложения);
	
	Возврат Вложения;
	
КонецФункции

Функция ПолучитьПараметрыТекста(ДанныеДляЗаполнения)
	
	ПараметрыТекста = Новый Структура;
	ДатаНачала = ДанныеДляЗаполнения.ДатаНачала;
	ДатаОкончания = ДанныеДляЗаполнения.ДатаОкончания;
	Организация = ДанныеДляЗаполнения.Организация;
	Контрагент = ДанныеДляЗаполнения.Контрагент;
	КонтактноеЛицо = ДанныеДляЗаполнения.КонтактноеЛицо;
	
	ПредставлениеОрганизации = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Организация,
		Ложь,
		ДатаОкончания);
	СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Контрагент, ДатаОкончания);
	ПредставлениеКонтрагента = СведенияОКонтрагенте.Представление;
	ПредставлениеПолучателя = ?(ЗначениеЗаполнено(КонтактноеЛицо),
		КонтактноеЛицо,
		ПредставлениеКонтрагента);
		
	
	ПараметрыТекста.Вставить("ПредставлениеОрганизации", ПредставлениеОрганизации);
	ПараметрыТекста.Вставить("ПредставлениеКонтрагента", ПредставлениеКонтрагента);
	ПараметрыТекста.Вставить("ДатаНачала", Формат(ДатаНачала, "ДЛФ=D"));
	ПараметрыТекста.Вставить("ДатаОкончания", Формат(ДатаОкончания, "ДЛФ=D"));
	ПараметрыТекста.Вставить("ЗапросОригиналов", "");
	ПараметрыТекста.Вставить("ПредставлениеПолучателя", ПредставлениеПолучателя);
	
	Возврат ПараметрыТекста;
	
КонецФункции

Процедура ДобавитьПараметрТекстаТребуетсяНастроитьОбменПоЭДО(ПараметрыТекста, ЭДО, ИдентификаторЭДО)
	
	Если ЭДО <> 2 Тогда
		Если ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
			ПараметрыТекста.Вставить("ТребуетсяНастроитьОбменПоЭДО",
				СтрЗаменить(СтрШаблон(НСтр("ru='При этом требуется настроить обмен по ЭДО с нашей организацией
					| (наш идентификатор ЭДО - %1).'"), ИдентификаторЭДО), Символы.ПС, " "));
		Иначе
			ПараметрыТекста.Вставить("ТребуетсяНастроитьОбменПоЭДО",
				НСтр("ru='При этом требуется настроить обмен по ЭДО с нашей организацией.'"));
		КонецЕсли;
	Иначе
		ПараметрыТекста.Вставить("ТребуетсяНастроитьОбменПоЭДО", "");
	КонецЕсли;
	
КонецПроцедуры

#Область ЗаполнениеТаблиц

Процедура ЗаполнитьПредставлениеПолейСверки(СтрокаСверки, СтатусыСверки)
	
	ПредставлениеСверки = Новый Массив;
	ПроверитьРезультатСервисаСверки = Ложь;
	
	КэшАктСверкиКоличествоТекст = Новый Соответствие; // ключ - количество актов, значение - согласованное строчное представление
	КэшНетОригиналовКоличествоТекст = Новый Соответствие; // ключ - количество документов, значение - согласованное строчное представление
	
	Если СтрокаСверки.ТребуетсяАктСверки И СтрокаСверки.АктСверкиКоличество > 0 Тогда
		// Акт(-ы) есть, но он(-и) не покрывает(-ют) все операции (т.е. неактуальный(-ые))
		ПредставлениеСверки.Добавить(НСтр("ru='сверка неполная'"));
		ПроверитьРезультатСервисаСверки = Истина;
		
	ИначеЕсли Не СтрокаСверки.ТребуетсяАктСверки И СтрокаСверки.АктСверкиКоличество = 1 Тогда
		// В периоде есть один акт сверки, и больше не требуется
		// (оформлен по всем договорам сразу или по единственному, нет операций после периода акта сверки).
		// Описываем состояние сверки.
		Если СтрокаСверки.АктСверкиВсеПодписаны Тогда
			СтатусАктаСтрокой = НСтр("ru='подписан акт сверки'");
		ИначеЕсли СтрокаСверки.АктСверкиЕстьРасхождения Тогда
			СтатусАктаСтрокой = НСтр("ru='есть расхождения'");
		ИначеЕсли СтрокаСверки.АктСверкиВсеСошлись Тогда
			СтатусАктаСтрокой = НСтр("ru='можно подписывать акт сверки'");
		ИначеЕсли СтрокаСверки.АктСверкиЭлектронный
			 И СтрокаСверки.АктСверкиСостояниеЭДО = Перечисления.СостоянияДокументовЭДО.ОжидаетсяИзвещениеОПолучении Тогда
			СтатусАктаСтрокой = НСтр("ru='ожидается ответ контрагента'");
		Иначе
			СтатусАктаСтрокой = НСтр("ru='сверка не завершена'");
			ПроверитьРезультатСервисаСверки = Истина;
		КонецЕсли;
		
		ПредставлениеСверки.Добавить(СтатусАктаСтрокой);
		
	ИначеЕсли Не СтрокаСверки.ТребуетсяАктСверки И СтрокаСверки.АктСверкиКоличество > 1 Тогда
		// В периоде несколько актов сверки, и больше не требуется.
		// Описываем состояние сверки.
		Если СтрокаСверки.АктСверкиВсеПодписаны Тогда
			СтатусАктовСтрокой = НСтр("ru='подписаны'");
		ИначеЕсли СтрокаСверки.АктСверкиЕстьРасхождения Тогда
			СтатусАктовСтрокой = НСтр("ru='есть расхождения'");
		ИначеЕсли СтрокаСверки.АктСверкиВсеСошлись Тогда
			СтатусАктовСтрокой = НСтр("ru='можно подписывать'");
		Иначе
			СтатусАктовСтрокой = НСтр("ru='сверка не завершена'");
			ПроверитьРезультатСервисаСверки = Истина;
		КонецЕсли;
		
		АктСверкиКоличествоПредставление = КэшАктСверкиКоличествоТекст.Получить(СтрокаСверки.АктСверкиКоличество);
		Если АктСверкиКоличествоПредставление = Неопределено Тогда
			АктСверкиКоличествоПредставление = ПолучитьСклоненияСтрокиПоЧислу(
				"акт сверки",
				СтрокаСверки.АктСверкиКоличество,
				"",
				"Л= ru_RU; ЧС=Количественное",
				"ПЧ=Число")[0];
			КэшАктСверкиКоличествоТекст.Вставить(СтрокаСверки.АктСверкиКоличество, АктСверкиКоличествоПредставление);
		КонецЕсли;
		
		ПредставлениеСверки.Добавить(СтрШаблон(НСтр("ru='%1, %2'"), АктСверкиКоличествоПредставление, СтатусАктовСтрокой));
		
	Иначе
		
		ПроверитьРезультатСервисаСверки = Истина;
		
	КонецЕсли;
	
	Если ПроверитьРезультатСервисаСверки
		 И СтрокаСверки.СервисСверкиИспользуется
		 И СтрокаСверки.СервисСверкиДанныеАктуальны Тогда
		 
		Если СтрокаСверки.АктСверкиКоличество = 0 Тогда
			 ПредставлениеСверки.Добавить(?(СтрокаСверки.СервисСверкиЕстьРасхождения,
				НСтр("ru='обнаружены расхождения'"),
				СтатусыСверки.АктСверкиГотовКПодписанию.Представление));
		Иначе
			 ПредставлениеСверки.Добавить(?(СтрокаСверки.СервисСверкиЕстьРасхождения,
				НСтр("ru='вероятны расхождения'"),
				НСтр("ru='расхождений не ожидается'")));
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокаСверки.СостояниеСверки = СтрСоединить(ПредставлениеСверки, ", ");
	
	// Первая буква - заглавная
	СтрокаСверки.СостояниеСверки = ВРег(Лев(СтрокаСверки.СостояниеСверки, 1)) + Сред(СтрокаСверки.СостояниеСверки, 2);
	
	Если СтрокаСверки.НетОригиналовКоличество > 0 Тогда
		НетОригиналовПредставление = КэшНетОригиналовКоличествоТекст.Получить(СтрокаСверки.НетОригиналовКоличество);
		Если НетОригиналовПредставление = Неопределено Тогда
			НетОригиналовПредставление = ПолучитьСклоненияСтрокиПоЧислу(
				"документ",
				СтрокаСверки.НетОригиналовКоличество,
				"",
				"Л= ru_RU; ЧС=Количественное",
				"ПЧ=Число")[0];
			КэшНетОригиналовКоличествоТекст.Вставить(СтрокаСверки.НетОригиналовКоличество, НетОригиналовПредставление);
		КонецЕсли;
		СтрокаСверки.НетОригиналовПредставление = НетОригиналовПредставление;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли