#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Конструктор таблицы, описывающей параметры задач, необходимые для расчета статуса задачи.
//
Функция НовыеПараметрыЗадач() Экспорт
	
	ПараметрыЗадач = РегистрыСведений.ЗадачиБухгалтера.КлючиЗадач();
	
	ПараметрыЗадач.Колонки.Добавить("Срок",   ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ПараметрыЗадач.Колонки.Добавить("Статус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	ПараметрыЗадач.Колонки.Добавить("СтатусУстановленВручную", Новый ОписаниеТипов("Булево"));
	ПараметрыЗадач.Колонки.Добавить("РучнойСтатус", РегистрыСведений.ЗадачиБухгалтера.ТипСтатуса());
	
	Возврат ПараметрыЗадач;
	
КонецФункции

// Заполняет в переданной таблице ПараметрыЗадач колонку Статус.
//
// Параметры:
//  ПараметрыЗадач - ТаблицаЗначений - Состав колонок см. Обработки.ПомощникУплатыНалога.НовыеПараметрыЗадачи()
//
Процедура ЗаполнитьСтатусыЗадач(ПараметрыЗадач) Экспорт
	
	Для Каждого ПараметрыЗадачи Из ПараметрыЗадач Цикл
		
		ПараметрыЗадачи.Статус = СтатусУплаты(ПараметрыЗадачи);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает структуру с данными по патенту для оплаты налога
//
// Параметры:
//  ДокументПатент  - ДокументСсылка.ОперацияСПатентом - Патент, для которого нужно получить данные.
//  СрокПлатежа     - Дата - Срок оплаты налога по патенту.
// 
// Возвращаемое значение:
//   Структура - Поля описаны в функции НовыйПоказателиПатента()
//
Функция ПоказателиПатента(ДокументПатент, СрокПлатежа) Экспорт
	
	Показатели = НовыйПоказателиПатента();
	
	ДанныеПатента = СведенияОПатенте(ДокументПатент);
	
	Показатели.Организация = ДанныеПатента.Организация;
	Показатели.РегистрацияВНалоговомОргане = ДанныеПатента.РегистрацияВНалоговомОргане;
	Показатели.НаименованиеИФНС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ДанныеПатента.РегистрацияВНалоговомОргане, "Наименование");
	
	Показатели.ДатаНачала = ДанныеПатента.ДатаНачала;
	Показатели.ДатаОкончания = ДанныеПатента.ДатаОкончания;
	Показатели.ПотенциальноВозможныйГодовойДоход = ДанныеПатента.ПотенциальноВозможныйГодовойДоход;
	Показатели.КБК = ДанныеПатента.КБК;
	Показатели.ДваПлатежа = ДанныеПатента.ДваПлатежа;
	
	Налог = Справочники.ВидыНалоговИПлатежейВБюджет.НалогПоКБК(Показатели.КБК);
	Показатели.ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Налог, "ВидНалога");
	
	ПредыдущийПлатеж = 0;
	ТекущийПлатеж    = 0;
	
	ПредыдущийПлатежОписание = "";
	ТекущийПлатежОписание    = "";
	
	ФорматДат = "ДФ=dd.MM.yyyy";
	
	Если ДанныеПатента.ДваПлатежа Тогда
		// Два платежа
		Если ДанныеПатента.ДатаПервогоПлатежа >= СрокПлатежа Тогда
			ТекущийПлатеж = ДанныеПатента.СуммаПервогоПлатежа;
			ТекущийПлатежОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '1/3 стоимости до %1:'"), Формат(ДанныеПатента.ДатаПервогоПлатежа, ФорматДат));
			СрокПлатежа = ДанныеПатента.ДатаПервогоПлатежа;
		Иначе
			ПредыдущийПлатеж = ДанныеПатента.СуммаПервогоПлатежа;
			ПредыдущийПлатежОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '1/3 стоимости до %1:'"), Формат(ДанныеПатента.ДатаПервогоПлатежа, ФорматДат));
			
			ТекущийПлатеж = ДанныеПатента.СуммаВторогоПлатежа;
			ТекущийПлатежОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '2/3 стоимости до %1:'"), Формат(ДанныеПатента.ДатаВторогоПлатежа, ФорматДат));
			СрокПлатежа = ДанныеПатента.ДатаВторогоПлатежа;
		КонецЕсли;
	Иначе
		// Один платеж
		ТекущийПлатеж = ДанныеПатента.СуммаПервогоПлатежа;
		ТекущийПлатежОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Стоимость патента до %1:'"), Формат(ДанныеПатента.ДатаПервогоПлатежа, ФорматДат));
	КонецЕсли;
	
	Показатели.ТекущийПлатеж            = ТекущийПлатеж;
	Показатели.ПредыдущийПлатеж         = ПредыдущийПлатеж;
	Показатели.ТекущийПлатежОписание    = ТекущийПлатежОписание;
	Показатели.ПредыдущийПлатежОписание = ПредыдущийПлатежОписание;
	
	Если ДанныеПатента.ДваПлатежа Тогда
		Если СрокПлатежа = ДанныеПатента.ДатаПервогоПлатежа Тогда
			ВидПлатежа = Перечисления.ВидыПлатежейПоПатенту.ПервыйПлатеж;
		Иначе
			ВидПлатежа = Перечисления.ВидыПлатежейПоПатенту.ВторойПлатеж;
		КонецЕсли;
	Иначе
		ВидПлатежа = Перечисления.ВидыПлатежейПоПатенту.ЕдинственныйПлатеж;
	КонецЕсли;
	
	Показатели.ВидПлатежа = ВидПлатежа;
	
	// Налоговые суммы рассчитываем в обратном порядке - от причитающихся к уплате к нормативным.
	Показатели.НалогПоПатентуКУплате     = Показатели.ТекущийПлатеж + Показатели.ПредыдущийПлатеж;
	Показатели.СуммаОсвобожденияОтНалога = ДанныеПатента.СуммаОсвобожденияОтНалога;
	Показатели.НалогПоПатенту            = Показатели.НалогПоПатентуКУплате + Показатели.СуммаОсвобожденияОтНалога;
	Показатели.НалогПоПатентуВсего       = ДанныеПатента.СуммаПервогоПлатежа + ДанныеПатента.СуммаВторогоПлатежа;
	
	Возврат Показатели;
	
КонецФункции

Функция ПоказателиРасходовККМУменьшающихНалог(Организация, РегистрацияВНалоговомОргане, Патент, ВидПлатежа, Дата = Неопределено) Экспорт
	
	ПоказателиРасходов = НовыйПоказателиРасходовККМУменьшающихНалог();
	
	РасходыВсего = РегистрыСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН.ЗарегистрированныеРасходы(
		Организация, РегистрацияВНалоговомОргане, Дата);
	
	ПоказателиРасходов.РасходыНаОнлайнКассыВсего          = РасходыВсего.Сумма;
	ПоказателиРасходов.РасходыНаОнлайнКассыКоличествоКасс = РасходыВсего.КоличествоКасс;
	
	Если ПоказателиРасходов.РасходыНаОнлайнКассыВсего = 0 Тогда
		// Не имеет смысла запрашивать распределение нулевой суммы.
		Возврат ПоказателиРасходов;
	КонецЕсли;
	
	РасходыПоПатентам = РегистрыСведений.РасходыНаОнлайнКассыУменьшающиеНалогПСН.УменьшениеПлатежейПоПатентам(
		Организация, РегистрацияВНалоговомОргане);
	
	НалоговыйВычетВсего = РасходыПоПатентам.Итог("НалоговыйВычет");
	
	ОтборПатент = Новый Структура("Патент", Патент);
	РасходыПоПатенту = РасходыПоПатентам.Скопировать(ОтборПатент);
	
	НалоговыйВычетТекущийПатент   = РасходыПоПатенту.Итог("НалоговыйВычет");
	НалоговыйВычетСледующийПлатеж = 0;
	
	Если ВидПлатежа = Перечисления.ВидыПлатежейПоПатенту.ПервыйПлатеж Тогда
		// Отдельно передадим уменьшение первого платежа
		ОтборВидПлатежа = Новый Структура("ВидПлатежа", Перечисления.ВидыПлатежейПоПатенту.ПервыйПлатеж);
		НалоговыйВычетТекущийПлатеж = РасходыПоПатенту.Скопировать(ОтборВидПлатежа, "НалоговыйВычет").Итог("НалоговыйВычет");
		НалоговыйВычетСледующийПлатеж = НалоговыйВычетТекущийПатент - НалоговыйВычетТекущийПлатеж;
	Иначе
		// Вычет по второму платежу или по оплате "короткого" патента показывается для всей стоимости патента.
		НалоговыйВычетТекущийПлатеж = НалоговыйВычетТекущийПатент;
	КонецЕсли;
	
	ПоказателиРасходов.НалоговыйВычетТекущийПатент   = НалоговыйВычетТекущийПатент;
	ПоказателиРасходов.НалоговыйВычетТекущийПлатеж   = НалоговыйВычетТекущийПлатеж;
	ПоказателиРасходов.НалоговыйВычетСледующийПлатеж = НалоговыйВычетСледующийПлатеж;
	ПоказателиРасходов.НалоговыйВычетДругиеПатенты   = НалоговыйВычетВсего - НалоговыйВычетТекущийПатент;
	ПоказателиРасходов.НалоговыйВычетОстаток         = Макс(РасходыВсего.Сумма - НалоговыйВычетВсего, 0);
	
	Возврат ПоказателиРасходов;
	
КонецФункции

Функция ПоказателиРасходовСтраховыеВзносы(Организация, ДокументПатент, ДатаНачала, ДатаОкончания, Срок) Экспорт
	
	ПоказателиРасходов = НовыйПоказателиРасходовСтраховыеВзносы();
	
	СведенияУменьшениеНалога = РегистрыСведений.УменьшениеНалогаПоПатенту.СведенияУменьшениеНалогаЗаГод(
		Организация, ДатаНачала);
	
	Если УчетСтраховыхВзносовИППовтИсп.УпрощенныйЗачетФиксированныхВзносов(Организация, Срок) Тогда
		// Можно уменьшить патент на взносы, подлежащие уплате
		Если КонецКвартала(Срок) = КонецГода(Срок) Тогда
			Периодичность = Перечисления.Периодичность.Год;
		Иначе
			Периодичность = Перечисления.Периодичность.Квартал;
		КонецЕсли;
		ДоляДоходовПоПатенту = УчетПСН.ДоляДоходовПатентнойСистемыНалогообложения(
			Организация,
			Срок,
			Периодичность);
		ПоказателиРасходов.ДоляДоходовПоПатенту = 100 * ДоляДоходовПоПатенту;
		ПоказателиРасходов.ВзносПоЕдиномуТарифу = УчетСтраховыхВзносовИП.ВзносыПоЕдиномуТарифуПодлежащиеУплате(
			Организация,
			Срок);
		ПоказателиРасходов.ВзносСДоходов = УчетСтраховыхВзносовИП.ВзносыСДоходовПодлежащиеУплате(Организация, Срок).Всего;
		
		РасходыНаСтраховыеВзносы = РегистрыНакопления.РасходыУменьшающиеНалогПоОтдельнымРежимам.РасходыПСНЗаПериод(
			Организация, ДатаНачала, ДатаОкончания);
		
		ЗачтеноПоЗаявлениям = Цел(УчетСтраховыхВзносовИП.ЗарезервированныеСуммыВзносовЗаНалоговыйПериод(
			Организация, ДатаНачала, ДатаОкончания, Истина));
		
		ПоказателиРасходов.УплаченныеСтраховыеВзносы = Макс(0, РасходыНаСтраховыеВзносы - ЗачтеноПоЗаявлениям);
		
		УчтенныеВзносыПодлежащиеУплате = УчетСтраховыхВзносовИП.СкорректированнаяСуммаВзносовПодлежащихУплате(
			Организация,
			Срок,
			Истина);
		
		Если УчтенныеВзносыПодлежащиеУплате <> Неопределено Тогда
			ПоказателиРасходов.ВсегоРасходыСтраховыеВзносы = Цел(УчтенныеВзносыПодлежащиеУплате 
				+ ПоказателиРасходов.УплаченныеСтраховыеВзносы);
		Иначе
			ПоказателиРасходов.ВсегоРасходыСтраховыеВзносы = Цел(ДоляДоходовПоПатенту * (ПоказателиРасходов.ВзносПоЕдиномуТарифу
				+ ПоказателиРасходов.ВзносСДоходов) + ПоказателиРасходов.УплаченныеСтраховыеВзносы);
		КонецЕсли;
		
		// Доля по ПСН может изменяться. Нужно контролировать, чтобы не израсходовать больше, чем есть взносов
		ИспользованныеВзносы = 
			УчетСтраховыхВзносовИП.ВзносыПодлежащиеУплатеИспользованныеДляУменьшенияНалога(Организация, Срок);
		НеиспользованныйОстаток = Макс(
			(ПоказателиРасходов.ВзносПоЕдиномуТарифу + ПоказателиРасходов.ВзносСДоходов) - ИспользованныеВзносы.Итого, 0);
		ПоказателиРасходов.ВсегоРасходыСтраховыеВзносы =
			Мин(ИспользованныеВзносы.ПСН + НеиспользованныйОстаток, ПоказателиРасходов.ВсегоРасходыСтраховыеВзносы);
			
		ПоказателиРасходов.ВсегоРасходыСтраховыеВзносы =
			ПоказателиРасходов.ВсегоРасходыСтраховыеВзносы + ПоказателиРасходов.УплаченныеСтраховыеВзносы;
		
	Иначе
		ПоказателиРасходов.ВсегоРасходыСтраховыеВзносы =
			Цел(РегистрыНакопления.РасходыУменьшающиеНалогПоОтдельнымРежимам.РасходыПСНЗаПериод(
				Организация, ДатаНачала, ДатаОкончания));
	КонецЕсли;
	
	УменьшениеПоВсемПатентам = 0;
	ПринятоУменьшениеТекущийПатент = 0;
	ПоданоУменьшениеТекущийПатент = 0;
	
	Для Каждого Элемент Из СведенияУменьшениеНалога Цикл
		УменьшениеПоВсемПатентам = УменьшениеПоВсемПатентам + Элемент.Значение.Сумма;
		Если Элемент.Ключ = ДокументПатент Тогда
			ПоданоУменьшениеТекущийПатент = Элемент.Значение.Сумма;
			Для Каждого ЭлементУведомление Из Элемент.Значение.Уведомления Цикл
				Строка = ПоказателиРасходов.СведенияОбУведомлениях.Добавить();
				ЗаполнитьЗначенияСвойств(Строка, ЭлементУведомление);
				Строка.Ссылка = ЭлементУведомление.Уведомление;
				СтатусУведомления = ИнтерфейсыВзаимодействияБРО.СостояниеДокумента(Строка.Ссылка);
				Если СтатусУведомления.Сдано Тогда
					ПринятоУменьшениеТекущийПатент = ПринятоУменьшениеТекущийПатент + Строка.Сумма;
				КонецЕсли;
				Строка.Сдано = СтатусУведомления.Сдано;
				Строка.СтатусПредставление = СтатусУведомления.Представление;
				Строка.ПредставлениеДокумента = СтрШаблон(НСтр("ru = 'Уведомление от %1 на сумму %2'"),
					Формат(Строка.Дата, "ДЛФ=D"), Строка.Сумма);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	СвободныйОстатокРасходов = Макс(ПоказателиРасходов.ВсегоРасходыСтраховыеВзносы - УменьшениеПоВсемПатентам, 0);
	
	РеквизитыПатента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДокументПатент, "СуммаПервогоПлатежа, СуммаВторогоПлатежа");
	ПолнаяСтоимостьПатента = РеквизитыПатента.СуммаПервогоПлатежа + РеквизитыПатента.СуммаВторогоПлатежа;
	ИспользуетсяТрудНаемныхРаботников = УчетЗарплаты.ИПИспользуетТрудНаемныхРаботниковЗаПериод(
		Организация, ДатаНачала, ДатаОкончания);
	
	// Если все расходы отнесены на основную систему, значит работников на ПСН нет
	// Минфин России письмо от 25.02.2021 № 03-11-11/13087
	Если ИспользуетсяТрудНаемныхРаботников Тогда
		ЕстьВзносыЗаРаботников = УчетПСН.РаботникиЗадействованыВДеятельностиНаПСН(Организация, ДатаНачала, ДатаОкончания);
	Иначе
		ЕстьВзносыЗаРаботников = Ложь;
	КонецЕсли;
	
	Если ИспользуетсяТрудНаемныхРаботников И ЕстьВзносыЗаРаботников Тогда
		ЛимитУменьшенияНалога = Цел(ПолнаяСтоимостьПатента / 2);
	Иначе
		ЛимитУменьшенияНалога = ПолнаяСтоимостьПатента;
	КонецЕсли;
	
	ОставшийсяЛимитУменьшенияНалога = Макс(ЛимитУменьшенияНалога - ПоданоУменьшениеТекущийПатент, 0);
	
	ПоказателиРасходов.УменьшениеСтраховыеВзносыТекущийПатент = ПринятоУменьшениеТекущийПатент;
	ПоказателиРасходов.ОстатокРасходыСтраховыеВзносы = Цел(Мин(СвободныйОстатокРасходов, ОставшийсяЛимитУменьшенияНалога));
	
	Возврат ПоказателиРасходов;
	
КонецФункции

Функция ПоказателиУведомления(Организация, РегистрацияВНалоговомОргане, Патент, ВидПлатежа) Экспорт
	
	Результат = НовыйПоказателиУведомления();
	
	ДанныеУведомления = Отчеты.РегламентированноеУведомлениеУменьшениеНалогаККТ.ДанныеПоследнегоУведомления(
		Организация, РегистрацияВНалоговомОргане);
	
	Результат.ЕстьУведомление = ДанныеУведомления <> Неопределено;
	
	Если Не Результат.ЕстьУведомление Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.Уведомление = ДанныеУведомления.Ссылка;
	
	Результат.УведомлениеПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 от %2'"),
			НСтр("ru = 'Уведомление об уменьшении налога'"),
			Формат(ДанныеУведомления.Дата, "ДФ=dd.MM.yyyy"));
	
	Результат.УведомлениеДата = ДанныеУведомления.Дата;
	
	РеквизитыПатента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Патент, "ДатаВыдачи, НомерПатента");
	
	НалоговыйВычетВсего         = 0;
	НалоговыйВычетТекущийПатент = 0;
	НалоговыйВычетТекущийПлатеж = 0;
	
	Для Каждого ЛистБ Из ДанныеУведомления.ДанныеЛистовБ Цикл
		
		НалоговыйВычетВсего = НалоговыйВычетВсего + ЛистБ.Стр160 + ЛистБ.Стр180 + ЛистБ.Стр200;
		
		Если СокрЛП(ЛистБ.Стр120) = СокрЛП(РеквизитыПатента.НомерПатента)
			И ЛистБ.Стр130 = РеквизитыПатента.ДатаВыдачи Тогда
			
			НалоговыйВычетТекущийПатент = НалоговыйВычетТекущийПатент + ЛистБ.Стр160 + ЛистБ.Стр180 + ЛистБ.Стр200;
			
			Если ВидПлатежа = Перечисления.ВидыПлатежейПоПатенту.ПервыйПлатеж Тогда
				НалоговыйВычетТекущийПлатеж = НалоговыйВычетТекущийПлатеж + ЛистБ.Стр180;
			ИначеЕсли ВидПлатежа = Перечисления.ВидыПлатежейПоПатенту.ВторойПлатеж Тогда
				// Нарастающий итог: платеж 2/3 + платеж 1/3.
				НалоговыйВычетТекущийПлатеж = НалоговыйВычетТекущийПлатеж + ЛистБ.Стр200 + ЛистБ.Стр180;
			Иначе
				// Единственный платеж.
				НалоговыйВычетТекущийПлатеж = НалоговыйВычетТекущийПлатеж + ЛистБ.Стр160;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	НалоговыйВычетДругиеПатенты = НалоговыйВычетВсего - НалоговыйВычетТекущийПатент;
	
	Результат.НалоговыйВычетТекущийПатент = НалоговыйВычетТекущийПатент;
	Результат.НалоговыйВычетТекущийПлатеж = НалоговыйВычетТекущийПлатеж;
	Результат.НалоговыйВычетДругиеПатенты = НалоговыйВычетДругиеПатенты;
	
	Результат.РасходыНаОнлайнКассыВсего = ДанныеУведомления.ДанныеЛистовА.Итог("СумРасхККТ");
	Результат.НалоговыйВычетОстаток     = Макс(Результат.РасходыНаОнлайнКассыВсего - НалоговыйВычетВсего, 0);
	
	Если НалоговыйВычетТекущийПатент = 0 И Результат.НалоговыйВычетОстаток = 0 Тогда
		Результат.ЕстьУведомление = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СравниваемыеПоказателиУведомления() Экспорт
	
	ИменаПоказателей = Новый Массив;
	
	ИменаПоказателей.Добавить("РасходыНаОнлайнКассыВсего");
	ИменаПоказателей.Добавить("НалоговыйВычетТекущийПатент");
	ИменаПоказателей.Добавить("НалоговыйВычетТекущийПлатеж");
	ИменаПоказателей.Добавить("НалоговыйВычетДругиеПатенты");
	ИменаПоказателей.Добавить("НалоговыйВычетОстаток");
	
	Возврат ИменаПоказателей;
	
КонецФункции

Функция ДокументыУплатыПатента(Организация, Патент, ПериодСобытия, ВидНалога) Экспорт
	
	ПараметрыУплатыНалогов = ПомощникиПоУплатеНалоговИВзносов.НовыеПараметрыУплатыНалогов();
	
	ПараметрыУплатыНалогов.Правило = Патент;
	
	ПараметрыУплатыНалогов.ВидыНалогов.Добавить(ВидНалога);
	ПараметрыУплатыНалогов.ВидыНалогов.Добавить(Перечисления.ВидыНалогов.ЕдиныйНалоговыйПлатеж);
	ПараметрыУплатыНалогов.ВидыНалоговыхОбязательств.Добавить(Перечисления.ВидыПлатежейВГосБюджет.Налог);
	
	НачалоПериода = НачалоГода(ПериодСобытия);
	КонецПериода  = КонецДня(ПериодСобытия);
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.ДокументыУплатыНалогов(Организация,
		НачалоПериода, КонецПериода, ПараметрыУплатыНалогов);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СведенияОПатенте(ДокументПатент)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументПатент", ДокументПатент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОперацияСПатентом.Патент КАК Патент,
	|	ОперацияСПатентом.Ссылка КАК ДокументПатент,
	|	ОперацияСПатентом.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ОперацияСПатентом.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|			ТОГДА ОперацияСПатентом.НалоговыйОрган
	|		ИНАЧЕ ОперацияСПатентом.Организация.РегистрацияВНалоговомОргане
	|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
	|	ОперацияСПатентом.КБК КАК КБК,
	|	ОперацияСПатентом.ПотенциальноВозможныйГодовойДоход КАК ПотенциальноВозможныйГодовойДоход,
	|	ОперацияСПатентом.ДатаНачала КАК ДатаНачала,
	|	ОперацияСПатентом.ДатаОкончания КАК ДатаОкончания,
	|	ОперацияСПатентом.СуммаПервогоПлатежа КАК СуммаПервогоПлатежа,
	|	ОперацияСПатентом.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	ОперацияСПатентом.СуммаВторогоПлатежа КАК СуммаВторогоПлатежа,
	|	ОперацияСПатентом.ДатаВторогоПлатежа КАК ДатаВторогоПлатежа,
	|	ВЫБОР
	|		КОГДА ОперацияСПатентом.СуммаВторогоПлатежа > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДваПлатежа,
	|	ОперацияСПатентом.СуммаОсвобожденияОтНалога КАК СуммаОсвобожденияОтНалога
	|ИЗ
	|	Документ.ОперацияСПатентом КАК ОперацияСПатентом
	|ГДЕ
	|	ОперацияСПатентом.Ссылка = &ДокументПатент";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ДанныеПатента = Новый Структура;
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ДанныеПатента.Вставить(Колонка.Имя, Выборка[Колонка.Имя]);
	КонецЦикла;
	
	Возврат ДанныеПатента;
	
КонецФункции

Функция СтатусУплаты(ПараметрыЗадачи)
	
	Если ПараметрыЗадачи.СтатусУстановленВручную Тогда
		Возврат ПараметрыЗадачи.РучнойСтатус;
	КонецЕсли;
	
	Организация   = ПараметрыЗадачи.Организация;
	ПериодСобытия = ПараметрыЗадачи.ПериодСобытия;
	Патент        = ПараметрыЗадачи.Правило;
	Срок          = ПараметрыЗадачи.Срок;
	
	// В задачах организации в качестве правила уплаты патентов используется ссылка на деятельность по патенту,
	// но сведения по оплате необходимо получить по конкретному патенту.
	// Поэтому необходимо получить ссылку на документ-патент.
	// Определять документ-патент будем по виду деятельности патента и сроку оплаты.
	ДокументПатент = УчетПСН.ДокументПатентПоДеятельностиИСроку(Патент, Срок);
	
	Если Не ЗначениеЗаполнено(ДокументПатент) Тогда
		Возврат "";
	КонецЕсли;
	
	ПоказателиПатента = ПоказателиПатента(ДокументПатент, Срок);
	
	СуммаПлатежаПоЗадаче = ПоказателиПатента.ТекущийПлатеж + ПоказателиПатента.ПредыдущийПлатеж;
	
	Если СуммаПлатежаПоЗадаче = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ПоказателиРасходовККМ = ПоказателиРасходовККМУменьшающихНалог(Организация,
		ПоказателиПатента.РегистрацияВНалоговомОргане,
		Патент,
		ПоказателиПатента.ВидПлатежа,
		ПоказателиПатента.ДатаОкончания);
	
	ПоказателиРасходовСтраховыеВзносы =
		РегистрыСведений.УменьшениеНалогаПоПатенту.СведенияУменьшениеНалогаЗаГод(
			Организация, ПоказателиПатента.ДатаНачала, ДокументПатент);
	Если ЗначениеЗаполнено(ПоказателиРасходовСтраховыеВзносы) Тогда
		СтраховыеВзносыУменьшающиеПлатеж = ПоказателиРасходовСтраховыеВзносы[ДокументПатент].Сумма;
	Иначе
		СтраховыеВзносыУменьшающиеПлатеж = 0;
	КонецЕсли;
	
	СуммаКУменьшению = ПоказателиРасходовККМ.НалоговыйВычетТекущийПлатеж + СтраховыеВзносыУменьшающиеПлатеж;
	
	СуммаНалогаКУплате = Макс(СуммаПлатежаПоЗадаче - СуммаКУменьшению, 0);
	
	ДокументыУплаты = ДокументыУплатыПатента(Организация, Патент, ПериодСобытия, ПоказателиПатента.ВидНалога);
	
	Уплачено = ПомощникиПоУплатеНалоговИВзносов.СуммаОплаты(ДокументыУплаты);
	
	Если Уплачено > 0 И Уплачено >= СуммаНалогаКУплате Тогда
		Возврат НСтр("ru='Оплачено'");
	КонецЕсли;
	
	Возврат ПомощникиПоУплатеНалоговИВзносов.СтатусОплаты(ДокументыУплаты.ВыгрузитьКолонку("Состояние"));
	
КонецФункции

Функция НовыйПоказателиПатента()
	
	Показатели = Новый Структура;
	
	Показатели.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Показатели.Вставить("РегистрацияВНалоговомОргане", Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка());
	Показатели.Вставить("НаименованиеИФНС", "");
	Показатели.Вставить("ДатаНачала", '00010101');
	Показатели.Вставить("ДатаОкончания", '00010101');
	Показатели.Вставить("КБК", "");
	Показатели.Вставить("ВидНалога", Перечисления.ВидыНалогов.ПустаяСсылка());
	Показатели.Вставить("ТекущийПлатеж", 0);
	Показатели.Вставить("ПредыдущийПлатеж", 0);
	Показатели.Вставить("ТекущийПлатежОписание", "");
	Показатели.Вставить("ПредыдущийПлатежОписание", "");
	Показатели.Вставить("ВидПлатежа", Перечисления.ВидыПлатежейПоПатенту.ПустаяСсылка());
	Показатели.Вставить("ДваПлатежа", Ложь);
	
	// Показатели используются при освобождении от налога.
	Показатели.Вставить("ПотенциальноВозможныйГодовойДоход", 0);
	Показатели.Вставить("НалогПоПатенту", 0);            // Стоимость патента "по бланку"
	Показатели.Вставить("НалогПоПатентуВсего", 0);       // Полная стоимость патента "по бланку"
	Показатели.Вставить("СуммаОсвобожденияОтНалога", 0); // Сумма освобождения от уплаты налога за 2 квартал 2020
	Показатели.Вставить("НалогПоПатентуКУплате",  0);    // Стоимость патента за вычетом суммы освобождения от уплаты налога
	
	Возврат Показатели;
	
КонецФункции

Функция НовыйПоказателиРасходовККМУменьшающихНалог()
	
	Показатели = Новый Структура;
	
	Показатели.Вставить("РасходыНаОнлайнКассыВсего", 0);
	Показатели.Вставить("РасходыНаОнлайнКассыКоличествоКасс", 0);
	Показатели.Вставить("НалоговыйВычетТекущийПатент", 0);
	Показатели.Вставить("НалоговыйВычетТекущийПлатеж", 0);
	Показатели.Вставить("НалоговыйВычетСледующийПлатеж", 0);
	Показатели.Вставить("НалоговыйВычетДругиеПатенты", 0);
	Показатели.Вставить("НалоговыйВычетОстаток", 0);
	
	Возврат Показатели;
	
КонецФункции

Функция НовыйПоказателиРасходовСтраховыеВзносы()
	
	Показатели = Новый Структура;
	
	Показатели.Вставить("ВсегоРасходыСтраховыеВзносы", 0);
	Показатели.Вставить("УменьшениеСтраховыеВзносыТекущийПатент", 0);
	Показатели.Вставить("ОстатокРасходыСтраховыеВзносы", 0);
	Показатели.Вставить("СведенияОбУведомлениях", НовыеСведенияОбУведомлениях());
	Показатели.Вставить("ДоляДоходовПоПатенту", 0);
	Показатели.Вставить("ВзносПоЕдиномуТарифу", 0);
	Показатели.Вставить("ВзносСДоходов", 0);
	Показатели.Вставить("УплаченныеСтраховыеВзносы", 0);
	
	Возврат Показатели;
	
КонецФункции

Функция НовыйПоказателиУведомления()
	
	Показатели = Новый Структура;
	
	Показатели.Вставить("Уведомление", Неопределено);
	Показатели.Вставить("ЕстьУведомление", Ложь);
	Показатели.Вставить("УведомлениеПредставление", "");
	Показатели.Вставить("УведомлениеДата", '00010101');
	Показатели.Вставить("РасходыНаОнлайнКассыВсего", 0);
	Показатели.Вставить("НалоговыйВычетТекущийПатент", 0);
	Показатели.Вставить("НалоговыйВычетТекущийПлатеж", 0);
	Показатели.Вставить("НалоговыйВычетДругиеПатенты", 0);
	Показатели.Вставить("НалоговыйВычетОстаток", 0);
	
	Возврат Показатели;
	
КонецФункции

Функция НовыеСведенияОбУведомлениях()
	
	Сведения = Новый ТаблицаЗначений;
	
	Сведения.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения"));
	Сведения.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	Сведения.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0)));
	Сведения.Колонки.Добавить("Сдано", Новый ОписаниеТипов("Булево"));
	Сведения.Колонки.Добавить("ПредставлениеДокумента", Новый ОписаниеТипов("Строка"));
	Сведения.Колонки.Добавить("СтатусПредставление", Новый ОписаниеТипов("Строка"));
	
	Возврат Сведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли
