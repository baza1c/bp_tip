
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить() Тогда
		Элементы.ГруппаВариантыУчетаЗарплаты.ТекущаяСтраница = Элементы.СтраницаУчетЗарплатыИКадровВоВнешнейПрограмме;
		Возврат;
	КонецЕсли;
	
	ЭтоМедленныйРежимРаботы = ОбщегоНазначенияБП.ЭтоМедленныйРежимРаботы();
	ЭтоВебКлиент            = ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	
	Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
	ТекущаяДатаРаботы = ТекущаяДатаСеанса();
	
	ПериодРегистрации = РасчетныйПериод(Организация, ТекущаяДатаРаботы);
	
	ПредставлениеПериодаРегистрации = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		Перечисления.ДоступныеПериодыОтчета.Месяц, ПериодРегистрации, КонецМесяца(ПериодРегистрации));
	
	СпособВыплатыЗарплата = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СпособыВыплатыЗарплаты.Зарплата");
	СпособВыплатыАванс    = Справочники.СпособыВыплатыЗарплаты.НайтиПоРеквизиту("ХарактерВыплаты", Перечисления.ХарактерВыплатыЗарплаты.Аванс);
	
	ЭтоПредприниматель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	ПрименяетсяАУСН    = УчетнаяПолитика.ПрименяетсяАУСН(Организация, ПериодРегистрации);
	
	ИспользоватьКадровыйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	ИспользоватьНачисленияПоДоговорам = ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорам");
	
	УстановитьУсловноеОформление();
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		УправлениеФормой(ЭтотОбъект);
	Иначе
		ПустойСписок = ПустойСписок(Организация);
		Если ПустойСписок Тогда
			УправлениеФормой(ЭтотОбъект);
		Иначе
			Если НЕ ЭтоМедленныйРежимРаботы И НЕ ЭтоВебКлиент Тогда
				ПолучитьДанныеРазделаНаСервере();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СкрыватьПомеченныеНаУдаление = ПомеченныеНаУдалениеСервер.СкрыватьПомеченныеНаУдаление();
	ОтслеживатьПометкуУдаления = ПомеченныеНаУдалениеСервер.ОтслеживатьПометкуУдаления();
	
	МобильныйКлиентАдаптацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Организация) 
		И НЕ ПустойСписок Тогда
		
		Если ЭтоМедленныйРежимРаботы ИЛИ ЭтоВебКлиент Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ПолучитьДанныеРаздела",0.5, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	ТекущийСотрудник = Неопределено;
	Если СтрокаТаблицы <> Неопределено Тогда
		ТекущийСотрудник = СтрокаТаблицы.Ссылка;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменениеДанныхМестаРаботы" Тогда
		
		ПолучитьДанныеРаздела(Параметр);
		
	ИначеЕсли ИмяСобытия = "СозданСотрудник"
		Или ИмяСобытия = "Запись_Сотрудники" Или ИмяСобытия = "ИзменениеКадровыхДокументов" Тогда
		
		ПолучитьДанныеРаздела(Параметр);
		
		Если ПустойСписок Тогда
			ПустойСписок = Ложь;
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_НачислениеЗарплаты"
		ИЛИ ИмяСобытия = "Запись_БольничныйЛист"
		ИЛИ ИмяСобытия = "Запись_Отпуск"
		ИЛИ ИмяСобытия = "Запись_ВедомостьНаВыплатуЗарплатыВБанк"
		ИЛИ ИмяСобытия = "Запись_ВедомостьНаВыплатуЗарплатыВКассу"
		ИЛИ ИмяСобытия = "Запись_ПриемНаРаботу"
		ИЛИ ИмяСобытия = "Запись_КадровыйПеревод"
		ИЛИ ИмяСобытия = "Запись_Увольнение" Тогда
		
		ПолучитьДанныеРаздела(Источник);
		
	ИначеЕсли ИмяСобытия = "Запись_НачислениеДивидендов"
		ИЛИ ИмяСобытия = "Запись_ДоговорРаботыУслуги" Тогда
		
		ПолучитьДанныеРаздела();
		
	ИначеЕсли ИмяСобытия = "Запись_СписаниеСРасчетногоСчета"
		ИЛИ ИмяСобытия = "Запись_РасходныйКассовыйОрдер" 
		ИЛИ ИмяСобытия = "ОбновитьФорму" Тогда
		
		Если ТребуетсяПолучитьДанныеОплаты(Источник) Тогда
			ПолучитьДанныеРаздела();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗарплатаРассчитана_ПомощникУчетаЗарплаты" Тогда
		Если Параметр.Организация = Организация Тогда
			ПолучитьДанныеРаздела();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	ПолучитьДанныеРаздела();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; // Незачем очищать
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииПриИзменении(Элемент)
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода", ПериодРегистрации, КонецМесяца(ПериодРегистрации));
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", ПараметрыВыбора, Элементы.ПредставлениеПериодаРегистрации, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПериода()
	
	Если ПериодРегистрации > ТекущаяДатаРаботы Тогда
		ТекущаяДатаРаботы = ПериодРегистрации;
	КонецЕсли;
	
	ПриИзмененииПериодаНаСервере();
	
	ПолучитьДанныеРаздела();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("ПоказатьИнформациюОПравеПримененияСпецрежима", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьКарточкуСотрудника();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УменьшитьПериод(Команда)
	
	ПериодРегистрации = ДобавитьМесяц(ПериодРегистрации,-1);
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьПериод(Команда)
	
	ПериодРегистрации = ДобавитьМесяц(ПериодРегистрации, 1);
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетТ2(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивСотрудников = Новый Массив;
	МассивСотрудников.Добавить(СтрокаТаблицы.Ссылка);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Справочник.Сотрудники",
		"ПФ_MXL_Т2",
		МассивСотрудников,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетВзаиморасчеты(Команда)
	
	Вариант = ПолучитьВариантОтчета("АнализНачисленийИУдержаний", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетНДФЛ(Команда)
	
	Вариант = ПолучитьВариантОтчета("АнализНДФЛ", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыплатыСотрудникамПриАУСН(Команда)
	
	Вариант = ПолучитьВариантОтчета("ВыплатыПриИспользованииАУСН", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСтраховыеВзносы(Команда)
	
	Вариант = ПолучитьВариантОтчета("АнализВзносовВФонды", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСотрудники(Команда)
	
	Вариант = ПолучитьВариантОтчета("ОтчетыПоСотрудникам", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетНалогиИВзносыКратко(Команда)
	
	Вариант = ПолучитьВариантОтчета("НалогиИВзносыКратко", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетТ13(Команда)
	
	Вариант = ПолучитьВариантОтчета("УнифицированнаяФормаТ13", СтрЗаменить(Команда.Имя, "Отчет",""));
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтотОбъект, Вариант);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетАнализРасходовНаОплатуТруда(Команда)
	
	ОткрытьФорму("Отчет.АнализРасходовНаОплатуТруда.ФормаОбъекта");
	
КонецПроцедуры

&НаКлиенте
Процедура КарточкаСотрудника(Команда)
	
	ОткрытьКарточкуСотрудника();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометкаУдаления(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СведенияОСотруднике = Новый Структура("Ссылка, ДатаПриема, ДатаУвольнения, ДатаРождения, Работает, ОформленПоДоговоруПодряда, Картинка, ПометкаУдаления");
	ЗаполнитьЗначенияСвойств(СведенияОСотруднике, СтрокаТаблицы);
	
	УстановитьСнятьПометкуУдаления(СведенияОСотруднике);
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СведенияОСотруднике, "Работает, Картинка, ПометкаУдаления");
	Объект.Список.Сортировать("Работает Убыв, ПометкаУдаления Возр, Наименование Возр");
	
	Если СкрыватьПомеченныеНаУдаление Тогда
		ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элементы.Список);
		ПолучитьДанныеРаздела();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	ПолучитьДанныеРаздела();
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//Оформление таблицы Список
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Список");
	ГруппаИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,
		"ПризнакРаботника",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"Объект.Список.Работает", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"Объект.Список.ПометкаУдаления", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидНачисленияЗарплата");
	ГруппаРасчетыИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,
		"ПризнакРасчетов",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаРасчетыИ,
		"Объект.Список.НачисленоДивиденды", ВидСравненияКомпоновкиДанных.Равно, 0);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаРасчетыИ,
		"Объект.Список.КВыплатеДивиденды", ВидСравненияКомпоновкиДанных.Равно, 0);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаРасчетыИ,
		"Объект.Список.НачисленоДоговоры", ВидСравненияКомпоновкиДанных.Равно, 0);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаРасчетыИ,
		"Объект.Список.КВыплатеДоговоры", ВидСравненияКомпоновкиДанных.Равно, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидНачисленияДивиденды");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НачисленоДивиденды");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НДФЛДивиденды");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВыплаченоДивиденды");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КВыплатеДивиденды");
	ГруппаДивидендыИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,
		"ПризнакДивиденды",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаДивидендыИ,
		"Объект.Список.НачисленоДивиденды", ВидСравненияКомпоновкиДанных.Равно, 0);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаДивидендыИ,
		"Объект.Список.КВыплатеДивиденды", ВидСравненияКомпоновкиДанных.Равно, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВидНачисленияДоговоры");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "НачисленоДоговоры");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ВыплаченоДоговоры");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КВыплатеДоговоры");
	ГруппаДоговорникиИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,
		"ПризнакДоговорники",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаДоговорникиИ,
		"Объект.Список.НачисленоДоговоры", ВидСравненияКомпоновкиДанных.Равно, 0);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаДоговорникиИ,
		"Объект.Список.КВыплатеДоговоры", ВидСравненияКомпоновкиДанных.Равно, 0);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Если НЕ ЗначениеЗаполнено(Форма.Организация) Тогда
		Форма.Элементы.ГруппаСписокСотрудников.ТекущаяСтраница = Форма.Элементы.СтраницаНеВыбранаОрганизация;
		Форма.Элементы.ГруппаКоманднаяПанель.Доступность = Ложь;
	ИначеЕсли Форма.ПустойСписок Тогда
		Форма.Элементы.ГруппаСписокСотрудников.ТекущаяСтраница = Форма.Элементы.СтраницаПустая;
		Форма.Элементы.ГруппаКоманднаяПанель.Доступность = Ложь;
		Форма.Элементы.ДекорацияИли.Видимость = Форма.ИспользоватьНачисленияПоДоговорам;
	Иначе
		Форма.Элементы.ГруппаСписокСотрудников.ТекущаяСтраница = Форма.Элементы.СтраницаСписок;
		Форма.Элементы.ГруппаКоманднаяПанель.Доступность = Истина;
	КонецЕсли;
	
	Форма.Элементы.ОтчетВыплатыСотрудникамПриАУСН.Видимость = Форма.ПрименяетсяАУСН;
	Форма.Элементы.ОтчетАнализВзносовВФонды.Видимость       = Не Форма.ПрименяетсяАУСН;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИтоги()
	
	ИтогоНачислено = Объект.Список.Итог("Начислено")
		+ Объект.Список.Итог("НачисленоДивиденды")
		+ Объект.Список.Итог("НачисленоДоговоры");
	ИтогоУдержано  = Объект.Список.Итог("Удержано")
		+ Объект.Список.Итог("НДФЛДивиденды");
	ИтогоВыплачено = Объект.Список.Итог("Выплачено")
		+ Объект.Список.Итог("ВыплаченоДивиденды")
		+ Объект.Список.Итог("ВыплаченоДоговоры");
	ИтогоКВыплате  = Объект.Список.Итог("КВыплате")
		+ Объект.Список.Итог("КВыплатеДивиденды")
		+ Объект.Список.Итог("КВыплатеДоговоры");
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЭтоПредприниматель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо") = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	ПрименяетсяАУСН    = УчетнаяПолитика.ПрименяетсяАУСН(Организация, ПериодРегистрации);
	ПустойСписок = ПустойСписок(Организация);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПериодаНаСервере()
	
	// Обновляем представление периода на форме.
	ПредставлениеПериодаРегистрации = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
		ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"),
		ПериодРегистрации,
		КонецМесяца(ПериодРегистрации));
		
	ПрименяетсяАУСН = УчетнаяПолитика.ПрименяетсяАУСН(Организация, ПериодРегистрации);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПустойСписок(Организация)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Сотрудники.Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ГоловнаяОрганизация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Пустой();
	
КонецФункции

&НаСервере
Функция ВыполнитьПолучениеДанныхНаСервере(Параметр = Неопределено)
	
	ПараметрыОбработки = СформироватьПараметрыОбработки(Параметр);
	
	ПараметрыЗадания = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыЗадания.ЗапуститьВФоне = Истина;
	ПараметрыЗадания.НаименованиеФоновогоЗадания = НСтр("ru = 'Раздел ""Сотрудники"": получение данных'");

	Результат = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыЗадания, "Обработки.ПростойИнтерфейсРазделСотрудники.ПодготовитьДанные", ПараметрыОбработки);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьРезультат(Результат)
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ОбновитьВесьСписок  = Ложь;
		ОбновитьСотрудников = Ложь;
		Результат.Свойство("ОбновитьВесьСписок",  ОбновитьВесьСписок);
		Результат.Свойство("ОбновитьСотрудников", ОбновитьСотрудников);
		
		Если ОбновитьВесьСписок ИЛИ ОбновитьСотрудников Тогда
			
			Если Результат.Свойство("Список") Тогда
				Если ТипЗнч(Результат.Список) = Тип("ТаблицаЗначений") Тогда
					
					Если ОбновитьВесьСписок Тогда
						Объект.Список.Очистить();
						Объект.Список.Загрузить(Результат.Список);
					КонецЕсли;
					
					Если ОбновитьСотрудников Тогда
						ОбновитьДанныеСотрудников(Результат.Список);
					КонецЕсли;
					
					Если Объект.Список.Итог("ИсполнительныйЛист")<> 0 Тогда
						Элементы.Удержано.Формат = "ЧФ='НДФЛ и алименты: Ч'";
					Иначе
						Элементы.Удержано.Формат = "ЧФ='НДФЛ: Ч'";
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьИтоги();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьДанныеРаздела()
	
	ПолучитьДанныеРаздела();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанныеРаздела(Параметр = Неопределено)
	
	РезультатВыполнения = ВыполнитьПолучениеДанныхНаСервере(Параметр);
	
	Если РезультатВыполнения.Статус = "Выполнено" Тогда
		ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
КонецПроцедуры
	
&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения)
	
	Результат = ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	ЗагрузитьРезультат(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеРазделаНаСервере()
	
	ПараметрыОбработки = СформироватьПараметрыОбработки();
	Результат = Обработки.ПростойИнтерфейсРазделСотрудники.ПодготовитьДанные(ПараметрыОбработки);
	ЗагрузитьРезультат(Результат);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ТребуетсяПолучитьДанныеОплаты(Источник)
	
	Если ТипЗнч(Источник) <> Тип("ДокументСсылка.СписаниеСРасчетногоСчета")
		И ТипЗнч(Источник) <> Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "Организация, ВидОперации");
	
	Если РеквизитыДокумента.Организация = ЭтотОбъект.Организация Тогда
		Если РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаДивидендов
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыПоВедомостям
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗаработнойПлатыРаботнику
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеДивидендов
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗП
			ИЛИ РеквизитыДокумента.ВидОперации = Перечисления.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеЗаработнойПлатыРаботнику Тогда
			
			Возврат Истина;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеСотрудников(ТаблицаДаных)
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаДаных Цикл
		
		МассивСтрок = Объект.Список.НайтиСтроки(Новый Структура("Ссылка", СтрокаТаблицы.Ссылка));
		Если МассивСтрок.Количество() = 1 Тогда
			ЗаполнитьЗначенияСвойств(МассивСтрок[0], СтрокаТаблицы);
		Иначе
			НоваяСтрока = Объект.Список.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			Объект.Список.Сортировать("Работает Убыв, ПометкаУдаления Возр, Наименование Возр");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПериодРегистрации = РезультатВыбора.НачалоПериода;
	
	ПриИзмененииПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьРасчетныйДокументЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", РезультатВыбора.Значение);
	ОткрытьФорму("Документ." + ДополнительныеПараметры.ТипРасчета + ".ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВариантОтчета(ИмяОтчета, ИмяВарианта)
	
	Отчет         = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Отчет." + ИмяОтчета);
	ВариантОтчета = ВариантыОтчетов.ВариантОтчета(Отчет, ИмяВарианта);
	Возврат ВариантОтчета;
	
КонецФункции

&НаКлиенте
Процедура ОтчетДругиеОтчеты(Команда)
	
	ВариантыОтчетовКлиент.ПоказатьПанельОтчетов(
		"Отчеты.Сотрудники",
		Неопределено);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуСотрудника()
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыЭлементаСправочникаСотрудники");
	
	ПараметрыФормы = Новый Структура("Ключ", СтрокаТаблицы.Ссылка);
	ОткрытьФорму("Справочник.Сотрудники.Форма.ФормаЭлемента", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСнятьПометкуУдаления(СведенияОСотруднике)
	
	СотрудникОбъект = СведенияОСотруднике.Ссылка.ПолучитьОбъект();
	СотрудникОбъект.УстановитьПометкуУдаления(НЕ СотрудникОбъект.ПометкаУдаления);
	
	СведенияОСотруднике.ПометкаУдаления = СотрудникОбъект.ПометкаУдаления;
	
	Обработки.ПростойИнтерфейсРазделСотрудники.УстановитьСтатусСотрудника(СведенияОСотруднике, ПериодРегистрации);
	
КонецПроцедуры

&НаСервере
Функция РасчетныйПериод(Организация, ТекущаяДатаРаботы);
	
	ТекущийПериод       = НачалоМесяца(ТекущаяДатаРаботы);
	ПериодЗадолженности = ТекущийПериод;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		ДатаВыплатыЗарплаты = День(УчетЗарплаты.ПланируемаяДатаВыплатыЗарплаты(Организация, ТекущийПериод));
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗарплатаКВыплатеОстатки.ПериодВзаиморасчетов КАК ПериодВзаиморасчетов
		|ИЗ
		|	РегистрНакопления.ЗарплатаКВыплате.Остатки(, Организация = &Организация) КАК ЗарплатаКВыплатеОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодВзаиморасчетов";
		
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ПериодЗадолженности = НачалоМесяца(Выборка.ПериодВзаиморасчетов);
		КонецЕсли;
	Иначе
		ДатаВыплатыЗарплаты = День(ТекущаяДатаРаботы);
	КонецЕсли;
	
	Если ПериодЗадолженности < ТекущийПериод Тогда
		РасчетныйПериод = ПериодЗадолженности;
	Иначе
		Если ДатаВыплатыЗарплаты >= День(ТекущаяДатаРаботы) Тогда
			РасчетныйПериод = НачалоМесяца(ДобавитьМесяц(ТекущийПериод, -1));
		Иначе
			РасчетныйПериод = ТекущийПериод;
		КонецЕсли;
	КонецЕсли;
	
	Возврат РасчетныйПериод;
	
КонецФункции

#Область ДлительныеОперации

&НаСервере
Функция СформироватьПараметрыОбработки(Параметр = Неопределено)
	
	ПараметрыОбработки = Новый Структура();
	ПараметрыОбработки.Вставить("Организация",             Организация);
	ПараметрыОбработки.Вставить("ПериодРегистрации",       ПериодРегистрации);
	ПараметрыОбработки.Вставить("ТекущаяДатаРаботы",       ТекущаяДатаРаботы);
	ПараметрыОбработки.Вставить("Параметр",                Параметр);
	
	Возврат ПараметрыОбработки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект, Истина);
	
	Если Не ЭтотОбъект.ПустойСписок Тогда
		Предупреждение = МобильныйКлиентБПФормаОбъекта.СоздатьПредупреждениеОбОграничениях(ЭтотОбъект,
			"Для изменения сведений о сотрудниках перейдите в версию для компьютера");
		МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКШапка(ЭтотОбъект, Предупреждение);
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКШапка(ЭтотОбъект, Элементы.ГруппаШапка);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКШапка(ЭтотОбъект, Элементы.ГруппаПериод);
	
	Элементы.ГруппаПринятьНаРаботу.Видимость = Ложь;
	Если ЭтотОбъект.ПустойСписок Тогда
		ЭлементПредупреждение = МобильныйКлиентБПФормаОбъекта.СоздатьПредупреждениеОбОграничениях(ЭтотОбъект,
			НСтр("ru = 'Добавьте нового сотрудника в версии для компьютера'"));
		Элементы.Переместить(ЭлементПредупреждение, Элементы.СтраницаПустая);
	КонецЕсли;
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.ГруппаСписокСотрудников);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	АдаптироватьМКСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.РазрешеноИзменение = Ложь;
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКПерваяКомандаПанели(ЭтотОбъект, Элементы.ГруппаКомандыОтчеты);
	МобильныйКлиентБПФормаСписка.ПеренестиЭлементВМККомандыФормы(ЭтотОбъект, Элементы.ФормаОбновитьДанные);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	Элементы.Список.Шапка = Ложь;
	Элементы.Список.Подвал = Ложь;
	Элементы.Список.ПоведениеПриСжатииПоГоризонтали = ПоведениеТаблицыПриСжатииПоГоризонтали.ПереноситьЭлементыПоВажности;
	
	Элементы.Наименование.ВажностьПриОтображении = ВажностьПриОтображении.ОченьВысокая;
	
	Элементы.Начислено.Формат = "ЧФ='Начислено з/п: Ч'";
	Элементы.НачисленоДивиденды.Формат = "ЧФ='Начислено дивиденды: Ч'";
	Элементы.НачисленоДоговоры.Формат = "ЧФ='Начислено по договору ГПХ: Ч'";
	
	Элементы.Удержано.Формат = "ЧФ='НДФЛ и алименты: Ч'";
	Элементы.НДФЛДивиденды.Формат = "ЧФ='НДФЛ дивиденды: Ч'";
	
	Элементы.Выплачено.Формат = "ЧФ='Выплачено: Ч'";
	Элементы.ВыплаченоДивиденды.Формат = "ЧФ='Выплачено дивиденды: Ч'";
	Элементы.ВыплаченоДоговоры.Формат = "ЧФ='Выплачено по договору ГПХ: Ч'";
	
	Элементы.КВыплате.Формат = "ЧФ='К выплате зарплата: Ч'";
	Элементы.КВыплатеДивиденды.Формат = "ЧФ='К выплате дивиденды: Ч'";
	Элементы.КВыплатеДоговоры.Формат = "ЧФ='К выплате по договору ГПХ: Ч'";
	
	Элементы.ТекущийПроцентСевернойНадбавки.Формат = "ЧФ='Северная надбавка: Ч'";
	Элементы.ТекущийАванс.Формат = "ЧФ='Аванс: Ч'";
	
КонецПроцедуры

#КонецОбласти

