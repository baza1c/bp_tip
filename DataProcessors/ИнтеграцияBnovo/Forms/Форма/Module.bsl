#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДатаЗагрузки = Параметры.ДатаЗагрузки;
	
	ЗаполнитьДеревоДокументов();
	
	УстановитьВидимостьДоступность();
	
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Результат = Новый Структура("Результат, ТребуетсяНастройка, СозданныеДокументы", Истина, Ложь, СозданныеДокументы());
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура ЗагрузитьИзBnovo(Команда)
	ПараметрыЗагрузки = Новый Структура;
	Если ОтборОрганизацияИспользование Тогда
		ПараметрыЗагрузки.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	Если ОтборГостиницаИспользование Тогда
		ПараметрыЗагрузки.Вставить("Гостиница", ОтборГостиница);
	КонецЕсли;
	ИнтеграцияBnovoКлиент.ВыполнитьЗагрузку(ЭтотОбъект, ПараметрыЗагрузки);
КонецПроцедуры 

&НаКлиенте
Процедура ЗагрузитьИзBNovo_Настройка(Команда)
	ОткрытьФорму("РегистрСведений.НастройкиИнтеграцииBnovo.ФормаСписка",,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзBNovo_ЗаПериод(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаНачала", ДатаЗагрузки);
	ПараметрыФормы.Вставить("ДатаОкончания",  НачалоДня(ТекущаяДата())-1);
	
	ОповещениеОВыбореПериода = НОвый ОписаниеОповещения("ВыборПериодаЗагрузкиЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.ИнтеграцияBnovo.Форма.ВыборПериода", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , ,ОповещениеОВыбореПериода ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура Вперед(Команда)
	ВпередНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	НазадНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборГостиницаИспользованиеПриИзменении(Элемент)
	ЗаполнитьДеревоДокументов();
КонецПроцедуры

&НаКлиенте
Процедура ОтборГостиницаПриИзменении(Элемент)
	ОтборГостиницаИспользование = ЗначениеЗаполнено(ОтборГостиница);
	ЗаполнитьДеревоДокументов();
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	ЗаполнитьДеревоДокументов();
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ЗаполнитьДеревоДокументов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормы

&НаКлиенте
Процедура ДеревоДокументовПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ТекущиеДанные = Элементы.ДеревоДокументов.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Документ);
		Если ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.СчетНаОплатуПокупателю") Тогда
			ОткрытьФорму("Документ.СчетНаОплатуПокупателю.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ОткрытьФорму("Документ.РеализацияТоваровУслуг.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.ОтчетКомиссионераОПродажах") Тогда
			ОткрытьФорму("Документ.ОтчетКомиссионераОПродажах.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
			ОткрытьФорму("Документ.ПриходныйКассовыйОрдер.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			ОткрытьФорму("Документ.РасходныйКассовыйОрдер.ФормаОбъекта", ПараметрыФормы, , , , , );
		ИначеЕсли ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.ОплатаПлатежнойКартой") Тогда
			ОткрытьФорму("Документ.ОплатаПлатежнойКартой.ФормаОбъекта", ПараметрыФормы, , , , , );
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборПериодаЗагрузкиЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("Период", ВыбранноеЗначение);
	
	Если ОтборОрганизацияИспользование Тогда
		ПараметрыЗагрузки.Вставить("Организация", ОтборОрганизация);
	КонецЕсли;
	Если ОтборГостиницаИспользование Тогда
		ПараметрыЗагрузки.Вставить("Гостиница",   ОтборГостиница);
	КонецЕсли;
	
	ИнтеграцияBnovoКлиент.ВыполнитьЗагрузку(ЭтотОбъект, ПараметрыЗагрузки);
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	ДокументыBnovo.ДатаЗагрузки КАК ДатаЗагрузки
	|ИЗ
	|	РегистрСведений.ДокументыBnovo КАК ДокументыBnovo";
	
	Элементы.ГруппаКнопкиПериод.Видимость = Запрос.Выполнить().Выбрать().Количество() > 1;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоДокументов()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыBnovo.Документ КАК Документ,
	|	ДокументыBnovo.НомерБрони КАК НомерБрони,
	|	ДокументыBnovo.ИмяГостя КАК ИмяГостя,
	|	ВЫБОР
	|		КОГДА ДокументыBnovo.Документ ССЫЛКА Документ.СчетНаОплатуПокупателю
	|			ТОГДА 1
	|		КОГДА ДокументыBnovo.Документ ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ИЛИ ДокументыBnovo.Документ ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК Раздел,
	|	ДокументыBnovo.Документ.Контрагент КАК Контрагент,
	|	ЕСТЬNULL(ДокументыBnovo.Документ.СуммаДокумента, 0) КАК Сумма,
	|	ДокументыBnovo.ДатаЗагрузки КАК ДатаЗагрузки
	|ИЗ
	|	РегистрСведений.ДокументыBnovo КАК ДокументыBnovo
	|ГДЕ
	|	&СтрокаУсловия";
	
	Условия = Новый Массив;
	Условия.Добавить("НЕ ДокументыBnovo.Документ.Дата ЕСТЬ NULL");
	
	Если ОтборОрганизацияИспользование Тогда
		Условия.Добавить("ДокументыBnovo.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", ОтборОрганизация);
	КонецЕсли;
	
	Если ОтборГостиницаИспользование Тогда
		Условия.Добавить("ДокументыBnovo.Гостиница = &Гостиница");
		Запрос.УстановитьПараметр("Гостиница", ОтборГостиница);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаЗагрузки) Тогда
		Условия.Добавить("ДокументыBnovo.ДатаЗагрузки = &ДатаЗагрузки");
		Запрос.УстановитьПараметр("ДатаЗагрузки", ДатаЗагрузки);
	Иначе
		СтрокаУсловия = "ДокументыBnovo.ДатаЗагрузки В
		|(ВЫБРАТЬ
		|	МАКСИМУМ(ДокументыBnovo.ДатаЗагрузки) КАК ДатаЗагрузки
		|ИЗ
		|	РегистрСведений.ДокументыBnovo КАК ДокументыBnovo 
		|ГДЕ 
		|	&СтрокаУсловия)";
		Условия.Добавить(СтрЗаменить(СтрокаУсловия, "&СтрокаУсловия", СтрСоединить(Условия, " И ")));
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтрокаУсловия", СтрСоединить(Условия, " И "));
	
	ДеревоЗначений = РеквизитФормыВЗначение("ДеревоДокументов");
	ДеревоЗначений.Строки.Очистить();
	
	НадписьДатаЗагрузки = НСтр("ru = 'Нет загруженных документов'");
	
	СтрокаСчета = ДеревоЗначений.Строки.Добавить();
	СтрокаСчета.Раздел = НСТр("ru = 'Бронирования'");

	СтрокаПроживания = ДеревоЗначений.Строки.Добавить();
	СтрокаПроживания.Раздел = НСТр("ru = 'Проживания'");
	
	СтрокаОплаты = ДеревоЗначений.Строки.Добавить();
	СтрокаОплаты.Раздел = НСТр("ru = 'Оплаты'");
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	Если ТаблицаРезультат.Количество() = 0 Тогда
		ЗначениеВРеквизитФормы(ДеревоЗначений, "ДеревоДокументов"); 
		
		Возврат;
	КонецЕсли;
	
	ДатаЗагрузки = ТаблицаРезультат[0].ДатаЗагрузки;
	
	НадписьДатаЗагрузки = СтрШаблон(НСтр("ru = 'Дата загрузки: %1'"), ФОрмат(ДатаЗагрузки, "ДЛФ=DT"));
	
	Для каждого СтрокаДанные Из ТаблицаРезультат Цикл
		Если СтрокаДанные.Раздел = 1 Тогда
			СтрокаДокумент = СтрокаСчета.Строки.Добавить();
			СтрокаСчета.Сумма = СтрокаСчета.Сумма + СтрокаДанные.Сумма;
		ИначеЕсли СтрокаДанные.Раздел = 2 Тогда
			СтрокаДокумент = СтрокаПроживания.Строки.Добавить();
			СтрокаПроживания.Сумма = СтрокаПроживания.Сумма + СтрокаДанные.Сумма;
		Иначе
			СтрокаДокумент = СтрокаОплаты.Строки.Добавить();
			СтрокаОплаты.Сумма = СтрокаОплаты.Сумма + СтрокаДанные.Сумма;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаДокумент, СтрокаДанные);
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ДеревоДокументов"); 
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//Оформление группы
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовНомерБрони");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.НомерБрони", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ДеревоДокументов.Раздел"));
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовНомерБрони");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовИмяГостя");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовКонтрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовСумма");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовОтступ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ИтогиФонГруппы);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,Истина,Ложь));
	
	//Оформление элемента
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоДокументовДокумент");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоДокументов.Документ", ВидСравненияКомпоновкиДанных.Заполнено, "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);
		
КонецПроцедуры

&НаКлиенте
Функция СозданныеДокументы()
	СозданныеДокументы = Новый Массив;
	Для каждого Строка Из ДеревоДокументов.ПолучитьЭлементы() Цикл
		Для каждого СтрокаДокумент Из Строка.ПолучитьЭлементы() Цикл
			СозданныеДокументы.Добавить(СтрокаДокумент.Документ);
		КонецЦикла;
	КонецЦикла;
	
	Возврат СозданныеДокументы;
КонецФункции

&НаСервере
Процедура ВпередНаСервере()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаЗагрузки", ДатаЗагрузки);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	ДокументыBnovo.ДатаЗагрузки КАК ДатаЗагрузки
	|ИЗ
	|	РегистрСведений.ДокументыBnovo КАК ДокументыBnovo
	|ГДЕ
	|	ДокументыBnovo.ДатаЗагрузки > &ДатаЗагрузки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаЗагрузки ВОЗР";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Элементы.КомандаВперед.Доступность = Выборка.Количество() > 1;
	Если Выборка.Следующий() Тогда
		Элементы.КомандаНазад.Доступность = Истина;
		ДатаЗагрузки = Выборка.ДатаЗагрузки;
		ЗаполнитьДеревоДокументов();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура НазадНаСервере()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаЗагрузки", ДатаЗагрузки);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	ДокументыBnovo.ДатаЗагрузки КАК ДатаЗагрузки
	|ИЗ
	|	РегистрСведений.ДокументыBnovo КАК ДокументыBnovo
	|ГДЕ
	|	ДокументыBnovo.ДатаЗагрузки < &ДатаЗагрузки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаЗагрузки УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Элементы.КомандаНазад.Доступность = Выборка.Количество() > 1;
	Если Выборка.Следующий() Тогда
		Элементы.КомандаВперед.Доступность = Истина;
		ДатаЗагрузки = Выборка.ДатаЗагрузки;
		ЗаполнитьДеревоДокументов();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти



