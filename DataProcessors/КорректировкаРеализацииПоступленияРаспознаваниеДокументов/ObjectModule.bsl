#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура Заполнить(Ссылка, УникальныйИдентификатор) Экспорт
	
	ДокументОбъект = Ссылка.ПолучитьОбъект();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДокументОбъект);
	
	Если Не УправлениеДоступом.ЧтениеРазрешено(Организация) Тогда
		Организация = Неопределено;
	КонецЕсли;
	
	АдресКартинки = ПоместитьВоВременноеХранилище(
		РаспознаваниеДокументовСлужебный.ИсходноеИзображение(Ссылка),
		УникальныйИдентификатор
	);
	
	ВсеСвойстваЯчеекТаблицы = РаспознаваниеДокументовСлужебный.СвойстваЯчеекТаблицы(ДокументОбъект);
	АдресСвойствЯчеекТаблицы = ПоместитьВоВременноеХранилище(ВсеСвойстваЯчеекТаблицы, УникальныйИдентификатор);
	
	РеквизитыРаспознанногоДокумента = РаспознаваниеДокументовСлужебный.РеквизитыРаспознанногоДокумента(ДокументОбъект);
	АдресСвойствРаспознанногоДокумента = ПоместитьВоВременноеХранилище(РеквизитыРаспознанногоДокумента, УникальныйИдентификатор);
	
	Для Каждого Строка Из ДокументОбъект.РеквизитыДокумента Цикл
		
		НоваяСтрока = РеквизитыДокумента.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		Если НоваяСтрока.ИмяРеквизита = "Продавец" Тогда
			НоваяСтрока.ИмяРеквизита = "Контрагент";
		КонецЕсли;
		
		Если НоваяСтрока.ИмяРеквизита = "ПокупательОрганизация" Тогда
			НоваяСтрока.ИмяРеквизита = "Организация";
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Строка Из ДокументОбъект.РеквизитыТабличныхЧастей Цикл
		
		НоваяСтрока = РеквизитыТабличныхЧастей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
	КонецЦикла;
	
	ТаблицаДокумента = РаспознаваниеДокументовСлужебный.ЗаполненнаяТаблицаДокумента(ДокументОбъект);
	КолонкаНомерСтроки = ТаблицаДокумента.ВыгрузитьКолонку("НомерСтроки");
	
	Товары.Загрузить(ТаблицаДокумента);
	Товары.ЗагрузитьКолонку(КолонкаНомерСтроки, "НомерСтрокиВДокументе");
	
	ДокументыОснованияКорректировки.Загрузить(ДокументОбъект.ДокументыОснованияКорректировки.Выгрузить());
	
КонецПроцедуры

Процедура Записать(Форма) Экспорт
	
	ДокументОбъект = Ссылка.ПолучитьОбъект();
	
	ДокументОбъект.Направление = Направление;
	ДокументОбъект.ТипДокумента = ТипДокумента;
	ДокументОбъект.Статус = Статус;
	
	РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, "НомерДокумента", НомерДокумента);
	РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, "ДатаДокумента", ДатаДокумента);
	
	ОсновныеРеквизиты = РаспознаваниеДокументовСлужебный.ОсновныеРеквизиты(ЭтотОбъект);
	РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, ОсновныеРеквизиты["Организация"], Организация);
	РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, ОсновныеРеквизиты["Контрагент"], Контрагент);
	
	РеквизитыПодвала = Новый Массив;
	РеквизитыПодвала.Добавить("ИтогоВсегоУвеличение");
	РеквизитыПодвала.Добавить("ИтогоСуммаНДСУвеличение");
	РеквизитыПодвала.Добавить("ИтогоСуммаУвеличение");
	РеквизитыПодвала.Добавить("ИтогоВсегоУменьшение");
	РеквизитыПодвала.Добавить("ИтогоСуммаНДСУменьшение");
	РеквизитыПодвала.Добавить("ИтогоСуммаУменьшение");
	
	Для Каждого РеквизитПодвала Из РеквизитыПодвала Цикл
		
		РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(
			ДокументОбъект, РеквизитПодвала, Форма[РеквизитПодвала]);
		
	КонецЦикла;
	
	ТаблицаДокумента = Форма.Объект.Товары.Выгрузить();
	ТаблицаДокумента.Колонки["НомерСтроки"].Имя = "ПорядокСтроки";
	ТаблицаДокумента.Колонки["НомерСтрокиВДокументе"].Имя = "НомерСтроки";
	РаспознаваниеДокументовСлужебный.СохранитьТаблицуДокумента(ДокументОбъект, ТаблицаДокумента);
	
	ДокументОбъект.ДокументыОснованияКорректировки.Загрузить(ДокументыОснованияКорректировки.Выгрузить());
	
	ДокументОбъект.Записать();
	
КонецПроцедуры

Функция СоздатьДокументы(ПараметрыОперации) Экспорт
	
	Результат = Новый Массив;
	
	Если ПараметрыОперации.ТипДокумента = "КорректировкаПоступления" Тогда
		РаспознаваниеДокументовПереопределяемый.ОбработкаСозданияКорректировкиПоступленияРеализации(Результат, ЭтотОбъект);
	КонецЕсли;
	
	Если ПараметрыОперации.ТипДокумента = "ВозвратТоваровПоставщику" Тогда
		РаспознаваниеДокументовПереопределяемый.ОбработкаСозданияВозвратаТоваровПоставщику(Результат, ЭтотОбъект);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
