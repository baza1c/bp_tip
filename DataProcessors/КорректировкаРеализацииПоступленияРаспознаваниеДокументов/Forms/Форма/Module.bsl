
#Область ОписаниеПеременных

&НаКлиенте
Перем ПредыдущееЗначениеДокументаВСтроке;

&НаКлиенте
Перем ДанныеПриближения;

&НаКлиенте
Перем СвойстваЯчеекТаблицы Экспорт;

&НаКлиенте
Перем КолонкиРеквизитыТабличныхЧастей;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРаспознаваниеДокументов") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Объект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий;
	
	Для Каждого Строка Из Объект.ДокументыОснованияКорректировки Цикл
		Если Не ЗначениеЗаполнено(Строка.ДокументОснование) Тогда
			Строка.ДокументОснование = Документы.КорректировкаПоступления.ПустаяСсылка();
		КонецЕсли;
	КонецЦикла;
	
	ТекущаяСтрокаТаблицы = -1;
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Заполнить(Параметры.Ключ, УникальныйИдентификатор);
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML();
	Заголовок = Объект.Наименование;
	
	Настройки = РегистрыСведений.ОбщиеНастройкиРаспознаваниеДокументов.ТекущиеНастройки();
	Если Настройки.ФормаОбработчикаПоказыватьИзображенияВШапке Тогда
		ДобавитьИзображенияРеквизитов(Настройки);
	КонецЕсли;
	ВариантСохраненияСоответствий = Настройки.ВариантСохраненияСоответствий;
	
	НайтиИЗаполнитьДокументыОснования();
	УправлениеГруппойОснований();
	
	ЗаполнитьЭлементыИтогов();
	
	НастроитьКолонкиТаблицы();
	ЗаполнитьДобавленныеКолонкиТаблиц();
	УстановитьУсловноеОформление();
	
	ИнициализироватьСтруктуруСозданныеДокументы();
	НайтиИЗаполнитьСозданныеДокументы();
	
	РаспознаваниеДокументовПраваДоступа.ЗаполнитьСправочникиБезПравНаСоздание(СправочникиБезПравНаСоздание);
	СправочникиБезПравНаСоздание.Добавить("Номенклатура");
	СправочникиБезПравНаСоздание.Добавить("Контрагенты");
	СправочникиБезПравНаСоздание.Добавить("ДокументОснованиеКорректировки");
	НастроитьЭлементыПоРеквизитам();
	
	ПриИзмененииДокументаОснования();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КолонкиРеквизитыТабличныхЧастей = КолонкиТаблицы("Объект.РеквизитыТабличныхЧастей");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодтвержденияЗакрытия", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеЗаписи() Экспорт
	Оповестить("РаспознанныйДокумент_ОбновитьОтборФормыСписка", Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипДокументаПриИзменении(Элемент)
	
	Обработчик = Новый ОписаниеОповещения("ПослеПроверкиИзменения", ЭтотОбъект);
	РаспознаваниеДокументовСлужебныйКлиент.ИзменитьТипДокумента(ЭтотОбъект, Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипДокументаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПолеПросмотраДокументСформирован(Элемент)
	
	РаспознаваниеДокументовСлужебныйКлиент.ПолеПросмотраДокументСформирован(Элемент, Элементы.ПолеПросмотра, Объект.АдресКартинки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПросмотраПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	РаспознаваниеДокументовСлужебныйКлиент.ПолеПросмотраПриНажатии(ДанныеСобытия, ПолеПросмотраГотово);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	Если Объект.ДокументыОснованияКорректировки.Количество() = 0 Тогда
		Объект.ДокументыОснованияКорректировки.Добавить();
	КонецЕсли;
	
	Объект.ДокументыОснованияКорректировки[0].ДокументОснование = ДокументОснованиеКорректировки;
	
	ПриИзмененииДокументаОснования();
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ПриИзмененииДокументаОснования();
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованияПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Основания.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено или Не ЗначениеЗаполнено(ТекущиеДанные.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ДокументОснованиеКорректировки", ТекущиеДанные.ДокументОснование);
	
	НайденныеСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(
			, 
			НСтр("ru = 'Нельзя удалить документ, так на него есть ссылки в списке товаров'"),
			30,
			НСтр("ru = 'Удаление документа-основания'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПодключитьОбработчикОжидания("ПодсветитьДокументОснование", 0.2, Истина);
	
	ОткрытьФормуВыбораОснования(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованияДокументОснованиеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПодключитьОбработчикОжидания("ПодсветитьДокументОснование", 0.2, Истина);
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ПараметрыПолученияДанных.Отбор.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПараметрыПолученияДанных.Отбор.Вставить("Контрагент", Объект.Контрагент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииПоля(Элемент)
	
	ПриИзмененииПоляНаКлиенте(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПоляНаКлиенте(Элемент)
	
	Модифицированность = Истина;
	
	ПриИзмененииПоляНаСервере(Элемент.Имя);
	
	ИмяРеквизита = Элемент.Имя;
	ОбработкаДобавитьСоответствиеРаспознаваемыхСтрок(
		Объект,
		ВариантСохраненияСоответствий,
		НаборСоответствийРаспознанныхСтрок,
		ИмяРеквизита);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПоляНаСервере(ИмяПоля)
	
	Отбор = Новый Структура("ИмяРеквизита", ИмяПоля);
	НайденныеСтроки = Объект.РеквизитыДокумента.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() Тогда
		Если НЕ НайденныеСтроки[0].ЗаполненоВручную Тогда
			НайденныеСтроки[0].ЗаполненоВручную = Истина;
		КонецЕсли;
		НайденныеСтроки[0].ЭлементСоздан = Ложь;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, ИмяПоля) Тогда
			НайденныеСтроки[0].Значение = Объект[ИмяПоля];
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, ИмяПоля) Тогда
			НайденныеСтроки[0].Значение = ЭтотОбъект[ИмяПоля];
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИмяПоля = "Контрагент" Или ИмяПоля = "Организация" Тогда
		НайтиИЗаполнитьДокументыОснования();
		ПриИзмененииДокументаОснования();
		УправлениеГруппойОснований();
		НайтиИЗаполнитьСозданныеДокументы();
	КонецЕсли;
	
	РаспознаваниеДокументовСлужебный.ПересчитатьПроблемныеЭлементы(
		ЭтотОбъект, Объект, "Объект.Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АвтоПодборПоля(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если СтрЗаканчиваетсяНа(Элемент.Имя, "Белый") Тогда
		ИмяРеквизита = СтрЗаменить(Элемент.Имя, "Белый", "");
	ИначеЕсли СтрЗаканчиваетсяНа(Элемент.Имя, "Красный") Тогда
		ИмяРеквизита = СтрЗаменить(Элемент.Имя, "Красный", "");
	Иначе
		ИмяРеквизита = Элемент.Имя;
	КонецЕсли;
	
	Реквизит = РеквизитРаспознанногоДокумента(ИмяРеквизита);
	Если Реквизит = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоординатыКартинки = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьНаборКоординат(Реквизит);
	РаспознаваниеДокументовСлужебныйКлиент.ПриблизитьПоКоординатам(Элементы.ПолеПросмотра, ПолеПросмотраГотово,
		КоординатыКартинки, Реквизит.СтрокВИзображении);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаВыбораПоля(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Модифицированность = Истина;
	
	ИмяРеквизита = Элемент.Имя;
	
	Если ИмяРеквизита = "ДокументОснованиеКорректировки" Тогда
		СтандартнаяОбработка = Ложь;
		Элементы.Товары.ТекущиеДанные.ДокументОснованиеКорректировки = ВыбранноеЗначение;
		Элементы.Товары.ЗакончитьРедактированиеСтроки(Ложь);
		ЗаполнитьСтрокуПоДокументу(Элементы.Товары.ТекущиеДанные);
		Возврат;
	КонецЕсли;
	
	Если ИмяРеквизита = "НомерИсходнойСтроки" и ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		СтандартнаяОбработка = Ложь;
		Элементы.Товары.ТекущиеДанные.НомерИсходнойСтроки = Число(СтрРазделить(ВыбранноеЗначение, " ")[0]);
		Элементы.Товары.ТекущиеДанные.ЭтоУслуга = СтрЗаканчиваетсяНа(ВыбранноеЗначение, "(Услуга)");
		Элементы.Товары.ЗакончитьРедактированиеСтроки(Ложь);
		ЗаполнитьСтрокуПоДокументу(Элементы.Товары.ТекущиеДанные);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка") Тогда
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура")
			И ВыбранноеЗначение.Свойство("СозданВФормеБРД") Тогда
			
			ВыбранноеЗначение = ВыбранноеЗначение.Ссылка;
			СозданНовыйЭлемент = Истина;
		Иначе
			// Выбрали значение из выпадающего списка
			СозданНовыйЭлемент = Ложь;
		КонецЕсли;
		
		Если ТекущийЭлемент = Элементы.Товары Тогда
			ТекущиеДанные = Элементы["Товары"].ТекущиеДанные;
			
			// В редких случаях в веб клиенте ТекущийЭлемент будет таблицей, хотя редактировалась шапка,
			// поэтому сделаем дополнительную проверку
			Если ТекущиеДанные.Свойство(ИмяРеквизита + "ЭлементСоздан") Тогда
				ТекущиеДанные[ИмяРеквизита + "ЭлементСоздан"] = СозданНовыйЭлемент;
			Иначе
				НовыйЭлементСозданВФормеБРД = СозданНовыйЭлемент;
			КонецЕсли;
		Иначе
			НовыйЭлементСозданВФормеБРД = СозданНовыйЭлемент;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если ИмяРеквизита = "Организация" ИЛИ ИмяРеквизита = "Контрагент"
		Или СправочникиБезПравНаСоздание.НайтиПоЗначению(ИмяРеквизита) <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)

	Если ТипЗнч(ТекущийЭлемент) <> Тип("ТаблицаФормы") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийВыбранныйЭлемент = Элемент.ТекущийЭлемент;
	Если ТекущийВыбранныйЭлемент = Неопределено Или Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = СтрЗаменить(ТекущийВыбранныйЭлемент.Имя, "Товары", "");
	НомерСтрокиТЧ = Элемент.ТекущиеДанные.НомерСтрокиВДокументе;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
		Элемент.ТекущиеДанные,
		ИмяРеквизита + "ТекстОшибки") Тогда
		
		НадписьОшибкаТЧ = Элемент.ТекущиеДанные[ИмяРеквизита + "ТекстОшибки"];
	Иначе
		НадписьОшибкаТЧ = "";
	КонецЕсли;
	
	Элементы.НадписьОшибкаТЧ.Видимость = Не ПустаяСтрока(НадписьОшибкаТЧ);
	
	ЭлементКолонка = Элементы[ИмяРеквизита];
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементКолонка, "СписокВыбора") Тогда
		Возврат;
	КонецЕсли;
	ЭлементКолонка.СписокВыбора.Очистить();
	
	Ключ = РаспознаваниеДокументовСлужебныйКлиентСервер.КлючСвойстваЯчеекТаблицы(ИмяРеквизита, НомерСтрокиТЧ);
	ДанныеСтрокиТаблицы = ПолучитьСтрокуРеквизитыТабличныхЧастейКакСтруктуру(ИмяРеквизита, НомерСтрокиТЧ);

	МетаданныеЯчейки = ПреобразоватьСтрокуТабличнойЧасти(ДанныеСтрокиТаблицы, Ключ, Объект.Ссылка);//РаспознаваниеДокументовСлужебныйКлиентСервер.МетаданныеЯчейки(СвойстваЯчеекТаблицы, НомерСтрокиТЧ, ИмяРеквизита);
	Если МетаданныеЯчейки = Неопределено Тогда
		ДанныеПриближения = Новый Структура("ПолеПросмотра, Координаты, СтрокВИзображении",Элементы.ПолеПросмотра, Неопределено, 0);
		ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.2, Истина);
		Возврат;
	КонецЕсли;
	МетаданныеЯчейки = МетаданныеЯчейки[Ключ];
	ДанныеПриближения = Новый Структура("ПолеПросмотра, Координаты, СтрокВИзображении", Элементы.ПолеПросмотра, МетаданныеЯчейки.Координаты, МетаданныеЯчейки.СтрокВИзображении);
	ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.2, Истина);
	
	Если ТекущийВыбранныйЭлемент.Имя = "ДокументОснованиеКорректировки" Тогда
		Возврат;
	КонецЕсли;
	
	СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиент.ПолучитьЗначенияСпискаВыбора(
		МетаданныеЯчейки.РаспознанныйТекст, МетаданныеЯчейки.ТипЗначения, МетаданныеЯчейки.ЗначенияВыбора,
		СправочникиБезПравНаСоздание.НайтиПоЗначению(ИмяРеквизита) = Неопределено);
	
	Для Каждого ДанныеВыбора Из СписокДляВыбора Цикл
		ЭлементКолонка.СписокВыбора.Добавить(ДанныеВыбора.Значение, ДанныеВыбора.Представление, , ДанныеВыбора.Картинка);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущийВыбранныйЭлемент = Элемент.ТекущийЭлемент;
	Если ТекущийВыбранныйЭлемент = Неопределено Или Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущийВыбранныйЭлемент.Имя = "НомерИсходнойСтроки" Тогда
		
		Если Объект.ДокументыОснованияКорректировки.Количество() = 1
			И ЗначениеЗаполнено(Объект.ДокументыОснованияКорректировки[0].ДокументОснование) Тогда
			Элемент.ТекущиеДанные.ДокументОснованиеКорректировки = Объект.ДокументыОснованияКорректировки[0].ДокументОснование;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриблизитьПоКоординатам()
	
	РаспознаваниеДокументовСлужебныйКлиент.ПриблизитьПоКоординатам(ДанныеПриближения.ПолеПросмотра, ПолеПросмотраГотово,
		ДанныеПриближения.Координаты, ДанныеПриближения.СтрокВИзображении)
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументОснованиеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущееЗначениеДокументаВСтроке = ТекущиеДанные.ДокументОснованиеКорректировки;
	
	ДанныеВыбора = Новый СписокЗначений;
	Для Каждого СтрокаТаблицы Из Объект.ДокументыОснованияКорректировки Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ДокументОснование) Тогда
			ДанныеВыбора.Добавить(СтрокаТаблицы.ДокументОснование);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ТоварыДокументОснованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	Модифицированность = Истина;
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Товары.ЗакончитьРедактированиеСтроки(Ложь);
	
	Если ПредыдущееЗначениеДокументаВСтроке = ВыбранноеЗначение Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ДокументОснованиеКорректировки = ВыбранноеЗначение;
	ТекущиеДанные.ДокументОснованиеКорректировкиЗаполненоВручную = Истина;
	
	// Значение документа-основания поменялось, поэтому нужно очистить номер строки и номенклатуру в строке с товарами
	ЗаполнитьСтрокуПоДокументу(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработкаАктивизацииСтрокиТоваров", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНомерИсходнойСтрокиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокВыбораЭлемента = Новый СписокЗначений;
	ЗаполнитьСписокВыбораНомерСтрокиИсходногоДокумента(СписокВыбораЭлемента);
	Контекст = Новый Структура();
	Контекст.Вставить("Элемент", Элемент);
	Контекст.Вставить("ДанныеВыбора", СписокВыбораЭлемента);
	ОбработчикВыбора = Новый ОписаниеОповещения("ПослеВыбораЗначенияИзСпискаВыбора", ЭтотОбъект, Контекст);
	Если СписокВыбораЭлемента.Количество() <= 2 Тогда
		ПоказатьВыборИзСписка(ОбработчикВыбора, СписокВыбораЭлемента, Элемент);
	Иначе
		ОткрытьФорму("Обработка.КорректировкаРеализацииПоступленияРаспознаваниеДокументов.Форма.ВыборНоменклатуры",
			Новый Структура("Варианты", СписокВыбораЭлемента),
			ЭтаФорма,
			Истина,,,
			ОбработчикВыбора,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Конецесли;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерИсходнойСтрокиАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		ДанныеВыбора = Новый СписокЗначений;
		Возврат;
	КонецЕсли;
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	ЗаполнитьСписокВыбораНомерСтрокиИсходногоДокумента(ДанныеВыбора, Текст);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПожаловатьсяНаКачество(Команда)
	
	РаспознаваниеДокументовСлужебныйКлиент.ОткрытьФормуОбратнойСвязи(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИдентификаторДокумента(Команда)
	
	ПараметрыСообщения = Новый Структура;
	ПараметрыСообщения.Вставить("Заголовок", НСтр("ru = 'Идентификатор документа'"));
	ПараметрыСообщения.Вставить("Сообщение", Объект.ИдентификаторРезультата);
	ПараметрыСообщения.Вставить("МногострочныйРежим", Ложь);
	
	ОткрытьФорму("Обработка.РаспознаваниеДокументов.Форма.ФормаСообщениеБРД", ПараметрыСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКорректировкаПоступления(Команда)
	
	Если Не РаспознаваниеДокументовСлужебныйВызовСервера.ИспользоватьКорректировочныеДокументы() Тогда
		ВызватьИсключение НСтр("ru = 'Для создания корректировок необходимо включить настройку ""Исправительные и корректировочные документы""'");
	КонецЕсли;
	
	ПараметрыОперации = РаспознаваниеДокументовКомплектыКлиентСервер.ТипДокументаИВидОперации(Команда.Имя);
	РаспознаваниеДокументовСлужебныйКлиент.ПоказатьСозданиеДокумента(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВозвратТоваровПоставщику(Команда)
	
	ПараметрыОперации = РаспознаваниеДокументовКомплектыКлиентСервер.ТипДокументаИВидОперации(Команда.Имя);
	РаспознаваниеДокументовСлужебныйКлиент.ПоказатьСозданиеДокумента(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда = Неопределено) Экспорт
	
	ЗаписатьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОснование(Команда)
	
	Элементы.ГруппаОснования.ТекущаяСтраница = Элементы.ГруппаОснованияМного;
	
	ПервоеОснованиеЗаполнено = Объект.ДокументыОснованияКорректировки.Количество()
		И ЗначениеЗаполнено(Объект.ДокументыОснованияКорректировки[0].ДокументОснование);
	
	Если ПервоеОснованиеЗаполнено Или Объект.ДокументыОснованияКорректировки.Количество() = 0 Тогда
		Элементы.Основания.ДобавитьСтроку();
	Иначе
		Элементы.Основания.ТекущаяСтрока = 0;
	КонецЕсли;
		
	Элементы.Основания.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРаспознанныйДокумент(Команда)
	
	Ид = Число(СтрРазделить(Команда.Имя, "_")[1]);
	ОткрытьДокументПоСсылке(ДокументыКомплектные[Ид].Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДубльДокумента(Команда)
	
	Ид = Число(СтрРазделить(Команда.Имя, "_")[1]);
	ОткрытьДокументПоСсылке(ДокументыДубли[Ид].ДубльДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСозданныйДокумент(Команда)
	
	Ид = Число(СтрРазделить(Команда.Имя, "_")[1]);
	ОткрытьДокументПоСсылке(ДокументыСозданные[Ид].СозданныйДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура Перепроверить(Команда)
	
	НайтиИЗаполнитьДокументыОснования();
	ПриИзмененииДокументаОснования();
	УправлениеГруппойОснований();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКОшибке(Команда)
	
	СтрокаПроблемногоЭлемента = Элементы.ПроблемныеЭлементы.ТекущиеДанные;
	Если СтрокаПроблемногоЭлемента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяЭлемента = СтрокаПроблемногоЭлемента.ИмяРеквизита;
	
	Если ЗначениеЗаполнено(СтрокаПроблемногоЭлемента.НомерСтроки) Тогда
		ТекущийЭлемент = Элементы.ТаблицаДокумента;
		Элементы.ТаблицаДокумента.ТекущийЭлемент = Неопределено;
		Элементы.ТаблицаДокумента.ТекущийЭлемент = Элементы[ИмяЭлемента];
		Элементы.ТаблицаДокумента.ТекущаяСтрока = СтрокаПроблемногоЭлемента.НомерСтроки - 1;
	Иначе
		
		Отбор = Новый Структура("ИмяРеквизита", ИмяЭлемента);
		НайденныеСтроки = Объект.РеквизитыДокумента.НайтиСтроки(Отбор);
		
		Если Не НайденныеСтроки.Количество() Тогда
			Возврат;
		КонецЕсли;
		
		Если ИмяЭлемента = "ИтогоСумма"
			Или ИмяЭлемента = "ИтогоСуммаНДС"
			Или ИмяЭлемента = "ИтогоВсего" Тогда
			ИмяЭлемента = ИмяЭлемента + "Красный";
		КонецЕсли;
		
		ТекущийЭлемент = Элементы[ИмяЭлемента];
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеПроверкиИзменения(ТребуетсяИзменениеФормы, Контекст) Экспорт
	
	// Нужна для обработки оповещения из ИзменитьТипДокумента
	Если ТребуетсяИзменениеФормы Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СоздатьНаСервере(ПараметрыОперации)
	
	ПеренестиОснования();
	
	НачатьТранзакцию();
	Попытка
		
		Объект.Статус = Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан;
		
		ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
		ОбработкаОбъект.Записать(ЭтотОбъект);
		
		Результат = ОбработкаОбъект.СоздатьДокументы(ПараметрыОперации);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Результат;
	
	//ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецФункции

&НаСервере
Процедура ЗаписатьНаСервере()
	
	ПеренестиОснования();
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Записать(ЭтотОбъект);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#Область УправлениеФормой

#Область УсловноеОформление

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Условное оформление, связанное с видимостью, устанавливаем сразу для всех колонок.
	УстановитьУсловноеОформлениеВидимость();
	УстановитьУсловноеОформлениеФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеВидимость()
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Номенклатура");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЦенаВРознице");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СтавкаНДСВРознице");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетУчета");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетУчетаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОтражениеВУСН");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Контрагент");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДоговорКонтрагента");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетРасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СпособУчетаНДС");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ЕстьВДокументеПоступленияРеализации", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УправлениеГруппойОснований()
	
	Элементы.ГруппаОснования.ТекущаяСтраница = Элементы.ГруппаОснованияОдно;
	
	Если Объект.ДокументыОснованияКорректировки.Количество() > 1 Тогда
		Элементы.ГруппаОснования.ТекущаяСтраница = Элементы.ГруппаОснованияМного;
	ИначеЕсли Объект.ДокументыОснованияКорректировки.Количество() = 1 Тогда
		ДокументОснованиеКорректировки = Объект.ДокументыОснованияКорректировки[0].ДокументОснование;
		ДокументОснованиеКорректировкиРаспознанныйТекст = Объект.ДокументыОснованияКорректировки[0].РаспознанныйТекст;
	КонецЕсли;
	
	Элементы.ДокументОснованиеКорректировки.Видимость = Объект.ДокументыОснованияКорректировки.Количество() >=2;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииДокументаОснования()
	
	Для Каждого СтрокаТаблицыТовары Из Объект.Товары Цикл
		НастройкаОтбора = Новый Структура("ДокументОснование", СтрокаТаблицыТовары.ДокументОснованиеКорректировки);
		НайденныеСтроки = Объект.ДокументыОснованияКорректировки.НайтиСтроки(НастройкаОтбора);
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаТаблицыТовары.ДокументОснованиеКорректировки = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Если Объект.ДокументыОснованияКорректировки.Количество() = 0 Тогда
		Элементы.ДокументОснованиеКорректировки.Видимость = Истина;
		Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
			СтрокаТаблицы.ДокументОснованиеКорректировки = Неопределено;
		КонецЦикла;
	ИначеЕсли Объект.ДокументыОснованияКорректировки.Количество() = 1 Тогда
		Элементы.ДокументОснованиеКорректировки.Видимость = Ложь;
		Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ДокументОснованиеКорректировки) Тогда
				СтрокаТаблицы.ДокументОснованиеКорректировки = Объект.ДокументыОснованияКорректировки[0].ДокументОснование;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Элементы.ДокументОснованиеКорректировки.Видимость = Истина;
	КонецЕсли;
	
	ЗаполнитьСтрокиИзОснований();
	
	Для Каждого Строка Из Объект.Товары Цикл
		
		Если Не ЗначениеЗаполнено(Строка.ДокументОснованиеКорректировки) Тогда
			ОчиститьЗначенияПоДокументу(Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	РаспознаваниеДокументовСлужебный.ПересчитатьПроблемныеЭлементы(
		ЭтотОбъект, Объект, "Объект.Товары");
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтрокуПоДокументу(ТекущиеДанные)
	
	ДанныеСтроки = ПолучитьРеквизитыСтрокиТаблицыДокумента(
		ТекущиеДанные.ДокументОснованиеКорректировки,
		ТекущиеДанные.НомерИсходнойСтроки,
		ТекущиеДанные.ЭтоУслуга);
		
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтроки);
	ТекущиеДанные.ВсегоПоДокументу = ТекущиеДанные.СуммаПоДокументу + ?(Объект.СуммаВключаетНДС, 0, ТекущиеДанные.СуммаНДСПоДокументу);
	ТекущиеДанные.НомерИсходнойСтрокиЗаполненоВручную = Истина;
	
	РаспознаваниеДокументовСлужебныйКлиентСервер.ПересчитатьПроблемныеРеквизитыТаблицыДокумента(
		ЭтотОбъект,
		ТекущиеДанные,
		"Объект.Товары");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокиИзОснований()
	
	РаспознаваниеДокументовПереопределяемый.ЗаполнитьСтрокиТаблицыТоварыКорректировкиИзОснований(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиОснования()
	
	Если Элементы.ГруппаОснования.ТекущаяСтраница = Элементы.ГруппаОснованияОдно Тогда 
		
		Если Объект.ДокументыОснованияКорректировки.Количество() > 0 Тогда
			СтрокаОснований = Объект.ДокументыОснованияКорректировки[0];
		Иначе
			СтрокаОснований = Объект.ДокументыОснованияКорректировки.Добавить();
		КонецЕсли;
		
		СтрокаОснований.ДокументОснование = ДокументОснованиеКорректировки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()
	
	Объект.РеквизитыТабличныхЧастей.Сортировать("НомерСтрокиТЧ");
	ИзменяемаяСтрока = Объект.Товары[0];
	
	Для Каждого Запись Из Объект.РеквизитыТабличныхЧастей Цикл
		Если Запись.СтрокаУдалена Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИзменяемаяСтрока.НомерСтрокиВДокументе <> Запись.НомерСтрокиТЧ Тогда
			Отбор = Новый Структура("НомерСтрокиВДокументе", Запись.НомерСтрокиТЧ);
			ИзменяемыеСтроки = Объект.Товары.НайтиСтроки(Отбор);
			ИзменяемаяСтрока = ИзменяемыеСтроки[0];
		КонецЕсли;
		
		Если ИзменяемаяСтрока.Свойство(Запись.ИмяРеквизита + "ЗаполненоВручную") Тогда
			ИзменяемаяСтрока[Запись.ИмяРеквизита + "ЗаполненоВручную"] = Запись.ЗаполненоВручную;
		КонецЕсли;
		Если ИзменяемаяСтрока.Свойство(Запись.ИмяРеквизита + "ЭлементСоздан") Тогда
			ИзменяемаяСтрока[Запись.ИмяРеквизита + "ЭлементСоздан"] = Запись.ЭлементСоздан;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
		СведенияОНоменклатуре = Неопределено;
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(ЭтотОбъект, 
			СтрокаТаблицы, Объект.СуммаВключаетНДС, СведенияОНоменклатуре);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(Форма, СтрокаТаблицы, СуммаВключаетНДС, СведенияОНоменклатуре)
	
	СтрокаТаблицы.Всего                = СтрокаТаблицы.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	СтрокаТаблицы.ВсегоДоКорректировки = СтрокаТаблицы.СуммаДоКорректировки + ?(СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДСДоКорректировки);
	СтрокаТаблицы.ВсегоПоДокументу     = СтрокаТаблицы.СуммаПоДокументу + ?(СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДСПоДокументу);
	
	СтрокаТаблицы.НоменклатураЕдиницаИзмерения = ?(СведенияОНоменклатуре <> Неопределено, 
		СведенияОНоменклатуре.ЕдиницаИзмерения, 
		Неопределено);
	
	СтрокаТаблицы.НадписьПоДокументу     = НСтр("ru = 'до (в основании)'");
	СтрокаТаблицы.НадписьДоКорректировки = НСтр("ru = 'до (распознано):'");
	СтрокаТаблицы.НадписьПослеИзменения  = НСтр("ru = 'после (распознано):'");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеДокументаОснования(ДокументСсылка)
	
	ВозвращаемыеДанные = Новый Структура;
	
	ТоварыИУслугиДокумента = Новый Массив;
	РаспознаваниеДокументовПереопределяемый.СтрокиДокументаОснованияКорректировки(ДокументСсылка, ТоварыИУслугиДокумента);
	
	ДоговорКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ДокументСсылка, "ДоговорКонтрагента");
	
	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДоговорКонтрагента, "ВидДоговора, НДСПоСтавкам4и2");
	
	ВидДоговораДокумента = РеквизитыДоговора.ВидДоговора;
	ПрименяютсяСтавки4и2 = РеквизитыДоговора.НДСПоСтавкам4и2;
	ВозвращаемыеДанные.Вставить("ТоварыИУслугиДокумента", ТоварыИУслугиДокумента);
	ВозвращаемыеДанные.Вставить("ВидДоговораДокумента", ВидДоговораДокумента);
	ВозвращаемыеДанные.Вставить("ПрименяютсяСтавки4и2", ПрименяютсяСтавки4и2);
	
	Возврат ВозвращаемыеДанные;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаАктивизацииСтрокиТоваров()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущаяСтрокаТаблицы = Элементы.Товары.ТекущаяСтрока;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокВыбораНомерСтрокиИсходногоДокумента(ДанныеВыбора, Текст = "")
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ДокументОснованиеКорректировки) Тогда
		ДанныеВыбора = Новый СписокЗначений;
		Возврат;
	Конецесли;
	
	ДанныеДокумента = ДанныеДокументаОснования(ТекущиеДанные.ДокументОснованиеКорректировки);

	Если ТипЗнч(ДанныеВыбора) <> Тип("СписокЗначений") Тогда
		ДанныеВыбора = Новый СписокЗначений;
	Иначе
		ДанныеВыбора.Очистить();
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из ДанныеДокумента.ТоварыИУслугиДокумента Цикл
		Если ЗначениеЗаполнено(Текст) И СтрНайти(Строка(ЭлементМассива.НомерСтроки), Текст) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстТоварУслуга = ?(ЭлементМассива.ЭтоУслуга, "(Услуга)", "(Товар)");
		
		Если ДанныеВыбора.НайтиПоЗначению(ЭлементМассива.ПредставлениеНоменклатуры) = Неопределено Тогда
			СтроковоеПредставление = СтрШаблон("%1 %2 %3",
				ЭлементМассива.НомерСтроки, ЭлементМассива.ПредставлениеНоменклатуры, ТекстТоварУслуга);
			ДанныеВыбора.Добавить(СтроковоеПредставление);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораЗначенияИзСпискаВыбора(ВыбранноеЗначение, Контекст) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если Контекст.Элемент = Элементы.НомерИсходнойСтроки Тогда
		НомерСтроки = СтрРазделить(ВыбранноеЗначение.Значение, " ")[0];
		ТекущиеДанные.ЭтоУслуга = СтрЗаканчиваетсяНа(ВыбранноеЗначение.Значение, "(Услуга)");
		ТекущиеДанные.НомерИсходнойСтроки = НомерСтроки;
		Элементы.Товары.ЗакончитьРедактированиеСтроки(Ложь);
		ТекущаяСтрокаТаблицы = Элементы.Товары.ТекущаяСтрока;
		ЗаполнитьСтрокуПоДокументу(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитыСтрокиТаблицыДокумента(Документ, НомерСтроки, ЭтоУслуга)
	
	ДанныеСтроки = Новый Структура;
	ДанныеСтроки.Вставить("КоличествоПоДокументу", 0);
	ДанныеСтроки.Вставить("ЦенаПоДокументу", 0);
	ДанныеСтроки.Вставить("СуммаПоДокументу", 0);
	ДанныеСтроки.Вставить("СтавкаНДСПоДокументу", Неопределено);
	ДанныеСтроки.Вставить("СуммаНДСПоДокументу", 0);
	ДанныеСтроки.Вставить("СтранаПроисхожденияПоДокументу", Неопределено);
	ДанныеСтроки.Вставить("НоменклатураЕдиницаИзмерения", Неопределено);
	ДанныеСтроки.Вставить("ЭтоУслуга", ЭтоУслуга);
	
	РаспознаваниеДокументовПереопределяемый.СвойстваСтрокиДокументаОснованияКорректировки(
		Документ,
		НомерСтроки,
		ДанныеСтроки);
		
	Возврат ДанныеСтроки;
	
КонецФункции

&НаСервере
Процедура НастроитьЭлементыПоРеквизитам()
	
	ДанныеДляВыбораРеквизитов = РаспознаваниеДокументовСлужебный.ПолучитьДанныеДляСпискаВыбора(Объект.Ссылка);
	АдресЗначенияВыбораДляШапки = ПоместитьВоВременноеХранилище(ДанныеДляВыбораРеквизитов, УникальныйИдентификатор);
	ОсновныеРеквизиты = РаспознаваниеДокументовСлужебный.ОсновныеРеквизиты(Объект);
	Для Каждого РеквизитШапки Из Объект.РеквизитыДокумента Цикл
		
		ИмяРеквизита = РеквизитШапки.ИмяРеквизита;
		ИмяРеквизита = РаспознаваниеДокументовСлужебный.РеквизитОбработкиИзОсновногоРеквизита(Объект,ИмяРеквизита);
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, ИмяРеквизита) Тогда
			
			ИзменяемыйЭлемент = Элементы[ИмяРеквизита];
			НастроитьСвойстваЭлементаРеквизита(ИзменяемыйЭлемент);
			НастроитьИзменяемыйЭлемент(РеквизитШапки, ИзменяемыйЭлемент, ДанныеДляВыбораРеквизитов);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьИзменяемыйЭлемент(РеквизитШапки, ИзменяемыйЭлемент, ДанныеДляВыбораРеквизитов)
	
	ИзменяемыйЭлемент.Подсказка = Неопределено;
	РаспознанныйТекст = РеквизитШапки.РаспознанныйТекст;
	ИмяРеквизита = РеквизитШапки.ИмяРеквизита;
	ИмяРеквизита = РаспознаваниеДокументовСлужебный.РеквизитОбработкиИзОсновногоРеквизита(Объект, ИмяРеквизита);
	Если ИмяРеквизита = "СуммаВключаетНДС" Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПримитивныйТип = РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗнч(РеквизитШапки.Значение));
	
	Отбор = Новый Структура("ИмяРеквизита", РеквизитШапки.ИмяРеквизита);
	ПодходящиеЗначения = ДанныеДляВыбораРеквизитов.НайтиСтроки(Отбор);
	
	Если ИмяРеквизита = "ПродавецОрганизация"
		Или ИмяРеквизита = "ПокупательОрганизация"
		Или ИмяРеквизита = "Исполнитель"
		Или ИмяРеквизита = "Организация"
		Или ИмяРеквизита = "Контрагент"
		Или СправочникиБезПравНаСоздание.НайтиПоЗначению(ИмяРеквизита) <> Неопределено
		Или ЭтоПримитивныйТип Тогда
		
		КартинкаСоздание = 2;
	Иначе
		КартинкаСоздание = 1;
	КонецЕсли;
	
	Если Не РаспознаваниеДокументовКлиентСервер.РаспознанныйТекстСодержитПустоеЗначениеПоля(ИмяРеквизита, РаспознанныйТекст) Тогда
		СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьСписокДляВыбораПользователем(РаспознанныйТекст, ПодходящиеЗначения, КартинкаСоздание);
		ИзменяемыйЭлемент.СписокВыбора.Очистить();
		Для Каждого ДанныеВыбора Из СписокДляВыбора Цикл
			Если ТипЗнч(ДанныеВыбора.Значение) = Тип("Структура") Тогда
				ИзменяемыйЭлемент.СписокВыбора.Добавить(ДанныеВыбора.Значение.Значение, ДанныеВыбора.Представление, , ДанныеВыбора.Картинка);
			Иначе
				Если ЭтоПримитивныйТип Тогда
					ИзменяемыйЭлемент.СписокВыбора.Добавить(ДанныеВыбора.Значение, ДанныеВыбора.Представление);
				Иначе
					ИзменяемыйЭлемент.СписокВыбора.Добавить(ДанныеВыбора.Значение, ДанныеВыбора.Представление, , ДанныеВыбора.Картинка);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РеквизитШапки.Значение) Тогда
		ИзменяемыйЭлемент.ПодсказкаВвода = СтрШаблон(НСтр("ru = 'Не сопоставлен: %1'"), РаспознанныйТекст);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСвойстваЭлементаРеквизита(ИзменяемыйЭлемент, ЗапретитьИсториюВыбора = Истина, РазрешитьБыстрыйВыбор = Ложь)
	
	Если ИзменяемыйЭлемент.Имя = "СуммаВключаетНДС"
		Или ИзменяемыйЭлемент.Имя = "ДокументОснованиеКорректировки" Тогда
		Возврат;
	КонецЕсли;
	
	ИзменяемыйЭлемент.БыстрыйВыбор = РазрешитьБыстрыйВыбор;
	ИзменяемыйЭлемент.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Если ЗапретитьИсториюВыбора Тогда
		ИзменяемыйЭлемент.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	КонецЕсли;
	ИзменяемыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииПоля");
	ИзменяемыйЭлемент.УстановитьДействие("АвтоПодбор", "Подключаемый_АвтоПодборПоля");
	ИзменяемыйЭлемент.УстановитьДействие("ОбработкаВыбора", "Подключаемый_ОбработкаВыбораПоля");
	Если РазрешитьБыстрыйВыбор Тогда
		ИзменяемыйЭлемент.УстановитьДействие("НачалоВыбора", "Подключаемый_НачалоВыбораПоля");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьИзображенияРеквизитов(Настройки, Родитель = Неопределено, ГруппаРеквизита = Неопределено)
	
	Если Родитель = Неопределено Тогда
		Родитель = Элементы.ГруппаРеквизиты;
	КонецЕсли;
	
	СоответствиеПолейИГрупп = Новый Соответствие;
	
	Для Каждого Поле Из Родитель.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(Поле) = Тип("ГруппаФормы") Тогда
			ДобавитьИзображенияРеквизитов(Поле, ГруппаРеквизита);
			Продолжить;
		ИначеЕсли НЕ ТипЗнч(Поле) = Тип("ПолеФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		ГруппаРеквизита = Элементы.Добавить("Группа" + Поле.Имя, Тип("ГруппаФормы"), Поле.Родитель);
		ГруппаРеквизита.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаРеквизита.ОтображатьЗаголовок = Ложь;
		СоответствиеПолейИГрупп.Вставить(Поле, ГруппаРеквизита);
		
	КонецЦикла;
	
	Для Каждого ДанныеСоответствия Из СоответствиеПолейИГрупп Цикл
		Элементы.Переместить(ДанныеСоответствия.Ключ, ДанныеСоответствия.Значение);
		ОтобразитьСвязаннуюКартинку(ДанныеСоответствия);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСвязаннуюКартинку(ДанныеСоответствия)
	
	Путь = ДанныеСоответствия.Ключ.ПутьКДанным;
	НачалоИдентификатора = СтрНайти(Путь, "[") ;
	Если НЕ НачалоИдентификатора Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторРеквизита = Сред(Путь, НачалоИдентификатора + 1, СтрНайти(Путь, "]") - НачалоИдентификатора + 1);
	ОписаниеЧисла = Новый ОписаниеТипов("Число");
	ИдентификаторРеквизита = ОписаниеЧисла.ПривестиЗначение(ИдентификаторРеквизита);
	
	ПолеКартинки = Элементы.Добавить("КартинкаРеквизита" + ИдентификаторРеквизита, Тип("ПолеФормы"), ДанныеСоответствия.Значение);
	ПолеКартинки.Вид = ВидПоляФормы.ПолеКартинки;
	ПолеКартинки.Высота = Объект.РеквизитыДокумента[ИдентификаторРеквизита].СтрокВИзображении;
	ПолеКартинки.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ПолеКартинки.РазмерКартинки = РазмерКартинки.Пропорционально;
	ПолеКартинки.РастягиватьПоВертикали = Ложь;
	ПолеКартинки.АвтоМаксимальнаяВысота = Ложь;
	ПолеКартинки.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Центр;
	ПолеКартинки.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	ПолеКартинки.ПутьКДанным = "Объект.РеквизитыДокумента[" + ИдентификаторРеквизита + "].АдресКартинки";
	
КонецПроцедуры

Процедура НастроитьКолонкиТаблицы()
	
	ДобавляемыеРеквизиты = Новый Массив;
	НастроитьКолонкиРекурсивно(ДобавляемыеРеквизиты, Элементы.Товары);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	СоздатьЭлементыДобавленныхРеквизитов(ДобавляемыеРеквизиты, Элементы.Товары);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКолонкиРекурсивно(ДобавляемыеРеквизиты, ЭлементыОбхода)
	
	Для Каждого ЭлементКолонка Из ЭлементыОбхода.ПодчиненныеЭлементы Цикл
		Если ЭлементКолонка.Вид = ВидГруппыФормы.ГруппаКолонок Тогда
			НастроитьКолонкиРекурсивно(ДобавляемыеРеквизиты, ЭлементКолонка);
		ИначеЕсли ЭлементКолонка.Вид = ВидПоляФормы.ПолеВвода Тогда
			ЭлементКолонка.ВыбиратьТип = Ложь;
			ЭлементКолонка.БыстрыйВыбор = Ложь;
			ЭлементКолонка.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
			ЭлементКолонка.УстановитьДействие("ОбработкаВыбора", "Подключаемый_ОбработкаВыбораПоля");
			
			Если ПустаяСтрока(ЭлементКолонка.ПолучитьДействие("ПриИзменении")) Тогда
				ЭлементКолонка.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииКолонки");
			КонецЕсли;
				
			Если ЭлементКолонка.Имя <> "ТаблицаДокументаПорядокСтроки"
				И Не СтрЗаканчиваетсяНа(ЭлементКолонка.Имя, "ТекстОшибки")
				И Не СтрЗаканчиваетсяНа(ЭлементКолонка.Имя, "ЗаполненоВручную")
				И Не СтрЗаканчиваетсяНа(ЭлементКолонка.Имя, "ЭлементСоздан") Тогда
				
				НовыйРеквизит = Новый РеквизитФормы(ЭлементКолонка.Имя + "ТекстОшибки", Новый ОписаниеТипов("Строка"));
				НовыйРеквизит.Путь = "Объект.Товары";
				ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
				
				НовыйРеквизит = Новый РеквизитФормы(ЭлементКолонка.Имя + "ЗаполненоВручную", Новый ОписаниеТипов("Булево"));
				НовыйРеквизит.Путь = "Объект.Товары";
				ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
				
				НовыйРеквизит = Новый РеквизитФормы(ЭлементКолонка.Имя + "ЭлементСоздан", Новый ОписаниеТипов("Булево"));
				НовыйРеквизит.Путь = "Объект.Товары";
				ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыДобавленныхРеквизитов(ДобавляемыеРеквизиты, ЭлементРодитель)
	
	Для Каждого Реквизит Из ДобавляемыеРеквизиты Цикл 
		НовыйЭлемент = Элементы.Добавить(Реквизит.Имя, Тип("ПолеФормы"), ЭлементРодитель);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = "Объект.Товары." + Реквизит.Имя;
		НовыйЭлемент.Видимость = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииКолонки(Элемент)
	
	Модифицированность = Истина;
	
	ИдентификаторСтроки = Элементы["Товары"].ТекущаяСтрока;
	
	ИмяРеквизита = Элемент.Имя;
	ТекущиеДанные = Элементы["Товары"].ТекущиеДанные;
	НомерСтрокиТЧ = ТекущиеДанные.НомерСтроки;
	ВыбранноеЗначение = ТекущиеДанные[ИмяРеквизита];
	
	ТекущиеДанные[ИмяРеквизита + "ЗаполненоВручную"] = Истина;
	
	РаспознаваниеДокументовСлужебныйКлиентСервер.ПриИзмененииКолонки(
		Объект,
		ТекущиеДанные,
		ИмяРеквизита,
		ВыбранноеЗначение
	);
	
	РаспознаваниеДокументовСлужебныйКлиентСервер.ПересчитатьПроблемныеРеквизитыТаблицыДокумента(
		ЭтотОбъект,
		ТекущиеДанные,
		"Объект.Товары");
	
	Отбор = Новый Структура("ИмяРеквизита, НомерСтроки", ИмяРеквизита, НомерСтрокиТЧ);
	НайденныеСтроки = ПроблемныеЭлементы.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() Тогда
		НадписьОшибкаТЧ = ТекущиеДанные[ИмяРеквизита + "ТекстОшибки"];
	Иначе
		НадписьОшибкаТЧ = "";
	КонецЕсли;
	
	Элементы.НадписьОшибкаТЧ.Видимость = НЕ ПустаяСтрока(НадписьОшибкаТЧ);
	
	СтрокиТаблицы = Объект.РеквизитыТабличныхЧастей.НайтиСтроки(Новый Структура("ИмяРеквизита, НомерСтрокиТЧ", ИмяРеквизита, НомерСтрокиТЧ));
	Если СтрокиТаблицы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	СтрокаТаблицы = СтрокиТаблицы[0];
	СтрокаТаблицы.Значение = ТекущиеДанные[ИмяРеквизита];
	СтрокаТаблицы.ЗаполненоВручную = Истина;
	
	РаспознаваниеДокументовСлужебныйКлиент.ДобавитьСоответствиеРаспознаваемыхСтрок(
		Объект,
		ВариантСохраненияСоответствий,
		НаборСоответствийРаспознанныхСтрок,
		ИмяРеквизита,
		ТекущиеДанные.НомерСтроки,
		ТекущиеДанные[ИмяРеквизита],
		СтрокаТаблицы
	);
	
	Если ИмяРеквизита = "НомерИсходнойСтроки" Тогда
		ЗаполнитьСтрокуПоДокументу(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗагрузитьСвойстваЯчеекТаблицы() Экспорт
	
	СвойстваЯчеекТаблицы = ПолучитьИзВременногоХранилища(Объект.АдресСвойствЯчеекТаблицы);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	// Оформление ячеек с ошибками
	
	ТаблицаДокументаТЗ = Объект.Товары.Выгрузить();
	КолонкиТаблицыДокумента = ТаблицаДокументаТЗ.Колонки;
	
	Для Каждого Колонка Из КолонкиТаблицыДокумента Цикл
		
		Если СтрЗаканчиваетсяНа(Колонка.Имя, "ТекстОшибки") 
			И Колонка.Имя <> "ОтступТекстОшибки" 
			И Колонка.Имя <> "ХарактеристикаТекстОшибки" 
			И СтрНайти(Колонка.Имя, "Надпись") = 0 Тогда
			
			ИмяКолонкиОригинал = СтрЗаменить(Колонка.Имя, "ТекстОшибки", "");
			
			Если СтрНачинаетсяС(ИмяКолонкиОригинал, "НомерГТД")
				Или СтрНачинаетсяС(ИмяКолонкиОригинал, "СтранаПроисхождения") Тогда
				Продолжить;
			КонецЕсли;
			
			ЭлементОформления = УсловноеОформление.Элементы.Добавить();
			
			Если ИмяКолонкиОригинал = "СуммаНДС" Тогда
				
				ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары." + Колонка.Имя);
				ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
			Иначе
				ГруппаИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
					"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
				ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
					"Объект.Товары." + Колонка.Имя, ВидСравненияКомпоновкиДанных.Заполнено);
				Если Не СтрНачинаетсяС(ИмяКолонкиОригинал, "Количество")
					И Не СтрНачинаетсяС(ИмяКолонкиОригинал, "Цена")
					И Не СтрНачинаетсяС(ИмяКолонкиОригинал, "Сумма")
					И Не СтрНачинаетсяС(ИмяКолонкиОригинал, "СтавкаНДС")
					И Не СтрНачинаетсяС(ИмяКолонкиОригинал, "СуммаНДС")
					И Не СтрНачинаетсяС(ИмяКолонкиОригинал , "Всего") Тогда
					ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
						"Объект.Товары." + ИмяКолонкиОригинал, ВидСравненияКомпоновкиДанных.НеЗаполнено);
				КонецЕсли;
			КонецЕсли;
			
			ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
			ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ИмяКолонкиОригинал);
			
			ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(251, 212, 212));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсветитьДокументОснование()
	
	ТекстСКоторымСравнивать = "";
	Если Элементы.ГруппаОснования.ТекущаяСтраница = Элементы.ГруппаОснованияМного Тогда
		ТекстСКоторымСравнивать = ДокументОснованиеКорректировкиРаспознанныйТекст;
	Иначе
		Для Каждого Строка Из Объект.ДокументыОснованияКорректировки Цикл
			Если ЗначениеЗаполнено(Строка.РаспознанныйТекст) Тогда
				ТекстСКоторымСравнивать = Строка.РаспознанныйТекст;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Реквизит = Неопределено;
	Для Каждого СтрокаРеквизитов Из Объект.РеквизитыДокумента Цикл
		Если СтрокаРеквизитов.РаспознанныйТекст = ТекстСКоторымСравнивать Тогда
			Реквизит = СтрокаРеквизитов;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Реквизит = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоординатыКартинки = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьНаборКоординат(Реквизит);
	РаспознаваниеДокументовСлужебныйКлиент.ПриблизитьПоКоординатам(Элементы.ПолеПросмотра, ПолеПросмотраГотово,
		КоординатыКартинки, Реквизит.СтрокВИзображении);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаДобавитьСоответствиеРаспознаваемыхСтрок(
		Объект,
		ВариантСохраненияСоответствий,
		НаборСоответствийРаспознанныхСтрок,
		ИмяЭлемента,
		НомерСтроки = Неопределено,
		ПереданноеЗначение = Неопределено,
		Свойства = Неопределено
	) Экспорт
	
	Контекст = ОбработкаСоответствиеПереданногоРаспознанногоЗначения(
		Объект,
		ИмяЭлемента,
		НомерСтроки,
		ПереданноеЗначение,
		Свойства
	);
	Контекст.Вставить("НаборСоответствийРаспознанныхСтрок", НаборСоответствийРаспознанныхСтрок);
	
	Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗнч(Контекст.ПереданноеЗначение)) Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьСоответствиеРаспознаваемыхСтрокЗавершение", РаспознаваниеДокументовСлужебныйКлиент, Контекст);
	
	ПараметрыСохранения = Новый Структура();
	ПараметрыСохранения.Вставить("РаспознанныйТекст", Контекст.РаспознанныйТекст);
	ПараметрыСохранения.Вставить("ВыбранныйОбъект", Контекст.ПереданноеЗначение);
	ПараметрыСохранения.Вставить("Оповещение", Оповещение);
	ПараметрыСохранения.Вставить("ВариантСохраненияСоответствий", ВариантСохраненияСоответствий);
	
	РаспознаваниеДокументовСлужебныйКлиент.ЗапросСохраненияСопоставленийТекстаСОбъектом(ПараметрыСохранения);
	
КонецПроцедуры

&НаКлиенте
Функция ОбработкаСоответствиеПереданногоРаспознанногоЗначения(
		Объект, // Форма обработки
		ИмяРеквизита,
		НомерСтроки = Неопределено,
		ПереданноеЗначение = Неопределено,
		Свойства = Неопределено
	)
	
	РаспознанныйТекст = Неопределено;
	
	Если НомерСтроки = Неопределено Тогда
		
		Для Каждого СтрокаРеквизитов Из Объект.РеквизитыДокумента Цикл
			Если СтрокаРеквизитов.ИмяРеквизита = ИмяРеквизита Тогда
				ПереданноеЗначение = СтрокаРеквизитов.Значение;
				РаспознанныйТекст = СтрокаРеквизитов.РаспознанныйТекст;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли Свойства <> Неопределено Тогда
		
		РаспознанныйТекст = Свойства.РаспознанныйТекст;
	Иначе 
		Свойства = ПолучитьСтрокуРеквизитыТабличныхЧастейКакСтруктуру(ИмяРеквизита, НомерСтроки);
		Если Свойства = Неопределено Тогда
			РаспознанныйТекст = Неопределено;
		Иначе
			РаспознанныйТекст = Свойства.РаспознанныйТекст;
		КонецЕсли;
	КонецЕсли;
	
	КлючеваяОперация = 
		?(НомерСтроки = Неопределено, 
			"РаспознаваниеДокументов.ИзменениеЗначенияПоля",
			"РаспознаваниеДокументов.ИзменениеЗначенияКолонки"
		);
	
	Замер = ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	Комментарий = Новый Структура;
	Комментарий.Вставить("ИдентификаторРезультата", Объект.ИдентификаторРезультата);
	Комментарий.Вставить("ИмяЭлемента", ИмяРеквизита);
	Комментарий.Вставить("НомерСтроки", НомерСтроки);
	Комментарий.Вставить("ЗначениеПредставление", Строка(ПереданноеЗначение));
	Комментарий.Вставить("РаспознанныйТекст", Строка(РаспознанныйТекст));
	
	ОценкаПроизводительностиКлиент.УстановитьКомментарийЗамера(Замер, Комментарий);
	
	Возврат Новый Структура("РаспознанныйТекст, ПереданноеЗначение", РаспознанныйТекст, ПереданноеЗначение);
	
КонецФункции

&НаКлиенте
Функция РеквизитРаспознанногоДокумента(ИмяРеквизита) 
	
	Для Каждого СтрокаРеквизитов Из Объект.РеквизитыДокумента Цикл
		Если СтрокаРеквизитов.ИмяРеквизита = ИмяРеквизита Тогда
			Возврат СтрокаРеквизитов;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Процедура НайтиИЗаполнитьСозданныеДокументы()
	
	Если Элементы.ГруппаСвязанныйДокумент.Видимость Тогда
		Элементы.ГруппаСвязанныйДокумент.Видимость = Ложь;
	КонецЕсли;
	
	// Поиск комплектов
	ДанныеДокумента = Новый Структура(
		"Ссылка, ПометкаУдаления, Статус, ТипДокумента, Направление, Организация, Контрагент, СуммаДокумента, НомерДокумента, ДатаДокумента",
		Объект.Ссылка,
		Объект.ПометкаУдаления,
		Объект.Статус,
		Объект.ТипДокумента,
		Объект.Направление,
		Объект.Организация,
		Объект.Контрагент,
		0,
		Объект.НомерДокумента,
		Объект.ДатаДокумента
	);
	ВсеКомплектныеДокументы = РаспознаваниеДокументовКомплекты.НайтиКомплектныеДокументы(ДанныеДокумента);
	
	// Удалим все предыдущие данные о комплектах, дублях, созданных документах, а также программно добавленные Элементы формы
	ДокументыКомплектные.Очистить();
	ДокументыДубли.Очистить();
	ДокументыСозданные.Очистить();
	СозданныеДокументы.Поступление.Ссылка = Неопределено;
	СозданныеДокументы.Реализация.Ссылка = Неопределено;
	СозданныеДокументы.СчетФактураПолученный.Ссылка = Неопределено;
	СозданныеДокументы.СчетФактураВыданный.Ссылка = Неопределено;
	
	ИдПодчиненного = Элементы.ГруппаВсплывающаяСвязанныеДокументы.ПодчиненныеЭлементы.Количество();
	Пока ИдПодчиненного > 0 Цикл
		ИдПодчиненного = ИдПодчиненного - 1;
		ДляПроверки = Элементы.ГруппаВсплывающаяСвязанныеДокументы.ПодчиненныеЭлементы[ИдПодчиненного];
		Если ДляПроверки.Видимость Тогда
			Элементы.Удалить(ДляПроверки);
		КонецЕсли;
	КонецЦикла;
	
	
	ДокументыПоТипам = Новый Соответствие;
	Для Каждого ЭтотКомплектныйДокумент Из ВсеКомплектныеДокументы Цикл
		ДанныеКомплектного = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭтотКомплектныйДокумент, "ПометкаУдаления, ТипДокумента, Статус, НомерДокумента, ДатаДокумента");
		
		СтрокаТаблицы = ДокументыКомплектные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеКомплектного);
		СтрокаТаблицы.Ссылка = ЭтотКомплектныйДокумент;
		СтрокаТаблицы.СтатусРаспознавания = РаспознаваниеДокументовКомплекты.СтатусДокументаЧислом(СтрокаТаблицы);
		
		ДокументыПоТипам.Вставить(ДанныеКомплектного.ТипДокумента, ЭтотКомплектныйДокумент);
	КонецЦикла;
	
	Если ВсеКомплектныеДокументы.Количество() = 1 Тогда
		ДанныеКомплекта = Неопределено;
		
		РаспознаваниеДокументовСлужебный.АктуализироватьОбъектыСвязанныеСРаспознаннымДокументом(Объект);
		Связанные = РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ВсеСвязанныеДокументы(Объект.Ссылка);
		
		ВсеСвязанные = Новый Массив;
		Для Каждого Связанный Из Связанные Цикл
			Если ВсеСвязанные.Найти(Связанный.Ссылка) = Неопределено Тогда
				ВсеСвязанные.Вставить(0, Связанный.Ссылка);
			КонецЕсли;
		КонецЦикла;
	Иначе
		
		// Подготовим структуру для создания документов из комплекта
		ДанныеКомплекта = РаспознаваниеДокументовКомплектыКлиентСервер.НовыеПараметрыСозданияКомплекта();
		ДанныеКомплекта.НаправлениеДокумента = Объект.Направление;
		ДанныеКомплекта.ТипКомплекта         = РаспознаваниеДокументовКомплекты.СформироватьТипКомплекта(ДокументыКомплектные, "ПоТаблице");
		ДанныеКомплекта.ПараметрыОперации    = РаспознаваниеДокументовКомплектыКлиентСервер.ТипДокументаИВидОперацииПоУмолчанию(Объект.Направление, ДанныеКомплекта.ТипКомплекта);
		ДанныеКомплекта.РаспознанныеДокументыПоТипам = ДокументыПоТипам;
		
		РаспознаваниеДокументовКомплекты.ОбновитьИЗаполнитьСвязанныеИСозданныеДокументы(ДанныеКомплекта);
		
		// Отсортировать таблицу ДокументыКомплектные
		ТипыРаспознанных = РаспознаваниеДокументовКомплектыКлиентСервер.ПорядокОтображенияСвязанныхДокументовПоТипам();
		ИдДокументаИзКомплекта = 0;
		ОтсутствующиеТипы = 0;
		Пока ИдДокументаИзКомплекта < ДокументыКомплектные.Количество() - 1 Цикл
			Если ТипыРаспознанных[ИдДокументаИзКомплекта + ОтсутствующиеТипы] <> ДокументыКомплектные[ИдДокументаИзКомплекта].ТипДокумента Тогда
				НайденныеСтроки = ДокументыКомплектные.НайтиСтроки(Новый Структура("ТипДокумента",
					ТипыРаспознанных[ИдДокументаИзКомплекта + ОтсутствующиеТипы]
				));
				Если НайденныеСтроки.Количество() = 0 Тогда
					ОтсутствующиеТипы = ОтсутствующиеТипы + 1;
					Продолжить;
				Иначе
					НужнаяСтрока = НайденныеСтроки[0];
					ИдНужнойСтроки = ДокументыКомплектные.Индекс(НужнаяСтрока);
					ДокументыКомплектные.Сдвинуть(ИдНужнойСтроки, ИдДокументаИзКомплекта - ИдНужнойСтроки);
				КонецЕсли;
			КонецЕсли;
			ИдДокументаИзКомплекта = ИдДокументаИзКомплекта + 1;
		КонецЦикла;
		
		ВсеСвязанные = ДанныеКомплекта.СвязанныеДокументы;
	КонецЕсли;
	
	Для Каждого ЭтотСвязанный Из ВсеСвязанные Цикл
		ТипСвязанного = ТипЗнч(ЭтотСвязанный);
		ПодходящиеТипы = РаспознаваниеДокументовКомплектыКлиентСервер.ПодходящиеТипыРаспознанногоДокумента(ТипСвязанного);
		
		Для Каждого ЭтотТип Из ПодходящиеТипы Цикл
			КомплектныеСтроки = ДокументыКомплектные.НайтиСтроки(Новый Структура("ТипДокумента", ЭтотТип));
			Если КомплектныеСтроки.Количество() <> 0 Тогда
				СтрокаТаблицы = ДокументыСозданные.Добавить();
				СтрокаТаблицы.РаспознанныйДокумент = КомплектныеСтроки[0].Ссылка;
				СтрокаТаблицы.СозданныйДокумент = ЭтотСвязанный;
				СтрокаТаблицы.ТипДокумента = ТипСвязанного;
				
				Если Метаданные.ОпределяемыеТипы.СчетФактураПолученныйБРД.Тип.СодержитТип(ТипСвязанного) Тогда
					СозданныеДокументы.СчетФактураПолученный.Ссылка = ЭтотСвязанный;
				ИначеЕсли Метаданные.ОпределяемыеТипы.ПоступлениеТоваровУслугБРД.Тип.СодержитТип(ТипСвязанного) Тогда
					СозданныеДокументы.Поступление.Ссылка = ЭтотСвязанный;
				ИначеЕсли Метаданные.ОпределяемыеТипы.СчетФактураВыданныйБРД.Тип.СодержитТип(ТипСвязанного) Тогда
					СозданныеДокументы.СчетФактураВыданный.Ссылка = ЭтотСвязанный;
				ИначеЕсли Метаданные.ОпределяемыеТипы.РеализацияТоваровУслугБРД.Тип.СодержитТип(ТипСвязанного) Тогда
					СозданныеДокументы.Реализация.Ссылка = ЭтотСвязанный;
				КонецЕсли;
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
#Область ДокументыКомплектные
	
	// Отображение комплектов, дублей и созданных документов
	ИдЧисломДокументаИзКомплекта = -1;
	Соль = "_" + СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "");
	ИдЧисломДубляДокумента = -1; // Тут сквозная нумерация
	Для Каждого СтрокаТаблицы Из ДокументыКомплектные Цикл
		ИдЧисломДокументаИзКомплекта = ИдЧисломДокументаИзКомплекта + 1;
		ИдДокументаИзКомплекта = XMLСтрока(ИдЧисломДокументаИзКомплекта) + Соль;
		
		// Добавление элементов на форму
		РаспознаваниеДокументовКомплекты.СкопироватьЭлементыФормыРекурсивно(
			ЭтотОбъект,
			Элементы.ГруппаСвязанныйДокумент,
			Элементы.ГруппаВсплывающаяСвязанныеДокументы,
			ИдДокументаИзКомплекта,
			"ГруппаДубльСтатусСсылка, ОткрытьСозданныйДокумент");
		
		Если СтрокаТаблицы.Ссылка = Объект.Ссылка Тогда
			ВидимыйЭлемент = Элементы["ДекорацияЭтотДокумент_"+ИдДокументаИзКомплекта];
			Элементы["ДекорацияЭтотДокумент_"+ИдДокументаИзКомплекта].Видимость = Истина;
			Элементы["ОткрытьРаспознанныйДокумент_"+ИдДокументаИзКомплекта].Видимость = Ложь;
		Иначе
			ВидимыйЭлемент = Элементы["ОткрытьРаспознанныйДокумент_"+ИдДокументаИзКомплекта];
		КонецЕсли;
		
		ВидимыйЭлемент.Заголовок = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),
			СтрокаТаблицы.ТипДокумента, СокрЛП(СтрокаТаблицы.НомерДокумента), Формат(СтрокаТаблицы.ДатаДокумента, "ДЛФ=D"));
		
#КонецОбласти
#Область ДокументыДубли
		
		Если СтрокаТаблицы.Ссылка = Объект.Ссылка Тогда
			НовыеДубли = РаспознаваниеДокументовКомплекты.ДублиРаспознанногоДокумента(ДанныеДокумента);
		Иначе
			НовыеДубли = РаспознаваниеДокументовКомплекты.ДублиРаспознанногоДокумента(СтрокаТаблицы.Ссылка);
		КонецЕсли;
		
		Если НовыеДубли.Количество() = 0 Тогда
			Элементы["ГруппаДублейГоризонтальная_"+ИдДокументаИзКомплекта].Видимость = Ложь;
		Иначе
			
			Для Каждого СтрокаДубля Из НовыеДубли Цикл
				СтрокаДубляНаФорме = ДокументыДубли.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДубляНаФорме, СтрокаДубля);
				СтрокаДубляНаФорме.СтатусРаспознавания = РаспознаваниеДокументовКомплекты.СтатусДокументаЧислом(СтрокаДубля);
				
				ИдЧисломДубляДокумента = ИдЧисломДубляДокумента + 1;
				ИдДубляДокумента = XMLСтрока(ИдЧисломДубляДокумента) + Соль;
				РаспознаваниеДокументовКомплекты.СкопироватьЭлементыФормыРекурсивно(
					ЭтотОбъект,
					Элементы.ГруппаДубльСтатусСсылка,
					Элементы["ГруппаДублей_"+ИдДокументаИзКомплекта],
					ИдДубляДокумента
				);
				
				Элементы["ОткрытьДубльДокумента_" + ИдДубляДокумента].Заголовок = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),
					СтрокаДубля.ТипДокумента, СокрЛП(СтрокаДубля.НомерДокумента), Формат(СтрокаДубля.ДатаДокумента, "ДЛФ=D"));
			КонецЦикла;
		КонецЕсли;
		
#КонецОбласти
#Область ДокументыСозданные
		
		ЧастьСозданных = ДокументыСозданные.НайтиСтроки(Новый Структура("РаспознанныйДокумент", СтрокаТаблицы.Ссылка));
		Если ЧастьСозданных.Количество() = 0 Тогда
			Элементы["ГруппаСозданныхГоризонтальная_"+ИдДокументаИзКомплекта].Видимость = Ложь;
		Иначе
			
			Для Каждого СтрокаСозданного Из ЧастьСозданных Цикл
				ИдСозданногоДокумента = ДокументыСозданные.Индекс(СтрокаСозданного);
				ИдСозданногоДокумента = XMLСтрока(ИдСозданногоДокумента) + Соль;
				РаспознаваниеДокументовКомплекты.СкопироватьКнопкуФормы(
					ЭтотОбъект,
					Элементы.ОткрытьСозданныйДокумент,
					Элементы["ГруппаСозданных_"+ИдДокументаИзКомплекта],
					ИдСозданногоДокумента
				);
				
				Элементы["ОткрытьСозданныйДокумент_" + ИдСозданногоДокумента].Заголовок =
					РаспознаваниеДокументовКомплектыКлиентСервер.ОтрезатьВремяУДаты(Строка(СтрокаСозданного.СозданныйДокумент));
			КонецЦикла;
		КонецЕсли;
		
#КонецОбласти
		
	КонецЦикла;
	
	КоличествоСвязанных = ДокументыКомплектные.Количество() - 1 + ДокументыДубли.Количество() + ДокументыСозданные.Количество();
	Если КоличествоСвязанных = 0 Тогда
		Элементы.ГруппаВсплывающаяСвязанныеДокументы.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаФормы;
		Элементы.ГруппаВсплывающаяСвязанныеДокументы.Доступность = Ложь;
		Элементы.ГруппаВсплывающаяСвязанныеДокументы.Заголовок = НСтр("ru = 'Нет связанных документов'");
	Иначе
		Элементы.ГруппаВсплывающаяСвязанныеДокументы.ЦветТекстаЗаголовка = ЦветаСтиля.ГиперссылкаЦвет;
		Элементы.ГруппаВсплывающаяСвязанныеДокументы.Доступность = Истина;
		Элементы.ГруппаВсплывающаяСвязанныеДокументы.Заголовок =
			СтрШаблон(НСтр("ru = 'Связанные документы (%1)'"), КоличествоСвязанных);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСтруктуруСозданныеДокументы()
	
	СозданныеДокументы = Новый Структура;
	СозданныеДокументы.Вставить("Поступление", Новый Структура("Ссылка", Неопределено));
	СозданныеДокументы.Вставить("Реализация", Новый Структура("Ссылка", Неопределено));
	СозданныеДокументы.Вставить("СчетФактураПолученный", Новый Структура("Ссылка", Неопределено));
	СозданныеДокументы.Вставить("СчетФактураВыданный", Новый Структура("Ссылка", Неопределено));
	
КонецПроцедуры

&НаКлиенте
Процедура СледующийДокумент()
	
	Результат = НайтиДокумент(Истина);
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Результат = НайтиДокумент(Ложь);
	КонецЕсли;
	
	ПерейтиНаДокумент(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаДокумент(Результат)
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ИмяФормыОбработчика = РаспознаваниеДокументовСлужебныйКлиентПовтИсп.ПолучитьИмяОткрываемойФормыПоТипу(Результат.ТипДокумента, Результат.ВариантОбработки);
		Если Не ПустаяСтрока(ИмяФормыОбработчика) Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", Результат.Ссылка);
			ПараметрыФормы.Вставить("ОтборИзСписка", Параметры.ОтборИзСписка);
			ОткрытьФорму(ИмяФормыОбработчика, ПараметрыФормы,, Истина);
		КонецЕсли;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция НайтиДокумент(Следующий)
	
	ОтборДата = Ложь;
	ОтборОрганизация = Ложь;
	ОтборКонтрагент = Ложь;
	
	Если ЗначениеЗаполнено(Параметры.ОтборИзСписка) Тогда 
		
		Если ЗначениеЗаполнено(Параметры.ОтборИзСписка.ТекущаяДата) Тогда 
			
			ОтборДата = Истина;
			
			Если Параметры.ОтборИзСписка.ТекущаяДатаПериод = "День" Тогда
				НачалоПериода = НачалоДня(Параметры.ОтборИзСписка.ТекущаяДата);
				КонецПериода = КонецДня(Параметры.ОтборИзСписка.ТекущаяДата);
			ИначеЕсли Параметры.ОтборИзСписка.ТекущаяДатаПериод = "Месяц" Тогда
				НачалоПериода = НачалоМесяца(Параметры.ОтборИзСписка.ТекущаяДата);
				КонецПериода = КонецМесяца(Параметры.ОтборИзСписка.ТекущаяДата);
			КонецЕсли;
			
		КонецЕсли;
		
		ОтборОрганизация = ЗначениеЗаполнено(Параметры.ОтборИзСписка.ТекущаяОрганизация);
		ОтборКонтрагент = ЗначениеЗаполнено(Параметры.ОтборИзСписка.ТекущийКонтрагент);
		
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	//Запрос.УстановитьПараметр("Ссылка", Объект.РаспознанныйДокумент);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	РаспознанныйДокумент.Ссылка КАК Ссылка,
		|	РаспознанныйДокумент.ТипДокумента КАК ТипДокумента,
		|	РаспознанныйДокумент.ВариантОбработки КАК ВариантОбработки
		|ИЗ
		|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
		|ГДЕ
		|	(РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый)
		|			ИЛИ РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Изменен))
		|	И НЕ РаспознанныйДокумент.ПометкаУдаления
		|	И РаспознанныйДокумент.Дата < &Дата
		|	И РаспознанныйДокумент.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РаспознанныйДокумент.Контрагент = &Контрагент
		|	И РаспознанныйДокумент.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	РаспознанныйДокумент.МоментВремени УБЫВ";
	
	Если Следующий Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, " УБЫВ", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "<", ">=");
	КонецЕсли;
	
	Если ОтборДата Тогда
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И РаспознанныйДокумент.Дата МЕЖДУ &НачалоПериода И &КонецПериода", "");
	КонецЕсли;
	
	Если ОтборОрганизация Тогда
		Запрос.УстановитьПараметр("Организация", Параметры.ОтборИзСписка.ТекущаяОрганизация);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И РаспознанныйДокумент.Организация = &Организация", "");
	КонецЕсли;
	
	Если ОтборКонтрагент Тогда
		Запрос.УстановитьПараметр("Контрагент", Параметры.ОтборИзСписка.ТекущийКонтрагент);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И РаспознанныйДокумент.Контрагент = &Контрагент", "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Новый Структура("Ссылка, ТипДокумента, ВариантОбработки");
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		Возврат Результат;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция КолонкиТаблицы(ИмяТЧ)

	Результат = Новый Массив;
	Для Каждого Реквизит Из ЭтаФорма.ПолучитьРеквизиты(ИмяТЧ) Цикл
		Результат.Добавить(Реквизит.Имя);
	КонецЦикла;
	Возврат Результат; 
	
КонецФункции

&НаКлиенте
Функция СтруктураДанных(СтрокаТаблицы)

	Результат = Новый Структура;
	Для Каждого Колонка Из КолонкиРеквизитыТабличныхЧастей Цикл
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТаблицы, Колонка) Тогда
			Результат.Вставить(Колонка, СтрокаТаблицы[Колонка]);
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПолучитьСтрокуРеквизитыТабличныхЧастейКакСтруктуру(ИмяРеквизита, НомерСтрокиТЧ)
	
	СтрокиТаблицы = Объект.РеквизитыТабличныхЧастей.НайтиСтроки(Новый Структура("ИмяРеквизита, НомерСтрокиТЧ", ИмяРеквизита, НомерСтрокиТЧ));
	Если СтрокиТаблицы.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	СтрокаТаблицы = СтрокиТаблицы[0];
	Возврат СтруктураДанных(СтрокаТаблицы);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПреобразоватьСтрокуТабличнойЧасти(СтрокаТЧ, Ключ, РаспознанныйДокумент)
	
	Если СтрокаТЧ = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	Свойства = Новый Структура;
	Свойства.Вставить("ИмяРеквизита", СтрокаТЧ.ИмяРеквизита);
	Свойства.Вставить("Значение", СтрокаТЧ.Значение);
	Свойства.Вставить("ТипЗначения", ТипЗнч(СтрокаТЧ.Значение));
	
	Свойства.Вставить("НайденноеЗначение", СтрокаТЧ.НайденноеЗначение);
	Свойства.Вставить("УверенностьНайденногоЗначения", СтрокаТЧ.УверенностьНайденногоЗначения);
	
	Свойства.Вставить("РаспознанныйТекст", СтрокаТЧ.РаспознанныйТекст);
	Свойства.Вставить("ЗаполненоВручную", СтрокаТЧ.ЗаполненоВручную);
	Свойства.Вставить("ЭлементСоздан", СтрокаТЧ.ЭлементСоздан);
	Свойства.Вставить("НомерСтроки", СтрокаТЧ.НомерСтроки);
	
	Свойства.Вставить("СтрокВИзображении", СтрокаТЧ.СтрокВИзображении);
	Свойства.Вставить("Координаты", РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьНаборКоординат(СтрокаТЧ));
	
	Свойства.Вставить("ЗначенияВыбора", Новый Массив);
	
	Результат.Вставить(Ключ, Свойства);
	
	ЗначенияВыбораЗначений = ЗначенияВыбораЗначенийТаблицыДокумента(РаспознанныйДокумент);
	
	Для Каждого ДанныеВыбора Из ЗначенияВыбораЗначений Цикл
		
		КлючВыбора = РаспознаваниеДокументовСлужебныйКлиентСервер.КлючСвойстваЯчеекТаблицы(
			ДанныеВыбора.ИмяРеквизита, ДанныеВыбора.НомерСтроки);
			
		Если КлючВыбора = Ключ Тогда
			ВариантВыбора = Новый Структура;
			ВариантВыбора.Вставить("Значение", ДанныеВыбора.Значение);
			ВариантВыбора.Вставить("ДополнительноеЗначение", ДанныеВыбора.ДополнительноеЗначение);
			ВариантВыбора.Вставить("Уверенность", ДанныеВыбора.Уверенность);
			ВариантВыбора.Вставить("НайденВТаблицеСоответствий", ДанныеВыбора.НайденВТаблицеСоответствий);
			
			Свойства = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, КлючВыбора);
			Свойства.ЗначенияВыбора.Добавить(ВариантВыбора);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначенияВыбораЗначенийТаблицыДокумента(Документ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СписокВыбораТабличныхЧастейРаспознаваниеДокументов.НомерСтрокиТЧ КАК НомерСтроки,
		|	СписокВыбораТабличныхЧастейРаспознаваниеДокументов.ИмяРеквизита КАК ИмяРеквизита,
		|	СписокВыбораТабличныхЧастейРаспознаваниеДокументов.Значение КАК Значение,
		|	СписокВыбораТабличныхЧастейРаспознаваниеДокументов.ДополнительноеЗначение КАК ДополнительноеЗначение,
		|	СписокВыбораТабличныхЧастейРаспознаваниеДокументов.Уверенность КАК Уверенность,
		|	СписокВыбораТабличныхЧастейРаспознаваниеДокументов.НайденВТаблицеСоответствий КАК НайденВТаблицеСоответствий
		|ИЗ
		|	РегистрСведений.СписокВыбораТабличныхЧастейРаспознаваниеДокументов КАК СписокВыбораТабличныхЧастейРаспознаваниеДокументов
		|ГДЕ
		|	СписокВыбораТабличныхЧастейРаспознаваниеДокументов.РаспознанныйДокумент = &Документ
		|
		|УПОРЯДОЧИТЬ ПО
		|	НайденВТаблицеСоответствий УБЫВ,
		|	Уверенность УБЫВ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

&НаКлиенте
Процедура ОткрытьДокументПоСсылке(ДокументСсылка)
	ПерейтиПоНавигационнойСсылке(ПолучитьНавигационнуюСсылку(ДокументСсылка));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СозданиеТипового(ПараметрыОперации) Экспорт
	
	ЗаписатьНаСервере();
	НовыеДокументы = СоздатьНаСервере(ПараметрыОперации);
	
	Если НовыеДокументы.Количество() Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		
		Для Каждого НовыйДокумент Из НовыеДокументы Цикл
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(НовыйДокумент);
			ПоказатьОповещениеПользователя(НСтр("ru = 'Создан документ'"), НавигационнаяСсылка, Строка(НовыйДокумент));
			Оповестить("РаспознанныйДокумент_СтатусОбработан", , НовыйДокумент);
		КонецЦикла;
		
	КонецЕсли;
	
	СледующийДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияЗакрытия(Результат, Контекст) Экспорт
	
	ЗаписатьНаСервере();
	Подключаемый_ПослеЗаписи();
	
	КлючеваяОперация = "РаспознаваниеДокументов.ЗакрытиеФормы." + ИмяФормы;
	Замер = ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	Комментарий = Новый Структура;
	Комментарий.Вставить("ИдентификаторРезультата", Объект.ИдентификаторРезультата);
	
	ОценкаПроизводительностиКлиент.УстановитьКомментарийЗамера(Замер, Комментарий);
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьЗначенияПоДокументу(СтрокаТаблицыТовары)
	
	СтрокаТаблицыТовары.КоличествоПоДокументу = 0;
	СтрокаТаблицыТовары.ЦенаПоДокументу = 0;
	СтрокаТаблицыТовары.СуммаПоДокументу = 0;
	СтрокаТаблицыТовары.СтавкаНДСПоДокументу = Неопределено;
	СтрокаТаблицыТовары.СуммаНДСПоДокументу = 0;
	СтрокаТаблицыТовары.СтранаПроисхожденияДоИзменения = Неопределено;
	СтрокаТаблицыТовары.ВсегоПоДокументу = 0;
	СтрокаТаблицыТовары.СтранаПроисхожденияПоДокументу = Неопределено;
	СтрокаТаблицыТовары.НомерИсходнойСтрокиЗаполненоВручную = Ложь;
	СтрокаТаблицыТовары.ДокументОснованиеКорректировкиЗаполненоВручную = Ложь;
	СтрокаТаблицыТовары.ЭтоУслуга = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораОснования(Элемент)
	
	Отбор = Новый Структура;
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Отбор.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Отбор.Вставить("Контрагент", Объект.Контрагент);
	КонецЕсли;
	
	ОткрытьФорму("Документ.ПоступлениеТоваровУслуг.ФормаВыбора",
		Новый Структура("Отбор", Отбор),
		Элемент);
	
КонецПроцедуры

&НаСервере
Процедура НайтиИЗаполнитьДокументыОснования()
	
	Если ЗначениеЗаполнено(Объект.Контрагент) И ЗначениеЗаполнено(Объект.Организация) Тогда
		РаспознаваниеДокументовПереопределяемый.НайтиИЗаполнитьДокументыОснованияКорректировки(Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементыИтогов()
	
	ИменаРеквизитов = Новый Массив;
	ИменаРеквизитов.Добавить("ИтогоВсегоУвеличение");
	ИменаРеквизитов.Добавить("ИтогоСуммаНДСУвеличение");
	ИменаРеквизитов.Добавить("ИтогоСуммаУвеличение");
	ИменаРеквизитов.Добавить("ИтогоВсегоУменьшение");
	ИменаРеквизитов.Добавить("ИтогоСуммаНДСУменьшение");
	ИменаРеквизитов.Добавить("ИтогоСуммаУменьшение");
	
	Для Каждого РеквизитДокумента Из Объект.РеквизитыДокумента Цикл
		
		Если ИменаРеквизитов.Найти(РеквизитДокумента.ИмяРеквизита) <> Неопределено Тогда
			ЭтотОбъект[РеквизитДокумента.ИмяРеквизита] = РеквизитДокумента.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
