#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Расходы         = Параметры.Расходы;
	РасходыБезНДС   = Параметры.РасходыБезНДС;
	ФондОплатыТруда = Параметры.ФондОплатыТруда;
	НДСВходящий     = Параметры.НДСВходящий;

	ВсегоРасходыДляРаспределения = Расходы - ФондОплатыТруда;
	Элементы.РасходыСтавкаБезНДС.МаксимальноеЗначение = ВсегоРасходыДляРаспределения;

	Если ВсегоРасходыДляРаспределения > 0 Тогда

		РасходыБезНДСШкала = РасходыБезНДС / ВсегоРасходыДляРаспределения * 100;
		НДСВходящийРасчетный = Окр(ВсегоРасходыДляРаспределения * 20 / 120, 0);
		Элементы.НДСВходящий.МаксимальноеЗначение = НДСВходящийРасчетный;
		
		ОстатокРасходовДляРаспределения = ВсегоРасходыДляРаспределения - РасходыБезНДС - НДСВходящий;
		
		Если ОстатокРасходовДляРаспределения > 0 И НДСВходящий > 0 Тогда 
			НДСВходящийШкала = Окр(НДСВходящий / ОстатокРасходовДляРаспределения, 2) * 100;
		КонецЕсли;
		
	ИначеЕсли РасходыБезНДС <> 0 Тогда
		РасходыБезНДСШкала = 100;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РасходыБезНДСШкалаПриИзменении(Элемент)
	
	РасходыБезФОТ = Расходы - ФондОплатыТруда;
	
	Если РасходыБезНДСШкала < 100 И РасходыБезФОТ > 0 Тогда
		РасходыБезНДС = (Расходы - ФондОплатыТруда) * (РасходыБезНДСШкала / 100);
	Иначе
		РасходыБезНДС = (Расходы - ФондОплатыТруда);
	КонецЕсли;

	РассчитатьНДС();
	
КонецПроцедуры

&НаКлиенте
Процедура НДСВходящийШкалаПриИзменении(Элемент)
	
	НДСВходящий = ВсегоРасходыДляРаспределения * НДСВходящийШкала / (100 + НДСВходящийШкала);

КонецПроцедуры

&НаКлиенте
Процедура НДСВходящийПриИзменении(Элемент)
	
	НДСВходящийРасчетный = ВсегоРасходыДляРаспределения * 20 / 120;

	Если НДСВходящий > НДСВходящийРасчетный Тогда
		НДСВходящий = Окр(НДСВходящийРасчетный, 0);
		ТекстСообщения = СтрШаблон(НСтр("ru='НДС к вычету при текущих расходах не может превышать %1'"), Окр(НДСВходящийРасчетный, 0));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "НДСВходящий");
	КонецЕсли;
	
	Если НДСВходящийРасчетный > 0 И НДСВходящий > 0 Тогда 
		НДСВходящийШкала = Окр(20 * (НДСВходящий / НДСВходящийРасчетный), 0);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РасходыБезНДСПриИзменении(Элемент)
	
	Если РасходыБезНДС > Расходы - ФондОплатыТруда Тогда
		РасходыБезНДС = Расходы - ФондОплатыТруда;
		ТекстСообщения = НСтр("ru='Расходы без НДС не могут превышать сумму расходов без зарплаты'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "РасходыБезНДС");
	КонецЕсли;
	РасходыБезНДСШкала = РасходыБезНДС / (Расходы - ФондОплатыТруда) * 100;
	
	РассчитатьНДС();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Результат = Новый Структура;
	Результат.Вставить("НДСВходящий", НДСВходящий);
	Результат.Вставить("РасходыБезНДС", РасходыБезНДС);

	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьНДС()
	
	ВсегоРасходыДляРаспределения = Расходы - ФондОплатыТруда - РасходыБезНДС;

	Если ВсегоРасходыДляРаспределения = 0 Тогда
		НДСВходящий = 0;
		НДСВходящийШкала = 0;
	Иначе
		Если НДСВходящийШкала > 0 Тогда
			НДСВходящий = ВсегоРасходыДляРаспределения * НДСВходящийШкала / (100 + НДСВходящийШкала);
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	НДСВходящийДоступен = Не Форма.ВсегоРасходыДляРаспределения = 0;
	
	Элементы.НДСВходящийШкала.Доступность = НДСВходящийДоступен;
	Элементы.НДСВходящий.Доступность = НДСВходящийДоступен;
	
КонецПроцедуры

#КонецОбласти