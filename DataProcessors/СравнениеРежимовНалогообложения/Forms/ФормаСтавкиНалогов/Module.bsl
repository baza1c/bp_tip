#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСвойств = "СтавкаНалогаНаПрибыльРегиональная, СтавкаУСНДоходы, СтавкаУСНДоходыРасходы, СтавкаЕНВД, К2,
		|ПрименяетсяПСН, СтавкаПСН, КодВидаДеятельностиПСН, НаименованиеВидаДеятельностиПСН,
		|КодРегиона, КодОКВЭД, НаименованиеОКВЭД, ВидОрганизации, Период";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Элементы.ЕНВД.Видимость = Параметры.УплачиваетсяЕНВД;
	Элементы.НалогНаПрибыль.Видимость = ВидОрганизации <> Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	Элементы.ПСН.Видимость = ПрименяетсяПСН;
	
	ИспользуетсяСервисРегиональныеСтавкиНалогов =
		РегиональныеСтавкиНалоговВызовСервера.ИспользуетсяСервисРегиональныхСтавок();
	
	Элементы.СсылкаНаРегиональныеОсобенностиУСН.Видимость = НЕ ИспользуетсяСервисРегиональныеСтавкиНалогов;
	Элементы.ДекорацияСтавкиПСНВВашемРегионе.Видимость = ИспользуетсяСервисРегиональныеСтавкиНалогов;
	Элементы.СсылкаНаРегиональныеОсобенностиПСН.Видимость = НЕ ИспользуетсяСервисРегиональныеСтавкиНалогов;
	
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолучитьОписаниеСтавокУСНВФоне();
	ПолучитьОписаниеСтавокПСНВФоне();
	
	// Отступ задает форме правильную высоту при создании
	Элементы.ДекорацияОтступ.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеСтавокУСНСравнениеРежимовНалогообложения" Тогда
	
		ОбработатьИзменениеСтавокУСН(Параметр);
	
	ИначеЕсли ИмяСобытия = "ИзменениеСтавокПСНСравнениеРежимовНалогообложения" Тогда
	
		ОбработатьИзменениеСтавокПСН(Параметр);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СсылкаНаРегиональныеОсобенностиУСНОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтавкиУСНВВашемРегионеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОтчета = РегиональныеСтавкиНалоговКлиент.ПараметрыОтчетаСтавкиНалогаВВашемРегионе();
	ПараметрыОтчета.Налог = "УСН";
	ПараметрыОтчета.Режим = "СравнениеРежимовНалогообложения";
	ПараметрыОтчета.КодРегиона = КодРегиона;
	ПараметрыОтчета.КодВидаДеятельности = КодОКВЭД;
	ПараметрыОтчета.НаименованиеВидаДеятельности = НаименованиеОКВЭД;
	ПараметрыОтчета.ЭтоЮрЛицо = ЭтоЮрЛицо(ВидОрганизации);
	ПараметрыОтчета.ПрименяетсяУСНДоходы = Истина;
	ПараметрыОтчета.ПрименяетсяУСНДоходыРасходы = Истина;
	ПараметрыОтчета.Период = Период;
	ПараметрыОтчета.ПоказыватьПрименить = Истина;
	
	РегиональныеСтавкиНалоговКлиент.ОбработатьНавигационнуюСсылкуСтавкиВВашемРегионе(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки,
		ПараметрыОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаРегиональныеОсобенностиЕНВДОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаРегиональныеОсобенностиПСНОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСтавкиПСНВВашемРегионеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СтавкиНалогаВВашемРегионе" Тогда
		
		ПараметрыОтчета = РегиональныеСтавкиНалоговКлиент.ПараметрыОтчетаСтавкиНалогаВВашемРегионе();
		ПараметрыОтчета.Налог = "ПСН";
		ПараметрыОтчета.Режим = "СравнениеРежимовНалогообложения";
		ПараметрыОтчета.КодРегиона = КодРегиона;
		ПараметрыОтчета.КодВидаДеятельности = КодВидаДеятельностиПСН;
		ПараметрыОтчета.НаименованиеВидаДеятельности = ПредставлениеВидаДеятельностиПСН(
			КодВидаДеятельностиПСН, 
			НаименованиеВидаДеятельностиПСН);
		ПараметрыОтчета.Период = Период;
		ПараметрыОтчета.ПоказыватьПрименить = Истина;
		
		ПараметрыФормы = Новый Структура("ПараметрыОтчета", ПараметрыОтчета);
		ОткрытьФорму("Отчет.СтавкиНалогаВВашемРегионе.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеВидаДеятельностиПСН(Код, Наименование)

	Возврат СтрШаблон("%1 - %2", Лев(Код, 6), Наименование);

КонецФункции

&НаКлиенте
Процедура СсылкаНаРегиональныеОсобенностиОСНООбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Ключи = "СтавкаНалогаНаПрибыльРегиональная, СтавкаУСНДоходы, СтавкаУСНДоходыРасходы, СтавкаЕНВД, К2, СтавкаПСН";
	Результат = Новый Структура(Ключи);
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьРегиональныеОсобенностиНаСайтеФНС(НавигационнаяСсылкаФорматированнойСтроки)
	
	СсылкаНаСайтФНС = Новый Массив;
	СсылкаНаСайтФНС.Добавить("https://www.nalog.ru/rn");
	Если НавигационнаяСсылкаФорматированнойСтроки = "СсылкаОСНО" Тогда
		СсылкаНаСайтФНС.Добавить(Параметры.КодРегиона);
		СсылкаНаСайтФНС.Добавить("/taxation/taxes/profitul/#title18");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СсылкаУСН" Тогда
		СсылкаНаСайтФНС.Добавить(Параметры.КодРегиона);
		СсылкаНаСайтФНС.Добавить("/taxation/taxes/usn/#title11");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СсылкаЕНВД" Тогда
		СсылкаНаСайтФНС.Добавить(Параметры.КодРегиона);
		СсылкаНаСайтФНС.Добавить("/taxation/taxes/envd/#title18");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СсылкаПСН" Тогда
		СсылкаНаСайтФНС.Добавить(Параметры.КодРегиона);
		СсылкаНаСайтФНС.Добавить("/taxation/taxes/patent/#title22");
	КонецЕсли;
	
	Если СсылкаНаСайтФНС.Количество() = 1 Тогда
		Возврат;
	КонецЕсли;
	
	ПерейтиПоНавигационнойСсылке(СтрСоединить(СсылкаНаСайтФНС, ""));
	
КонецПроцедуры

#Область СервисСтавокУСН

&НаКлиенте
Процедура ПолучитьОписаниеСтавокУСНВФоне()
	
	Если Не ИспользуетсяСервисРегиональныеСтавкиНалогов Тогда
		Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок =
			РегиональныеСтавкиНалоговКлиент.ТекстПодсказкиИнтернетПоддержкаНеПодключена();
		Возврат;
	КонецЕсли;
	
	Элементы.ДекорацияСтавкиУСНВВашемРегионе.Заголовок = "";
	
	Если Не ЗначениеЗаполнено(КодРегиона) Тогда
		Элементы.ДекорацияСтавкиПСНВВашемРегионе.Заголовок = НСтр("ru='Не указан регион регистрации организации'");
		Возврат;
	КонецЕсли;
	
	ПараметрыМетода = РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаОписаниеСтавокУСН();
	ПараметрыМетода.КодРегиона = КодРегиона;
	ПараметрыМетода.КодВидаДеятельности = КодОКВЭД;
	ПараметрыМетода.ЭтоЮрЛицо = ЭтоЮрЛицо(ВидОрганизации);
	ПараметрыМетода.ПрименяетсяУСНДоходы = Истина;
	ПараметрыМетода.ПрименяетсяУСНДоходыРасходы = Истина;
	ПараметрыМетода.Период = Период;
	
	Обработчик = Новый ОписаниеОповещения("ОбработкаПолученияОписанияСтавокУСН", ЭтотОбъект);
	
	РегиональныеСтавкиНалоговКлиент.ПолучитьОписаниеСтавокУСНВФоне(
		ЭтотОбъект,
		Элементы.ДекорацияСтавкиУСНВВашемРегионе,
		Элементы.ДекорацияСтавкиУСНДлительнаяОперация,
		ПараметрыМетода,
		Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПолученияОписанияСтавокУСН(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	РегиональныеСтавкиНалоговКлиент.ОбработкаПолученияОписанияСтавокНалога(
		ДлительнаяОперация,
		Элементы.ДекорацияСтавкиУСНВВашемРегионе,
		Элементы.ДекорацияСтавкиУСНДлительнаяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСтавокУСН(НовыеСтавки)
	
	Если ТипЗнч(НовыеСтавки) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если КодРегиона <> ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовыеСтавки, "КодРегиона") Тогда
		ТекстПредупреждения = НСтр("ru='Нельзя применить ставку налога для другого региона.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	ИначеЕсли КодОКВЭД <> ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовыеСтавки, "КодВидаДеятельности") Тогда
		ТекстПредупреждения = НСтр("ru='Нельзя применить ставку налога для другого вида деятельности.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если НовыеСтавки.Свойство("НалоговыеКаникулы") Тогда
		Если ЭтоЮрЛицо(ВидОрганизации) Тогда
			Возврат;
		КонецЕсли;
		СтавкаУСНДоходы = 0;
		СтавкаУСНДоходыРасходы = 0;
		ТекущийЭлемент = Элементы.СтавкаУСНДоходы;
	ИначеЕсли НовыеСтавки.УСНДоходы Тогда
		СтавкаУСНДоходы = НовыеСтавки.СтавкаНалога;
		ТекущийЭлемент = Элементы.СтавкаУСНДоходы;
	Иначе
		СтавкаУСНДоходыРасходы = НовыеСтавки.СтавкаНалога;
		ТекущийЭлемент = Элементы.СтавкаУСНДоходыРасходы;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СервисСтавокПСН

&НаКлиенте
Процедура ПолучитьОписаниеСтавокПСНВФоне()
	
	Если Не ИспользуетсяСервисРегиональныеСтавкиНалогов 
		Или Не ПрименяетсяПСН Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КодРегиона) Тогда
		Элементы.ДекорацияСтавкиПСНВВашемРегионе.Заголовок = НСтр("ru='Не указан регион регистрации'");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(КодВидаДеятельностиПСН) Тогда
		Элементы.ДекорацияСтавкиПСНВВашемРегионе.Заголовок = НСтр("ru='Не указан вид деятельности для патента'");
		Возврат;
	КонецЕсли;
	
	ПараметрыМетода = РегиональныеСтавкиНалоговКлиент.ПараметрыМетодаСтавкиПСН();
	ПараметрыМетода.КодРегиона = КодРегиона;
	ПараметрыМетода.КодВидаДеятельности = КодВидаДеятельностиПСН;
	ПараметрыМетода.Период = Период;
	
	Обработчик = Новый ОписаниеОповещения("ОбработкаПолученияОписанияСтавокПСН", ЭтотОбъект);
	
	РегиональныеСтавкиНалоговКлиент.ПолучитьОписаниеСтавокПСНВФоне(
		ЭтотОбъект,
		Элементы.ДекорацияСтавкиПСНВВашемРегионе, 
		Элементы.ДекорацияСтавкиПСНДлительнаяОперация,
		ПараметрыМетода,
		Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПолученияОписанияСтавокПСН(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	РегиональныеСтавкиНалоговКлиент.ОбработкаПолученияОписанияСтавокНалога(
		ДлительнаяОперация, 
		Элементы.ДекорацияСтавкиПСНВВашемРегионе, 
		Элементы.ДекорацияСтавкиПСНДлительнаяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСтавокПСН(НовыеСтавки)
	
	Если ТипЗнч(НовыеСтавки) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если КодРегиона <> ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовыеСтавки, "КодРегиона") Тогда
		ТекстПредупреждения = НСтр("ru='Нельзя применить ставку налога для другого региона.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	ИначеЕсли КодВидаДеятельностиПСН <> ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НовыеСтавки, "КодВидаДеятельности") Тогда
		ТекстПредупреждения = НСтр("ru='Нельзя применить ставку налога для другого вида деятельности.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если НовыеСтавки.Свойство("НалоговыеКаникулы") Тогда
		СтавкаПСН = 0;
	Иначе
		СтавкаПСН = НовыеСтавки.СтавкаНалога;
	КонецЕсли;
	ТекущийЭлемент = Элементы.СтавкаПСН;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользуетсяСервисРегиональныеСтавкиНалогов =
		РегиональныеСтавкиНалоговВызовСервера.ИспользуетсяСервисРегиональныхСтавок();
	ПолучитьОписаниеСтавокУСНВФоне();
	ПолучитьОписаниеСтавокПСНВФоне();
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеСистемныхНастроек.Удалить(ИмяФормы, "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоЮрЛицо(ВидОрганизации)
	
	Возврат (ВидОрганизации = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	
КонецФункции

#КонецОбласти
