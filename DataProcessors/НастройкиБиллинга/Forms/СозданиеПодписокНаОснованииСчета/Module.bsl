#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КодОО = Параметры.КодОО;
	ДатаПодключения = Параметры.ДатаПодключения;
	КодОбслуживаемогоАбонента = Параметры.КодОбслуживаемогоАбонента; 
	ИдентификаторСчета = Параметры.ИдентификаторСчета;
	СчетНаОплату = Параметры.СчетНаОплату;
	ЗаполнитьСписок();
	
	ПодпискиПоСчету = Список.НайтиСтроки(Новый Структура("ИдентификаторСчетаНаОплату", ИдентификаторСчета));
	Элементы.ГруппаПредупреждение.Видимость = (ПодпискиПоСчету.Количество() > 0);
	НомераПодписок = Новый Массив;
	Для Каждого Элемент Из ПодпискиПоСчету Цикл
		НомераПодписок.Добавить(Элемент.Номер);
	КонецЦикла;
	
	Элементы.ПредупреждениеТекст.Заголовок = СтрШаблон(
		НСтр("ru = 'На основании текущего счета уже оформлены подписки с номерами: %1'"),
		СтрСоединить(НомераПодписок,", "));
		
	ВыделитьПодпискиПоТекущемуСчетуНаОплату();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьСписокУчитываяТекущуюСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПодписки(Команда)
	
	Результат = СоздатьПодпискиНаТарифыНаСервере(СчетНаОплату, ДатаПодключения);
	КлючТекущейСтроки = "";
	Если Результат.Количество() > 0 Тогда
		
		КодыПодписок = Новый Массив;
		Для Каждого СтрокаПодписки Из Результат Цикл
			КодыПодписок.Добавить(СтрокаПодписки.КодПодписки);
		КонецЦикла;
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Создание:'"),, СтрШаблон(
			НСтр("ru = 'Созданы подписки: %1'"), СтрСоединить(КодыПодписок, ", ")),
			БиблиотекаКартинок.ДиалогИнформация);
		КлючТекущейСтроки = Результат[0].КодПодписки;
		ЗаполнитьСписок(КлючТекущейСтроки);
	Иначе
		ЗаполнитьСписокУчитываяТекущуюСтроку();
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьСписокУчитываяТекущуюСтроку()
	
	КлючТекущейСтроки = "";
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		КлючТекущейСтроки = Элементы.Список.ТекущиеДанные["Номер"];
	КонецЕсли;
	ЗаполнитьСписок(КлючТекущейСтроки);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписок(КлючТекущейСтроки = Неопределено)
	
	Отбор = ПрограммныйИнтерфейсСервиса.НовыйОтборПодписокНаТарифы();
	Отбор.ТолькоАктивные = Истина;
	Отбор.ТолькоОсновные = Истина;
	Отбор.КодОбслуживаемогоАбонента = КодОбслуживаемогоАбонента;
	
	ДанныеПодписок = ПрограммныйИнтерфейсСервиса.ПодпискиНаТарифы(Отбор, КодОО);
	ДанныеПодписок.Колонки.Добавить("ИндексКартинки", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	ДанныеПодписок.ЗаполнитьЗначения(14, "ИндексКартинки");
	
	Список.Загрузить(ДанныеПодписок);
	
	Если КлючТекущейСтроки <> 0 Тогда
		Поиск = Список.НайтиСтроки(Новый Структура("Номер", КлючТекущейСтроки));
		Если Поиск.Количество() > 0 Тогда
			Элементы.Список.ТекущаяСтрока = Поиск[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьПодпискиНаТарифыНаСервере(Счет, ДатаПодключения)
	
	Возврат ОплатаСервисаПодпискиБП.СоздатьПодпискиПоСчету(Счет, ДатаПодключения);
	
КонецФункции 

&НаСервере
Процедура ВыделитьПодпискиПоТекущемуСчетуНаОплату()	
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Список");
	ОформляемоеПоле.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ИдентификаторСчетаНаОплату");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = ИдентификаторСчета;
	ЭлементОтбораДанных.Использование = Истина;
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Шрифт");
	ЭлементЦветаОформления.Значение = ШрифтыСтиля.ВажнаяНадписьШрифт; 
	ЭлементЦветаОформления.Использование = Истина;

КонецПроцедуры

#КонецОбласти