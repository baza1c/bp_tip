
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ТекстОшибки = "";
	ДанныеСчета = ДанныеСчета(ПараметрКоманды, ТекстОшибки);
	Если ЗначениеЗаполнено(ТекстОшибки)  Тогда
		ПоказатьПредупреждение( , ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторСчета", ПараметрКоманды.УникальныйИдентификатор());
	ПараметрыФормы.Вставить("ДатаПодключения", ДанныеСчета.ДатаПодключения);
	ПараметрыФормы.Вставить("КодОО", ДанныеСчета.КодОО);
	ПараметрыФормы.Вставить("КодОбслуживаемогоАбонента", ДанныеСчета.КодОбслуживаемогоАбонента);
	ПараметрыФормы.Вставить("СчетНаОплату", ПараметрКоманды);
	
	ОткрытьФорму("Обработка.НастройкиБиллинга.Форма.СозданиеПодписокНаОснованииСчета", 
		ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ДанныеСчета(СчетЗаказ, ТекстОшибки)
	
	ВозвращаемыеДанные = Новый Структура;
	ВозвращаемыеДанные.Вставить("КодОО", 0);
	ВозвращаемыеДанные.Вставить("КодОбслуживаемогоАбонента", 0);
	ВозвращаемыеДанные.Вставить("ДатаПодключения", НачалоДня(ТекущаяДатаСеанса()));
	
	СвойстваОтвета = ПрограммныйИнтерфейсСервиса.НовыйОсновныеСвойстваОтвета();
	
	Попытка
		ДанныеСчета = ПрограммныйИнтерфейсСервиса.ДанныеСчетаНаОплату(СчетЗаказ.УникальныйИдентификатор(), , , СвойстваОтвета);
	Исключение
		Если СвойстваОтвета.КодОтвета = 10404 Тогда
			ТекстОшибки = НСтр("ru = 'В Менеджере сервиса отсутствует счет на оплату, соответствующий текущему счету.'");
		Иначе
			ТекстОшибки = ТехнологияСервиса.КраткийТекстОшибки(ИнформацияОбОшибке());
		КонецЕсли;
		Возврат Ложь;
	КонецПопытки;
	
	Если ДанныеСчета.Тарифы.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Счет на оплату не содержит тарифов сервиса для оформления подписки.'");
		Возврат Ложь;
	КонецЕсли;
	
	НачалоНовойПодпискиАбонента = 
		ОплатаСервисаПодпискиБП.НачалоНовойПодпискиАбонента(ДанныеСчета.КодПродавца, ДанныеСчета.КодПокупателя);
	
	Если ЗначениеЗаполнено(НачалоНовойПодпискиАбонента) Тогда
		ВозвращаемыеДанные.ДатаПодключения = НачалоНовойПодпискиАбонента;
	КонецЕсли;
	ВозвращаемыеДанные.КодОО = ДанныеСчета.КодПродавца;
	ВозвращаемыеДанные.КодОбслуживаемогоАбонента = ДанныеСчета.КодПокупателя;
	
	Возврат ВозвращаемыеДанные;
	
КонецФункции
