
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	
	ИмяОтчета =
		УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПолучитьСоответствиеВидовУведомленийИменамОтчетов()[Параметры.ВидУведомления];
	
	ТаблицаФорм = Отчеты[ИмяОтчета].ПолучитьТаблицуФорм();
	СписокВыбора = Элементы.ПолеРедакцияФормы.СписокВыбора;
	СписокВыбора.Очистить();
	Для Каждого СтрокаТаблицы Из ТаблицаФорм Цикл
		СписокВыбора.Добавить(СтрокаТаблицы.ИмяФормы, СтрокаТаблицы.ОписаниеФормы);
		ПолеРедакцияФормы = СтрокаТаблицы.ИмяФормы;
	КонецЦикла;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если РегистрацияОрганизацииКлиентСервер.ЭтоРегистрацияНовогоБизнеса(ИмяОтчета) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПроверяемыеРеквизиты, "Организация");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Элементы.Создать.Доступность = ЗначениеЗаполнено(Организация);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	СоздатьУведомление();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБезПомощника(Команда)
	
	СоздатьУведомление(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьУведомление(ИспользоватьПомощник = Истина)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьПомощник Тогда
		ОткрытьПомощникПодготовкиУведомления();
	Иначе
		ОткрытьСтандартнуюФормуУведомления();
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПомощникПодготовкиУведомления()
	
	Если УчетПСНКлиентСервер.ЭтоЗаявлениеНаПолучениеПатента(ИмяОтчета) Тогда
		
		Если ВыбранаАктуальнаяФормаЗаявленияНаПатент(ПолеРедакцияФормы) Тогда
			
			ПараметрыПомощника = Новый Структура;
			ПараметрыПомощника.Вставить("Организация", Организация);
			ПараметрыПомощника.Вставить("СоздатьНовоеЗаявление", Истина);
			
			ОткрытьФорму("Обработка.ПомощникЗаполненияЗаявленияНаПолучениеПатента.Форма", ПараметрыПомощника);
			
		Иначе
			ОткрытьСтандартнуюФормуУведомления();
		КонецЕсли;
		
	ИначеЕсли ИмяОтчета = "РегламентированноеУведомлениеФормаР26001" Тогда
		
		ПараметрыПомощника = РегистрацияОрганизацииКлиентСервер.НовыеПараметрыПомощникаВнесенияИзменений();
		ПараметрыПомощника.Организация = Организация;
		ПараметрыПомощника.КонтекстныйВызов = Истина;
		ПараметрыПомощника.СоздатьПриОткрытии = Истина;
		
		РегистрацияОрганизацииКлиент.ОткрытьПомощникСнятияСУчета(ПараметрыПомощника);
		
	ИначеЕсли РегистрацияОрганизацииКлиентСервер.ЭтоРегистрацияНовогоБизнеса(ИмяОтчета) Тогда
		
		ЮридическоеФизическоеЛицо = РегистрацияОрганизацииВызовСервера.ЮридическоеФизическоеЛицоПоИмениОтчета(ИмяОтчета); 
		
		ПараметрыПомощника = Новый Структура;
		ПараметрыПомощника.Вставить("НавигацияПараметрФормы", "ОКВЭД");
		ПараметрыПомощника.Вставить("НоваяРегистрация", Истина);
		ПараметрыПомощника.Вставить("КонтекстныйВызов", Истина);
		ПараметрыПомощника.Вставить("ЮридическоеФизическоеЛицо", ЮридическоеФизическоеЛицо);
		ОткрытьФорму("Обработка.РегистрацияОрганизации.Форма.ФормаВыбораВидовДеятельности", ПараметрыПомощника, ЭтотОбъект);
		
	ИначеЕсли УведомлениеМожноЗаполнитьВПомощнике Тогда
		
		ПараметрыПомощника = РегистрацияОрганизацииКлиентСервер.НовыеПараметрыПомощникаВнесенияИзменений();
		ПараметрыПомощника.Организация = Организация;
		ПараметрыПомощника.КонтекстныйВызов = Истина;
		ПараметрыПомощника.СоздатьПриОткрытии = Истина;
		
		РегистрацияОрганизацииКлиент.ОткрытьПомощникВнесенияИзменений(ПараметрыПомощника);
		
	Иначе
		
		ОткрытьСтандартнуюФормуУведомления();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтандартнуюФормуУведомления()
	
	ПараметрыОткрытия = Новый Структура("Организация", Организация);
	ОткрытьФорму(СтрШаблон("Отчет.%1.Форма.%2", ИмяОтчета, ПолеРедакцияФормы), ПараметрыОткрытия, ВладелецФормы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыбранаАктуальнаяФормаЗаявленияНаПатент(ВыбраннаяФорма)
	
	ИмяАктуальнойФормы = РегламентированнаяОтчетность.ПолучитьАктуальнуюФормуУведомления(
		Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеНаПолучениеПатентаРекомендованнаяФорма);
	
	Возврат ВыбраннаяФорма = ИмяАктуальнойФормы;
	
КонецФункции

&НаСервере
Функция УправлениеФормой()
	
	УведомлениеМожноЗаполнитьВПомощнике = Обработки.РегистрацияОрганизации.ДоступенСервис1СКонтрагент();
	
	Если РегистрацияОрганизацииКлиентСервер.ЭтоРегистрацияНовогоБизнеса(ИмяОтчета) Тогда
		// В помощнике регистрации нового ИП или ООО создается элемент справочника Организации:
		// если нет доступа к справочнику, то пользователь не сможет воспользоваться помощником -
		// заявление будет открыто в регламентированной форме
		Если РегистрацияОрганизацииСервер.ДоступенПомощникРегистрацииНовогоБизнеса() Тогда
			Элементы.Создать.Видимость = Истина;
			Элементы.Создать.Доступность = Истина;
			Элементы.ФормаСоздатьБезПомощника.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
			Элементы.ФормаСоздатьБезПомощника.КнопкаПоУмолчанию = Ложь;
		Иначе
			Элементы.Создать.Видимость = Ложь;
			Элементы.Создать.Доступность = Ложь;
			Элементы.ФормаСоздатьБезПомощника.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
			Элементы.ФормаСоздатьБезПомощника.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		Элементы.Организация.Видимость = Ложь;
	Иначе
		Элементы.Создать.Доступность = ЗначениеЗаполнено(Организация);
		Элементы.Организация.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ФормаСоздатьБезПомощника.Видимость = УведомлениеМожноЗаполнитьВПомощнике;
	
КонецФункции

#КонецОбласти
