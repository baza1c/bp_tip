
#Область ОписаниеПеременных

&НаКлиенте
Перем НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ДатаОтправкиОнлайн = ТекущаяДатаСеанса();
	ЭтоНачалоРаботы = ПолучитьФункциональнуюОпцию("НачалоРаботы");
	ИспользоватьНесколькоОрганизаций = Справочники.Организации.ИспользуетсяНесколькоОрганизаций();
	
	Если ЭтоНачалоРаботы Тогда
		// Реквизит ПоискИННЗапрос сохраняется в данных формы автоматически
		Организация = Справочники.Организации.ПустаяСсылка();
	Иначе
		ПоискИННЗапрос = "";
		Организация = Параметры.Организация;
		СоздатьПриОткрытии = Параметры.СоздатьПриОткрытии;
		ЗаявлениеОВнесенииИзмененийЕГР = Параметры.Заявление;
		ОКВЭД2ИзКлассификацииВыручки = Параметры.ОКВЭД2ИзКлассификацииВыручки;
	КонецЕсли;
	
	ВыдатьНаБумажномНосителе = Истина;
	ВидУстава = ЗначениеТиповойУстав();
	
	ШрифтИзмененныхРеквизитов = Новый Шрифт(ШрифтыСтиля.ОбычныйШрифтТекста, , , , , , Истина); // Зачеркнутый
	
	СервисУзнатьИННДоступен = Обработки.РегистрацияОрганизации.СервисУзнатьИННДоступен();
	УзнатьИНН = НСтр("ru = 'Узнать ИНН'");
	
	РегистрирующийОрганАдресНаКарте        = УправлениеКонтактнойИнформациейБП.СтрокаСсылкиПоказатьНаКарте();
	РегистрирующийОрганПрежнийАдресНаКарте = УправлениеКонтактнойИнформациейБП.СтрокаСсылкиПоказатьНаКарте();
	МФЦАдресНаКарте = УправлениеКонтактнойИнформациейБП.СтрокаСсылкиПоказатьНаКарте();
	
	Элементы.УплатаГоспошлиныЗаголовокДекорация.Заголовок = СтрШаблон(НСтр("ru = 'Оплатите госпошлину %1 руб.'"),
		Обработки.РегистрацияОрганизации.СуммаПошлиныЗаВнесениеИзмененийВУчредительныеДокументы());
	
	СвойстваСтраныИГражданства = РегистрацияОрганизацииСервер.СвойстваСтраныИГражданстваПоУмолчанию();
	Гражданство = СвойстваСтраныИГражданства.Гражданство;
	КодРоссии = СвойстваСтраныИГражданства.КодРоссии;
	
	Элементы.Гражданство.СписокВыбора.Очистить();
	Для Каждого ЗначениеСписка Из СвойстваСтраныИГражданства.СписокВыбораГражданства Цикл
		Элементы.Гражданство.СписокВыбора.Добавить(ЗначениеСписка.Значение, ЗначениеСписка.Представление);
	КонецЦикла;
	
	ПодачаДокументовВЭлектронномВидеДоступна = ИнтерфейсыВзаимодействияБРОКлиентСервер.ПоддерживаетсяФормированиеПакетаДляВнесенияИзмененийВЕГРЮЛ();
	
	СписокНалоговыхОрганов = РегистрыСведений.РегистрирующиеНалоговыеОрганы.СписокРегистрирующихОрганов();
	Элементы.РегистрирующийОрганСписок.СписокВыбора.Очистить();
	
	Для Каждого ЗначениеСписка Из СписокНалоговыхОрганов Цикл
		Элементы.РегистрирующийОрганСписок.СписокВыбора.Добавить(ЗначениеСписка.Значение, ЗначениеСписка.Представление);
	КонецЦикла;
	
	РегистрацияОрганизацииСервер.УстановитьПросмотрЭлементовФормы(
		ЭтотОбъект,
		Не РегистрацияОрганизацииСервер.ДоступенПомощникРегистрацииНовогоБизнеса());
	
	ИзменитьЭтап("ВнесениеИзмененийЕГР_СписокИзменений");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтоНачалоРаботы Тогда
		// Оповестим другие формы сервиса регистрации, чтобы они закрылись.
		РегистрацияОрганизацииКлиент.ОповеститьОбОткрытии(ЭтотОбъект,
			РегистрацияОрганизацииКлиентСервер.ИмяПомощникаВнесенияИзменений(),
			НавигацияНомерШага);
	КонецЕсли;
	
	Если ЭтоНачалоРаботы Тогда
		Если Не ПустаяСтрока(ПоискИННЗапрос) Тогда
			ПоказатьИндикаторОжидания();
			ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьРеквизитыОрганизацииПоИННПриОткрытии", 0.1, Истина);
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(Организация) Тогда
		Если Не ПустаяСтрока(ИННОрганизации(Организация)) Тогда
			ПоказатьИндикаторОжидания();
			ПодключитьОбработчикОжидания("Подключаемый_ЗаполнитьРеквизитыОрганизацииПоИННПриОткрытии", 0.1, Истина);
		ИначеЕсли Не ИспользоватьНесколькоОрганизаций Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не указан ИНН организации'"));
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ Модифицированность ИЛИ ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
	ПоказатьВопрос(
		ОписаниеОповещения,
		НСтр("ru='Данные были изменены. Сохранить изменения?'"),
		РежимДиалогаВопрос.ДаНетОтмена,
		,
		КодВозвратаДиалога.Да);
	
	Отказ = Истина;
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ИсточникВыбора) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		Если ИсточникВыбора.ИмяФормы = ИмяФормыИзмененияНаименования() Тогда
			ОбработатьИзменениеНаименования(ВыбранноеЗначение);
		ИначеЕсли ИсточникВыбора.ИмяФормы = ИмяФормыИзмененияРуководителя() Тогда
			ОбработатьИзменениеРуководителя(ВыбранноеЗначение);
		ИначеЕсли ИсточникВыбора.ИмяФормы = ИмяФормыИзмененияУчредителей() Тогда
			ОбработатьИзменениеУчредителей(ВыбранноеЗначение);
		ИначеЕсли ИсточникВыбора.ИмяФормы = ИмяФормыИзмененияВидовДеятельности() Тогда
			ОбработатьИзменениеВидовДеятельности(ВыбранноеЗначение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = ЭтотОбъект Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменитьПараметрНавигации" Тогда
		
		ИзменитьЭтап(Параметр);
		
	ИначеЕсли ИмяСобытия = "ОткрытШагПомощника_РегистрацияОрганизации" Тогда
		
		ИмяЭтогоПомощника = РегистрацияОрганизацииКлиентСервер.ИмяПомощникаВнесенияИзменений();
		Если ЭтоНачалоРаботы И (Параметр = Неопределено Или Параметр.ИмяПомощника <> ИмяЭтогоПомощника) Тогда
			// Пользователь открыл другую форму помощника регистрации
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗакрытьПомощникВнесенияИзмененийЕГР"
		И (Параметр = ЗаявлениеОВнесенииИзмененийУчредительныеДокументы
			Или Параметр = ЗаявлениеОВнесенииИзмененийЕГР) Тогда
		
		// Пользователь изменил заявление вручную в форме регл. уведомления - закрываем помощник.
		Закрыть();
		
	ИначеЕсли ИмяСобытия = "ЗавершенаРаботаПомощникаНачалаРаботы" Тогда
		
		// Работа помощника начала работы может быть завершена в другом окне (например, из окна БыстрыйСтарт).
		// В этом случае нужно закрыть окно помощника регистрации. Но пользователь мог начать изменять значение какого-нибудь
		// текстового поля и при закрытии окна сработает обработчик "ПриИзменении" и форма станет Модифицированной и не сможет
		// закрыться без вопроса. Для этого изменяем текущий элемент и снимаем модифицированность.
		ТекущийЭлемент = Элементы.СоздатьЗаявление;
		Модифицированность = Ложь;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗавершеноВнесениеИзмененийЕГР" И Параметр = Организация Тогда
		
		// Внесение изменений в ЕГР завершено, открываем первый шаг помощника и сбрасываем изменения реквизитов.
		ЗаполнитьРеквизитыОрганизацииПоИНН(ИННОрганизации(Организация), Ложь);
		
	ИначеЕсли ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		
		Если Параметр = Объект.Ссылка
			Или Параметр = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Объект.Ссылка) Тогда
			УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Ссылка));
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_ФизическиеЛица" Тогда
		
		Если Источник = Заявитель.Ссылка Тогда
			
			ПрочитатьДанныеЗаявителя();
			Если Не НомерТелефонаПодтвержден Тогда
				РегистрацияОрганизацииКлиент.ЗаявительТелефонПриИзменении(
					ЭтотОбъект,
					ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительТелефонПредставление"));
				ТелефонЗаявителяПриИзменении(ЗаявительТелефонПредставление);
			КонецЕсли;
			
			Если Не ЭлектроннаяПочтаПодтверждена Тогда
				ЗаявительEMailПриИзменении();
				РегистрацияОрганизацииКлиент.ЗаявительПочтаПриИзменении(
					ЭтотОбъект,
					ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительEMailПолучениеДокументов"));
			КонецЕсли;
			
		КонецЕсли;
		
		НайденныеУчредители = Учредители.НайтиСтроки(Новый Структура("Ссылка", Источник));
		Для Каждого Учредитель Из НайденныеУчредители Цикл
			ЗаполнитьДобавленныеКолонкиВСтрокеТаблицыУчредители(Учредитель, Заявитель.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ТекущийЭтап = "ВнесениеИзмененийЕГР_СписокИзменений" Тогда
		
		ПроверитьЗаполнениеСпискаИзменений(Отказ);
		
	ИначеЕсли ТекущийЭтап = "ВнесениеИзмененийЕГР_Устав" Тогда
		
		ПроверитьЗаполнениеУстава(Отказ);
		
	ИначеЕсли ТекущийЭтап = "ВнесениеИзмененийЕГР_Заявление" Тогда
		
		Если Модифицированность Тогда
			ДанныеОСрокеПодачи = ДанныеОСрокеПодачиЗаявления();
			Если ДанныеОСрокеПодачи.МаксимальнаяДата < ТекущаяДатаСеанса() Тогда
				РабочиеДниСтрокой = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';%1 рабочего дня;%1 рабочих дней;%1 рабочих дней;%1 рабочих дней;%1 рабочих дней'"),
					ДанныеОСрокеПодачи.КоличествоДней);
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Заявление должно быть подано в течение %1 после изменения сведений.'"),
					РабочиеДниСтрокой);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
		
		Если ЭтоЮрЛицо(ЭтотОбъект) И Не ИзмененРуководитель(ЭтотОбъект) Тогда
			ПроверитьЗаполнениеПаспортныхДанныхЗаявителя(Отказ);
		КонецЕсли;
		
		ПроверитьЗаполнениеЗаявления(Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигацияПомощниковКлиент.ЭтоНавигационнаяСсылкаШага(НавигационнаяСсылкаФорматированнойСтроки) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		// Для корректной работы навигации необходимо передавать ключевые параметры формы.
		СтруктураНавигации = СтруктураНавигации(ДатаРешения, ЭтоЮрЛицо(ЭтотОбъект));
		СтруктураШага = СтруктураНавигации[НавигационнаяСсылкаФорматированнойСтроки];
		Если СтруктураШага <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтруктураШага.СтруктураПараметровФормы, Параметры);
		КонецЕсли;
		
		НавигацияПомощниковКлиент.ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки, СтруктураНавигации);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область СписокИзменений

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Организация) И Организация <> Объект.Ссылка Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаИННПриИзменении(Элемент)
	
	Если Не ПустаяСтрока(ПоискИННЗапрос) Тогда
		ПоискИННОтвет = ОрганизацииФормыДляОтчетностиВызовСервера.ЗапроситьДанныеЕГР(НовыйЗапросДанныхЕГР(ПоискИННЗапрос));
		
		// Пользователь сразу после изменения поля мог нажать команду "Заполнить".
		// Дадим возможность выполниться обработчику команды заполнения.
		// Поэтому фактическую обработку результата выполняем после обработчика изменения поля.
		ПодключитьОбработчикОжидания("Подключаемый_ОбработатьРезультатПоискаИНН", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНаименованиеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	Если Не ПустаяСтрока(НаименованиеПолноеНовое) И ПустаяСтрока(НаименованиеСокращенноеНовое) Тогда
		ПараметрыФормы.Вставить("НаименованиеСокращенное", НаименованиеСокращенное);
	Иначе
		ПараметрыФормы.Вставить("НаименованиеСокращенное", НаименованиеСокращенноеНовое);
	КонецЕсли;
	Если Не ПустаяСтрока(НаименованиеСокращенноеНовое) И ПустаяСтрока(НаименованиеПолноеНовое) Тогда
		ПараметрыФормы.Вставить("НаименованиеПолное", НаименованиеПолное);
	Иначе
		ПараметрыФормы.Вставить("НаименованиеПолное", НаименованиеПолноеНовое);
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормыИзмененияНаименования(), ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЮридическийАдресНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации"));
	ПараметрыОткрытия.Вставить("ЗначенияПолей", ЮридическийАдресНовыйЗначениеJSON);
	ПараметрыОткрытия.Вставить("Представление", ЮридическийАдресНовый);
	ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru = 'Новый юридический адрес'"));
	ПараметрыОткрытия.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаЗакрытияФормыИзмененияЮридическогоАдреса", ЭтотОбъект);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, Элемент, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРуководителяНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Новый руководитель'"));
	ПараметрыФормы.Вставить("ИспользоватьДолжность", Истина);
	
	Если ЗначениеЗаполнено(ОтветственныеЛицаНовые.ФизическоеЛицо) Тогда
		ПараметрыФормы.Вставить("Ключ",      ОтветственныеЛицаНовые.ФизическоеЛицо);
		ПараметрыФормы.Вставить("Должность", ОтветственныеЛицаНовые.Должность);
		ПараметрыФормы.Вставить("СрокПолномочий", ОтветственныеЛицаНовые.СрокПолномочий);
	Иначе
		ПараметрыФормы.Вставить("Должность", РуководительДолжность);
		ПараметрыФормы.Вставить("СрокПолномочий", 5);
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормыИзмененияРуководителя(), ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьУчредителейНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяИзменяемойТаблицы = ?(ИзмененыУчредители(ЭтотОбъект), "УчредителиНовые", "Учредители");
	ИзменяемыйУставныйКапитал = ?(ИзмененУставныйКапитал(ЭтотОбъект), УставныйКапиталНовый, УставныйКапитал);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("АдресХранилища",            СохранитьТаблицуВоВременноеХранилище(ИмяИзменяемойТаблицы));
	ПараметрыФормы.Вставить("АдресХранилищаДоИзменения", СохранитьТаблицуВоВременноеХранилище("Учредители"));
	ПараметрыФормы.Вставить("УставныйКапитал",            ИзменяемыйУставныйКапитал);
	ПараметрыФормы.Вставить("УставныйКапиталДоИзменения", УставныйКапитал);
	
	ОткрытьФорму(ИмяФормыИзмененияУчредителей(), ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидыДеятельностиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяИзменяемойТаблицы = ?(ИзмененыВидыДеятельности(ЭтотОбъект), "ВидыДеятельностиНовые", "ВидыДеятельности");
	ИзменяемыйОсновнойКод = ?(ИзмененОсновнойВидДеятельности(ЭтотОбъект), КодОКВЭД2Новый, КодОКВЭД2);
	
	ИзменяемаяТаблица = ЭтотОбъект[ИмяИзменяемойТаблицы];
	Если ИзменяемаяТаблица.Количество() > 0 Тогда
		Если ПустаяСтрока(ИзменяемаяТаблица[0].Наименование) Тогда
			// Отложеное заполнение
			ЗаполнитьНаименованияВидовДеятельности();
		КонецЕсли;
	КонецЕсли;
	
	КодыОКВЭДИзЕГРЮЛ = Новый Массив;
	Для Каждого ТекущийОКВЭД Из ВидыДеятельности Цикл
		КодыОКВЭДИзЕГРЮЛ.Добавить(ТекущийОКВЭД.Код);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЮридическоеФизическоеЛицо", Объект.ЮридическоеФизическоеЛицо);
	ПараметрыФормы.Вставить("АдресХранилища", СохранитьТаблицуВоВременноеХранилище(ИмяИзменяемойТаблицы));
	ПараметрыФормы.Вставить("ОсновнойКод", ИзменяемыйОсновнойКод);
	ПараметрыФормы.Вставить("ОсновнойКодЕГР", КодОКВЭД2);
	ПараметрыФормы.Вставить("КодыОКВЭДИзЕГРЮЛ", ПоместитьВоВременноеХранилище(КодыОКВЭДИзЕГРЮЛ, УникальныйИдентификатор));
	
	ОткрытьФорму(ИмяФормыИзмененияВидовДеятельности(), ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ГражданствоПриИзменении(Элемент)
	
	ОбработатьИзменениеСтраныГражданства();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ФамилияЗаявителяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Фамилия'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(Текст, "Заявитель.Фамилия", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ФамилияЗаявителяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Фамилия'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(Текст, "Заявитель.Фамилия", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяЗаявителяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Имя'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(Текст, "Заявитель.Имя", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяЗаявителяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Имя'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(Текст, "Заявитель.Имя", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоЗаявителяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Отчество'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(Текст, "Заявитель.Отчество", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоЗаявителяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Отчество'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(Текст, "Заявитель.Отчество", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура МестоРожденияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Место рождения'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(
		Текст, "Заявитель.МестоРожденияПредставление", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура МестоРожденияОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Место рождения'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(
		Текст, "Заявитель.МестоРожденияПредставление", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПаспортКемВыданАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Кем выдан паспорт'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(
		Текст, "ПаспортныеДанныеЗаявителя.КемВыдан", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПаспортКемВыданОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Кем выдан паспорт'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(
		Текст, "ПаспортныеДанныеЗаявителя.КемВыдан", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПаспортныеДанныеЗаявителяКемВыданАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Кем выдан паспорт'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(
		Текст, "ПаспортныеДанныеЗаявителя.КемВыдан", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПаспортныеДанныеЗаявителяКемВыданОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗаголовокЭлемента = НСтр("ru = 'Кем выдан паспорт'");
	РегистрацияОрганизацииКлиент.ПроверитьЯзыкВвода(
		Текст, "ПаспортныеДанныеЗаявителя.КемВыдан", ЗаголовокЭлемента, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Устав

&НаКлиенте
Процедура НадписьИзменениеУставаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ИндивидуальныйУстав" Тогда
		ОткрытьПрисоединенныйКЗаявлениюФайл("Устав");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидУставаПриИзменении(Элемент)
	
	НомерТиповогоУстава = НомерТиповогоУставаПрежний;
	
	Если ИзменитьВидУстава И ВидУстава = ЗначениеТиповойУстав() И НомерТиповогоУстава = 0 Тогда
		НомерТиповогоУстава = РегистрацияОрганизацииКлиентСервер.НомерУставаПоУмолчанию();
	КонецЕсли;
	
	НомерТиповогоУставаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидУставаПриИзменении(Элемент)
	
	НомерТиповогоУстава = НомерТиповогоУставаПрежний;
	
	Если ВидУстава = ЗначениеТиповойУстав() И НомерТиповогоУстава = 0 Тогда
		НомерТиповогоУстава = РегистрацияОрганизацииКлиентСервер.НомерУставаПоУмолчанию();
	ИначеЕсли ВидУстава <> ЗначениеТиповойУстав() Тогда
		НомерТиповогоУстава = 0;
	КонецЕсли;
	
	НомерТиповогоУставаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УставДобровольныйВыходИзОбществаПриИзменении(Элемент)
	
	ПриИзмененииПараметровУстава();
	
КонецПроцедуры

&НаКлиенте
Процедура УставРешенияПодтверждаютсяНотариальноПриИзменении(Элемент)
	
	ПриИзмененииПараметровУстава();
	
КонецПроцедуры

&НаКлиенте
Процедура УставПродажаДолиУчастникамБезСогласияПриИзменении(Элемент)
	
	ПриИзмененииПараметровУстава();
	
КонецПроцедуры

&НаКлиенте
Процедура УставПродажаДолиТретьимЛицамБезСогласияПриИзменении(Элемент)
	
	ПриИзмененииПараметровУстава();
	
КонецПроцедуры

&НаКлиенте
Процедура УставОтсутствуетПреимущественноеПравоПриПродажеПриИзменении(Элемент)
	
	ПриИзмененииПараметровУстава();
	
КонецПроцедуры

&НаКлиенте
Процедура УставНаследованиеБезСогласияПриИзменении(Элемент)
	
	ПриИзмененииПараметровУстава();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТиповогоУставаПриИзменении(Элемент)
	
	НомерТиповогоУставаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьНомерТиповогоУставаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегистрацияОрганизацииКлиент.ОткрытьФормуПросмотраТиповогоУстава(
		ЭтотОбъект,
		НомерТиповогоУстава,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область Заявление

&НаКлиенте
Процедура ОтчествоЗаявителяПриИзменении(Элемент)
	
	ОрганизацииФормыКлиентСервер.ОпределитьПолПоОтчеству(Заявитель.Пол, Заявитель.Отчество);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьАвтоПодбор(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьРуководителяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСДолжностямиБПКлиент.ДолжностьОкончаниеВводаТекста(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИННЗаявителяПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УзнатьИНННажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	ПроверитьЗаполнениеПаспортныхДанныхЗаявителя(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ДанныеФизическогоЛица", ДанныеЗаявителя());
	
	ОткрытьФорму("Обработка.РегистрацияОрганизации.Форма.УзнатьИНН", ПараметрыФормы, Элементы.ИННЗаявителя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительАдресПропискиПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	ВыборАдресаФизическогоЛица(
		Элемент,
		СтандартнаяОбработка,
		ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица"),
		"ЗаявительАдресПропискиЗначениеJSON");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительАдресДляИнформированияПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	ВыборАдресаФизическогоЛица(
		Элемент,
		СтандартнаяОбработка,
		ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресДляИнформированияФизическиеЛица"),
		"ЗаявительАдресДляИнформированияЗначениеJSON");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительТелефонПредставлениеПриИзменении(Элемент)
	
	ТелефонЗаявителяПриИзменении(Элемент.ТекстРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительТелефонПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформацииЗаявителя(ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ПараметрыКонтактнойИнформации.Телефон);
	ПараметрыОткрытия.Вставить("ЗначенияПолей", ЗаявительТелефонЗначениеJSON);
	ПараметрыОткрытия.Вставить("Представление", ЗаявительТелефонПредставление);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительТелефонПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		// Отказ от выбора, данные неизменны
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаявительТелефонЗначениеJSON = ВыбранноеЗначение.Значение;
	ЗаявительТелефонПредставление = ВыбранноеЗначение.Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительEMailПредставлениеПриИзменении(Элемент)
	
	ЗаявительEMailПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительEMailПолучениеДокументовПриИзменении(Элемент)
	
	ЗаявительEMailПриИзменении();
	
	РегистрацияОрганизацииКлиент.ЗаявительПочтаПриИзменении(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, Элемент.Имя));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительТелефонПриИзменении(Элемент)
	
	РегистрацияОрганизацииКлиент.ЗаявительТелефонПриИзменении(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, Элемент.Имя));
	
	ТелефонЗаявителяПриИзменении(ЗаявительТелефонПредставление);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПодачиДокументоЭлектронноОнлайнПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПодачиДокументоЭлектронноПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПодачиДокументовИФНСЛичноПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПодачиДокументовМФЦличноПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПодачиДокументовПочтаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПодачиДокументовИФНСПредставительПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КодПодтвержденияТелефонаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КодПодтвержденияТелефона = СокрЛП(Текст);
	
	Если СтрДлина(Текст) = РегистрацияОрганизацииКлиентСервер.ДлинаКодаПодтверждения() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьКодПодтверждения_Телефон", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодПодтвержденияПочтыИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	КодПодтвержденияПочты = СокрЛП(Текст);
	
	Если СтрДлина(Текст) = РегистрацияОрганизацииКлиентСервер.ДлинаКодаПодтверждения() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьКодПодтверждения_Почта", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстВыслатьПовторноКодПодтвержденияНажатие(Элемент)
	
	СрокПовторнойОтправкиКодаДляТелефона = Неопределено;
	КодПодтвержденияТелефона = "";
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения_Телефон", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстВыслатьПовторноКодПодтвержденияПочтаНажатие(Элемент)
	
	СрокПовторнойОтправкиКодаДляПочты = Неопределено;
	КодПодтвержденияПочты = "";
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения_Почта", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Документы

&НаКлиенте
Процедура ДатаРешенияПриИзменении(Элемент)
	
	ОбновитьРеквизитыВыбраннойФормы(ДатаРешения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеОВнесенииИзмененийЕГРИПДекорацияНажатие(Элемент)
	
	ОткрытьЗаявлениеОРегистрацииИзменений("ЗаявлениеОВнесенииИзмененийЕГР", Элемент.Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеОВнесенииИзмененийЕГРЮЛДекорацияНажатие(Элемент)
	
	ОткрытьЗаявлениеОРегистрацииИзменений("ЗаявлениеОВнесенииИзмененийЕГР", Элемент.Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеОРегистрацииИзмененийВУставеДекорацияНажатие(Элемент)
	
	ОткрытьЗаявлениеОРегистрацииИзменений("ЗаявлениеОВнесенииИзмененийУчредительныеДокументы", Элемент.Заголовок);
	
КонецПроцедуры

&НаКлиенте
Процедура УплатаГоспошлиныОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОрганизацииФормыКлиент.ПерейтиКСервисуСкачатьКвитанциюНаОплатуГоспошлиныПриРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыУплатыГоспошлиныНажатие(Элемент)
	
	ПлатежныеРеквизиты = Новый Структура;
	ПлатежныеРеквизиты.Вставить("Получатель", ПлатежныеРеквизитыНаименованиеПолучателя);
	ПлатежныеРеквизиты.Вставить("ИНН", ПлатежныеРеквизитыИНН);
	ПлатежныеРеквизиты.Вставить("КПП", ПлатежныеРеквизитыКПП);
	ПлатежныеРеквизиты.Вставить("НомерСчета", ПлатежныеРеквизитыНомерСчета);
	ПлатежныеРеквизиты.Вставить("БанкПолучателя", ПлатежныеРеквизитыНаименованиеБанка);
	ПлатежныеРеквизиты.Вставить("БИК", ПлатежныеРеквизитыБИК);
	ПлатежныеРеквизиты.Вставить("КБК", ПлатежныеРеквизитыКБК);
	ПлатежныеРеквизиты.Вставить("Сумма", ПлатежныеРеквизитыСумма);
	
	ОткрытьФорму("Обработка.РегистрацияОрганизации.Форма.РеквизитыОплатыГоспошлины", ПлатежныеРеквизиты, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КвитанцияНаОплатуГоспошлиныНажатие(Элемент)
	
	ОткрытьПрисоединенныйКЗаявлениюФайл("КвитанцияНаОплатуГоспошлины");
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрирующийОрганАдресНаКартеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, РегистрирующийОрганАдрес);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрирующийОрганПрежнийАдресНаКартеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, РегистрирующийОрганПрежнийАдрес);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаАдресИФНСНаКартеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, РегистрирующийОрганАдрес);
	
КонецПроцедуры

&НаКлиенте
Процедура МФЦАдресНаКартеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьНаКартеБлижайшийМФЦ(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаИнструкцияЕГРИПОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьПрисоединенныйКЗаявлениюФайл("ИнструкцияПоВнесениюИзмененийВЕГРИП");
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаИнструкцияЕГРЮЛОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьПрисоединенныйКЗаявлениюФайл("ИнструкцияПоВнесениюИзмененийВЕГРЮЛ");
	
КонецПроцедуры

&НаКлиенте
Процедура УставОООНажатие(Элемент)
	
	ОткрытьПрисоединенныйКЗаявлениюФайл("Устав");
	
КонецПроцедуры

&НаКлиенте
Процедура РешениеЕдинственногоУчредителяОООНажатие(Элемент)
	
	ОткрытьПрисоединенныйКЗаявлениюФайл("РешениеЕдинственногоУчредителя");
	
КонецПроцедуры

&НаКлиенте
Процедура ПротоколСобранияУчредителейОООНажатие(Элемент)
	
	ОткрытьПрисоединенныйКЗаявлениюФайл("ПротоколСобранияУчредителей");
	
КонецПроцедуры

&НаКлиенте
Процедура ПротоколСобранияУчредителейЕдинственногоУчредителяОООНажатие(Элемент)
	
	ОткрытьПрисоединенныйКЗаявлениюФайл("ПротоколСобранияУчредителейЕдинственногоУчредителя");
	
КонецПроцедуры

&НаКлиенте
Процедура СканЗаявлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегистрацияОрганизацииКлиент.ВыборОтсканированногоФайлаНажатие(ЭтотОбъект, ЗаявлениеОВнесенииИзмененийЕГР, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СканУставНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегистрацияОрганизацииКлиент.ВыборОтсканированногоФайлаНажатие(ЭтотОбъект, ЗаявлениеОВнесенииИзмененийЕГР, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СканПротоколНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегистрацияОрганизацииКлиент.ВыборОтсканированногоФайлаНажатие(ЭтотОбъект, ЗаявлениеОВнесенииИзмененийЕГР, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СканРешениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегистрацияОрганизацииКлиент.ВыборОтсканированногоФайлаНажатие(ЭтотОбъект, ЗаявлениеОВнесенииИзмененийЕГР, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура СканДокументПодтверждающийАдресНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегистрацияОрганизацииКлиент.ВыборОтсканированногоФайлаНажатие(ЭтотОбъект, ЗаявлениеОВнесенииИзмененийЕГР, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрирующийОрганКодПриИзменении(Элемент)
	
	РегистрацияОрганизацииКлиент.РегистрирующийОрганКодПриИзменении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрирующийОрганСписокАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РегистрацияОрганизацииКлиент.РегистрирующийОрганСписокАвтоПодбор(ЭтотОбъект, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписанныеДокументыЗначениеНажатие(Элемент)
	
	ДанныеФайла = РегистрацияОрганизацииВызовСервера.ДанныеФайлаДляОткрытия(
		ПодписанныеДокументы,
		УникальныйИдентификатор);
	
	РаботаСФайламиКлиент.СохранитьФайлКак(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаОшибкаПодписиДокументовРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегистрацияОрганизацииКлиент.ОткрытьЖурналРегистрацииПриОшибкиПодписания();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтправкиНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	РегистрацияОрганизацииКлиент.СтатусОтправкиОбработкаНавигационнойСсылки(
		НавигационнаяСсылкаФорматированнойСтроки,
		ЗаявлениеОВнесенииИзмененийЕГР,
		РегистрацияОрганизацииКлиентСервер.ТипФайлаЛистЗаписиЕГР(Объект.ЮридическоеФизическоеЛицо),
		УникальныйИдентификатор,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУчредители

&НаКлиенте
Процедура УчредителиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "УчредителиНаименование" Тогда
		СтандартнаяОбработка = Ложь;
		ТекущиеДанные = Элементы.Учредители.ТекущиеДанные;
		Если ТекущиеДанные.Ссылка = Заявитель.Ссылка И Модифицированность Тогда
			ЗаписатьЗаявителя();
			ОповеститьОбИзменении(Заявитель.Ссылка);
		КонецЕсли;
		РегистрацияОрганизацииКлиент.ОткрытьФормуУчредителя(Элемент, ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчредителиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) И Элементы.Учредители.ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.Учредители.ТекущиеДанные;
		Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Контрагенты") Тогда
			ТекущиеДанные.Ссылка = ВыбранноеЗначение;
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ВыбранноеЗначение, "ФизическоеЛицо") Тогда
			ТекущиеДанные.Ссылка = ВыбранноеЗначение.ФизическоеЛицо;
			ТекущиеДанные.Наименование = ВыбранноеЗначение.Наименование;
		КонецЕсли;
		
		ТекущиеДанные.ОтметкаНезаполненного = Не РеквизитыУчредителяЗаполнены(ТекущиеДанные.Ссылка);
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область СписокИзменений

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоПолюПоискаИНН(Команда)
	
	Если ПустаяСтрока(ПоискИННЗапрос) Тогда
		ТекстСообщения = НСтр("ru = 'Не указан ИНН организации'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , ПолеИсточникИНН(ЭтоНачалоРаботы));
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыОрганизацииПоИНН(ПоискИННЗапрос, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыВыбраннойОрганизации(Команда)
	
	Перем ИННОрганизации;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле", "Заполнение", Элементы.Организация.Заголовок);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , ПолеИсточникИНН(ЭтоНачалоРаботы));
		Возврат;
	КонецЕсли;
	
	ИННОрганизации = ИННОрганизации(Организация);
	Если Не ЗначениеЗаполнено(ИННОрганизации) Тогда
		ТекстСообщения = НСтр("ru = 'Не указан ИНН организации'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , ПолеИсточникИНН(ЭтоНачалоРаботы));
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыОрганизацииПоИНН(ИННОрганизации, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаявление(Команда)
	
	СоздатьЗаявлениеНаКлиенте(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область Устав

&НаКлиенте
Процедура ПерейтиНаШагИзменений(Команда)
	
	ИзменитьЭтап("ВнесениеИзмененийЕГР_СписокИзменений");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаШагЗаявление(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ИзменитьЭтап("ВнесениеИзмененийЕГР_Заявление");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Заявление

&НаКлиенте
Процедура ПерейтиНаПредыдущийШаг(Команда)
	
	Если ОтображатьШагУстав Тогда
		ИзменитьЭтап("ВнесениеИзмененийЕГР_Устав");
	Иначе
		ИзменитьЭтап("ВнесениеИзмененийЕГР_СписокИзменений");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗаявление(Команда)
	
	ЗаполнитьЗаявлениеНаКлиенте(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыслатьСМССКодом(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения_Телефон", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыслатьПисьмоНаПочту(Команда)
	
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения_Почта", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Документы

&НаКлиенте
Процедура ХочуКонсультацию(Команда)
	
	ОткрытьФорму("Обработка.РегистрацияОрганизации.Форма.ФормаХочуКонсультацию");
	
КонецПроцедуры

&НаКлиенте
Процедура АдресИРеквизитыВашейИнспекции(Команда)
	
	ОрганизацииФормыКлиент.ПерейтиКСервисуОпределитьРеквизитыИФНС();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьРаботу(Команда)
	
	ПоказатьИндикаторОжидания();
	
	ПредупреждениеОбОшибке = Новый Структура;
	Если Не ПодготовитьНачалоРаботы(ПредупреждениеОбОшибке) Тогда
		ВызватьИсключение ПредупреждениеОбОшибке;
	КонецЕсли;
	
	ОбщегоНазначенияБПКлиент.УстановитьОсновнуюОрганизацию(Объект.Ссылка, Ложь);
	
	ОбновитьИнтерфейс();
	
	Оповестить("ЗавершенаРаботаПомощникаНачалаРаботы", ИмяФормы, ЭтотОбъект);
	
	ОткрытьФорму("Обработка.НачалоРаботы.Форма.НачалоРаботыВСервисе");
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗаявление(Команда)
	
	Модифицированность = Ложь;
	ИзменитьЭтап("ВнесениеИзмененийЕГР_Заявление");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБлижайшийМФЦ(Команда)
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(ЭтотОбъект);
	Если ЭтоЮрЛицо Тогда
		Изменено = СтруктураИзменений(ЭтотОбъект);
		ЮрАдресИПропискаГлавногоСовпадают = Изменено.Местоположение
			И ЮрАдресИПропискаГлавногоСовпадают(ЗаявительАдресПропискиЗначениеJSON, ЮридическийАдресНовыйЗначениеJSON, Учредители);
		ИзмененияВносятсяВУчредительныеДокументы = ИзмененияВносятсяВУчредительныеДокументы(
			ЭтоЮрЛицо,
			Изменено,
			АдресВУставеПолный(АдресВУставе),
			КодыОКВЭДпротиворечатУставу(ВидыДеятельностиВУставе),
			ВидУстава);
		ИзмененияТребуютУведомления = ИзмененияТребуютУведомления(ЭтоЮрЛицо, Изменено, ЮрАдресИПропискаГлавногоСовпадают);
		
		АдресДляПоискаЗначениеJSON = ?(ИзмененияТребуютУведомления, ЮридическийАдресНовыйЗначениеJSON, ЮридическийАдресЗначениеJSON);
	Иначе
		АдресДляПоискаЗначениеJSON = ЗаявительАдресПропискиЗначениеJSON;
	КонецЕсли;
	
	ПредставлениеАдреса = РегистрацияОрганизацииВызовСервера.ПредставлениеАдресаДляПоискаМФЦ(АдресДляПоискаЗначениеJSON);
	ДополнениеАдреса = НСтр("ru = 'МФЦ Мои документы'");
	
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(
		ЭтотОбъект, Элементы.ПоказатьБлижайшийМФЦ, ПредставлениеАдреса, ДополнениеАдреса);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБлижайшийМФЦУведомление(Команда)
	
	ПоказатьНаКартеБлижайшийМФЦ(Элементы.ПоказатьБлижайшийМФЦУведомление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтсканированноеЗаявление(Команда)
	
	РегистрацияОрганизацииКлиентСервер.ОчиститьПрикрепленныйДокумент(
		ЭтотОбъект,
		РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов().Заявление, ,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтсканированныйПротокол(Команда)
	
	РегистрацияОрганизацииКлиентСервер.ОчиститьПрикрепленныйДокумент(
		ЭтотОбъект,
		РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов().Протокол, ,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтсканированныйУстав(Команда)
	
	РегистрацияОрганизацииКлиентСервер.ОчиститьПрикрепленныйДокумент(
		ЭтотОбъект,
		РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов().Устав, ,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтсканированныйРешение(Команда)
	
	РегистрацияОрганизацииКлиентСервер.ОчиститьПрикрепленныйДокумент(
		ЭтотОбъект,
		РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов().Решение, ,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПодписанныеДокументы(Команда)
	
	ОчиститьПодписанныеДокументыИУстановитьКодРегистрирующегоОргана();
	Оповестить("ОбновитьСтатусЗаявленияНаГосРегистрацию", Организация, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДокументПодтверждающийАдрес(Команда)
	
	РегистрацияОрганизацииКлиентСервер.ОчиститьПрикрепленныйДокумент(
		ЭтотОбъект,
		РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов().ДокументПодтверждающийАдрес, ,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтсканированныйИнойДокумент(Команда)
	
	НомерДокумента = СтрЗаменить(ТекущийЭлемент.Имя, "ОчиститьОтсканированныйИнойДокумент", "");
	РегистрацияОрганизацииКлиентСервер.ОчиститьПрикрепленныйДокумент(
		ЭтотОбъект,
		РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов().ИнойДокумент,
		НомерДокумента,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект));
	
	ОчиститьПрикрепленныйИнойДокументНаСервере(НомерДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИнойДокумент(Команда)
	
	ИмяНовогоЭлемента = ДобавитьИнойДокументНаСервере();
	
	Если ЗначениеЗаполнено(ИмяНовогоЭлемента) Тогда
		РегистрацияОрганизацииКлиент.ВыборОтсканированногоФайлаНажатие(
			ЭтотОбъект,
			ЗаявлениеОВнесенииИзмененийЕГР,
			ИмяНовогоЭлемента);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокументы(Команда)
	
	ПроверитьИПодписатьДокументы();
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		// Если нажали кнопку "Отмена", то ничего не делаем.
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ТекущийЭтап = "ВнесениеИзмененийЕГР_СписокИзменений" Тогда
			СоздатьЗаявлениеНаКлиенте(Истина);
		Иначе
			ЗаполнитьЗаявлениеНаКлиенте(Истина);
		КонецЕсли;
	Иначе
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИндикаторОжидания()
	
	// Устанавливаем видимость страницы
	УстановитьВидимостьСтраницы(ЭтотОбъект, "Ожидание");
	
	// Запускаем индикатор с задержкой, достаточной для загрузки в веб-кленте
	ЗапуститьИндикаторОжидания();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьИндикаторОжидания()
	
	ИндикаторОжидания = Элементы.ДлительнаяОперацияКартинка;
	
	Если ИндикаторОжидания.Видимость Тогда
		ИндикаторОжидания.Видимость = Ложь;
		ПодключитьОбработчикОжидания("ЗапуститьИндикаторОжидания", 0.5, Истина);
	Иначе
		ИндикаторОжидания.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СпособРегистрации(СпособПодачиДокументов)
	
	Возврат РегистрацияОрганизацииКлиентСервер.СпособРегистрацииПоСпособуПодачиДокументов(СпособПодачиДокументов);
	
КонецФункции

&НаКлиенте
Процедура ПоказатьНаКартеБлижайшийМФЦ(Элемент)
	
	Если ЭтоЮрЛицо(ЭтотОбъект) Тогда
		АдресДляПоискаЗначениеJSON = ЮридическийАдресЗначениеJSON;
	Иначе
		АдресДляПоискаЗначениеJSON = ЗаявительАдресПропискиЗначениеJSON;
	КонецЕсли;
	
	ПредставлениеАдреса = РегистрацияОрганизацииВызовСервера.ПредставлениеАдресаДляПоискаМФЦ(АдресДляПоискаЗначениеJSON);
	ДополнениеАдреса = НСтр("ru = 'МФЦ Мои документы'");
	
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, ПредставлениеАдреса, ДополнениеАдреса);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСтраныГражданства()
	
	ГражданствоРФ = Гражданство = КодРоссии;
	БезГражданства = Гражданство = РегистрацияОрганизацииКлиентСервер.КодПустогоЗначенияВСпискеВыбораГражданства();
	Если БезГражданства И Не ЗначениеЗаполнено(ВидДокумента) Тогда
		ВидДокумента = РегистрацияОрганизацииКлиентСервер.ЗначениеРазрешенияНаВременноеПроживаниеВСпискеВыбора();
	КонецЕсли;
	Если ГражданствоРФ Или БезГражданства Тогда
		ИностранныйПаспортСерияНомер = "";
	КонецЕсли;
	Если Не ГражданствоРФ Тогда
		ПаспортСерияНомер = "";
		ПаспортныеДанныеЗаявителя.Серия = "";
		ПаспортныеДанныеЗаявителя.КодПодразделения = "";
	КонецЕсли;
	Если Не БезГражданства Тогда
		СерияНомерОснованияДляПроживания = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяPDFФайлаЗаявления(ЭтоЮрЛицо)
	
	ИменаФайлов = РегистрацияОрганизацииКлиентСервер.ИменаПрикрепляемыхФайлов();
	Возврат ИменаФайлов[ИдентификаторPDFФайлаЗаявления(ЭтоЮрЛицо)];
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторPDFФайлаЗаявления(ЭтоЮрЛицо)
	
	Если ЭтоЮрЛицо Тогда
		Возврат "ЗаявлениеНаИзменениеООО";
	Иначе
		Возврат "ЗаявлениеНаИзменениеИП";
	КонецЕсли;
	
КонецФункции

#Область УправлениеФормой

&НаСервереБезКонтекста
Функция ЗаголовокФормы(ЮридическоеФизическоеЛицо = Неопределено)
	
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		Заголовок = НСтр("ru = 'Внесение изменений в ЕГРЮЛ'")
	ИначеЕсли ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		Заголовок = НСтр("ru = 'Внесение изменений в ЕГРИП'")
	Иначе
		Заголовок = НСтр("ru = 'Внесение изменений в ЕГРЮЛ (ЕГРИП)'")
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Если Форма.ТекущийЭтап = "ВнесениеИзмененийЕГР_СписокИзменений" Тогда
		
		УправлениеЗакладкойИзменения(Форма);
		
	ИначеЕсли Форма.ТекущийЭтап = "ВнесениеИзмененийЕГР_Устав" Тогда
		
		УправлениеЗакладкойУставКИзменению(Форма);
		
	ИначеЕсли Форма.ТекущийЭтап = "ВнесениеИзмененийЕГР_Заявление" Тогда
		
		УправлениеЗакладкойЗаявление(Форма);
		
	ИначеЕсли Форма.ТекущийЭтап = "ВнесениеИзмененийЕГР_Документы" Тогда
		
		УстановитьКнопкуПоУмолчанию(Форма);
		УправлениеЗакладкойДокументы(Форма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЗакладкойИзменения(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	ОбъектЗаполнен = ОбъектЗаполнен(Объект);
	
	Элементы.ПоискПоОрганизации.Видимость = Не Форма.ЭтоНачалоРаботы И Форма.ИспользоватьНесколькоОрганизаций;
	Элементы.ПоискПоИНН.Видимость = Форма.ЭтоНачалоРаботы;
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(Форма);
	
	Изменено = СтруктураИзменений(Форма);
	
	Элементы.ПредставлениеОрганизации.Видимость = ОбъектЗаполнен;
	Элементы.РеквизитыОрганизации.Видимость = ОбъектЗаполнен И ЭтоЮрЛицо;
	Элементы.ПодсказкаНеобходимостьИзмененийИП.Видимость = НЕ ЭтоЮрЛицо;
	
	ШрифтПоУмолчанию = Новый Шрифт;
	
	Если ЭтоЮрЛицо Тогда
		
		// Наименование организации
		
		Если Изменено.ПолноеНаименование И Не Изменено.СокращенноеНаименование Тогда
			Элементы.НаименованиеСокращенное.Видимость = Ложь;
			Элементы.НаименованиеПолное.Видимость = Истина;
			Элементы.НаименованиеПолное.Шрифт = ?(Изменено.ПолноеНаименование, Форма.ШрифтИзмененныхРеквизитов, ШрифтПоУмолчанию);
		Иначе
			Элементы.НаименованиеСокращенное.Видимость = Истина;
			Элементы.НаименованиеСокращенное.Шрифт = ?(Изменено.СокращенноеНаименование, Форма.ШрифтИзмененныхРеквизитов, ШрифтПоУмолчанию);
			Элементы.НаименованиеПолное.Видимость = Ложь;
		КонецЕсли;
		
		Форма.ИзменитьНаименование = ТекстГиперссылкиИзменитьНаименование(Форма);
		
		// Юридический адрес
		
		Элементы.ЮридическийАдрес.Шрифт =
			?(Изменено.ЮридическийАдрес, Форма.ШрифтИзмененныхРеквизитов, ШрифтПоУмолчанию);
		
		Форма.ИзменитьЮридическийАдрес = ТекстГиперссылкиИзменитьЮридическийАдрес(Форма);
		
		// Уточнять как указан адрес в уставе имеет смысл только, если другие изменения не вносятся в устав
		Элементы.АдресВУставе.Видимость = СпрашиватьКакУказанАдресВУставе(Форма);
		
		// Руководитель организации
		
		ЕстьСведенияОРуководителе = ЕстьСведенияОРуководителе(Форма);
		Элементы.Руководитель.Доступность = ЕстьСведенияОРуководителе;
		
		Элементы.РуководительНаименование.Заголовок =
			?(ЗначениеЗаполнено(Форма.РуководительДолжность),
			НСтр("ru = 'Руководитель'"),
			Строка(Форма.РуководительДолжность));
		
		Элементы.РуководительНаименование.Шрифт = ?(Изменено.Руководитель, Форма.ШрифтИзмененныхРеквизитов, ШрифтПоУмолчанию);
		
		Элементы.ИзменитьРуководителя.Видимость = ЕстьСведенияОРуководителе;
		Форма.ИзменитьРуководителя = ТекстГиперссылкиИзменитьРуководителя(Форма);
		
	КонецЕсли;
	
	// Виды деятельности
	Элементы.СписокВидовДеятельности.Видимость = ОбъектЗаполнен;
	
	Элементы.ВидыДеятельностиВУставе.Видимость = ЭтоЮрЛицо И СпрашиватьКакУказаныВидыДеятельностиВУставе(Форма);
	
	Элементы.ПодсказкаИзменитьВидУстава.Видимость = ОбъектЗаполнен И Форма.ОтображатьШагУстав;
	
	// Командная панель
	Элементы.СоздатьЗаявление.Видимость = ОбъектЗаполнен;
	
	Элементы.ПодсказкаСправкаОСудимости.Видимость = ТребуетсяСправкаОбОтсутствииСудимости(Форма);
	Если РегистрацияОрганизацииКлиентСервер.РешениеОНовомРуководителеЗаверяетсяУНотариуса(Форма.ДатаОтправкиОнлайн) Тогда
		Элементы.РуководительНаименование.Подсказка = НСтр("ru = 'Изменение руководителя нужно заверить через нотариуса'");
		Элементы.РуководительНаименование.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		Элементы.ИзменитьРуководителя.Видимость = Ложь;
	Иначе
		Элементы.РуководительНаименование.Подсказка = "";
		Элементы.РуководительНаименование.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		Элементы.ИзменитьРуководителя.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЗакладкойУставКИзменению(Форма)
	
	Элементы = Форма.Элементы;
	
	ПредставленияИзменений = ПредставлениеСтруктурыИзмененийВРодительномПадеже();
	СтруктураИзменений = СтруктураИзменений(Форма);
	
	СтрокиИзменения = Новый Массив;
	Для Каждого ТекущийПараметр Из СтруктураИзменений Цикл
		
		Если ТекущийПараметр.Ключ = "Устав" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущийПараметр.Значение Тогда
			
			ПредставлениеИзменения = ПредставленияИзменений[ТекущийПараметр.Ключ];
			Если СтрокиИзменения.Найти(ПредставлениеИзменения) = Неопределено Тогда
				СтрокиИзменения.Добавить(ПредставлениеИзменения);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ИзмененияВносятсяВУчредительныеДокументы = ИзмененияВносятсяВУчредительныеДокументы(
		ЭтоЮрЛицо(Форма),
		СтруктураИзменений,
		АдресВУставеПолный(Форма.АдресВУставе),
		КодыОКВЭДпротиворечатУставу(Форма.ВидыДеятельностиВУставе),
		Форма.ВидУстава);
	
	СтрокиПодсказки = Новый Массив;
	
	Если ИзмененияВносятсяВУчредительныеДокументы Тогда
		
		Разделитель = ?(СтрокиИзменения.Количество() = 2, НСтр("ru = ' и '"), ", ");
		
		СтрокиПодсказки.Добавить(НСтр("ru = 'Для изменения '"));
		СтрокиПодсказки.Добавить(СтрСоединить(СтрокиИзменения, Разделитель));
		СтрокиПодсказки.Добавить(НСтр("ru = ' собрание участников должно утвердить '"));
		СтрокиПодсказки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Новую редакцию устава'"), , , , "ИндивидуальныйУстав"));
		СтрокиПодсказки.Добавить(НСтр("ru = ', либо принять решение о переходе на Типовой устав.'"));
		
	ИначеЕсли СтрокиИзменения.Количество() > 0 Тогда
		
		СтрокиПодсказки.Добавить(НСтр("ru = 'Для изменения '"));
		СтрокиПодсказки.Добавить(СтрСоединить(СтрокиИзменения, ", "));
		СтрокиПодсказки.Добавить(НСтр("ru = ' изменять устав не нужно.'"));
		СтрокиПодсказки.Добавить(НСтр("ru = 'Однако учредители общества могут принять решение о переходе на Типовой устав.'"));
		
	КонецЕсли;
	
	Если СтрокиПодсказки.Количество() > 0 Тогда
		Элементы.НадписьИзменениеУстава.Заголовок = Новый ФорматированнаяСтрока(СтрокиПодсказки);
		Элементы.ТиповойУставПодсказки.Видимость = Истина;
	Иначе
		Элементы.ТиповойУставПодсказки.Видимость = Ложь;
	КонецЕсли;
	
	// Заполнение списка выбора
	Если Элементы.НомерТиповогоУстава.СписокВыбора.Количество() = 0 Тогда
		Элементы.НомерТиповогоУстава.СписокВыбора.ЗагрузитьЗначения(
			СписокНомеровТиповыхУставов(Форма.АдресТиповыеУставы));
	КонецЕсли;
	
	Элементы.ВидУстава.Доступность = Форма.ИзменитьВидУстава;
	
	ЭтоТиповойУстав = ВыбранТиповойУстав(Форма.ВидУстава);
	
	Если ЭтоТиповойУстав И Форма.ИзменитьВидУстава Тогда
		
		Элементы.ГруппаТиповойУстав.Видимость = Истина;
		Элементы.НадписьИндивидуальныйУстав.Видимость = Ложь;
		Элементы.ГруппаНадписьУстав.Видимость = ЗначениеЗаполнено(Форма.НомерТиповогоУстава);
		
		Форма.ПримечаниеТиповойУстав = ?(ЗначениеЗаполнено(Форма.НомерТиповогоУстава),
			НСтр("ru='распечатывать не нужно'"),
			НСтр("ru = 'отсутствует типовой устав с такими параметрами'"));
		Форма.НадписьНомерТиповогоУстава = РегистрацияОрганизацииКлиентСервер.ИмяТиповогоУстава(Форма.НомерТиповогоУстава);
		
	Иначе
		
		Элементы.ГруппаТиповойУстав.Видимость = Ложь;
		Элементы.НадписьИндивидуальныйУстав.Видимость = Форма.ИзменитьВидУстава И Форма.ВидУстава = "Индивидуальный";
		Элементы.ГруппаНадписьУстав.Видимость = Ложь;
		
		Форма.ПримечаниеТиповойУстав = "";
		Форма.НадписьНомерТиповогоУстава = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЗакладкойЗаявление(Форма)
	
	Элементы = Форма.Элементы;
	Заявитель = Форма.Заявитель;
	
	Изменено = СтруктураИзменений(Форма);
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(Форма);
	
	Элементы.ДатаРешения.Видимость = ЭтоЮрЛицо;
	
	Элементы.Заявитель.ОтображатьЗаголовок = ЭтоЮрЛицо;
	Если ЕстьСведенияОРуководителе(Форма) Тогда
		Элементы.Заявитель.Заголовок = НСтр("ru = 'Руководитель'");
	Иначе
		// Исполнительным органом является управляющая компания
		Элементы.Заявитель.Заголовок = НСтр("ru = 'Заявитель'");
	КонецЕсли;
	
	// Если изменен руководитель организации, заявителелем является новый руководитель,
	// данные по которому введены на первом этапе.
	Элементы.Заявитель.Видимость = Не (ЭтоЮрЛицо И Изменено.Руководитель);
	
	Элементы.ЗаявительИП.Видимость = Не ЭтоЮрЛицо;
	Элементы.ЗаявительООО.Видимость = ЭтоЮрЛицо;
	
	// Паспортные данные и ИНН
	Элементы.ПаспортныеДанныеЗаявителя.ОтображатьЗаголовок = ЭтоЮрЛицо;
	Элементы.ПаспортныеДанные.Видимость = ЭтоЮрЛицо;
	
	// ИНН индивидуального предпринимателя вводится на этапе "Изменения"
	Элементы.ИННФизическогоЛица.Видимость = ЭтоЮрЛицо;
	Если ЭтоЮрЛицо Тогда
		Форма.НадписьОшибкаИННЗаявителя = СообщениеОбОшибкеИНН(Заявитель.ИНН);
		Элементы.НадписьОшибкаИННЗаявителя.Видимость = Не ПустаяСтрока(Форма.НадписьОшибкаИННЗаявителя);
	КонецЕсли;
	
	// Контактная информация
	Элементы.КонтактнаяИнформация.ОтображатьЗаголовок = ЭтоЮрЛицо;
	
	Элементы.ГруппаУчредители.Видимость = ЭтоЮрЛицо И Не ЕдинственныйУчредительРуководитель(Форма);
	
	ГражданствоРФ = Форма.Гражданство = Форма.КодРоссии;
	БезГражданства = Форма.Гражданство = РегистрацияОрганизацииКлиентСервер.КодПустогоЗначенияВСпискеВыбораГражданства();
	Элементы.ГруппаПаспортныеДанныеРФ.Видимость = ГражданствоРФ;
	Элементы.ГруппаПаспортныеДанныеИностранныйГражданин.Видимость = Не ГражданствоРФ И Не БезГражданства;
	Элементы.ГруппаАдресРФ.Видимость = Не ЭтоЮрЛицо Или ГражданствоРФ;
	Элементы.ГруппаИностранныйАдрес.Видимость = ЭтоЮрЛицо И Не ГражданствоРФ;
	Элементы.ОснованиеДляПроживанияВРФ.Видимость = БезГражданства;
	Элементы.УзнатьИНН.Видимость = Форма.СервисУзнатьИННДоступен И ГражданствоРФ;
	
	Если ЭтоФорма2014(Форма.ВыбраннаяФорма) Тогда
		Элементы.ГруппаКонтактнаяИнформацияЗаявитель.Видимость = Истина;
		Элементы.ГруппаСпособПолученияДокументов.Видимость = Ложь;
	Иначе
		Элементы.ГруппаКонтактнаяИнформацияЗаявитель.Видимость = Ложь;
		Элементы.ГруппаСпособПолученияДокументов.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ГруппаСпособПодачиДокументоЭлектронно.Видимость = Форма.ПодачаДокументовВЭлектронномВидеДоступна;
	
	РегистрацияОрганизацииКлиентСервер.УправлениеСвойствамиГруппыПодтвержденияКонтактнойИнформации(
		Форма,
		ДополнительныеПараметрыКонтактнойИнформации(Форма));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЗакладкойДокументы(Форма)
	
	Элементы = Форма.Элементы;
	
	ПараметрыОтображения = ПараметрыОтображенияЭлементовФормы(Форма);
	ЭтоЮрЛицо = ПараметрыОтображения.ЭтоЮрЛицо;
	ИзмененияТребуютУведомления = ПараметрыОтображения.ИзмененияТребуютУведомления;
	ОтображатьИндивидуальныйУстав = ПараметрыОтображения.ОтображатьИндивидуальныйУстав;
	ИзмененияВносятсяВУчредительныеДокументы = ПараметрыОтображения.ИзмененияВносятсяВУчредительныеДокументы;
	Изменено = ПараметрыОтображения.Изменено;
	
	РегистрацияЧерезМФЦ = (Форма.СпособПодачиДокументов = РегистрацияОрганизацииКлиентСервер.СпособыПодачиДокументов().МФЦлично);
	
	// Пакет документов
	Элементы.ЗаявлениеОВнесенииИзмененийЕГРИП.Видимость = Не ЭтоЮрЛицо;
	
	ИмяОтчета = ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР(Форма.ДатаРешения, ЭтоЮрЛицо(Форма));
	Если Не ЭтоЗаявлениеР13014(ИмяОтчета) Тогда
		
		Элементы.ЗаявлениеОВнесенииИзмененийЕГРЮЛ.Видимость = ЭтоЮрЛицо
			И (Не ИзмененияВносятсяВУчредительныеДокументы Или ИзмененияТребуютУведомления Или Изменено.Руководитель);
		
		Элементы.ЗаявлениеОРегистрацииИзмененийВУставе.Видимость = (ЭтоЮрЛицо И ИзмененияВносятсяВУчредительныеДокументы);
		
	Иначе
		
		Элементы.ЗаявлениеОВнесенииИзмененийЕГРЮЛ.Видимость = ЭтоЮрЛицо;
		Элементы.ЗаявлениеОРегистрацииИзмененийВУставе.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.Устав.Видимость = ОтображатьИндивидуальныйУстав;
	
	Если ЭтоЮрЛицо Тогда
		
		Элементы.РешениеЕдинственногоУчредителя.Видимость = ПараметрыОтображения.ЕдинственныйУчредительФизическоеЛицо;
		
		Если ПараметрыОтображения.ЕдинственныйУчредительЮридическоеЛицо Тогда
			Элементы.ПротоколСобранияУчредителей.Видимость = Ложь;
			Элементы.ПротоколСобранияУчредителейЕдинственногоУчредителя.Видимость = Истина;
			Элементы.ПротоколСобранияУчредителейЕдинственногоУчредителяДекорация.Заголовок =
				СтрШаблон(НСтр("ru = 'Образец протокола собрания учредителей %1'"), ПараметрыОтображения.ОбразецПротокола);
		Иначе
			Элементы.ПротоколСобранияУчредителейЕдинственногоУчредителя.Видимость = Ложь;
			Элементы.ПротоколСобранияУчредителей.Видимость = Не ПараметрыОтображения.ЕдинственныйУчредительФизическоеЛицо;
		КонецЕсли;
		
	Иначе
		Элементы.ПротоколСобранияУчредителейЕдинственногоУчредителя.Видимость = Ложь;
		Элементы.РешениеЕдинственногоУчредителя.Видимость = Ложь;
		Элементы.ПротоколСобранияУчредителей.Видимость    = Ложь;
	КонецЕсли;
	
	Элементы.УслугаКонсультации.Видимость = ОказываютсяУслугиКонсультации();
	
	Если Форма.ОтображатьШагУстав Тогда
		
		ТекстНадписи = СтрШаблон(НСтр("ru='Вы выбрали %1'"),
			РегистрацияОрганизацииКлиентСервер.ИмяТиповогоУстава(Форма.НомерТиповогоУстава));
		
		Форма.НадписьОписаниеТиповойУстав = ТекстНадписи;
		Элементы.Устав.Видимость          = ОтображатьИндивидуальныйУстав;
		Элементы.ТиповойУстав.Видимость   = Не ОтображатьИндивидуальныйУстав И Форма.ИзменитьВидУстава;
		
	Иначе
		
		Форма.НадписьОписаниеТиповойУстав = "";
		Элементы.ТиповойУстав.Видимость = Ложь;
		
	КонецЕсли;
	
	// Уведомление
	Элементы.Уведомление.Видимость = ИзмененияТребуютУведомления;
	
	ОпределенПрежнийРегистрирующийОрган = Не ПустаяСтрока(Форма.РегистрирующийОрганПрежнийНаименование);
	Элементы.РегистрирующаяНалоговаяПрежняя.Видимость = ОпределенПрежнийРегистрирующийОрган И Не РегистрацияЧерезМФЦ;
	Элементы.РегистрирующийОрганПрежнийИнформация.Видимость = Не ПустаяСтрока(Форма.РегистрирующийОрганПрежнийИнформация);
	Элементы.АдресИРеквизитыВашейИнспекцииУведомление.Видимость = Не ОпределенПрежнийРегистрирующийОрган И Не РегистрацияЧерезМФЦ;
	Элементы.ПоказатьБлижайшийМФЦУведомление.Видимость = РегистрацияЧерезМФЦ;

	// Уплата госпошлины
	УплачиваетсяГоспошлинаЗаРегистрацию = РегистрацияОрганизацииКлиентСервер.УплачиваетсяГоспошлинаЗаРегистрацию(
		Форма.ДатаРешения,
		Форма.СпособПодачиДокументов,
		ИзмененияВносятсяВУчредительныеДокументы,
		Ложь); // При внесении изменений новая организация не создаётся
	
	НомерЭтапаУплатыГоспошлины = НомерЭтапаУплатыГоспошлины(ИзмененияТребуютУведомления, УплачиваетсяГоспошлинаЗаРегистрацию);
	Элементы.УплатаГоспошлиныЗаголовокКартинка2.Видимость = (НомерЭтапаУплатыГоспошлины = 2);
	Элементы.УплатаГоспошлиныЗаголовокКартинка3.Видимость = (НомерЭтапаУплатыГоспошлины = 3);
	Элементы.ГруппаИнструкцииПоОплатеГоспошлиныБезКвитанции.Видимость = ПустаяСтрока(Форма.ПлатежныеРеквизитыНаименованиеПолучателя);
	Элементы.ГруппаИнструкцииПоОплатеГоспошлиныСКвитанцией.Видимость = Не ПустаяСтрока(Форма.ПлатежныеРеквизитыНаименованиеПолучателя);
	Элементы.РеквизитыУплатыГоспошлины.Видимость = Не ПустаяСтрока(Форма.ПлатежныеРеквизитыНаименованиеПолучателя);
	
	Элементы.ИнструкцияУплатаГоспошлиныИФНС.Видимость = Не РегистрацияЧерезМФЦ;
	Элементы.ИнструкцияУплатаГоспошлиныМФЦ.Видимость = РегистрацияЧерезМФЦ;
	
	Элементы.ИнструкцияУплатаГоспошлиныСКвитанциейИФНС.Видимость = Не РегистрацияЧерезМФЦ;
	Элементы.ИнструкцияУплатаГоспошлиныСКвитанциейМФЦ.Видимость = РегистрацияЧерезМФЦ;
	
	// Регистрация изменений
	НомерЭтапаРегистрацииИзменений = НомерЭтапаРегистрацииИзменений(
		ИзмененияТребуютУведомления,
		ИзмененияВносятсяВУчредительныеДокументы,
		УплачиваетсяГоспошлинаЗаРегистрацию);
	
	Элементы.РегистрацияИзмененийЗаголовокКартинка2.Видимость = (НомерЭтапаРегистрацииИзменений = 2);
	Элементы.РегистрацияИзмененийЗаголовокКартинка3.Видимость = (НомерЭтапаРегистрацииИзменений = 3);
	Элементы.РегистрацияИзмененийЗаголовокКартинка4.Видимость = (НомерЭтапаРегистрацииИзменений = 4);
	
	Элементы.Инструкция.Видимость = Не ИзмененияТребуютУведомления;
	Элементы.СсылкаИнструкцияЕГРИП.Видимость = Не ЭтоЮрЛицо;
	Элементы.СсылкаИнструкцияЕГРЮЛ.Видимость = ЭтоЮрЛицо;
	
	Элементы.ПредупреждениеПриРегистрацииИзмененийМестаНахождения.Видимость = Изменено.Местоположение И ИзмененияТребуютУведомления;
	
	ОпределенРегистрирующийОрган = Не ПустаяСтрока(Форма.РегистрирующийОрганНаименование);
	Элементы.РегистрирующаяНалоговая.Видимость = ОпределенРегистрирующийОрган И Не РегистрацияЧерезМФЦ;
	Элементы.РегистрирующийОрганИнформация.Видимость = Не ПустаяСтрока(Форма.РегистрирующийОрганИнформация) И Не РегистрацияЧерезМФЦ;
	Элементы.АдресИРеквизитыВашейИнспекции.Видимость = Не ОпределенРегистрирующийОрган И Не РегистрацияЧерезМФЦ;
	
	Элементы.ПоказатьБлижайшийМФЦ.Видимость = РегистрацияЧерезМФЦ;
	
	Элементы.РегистрацияИзмененийЗаголовокДекорацияИФНС.Видимость = Не РегистрацияЧерезМФЦ;
	Элементы.РегистрацияИзмененийЗаголовокДекорацияМФЦ.Видимость = РегистрацияЧерезМФЦ;
	
	ВидыПрикрепленныхДокументов = РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов();
	Элементы.УставДекорация.Подсказка = РегистрацияОрганизацииКлиентСервер.ТекстПодсказкиКСформированнымДокументам(
		Форма.СпособПодачиДокументов,
		Форма.ФорматОтправкиДокументов,
		Форма.ДатаРешения,
		ВидыПрикрепленныхДокументов.Устав);
	
	Элементы.РешениеЕдинственногоУчредителяДекорация.Подсказка = РегистрацияОрганизацииКлиентСервер.ТекстПодсказкиКСформированнымДокументам(
		Форма.СпособПодачиДокументов,
		Форма.ФорматОтправкиДокументов,
		Форма.ДатаРешения,
		ВидыПрикрепленныхДокументов.Решение);
		
	Элементы.ПротоколСобранияУчредителейДекорация.Подсказка = РегистрацияОрганизацииКлиентСервер.ТекстПодсказкиКСформированнымДокументам(
		Форма.СпособПодачиДокументов,
		Форма.ФорматОтправкиДокументов,
		Форма.ДатаРешения,
		ВидыПрикрепленныхДокументов.Протокол);
	
	ПодсказкаКЗаявлениям = РегистрацияОрганизацииКлиентСервер.ТекстПодсказкиКСформированнымДокументам(
		Форма.СпособПодачиДокументов,
		Форма.ФорматОтправкиДокументов,
		Форма.ДатаРешения,
		ВидыПрикрепленныхДокументов.Заявление);
	
	Элементы.ЗаявлениеОВнесенииИзмененийЕГРИПДекорация.Подсказка = ПодсказкаКЗаявлениям;
	Элементы.ЗаявлениеОВнесенииИзмененийЕГРЮЛДекорация.Подсказка = ПодсказкаКЗаявлениям;
	Элементы.ЗаявлениеОРегистрацииИзмененийВУставеДекорация.Подсказка = ПодсказкаКЗаявлениям;
	
	Если РегистрацияОрганизацииКлиентСервер.ЭтоЭлектронныйСпособПодачиДокументов(Форма.СпособПодачиДокументов) Тогда
		
		Элементы.РегистрацияИзменений.Видимость = Ложь;
		Элементы.ИнструкцияСУведомлением.Видимость = Ложь;
		
		РегистрацияОрганизацииКлиентСервер.УправлениеФормойПриПодачеДокументовВЭлектронномВиде(Форма, ПараметрыОтображения);
		
	Иначе
		
		Элементы.ЗаголовокПакетДокументов.Заголовок = Нстр("ru = 'Документы готовы. Проверьте их и распечатайте.'");
		Элементы.ПодписьДокументовЭЦП.Видимость = Ложь;
		Элементы.РегистрацияИзмененийЭлектронно.Видимость = Ложь;
		Элементы.УплатаГоспошлины.Видимость = УплачиваетсяГоспошлинаЗаРегистрацию;
		Элементы.РегистрацияИзменений.Видимость = Истина;
		
		Элементы.ИнструкцияСУведомлением.Видимость = ИзмененияТребуютУведомления;
		Элементы.СсылкаИнструкцияСУведомлениемЕГРИП.Видимость = Не ЭтоЮрЛицо;
		Элементы.СсылкаИнструкцияСУведомлениемЕГРЮЛ.Видимость = ЭтоЮрЛицо;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтруктурыИзмененийВРодительномПадеже()
	
	Результат = Новый Структура;
	Результат.Вставить("СокращенноеНаименование", Нстр("ru = 'наименования ООО'"));
	Результат.Вставить("ПолноеНаименование", Нстр("ru = 'наименования ООО'"));
	Результат.Вставить("ЮридическийАдрес", Нстр("ru = 'юридического адреса'"));
	Результат.Вставить("Местоположение", Нстр("ru = 'местоположения'"));
	Результат.Вставить("Руководитель", Нстр("ru = 'руководителя'"));
	Результат.Вставить("Учредители", Нстр("ru = 'состава учредителей'"));
	Результат.Вставить("УставныйКапитал", Нстр("ru = 'уставного капитала'"));
	Результат.Вставить("ОсновнойВидДеятельности", Нстр("ru = 'перечня кодов ОКВЭД'"));
	Результат.Вставить("ВидыДеятельности", Нстр("ru = 'перечня кодов ОКВЭД'"));
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОказываютсяУслугиКонсультации()
	
	Возврат Обработки.РегистрацияОрганизации.ОказываютсяУслугиКонсультации();
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Адрес по прописке
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗаявительАдресПропискиПредставление");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(
		ЭлементУО.Поля, "ЗаявительАдресПропискиПредставлениеИностранныйГражданин");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЗаявительАдресПропискиПредставление",
		ВидСравненияКомпоновкиДанных.Равно,
		УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки());
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
	// Адрес для информирования
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ЗаявительАдресДляИнформированияПредставление");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(
		ЭлементУО.Поля, "ЗаявительАдресДляИнформированияПредставление");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЗаявительАдресДляИнформированияПредставление",
		ВидСравненияКомпоновкиДанных.Равно,
		УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки());
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
	// Представитель учредителя - юридического лица, должность и основание полномочий.
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УчредителиПредставительУчредителя");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УчредителиНадписьПредставитель");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УчредителиНадписьДолжность");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УчредителиДолжностьПредставителя");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УчредителиНадписьНаОсновании");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УчредителиПредставительНаОсновании");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Учредители.ТипУчредителя",
		ВидСравненияКомпоновкиДанных.НеРавно,
		ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Заполненность учредителя
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УчредителиНаименование");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Учредители.ОтметкаНезаполненного",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "РегистрирующийОрганКод");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"РегистрирующийОрганКод",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "РегистрирующийОрганКод");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"РегистрирующийОрганКод",
		ВидСравненияКомпоновкиДанных.Заполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблицыУчредители()
	
	Для Каждого Учредитель Из Учредители Цикл
		ЗаполнитьДобавленныеКолонкиВСтрокеТаблицыУчредители(Учредитель, Заявитель.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиВСтрокеТаблицыУчредители(СтрокаТаблицы, Знач Заявитель)
	
	СтрокаТаблицы.НадписьПредставитель = НСтр("ru = 'Представитель:'");
	СтрокаТаблицы.НадписьДолжность     = НСтр("ru = 'Должность:'");
	СтрокаТаблицы.НадписьНаОсновании   = НСтр("ru = 'На основании:'");
	
	Если СтрокаТаблицы.Ссылка = Заявитель Тогда
		СтрокаТаблицы.ОтметкаНезаполненного = Ложь;
	Иначе
		СтрокаТаблицы.ОтметкаНезаполненного = Не РеквизитыУчредителяЗаполнены(СтрокаТаблицы.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыУчредителяЗаполнены(Знач Учредитель)
	Возврат Обработки.РегистрацияОрганизации.РеквизитыУчредителяЗаполнены(Учредитель)
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НомерЭтапаРегистрацииИзменений(ИзмененияТребуютУведомления, ИзмененияВносятсяВУчредительныеДокументы, УплачиваетсяГоспошлинаЗаРегистрацию)
	
	Если ИзмененияТребуютУведомления И ИзмененияВносятсяВУчредительныеДокументы И УплачиваетсяГоспошлинаЗаРегистрацию Тогда
		Возврат 4;
	ИначеЕсли ИзмененияТребуютУведомления И ИзмененияВносятсяВУчредительныеДокументы И Не УплачиваетсяГоспошлинаЗаРегистрацию Тогда
		Возврат 3;
	ИначеЕсли (ИзмененияТребуютУведомления Или ИзмененияВносятсяВУчредительныеДокументы) И УплачиваетсяГоспошлинаЗаРегистрацию Тогда
		Возврат 3;
	Иначе
		Возврат 2;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НомерЭтапаУплатыГоспошлины(ИзмененияТребуютУведомления, УплачиваетсяГоспошлинаЗаРегистрацию)
	
	Если Не УплачиваетсяГоспошлинаЗаРегистрацию Тогда
		Возврат 0;
	ИначеЕсли ИзмененияТребуютУведомления Тогда
		Возврат 3;
	Иначе
		Возврат 2;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстГиперссылкиИзменитьНаименование(Форма)
	
	ИзмененоСокращенноеНаименование = ИзмененоСокращенноеНаименование(Форма);
	ИзмененоПолноеНаименование      = ИзмененоПолноеНаименование(Форма);
	
	Если ИзмененоПолноеНаименование И Не ИзмененоСокращенноеНаименование Тогда
		Возврат Форма.НаименованиеПолноеНовое;
	ИначеЕсли ИзмененоСокращенноеНаименование Тогда
		Возврат Форма.НаименованиеСокращенноеНовое;
	Иначе
		Возврат НСтр("ru = 'Изменить'");
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстГиперссылкиИзменитьРуководителя(Форма)
	
	Если ИзмененРуководитель(Форма) Тогда
		Возврат Строка(Форма.ОтветственныеЛицаНовые.ФизическоеЛицо);
	Иначе
		Возврат НСтр("ru = 'Изменить'");
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстГиперссылкиИзменитьЮридическийАдрес(Форма)
	
	Если ИзмененЮридическийАдрес(Форма) Тогда
		Возврат Строка(Форма.ЮридическийАдресНовый);
	Иначе
		Возврат НСтр("ru = 'Изменить'");
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОтобразитьУчредителей()
	
	// Признаки изменений
	ВыходУчастника      = 1;
	СтараяДоляУчастия   = 2;
	НоваяДоляУчастия    = 3;
	ВходНовогоУчастника = 4;
	
	ТаблицаУчредителей = Учредители.Выгрузить();
	ТаблицаУчредителей.Колонки.Добавить("Изменение", ОбщегоНазначения.ОписаниеТипаЧисло(1, 0));
	
	НовыхУчастников = 0;
	ВышлоУчастников = 0;
	Если УчредителиНовые.Количество() > 0 Тогда
		
		ТаблицаУчредителей.Колонки.Добавить("ПорядокВРеестре", ОбщегоНазначения.ОписаниеТипаЧисло(1, 0));
		ТаблицаУчредителей.Индексы.Добавить("ИНН");
		
		ТаблицаНовыхУчредителей = УчредителиНовые.Выгрузить();
		ТаблицаНовыхУчредителей.Индексы.Добавить("ИНН");
		
		ПорядокВРеестре = 0;
		
		// Определяем вид изменения для существующих участников общества
		
		Для Каждого Учредитель Из ТаблицаУчредителей Цикл
			
			НайденаяСтрока = ТаблицаНовыхУчредителей.Найти(Учредитель.ИНН, "ИНН");
			Если НайденаяСтрока = Неопределено Тогда
				Учредитель.Изменение = ВыходУчастника;
				ВышлоУчастников = ВышлоУчастников + 1;
			ИначеЕсли НайденаяСтрока.СуммаВзноса <> Учредитель.СуммаВзноса Или НайденаяСтрока.ДоляУчастия <> Учредитель.ДоляУчастия Тогда
				Учредитель.Изменение = СтараяДоляУчастия;
			КонецЕсли;
			
			Учредитель.ПорядокВРеестре = ПорядокВРеестре;
			ПорядокВРеестре = ПорядокВРеестре + 1;
			
		КонецЦикла;
		
		// Добавляем в таблицу новых участников общества
		
		Для Каждого НовыйУчредитель Из ТаблицаНовыхУчредителей Цикл
			
			НайденаяСтрока = ТаблицаУчредителей.Найти(НовыйУчредитель.ИНН, "ИНН");
			
			Если НайденаяСтрока = Неопределено Тогда
				
				Учредитель = ТаблицаУчредителей.Добавить();
				ЗаполнитьЗначенияСвойств(Учредитель, НовыйУчредитель);
				Учредитель.Изменение = ВходНовогоУчастника;
				НовыхУчастников = НовыхУчастников + 1;
				
				Учредитель.ПорядокВРеестре = ПорядокВРеестре;
				ПорядокВРеестре = ПорядокВРеестре + 1;
				
			ИначеЕсли НайденаяСтрока.СуммаВзноса <> НовыйУчредитель.СуммаВзноса Или НайденаяСтрока.ДоляУчастия <> НовыйУчредитель.ДоляУчастия Тогда
				
				Учредитель = ТаблицаУчредителей.Добавить();
				ЗаполнитьЗначенияСвойств(Учредитель, НовыйУчредитель);
				Учредитель.Изменение = НоваяДоляУчастия;
				
				Учредитель.ПорядокВРеестре = ПорядокВРеестре;
				ПорядокВРеестре = ПорядокВРеестре + 1;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ТаблицаУчредителей.Сортировать("Изменение, ПорядокВРеестре");
		ТаблицаУчредителей.Колонки.Удалить("ПорядокВРеестре");
		
	КонецЕсли;
	
	ЕстьСведенияОбУчредителях = (ТаблицаУчредителей.Количество() > 0);
	Если Не ЕстьСведенияОбУчредителях Тогда
		Учредитель = ТаблицаУчредителей.Добавить();
		Учредитель.Наименование = НСтр("ru = 'В реестре отсутствуют данные об учредителях'");
	КонецЕсли;
	
	// Выводим сформированную таблицу на форму
	
	ШрифтПоУмолчанию = Новый Шрифт;
	
	КоличествоПредопределенных = КоличествоПредопределенныхЭлементов("Учредитель");
	КоличествоУчредителей = ТаблицаУчредителей.Количество();
	
	// Добавляем недостающие элементы формы
	Для Индекс = КоличествоПредопределенных По КоличествоУчредителей - 1 Цикл
		ДобавитьПредставлениеУчредителя(Индекс);
	КонецЦикла;
	
	Для Индекс = 0 По КоличествоУчредителей - 1 Цикл
		
		Учредитель = ТаблицаУчредителей[Индекс];
		
		ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(Индекс, "Учредитель")];
		ЭлементФормы.Видимость = Истина;
		ЭлементФормы.Доступность = ЕстьСведенияОбУчредителях;
		
		Шрифт = ?(Учредитель.Изменение = ВыходУчастника Или Учредитель.Изменение = СтараяДоляУчастия, ШрифтИзмененныхРеквизитов, ШрифтПоУмолчанию);
		
		ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(Индекс, "НаименованиеУчредителя")];
		ЭлементФормы.Заголовок = Учредитель.Наименование;
		ЭлементФормы.Шрифт = Шрифт;
		
		// Для основной формы долю участия отображаем до 2 знаков
		// в рег. форме и расшифровке доля отображается полностью до 15 знаков после запятой
		ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(Индекс, "ДоляУчастияУчредителя")];
		ЭлементФормы.Заголовок = ?(Учредитель.ДоляУчастия <> 0, Формат(Учредитель.ДоляУчастия, "ЧДЦ=2; ЧН=") + "%", "");
		ЭлементФормы.Шрифт = Шрифт;
		
		ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(Индекс, "СуммаВзносаУчредителя")];
		ЭлементФормы.Заголовок = ?(Учредитель.СуммаВзноса <> 0, СтрШаблон(НСтр("ru = '%1 руб.'"), Учредитель.СуммаВзноса), "");
		ЭлементФормы.Шрифт = Шрифт;
		
	КонецЦикла;
	
	Для Индекс = КоличествоУчредителей По КоличествоПредопределенных - 1 Цикл
		ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(Индекс, "Учредитель")];
		ЭлементФормы.Видимость = Ложь;
	КонецЦикла;
	
	ИзменитьУчредителей = ТекстГиперссылкиИзменитьУчредителей(НовыхУчастников, ВышлоУчастников);
	
	Элементы.ИзменитьУчредителей.Видимость = ЕстьСведенияОбУчредителях;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПредставлениеУчредителя(Индекс)
	
	ГруппаУчредитель = Элементы.Добавить(
		ИмяЭлементаПоИндексу(Индекс, "Учредитель"),
		Тип("ГруппаФормы"),
		Элементы.УчредителиПредставление);
	ГруппаУчредитель.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаУчредитель.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаУчредитель.Заголовок = СтрШаблон("Учредитель[%1]", Индекс);
	ГруппаУчредитель.ОтображатьЗаголовок = Ложь;
	ГруппаУчредитель.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаУчредитель.Объединенная = Ложь;
	
	ЗаголовокУчредителя = Элементы.Добавить(
		ИмяЭлементаПоИндексу(Индекс, "ЗаголовокУчредителя"),
		Тип("ДекорацияФормы"),
		ГруппаУчредитель);
	ЗаголовокУчредителя.Заголовок = НСтр("ru = 'Учредители'");
	ЗаголовокУчредителя.Ширина = Элементы[ИмяЭлементаПоИндексу(0, "ЗаголовокУчредителя")].Ширина;
	ЗаголовокУчредителя.РастягиватьПоГоризонтали = Ложь;
	ЗаголовокУчредителя.ЦветТекста = ЦветаСтиля.ЦветФонаФормы;
	
	Элементы.Добавить(
		ИмяЭлементаПоИндексу(Индекс, "НаименованиеУчредителя"),
		Тип("ДекорацияФормы"),
		ГруппаУчредитель);
	
	ДоляУчастияУчредителя = Элементы.Добавить(
		ИмяЭлементаПоИндексу(Индекс, "ДоляУчастияУчредителя"),
		Тип("ДекорацияФормы"),
		ГруппаУчредитель);
	ДоляУчастияУчредителя.ГоризонтальноеПоложение = Элементы[ИмяЭлементаПоИндексу(0, "ДоляУчастияУчредителя")].ГоризонтальноеПоложение;
	
	СуммаВзносаУчредителя = Элементы.Добавить(
		ИмяЭлементаПоИндексу(Индекс, "СуммаВзносаУчредителя"),
		Тип("ДекорацияФормы"),
		ГруппаУчредитель);
	СуммаВзносаУчредителя.ГоризонтальноеПоложение = Элементы[ИмяЭлементаПоИндексу(0, "СуммаВзносаУчредителя")].ГоризонтальноеПоложение;
	
КонецПроцедуры

&НаСервере
Функция ТекстГиперссылкиИзменитьУчредителей(НовыхУчастников, ВышлоУчастников)
	
	МассивИзменений = Новый Массив;
	
	Если НовыхУчастников = 0 И ВышлоУчастников = 0 Тогда
		Если ИзмененУставныйКапитал(ЭтотОбъект) Тогда
			МассивИзменений.Добавить(НСтр("ru = 'Изменен размер уставного капитала общества'"));
		КонецЕсли;
	ИначеЕсли НовыхУчастников = 1 И ВышлоУчастников = 1 И Не ИзмененУставныйКапитал(ЭтотОбъект) Тогда
		МассивИзменений.Добавить(НСтр("ru = 'Отражена продажа доли участника общества'"));
	ИначеЕсли НовыхУчастников = ВышлоУчастников И Не ИзмененУставныйКапитал(ЭтотОбъект) Тогда
		МассивИзменений.Добавить(НСтр("ru = 'Отражена продажа долей участников общества'"));
	Иначе
		Если НовыхУчастников <> 0 Тогда
			НовыхУчастниковЦифрамиИПрописью = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 новый участник общества;;%1 новых участника общества;%1 новых участников общества;'"),
				НовыхУчастников,
				,
				"ЧДЦ=0");
			МассивИзменений.Добавить(НовыхУчастниковЦифрамиИПрописью);
		КонецЕсли;
		Если ВышлоУчастников <> 0 Тогда
			ВышлоУчастниковЦифрамиИПрописью = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';%1 участник вышел из общества;;%1 участника вышло из общества;%1 участников вышло из общества;'"),
				ВышлоУчастников,
				,
				"ЧДЦ=0");
			МассивИзменений.Добавить(ВышлоУчастниковЦифрамиИПрописью);
		КонецЕсли;
	КонецЕсли;
	
	Если МассивИзменений.Количество() > 0 Тогда
		ТекстГиперссылки = СтрСоединить(МассивИзменений, ", ");
	Иначе
		ТекстГиперссылки = НСтр("ru = 'Изменить'");
	КонецЕсли;
	
	Возврат ТекстГиперссылки;
	
КонецФункции

&НаСервере
Процедура ОтобразитьВидыДеятельности()
	
	// Признаки изменений
	ИсключениеКода = 1;
	ДобавлениеКода = 2;
	
	ТаблицаВидовДеятельности = ВидыДеятельности.Выгрузить();
	ТаблицаВидовДеятельности.Колонки.Добавить("Изменение", ОбщегоНазначения.ОписаниеТипаЧисло(1, 0));
	ТаблицаВидовДеятельности.Колонки.Добавить("ПорядокВРеестре", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	
	НовыхКодов   = 0;
	УдаленоКодов = 0;
	
	Если ИзмененыВидыДеятельности(ЭтотОбъект) Тогда
		
		ТаблицаВидовДеятельности.Индексы.Добавить("Код");
		
		ТаблицаНовыхВидовДеятельности = ВидыДеятельностиНовые.Выгрузить();
		ТаблицаНовыхВидовДеятельности.Индексы.Добавить("Код");
		
		ПорядокВРеестре = 0;
		
		// Определяем вид изменения
		
		Для Каждого ВидДеятельности Из ТаблицаВидовДеятельности Цикл
			
			ВидДеятельностиУдален = (ТаблицаНовыхВидовДеятельности.Найти(ВидДеятельности.Код, "Код") = Неопределено);
			Если ВидДеятельностиУдален Тогда
				ВидДеятельности.Изменение = ИсключениеКода;
				УдаленоКодов = УдаленоКодов + 1;
			КонецЕсли;
			
			ВидДеятельности.ПорядокВРеестре = ПорядокВРеестре;
			ПорядокВРеестре = ПорядокВРеестре + 1;
			
		КонецЦикла;
		
		Для Каждого НовыйВидДеятельности Из ТаблицаНовыхВидовДеятельности Цикл
			
			ВидДеятельностиДобавлен = (ТаблицаВидовДеятельности.Найти(НовыйВидДеятельности.Код, "Код") = Неопределено);
			Если ВидДеятельностиДобавлен Тогда
				
				ВидДеятельности = ТаблицаВидовДеятельности.Добавить();
				ЗаполнитьЗначенияСвойств(ВидДеятельности, НовыйВидДеятельности);
				ВидДеятельности.Изменение = ДобавлениеКода;
				НовыхКодов = НовыхКодов + 1;
				
				ВидДеятельности.ПорядокВРеестре = ПорядокВРеестре;
				ПорядокВРеестре = ПорядокВРеестре + 1;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НовыхКодов = 0 И УдаленоКодов = 0 Тогда
		ВидыДеятельностиНовые.Очистить();
	Иначе
		ТаблицаВидовДеятельности.Сортировать("Изменение, ПорядокВРеестре");
		ТаблицаВидовДеятельности.Колонки.Удалить("ПорядокВРеестре");
	КонецЕсли;
	
	// Основной код ОКВЭД
	ЗначениеОсновногоКода = ?(ИзмененОсновнойВидДеятельности(ЭтотОбъект), КодОКВЭД2Новый, КодОКВЭД2);
	НайденаяСтрока = ТаблицаВидовДеятельности.Найти(ЗначениеОсновногоКода, "Код");
	Если НайденаяСтрока <> Неопределено Тогда
		ТаблицаВидовДеятельности.Удалить(НайденаяСтрока);
	КонецЕсли;
	
	ЕстьОсновнойКодОКВЭД = Не ПустаяСтрока(ЗначениеОсновногоКода);
	ЕстьПрочиеКодыОКВЭД = (ТаблицаВидовДеятельности.Количество() > 0);
	
	Если ЕстьОсновнойКодОКВЭД Тогда
		ПредставлениеКодаОКВЭД2 = СтрШаблон(НСтр("ru = '%1 (основной)'"), ЗначениеОсновногоКода);
		СообщениеОбОшибке = "";
		Если ОрганизацииФормыКлиентСервер.ОКВЭДСоответствуетТребованиям(СообщениеОбОшибке, КодОКВЭД2) Тогда
			ОсновнойКодОКВЭД = Новый ФорматированнаяСтрока(ПредставлениеКодаОКВЭД2);
		Иначе
			ОсновнойКодОКВЭД = Новый ФорматированнаяСтрока(
				ПредставлениеКодаОКВЭД2,
				" ",
				Новый ФорматированнаяСтрока(СообщениеОбОшибке, , ЦветаСтиля.ЦветТекстаНекорректногоКонтрагента));
		КонецЕсли;
	ИначеЕсли ЕстьПрочиеКодыОКВЭД Тогда
		СообщениеОбОшибке = НСтр("ru = 'В реестре не указан основной код ОКВЭД'");
		ОсновнойКодОКВЭД = Новый ФорматированнаяСтрока(СообщениеОбОшибке, , ЦветаСтиля.ЦветТекстаНекорректногоКонтрагента);
	Иначе
		СообщениеОбОшибке = НСтр("ru = 'В реестре не указаны коды ОКВЭД'");
		ОсновнойКодОКВЭД = Новый ФорматированнаяСтрока(СообщениеОбОшибке);
	КонецЕсли;
	
	Элементы.ОсновнойКодОКВЭД.Доступность = ЕстьОсновнойКодОКВЭД Или ЕстьПрочиеКодыОКВЭД;
	
	// Выводим сформированную таблицу на форму
	
	КоличествоПредопределенныхСтрок = КоличествоПредопределенныхЭлементов("ВидДеятельности");
	
	ЭлементыСтрокиКодовОКВЭД = Новый Массив;
	МаксКоличествоКодовОКВЭДвСтроке = МаксКоличествоКодовОКВЭДвСтроке(ТаблицаВидовДеятельности.Количество());
	
	Если ТаблицаВидовДеятельности.Количество() > 0 Тогда
		ИзменениеТекущйСтроки = ТаблицаВидовДеятельности[0].Изменение;
		// Спецкостыль для выравнивания декораций
		ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(0, "ЗаголовокВидаДеятельности")];
		ЭлементФормы.Заголовок = ?(ЭтоЮрЛицо(ЭтотОбъект), НСтр("ru = 'Юридический адрес'"), НСтр("ru = 'Коды ОКВЭД'"));
	КонецЕсли;
	
	ИндексСтроки = 0;
	Для Каждого ВидДеятельности Из ТаблицаВидовДеятельности Цикл
		
		Если ЭлементыСтрокиКодовОКВЭД.Количество() = МаксКоличествоКодовОКВЭДвСтроке
			Или ВидДеятельности.Изменение <> ИзменениеТекущйСтроки Тогда
			
			Если ИндексСтроки = КоличествоПредопределенныхСтрок Тогда
				ДобавитьПредставлениеВидаДеятельности(ИндексСтроки);
				КоличествоПредопределенныхСтрок = КоличествоПредопределенныхСтрок + 1;
			КонецЕсли;
			
			ОтобразитьСтрокуКодовОКВЭД(ИндексСтроки, ЭлементыСтрокиКодовОКВЭД, ИзменениеТекущйСтроки);
			ЭлементыСтрокиКодовОКВЭД.Очистить();
			
			ИндексСтроки = ИндексСтроки + 1;
			
		КонецЕсли;
		
		ЭлементыСтрокиКодовОКВЭД.Добавить(ВидДеятельности.Код);
		
		ИзменениеТекущйСтроки = ВидДеятельности.Изменение;
		
	КонецЦикла;
	
	Если ЭлементыСтрокиКодовОКВЭД.Количество() > 0 Тогда
		
		Если ИндексСтроки = КоличествоПредопределенныхСтрок Тогда
			ДобавитьПредставлениеВидаДеятельности(ИндексСтроки);
			КоличествоПредопределенныхСтрок = КоличествоПредопределенныхСтрок + 1;
		КонецЕсли;
			
		ОтобразитьСтрокуКодовОКВЭД(ИндексСтроки, ЭлементыСтрокиКодовОКВЭД, ИзменениеТекущйСтроки);
		ЭлементыСтрокиКодовОКВЭД.Очистить();
		
		ИндексСтроки = ИндексСтроки + 1;
		
	КонецЕсли;
	
	КоличествоСтрокВидовДеятельности = ИндексСтроки;
	
	Для Индекс = КоличествоСтрокВидовДеятельности По КоличествоПредопределенныхСтрок - 1 Цикл
		ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(Индекс, "ВидДеятельности")];
		ЭлементФормы.Видимость = Ложь;
	КонецЦикла;
	
	ИзменитьВидыДеятельности = ТекстГиперссылкиВидыДеятельности(НовыхКодов, УдаленоКодов);
	
	Элементы.ИзменитьВидыДеятельности.Видимость = ЕстьОсновнойКодОКВЭД Или ЕстьПрочиеКодыОКВЭД;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСтрокуКодовОКВЭД(Индекс, ЭлементыСтрокиКодовОКВЭД, Изменение)
	
	ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(Индекс, "ВидДеятельности")];
	ЭлементФормы.Видимость = Истина;
	
	Шрифт = ?(Изменение = 1, ШрифтИзмененныхРеквизитов, Новый Шрифт);
	
	ЭлементФормы = Элементы[ИмяЭлементаПоИндексу(Индекс, "СтрокаКодовОКВЭД")];
	ЭлементФормы.Заголовок = СтрСоединить(ЭлементыСтрокиКодовОКВЭД, "; ");
	ЭлементФормы.Шрифт = Шрифт;
	
КонецПроцедуры

// Добавляет новую строку кодов ОКВЭД на форму
//
&НаСервере
Процедура ДобавитьПредставлениеВидаДеятельности(Индекс)
	
	ГруппаВидДеятельности = Элементы.Добавить(
		ИмяЭлементаПоИндексу(Индекс, "ВидДеятельности"),
		Тип("ГруппаФормы"),
		Элементы.ВидыДеятельности);
	ГруппаВидДеятельности.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаВидДеятельности.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаВидДеятельности.Заголовок = СтрШаблон("ВидДеятельности[%1]", Индекс);
	ГруппаВидДеятельности.ОтображатьЗаголовок = Ложь;
	ГруппаВидДеятельности.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаВидДеятельности.Объединенная = Ложь;
	
	ЗаголовокВидаДеятельности = Элементы.Добавить(
		ИмяЭлементаПоИндексу(Индекс, "ЗаголовокВидаДеятельности"),
		Тип("ДекорацияФормы"),
		ГруппаВидДеятельности);
	ЗаголовокВидаДеятельности.Заголовок = НСтр("ru = 'КодыОКВЭД'");
	ЗаголовокВидаДеятельности.Ширина = Элементы[ИмяЭлементаПоИндексу(0, "ЗаголовокВидаДеятельности")].Ширина;
	ЗаголовокВидаДеятельности.РастягиватьПоГоризонтали = Ложь;
	ЗаголовокВидаДеятельности.ЦветТекста = ЦветаСтиля.ЦветФонаФормы;
	
	СтрокаКодовОКВЭД = Элементы.Добавить(
		ИмяЭлементаПоИндексу(Индекс, "СтрокаКодовОКВЭД"),
		Тип("ДекорацияФормы"),
		ГруппаВидДеятельности);
	СтрокаКодовОКВЭД.АвтоМаксимальнаяШирина = Ложь;
	
КонецПроцедуры

// Возвращает число кодов ОКВЭД в строке формы
//
&НаСервереБезКонтекста
Функция МаксКоличествоКодовОКВЭДвСтроке(КоличествоВидовДеятельности)
	
	ПредопределенноеЗначение = 10;
	Если КоличествоВидовДеятельности < ПредопределенноеЗначение Тогда
		Возврат ПредопределенноеЗначение; // Не имеет значения
	КонецЕсли;
	
	КоличествоВПоследнейСтроке = КоличествоВидовДеятельности
		- (ПредопределенноеЗначение * Цел(КоличествоВидовДеятельности / ПредопределенноеЗначение));
	Если КоличествоВПоследнейСтроке = 1 Тогда
		Возврат ПредопределенноеЗначение + 1; // Избегаем переноса ради одного кода
	Иначе
		Возврат ПредопределенноеЗначение;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ТекстГиперссылкиВидыДеятельности(НовыхКодов, УдаленоКодов)
	
	МассивИзменений = Новый Массив;
	
	Если ИзмененОсновнойВидДеятельности(ЭтотОбъект) Тогда
		МассивИзменений.Добавить(НСтр("ru = 'Основной код изменен'"));
	КонецЕсли;
	
	Если УдаленоКодов <> 0 Тогда
		УдаленоЦифрамиИПрописью = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 код удален;;%1 кода удалено;%1 кодов удалено;'"),
			УдаленоКодов,
			,
			"ЧДЦ=0");
		МассивИзменений.Добавить(УдаленоЦифрамиИПрописью);
	КонецЕсли;
	
	Если НовыхКодов <> 0 Тогда
		ДобавленоЦифрамиИПрописью = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 код добавлен;;%1 кода добавлено;%1 кодов добавлено;'"),
			НовыхКодов,
			,
			"ЧДЦ=0");
		МассивИзменений.Добавить(ДобавленоЦифрамиИПрописью);
	КонецЕсли;
	
	Если МассивИзменений.Количество() > 0 Тогда
		ТекстГиперссылки = СтрСоединить(МассивИзменений, ", ");
	Иначе
		ТекстГиперссылки = НСтр("ru = 'Изменить'");
	КонецЕсли;
	
	Возврат ТекстГиперссылки;
	
КонецФункции

// Возвращает число элементов на форме с именем ПрефиксИмени1, ПрефиксИмени2 и т.д.
//
&НаСервере
Функция КоличествоПредопределенныхЭлементов(ПрефиксИмени)
	
	Перем Количество;
	
	Количество = 0;
	Пока Истина Цикл
		Если Элементы.Найти(ИмяЭлементаПоИндексу(Количество, ПрефиксИмени)) <> Неопределено Тогда
			Количество = Количество + 1;
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Количество;
	
КонецФункции

// Возвращает имя элемента формы
//
&НаСервереБезКонтекста
Функция ИмяЭлементаПоИндексу(Индекс, ПрефиксИмени)
	
	Возврат СтрШаблон(ПрефиксИмени + "%1", Индекс);
	
КонецФункции

#КонецОбласти

#Область Навигация

&НаСервере
Процедура ИзменитьЭтап(Идентификатор)
	
	Если Идентификатор = "ВнесениеИзмененийЕГР_СписокИзменений" Тогда
		
		Если ОбъектЗаполнен(Объект) Тогда
			Элементы.СоздатьЗаявление.КнопкаПоУмолчанию = Истина;
		ИначеЕсли ЭтоНачалоРаботы Тогда
			Элементы.ЗаполнитьРеквизитыПоДаннымЕГР.КнопкаПоУмолчанию = Истина;
		Иначе
			Элементы.ЗаполнитьРеквизитыВыбраннойОрганизации.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		
		УстановитьВидимостьСтраницы(ЭтотОбъект, "СписокИзменений");
		
	ИначеЕсли Идентификатор = "ВнесениеИзмененийЕГР_Устав" Тогда
		
		Если Не ЗначениеЗаполнено(АдресТиповыеУставы) тогда
			АдресТиповыеУставы = Обработки.РегистрацияОрганизации.ПодготовитьТаблицуТиповыхУставов(УникальныйИдентификатор);
		КонецЕсли;
		
		Элементы.ПерейтиНаШагЗаявление.КнопкаПоУмолчанию = Истина;
		УстановитьВидимостьСтраницы(ЭтотОбъект, "УставКИзменению");
	
	ИначеЕсли Идентификатор = "ВнесениеИзмененийЕГР_Заявление" Тогда
		
		Если ЭтоЮрЛицо(ЭтотОбъект) И Не ЗначениеЗаполнено(ДатаРешения) Тогда
			ДатаРешения = Обработки.РегистрацияОрганизации.ДатаСоставленияЗаявления();
			ОбновитьРеквизитыВыбраннойФормы(ДатаРешения);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СпособПодачиДокументов) Тогда
			
			Если ПодачаДокументовВЭлектронномВидеДоступна Тогда
				СпособПодачиДокументов = РегистрацияОрганизацииКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронноОнлайн;
			Иначе
				СпособПодачиДокументов = РегистрацияОрганизацииКлиентСервер.СпособыПодачиДокументов().ИФНСлично;
			КонецЕсли;
			
		КонецЕсли;
		
		ПрочитатьОтветственныеЛица();
		ПрочитатьЗаявителя();
		ЗаполнитьОтветственныеЛица();
		
		ЗаполнитьДобавленныеКолонкиТаблицыУчредители();
		
		Элементы.ЗаполнитьЗаявление.КнопкаПоУмолчанию = Истина;
		
		УстановитьВидимостьСтраницы(ЭтотОбъект, "Заявление");
		
	ИначеЕсли Идентификатор = "ВнесениеИзмененийЕГР_Документы" Тогда
		
		Если ИзмененоМестоположение(ЭтотОбъект) Тогда
			Элементы.АдресИРеквизитыВашейИнспекции.Заголовок = НСтр("ru = 'Адрес и реквизиты инспекции по новому адресу'");
		Иначе
			Элементы.АдресИРеквизитыВашейИнспекции.Заголовок = НСтр("ru = 'Адрес и реквизиты вашей инспекции'");
		КонецЕсли;
		
		УстановитьВидимостьБаннераНачалоРаботы();
		УстановитьПредставлениеПрикрепленныхФайлов();
		УстановитьВидимостьСтраницы(ЭтотОбъект, "Документы");
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	ТекущийЭтап = Идентификатор;
	
	Если Объект.Ссылка.Пустая() И ЗначениеЗаполнено(Организация) Тогда
		ЮридическоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо");
	Иначе
		ЮридическоеФизическоеЛицо = Объект.ЮридическоеФизическоеЛицо;
	КонецЕсли;
	
	НавигацияПомощников.РазместитьНавигацию(
		ЭтотОбъект,
		СтруктураНавигации(ДатаРешения, ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо),
		Новый Структура("НавигацияПараметрФормы", Идентификатор));
	
	Заголовок = ЗаголовокФормы(ЮридическоеФизическоеЛицо);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьСтраницы(Форма, ИмяСтраницы)
	
	Элементы = Форма.Элементы;
	
	Для Каждого Страница ИЗ ИменаСтраницПомощника() Цикл
		Элементы[Страница].Видимость = (Страница = ИмяСтраницы);
	КонецЦикла;
	
	Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы[ИмяСтраницы];
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИменаСтраницПомощника()
	
	ИменаСтраницПомощника = Новый Массив;
	ИменаСтраницПомощника.Добавить("СписокИзменений");
	ИменаСтраницПомощника.Добавить("УставКИзменению");
	ИменаСтраницПомощника.Добавить("Заявление");
	ИменаСтраницПомощника.Добавить("Документы");
	ИменаСтраницПомощника.Добавить("Ожидание");
	
	Возврат ИменаСтраницПомощника;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтруктураНавигации(Период, ЭтоЮрЛицо)
	
	СтруктураНавигацииПомощника = Обработки.РегистрацияОрганизации.СтруктураНавигацииПомощникаИзмененийЕГР(
		ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР(Период, ЭтоЮрЛицо));
	
	Возврат СтруктураНавигацииПомощника.Структура;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИдентификаторЭтапаПоСостояниюЗаявления(Состояние)
	
	Состояние = СостояниеЗаявления(Состояние);
	
	Если Состояние.Статус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусВРаботеСтрокой() Тогда
		ИдентификаторЭтапа = "ВнесениеИзмененийЕГР_Заявление";
	ИначеЕсли Состояние.Статус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусПодготовленоСтрокой()
		Или Состояние.Статус = ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусСданоСтрокой()
		Или Состояние.Статус = ДокументооборотРегистрацияВФНС.СтатусыЭтаповОтправки(Истина).НеПринято
		Или Состояние.Отправлено Тогда
		ИдентификаторЭтапа = "ВнесениеИзмененийЕГР_Документы";
	Иначе
		ИдентификаторЭтапа = "ВнесениеИзмененийЕГР_СписокИзменений";
	КонецЕсли;
	
	Возврат ИдентификаторЭтапа;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеДаннымиЕГР

&НаКлиенте
Процедура Подключаемый_ЗаполнитьРеквизитыОрганизацииПоИННПриОткрытии()
	
	Если ЭтоНачалоРаботы И Не ПустаяСтрока(ПоискИННЗапрос) Тогда
		// Если ИНН был сохранен в данных формы, заполним реквизиты из ЕГР
		ЗаполнитьРеквизитыОрганизацииПоИНН(ПоискИННЗапрос, Ложь);
	ИначеЕсли ЗначениеЗаполнено(Организация) Тогда
		// Если организация задана в параметрах формы, заполним реквизиты из ЕГР
		ЗаполнитьРеквизитыОрганизацииПоИНН(ИННОрганизации(Организация), Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыОрганизацииПоИНН(ИННОрганизации, АктивныйДиалогСПользователем, ВосстановитьТекущийЭтап = Ложь)
	
	ПоискИННОтвет = ОрганизацииФормыДляОтчетностиВызовСервера.ЗапроситьДанныеЕГР(
		НовыйЗапросДанныхЕГР(ИННОрганизации));
	
	Если Не ПустаяСтрока(ПоискИННОтвет.Ошибка.Код) Или Не ПустаяСтрока(ПоискИННОтвет.Ошибка.Описание) Тогда
		// Вызов завершился ошибкой, описание оповещения ОписаниеОповещенияЗакончитьЗаполнениеДаннымиЕГР() вызвано не будет.
		ТекущийЭтап = "ВнесениеИзмененийЕГР_СписокИзменений";
		ПослеЗаполненияДанныхЕГР();
	КонецЕсли;
	
	Если АктивныйДиалогСПользователем Тогда
		ОрганизацииФормыДляОтчетностиКлиент.ЗаполнитьРеквизитыПоПолюПоискаИНН(
			НовыйЗапросДанныхЕГР(ИННОрганизации),
			ПоискИННОтвет,
			ОписаниеОповещенияЗакончитьЗаполнениеДаннымиЕГР(),
			ТекущийЭлемент,
			ПолеИсточникИНН(ЭтоНачалоРаботы));
	Иначе
		ОрганизацииФормыДляОтчетностиКлиент.ОбработатьРезультатПоискаИННПриИзмененииПоляПоиска(
			ПоискИННОтвет,
			ОписаниеОповещенияЗакончитьЗаполнениеДаннымиЕГР(ВосстановитьТекущийЭтап), Истина);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	СброситьИзмененияРеквизитов();
	
	УстановитьПривилегированныйРежим(Истина);
	ЗначениеВРеквизитФормы(Справочники.Организации.СоздатьЭлемент(), "Объект");
	УстановитьПривилегированныйРежим(Ложь);
	
	Модифицированность = Ложь;
	
	ОбновитьРеквизитыВыбраннойФормы(ДатаРешения);
	ИзменитьЭтап("ВнесениеИзмененийЕГР_СписокИзменений");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИННОрганизации(Знач Организация)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИНН");
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолеИсточникИНН(ЭтоНачалоРаботы)
	
	Возврат ?(ЭтоНачалоРаботы, "ПоискИННЗапрос", "Организация");
	
КонецФункции

&НаКлиенте
Функция НовыйЗапросДанныхЕГР(ИНН)
	
	Запрос = ОрганизацииФормыДляОтчетностиКлиентСервер.НовыйЗапросДанныхЕГР();
	Запрос.ИНН = ИНН;
	Запрос.ЮридическоеФизическоеЛицо = Объект.ЮридическоеФизическоеЛицо;
	Запрос.Ссылка = Объект.Ссылка;
	Запрос.ОбъектЗаполнен = ОбъектЗаполнен(Объект);
	Запрос.ОтборОПФ = РегистрацияОрганизацииКлиентСервер.ПоддерживаемыеПравовыеФормы();
	
	Возврат Запрос;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПоискаИНН()
	
	ОрганизацииФормыДляОтчетностиКлиент.ОбработатьРезультатПоискаИННПриИзмененииПоляПоиска(
		ПоискИННОтвет,
		ОписаниеОповещенияЗакончитьЗаполнениеДаннымиЕГР());
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеОповещенияЗакончитьЗаполнениеДаннымиЕГР(ВосстановитьТекущийЭтап = Ложь)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ВосстановитьТекущийЭтап", ВосстановитьТекущийЭтап);
	
	// Параметр оповещения может быть модифицирован - см. НачатьЗаполнениеДаннымиЕГР()
	Возврат Новый ОписаниеОповещения("ЗакончитьЗаполнениеДаннымиЕГР", ЭтотОбъект, ПараметрыЗаполнения);
	
КонецФункции

&НаКлиенте
Процедура ЗакончитьЗаполнениеДаннымиЕГР(Ответ, ПараметрыЗаполнения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да И ПараметрыЗаполнения.Свойство("ПоискИННОтвет") Тогда
		ПоказатьИндикаторОжидания();
		УправлениеФормой(ЭтотОбъект);
		ЗаполнитьДаннымиЕГР(ПараметрыЗаполнения.ПоискИННОтвет, ПараметрыЗаполнения.ВосстановитьТекущийЭтап);
		НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД = 0;
		ПолучитьИнформациюОбУчредителяхИКодахОКВЭД();
	ИначеЕсли Ответ = Ложь Тогда // Отказались от подключения тестового периода 1С:Контрагент
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиЕГР(Знач ПоискИННОтвет, ВосстановитьТекущийЭтап = Ложь)
	
	СброситьИзмененияРеквизитов();
	
	Обработки.РегистрацияОрганизации.ЗаполнитьДаннымиЕГР(ПоискИННОтвет, ЭтотОбъект);
	
	Если СоздатьПриОткрытии Тогда
		// Помощник открыт командой "Создать",
		// пропускаем поиск существующих заявлений и открываем первый этап помощника.
		СоздатьПриОткрытии = Ложь;
		ВосстановитьТекущийЭтап = Ложь;
	Иначе
		АктуальныеЗаявления = НайтиАктуальныеЗаявления(Объект.Ссылка);
		ЗаявлениеОВнесенииИзмененийЕГР = АктуальныеЗаявления.Заявление;
		ЗаявлениеОВнесенииИзмененийУчредительныеДокументы = АктуальныеЗаявления.ЗаявлениеОВнесенииИзмененийУчредительныеДокументы;
		ПрочитатьЗаявление();
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если ВосстановитьТекущийЭтап Тогда
		Если ЗначениеЗаполнено(ЗаявлениеОВнесенииИзмененийУчредительныеДокументы) Тогда
			ИдентификаторЭтапа = ИдентификаторЭтапаПоСостояниюЗаявления(ЗаявлениеОВнесенииИзмененийУчредительныеДокументыСостояниеОтправки);
		ИначеЕсли ЗначениеЗаполнено(ЗаявлениеОВнесенииИзмененийЕГР) Тогда
			
			УстановитьПредставлениеПрикрепленныхФайлов();
			
			Если РегистрацияОрганизацииКлиентСервер.ЭтоЭлектронныйСпособПодачиДокументов(СпособПодачиДокументов) Тогда
				КонтактныеДанныеПодтверждены = (НомерТелефонаПодтвержден И ЭлектроннаяПочтаПодтверждена);
			Иначе
				КонтактныеДанныеПодтверждены = Ложь;
			КонецЕсли;
			
			ДокументДоступенКРедактированию = РегистрацияОрганизацииСервер.ДокументДоступенКРедактированию(ЗаявлениеОВнесенииИзмененийЕГР);
			ОтправлятьЗаявлениеВXMLФормате = РегистрацияОрганизацииКлиентСервер.ОтправлятьЗаявлениеВXMLФормате(
				ДатаРешения,
				СпособПодачиДокументов,
				ФорматОтправкиДокументов);
			
			Если ДокументДоступенКРедактированию И ОтправлятьЗаявлениеВXMLФормате Тогда
				ЗаявлениеPDF = Обработки.РегистрацияОрганизации.ПрисоединенныйФайлЗаявленияПоИмени(
					ЗаявлениеОВнесенииИзмененийЕГР,
					ИмяPDFФайлаЗаявления(ЭтоЮрЛицо(ЭтотОбъект)));
				Если Не ЗначениеЗаполнено(ЗаявлениеPDF) Тогда
					// Заявение создано, но не получен PDF-файл от налоговой
					ТребуетсяФормированиеPDF = Истина;
				Иначе
					ТребуетсяФормированиеPDF = Ложь;
				КонецЕсли;
			Иначе
				ТребуетсяФормированиеPDF = Ложь;
			КонецЕсли;
			
			Если Не ТребуетсяФормированиеPDF И (ЗначениеЗаполнено(ПодписанныеДокументы) Или КонтактныеДанныеПодтверждены) Тогда
				ИдентификаторЭтапа = "ВнесениеИзмененийЕГР_Документы";
			Иначе
				ИдентификаторЭтапа = ИдентификаторЭтапаПоСостояниюЗаявления(ЗаявлениеОВнесенииИзмененийЕГРСостояниеОтправки);
			КонецЕсли;
			
		Иначе
			ИдентификаторЭтапа = "ВнесениеИзмененийЕГР_СписокИзменений";
		КонецЕсли;
		
		// Если работа помощника была закончена на втором этапе,
		// то при повторном открытии нужно установить Модифицированность для заполнения заявления.
		Если ИдентификаторЭтапа = "ВнесениеИзмененийЕГР_Заявление" Тогда
			Модифицированность = Истина;
		КонецЕсли;
		
		ТекущийЭтап = ИдентификаторЭтапа;
	Иначе
		ТекущийЭтап = "ВнесениеИзмененийЕГР_СписокИзменений"
	КонецЕсли;
	
	Заголовок = ЗаголовокФормы(Объект.ЮридическоеФизическоеЛицо);
	
	ОтобразитьУчредителей();
	ОтобразитьВидыДеятельности();
	
	ОбновитьРеквизитыВыбраннойФормы(ДатаРешения);
	Если ЗначениеЗаполнено(ЗаявлениеОВнесенииИзмененийЕГР)
		И ВыбраннаяФорма <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаявлениеОВнесенииИзмененийЕГР, "ИмяФормы") Тогда
		
		Модифицированность = Истина;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьПолучениеИнформацииОбУчредителяхИКодахОКВЭД(Знач ИННЗапрос, Знач УникальныйИдентификатор)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение информации о связях по данным ЕГР'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.РегистрацияОрганизации.ПолучитьИнформациюОбУчредителяхИКодахОКВЭДВФоне",
		ИННЗапрос,
		НастройкиЗапуска);
		
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПослеПолученияИнформацииОбУчредителяхИКодахОКВЭД(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД = 0;
		Возврат; // Отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОжиданиеОтвета = Ложь;
		ОписаниеОшибки = "";
		
		ПослеПолученияИнформацииОбУчредителяхИКодахОКВЭДНаСервере(ДлительнаяОперация.АдресРезультата, ОжиданиеОтвета, ОписаниеОшибки);
		
		Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			Если ПовторятьПопыткуПолученияИнформацииОбУчредителяхИКодахОКВЭДПриОшибкеСервиса() Тогда
				ПодключитьОбработчикОжидания("ПолучитьИнформациюОбУчредителяхИКодахОКВЭД",
					ТаймаутПопытокПолученияИнформацииОбУчредителяхИКодахОКВЭД(), Истина);
			Иначе
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиПолученияИнформацииОбУчредителяхИКодахОКВЭД(ОписаниеОшибки), , ,
					ПолеИсточникИНН(ЭтоНачалоРаботы));
			КонецЕсли;
		ИначеЕсли ОжиданиеОтвета Тогда
			ПодключитьОбработчикОжидания("ПолучитьИнформациюОбУчредителяхИКодахОКВЭД",
				ТаймаутПопытокПолученияИнформацииОбУчредителяхИКодахОКВЭД(), Истина);
		КонецЕсли;
		
	Иначе
		Если ПовторятьПопыткуПолученияИнформацииОбУчредителяхИКодахОКВЭДПриОшибкеСервиса() Тогда
			ПодключитьОбработчикОжидания("ПолучитьИнформациюОбУчредителяхИКодахОКВЭД",
				ТаймаутПопытокПолученияИнформацииОбУчредителяхИКодахОКВЭД(), Истина);
		Иначе
			ВызватьИсключение ТекстОшибкиПолученияИнформацииОбУчредителяхИКодахОКВЭД(ДлительнаяОперация.КраткоеПредставлениеОшибки);
			ПослеЗаполненияДанныхЕГР();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеПолученияИнформацииОбУчредителяхИКодахОКВЭДНаСервере(АдресРезультата, ОжиданиеОтвета, ОписаниеОшибки = "")
	
	ИнформацияОСвязях = ПолучитьИзВременногоХранилища(АдресРезультата);
	ОжиданиеОтвета = ИнформацияОСвязях.ОжиданиеОтвета;
	
	Если ЗначениеЗаполнено(ИнформацияОСвязях.ОписаниеОшибки) Тогда
		ОписаниеОшибки = ИнформацияОСвязях.ОписаниеОшибки;
	КонецЕсли;
	
	Если Не ИнформацияОСвязях.ОжиданиеОтвета Тогда
		Обработки.РегистрацияОрганизации.ЗаполнитьИнформацияОСвязях(ИнформацияОСвязях, ЭтотОбъект);
		ПослеЗаполненияДанныхЕГР();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИнформациюОбУчредителяхИКодахОКВЭД()
	
	Если НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД = Неопределено Тогда
		НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД = 1;
	Иначе
		НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД = НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД + 1;
	КонецЕсли;
	
	ДлительнаяОперация = НачатьПолучениеИнформацииОбУчредителяхИКодахОКВЭД(Объект.ИНН, УникальныйИдентификатор);
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		Если ПовторятьПопыткуПолученияИнформацииОбУчредителяхИКодахОКВЭДПриОшибкеСервиса() Тогда
			ПодключитьОбработчикОжидания("ПолучитьИнформациюОбУчредителяхИКодахОКВЭД",
				ТаймаутПопытокПолученияИнформацииОбУчредителяхИКодахОКВЭД(), Истина);
		Иначе
			ВызватьИсключение ТекстОшибкиПолученияИнформацииОбУчредителяхИКодахОКВЭД(ДлительнаяОперация.КраткоеПредставлениеОшибки);
			ПослеЗаполненияДанныхЕГР();
		КонецЕсли;
	Иначе
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			Новый ОписаниеОповещения("Подключаемый_ПослеПолученияИнформацииОбУчредителяхИКодахОКВЭД", ЭтотОбъект),
			НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПовторятьПопыткуПолученияИнформацииОбУчредителяхИКодахОКВЭДПриОшибкеСервиса()
	
	Если НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД = Неопределено Тогда
		Возврат Истина;
	Иначе
		Возврат НомерПопыткиПолученияИнформацииОбУчредителяхИКодахОКВЭД <= 3;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ТаймаутПопытокПолученияИнформацииОбУчредителяхИКодахОКВЭД()
	
	Возврат 3;
	
КонецФункции

&НаКлиенте
Функция ТекстОшибкиПолученияИнформацииОбУчредителяхИКодахОКВЭД(ОписаниеОшибки)
	
	Возврат НСтр("ru = 'Ошибка при получении информации об учредителях и кодах ОКВЭД:'") + Символы.ПС + ОписаниеОшибки;
	
КонецФункции

&НаСервере
Процедура ПослеЗаполненияДанныхЕГР()
	
	Обработки.РегистрацияОрганизации.ЗаполнитьДополнительныеРеквизитыПоДаннымЕГР(ЭтотОбъект);
	
	Если ПустаяСтрока(РуководительНаименование) Тогда
		РуководительНаименование = НСтр("ru = 'В реестре не указаны данные о руководителе'");
	КонецЕсли;
	
	ОтобразитьУчредителей();
	
	Если ЗначениеЗаполнено(ОКВЭД2ИзКлассификацииВыручки) Тогда
		Модифицированность = Истина;
		
		Если СокрЛП(ОКВЭД2ИзКлассификацииВыручки.ОсновнойКод) <> СокрЛП(КодОКВЭД2) Тогда
			КодОКВЭД2Новый = ОКВЭД2ИзКлассификацииВыручки.ОсновнойКод;
			НаименованиеОКВЭД2Новое = ОКВЭД2ИзКлассификацииВыручки.ОсновнойКодНаименование;
		Иначе
			КодОКВЭД2Новый = "";
			НаименованиеОКВЭД2Новое = "";
		КонецЕсли;
		
		ВидыДеятельностиНовые.Загрузить(ВидыДеятельности.Выгрузить());
		НайденныеСтроки = ВидыДеятельностиНовые.НайтиСтроки(Новый Структура("Код", ОКВЭД2ИзКлассификацииВыручки.ОсновнойКод));
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = ВидыДеятельностиНовые.Добавить();
			НоваяСтрока.Код = ОКВЭД2ИзКлассификацииВыручки.ОсновнойКод;
			НоваяСтрока.Наименование = ОКВЭД2ИзКлассификацииВыручки.ОсновнойКодНаименование;
			НоваяСтрока.ЭтоОКВЭД2 = Истина;
		КонецЕсли;
	КонецЕсли;
	ОтобразитьВидыДеятельности();
	
	// Этап меняем для выключения страницы "Ожидание" и управления элементами формы,
	// поэтому в параметре передаем ТекущийЭтап.
	ИзменитьЭтап(ТекущийЭтап);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыРегистрирующихОрганов(ДанныеЗаполнения)
	
	РеквизитыРегистрирующегоОргана = Неопределено;
	Если ДанныеЗаполнения.Свойство("РеквизитыРегистрирующегоОргана", РеквизитыРегистрирующегоОргана) Тогда
		РегистрирующийОрганКод = РеквизитыРегистрирующегоОргана.Код;
		РегистрирующийОрганНаименование = РеквизитыРегистрирующегоОргана.Наименование;
		РегистрирующийОрганТелефон = РеквизитыРегистрирующегоОргана.Телефон;
		РегистрирующийОрганАдрес = РеквизитыРегистрирующегоОргана.Адрес;
		РегистрирующийОрганИнформация = РеквизитыРегистрирующегоОргана.Информация;
	КонецЕсли;
	
	РеквизитыПрежнегоРегистрирующегоОргана = Неопределено;
	Если ДанныеЗаполнения.Свойство("РеквизитыПрежнегоРегистрирующегоОргана", РеквизитыПрежнегоРегистрирующегоОргана) Тогда
		РегистрирующийОрганКод = РеквизитыРегистрирующегоОргана.Код;
		РегистрирующийОрганПрежнийНаименование = РеквизитыПрежнегоРегистрирующегоОргана.Наименование;
		РегистрирующийОрганПрежнийТелефон = РеквизитыПрежнегоРегистрирующегоОргана.Телефон;
		РегистрирующийОрганПрежнийАдрес = РеквизитыПрежнегоРегистрирующегоОргана.Адрес;
		РегистрирующийОрганПрежнийИнформация = РеквизитыПрежнегоРегистрирующегоОргана.Информация;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПлатежныеРеквизитыОплатаГоспошлины(ДанныеЗаполнения)
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ПлатежныеРеквизитыОКТМО = ДанныеЗаполнения.ОКТМО;
	ПлатежныеРеквизитыИНН = ДанныеЗаполнения.ИНН;
	ПлатежныеРеквизитыКПП = ДанныеЗаполнения.КПП;
	ПлатежныеРеквизитыНаименованиеПолучателя = ДанныеЗаполнения.НаименованиеПолучателя;
	ПлатежныеРеквизитыБИК = ДанныеЗаполнения.БИК;
	ПлатежныеРеквизитыНомерСчета = ДанныеЗаполнения.НомерСчета;
	ПлатежныеРеквизитыКоррСчет = ДанныеЗаполнения.КоррСчет;
	ПлатежныеРеквизитыНаименованиеБанка = ДанныеЗаполнения.НаименованиеБанка;
	ПлатежныеРеквизитыКБК = ДанныеЗаполнения.КБК;
	ПлатежныеРеквизитыСумма = ДанныеЗаполнения.Сумма;
	
КонецПроцедуры

#Область ФормированиеДокументовВФоне

&НаКлиенте
Процедура СоздатьЗаявлениеНаКлиенте(ЗакрытьФормуПослеВыполнения = Ложь)
	
	Если Не ЗакрытьФормуПослеВыполнения И Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		ДлительнаяОперация = СоздатьЗаявлениеВФоне();
		Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
			ПоказатьИндикаторОжидания();
			УправлениеФормой(ЭтотОбъект);
			
			ПараметрыОповещения = Новый Структура("ЗакрытьФормуПослеВыполнения", ЗакрытьФормуПослеВыполнения);
			ОповещениеОЗавершении = Новый ОписаниеОповещения("Подключаемый_ПослеСозданияЗаявления", ЭтотОбъект, ПараметрыОповещения);
			
			НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
			
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, НастройкиОжидания);
		Иначе
			ПоказатьОшибкуСозданияЗаполненияЗаявления(ДлительнаяОперация);
		КонецЕсли;
		
	ИначеЕсли ОтображатьШагУстав Тогда
		ИзменитьЭтап("ВнесениеИзмененийЕГР_Устав");
	Иначе
		ИзменитьЭтап("ВнесениеИзмененийЕГР_Заявление");
	КонецЕсли;
	
КонецПроцедуры

// Создает черновик заявления в фоне
// 
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьВФоне()
//
&НаСервере
Функция СоздатьЗаявлениеВФоне()
	
	ИмяПроцедуры = "Обработки.РегистрацияОрганизации.СоздатьЗаявлениеОВнесенииИзмененийЕГР";
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ЗаявлениеОВнесенииИзмененийЕГР", ЗаявлениеОВнесенииИзмененийЕГР);
	ПараметрыПроцедуры.Вставить("ЗаявлениеОВнесенииИзмененийУчредительныеДокументы", ЗаявлениеОВнесенииИзмененийУчредительныеДокументы);
	ПараметрыПроцедуры.Вставить("ДанныеПомощникаЗаполнения", ДанныеПомощникаЗаполнения());
	ПараметрыПроцедуры.Вставить("ИмяФормы", ВыбраннаяФорма);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Подготовка документов для внесения изменений в ЕГР'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПослеСозданияЗаявления(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	ЗакрытьФорму = (ДлительнаяОперация.Статус = "Выполнено") И ДополнительныеПараметры.ЗакрытьФормуПослеВыполнения;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		РезультатВыполнения = ОбработатьРезультатСозданияЗаполненияЗаявления(ДлительнаяОперация.АдресРезультата);
		Если РезультатВыполнения <> Неопределено Тогда
			
			Для Каждого ИзмененныйОбъект Из РезультатВыполнения.ИзмененныеОбъекты Цикл
				ОповеститьОбИзменении(ИзмененныйОбъект);
			КонецЦикла;
			
			Если Не ЗакрытьФорму И ОтображатьШагУстав Тогда
				ИзменитьЭтап("ВнесениеИзмененийЕГР_Устав");
			ИначеЕсли Не ЗакрытьФорму Тогда
				ИзменитьЭтап("ВнесениеИзмененийЕГР_Заявление");
			КонецЕсли;
			Оповестить("ВнесениеИзмененийЕГРнеЗавершено", Объект.Ссылка, ЗаявлениеОВнесенииИзмененийЕГР);
			
		КонецЕсли;
	Иначе
		ПоказатьОшибкуСозданияЗаполненияЗаявления(ДлительнаяОперация);
		ИзменитьЭтап("ВнесениеИзмененийЕГР_СписокИзменений");
	КонецЕсли;
	
	Если ЗакрытьФорму Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗаявлениеНаКлиенте(ЗакрытьФормуПослеВыполнения = Ложь)
	
	Если Не ЗакрытьФормуПослеВыполнения И Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	// Если форма закрывается на этапе "Заявление", то у заявления нужно установить статус "В работе".
	ЗаявлениеВРаботе = ЗакрытьФормуПослеВыполнения;
	
	ДлительнаяОперация = ЗаполнитьЗаявлениеВФоне(ЗаявлениеВРаботе);
	Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
		ПоказатьИндикаторОжидания();
		УправлениеФормой(ЭтотОбъект);
		
		ПараметрыОповещения = Новый Структура("ЗакрытьФормуПослеВыполнения", ЗакрытьФормуПослеВыполнения);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("Подключаемый_ПослеЗаполненияЗаявления", ЭтотОбъект, ПараметрыОповещения);
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, НастройкиОжидания);
	Иначе
		ПоказатьОшибкуСозданияЗаполненияЗаявления(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет созданное ранее заявление с учетом сведений,
// указанных на закладке Заявление
// 
// Параметры:
//  ЗаявлениеВРаботе - Булево - признак того, что заполнение заявления еще не завершено
//                              и еще рано устанавливать статус на "Подготовлено".
//
// Возвращаемое значение:
//  Структура - см. ДлительныеОперации.ВыполнитьВФоне()
//
&НаСервере
Функция ЗаполнитьЗаявлениеВФоне(ЗаявлениеВРаботе)
	
	ИмяПроцедуры = "Обработки.РегистрацияОрганизации.ЗаполнитьЗаявлениеОВнесенииИзмененийЕГР";
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Модифицированность", Модифицированность);
	ПараметрыПроцедуры.Вставить("ЗаявлениеОВнесенииИзмененийЕГР", ЗаявлениеОВнесенииИзмененийЕГР);
	ПараметрыПроцедуры.Вставить("ЗаявлениеОВнесенииИзмененийУчредительныеДокументы", ЗаявлениеОВнесенииИзмененийУчредительныеДокументы);
	ПараметрыПроцедуры.Вставить("ДанныеПомощникаЗаполнения", ДанныеПомощникаЗаполнения());
	ПараметрыПроцедуры.Вставить("ИмяФормы", ВыбраннаяФорма);
	
	Если РегистрацияОрганизацииКлиентСервер.ЭтоЭлектронныйСпособПодачиДокументов(СпособПодачиДокументов) Тогда
		ПараметрыПроцедуры.Вставить("ЗаявлениеВРаботе", Истина);
	Иначе
		ПараметрыПроцедуры.Вставить("ЗаявлениеВРаботе", ЗаявлениеВРаботе);
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование документов для внесения изменений в ЕГР'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПослеЗаполненияЗаявления(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	ЗакрытьФорму = (ДлительнаяОперация.Статус = "Выполнено") И ДополнительныеПараметры.ЗакрытьФормуПослеВыполнения;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		РезультатВыполнения = ОбработатьРезультатСозданияЗаполненияЗаявления(ДлительнаяОперация.АдресРезультата);
		Если РезультатВыполнения <> Неопределено Тогда
			
			Для Каждого ИзмененныйОбъект Из РезультатВыполнения.ИзмененныеОбъекты Цикл
				ОповеститьОбИзменении(ИзмененныйОбъект);
			КонецЦикла;
			
			Если Модифицированность
				И РегистрацияОрганизацииКлиентСервер.НеУдалосьСформироватьПакетДокументов(РезультатВыполнения,
					ДатаРешения, СпособПодачиДокументов, ФорматОтправкиДокументов) Тогда
				
				Модифицированность = Истина;
				ИзменитьЭтап("ВнесениеИзмененийЕГР_Заявление");
				ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатВыполнения.ТекстОшибки, ЗаявлениеОВнесенииИзмененийЕГР);
			Иначе
				Модифицированность = Ложь;
				Если Не ЗакрытьФорму Тогда
					ИзменитьЭтап("ВнесениеИзмененийЕГР_Документы");
					Оповестить("ПредложитьСохранитьИзмененияЕГР", Объект.Ссылка);
				Иначе
					Оповестить("ВнесениеИзмененийЕГРнеЗавершено", Объект.Ссылка, ЗаявлениеОВнесенииИзмененийЕГР);
				КонецЕсли;
			КонецЕсли
			
		КонецЕсли;
		
	Иначе
		ПоказатьОшибкуСозданияЗаполненияЗаявления(ДлительнаяОперация);
		ИзменитьЭтап("ВнесениеИзмененийЕГР_Заявление");
	КонецЕсли;
	
	Если ЗакрытьФорму Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатСозданияЗаполненияЗаявления(АдресРезультата)
	
	Результат = Обработки.РегистрацияОрганизации.НовыйРезультатФормированияЗаявления();
	Результат.Вставить("ИзмененныеОбъекты");
	
	СсылкиНаИзмененныеОбъекты = Новый Массив;
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если РезультатВыполнения = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если РезультатВыполнения.Свойство("Организация") Тогда
		ОрганизацияОбъект = РезультатВыполнения.Организация.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(ОрганизацияОбъект, "Объект");
		СсылкиНаИзмененныеОбъекты.Добавить(Объект.Ссылка);
	КонецЕсли;
	
	Если РезультатВыполнения.Свойство("Учредители") Тогда
		Учредители.Загрузить(РезультатВыполнения.Учредители);
		Для Каждого Учредитель Из Учредители Цикл
			СсылкиНаИзмененныеОбъекты.Добавить(Учредитель.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Если РезультатВыполнения.Свойство("Заявитель") Тогда
		// В результате заполнения был записан Заявитель
		ЗаявительОбъект = РезультатВыполнения.Заявитель.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(ЗаявительОбъект, "Заявитель");
		СсылкиНаИзмененныеОбъекты.Добавить(ЗаявительОбъект.Ссылка);
	КонецЕсли;
	
	Если РезультатВыполнения.Свойство("ПлатежныеРеквизитыОплатаГоспошлины") Тогда
		ЗаполнитьПлатежныеРеквизитыОплатаГоспошлины(РезультатВыполнения.ПлатежныеРеквизитыОплатаГоспошлины)
	КонецЕсли;
	
	ЗаполнитьРеквизитыРегистрирующихОрганов(РезультатВыполнения);
	
	Если РезультатВыполнения.Свойство("ЗаявлениеОВнесенииИзмененийЕГР") Тогда
		
		ЗаявлениеОВнесенииИзмененийЕГР = РезультатВыполнения.ЗаявлениеОВнесенииИзмененийЕГР;
		ЗаявлениеОВнесенииИзмененийЕГРСостояниеОтправки = ИнтерфейсыВзаимодействияБРО.СостояниеДокумента(
			ЗаявлениеОВнесенииИзмененийЕГР);
		СсылкиНаИзмененныеОбъекты.Добавить(ЗаявлениеОВнесенииИзмененийЕГР);
		
		ОчиститьПодписанныеДокументыНаСервере();
		ВидыПрикрепленныхДокументов = РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов();
		ПараметрыОтображения = ПараметрыОтображенияЭлементовФормы(ЭтотОбъект);
		
		Для Каждого ТекущийВидДокумента Из ВидыПрикрепленныхДокументов Цикл
			
			РегистрацияОрганизацииКлиентСервер.ОчиститьПрикрепленныйДокумент(
				ЭтотОбъект,
				ТекущийВидДокумента.Значение, ,
				ПараметрыОтображения);
			
			Если ТекущийВидДокумента.Значение = ВидыПрикрепленныхДокументов.ИнойДокумент Тогда
				РегистрацияОрганизацииСервер.ОчиститьПрикрепленныйИнойДокумент(ЭтотОбъект);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если РезультатВыполнения.Свойство("ЗаявлениеОВнесенииИзмененийУчредительныеДокументы") Тогда
		ЗаявлениеОВнесенииИзмененийУчредительныеДокументы = РезультатВыполнения.ЗаявлениеОВнесенииИзмененийУчредительныеДокументы;
		ЗаявлениеОВнесенииИзмененийУчредительныеДокументыСостояниеОтправки = ИнтерфейсыВзаимодействияБРО.СостояниеДокумента(
			ЗаявлениеОВнесенииИзмененийУчредительныеДокументы);
		СсылкиНаИзмененныеОбъекты.Добавить(ЗаявлениеОВнесенииИзмененийУчредительныеДокументы);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Результат, РезультатВыполнения);
	Результат.ИзмененныеОбъекты = СсылкиНаИзмененныеОбъекты;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьОшибкуСозданияЗаполненияЗаявления(ДлительнаяОперация)
	
	Текст = НСтр("ru = 'Ошибка при создании пакета документов:'");
	Текст = Текст + Символы.ПС + ДлительнаяОперация.КраткоеПредставлениеОшибки;
	ВызватьИсключение Текст;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗаявителя()
	
	Попытка
		РезультатВыполнения = Обработки.РегистрацияОрганизации.ЗаписатьЗаявителя(ДанныеЗаявителя());
	Исключение
		ЗаписьЖурналаРегистрации(
			Обработки.РегистрацияОрганизации.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ФизическиеЛица,
			,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	ЗначениеВРеквизитФормы(РезультатВыполнения.Заявитель.ПолучитьОбъект(), "Заявитель");
	
КонецПроцедуры

&НаСервере
Функция ДанныеПомощникаЗаполнения()
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(ЭтотОбъект);
	
	// Все реквизиты, которые выведены на форму необходимо
	// перенести в структуру для передачи в фоновое задание.
	
	Изменено = СтруктураИзменений(ЭтотОбъект);
	ЮрАдресИПропискаГлавногоСовпадают = Изменено.Местоположение
		И ЮрАдресИПропискаГлавногоСовпадают(ЗаявительАдресПропискиЗначениеJSON, ЮридическийАдресНовыйЗначениеJSON, Учредители);
	ИзмененияВносятсяВУчредительныеДокументы = ИзмененияВносятсяВУчредительныеДокументы(
		ЭтоЮрЛицо,
		Изменено,
		АдресВУставеПолный(АдресВУставе),
		КодыОКВЭДпротиворечатУставу(ВидыДеятельностиВУставе),
		ВидУстава);
	
	ДанныеПомощникаЗаполнения = Новый Структура;
	
	КолонкиТаблицыУчредители = "Ссылка, Наименование, ИНН, ДоляУчастия, СуммаВзноса, ТипУчредителя, ПредставительУчредителя, ДолжностьПредставителя, ПредставительНаОсновании";
	
	// Данные организации
	Если ЭтоЮрЛицо(ЭтотОбъект) Тогда
		ДанныеПомощникаЗаполнения.Вставить("ДанныеОрганизации", ДанныеЮридическогоЛица());
	Иначе
		ДанныеПомощникаЗаполнения.Вставить("ДанныеОрганизации", ДанныеИндивидуальногоПредпринимателя());
	КонецЕсли;
	
	// Изменения
	ДанныеПомощникаЗаполнения.Вставить("Изменено", Изменено);
	ДанныеПомощникаЗаполнения.Вставить("ИзмененияВносятсяВУчредительныеДокументы", ИзмененияВносятсяВУчредительныеДокументы);
	ДанныеПомощникаЗаполнения.Вставить("ИзмененияТребуютУведомления", ИзмененияТребуютУведомления(ЭтоЮрЛицо, Изменено, ЮрАдресИПропискаГлавногоСовпадают));
	
	Если Изменено.ПолноеНаименование Или Изменено.СокращенноеНаименование Тогда
		ДанныеПомощникаЗаполнения.Вставить("НаименованиеПолноеНовое",      НаименованиеПолноеНовое);
		ДанныеПомощникаЗаполнения.Вставить("НаименованиеСокращенноеНовое", НаименованиеСокращенноеНовое);
	КонецЕсли;
	
	Если Изменено.ЮридическийАдрес Или Изменено.Местоположение Тогда
		ДанныеПомощникаЗаполнения.Вставить("ЮридическийАдресНовый", ЮридическийАдресНовый);
		ДанныеПомощникаЗаполнения.Вставить("ЮридическийАдресНовыйЗначениеJSON", ЮридическийАдресНовыйЗначениеJSON);
		ДанныеПомощникаЗаполнения.Вставить("АдресВУставе", АдресВУставе);
	КонецЕсли;
	
	Если Изменено.Руководитель Тогда
		ДанныеПомощникаЗаполнения.Вставить("ДанныеНовогоРуководителя", ДанныеРуководителя(Истина));
		ДанныеПомощникаЗаполнения.Вставить("ДанныеОДолжностиНовогоРуководителя", ДанныеОДолжностиРуководителя(Истина));
	КонецЕсли;
	
	Если Изменено.Учредители Или Изменено.УставныйКапитал Тогда
		ДанныеПомощникаЗаполнения.Вставить("УчредителиНовые", УчредителиНовые.Выгрузить(, КолонкиТаблицыУчредители));
		ДанныеПомощникаЗаполнения.Вставить("УставныйКапитал", УставныйКапитал);
		ДанныеПомощникаЗаполнения.Вставить("УставныйКапиталНовый", УставныйКапиталНовый);
	КонецЕсли;
	
	Если Изменено.ОсновнойВидДеятельности Тогда
		ДанныеПомощникаЗаполнения.Вставить("КодОКВЭД2Новый", КодОКВЭД2Новый);
		ДанныеПомощникаЗаполнения.Вставить("НаименованиеОКВЭД2Новое", НаименованиеОКВЭД2Новое);
	КонецЕсли;
	
	Если Изменено.ВидыДеятельности Тогда
		Если ИзмененыВидыДеятельности(ЭтотОбъект) Тогда
			// Изменен состав кодов
			ДанныеПомощникаЗаполнения.Вставить("ВидыДеятельностиНовые", ВидыДеятельностиНовые.Выгрузить());
			ДанныеПомощникаЗаполнения.Вставить("ВидыДеятельностиВУставе", ВидыДеятельностиВУставе);
			ДанныеПомощникаЗаполнения.Вставить("ДополнительныеКодыОКВЭД2Новые",
				ДополнительныеКодыОКВЭД2(ВидыДеятельностиНовые, ?(ИзмененОсновнойВидДеятельности(ЭтотОбъект), КодОКВЭД2Новый, КодОКВЭД2)));
		Иначе
			// Изменен основной код
			ДанныеПомощникаЗаполнения.Вставить("ВидыДеятельностиНовые", ВидыДеятельности.Выгрузить());
			ДанныеПомощникаЗаполнения.Вставить("ВидыДеятельностиВУставе", ВидыДеятельностиВУставе);
			ДанныеПомощникаЗаполнения.Вставить("ДополнительныеКодыОКВЭД2Новые",
				ДополнительныеКодыОКВЭД2(ВидыДеятельности, ?(ИзмененОсновнойВидДеятельности(ЭтотОбъект), КодОКВЭД2Новый, КодОКВЭД2)));
		КонецЕсли;
	КонецЕсли;
	
	// Данные устава
	
	ДанныеПомощникаЗаполнения.Вставить("ИзменитьВидУстава", ИзменитьВидУстава);
	ДанныеПомощникаЗаполнения.Вставить("НомерТиповогоУстава", НомерТиповогоУстава);
	
	// Данные заявления
	ДанныеПомощникаЗаполнения.Вставить("ДатаРешения", ДатаРешения);
	ДанныеПомощникаЗаполнения.Вставить("ДатаСоставления", ДатаРешения);
	ДанныеПомощникаЗаполнения.Вставить("ДанныеЗаявителя", ДанныеЗаявителя());
	
	ДанныеПомощникаЗаполнения.Вставить("СпособРегистрации", СпособРегистрации(СпособПодачиДокументов));
	ДанныеПомощникаЗаполнения.Вставить("СпособПодачиДокументов", СпособПодачиДокументов);
	ДанныеПомощникаЗаполнения.Вставить("ВыдатьНаБумажномНосителе", ВыдатьНаБумажномНосителе);
	
	ДанныеПомощникаЗаполнения.Вставить(
		"Гражданство", УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(Гражданство));
	
	ДанныеПомощникаЗаполнения.Вставить("НомерТелефонаПодтвержден", НомерТелефонаПодтвержден);
	ДанныеПомощникаЗаполнения.Вставить("ЭлектроннаяПочтаПодтверждена", ЭлектроннаяПочтаПодтверждена);
	
	ДанныеПомощникаЗаполнения.Вставить("ТребуетсяСправкаОбОтсутствииСудимости",
		ТребуетсяСправкаОбОтсутствииСудимости(ЭтотОбъект));
	
	ДанныеПомощникаЗаполнения.Вставить("ФорматОтправкиДокументов", ФорматОтправкиДокументов);
	
	Возврат ДанныеПомощникаЗаполнения;
	
КонецФункции

&НаСервере
Функция ДанныеЮридическогоЛица()
	
	ДанныеОрганизации = Обработки.РегистрацияОрганизации.НовыеДанныеЮридическогоЛица();
	ЗаполнитьЗначенияСвойств(ДанныеОрганизации, Объект);
	Если ДанныеОрганизации.Ссылка.Пустая() Тогда
		ДанныеОрганизации.Ссылка = Организация;
	КонецЕсли;
	ДанныеОрганизации.НаименованиеСокращенное = НаименованиеСокращенное;
	ДанныеОрганизации.НаименованиеПолное = НаименованиеПолное;
	
	ДанныеОрганизации.ЮридическийАдрес = ЮридическийАдрес;
	ДанныеОрганизации.ЮридическийАдресЗначениеJSON = ЮридическийАдресЗначениеJSON;
	
	ДанныеОрганизации.КодОКВЭД2 = КодОКВЭД2;
	
	ДанныеОрганизации.ДополнительныеКодыОКВЭД2 = ДополнительныеКодыОКВЭД2(ВидыДеятельности, ДанныеОрганизации.КодОКВЭД2);
	
	ДанныеОрганизации.ДанныеРегистрацииВНалоговомОргане = ДанныеРегистрацииВНалоговомОргане();
	
	ДанныеОрганизации.ДанныеРуководителя = ДанныеРуководителя(Ложь);
	ДанныеОрганизации.ДанныеОДолжностиРуководителя = ДанныеОДолжностиРуководителя(Ложь);
	ДанныеОрганизации.УставныйКапитал = УставныйКапитал;
	
	ДанныеОрганизации.Учредители.Очистить();
	Для Каждого СтрокаТаблицы Из Учредители Цикл
		ЗаполнитьЗначенияСвойств(ДанныеОрганизации.Учредители.Добавить(), СтрокаТаблицы);
	КонецЦикла;
	
	Возврат ДанныеОрганизации;
	
КонецФункции

&НаСервере
Функция ДанныеИндивидуальногоПредпринимателя()
	
	ДанныеОрганизации = Новый Структура();
	ДанныеОрганизации.Вставить("Ссылка", ?(Объект.Ссылка.Пустая(), Организация, Объект.Ссылка));
	ДанныеОрганизации.Вставить("ЮридическоеФизическоеЛицо", Объект.ЮридическоеФизическоеЛицо);
	ДанныеОрганизации.Вставить("Наименование", Объект.Наименование);
	ДанныеОрганизации.Вставить("ИНН", Объект.ИНН);
	ДанныеОрганизации.Вставить("ОГРН", Объект.ОГРН);
	
	ДанныеОрганизации.Вставить("ДатаРегистрации", Объект.ДатаРегистрации);
	ДанныеОрганизации.Вставить("СвидетельствоСерияНомер", Объект.СвидетельствоСерияНомер);
	ДанныеОрганизации.Вставить("СвидетельствоДатаВыдачи", Объект.СвидетельствоДатаВыдачи);
	
	ДанныеОрганизации.Вставить("КодНалоговогоОргана", "");
	
	ДанныеОрганизации.Вставить("ИПРегистрационныйНомерПФР", Объект.ИПРегистрационныйНомерПФР);
	ДанныеОрганизации.Вставить("РегистрационныйНомерПФР", Объект.РегистрационныйНомерПФР);
	ДанныеОрганизации.Вставить("КодОрганаПФР", Объект.КодОрганаПФР);
	ДанныеОрганизации.Вставить("НаименованиеТерриториальногоОрганаПФР", Объект.НаименованиеТерриториальногоОрганаПФР);
	
	ДанныеОрганизации.Вставить("ИПРегистрационныйНомерФСС", Объект.ИПРегистрационныйНомерФСС);
	ДанныеОрганизации.Вставить("РегистрационныйНомерФСС", Объект.РегистрационныйНомерФСС);
	ДанныеОрганизации.Вставить("КодПодчиненностиФСС", Объект.КодПодчиненностиФСС);
	ДанныеОрганизации.Вставить("НаименованиеТерриториальногоОрганаФСС", Объект.НаименованиеТерриториальногоОрганаФСС);
	
	ДанныеОрганизации.Вставить("КодОКВЭД2", КодОКВЭД2);
	ДанныеОрганизации.Вставить("НаименованиеОКВЭД2", Объект.НаименованиеОКВЭД2);
	ДанныеОрганизации.Вставить("ДополнительныеКодыОКВЭД2",
		ДополнительныеКодыОКВЭД2(ВидыДеятельности, ДанныеОрганизации.КодОКВЭД2));
	
	ДанныеОрганизации.Вставить("ДанныеРегистрацииВНалоговомОргане", ДанныеРегистрацииВНалоговомОргане());
	
	ДанныеИндивидуальногоПредпринимателя = Обработки.РегистрацияОрганизации.НовыеДанныеФизическогоЛица();
	ДанныеИндивидуальногоПредпринимателя.Ссылка = Заявитель.Ссылка;
	ДанныеИндивидуальногоПредпринимателя.Фамилия = Объект.ФамилияИП;
	ДанныеИндивидуальногоПредпринимателя.Имя = Объект.ИмяИП;
	ДанныеИндивидуальногоПредпринимателя.Отчество = Объект.ОтчествоИП;
	ДанныеИндивидуальногоПредпринимателя.Пол = Заявитель.Пол;
	ДанныеИндивидуальногоПредпринимателя.ИНН = Объект.ИНН;
	ДанныеИндивидуальногоПредпринимателя.ДатаРождения = Заявитель.ДатаРождения;
	ДанныеИндивидуальногоПредпринимателя.МестоРожденияПредставление = Заявитель.МестоРожденияПредставление;
	
	ГражданинРФ = Гражданство = КодРоссии;
	ИностранныйГражданин = Не ГражданинРФ
		И Гражданство <> РегистрацияОрганизацииКлиентСервер.КодПустогоЗначенияВСпискеВыбораГражданства();
	
	Если ГражданинРФ Или ИностранныйГражданин Тогда
		ДанныеИндивидуальногоПредпринимателя.Гражданство =
			УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(Гражданство);
	КонецЕсли;
	
	ДанныеИндивидуальногоПредпринимателя.АдресПропискиЗначениеJSON = ЗаявительАдресПропискиЗначениеJSON;
	ДанныеИндивидуальногоПредпринимателя.ТелефонРабочийЗначениеJSON = ЗаявительТелефонЗначениеJSON;
	ДанныеИндивидуальногоПредпринимателя.EMailЗначениеJSON = ЗаявительEMailЗначениеJSON;
	
	СерияИНомерПаспорта = ОрганизацииФормы.СерияНомерПаспортаРаздельно(ПаспортСерияНомер);
	
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.Серия            = СерияИНомерПаспорта.Серия;
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.Номер            = СерияИНомерПаспорта.Номер;
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.КемВыдан         = ПаспортныеДанныеЗаявителя.КемВыдан;
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.ДатаВыдачи       = ПаспортныеДанныеЗаявителя.ДатаВыдачи;
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.КодПодразделения = ПаспортныеДанныеЗаявителя.КодПодразделения;
	
	ДанныеОрганизации.Вставить("ИндивидуальныйПредприниматель", ДанныеИндивидуальногоПредпринимателя);
	
	Возврат ДанныеОрганизации;
	
КонецФункции

&НаСервере
Функция ДанныеЗаявителя()
	
	ДанныеЗаявителя = Обработки.РегистрацияОрганизации.НовыеДанныеФизическогоЛица();
	ЗаполнитьЗначенияСвойств(ДанныеЗаявителя, Заявитель);
	ГражданинРФ = Гражданство = КодРоссии;
	ЭтоЮрЛицо = ЭтоЮрЛицо(ЭтотОбъект);
	
	Если ЭтоЮрЛицо Тогда
		ЕстьГражданство = Гражданство <> РегистрацияОрганизацииКлиентСервер.КодПустогоЗначенияВСпискеВыбораГражданства();
		КодыВидовДокументов = ДокументыФизическихЛицБП.КодыФНСВидовДокументов();
		Если ЕстьГражданство Тогда
			ЗаполнитьЗначенияСвойств(ДанныеЗаявителя.ПаспортныеДанные, ПаспортныеДанныеЗаявителя);
			Если ГражданинРФ Тогда
				СерияИНомерПаспорта = ОрганизацииФормы.СерияНомерПаспортаРаздельно(ПаспортСерияНомер);
				ДанныеЗаявителя.ПаспортныеДанные.Серия = СерияИНомерПаспорта.Серия;
				ДанныеЗаявителя.ПаспортныеДанные.Номер = СерияИНомерПаспорта.Номер;
				ДанныеЗаявителя.ПаспортныеДанные.ДокументКодМВД = КодыВидовДокументов.ПаспортГражданинаРФ;
			Иначе
				ДанныеЗаявителя.ПаспортныеДанные.Номер = ИностранныйПаспортСерияНомер;
				ДанныеЗаявителя.ПаспортныеДанные.ДокументКодМВД = КодыВидовДокументов.ИностранныйПаспорт;
			КонецЕсли;
		Иначе
			ЗаполнитьЗначенияСвойств(ДанныеЗаявителя.ДанныеОснованияДляПроживанияВРФ, ДанныеОснованияДляПроживанияВРФ);
			Если ВидДокумента = РегистрацияОрганизацииКлиентСервер.ЗначениеРазрешенияНаВременноеПроживаниеВСпискеВыбора() Тогда
				ДанныеЗаявителя.ДанныеОснованияДляПроживанияВРФ.ВидДокумента = "0";
				ДанныеЗаявителя.ДанныеОснованияДляПроживанияВРФ.ДокументКодМВД =
					КодыВидовДокументов.РазрешениеНаВременноеПроживание;
			Иначе
				ДанныеЗаявителя.ДанныеОснованияДляПроживанияВРФ.ВидДокумента = "1";
				ДанныеЗаявителя.ДанныеОснованияДляПроживанияВРФ.ДокументКодМВД =
					КодыВидовДокументов.ВидНаЖительство;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ДанныеЗаявителя.ДанныеОснованияДляПроживанияВРФ,
				ДокументыФизическихЛицБП.СерияНомерДокументаРаздельно(СерияНомерОснованияДляПроживания));
		КонецЕсли;
	Иначе
		// Паспортные данные для ИП в заявлении не заполняются
		ДанныеЗаявителя.Удалить("ПаспортныеДанные");
		ДанныеЗаявителя.Удалить("ДанныеОснованияДляПроживанияВРФ");
	КонецЕсли;
	
	Если Не ЭтоЮрЛицо Или ГражданинРФ Тогда
		ДанныеЗаявителя.АдресПрописки = ЗаявительАдресПропискиПредставление;
		ДанныеЗаявителя.АдресПропискиЗначениеJSON = ЗаявительАдресПропискиЗначениеJSON;
	Иначе
		ДанныеЗаявителя.АдресИнформирования = ЗаявительАдресДляИнформированияПредставление;
		ДанныеЗаявителя.АдресИнформированияЗначениеJSON = ЗаявительАдресДляИнформированияЗначениеJSON;
	КонецЕсли;
	ДанныеЗаявителя.ТелефонРабочий = ЗаявительТелефонПредставление;
	ДанныеЗаявителя.ТелефонРабочийЗначениеJSON = ЗаявительТелефонЗначениеJSON;
	ДанныеЗаявителя.EMail = ЗаявительEMailПредставление;
	ДанныеЗаявителя.EMailЗначениеJSON = ЗаявительEMailЗначениеJSON;
	ДанныеЗаявителя.Гражданство = УправлениеКонтактнойИнформацией.СтранаМираПоКодуИлиНаименованию(Гражданство);
	
	Возврат ДанныеЗаявителя;
	
КонецФункции

&НаСервере
Функция ДанныеРуководителя(НовыйРуководитель = Ложь)
	
	Если НовыйРуководитель Тогда
		Возврат ДанныеЗаявителя();
	КонецЕсли;
	
	ДанныеРуководителя = Обработки.РегистрацияОрганизации.НовыеДанныеРуководителя();
	
	Если ЗначениеЗаполнено(ОтветственныеЛица.ФизическоеЛицо) Тогда
		ЗаполнитьЗначенияСвойств(ДанныеРуководителя,
			ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ОтветственныеЛица.ФизическоеЛицо, "Ссылка, Фамилия, Имя, Отчество, ИНН, Пол"));
	Иначе
		ДанныеРуководителя.Фамилия  = РуководительФамилия;
		ДанныеРуководителя.Имя      = РуководительИмя;
		ДанныеРуководителя.Отчество = РуководительОтчество;
		ДанныеРуководителя.ИНН      = РуководительИНН;
		ОрганизацииФормыКлиентСервер.ОпределитьПолПоОтчеству(ДанныеРуководителя.Пол, РуководительОтчество);
	КонецЕсли;
	
	Возврат ДанныеРуководителя;
	
КонецФункции

&НаСервере
Функция ДанныеОДолжностиРуководителя(НовыйРуководитель = Ложь)
	
	ИмяМенеджера = ?(НовыйРуководитель, "ОтветственныеЛицаНовые", "ОтветственныеЛица");
	
	ДанныеОДолжностиРуководителя = Обработки.РегистрацияОрганизации.НовыеДанныеДолжности();
	ДанныеОДолжностиРуководителя.ДатаНазначения = ЭтотОбъект[ИмяМенеджера].Период;
	ДанныеОДолжностиРуководителя.Должность      = ЭтотОбъект[ИмяМенеджера].Должность;
	ДанныеОДолжностиРуководителя.СрокПолномочий = ЭтотОбъект[ИмяМенеджера].СрокПолномочий;
	
	Если Не ЗначениеЗаполнено(ДанныеОДолжностиРуководителя.ДатаНазначения) Тогда
		Если НовыйРуководитель Тогда
			ДанныеОДолжностиРуководителя.ДатаНазначения = ДатаРешения;
		Иначе
			Если ЗначениеЗаполнено(Объект.ДатаРегистрации) Тогда
				ДанныеОДолжностиРуководителя.ДатаНазначения = Объект.ДатаРегистрации;
			Иначе
				ДанныеОДолжностиРуководителя.ДатаНазначения = Дата(1899, 12, 31);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеОДолжностиРуководителя.Должность) Тогда
		ДанныеОДолжностиРуководителя.Должность = РуководительДолжность;
	КонецЕсли;
	
	Возврат ДанныеОДолжностиРуководителя;
	
КонецФункции

&НаСервере
Функция ДанныеРегистрацииВНалоговомОргане()
	
	ДанныеРегистрацииВНалоговомОргане = Обработки.РегистрацияОрганизации.НовыеДанныеРегистрацииВНалоговомОргане();
	
	Если ЭтоЮрЛицо(ЭтотОбъект) Тогда
		ЗначениеJSONАдреса = ЮридическийАдресЗначениеJSON;
	Иначе
		ЗначениеJSONАдреса = ЗаявительАдресПропискиЗначениеJSON;
	КонецЕсли;
	
	СведенияОНалоговомОргане = СведенияОНалоговомОрганеПоАдресу(ЗначениеJSONАдреса);
	Если СведенияОНалоговомОргане <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДанныеРегистрацииВНалоговомОргане, СведенияОНалоговомОргане);
	ИначеЕсли ЗначениеЗаполнено(Объект.РегистрацияВНалоговомОргане) Тогда
		ДанныеРегистрацииВНалоговомОргане.КодНалоговогоОргана = Объект.КодНалоговогоОргана;
		ДанныеРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.РегистрацияВНалоговомОргане,
			"КодПоОКАТО, КодПоОКТМО");
		ЗаполнитьЗначенияСвойств(ДанныеРегистрацииВНалоговомОргане, ДанныеРегистрации);
	Иначе
		ДанныеРегистрацииВНалоговомОргане.КодНалоговогоОргана = Объект.КодНалоговогоОргана;
	КонецЕсли;
	
	Возврат ДанныеРегистрацииВНалоговомОргане;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЮрЛицо(Форма)
	
	Возврат (Форма.Объект.ЮридическоеФизическоеЛицо <> ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо"));
	
КонецФункции

&НаСервереБезКонтекста
Функция ДополнительныеКодыОКВЭД2(ТаблицаВидовДеятельности, ОсновнойКод)
	
	ДополнительныеКоды = Новый Массив;
	Для Каждого ВидДеятельности Из ТаблицаВидовДеятельности Цикл
		Если ВидДеятельности.ЭтоОКВЭД2 И ВидДеятельности.Код <> ОсновнойКод Тогда
			ДополнительныеКоды.Добавить(ВидДеятельности.Код);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(ДополнительныеКоды, ";");
	
КонецФункции

#КонецОбласти

#Область СписокИзменений

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьСведенияОРуководителе(Форма)
	
	Возврат Не ПустаяСтрока(Форма.РуководительФамилия)
		Или Не ПустаяСтрока(Форма.РуководительИмя)
		Или Не ПустаяСтрока(Форма.РуководительОтчество);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененияВносятсяВУчредительныеДокументы(ЭтоЮрЛицо, Изменено, АдресВУставеПолный, КодыОКВЭДпротиворечатУставу, ВидУстава)
	
	Если ЭтоЮрЛицо Тогда
		ИзмененияВносятсяВУчредительныеДокументы = РегистрацияОрганизацииКлиентСервер.ИзмененияВносятсяВУчредительныеДокументы(
			Изменено,
			АдресВУставеПолный,
			КодыОКВЭДпротиворечатУставу,
			Изменено.Устав И Не ВыбранТиповойУстав(ВидУстава));
	Иначе
		ИзмененияВносятсяВУчредительныеДокументы = Ложь;
	КонецЕсли;
	
	Возврат ИзмененияВносятсяВУчредительныеДокументы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененияТребуютУведомления(ЭтоЮрЛицо, Изменено, ЮрАдресИПропискаГлавногоСовпадают)
	
	Если ЭтоЮрЛицо Тогда
		ИзмененияТребуютУведомления = РегистрацияОрганизацииКлиентСервер.ИзмененияТребуютУведомления(Изменено, ЮрАдресИПропискаГлавногоСовпадают);
	Иначе
		ИзмененияТребуютУведомления = Ложь;
	КонецЕсли;
	
	Возврат ИзмененияТребуютУведомления;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураИзменений(Форма)
	
	Изменено = РегистрацияОрганизацииКлиентСервер.НоваяСтруктураИзменений();
	Изменено.СокращенноеНаименование = ИзмененоСокращенноеНаименование(Форма);
	Изменено.ПолноеНаименование      = ИзмененоПолноеНаименование(Форма);
	Изменено.ЮридическийАдрес        = ИзмененЮридическийАдрес(Форма);
	Изменено.Местоположение          = ИзмененоМестоположение(Форма);
	Изменено.Руководитель            = ИзмененРуководитель(Форма);
	Изменено.Учредители              = ИзмененыУчредители(Форма);
	Изменено.УставныйКапитал         = ИзмененУставныйКапитал(Форма);
	Изменено.ОсновнойВидДеятельности = ИзмененОсновнойВидДеятельности(Форма);
	Изменено.ВидыДеятельности        = Изменено.ОсновнойВидДеятельности Или ИзмененыВидыДеятельности(Форма);
	Изменено.Устав                   = Форма.ИзменитьВидУстава;
	
	Возврат Изменено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененоСокращенноеНаименование(Форма)
	
	Возврат Не ПустаяСтрока(Форма.НаименованиеСокращенноеНовое);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененоПолноеНаименование(Форма)
	
	Возврат Не ПустаяСтрока(Форма.НаименованиеПолноеНовое);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененЮридическийАдрес(Форма)
	
	Возврат Не ПустаяСтрока(Форма.ЮридическийАдресНовый);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СпрашиватьКакУказанАдресВУставе(Форма)
	
	Если Не ЭтоЮрЛицо(Форма) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Изменено = СтруктураИзменений(Форма);
	
	Возврат Изменено.ЮридическийАдрес;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СпрашиватьКакУказаныВидыДеятельностиВУставе(Форма)
	
	Если Не ЭтоЮрЛицо(Форма) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Изменено = СтруктураИзменений(Форма);
	
	Возврат Изменено.ВидыДеятельности
		И Не РегистрацияОрганизацииКлиентСервер.ИзмененияВносятсяВУчредительныеДокументы(Изменено);
	
КонецФункции

// Преобразует числовое значение переключателя в булево
//
&НаКлиентеНаСервереБезКонтекста
Функция АдресВУставеПолный(АдресВУставе)
	
	Если АдресВУставе = 2 Тогда
		Возврат Истина;
	ИначеЕсли АдресВУставе = 1 Тогда
		Возврат Ложь;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Преобразует числовое значение переключателя в булево
//
&НаКлиентеНаСервереБезКонтекста
Функция КодыОКВЭДпротиворечатУставу(ВидыДеятельностиВУставе)
	
	Если ВидыДеятельностиВУставе = 2 Тогда
		Возврат Истина;
	ИначеЕсли ВидыДеятельностиВУставе = 1 Тогда
		Возврат Ложь;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененоМестоположение(Форма)
	
	Если Не ИзмененЮридическийАдрес(Форма) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Не РегистрацияОрганизацииВызовСервера.СравнитьНаселенныеПункты(
		Форма.ЮридическийАдресЗначениеJSON, Форма.ЮридическийАдресНовыйЗначениеJSON);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЮрАдресИПропискаГлавногоСовпадают(Знач АдресРуководителяЗначениеJSON, Знач ЮридическийАдресНовыйЗначениеJSON, Знач Учредители)
	
	ЮрАдресИПропискаРуководителяСовпадают =
		УправлениеКонтактнойИнформациейБП.СравнитьАдреса(АдресРуководителяЗначениеJSON, ЮридическийАдресНовыйЗначениеJSON);
	
	Если ЮрАдресИПропискаРуководителяСовпадают Тогда
		// Новый адрес является адресом места жительства лица, наделенного правом действовать от имени общества без доверенности.
		Возврат Истина;
	КонецЕсли;
	
	ЮрАдресИПропискаГлавногоУчредителяСовпадают = Ложь;
	Для каждого Учредитель Из Учредители Цикл
		Если Учредитель.ДоляУчастия >= 50 И ТипЗнч(Учредитель.Ссылка) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			АдресПоПропискеУчредителя = АдресПоПропискеУчредителя(Учредитель.Ссылка);
			Если АдресПоПропискеУчредителя <> Неопределено И Не ПустаяСтрока(АдресПоПропискеУчредителя.ЗначениеJSON) Тогда
				ЮрАдресИПропискаГлавногоУчредителяСовпадают = УправлениеКонтактнойИнформациейБП.СравнитьАдреса(
					АдресПоПропискеУчредителя.ЗначениеJSON, ЮридическийАдресНовыйЗначениеJSON);
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ЮрАдресИПропискаГлавногоУчредителяСовпадают Тогда
		// Новый адрес является адресом места жительства одного из участников ООО,
		// владеющего не менее 50% голосов от общего количества голосов всех учредителей.
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаСервереБезКонтекста
Функция АдресПоПропискеУчредителя(Учредитель)
	
	Если Не ЗначениеЗаполнено(Учредитель) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВидКонтактнойИнформации =
		ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица");
	
	КонтактнаяИнформацияУчредителя = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
		Учредитель, ВидКонтактнойИнформации, , Ложь);
	
	Возврат ЗначениеJSONКонтактнойИнформации(КонтактнаяИнформацияУчредителя, ВидКонтактнойИнформации);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененРуководитель(Форма)
	
	Возврат ЗначениеЗаполнено(Форма.ОтветственныеЛицаНовые.ФизическоеЛицо);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененУставныйКапитал(Форма)
	
	Возврат Форма.УставныйКапиталНовый <> 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененыУчредители(Форма)
	
	Возврат Форма.УчредителиНовые.Количество() <> 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененОсновнойВидДеятельности(Форма)
	
	Возврат Не ПустаяСтрока(Форма.КодОКВЭД2Новый) И Форма.КодОКВЭД2Новый <> Форма.КодОКВЭД2;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИзмененыВидыДеятельности(Форма)
	
	Возврат Форма.ВидыДеятельностиНовые.Количество() <> 0;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьИзменениеНаименования(РезультатВыбора)
	
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если РезультатВыбора.Свойство("НаименованиеСокращенное") Тогда
		Если ВРег(НаименованиеСокращенное) <> ВРег(РезультатВыбора.НаименованиеСокращенное) Тогда
			НаименованиеСокращенноеНовое = РезультатВыбора.НаименованиеСокращенное;
		Иначе
			НаименованиеСокращенноеНовое = "";
		КонецЕсли;
	КонецЕсли;
	
	Если РезультатВыбора.Свойство("НаименованиеПолное") Тогда
		Если ВРег(НаименованиеПолное) <> ВРег(РезультатВыбора.НаименованиеПолное) Тогда
			НаименованиеПолноеНовое = РезультатВыбора.НаименованиеПолное;
		Иначе
			НаименованиеПолноеНовое = "";
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗакрытияФормыИзмененияЮридическогоАдреса(Знач РезультатЗакрытия, Знач ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЮридическийАдресНовыйЗначениеJSON = РезультатЗакрытия.Значение;
	ЮридическийАдресНовый = РезультатЗакрытия.Представление;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеРуководителя(РезультатВыбора)
	
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаполнитьЗначенияСвойств(ОтветственныеЛицаНовые, РезультатВыбора);
	
	ПрочитатьЗаявителя();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеУчредителей(РезультатВыбора)
	
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если УставныйКапитал <> РезультатВыбора.УставныйКапитал Тогда
		УставныйКапиталНовый = РезультатВыбора.УставныйКапитал;
	Иначе
		УставныйКапиталНовый = 0;
	КонецЕсли;
	
	ЗагрузитьТаблицуИзВременногоХранилища("УчредителиНовые", РезультатВыбора.АдресХранилища);
	ОтобразитьУчредителей();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеВидовДеятельности(РезультатВыбора)
	
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если СокрЛП(РезультатВыбора.ОсновнойКод) <> СокрЛП(КодОКВЭД2) Тогда
		КодОКВЭД2Новый = РезультатВыбора.ОсновнойКод;
		НаименованиеОКВЭД2Новое = РезультатВыбора.ОсновнойКодНаименование;
	Иначе
		КодОКВЭД2Новый = "";
		НаименованиеОКВЭД2Новое = "";
	КонецЕсли;
	
	ЗагрузитьТаблицуИзВременногоХранилища("ВидыДеятельностиНовые", РезультатВыбора.АдресХранилища);
	ОтобразитьВидыДеятельности();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

// Заполняет наименования в коллекции ВидыДеятельности из ОКВЭД
//
&НаСервере
Процедура ЗаполнитьНаименованияВидовДеятельности()
	
	Обработки.РегистрацияОрганизации.ЗаполнитьНаименованияВидовДеятельности(ВидыДеятельности);
	
КонецПроцедуры

&НаСервере
Функция СохранитьТаблицуВоВременноеХранилище(ИмяТаблицы)
	
	Возврат ПоместитьВоВременноеХранилище(ЭтотОбъект[ИмяТаблицы].Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ЗагрузитьТаблицуИзВременногоХранилища(ИмяТаблицы, АдресТаблицы)
	
	ЭтотОбъект[ИмяТаблицы].Загрузить(ПолучитьИзВременногоХранилища(АдресТаблицы));
	
КонецПроцедуры

&НаКлиенте
Функция ИмяФормыИзмененияНаименования()
	
	Возврат "Обработка.РегистрацияОрганизации.Форма.ИзменениеНаименования";
	
КонецФункции

&НаКлиенте
Функция ИмяФормыИзмененияРуководителя()
	
	Возврат "Обработка.РегистрацияОрганизации.Форма.ФормаФизическогоЛица";
	
КонецФункции

&НаКлиенте
Функция ИмяФормыИзмененияУчредителей()
	
	Возврат "Обработка.РегистрацияОрганизации.Форма.ИзменениеУчредителей";
	
КонецФункции

&НаКлиенте
Функция ИмяФормыИзмененияВидовДеятельности()
	
	Возврат "Обработка.РегистрацияОрганизации.Форма.ИзменениеВидовДеятельности";
	
КонецФункции

&НаСервере
Процедура СброситьИзмененияРеквизитов()
	
	// Данные закладки "Изменения"
	
	НаименованиеСокращенноеНовое = "";
	НаименованиеПолноеНовое = "";
	
	НаименованиеСокращенное = "";
	НаименованиеПолное = "";
	
	ЮридическийАдресНовыйЗначениеJSON = "";
	ЮридическийАдресНовый = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(ЮридическийАдресНовыйЗначениеJSON);
	АдресВУставе = 0;
	
	ЗначениеВРеквизитФормы(РегистрыСведений.ОтветственныеЛицаОрганизаций.СоздатьМенеджерЗаписи(), "ОтветственныеЛицаНовые");
	
	УставныйКапиталНовый = 0;
	УчредителиНовые.Очистить();
	
	КодОКВЭД2 = "";
	КодОКВЭД2Новый = "";
	ВидыДеятельностиНовые.Очистить();
	ВидыДеятельностиВУставе = 0;
	
	// Данные закладки "Заявление"
	
	ДатаРешения = Обработки.РегистрацияОрганизации.ДатаСоставленияЗаявления();
	ИзменитьВидУстава = Ложь;
	ФорматОтправкиДокументов = РегистрацияОрганизацииСервер.ФорматПодачиДокументовПоУмолчанию();
	
	ЗначениеВРеквизитФормы(Справочники.ФизическиеЛица.СоздатьЭлемент(), "Заявитель");
	
КонецПроцедуры

#КонецОбласти

#Область Устав

&НаСервере
Процедура НомерТиповогоУставаПриИзмененииНаСервере()
	
	ВидДокументаУстав = РегистрацияОрганизацииКлиентСервер.ВидыПрикрепленныхДокументов().Устав;
	РегистрацияОрганизацииКлиентСервер.ОчиститьПрикрепленныйДокумент(
		ЭтотОбъект,
		ВидДокументаУстав, ,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект));
	
	УстановитьПараметрыТиповогоУстава(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыТиповогоУстава(Форма)
	
	Если ВыбранТиповойУстав(Форма.ВидУстава) Тогда
		
		ДанныеУстава = ПолучитьДанныеТиповогоУстава(РегистрацияОрганизацииКлиентСервер.НовыйДанныеТиповогоУстава(),
			Форма.НомерТиповогоУстава,
			Форма.АдресТиповыеУставы);
		
		ЗаполнитьЗначенияСвойств(Форма, ДанныеУстава);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеТиповогоУстава(ДанныеУстава, НомерУстава, Адрес)
	
	ТиповыеУставы = ПолучитьИзВременногоХранилища(Адрес);
	
	НайденнаяСтрока = ТиповыеУставы.Найти(НомерУстава, "НомерУстава");
	Если НайденнаяСтрока <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДанныеУстава, НайденнаяСтрока);
	КонецЕсли;
	
	Возврат ДанныеУстава;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииПараметровУстава()
	
	НомерТиповогоУстава = НомерТиповогоУстава();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция НомерТиповогоУстава()
	
	ДанныеТиповогоУстава = РегистрацияОрганизацииКлиентСервер.НовыйДанныеТиповогоУстава();
	ЗаполнитьЗначенияСвойств(ДанныеТиповогоУстава, ЭтотОбъект);
	
	Возврат ОпределитьНомерТиповогоУстава(АдресТиповыеУставы, ДанныеТиповогоУстава);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОпределитьНомерТиповогоУстава(Адрес, ДанныеУстава)
	
	НомерУстава = 0;
	Если Не ЭтоАдресВременногоХранилища(Адрес) Тогда
		Возврат НомерУстава;
	КонецЕсли;
	
	ТиповыеУставы = ПолучитьИзВременногоХранилища(Адрес);
	Если ТипЗнч(ТиповыеУставы) = Тип("ТаблицаЗначений") Тогда
		НайденныеСтроки = ТиповыеУставы.НайтиСтроки(ДанныеУстава);
		НомерУстава = ?(НайденныеСтроки.Количество() > 0, НайденныеСтроки[0].НомерУстава, НомерУстава);
	КонецЕсли;
	
	Возврат НомерУстава;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеТиповойУстав()
	
	Возврат "Типовой";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВыбранТиповойУстав(ВидУстава)
	
	Возврат ВидУстава = ЗначениеТиповойУстав();
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокНомеровТиповыхУставов(АдресХранилища)
	
	ТиповыеУставы = ПолучитьИзВременногоХранилища(АдресХранилища);
	Возврат ТиповыеУставы.ВыгрузитьКолонку("НомерУстава");
	
КонецФункции

#КонецОбласти

#Область Заявление

&НаСервереБезКонтекста
Функция НайтиФизическоеЛицо(Знач Фамилия, Знач Имя, Знач Отчество = "", Знач ИНН = "")
	
	Возврат Справочники.Организации.ФизическоеЛицо(Фамилия, Имя, Отчество, ИНН);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СообщениеОбОшибкеИНН(Знач ИНН)
	
	ТекстСообщения = "";
	Если НЕ ПустаяСтрока(ИНН) Тогда
		ЦифрыИНН = ОрганизацииФормыКлиентСервер.ТолькоЦифры(ИНН);
		РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ЦифрыИНН, Ложь, ТекстСообщения);
	КонецЕсли;
	Возврат ТекстСообщения;

КонецФункции

&НаСервере
Процедура ПроверитьЗаполнениеСпискаИзменений(Отказ)
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(ЭтотОбъект);
	
	Если ЭтоЮрЛицо И Учредители.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'В реестре не найдены сведения об учредителях организации'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПолеИсточникИНН(ЭтоНачалоРаботы), , Отказ);
	КонецЕсли;
	
	Изменено = СтруктураИзменений(ЭтотОбъект);
	
	ЕстьИзменения = Ложь;
	Для Каждого КлючИЗначение Из Изменено Цикл
		Если КлючИЗначение.Значение Тогда
			ЕстьИзменения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьИзменения И Не ОтображатьШагУстав Тогда
		
		Если ЭтоЮрЛицо Тогда
			ТекстСообщения = НСтр("ru = 'Укажите изменения в сведениях о юридическом лице, содержащихся в ЕГРЮЛ'");
			ПолеФормы = "ИзменитьНаименование";
		Иначе
			ТекстСообщения = НСтр("ru = 'Укажите изменения в сведениях об индивидуальном предпринимателе, содержащихся в ЕГРИП'");
			ПолеФормы = "ИзменитьВидыДеятельности";
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПолеФормы, , Отказ);
		
	КонецЕсли;
	
	ДопустимоВНаименованииКромеКириллицы = "!""№()+-., 0123456789";
	
	Если Изменено.СокращенноеНаименование Тогда
		Префикс = НСтр("ru='ООО'");
		Если Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(НаименованиеСокращенноеНовое, Истина, ДопустимоВНаименованииКромеКириллицы) Тогда
			ТекстСообщения = НСтр("ru = 'Сокращенное наименование заполняется на русском языке'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ИзменитьНаименование", , Отказ);
		ИначеЕсли СтрНайти(НаименованиеСокращенноеНовое, Префикс) <> 1 Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Сокращенное наименование должно начинаться с ""%1""'"), Префикс);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ИзменитьНаименование", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Изменено.ПолноеНаименование Тогда
		Префикс = НСтр("ru = 'Общество с ограниченной ответственностью'");
		Если Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(НаименованиеПолноеНовое, Истина, ДопустимоВНаименованииКромеКириллицы) Тогда
			ТекстСообщения = НСтр("ru = 'Полное наименование заполняется на русском языке'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ИзменитьНаименование", , Отказ);
		ИначеЕсли СтрНайти(НаименованиеПолноеНовое, Префикс) <> 1 Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Полное наименование должно начинаться со слов ""%1""'"), Префикс);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ИзменитьНаименование", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресВУставе) И СпрашиватьКакУказанАдресВУставе(ЭтотОбъект) Тогда
		ТекстСообщения = НСтр("ru = 'Уточните как указан адрес общества в уставе'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "АдресВУставе", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидыДеятельностиВУставе) И СпрашиватьКакУказаныВидыДеятельностиВУставе(ЭтотОбъект) Тогда
		ТекстСообщения = НСтр("ru = 'Уточните нужно ли вносить изменения в устав'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ВидыДеятельностиВУставе", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеУстава(Отказ)
	
	Изменено = СтруктураИзменений(ЭтотОбъект);
	
	ЕстьИзменения = Ложь;
	Для Каждого КлючИЗначение Из Изменено Цикл
		Если КлючИЗначение.Значение Тогда
			ЕстьИзменения = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьИзменения И Не ИзменитьВидУстава Тогда
		
		ТекстСообщения = НСтр("ru = 'Укажите изменения в сведениях об юридическом лице, содержащихся в ЕГРЮЛ'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ИзменитьВидУстава", , Отказ);
		
	КонецЕсли;
	
	Если ИзменитьВидУстава И ВыбранТиповойУстав(ВидУстава) И Не ЗначениеЗаполнено(НомерТиповогоУстава) Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле",
			"Заполнение",
			НСтр("ru = 'Номер типового устава'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "НомерТиповогоУстава", , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеЗаявления(Отказ)
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(ЭтотОбъект);
	
	// Дата решения
	Если ЭтоЮрЛицо Тогда
		Если Не ЗначениеЗаполнено(ДатаРешения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Заполнение", НСтр("ru = 'Дата решения'"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ДатаРешения", , Отказ);
		Иначе
			КорректныйПериод = ОбщегоНазначенияБПСобытия.КорректныйПериодВводаДокументов();
			Если ДатаРешения < КорректныйПериод.НачалоКорректногоПериода 
				Или ДатаРешения > КорректныйПериод.КонецКорректногоПериода Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность", НСтр("ru = 'Дата решения'"));
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ДатаРешения", , Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Должность заявителя
	// Проверяем, если руководитель не изменился, иначе проверка выполняется в форме физ. лица
	Если ЭтоЮрЛицо И Не ИзмененРуководитель(ЭтотОбъект) Тогда
		Если Не ЗначениеЗаполнено(ОтветственныеЛица.Должность) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Должность руководителя'"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ОтветственныеЛица.Должность", , Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОтветственныеЛица.СрокПолномочий) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Назначен на срок'"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ОтветственныеЛица.СрокПолномочий", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// Контактные данные руководителя проверяем только в том случае, если руководитель не изменился,
	// т.к. в этом случае проверка выполняется в форме физ. лица.
	Если Не ЭтоЮрЛицо Или Не ИзмененРуководитель(ЭтотОбъект) Тогда
		
		// Адрес заявителя
		ПредставлениеПустогоАдреса = УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки();
		Если Не ЭтоЮрЛицо Или Гражданство = КодРоссии Тогда
			Если ЗаявительАдресПропискиПредставление = ПредставлениеПустогоАдреса Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Адрес по прописке'"));
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
					, "ЗаявительАдресПропискиПредставление", , Отказ);
			КонецЕсли;
		Иначе
			Если ЗаявительАдресДляИнформированияПредставление = ПредставлениеПустогоАдреса Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Адрес места проживания'"));
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, , "ЗаявительАдресДляИнформированияПредставление", , Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если ТребуетсяИсправлениеНомераТелефонаЗаявителя(ЗаявительТелефонПредставление)
			Или ТребуетсяИсправлениеАдресаЭлектроннойПочтыЗаявителя(ЗаявительEMailПредставление) Тогда
			
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не РегистрацияОрганизацииСервер.КонтанктнаяИнформацияПодтверждена(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительТелефонПредставление")) Тогда
		
		Отказ = Истина;
	КонецЕсли;
	
	// Учредители
	Если ЭтоЮрЛицо И Не ЕдинственныйУчредительРуководитель(ЭтотОбъект) Тогда
		Для Индекс = 0 По Учредители.Количество() - 1 Цикл
			
			Учредитель = Учредители[Индекс];
			Если Учредитель.Ссылка <> Заявитель.Ссылка
				И Не Обработки.РегистрацияОрганизации.РеквизитыУчредителяЗаполнены(Учредитель.Ссылка) Тогда
				ПутьКСтроке = "Учредители[" + Формат(Индекс, "ЧГ=") + "].";
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнены обязательные данные учредителя %1'"), Учредитель.Наименование);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
					, ПутьКСтроке + "Ссылка", , Отказ);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	// Способ подачи документов
	Если Не ЗначениеЗаполнено(СпособПодачиДокументов) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран способ подачи документов'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СпособПодачиДокументов", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеПаспортныхДанныхЗаявителя(Отказ)
	
	// Дата рождения заявителя
	Если Не ЗначениеЗаполнено(Заявитель.ДатаРождения) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Дата рождения'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Заявитель.ДатаРождения", , Отказ);
	ИначеЕсли Заявитель.ДатаРождения > ТекущаяДатаСеанса() Тогда
		ТекстСообщения = НСтр("ru = 'Дата рождения должна быть раньше текущей даты'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Заявитель.ДатаРождения", , Отказ);
	КонецЕсли;
	
	// Место рождения заявителя
	Если Не ЗначениеЗаполнено(Заявитель.МестоРожденияПредставление) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Место рождения'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
			, "Заявитель.МестоРожденияПредставление", , Отказ);
	КонецЕсли;
	
	// Гражданство
	Если Не ЗначениеЗаполнено(Гражданство) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Гражданство'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Гражданство", , Отказ);
	КонецЕсли;
	
	ГражданинРФ = Гражданство = КодРоссии;
	ЕстьГражданство =
		Гражданство <> РегистрацияОрганизацииКлиентСервер.КодПустогоЗначенияВСпискеВыбораГражданства();
	
	// Серия и номер паспорта заявителя
	Если ГражданинРФ И Не ЗначениеЗаполнено(ПаспортСерияНомер) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Серия и номер паспорта'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ПаспортСерияНомер", , Отказ);
	ИначеЕсли ГражданинРФ Тогда
		ЦифрыСерииНомераПаспорта = ОрганизацииФормыКлиентСервер.ТолькоЦифры(СокрЛП(ПаспортСерияНомер));
		Если СтрДлина(ЦифрыСерииНомераПаспорта) <> 10 Тогда
			ТекстСообщения = НСтр("ru = 'Серия и номер паспорта должны состоять из 10 цифр'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ПаспортСерияНомер", , Отказ);
		КонецЕсли;
	ИначеЕсли ЕстьГражданство И Не ЗначениеЗаполнено(ИностранныйПаспортСерияНомер) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Серия и номер паспорта'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ИностранныйПаспортСерияНомер", , Отказ);
	КонецЕсли;
	
	ДопустимоКромеКириллицы = "-. 0123456789";
	
	// Кем выдан паспорта заявителя
	Если ЕстьГражданство И Не ЗначениеЗаполнено(ПаспортныеДанныеЗаявителя.КемВыдан) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Кем выдан паспорт'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
			, "ПаспортныеДанныеЗаявителя.КемВыдан", , Отказ);
	ИначеЕсли ЕстьГражданство И Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(
		ПаспортныеДанныеЗаявителя.КемВыдан, Ложь, ДопустимоКромеКириллицы) Тогда
		ТекстСообщения = НСтр("ru = 'Кем выдан паспорт заполняется на русском языке'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ,
			"ПаспортныеДанныеЗаявителя.КемВыдан", , Отказ);
	КонецЕсли;
	
	// Дата выдачи паспорта заявителя
	Если ЕстьГражданство И Не ЗначениеЗаполнено(ПаспортныеДанныеЗаявителя.ДатаВыдачи) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Дата выдачи паспорта'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
			, "ПаспортныеДанныеЗаявителя.ДатаВыдачи", , Отказ);
	ИначеЕсли ЕстьГражданство Тогда
		ПричинаОшибки = "";
		Если Не Обработки.РегистрацияОрганизации.ПаспортДействителен(
			ПаспортныеДанныеЗаявителя.ДатаВыдачи, Заявитель.ДатаРождения, ПричинаОшибки, ГражданинРФ) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Паспорт недействителен: %1'"), ПричинаОшибки);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, , "ПаспортныеДанныеЗаявителя.ДатаВыдачи", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// Код подразделения паспорта заявителя
	Если ГражданинРФ И Не ЗначениеЗаполнено(ПаспортныеДанныеЗаявителя.КодПодразделения) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Код подразделения'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
			, "ПаспортныеДанныеЗаявителя.КодПодразделения", , Отказ);
	ИначеЕсли ГражданинРФ Тогда
		ЦифрыКодаПодразделения = ОрганизацииФормыКлиентСервер.ТолькоЦифры(ПаспортныеДанныеЗаявителя.КодПодразделения);
		Если СтрДлина(ЦифрыКодаПодразделения) <> 6 Тогда
			ТекстСообщения = НСтр("ru = 'Код подразделения должен состоять из 6 цифр'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
				, "ПаспортныеДанныеЗаявителя.КодПодразделения", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// Основание для проживания в РФ
	Если Не ЕстьГражданство Тогда
		Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Вид документа'"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ВидДокумента", , Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(СерияНомерОснованияДляПроживания) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Серия и номер'"));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СерияНомерОснованияДляПроживания", , Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДанныеОснованияДляПроживанияВРФ.КемВыдан) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Кем выдан'"));
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, , "ДанныеОснованияДляПроживанияВРФ.КемВыдан", , Отказ);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДанныеОснованияДляПроживанияВРФ.ДатаВыдачи) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Дата выдачи'"));
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, , "ДанныеОснованияДляПроживанияВРФ.ДатаВыдачи", , Отказ);
		КонецЕсли;
		Если ВидДокумента = РегистрацияОрганизацииКлиентСервер.ЗначениеРазрешенияНаВременноеПроживаниеВСпискеВыбора()
			И Не ЗначениеЗаполнено(ДанныеОснованияДляПроживанияВРФ.СрокДействия) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Действует до'"));
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, , "ДанныеОснованияДляПроживанияВРФ.СрокДействия", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЕдинственныйУчредительРуководитель(Форма)
	
	Возврат (Форма.Учредители.Количество() = 1) И (Форма.Учредители[0].Ссылка = Форма.Заявитель.Ссылка);
	
КонецФункции

&НаКлиенте
Процедура ЗаявительАдресПропискиПослеВводаАдреса(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		// Отказ от выбора, данные неизменны
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЭтотОбъект[ДополнительныеПараметры.ИмяЗначения] = Результат.Значение;
	ЭтотОбъект[ДополнительныеПараметры.ИмяПредставления] = Результат.Представление;
	
	Если Не ЭтоЮрЛицо(ЭтотОбъект) Тогда
		ЗаявительАдресПропискиПриИзмененииНаСервере(ДополнительныеПараметры.ИмяЗначения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаявительАдресПропискиПриИзмененииНаСервере(ИмяЗначения)
	
	СведенияОНалоговомОргане = СведенияОНалоговомОрганеПоАдресу(ЭтотОбъект[ИмяЗначения]);
	Если СведенияОНалоговомОргане <> Неопределено Тогда
		КодНалоговогоОргана = СведенияОНалоговомОргане.КодНалоговогоОргана;
	Иначе
		КодНалоговогоОргана = Объект.КодНалоговогоОргана;
	КонецЕсли;
	
	РеквизитыРегистрирующегоОргана =
		Обработки.РегистрацияОрганизации.РеквизитыРегистрирующегоОргана(КодНалоговогоОргана, Истина);
	
	РегистрирующийОрганНаименование = РеквизитыРегистрирующегоОргана.Наименование;
	РегистрирующийОрганАдрес = РеквизитыРегистрирующегоОргана.Адрес;
	РегистрирующийОрганТелефон = РеквизитыРегистрирующегоОргана.Телефон;
	РегистрирующийОрганИнформация = РеквизитыРегистрирующегоОргана.Информация;
	
КонецПроцедуры

#КонецОбласти

#Область Документы

&НаКлиенте
Процедура ОткрытьЗаявлениеОРегистрацииИзменений(ИмяЗаявления, ПредставлениеЗаявления)
	
	Заявление = ЭтотОбъект[ИмяЗаявления];
	
	Если Не ЗначениеЗаполнено(Заявление) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = '%1 не создано'"), ПредставлениеЗаявления);
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОтправлятьЗаявлениеВXMLФормате = РегистрацияОрганизацииКлиентСервер.ОтправлятьЗаявлениеВXMLФормате(
		ДатаРешения,
		СпособПодачиДокументов,
		ФорматОтправкиДокументов);
	
	Если ОтправлятьЗаявлениеВXMLФормате Тогда
		// Открываем PDF-файл, сформированный из сервиса налоговой
		РегистрацияОрганизацииКлиент.ОткрытьПрисоединенныйКЗаявлениюФайл(
			Заявление,
			ИдентификаторPDFФайлаЗаявления(ЭтоЮрЛицо(ЭтотОбъект)),
			УникальныйИдентификатор);
	Иначе
		
		ИмяОтчета = ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР(ДатаРешения, ЭтоЮрЛицо(ЭтотОбъект));
		Если ДоступнаПечатьPDF417(ВыбраннаяФорма, ИмяОтчета) Тогда
			РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФормуУведомленияОСпецрежимахПоСсылке(
				ЭтотОбъект,
				Заявление);
		Иначе
			РеквизитыЗаявления = РеквизитыЗаявления(Заявление);
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", Заявление);
			ПараметрыФормы.Вставить("Организация", РеквизитыЗаявления.Организация);
			ФормаОтчета = ПолучитьФорму(РеквизитыЗаявления.ПолноеИмяФормы, ПараметрыФормы, ЭтотОбъект, Объект.Ссылка);
			
			РегистрацияОрганизацииКлиент.ОткрытьФормуПредварительногоПросмотраЗаявленияБезДвумерногоКода(
				ФормаОтчета,
				ИмяОтчета,
				СтрШаблон(НСтр("ru = '%1.pdf'"), ПредставлениеЗаявления));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыЗаявления(Знач Заявление)
	
	РеквизитыЗаявления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Заявление, "Организация, ИмяОтчета, ИмяФормы");
	РеквизитыЗаявления.Вставить("ПолноеИмяФормы",
		СтрШаблон("Отчет.%1.Форма.%2", РеквизитыЗаявления.ИмяОтчета, РеквизитыЗаявления.ИмяФормы));
	
	Возврат РеквизитыЗаявления;
	
КонецФункции

&НаСервереБезКонтекста
Функция СведенияОНалоговомОрганеПоАдресу(Знач Адрес)
	
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СведенияОНалоговомОрганеПоАдресу = АдресныйКлассификатор.КодыАдреса(Адрес, АдресныйКлассификаторБП.ИсточникПолученияСведенийОбАдресныхСведениях());
	
	КодНалоговогоОрганаПоАдресу = Формат(СведенияОНалоговомОрганеПоАдресу.КодИФНСЮЛ, "ЧЦ=4; ЧДЦ=; ЧВН=; ЧГ=0");
	
	Если ЗначениеЗаполнено(КодНалоговогоОрганаПоАдресу) Тогда
		
		Сведения = Новый Структура();
		Сведения.Вставить("КодНалоговогоОргана", КодНалоговогоОрганаПоАдресу);
		Сведения.Вставить("КодПоОКТМО", Формат(СведенияОНалоговомОрганеПоАдресу.ОКТМО, "ЧДЦ=; ЧГ=0"));
		Сведения.Вставить("КодПоОКАТО", Формат(СведенияОНалоговомОрганеПоАдресу.ОКАТО, "ЧДЦ=; ЧГ=0"));
		
		Возврат Сведения;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция ПодготовитьНачалоРаботы(ПредупреждениеОбОшибке)
	
	НачатьТранзакцию();
	
	Попытка
		
		Константы.НачалоРаботы.Установить(Ложь);
		Константы.ИзмененияЕГРЗавершены.Установить(Истина); // Для показа инструкции по началу работы предпринимателя в сервисе.
		
		Если ЭтоЮрЛицо(ЭтотОбъект) Тогда
			ВидОрганизации = "";
		Иначе
			ВидОрганизации = "ИндивидуальныйПредприниматель";
		КонецЕсли;
		
		ВыбранныйРежим = "УпрощеннаяДоходы"; // Всегда по умолчанию проставляется при внесении изменений из сервиса
		
		Обработки.НачалоРаботы.УстановитьПодходящийИнтерфейс(
			ВидОрганизации,
			ВыбранныйРежим,
			УчетЗарплаты.ИПИспользуетТрудНаемныхРаботников(Организация),
			ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности());
		
		ОбщегоНазначенияБПВызовСервера.УстановитьСтандартныйИнтерфейс();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Текст = НСтр("ru = 'Ошибка при начале работы:'");
		СведенияОбОшибке = ИнформацияОбОшибке();
		ПредупреждениеОбОшибке.Вставить("Текст",    Текст + Символы.ПС + ОбработкаОшибок.КраткоеПредставлениеОшибки(СведенияОбОшибке));
		ПредупреждениеОбОшибке.Вставить("Подробно", ОбработкаОшибок.ПодробноеПредставлениеОшибки(СведенияОбОшибке));
		
		УстановитьВидимостьСтраницы(ЭтотОбъект, "Документы");
		
		Возврат Ложь;
		
	КонецПопытки;
	
	УстановитьВидимостьБаннераНачалоРаботы();
	
	Обработки.РегистрацияОрганизации.ОчиститьСлужебныеДанныеПослеРегистрации();
	
	УправлениеФормой(ЭтотОбъект);
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьБаннераНачалоРаботы()
	
	Элементы.НачалоРаботы.Видимость = ЭтоНачалоРаботы;
	Элементы.Готово.Видимость = Не ЭтоНачалоРаботы;
	
	ПредставлениеПрограммы = ОбщегоНазначенияБП.ПредставлениеПрограммыПредложныйПадеж();
	Элементы.ЗаголовокНачалоРаботы.Заголовок =
		СтрШаблон(НСтр("ru = 'Попробуйте вести учет в %1'"), ПредставлениеПрограммы);
	
	ПодсказкаНачалоРаботы = НСтр("ru = 'Пока регистрируются изменения, познакомьтесь с программой.
		|В %1 можно вести учет и сдавать отчетность. 
		|Ваше приложение полностью готово к работе!'");
	Элементы.ДекорацияНачалоРаботы.Заголовок = СтрШаблон(ПодсказкаНачалоРаботы, ПредставлениеПрограммы);
	
	Команды.НачатьРаботу.Подсказка = 
		СтрШаблон(НСтр("ru = 'Закрыть помощник и начать работу в %1'"), ПредставлениеПрограммы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКнопкуПоУмолчанию(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.ТекущийЭтап = "ВнесениеИзмененийЕГР_Документы"
		И Не ЗначениеЗаполнено(Форма.ПодписанныеДокументы) Тогда
		
		Если Форма.СпособПодачиДокументов = РегистрацияОрганизацииКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронноОнлайн Тогда
			Элементы.ПодписатьИОтправить.КнопкаПоУмолчанию = Истина;
			Элементы.ПодписатьДокументы.КнопкаПоУмолчанию = Ложь;
		Иначе
			Элементы.ПодписатьИОтправить.КнопкаПоУмолчанию = Ложь;
			Элементы.ПодписатьДокументы.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		
		Элементы.НачатьРаботу.КнопкаПоУмолчанию = Ложь;
		Элементы.Готово.КнопкаПоУмолчанию = Ложь;
		
	Иначе
		Элементы.НачатьРаботу.КнопкаПоУмолчанию = Форма.ЭтоНачалоРаботы;
		Элементы.Готово.КнопкаПоУмолчанию = Не Форма.ЭтоНачалоРаботы;
		Элементы.ПодписатьДокументы.КнопкаПоУмолчанию = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрисоединенныйКЗаявлениюФайл(Идентификатор)
	
	Заявление = ВладелецПрисоединенныхФайлов();
	РегистрацияОрганизацииКлиент.ОткрытьПрисоединенныйКЗаявлениюФайл(Заявление, Идентификатор, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Функция ВладелецПрисоединенныхФайлов()
	
	ИмяОтчета = ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР(ДатаРешения, ЭтоЮрЛицо(ЭтотОбъект));
	Если ЭтоЗаявлениеР13014(ИмяОтчета) Тогда
		
		Возврат ЗаявлениеОВнесенииИзмененийЕГР;
		
	КонецЕсли;
	
	ИзмененияВносятсяВУчредительныеДокументы = ИзмененияВносятсяВУчредительныеДокументы(
		ЭтоЮрЛицо(ЭтотОбъект),
		СтруктураИзменений(ЭтотОбъект),
		АдресВУставеПолный(АдресВУставе),
		КодыОКВЭДпротиворечатУставу(ВидыДеятельностиВУставе),
		ВидУстава);
	
	Возврат ?(ИзмененияВносятсяВУчредительныеДокументы, ЗаявлениеОВнесенииИзмененийУчредительныеДокументы, ЗаявлениеОВнесенииИзмененийЕГР);
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР(Период, ЭтоЮрЛицо) Экспорт
	
	КодыФормЗаявлений = Обработки.РегистрацияОрганизации.КодыФормЗаявлений(Период);
	Если ЭтоЮрЛицо Тогда
		КодФормыЗаявления = КодыФормЗаявлений.ЗаявлениеРегистрацииИзмененийЕГРЮЛ;
	Иначе
		КодФормыЗаявления = КодыФормЗаявлений.ЗаявлениеРегистрацииИзмененийЕГРИП;
	КонецЕсли;
	
	Возврат Обработки.РегистрацияОрганизации.ИмяРегламентированногоУведомления(КодФормыЗаявления);
	
КонецФункции

#КонецОбласти

#Область КонтактнаяИнформация

&НаСервереБезКонтекста
Функция ЗначениеJSONКонтактнойИнформации(КонтактнаяИнформацияОбъекта, ВидКонтактнойИнформации)
	
	Результат = Новый Структура("ЗначениеJSON, Представление", "", "");
	
	Отбор = Новый Структура("Вид", ВидКонтактнойИнформации);
	
	КонтактнаяИнформацияПоВиду = КонтактнаяИнформацияОбъекта.Скопировать(Отбор);
	Если КонтактнаяИнформацияПоВиду.Количество() > 0 Тогда
		КонтактнаяИнформацияПоВиду.Сортировать("Дата УБЫВ");
		Результат.ЗначениеJSON = КонтактнаяИнформацияПоВиду[0].Значение;
		Результат.Представление = КонтактнаяИнформацияПоВиду[0].Представление;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция КонтактнаяИнформацияПоПредставлению(Знач Представление, Знач ВидКонтактнойИнформации)
	
	// Создаем новый экземпляр по представлению.
	Результат = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ВидКонтактнойИнформации);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВыборАдресаФизическогоЛица(Элемент, СтандартнаяОбработка, ВидКонтактнойИнформации, ИмяЗначения)
	
	СтандартнаяОбработка = Ложь;
	
	// Данные для редактирования
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформацииЗаявителя(ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформации);
	ПараметрыОткрытия.Вставить("ЗначенияПолей", ЭтотОбъект[ИмяЗначения]);
	
	ТекстПустогоАдреса = УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки();
	Если ЭтотОбъект[Элемент.Имя] <> ТекстПустогоАдреса Тогда
		ПараметрыОткрытия.Вставить("Представление", ЭтотОбъект[Элемент.Имя]);
	Иначе
		ПараметрыОткрытия.Вставить("Представление", "");
	КонецЕсли;
	
	ПараметрыВведенногоАдреса = Новый Структура;
	ПараметрыВведенногоАдреса.Вставить("ИмяПредставления", Элемент.Имя);
	ПараметрыВведенногоАдреса.Вставить("ИмяЗначения", ИмяЗначения);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗаявительАдресПропискиПослеВводаАдреса", ЭтотОбъект, ПараметрыВведенногоАдреса);
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(
		ПараметрыОткрытия, Элемент, ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ЧтениеДанныхПомощника

// Находит актуальные заявления по выбранной организации и возвращает в структуре
//
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Структура - содержит ссылки на документы и состояние их отправки
//
&НаСервереБезКонтекста
Функция НайтиАктуальныеЗаявления(Организация)
	
	Возврат Обработки.РегистрацияОрганизации.НайтиАктуальныеЗаявленияОВнесенииИзмененийЕГР(Организация);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеЗаявления(СостояниеОтправки)
	
	Если СостояниеОтправки = Неопределено Тогда
		
		Возврат ИнтерфейсыВзаимодействияБРОКлиентСервер.НовыйСостояниеДокумента();
		
	КонецЕсли;
	
	Возврат СостояниеОтправки;
	
КонецФункции

// Заполняет реквизиты формы данными, сохраненными в заявлениях
//
&НаСервере
Процедура ПрочитатьЗаявление()
	Перем Изменено;
	Перем ДанныеНовогоРуководителя, ДанныеЗаявителя;
	
	ДанныеПомощникаЗаполнения = Новый Структура();
	Если ЗначениеЗаполнено(ЗаявлениеОВнесенииИзмененийЕГР) Тогда
		ДанныеПомощникаЗаполнения = Обработки.РегистрацияОрганизации.ДанныеПомощникаЗаполнения(ЗаявлениеОВнесенииИзмененийЕГР);
	КонецЕсли;
	Если ДанныеПомощникаЗаполнения.Количество() = 0 И ЗначениеЗаполнено(ЗаявлениеОВнесенииИзмененийУчредительныеДокументы) Тогда
		ДанныеПомощникаЗаполнения = Обработки.РегистрацияОрганизации.ДанныеПомощникаЗаполнения(ЗаявлениеОВнесенииИзмененийУчредительныеДокументы);
	КонецЕсли;
	
	// Читаем из сохраненного заявления данные помощника - см. ДанныеПомощникаЗаполнения()
	
	Если Не ДанныеПомощникаЗаполнения.Свойство("Изменено", Изменено) Тогда
		Возврат;
	КонецЕсли;
	
	// Контактная информация могла быть сохранена в старом формате, приведем поля контактной информации к новому формату.
	КонвертироватьКонтактнуюИнформациюПомощникаЗаполнения(ДанныеПомощникаЗаполнения);
	
	// Изменения
	
	Если Изменено.ПолноеНаименование Или Изменено.СокращенноеНаименование Тогда
		ДанныеПомощникаЗаполнения.Свойство("НаименованиеПолноеНовое", НаименованиеПолноеНовое);
		ДанныеПомощникаЗаполнения.Свойство("НаименованиеСокращенноеНовое", НаименованиеСокращенноеНовое);
	КонецЕсли;
	
	Если Изменено.ЮридическийАдрес Или Изменено.Местоположение Тогда
		ДанныеПомощникаЗаполнения.Свойство("ЮридическийАдресНовый", ЮридическийАдресНовый);
		ДанныеПомощникаЗаполнения.Свойство("ЮридическийАдресНовыйЗначениеJSON", ЮридическийАдресНовыйЗначениеJSON);
		ДанныеПомощникаЗаполнения.Свойство("АдресВУставе", АдресВУставе);
	КонецЕсли;
	
	Если Изменено.Руководитель Тогда
		Если ДанныеПомощникаЗаполнения.Свойство("ДанныеНовогоРуководителя", ДанныеНовогоРуководителя) Тогда
			Если ОбщегоНазначения.СсылкаСуществует(ДанныеНовогоРуководителя.Ссылка) Тогда
				ОтветственныеЛицаНовые.ФизическоеЛицо = ДанныеНовогоРуководителя.Ссылка;
			КонецЕсли;
		КонецЕсли;
		Если ДанныеПомощникаЗаполнения.Свойство("ДанныеОДолжностиНовогоРуководителя") Тогда
			ДанныеОДолжностиНовогоРуководителя = ДанныеПомощникаЗаполнения.ДанныеОДолжностиНовогоРуководителя;
			ОтветственныеЛицаНовые.Период = ДанныеОДолжностиНовогоРуководителя.ДатаНазначения;
			ОтветственныеЛицаНовые.Должность = ДанныеОДолжностиНовогоРуководителя.Должность;
			ОтветственныеЛицаНовые.СрокПолномочий = ДанныеОДолжностиНовогоРуководителя.СрокПолномочий;
		КонецЕсли;
	КонецЕсли;
	
	Если Изменено.Учредители Тогда
		Если ДанныеПомощникаЗаполнения.Свойство("УчредителиНовые") Тогда
			УчредителиНовые.Загрузить(ДанныеПомощникаЗаполнения.УчредителиНовые);
		КонецЕсли;
	КонецЕсли;
	
	Если Изменено.УставныйКапитал Тогда
		ДанныеПомощникаЗаполнения.Свойство("УставныйКапиталНовый", УставныйКапиталНовый);
	КонецЕсли;
	
	Если Изменено.ОсновнойВидДеятельности Тогда
		ДанныеПомощникаЗаполнения.Свойство("КодОКВЭД2Новый", КодОКВЭД2Новый);
	КонецЕсли;
	
	Если Изменено.ВидыДеятельности Тогда
		Если ДанныеПомощникаЗаполнения.Свойство("ВидыДеятельностиНовые") Тогда
			ВидыДеятельностиНовые.Загрузить(ДанныеПомощникаЗаполнения.ВидыДеятельностиНовые);
		КонецЕсли;
		ДанныеПомощникаЗаполнения.Свойство("ВидыДеятельностиВУставе", ВидыДеятельностиВУставе);
	КонецЕсли;
	
	Если Изменено.Свойство("Устав") И Изменено.Устав Тогда
		
		НомерТиповогоУстава = ДанныеПомощникаЗаполнения.НомерТиповогоУстава;
		ИзменитьВидУстава = ДанныеПомощникаЗаполнения.ИзменитьВидУстава;
		ВидУстава = ?(ЗначениеЗаполнено(НомерТиповогоУстава), ЗначениеТиповойУстав(), "Индивидуальный");
		
	КонецЕсли;
	
	// Данные заявления
	
	ДанныеПомощникаЗаполнения.Свойство("СпособПодачиДокументов", СпособПодачиДокументов);
	Если Не ЗначениеЗаполнено(СпособПодачиДокументов) Тогда
		СпособРегистрации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеПомощникаЗаполнения, "СпособРегистрации", 1);
		СпособПодачиДокументов = СпособПодачиДокументовПоСпособуРегистрации(СпособРегистрации);
	КонецЕсли;
	
	Если ЭтоЮрЛицо(ЭтотОбъект) Тогда
		ДанныеПомощникаЗаполнения.Свойство("ДатаРешения", ДатаРешения);
	Иначе
		ДатаРешения = Обработки.РегистрацияОрганизации.ДатаСоставленияЗаявления();
	КонецЕсли;
	
	Если ДанныеПомощникаЗаполнения.Свойство("ДанныеЗаявителя", ДанныеЗаявителя) Тогда
		Если ЗначениеЗаполнено(ДанныеЗаявителя.Ссылка) И ОбщегоНазначения.СсылкаСуществует(ДанныеЗаявителя.Ссылка) Тогда
			ЗаявительОбъект = ДанныеЗаявителя.Ссылка.ПолучитьОбъект();
			ЗначениеВРеквизитФормы(ЗаявительОбъект, "Заявитель");
			ПрочитатьДанныеЗаявителя();
		Иначе
			ЗаявительОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
			ЗаявительОбъект.Заполнить(Неопределено);
			ЗаполнитьЗначенияСвойств(ЗаявительОбъект, ДанныеЗаявителя);
			ЗначениеВРеквизитФормы(ЗаявительОбъект, "Заявитель");
			Если ДанныеЗаявителя.Свойство("ПаспортныеДанные") Тогда
				ДокументыФизическихЛицМенеджер = РегистрыСведений.ДокументыФизическихЛиц.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(ДокументыФизическихЛицМенеджер, ДанныеЗаявителя.ПаспортныеДанные);
				ЗначениеВРеквизитФормы(ДокументыФизическихЛицМенеджер, "ПаспортныеДанныеЗаявителя");
				ПаспортСерияНомер = СокрЛП(СтрШаблон("%1 %2", ДанныеЗаявителя.ПаспортныеДанные.Серия, ДанныеЗаявителя.ПаспортныеДанные.Номер));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Заполним сведения о налоговых органах
	РегистрирующиеОрганы = Обработки.РегистрацияОрганизации.ОпределитьРегистрирующийОрган(ДанныеПомощникаЗаполнения);
	ЗаполнитьРеквизитыРегистрирующихОрганов(РегистрирующиеОрганы);
	
	// Если изменены учредительные документы, надо платить госпошлину.
	ИзмененияВносятсяВУчредительныеДокументы = ИзмененияВносятсяВУчредительныеДокументы(
		ЭтоЮрЛицо(ЭтотОбъект),
		Изменено,
		АдресВУставеПолный(АдресВУставе),
		КодыОКВЭДпротиворечатУставу(ВидыДеятельностиВУставе),
		ВидУстава);
	
	Если ИзмененияВносятсяВУчредительныеДокументы Тогда
		АдресРегистрации = ?(Изменено.Местоположение, ЮридическийАдресНовыйЗначениеJSON, ЮридическийАдресЗначениеJSON);
		СуммаПошлины = Обработки.РегистрацияОрганизации.СуммаПошлиныЗаВнесениеИзмененийВУчредительныеДокументы();
		КБК = Обработки.РегистрацияОрганизации.КБКНалогаПоСпособуПодачиДокументов(СпособПодачиДокументов);
		
		Если Обработки.РегистрацияОрганизации.СервисКвитанцияНаОплатуГоспошлиныДоступен() Тогда
			ПлатежныеРеквизиты = ДанныеСервисаРегистрации.ПлатежныеРеквизитыОплатаГоспошлины(
				ДанныеЗаявителя(), АдресРегистрации, КБК, СуммаПошлины);
			ЗаполнитьПлатежныеРеквизитыОплатаГоспошлины(ПлатежныеРеквизиты);
		КонецЕсли;
	КонецЕсли;
	
	ДанныеПомощникаЗаполнения.Свойство("НомерТелефонаПодтвержден", НомерТелефонаПодтвержден);
	ДанныеПомощникаЗаполнения.Свойство("ЭлектроннаяПочтаПодтверждена", ЭлектроннаяПочтаПодтверждена);
	ДанныеПомощникаЗаполнения.Свойство("ВыдатьНаБумажномНосителе", ВыдатьНаБумажномНосителе);
	ДанныеПомощникаЗаполнения.Свойство("ФорматОтправкиДокументов", ФорматОтправкиДокументов);
	ОбновитьРеквизитыВыбраннойФормы(ДатаРешения);
	
	Если ЗначениеЗаполнено(ЗаявлениеОВнесенииИзмененийЕГР) Тогда
		ЗаявлениеОВнесенииИзмененийЕГРСостояниеОтправки = ИнтерфейсыВзаимодействияБРО.СостояниеДокумента(
			ЗаявлениеОВнесенииИзмененийЕГР,
			Перечисления.ТипыКонтролирующихОрганов.РегистрацияЮЛФНС);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СпособПодачиДокументовПоСпособуРегистрации(СпособРегистрации)
	
	СпособыПодачиДокументов = РегистрацияОрганизацииКлиентСервер.СпособыПодачиДокументов();
	Если СпособРегистрации = 3 Тогда
		Возврат СпособыПодачиДокументов.Почта;
	ИначеЕсли СпособРегистрации = 2 Тогда
		Возврат СпособыПодачиДокументов.ИФНСпредставитель;
	Иначе
		Возврат СпособыПодачиДокументов.ИФНСлично;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура КонвертироватьКонтактнуюИнформациюПомощникаЗаполнения(ДанныеПомощникаЗаполнения)
	
	Обработки.РегистрацияОрганизации.КонвертироватьКонтактнуюИнформациюПомощникаРегистрацииИзменений(ДанныеПомощникаЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЗаявителя()
	
	Перем ЗаявительОбъект, НайденноеФизическоеЛицо;
	
	Если ЭтоЮрЛицо(ЭтотОбъект) Тогда
		Если ИзмененРуководитель(ЭтотОбъект) Тогда
			// Заявителем является новый руководитель организации
			ЗаявительОбъект = ОтветственныеЛицаНовые.ФизическоеЛицо.ПолучитьОбъект();
		Иначе
			Если Не ОтветственныеЛица.ФизическоеЛицо.Пустая() Тогда
				ЗаявительОбъект = ОтветственныеЛица.ФизическоеЛицо.ПолучитьОбъект();
			Иначе
				// Заявителем является руководитель организации
				НайденноеФизическоеЛицо = НайтиФизическоеЛицо(РуководительФамилия, РуководительИмя, РуководительОтчество, РуководительИНН);
				Если ЗначениеЗаполнено(НайденноеФизическоеЛицо) Тогда
					ЗаявительОбъект = НайденноеФизическоеЛицо.ПолучитьОбъект();
				Иначе
					ЗаявительОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
					ЗаявительОбъект.Заполнить(Неопределено);
					ЗаявительОбъект.Фамилия  = РуководительФамилия;
					ЗаявительОбъект.Имя      = РуководительИмя;
					ЗаявительОбъект.Отчество = РуководительОтчество;
					ЗаявительОбъект.ИНН      = РуководительИНН;
					ОрганизацииФормыКлиентСервер.ОпределитьПолПоОтчеству(ЗаявительОбъект.Пол, ЗаявительОбъект.Отчество);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		// Заявителем является индивидуальный предприниматель
		Если Не Объект.ИндивидуальныйПредприниматель.Пустая() Тогда
			ЗаявительОбъект = Объект.ИндивидуальныйПредприниматель.ПолучитьОбъект();
		Иначе
			НайденноеФизическоеЛицо = НайтиФизическоеЛицо(Объект.ФамилияИП, Объект.ИмяИП, Объект.ОтчествоИП, Объект.ИНН);
			Если ЗначениеЗаполнено(НайденноеФизическоеЛицо) Тогда
				ЗаявительОбъект = НайденноеФизическоеЛицо.ПолучитьОбъект();
			Иначе
				ЗаявительОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
				ЗаявительОбъект.Заполнить(Неопределено);
				ЗаявительОбъект.Фамилия  = Объект.ФамилияИП;
				ЗаявительОбъект.Имя      = Объект.ИмяИП;
				ЗаявительОбъект.Отчество = Объект.ОтчествоИП;
				ЗаявительОбъект.ИНН      = Объект.ИНН;
				ОрганизацииФормыКлиентСервер.ОпределитьПолПоОтчеству(ЗаявительОбъект.Пол, ЗаявительОбъект.Отчество);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ЗаявительОбъект, "Заявитель");
	ПрочитатьДанныеЗаявителя();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеЗаявителя()
	
	ИменаРеквизитов = Новый Структура;
	ИменаРеквизитов.Вставить("ФизическоеЛицо", "Заявитель");
	ИменаРеквизитов.Вставить("ПаспортныеДанные", "ПаспортныеДанныеЗаявителя");
	ИменаРеквизитов.Вставить("СерияНомерПаспорта", "ПаспортСерияНомер");
	ИменаРеквизитов.Вставить("ИностранныйПаспортСерияНомер", "ИностранныйПаспортСерияНомер");
	ИменаРеквизитов.Вставить("ДанныеОснованияДляПроживанияВРФ", "ДанныеОснованияДляПроживанияВРФ");
	ИменаРеквизитов.Вставить("СерияНомерОснованияДляПроживания", "СерияНомерОснованияДляПроживания");
	ИменаРеквизитов.Вставить("ВидОснованияДляПроживанияВРФ", "ВидДокумента");
	ИменаРеквизитов.Вставить("Гражданство", "Гражданство");
	
	ОрганизацииФормы.ПрочитатьДанныеФизическогоЛица(ЭтотОбъект, Заявитель.Ссылка, ИменаРеквизитов);
	
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформацииЗаявителя(ЭтотОбъект);
	
	КонтактнаяИнформацияОбъекта = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
		ПараметрыКонтактнойИнформации.Владелец, , , Ложь);
	
	// Адрес по прописке
	АдресПрописки = ЗначениеJSONКонтактнойИнформации(
		КонтактнаяИнформацияОбъекта, ПараметрыКонтактнойИнформации.АдресПрописки);
	ЗаявительАдресПропискиЗначениеJSON = АдресПрописки.ЗначениеJSON;
	ЗаявительАдресПропискиПредставление = АдресПрописки.Представление;
	
	Если ПустаяСтрока(ЗаявительАдресПропискиПредставление) Тогда
		ЗаявительАдресПропискиЗначениеJSON = "";
		ЗаявительАдресПропискиПредставление =
			УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки();
	КонецЕсли;
	
	// Адрес для информирования
	АдресДляИнформирования = ЗначениеJSONКонтактнойИнформации(
		КонтактнаяИнформацияОбъекта, ПараметрыКонтактнойИнформации.АдресДляИнформирования);
	ЗаявительАдресДляИнформированияЗначениеJSON = АдресДляИнформирования.ЗначениеJSON;
	ЗаявительАдресДляИнформированияПредставление = АдресДляИнформирования.Представление;
	
	Если ПустаяСтрока(ЗаявительАдресДляИнформированияПредставление) Тогда
		ЗаявительАдресДляИнформированияЗначениеJSON = "";
		ЗаявительАдресДляИнформированияПредставление =
			УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки();
	КонецЕсли;
	
	// Телефон
	ТелефонЗаявителя = ЗначениеJSONКонтактнойИнформации(КонтактнаяИнформацияОбъекта, ПараметрыКонтактнойИнформации.Телефон);
	ЗаявительТелефонЗначениеJSON = ТелефонЗаявителя.ЗначениеJSON;
	Если ЗначениеЗаполнено(ЗаявительТелефонПредставление)
		И (ЗаявительТелефонПредставление <> ТелефонЗаявителя.Представление) Тогда
		
		НомерТелефонаПодтвержден = Ложь;
	КонецЕсли;
	ЗаявительТелефонПредставление = ТелефонЗаявителя.Представление;
	
	// E-mail
	EMailЗаявителя = ЗначениеJSONКонтактнойИнформации(КонтактнаяИнформацияОбъекта, ПараметрыКонтактнойИнформации.Email);
	ЗаявительEMailЗначениеJSON = EMailЗаявителя.ЗначениеJSON;
	Если ЗначениеЗаполнено(ЗаявительEMailПредставление)
		И (ЗаявительEMailПредставление <> EMailЗаявителя.Представление) Тогда
		
		ЭлектроннаяПочтаПодтверждена = Ложь;
	КонецЕсли;
	ЗаявительEMailПредставление = EMailЗаявителя.Представление;
	
	// Место рождения
	Если ПустаяСтрока(Заявитель.МестоРожденияПредставление) Тогда
		Заявитель.МестоРожденияПредставление = ПерсонифицированныйУчетКлиентСервер.ПредставлениеМестаРождения(
			Заявитель.МестоРождения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПолученияКонтактнойИнформацииЗаявителя(Форма)
	
	Параметры = Новый Структура;
	Параметры.Вставить("Владелец", Неопределено);
	Параметры.Вставить("EMail", Неопределено);
	Параметры.Вставить("АдресПрописки", Неопределено);
	Параметры.Вставить("АдресДляИнформирования", Неопределено);
	Параметры.Вставить("Телефон", Неопределено);
	
	Если Не ЭтоЮрЛицо(Форма) Тогда
		Параметры.Владелец = Форма.Объект.Ссылка;
		Параметры.Email =
			ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.EmailОрганизации");
		Параметры.АдресПрописки =
			ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации");
		Параметры.Телефон =
			ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации");
	Иначе
		Параметры.Владелец = Форма.Заявитель.Ссылка;
		Параметры.Email = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(
			"Справочник.ВидыКонтактнойИнформации.EMailФизическиеЛица");
		Параметры.АдресПрописки = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(
			"Справочник.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица");
		Параметры.АдресДляИнформирования = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(
			"Справочник.ВидыКонтактнойИнформации.АдресДляИнформированияФизическиеЛица");
		Параметры.Телефон = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент(
			"Справочник.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица");
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

&НаСервере
Процедура ПрочитатьОтветственныеЛица()
	
	Если Не ЭтоЮрЛицо(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписиОтветственныхЛиц = РегистрыСведений.ОтветственныеЛицаОрганизаций.СоздатьМенеджерЗаписи();
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОтборОтветственныхЛиц = Новый Структура;
		ОтборОтветственныхЛиц.Вставить("СтруктурнаяЕдиница", Объект.Ссылка);
		ОтборОтветственныхЛиц.Вставить("ОтветственноеЛицо", Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
		СрезПоследнихОтветственных = РегистрыСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(ДатаРешения, ОтборОтветственныхЛиц);
		Если СрезПоследнихОтветственных.Количество() <> 0 Тогда
			ЗаполнитьЗначенияСвойств(МенеджерЗаписиОтветственныхЛиц, СрезПоследнихОтветственных[0]);
		Иначе
			ЗаполнитьЗначенияСвойств(МенеджерЗаписиОтветственныхЛиц, ОтборОтветственныхЛиц);
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(МенеджерЗаписиОтветственныхЛиц, "ОтветственныеЛица");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтветственныеЛица()
	
	Если Не ЭтоЮрЛицо(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОтветственныеЛица.Период) Тогда
		ОтветственныеЛица.Период = Объект.ДатаРегистрации;
	КонецЕсли;
	
	Если Не ИзмененРуководитель(ЭтотОбъект) Тогда
		Если Не ЗначениеЗаполнено(ОтветственныеЛица.ФизическоеЛицо) Тогда
			ОтветственныеЛица.ФизическоеЛицо = Заявитель.Ссылка;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ОтветственныеЛица.Должность) Тогда
			ОтветственныеЛица.Должность = РуководительДолжность;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОтветственныеЛица.СрокПолномочий) Тогда
		ОтветственныеЛица.СрокПолномочий = 5;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОбъектЗаполнен(Объект)
	
	Возврат ЗначениеЗаполнено(Объект.ИНН);
	
КонецФункции

#КонецОбласти

&НаСервереБезКонтекста
Функция ДоступнаПечатьPDF417(ВыбраннаяФорма, ИмяОтчета)
	
	Если Не ЗначениеЗаполнено(ВыбраннаяФорма) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ИмяОтчета = "РегламентированноеУведомлениеФормаР13014" Тогда
		Возврат Отчеты.РегламентированноеУведомлениеФормаР13014.ДоступнаПечатьPDF417(ВыбраннаяФорма);
	Иначе
		Возврат Отчеты.РегламентированноеУведомлениеФормаР24001.ДоступнаПечатьPDF417(ВыбраннаяФорма);
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоФорма2014(Знач ИмяФормы)
	
	Возврат ИмяФормы = "Форма2014_1";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоЗаявлениеР13014(Знач ИмяОтчета)
	
	Возврат ИмяОтчета = "РегламентированноеУведомлениеФормаР13014";
	
КонецФункции

&НаСервере
Процедура ОбновитьРеквизитыВыбраннойФормы(Период)
	
	Если Объект.Ссылка.Пустая() И ЗначениеЗаполнено(Организация) Тогда
		ЮридическоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо");
	Иначе
		ЮридическоеФизическоеЛицо = Объект.ЮридическоеФизическоеЛицо;
	КонецЕсли;
	
	ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР = ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР(
		Период,
		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	
	ОтображатьШагУстав = ЭтоЗаявлениеР13014(ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР);
	
	ВыбраннаяФорма = РегистрацияОрганизацииВызовСервера.ФормаЗаявленияПоУмолчанию(
		Период,
		ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР);
	
	Если Не ЗначениеЗаполнено(ВыбраннаяФорма) Тогда
		ВыбраннаяФорма = РегистрацияОрганизацииВызовСервера.ФормаЗаявленияПоУмолчанию(
			ТекущаяДатаСеанса(),
			ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР);
	КонецЕсли;
	
	Если Не Объект.Ссылка.Пустая() Тогда
		УстановитьФорматОтправкиДокументов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительEMailПриИзменении()
	
	ЗаявительEMailПредставление = СокрЛП(ЗаявительEMailПредставление);
	Если ПустаяСтрока(ЗаявительEMailПредставление) Тогда
		// Очистка данных, сбрасываем как представления, так и внутренние значения полей.
		ЗаявительEMailЗначениеJSON = "";
		Возврат;
	КонецЕсли;
	
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформацииЗаявителя(ЭтотОбъект);
	
	// Формируем внутренние значения полей по тексту и параметрам формирования
	ЗаявительEMailЗначениеJSON =
		КонтактнаяИнформацияПоПредставлению(ЗаявительEMailПредставление, ПараметрыКонтактнойИнформации.Email);
	
КонецПроцедуры

#КонецОбласти

#Область ПодписаниеДокументов

&НаКлиенте
Процедура ВыборОтсканированногоФайлаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	РегистрацияОрганизацииКлиент.ВыборОтсканированногоФайлаЗавершение(
		ЭтотОбъект,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект),
		РезультатВыбора,
		ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОтображатьИндивидуальныйУстав(Форма)
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(Форма);
	
	Если Не ЭтоЮрЛицо Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Изменено = СтруктураИзменений(Форма);
	
	ИзмененияВносятсяВУчредительныеДокументы = ИзмененияВносятсяВУчредительныеДокументы(
		ЭтоЮрЛицо,
		Изменено,
		АдресВУставеПолный(Форма.АдресВУставе),
		КодыОКВЭДпротиворечатУставу(Форма.ВидыДеятельностиВУставе),
		Форма.ВидУстава);
	
	Если Форма.ИзменитьВидУстава И ВыбранТиповойУстав(Форма.ВидУстава)
		Или Не Форма.ИзменитьВидУстава И ЗначениеЗаполнено(Форма.НомерТиповогоУставаПрежний) Тогда
		
		Возврат Ложь;
	ИначеЕсли Форма.ИзменитьВидУстава Тогда
		Возврат Истина;
	ИначеЕсли Изменено.ЮридическийАдрес Тогда
		// Если изменен населенный пункт или в уставе указан полный юридический адрес,
		// нужно заполнить устав
		АдресВУставеПолный = АдресВУставеПолный(Форма.АдресВУставе);
		Возврат АдресВУставеПолный <> Неопределено И АдресВУставеПолный Или Изменено.Местоположение;
	Иначе
		Возврат ИзмененияВносятсяВУчредительныеДокументы И Не Форма.ИзменитьВидУстава;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОчиститьПодписанныеДокументыИУстановитьКодРегистрирующегоОргана()
	
	УстановитьКодРегистрирующегоОргана();
	ОчиститьПодписанныеДокументыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьПодписанныеДокументыНаСервере()
	
	РегистрацияОрганизацииВызовСервера.УдалитьПрикрепленныеФайлы(
		ЗаявлениеОВнесенииИзмененийЕГР,
		РегистрацияОрганизацииКлиентСервер.ПредставлениеПодписанныеФайлы());
	
	ПодписанныеДокументы = Неопределено;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКодРегистрирующегоОргана()
	
	Если ЭтоЮрЛицо(ЭтотОбъект) Тогда
		АдресJSON = ЮридическийАдресЗначениеJSON;
	Иначе
		АдресJSON = ЗаявительАдресПропискиЗначениеJSON;
	КонецЕсли;
	
	РегистрирующийОрганКод = Обработки.РегистрацияОрганизации.КодРегистрирующегоОргана(
		АдресJSON,
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИНН"),
		ЭтоЮрЛицо(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьФормированиеПакетаДокументовПоЕГРЮЛ(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ИдентификаторЗаданияФормированияПакетаДокументов = Неопределено;
	Иначе
		ИдентификаторЗаданияФормированияПакетаДокументов = ДлительнаяОперация.Статус;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокументыЗавершение(РезультатПодписания, ДополнительныеПараметры) Экспорт
	
	ПодписатьДокументыНаСервере(РезультатПодписания, ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(ПодписанныеДокументы)
		И СпособПодачиДокументов = РегистрацияОрганизацииКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронноОнлайн Тогда
		
		Оповестить("ОбновитьСтатусЗаявленияНаГосРегистрацию", Организация, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодписатьДокументыНаСервере(РезультатПодписания, ДополнительныеПараметры)
	
	РегистрацияОрганизацииСервер.ОбработатьРезультатПодписанияДокументов(
		ЭтотОбъект,
		РезультатПодписания,
		ПараметрыОтображенияЭлементовФормы(ЭтотОбъект),
		ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(ПодписанныеДокументы)
		И СпособПодачиДокументов = РегистрацияОрганизацииКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронно Тогда
		// При подаче заявления через личный кабинет статус устанавливается вручную
		РегистрацияОрганизацииСервер.СохранитьСтатусОтправкиУведомления(
			ЗаявлениеОВнесенииИзмененийЕГР,
			ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусПодготовленоСтрокой());
	КонецЕсли;
	
	УстановитьКнопкуПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИПодписатьДокументы()
	
	Если ИзмененРуководитель(ЭтотОбъект)
		И РегистрацияОрганизацииКлиентСервер.РешениеОНовомРуководителеЗаверяетсяУНотариуса(ДатаОтправкиОнлайн) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'При смене руководителя документы подписывает и заверяет нотариус.'"));
		Возврат;
		
	КонецЕсли;
	
	ПараметрыЗаявления = РегистрацияОрганизацииКлиент.НовыеПараметрыПодписанияЗаявления();
	ПараметрыЗаявления.EMailЗаявителя = ЗаявительEMailПредставление;
	ПараметрыЗаявления.Заявление = ЗаявлениеОВнесенииИзмененийЕГР;
	ПараметрыЗаявления.ИННЗаявителя = Заявитель.ИНН;
	
	Если ЭтоЮрЛицо(ЭтотОбъект) Тогда
		ПараметрыЗаявления.ЭтоЮрЛицо = Истина;
		ПараметрыЗаявления.ВидДокумента = "ЗаявлениеПоФормеР13014";
		ПараметрыЗаявления.ОтображатьИндивидуальныйУстав = ОтображатьИндивидуальныйУстав(ЭтотОбъект);
		ПараметрыЗаявления.Учредители = РегистрацияОрганизацииКлиентСервер.УчредителиПоТипам(ЭтотОбъект);
		ПараметрыЗаявления.НуженДокументПодтверждающийАдрес = ИзмененЮридическийАдрес(ЭтотОбъект);
		ПараметрыЗаявления.НоваяРедакцияУстава = ИзменитьВидУстава И ПараметрыЗаявления.ОтображатьИндивидуальныйУстав;
	Иначе;
		ПараметрыЗаявления.ВидДокумента = "ЗаявлениеПоФормеР24001";
	КонецЕсли;
	
	ПараметрыЗаявления.ЗаявлениеВXML = РегистрацияОрганизацииКлиентСервер.ОтправлятьЗаявлениеВXMLФормате(
		ДатаРешения,
		СпособПодачиДокументов,
		ФорматОтправкиДокументов);
	
	РегистрацияОрганизацииКлиент.ПроверитьИПодписатьДокументы(ЭтотОбъект, ПараметрыЗаявления);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеПрикрепленныхФайлов()
	
	Если Не РегистрацияОрганизацииКлиентСервер.ЭтоЭлектронныйСпособПодачиДокументов(СпособПодачиДокументов) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрацияОрганизацииСервер.УстановитьПредставлениеПрикрепленныхФайлов(ЭтотОбъект, ЗаявлениеОВнесенииИзмененийЕГР);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрирующийОрганКодПриИзмененииЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		РегистрирующийОрганКод = РегистрацияОрганизацииКлиентСервер.КодРегистрирующегоОрганаПоИмениФайла(Строка(ПодписанныеДокументы));
		Возврат;
	КонецЕсли;
	
	ОчиститьПодписанныеДокументыНаСервере();
	ПроверитьИПодписатьДокументы();
	
КонецПроцедуры

&НаСервере
Функция ДанныеОСрокеПодачиЗаявления()
	
	Результат = Новый Структура();
	Результат.Вставить("КоличествоДней", 7); // п.5 ст.5 Федеральный закон от 08.08.2001 N 129-ФЗ
	Результат.Вставить("МаксимальнаяДата");
	
	Изменено = СтруктураИзменений(ЭтотОбъект); 
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(ЭтотОбъект);
	
	Если ЭтоЮрЛицо И Изменено.Местоположение Тогда
		Результат.КоличествоДней = 3; // п.6 ст.17 Федеральный закон от 08.08.2001 N 129-ФЗ
	ИначеЕсли ЭтоЮрЛицо И (Изменено.ПолноеНаименование Или Изменено.СокращенноеНаименование) Тогда
		Результат.КоличествоДней = 5; // п.4 ст.5 Федеральный закон от 08.08.2001 N 129-ФЗ
	КонецЕсли;
	
	ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	
	Если ЗначениеЗаполнено(ПроизводственныйКалендарь) Тогда
		Результат.МаксимальнаяДата = КалендарныеГрафики.ДатаПоКалендарю(
			ПроизводственныйКалендарь,
			ДатаРешения,
			Результат.КоличествоДней,
			Ложь);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат.МаксимальнаяДата) Тогда
	// Если не задан производственный календарь, тогда рассчитываем по календарным дням
		Результат.МаксимальнаяДата = ДатаРешения + Результат.КоличествоДней * 24 * 60 * 60;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_КодПодтверждения_Телефон()
	
	Если ТребуетсяИсправлениеНомераТелефонаЗаявителя(ЗаявительТелефонПредставление) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрацияОрганизацииКлиент.ПодтвердитьНомерТелефона(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительТелефонПредставление"));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КодПодтверждения_Почта()
	
	Если ТребуетсяИсправлениеАдресаЭлектроннойПочтыЗаявителя(ЗаявительEMailПредставление) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрацияОрганизацииКлиент.ПодтвердитьАдресЭлектроннойПочты(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительEMailПолучениеДокументов"));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьКодПодтверждения_Телефон()
	
	РегистрацияОрганизацииКлиент.ПроверитьКодПодтвержденияТелефона(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительТелефонПредставление"));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьКодПодтверждения_Почта()
	
	РегистрацияОрганизацииКлиент.ПроверитьКодПодтвержденияПочты(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительEMailПолучениеДокументов"));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПовторнаяОтправка_Телефон()
	
	РегистрацияОрганизацииКлиент.ПовторнаяОтправкаКодаПодтвержденияТелефона(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПовторнаяОтправка_Почта()
	
	РегистрацияОрганизацииКлиент.ПовторнаяОтправкаКодаПодтвержденияПочты(
		ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АктивироватьПолеКодПодтвержденияТелефон()
	
	ТекущийЭлемент = Элементы.КодПодтвержденияТелефона;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АктивироватьПолеКодПодтвержденияПочта()
	
	ТекущийЭлемент = Элементы.КодПодтвержденияПочты;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеОтправкиКодаПодтверждения_Телефон(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	РегистрацияОрганизацииКлиент.ПослеОтправкиКодаПодтвержденияТелефона(
		ЭтотОбъект,
		ДлительнаяОперация,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеОтправкиКодаПодтверждения_Почта(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	РегистрацияОрганизацииКлиент.ПослеОтправкиКодаПодтвержденияПочты(
		ЭтотОбъект,
		ДлительнаяОперация,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СканИнойДокументНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РегистрацияОрганизацииКлиент.ВыборОтсканированногоФайлаНажатие(ЭтотОбъект, ЗаявлениеОВнесенииИзмененийЕГР, Элемент.Имя);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДополнительныеПараметрыКонтактнойИнформации(Форма, ИмяТекущегоЭлемента = "")
	
	Возврат РегистрацияОрганизацииКлиентСервер.ДополнительныеПараметрыКонтактнойИнформации(
		Форма.ЗаявительТелефонПредставление,
		Форма.ЗаявительEMailПредставление,
		"ЗаявительТелефонПредставление",
		"ЗаявительEMailПредставление",
		ИмяТекущегоЭлемента);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяИсправлениеАдресаЭлектроннойПочтыЗаявителя(АдресЭлектроннойПочты)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(АдресЭлектроннойПочты) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения( , , НСтр("ru = 'Email'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗаявительEMailПредставление", , Отказ);
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(АдресЭлектроннойПочты) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", НСтр("ru = 'Email'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗаявительEMailПредставление", , Отказ);
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяИсправлениеНомераТелефонаЗаявителя(НомерТелефона)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(НомерТелефона) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения( , , НСтр("ru = 'Контактный телефон'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗаявительТелефонПредставление", , Отказ);
	ИначеЕсли Не ОрганизацииФормыКлиентСервер.ТелефонСоответствуетТребованиям(НомерТелефона)
		Или Не ЗначениеЗаполнено(ЭлектроннаяПодписьВМоделиСервисаКлиентСервер.ПолучитьПредставлениеТелефона(НомерТелефона)) Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения( , "Корректность", НСтр("ru = 'Контактный телефон'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗаявительТелефонПредставление", , Отказ);
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура ТелефонЗаявителяПриИзменении(Текст)
	
	Если ПустаяСтрока(Текст) Тогда
		// Очистка данных, сбрасываем как представления, так и внутренние значения полей.
		ЗаявительТелефонПредставление = "";
		ЗаявительТелефонЗначениеJSON = "";
		Возврат;
	КонецЕсли;
	
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформацииЗаявителя(ЭтотОбъект);
	
	// Формируем внутренние значения полей по тексту и параметрам формирования
	Телефон = ОрганизацииФормыКлиентСервер.ТелефонДляЗаявления(Текст);
	ЗаявительТелефонПредставление = Телефон;
	ЗаявительТелефонЗначениеJSON = КонтактнаяИнформацияПоПредставлению(Телефон, ПараметрыКонтактнойИнформации.Телефон);
	
КонецПроцедуры

&НаСервере
Функция ДобавитьИнойДокументНаСервере(НомерИногоДокумента = "")
	
	Возврат РегистрацияОрганизацииСервер.ДобавитьИнойДокумент(ЭтотОбъект, НомерИногоДокумента);
	
КонецФункции

&НаСервере
Процедура ОчиститьПрикрепленныйИнойДокументНаСервере(НомерДокумента)
	
	РегистрацияОрганизацииСервер.ОчиститьПрикрепленныйИнойДокумент(ЭтотОбъект, НомерДокумента);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыОтображенияЭлементовФормы(Форма)
	
	ЭтоЮрЛицо = ЭтоЮрЛицо(Форма);
	Изменено = СтруктураИзменений(Форма);
	
	ЮрАдресИПропискаГлавногоСовпадают = Изменено.Местоположение
		И ЮрАдресИПропискаГлавногоСовпадают(Форма.ЗаявительАдресПропискиЗначениеJSON, Форма.ЮридическийАдресНовыйЗначениеJSON, Форма.Учредители);
	
	ИзмененияВносятсяВУчредительныеДокументы = ИзмененияВносятсяВУчредительныеДокументы(
		ЭтоЮрЛицо,
		Изменено,
		АдресВУставеПолный(Форма.АдресВУставе),
		КодыОКВЭДпротиворечатУставу(Форма.ВидыДеятельностиВУставе),
		Форма.ВидУстава);
	
	ИзмененияТребуютУведомления = ИзмененияТребуютУведомления(ЭтоЮрЛицо, Изменено, ЮрАдресИПропискаГлавногоСовпадают);
	ОбразецПротокола = Неопределено;
	
	Если ЭтоЮрЛицо Тогда
		
		УчредителиПоТипам = РегистрацияОрганизацииКлиентСервер.УчредителиПоТипам(Форма);
		КоличествоЮридическихЛиц = УчредителиПоТипам.ЮридическиеЛица.Количество();
		КоличествоФизическихЛиц  = УчредителиПоТипам.ФизическиеЛица.Количество();
		ЕдинственныйУчредительЮридическоеЛицо = (КоличествоЮридическихЛиц = 1) И (КоличествоФизическихЛиц = 0);
		ЕдинственныйУчредительФизическоеЛицо = (КоличествоЮридическихЛиц = 0) И (КоличествоФизическихЛиц = 1);
		
		Если ЕдинственныйУчредительЮридическоеЛицо Тогда
			ОбразецПротокола = УчредителиПоТипам.ЮридическиеЛица[0];
		КонецЕсли;
		
	Иначе
		ЕдинственныйУчредительФизическоеЛицо = Ложь;
		ЕдинственныйУчредительЮридическоеЛицо = Ложь;
	КонецЕсли;
	
	Результат = РегистрацияОрганизацииКлиентСервер.НовыеПараметрыУправленияФормойПриПодачеДокументовВЭлектронномВиде();
	Результат.Заявление = Форма.ЗаявлениеОВнесенииИзмененийЕГР;
	Результат.ЭтоЮрЛицо = ЭтоЮрЛицо;
	Результат.ОтображатьИндивидуальныйУстав = ОтображатьИндивидуальныйУстав(Форма);
	Результат.ИзмененияТребуютУведомления = ИзмененияТребуютУведомления;
	Результат.ЕдинственныйУчредительФизическоеЛицо = ЕдинственныйУчредительФизическоеЛицо;
	Результат.ЕдинственныйУчредительЮридическоеЛицо = ЕдинственныйУчредительЮридическоеЛицо;
	Результат.НуженДокументПодтверждающийАдрес = Изменено.ЮридическийАдрес;
	Результат.ДатаСоставления = Форма.ДатаРешения;
	
	Результат.Вставить("ИзмененияВносятсяВУчредительныеДокументы", ИзмененияВносятсяВУчредительныеДокументы);
	Результат.Вставить("Изменено", Изменено);
	Результат.Вставить("ОбразецПротокола", ОбразецПротокола);
	
	Результат.ТребуетсяСправкаОбОтсутствииСудимости = ТребуетсяСправкаОбОтсутствииСудимости(Форма);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяСправкаОбОтсутствииСудимости(Форма)
	
	Если ЭтоЮрЛицо(Форма) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПроверяемыеОКВЭД = Новый Массив;
	Для Каждого ТекущийОКВЭД Из Форма.ВидыДеятельностиНовые Цикл
		ПроверяемыеОКВЭД.Добавить(ТекущийОКВЭД.Код);
	КонецЦикла;
	
	Для Каждого ТекущийОКВЭД Из Форма.ВидыДеятельности Цикл
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеОКВЭД, ТекущийОКВЭД.Код);
	КонецЦикла;
	
	Возврат ТребуетсяСправкаОбОтсутствииСудимостиНаСервере(ПроверяемыеОКВЭД);
	
КонецФункции

&НаСервереБезКонтекста
Функция ТребуетсяСправкаОбОтсутствииСудимостиНаСервере(ПроверяемыеОКВЭД)
	
	Возврат Обработки.РегистрацияОрганизации.ТребуетсяСправкаОбОтсутствииСудимости(ПроверяемыеОКВЭД);
	
КонецФункции

&НаСервере
Процедура УстановитьФорматОтправкиДокументов()
	
	ДоступныеФорматы = РегистрацияОрганизацииСервер.ДоступныеФорматыОтправкиДокументов();
	
	ИмяОтчета = ИмяОбъектаЗаявлениеРегистрацииИзмененийЕГР(ДатаРешения, ЭтоЮрЛицо(ЭтотОбъект));
	ДоступнаПечатьPDF417 = ДоступнаПечатьPDF417(ВыбраннаяФорма, ИмяОтчета);
	
	Для Каждого ТекущаяСтрока Из ДоступныеФорматы Цикл
		
		ИмяЭлемента = СтрШаблон("ФорматОтправкиДокументов%1", ТекущаяСтрока.Порядок);
		
		Если Элементы.Найти(ИмяЭлемента) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Элементы[ИмяЭлемента].Подсказка = ТекущаяСтрока.Подсказка;
		Элементы[ИмяЭлемента].СписокВыбора.Очистить();
		Элементы[ИмяЭлемента].СписокВыбора.Добавить(ТекущаяСтрока.Тип, ТекущаяСтрока.Наименование);
		
		Если ТекущаяСтрока.Тип = "XML" Тогда
			Элементы[ИмяЭлемента].Доступность = ДоступнаПечатьPDF417;
			Если Не ДоступнаПечатьPDF417 Тогда
				Элементы[ИмяЭлемента].Подсказка = НСтр("ru = 'Пока формат выгрузки не опубликован.
					|Вы можете отправить заявление в виде отсканированного документа.'");
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ФорматОтправкиДокументов) Или Не ДоступнаПечатьPDF417 Тогда
		ФорматОтправкиДокументов = РегистрацияОрганизацииСервер.ФорматПодачиДокументовПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти