
&НаКлиенте
Перем ПараметрыОбработчикаОжидания Экспорт;

&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	ДиаграммаСравнение.ТипДиаграммы = ТипДиаграммы.ГистограммаСНакоплениемГоризонтальная;
	
	ДиаграммаПоМесяцам.ТипДиаграммы = ТипДиаграммы.График;
		
	Элементы.ГруппаНеделиWB.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Элементы.ДекорацияОтступ1.Видимость = Ложь;
	Элементы.ДекорацияОтступ2.Видимость = Ложь;
	Элементы.ДекорацияОтступ3.Видимость = Ложь;
	Элементы.ДекорацияОтступ4.Видимость = Ложь;
	
	Элементы.ГруппаНеделя2.Видимость = Ложь;
	Элементы.ГруппаНеделя3.Видимость = Ложь;
	Элементы.ГруппаНеделя4.Видимость = Ложь;
	Элементы.ГруппаНеделя5.Видимость = Ложь;
	Элементы.ГруппаНеделя6.Видимость = Ложь;
	
	Показатель = "Продажи";
	
	/////////////////////////////////
	
	Период = НачалоМесяца(ТекущаяДатаСеанса()) - 1;
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоМесяца(Период), КонецМесяца(Период), Истина);
			
	ЗаполнитьКонтрагентов();
	ПроверитьНастройкиПодключения();
	УправлениеФормой(ЭтотОбъект);
	
	Если ЕстьWB ИЛИ ЕстьOzon ИЛИ ЕстьЯндекс Тогда
		ПолучитьДанныеПродаж();
		ОбновитьДанныеДиаграммыСравнения();
		ОбновитьДанныеДиаграммыПоМесяцам();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ (ЕстьWB ИЛИ ЕстьOzon ИЛИ ЕстьЯндекс) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не найдены взаиморасчеты с ""Wildberries"", ""Ozon"" или ""Яндекс.Маркет""'"),,);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("НачалоПериода",     НачалоМесяца(Период));
	ПараметрыВыбора.Вставить("КонецПериода",      КонецМесяца(Период));
	ПараметрыВыбора.Вставить("ВидПериода",        ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодЗавершениеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериодаМесяц", 
		ПараметрыВыбора, Элементы.Период, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательПриИзменении(Элемент)
	
	ОбновитьДанныеДиаграммыПоМесяцам();
	
КонецПроцедуры

&НаКлиенте
Процедура НаписатьРазработчикам(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
	РаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДокументыWB(Команда)
	
	КонтрагентыWB = Новый СписокЗначений;
	КонтрагентыWB.ЗагрузитьЗначения(СписокКонтрагентовWB());
	
	Если КонтрагентыWB.Количество() > 1 Тогда
		ОповещенияОЗакрытии = Новый ОписаниеОповещения("ЗавершениеВыбораКонтрагента", ЭтотОбъект);
		ПоказатьВыборИзМеню(ОповещенияОЗакрытии, КонтрагентыWB, Элементы.ДокументыWB);
	Иначе
		ОткрытьСписокДокументовКонтрагента(КонтрагентWB);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыOzon(Команда)
	
	ОткрытьСписокДокументовКонтрагента(КонтрагентOzon);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыЯндекс(Команда)
	
	МассивПокупателей = КонтрагентыИСкладыЯндекс(Организация).Покупатели;
	
	Если МассивПокупателей.Количество() > 1 Тогда
		КонтрагентыЯндекс = Новый СписокЗначений;
		КонтрагентыЯндекс.ЗагрузитьЗначения(МассивПокупателей);
		ОповещенияОЗакрытии = Новый ОписаниеОповещения("ЗавершениеВыбораКонтрагента", ЭтотОбъект);
		ПоказатьВыборИзМеню(ОповещенияОЗакрытии, КонтрагентыЯндекс, Элементы.ДокументыЯндекс);
	Иначе
		ОткрытьСписокДокументовКонтрагента(КонтрагентЯндекс);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОстаткиWB(Команда)
	ОткрытьФормуОстаткиМаркетплейсов(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОстаткиOzon(Команда)
	ОткрытьФормуОстаткиМаркетплейсов(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОстаткиЯндекс(Команда)
	ОткрытьФормуОстаткиМаркетплейсов(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетыAPIWB(Команда)
	ЗагрузитьИзМаркетплейс(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетыAPIOzon(Команда)
	ЗагрузитьИзМаркетплейс(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетыAPIЯндекс(Команда)
	ЗагрузитьИзМаркетплейс(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет"));
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетыИзФайлаWB(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Маркетплейс", ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.Wildberries"));
	ОткрытьФорму("ОбщаяФорма.МаркетплейсыЗагрузкаФайлов", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетыИзФайлаOzon(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Маркетплейс", ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.OZON"));
	ОткрытьФорму("ОбщаяФорма.МаркетплейсыЗагрузкаФайлов", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОтчетыИзФайлаЯндекс(Команда)
	ЭлектронноеВзаимодействиеБПКлиент.ЗагрузитьОтчетЯндексМаркетИзФайла(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ПроверитьНастройкиПодключения();
	Если ЗначениеЗаполнено(КонтрагентWB) Тогда
		ЗаполнитьТаблицуWB();
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентOzon) Тогда
		ЗаполнитьТаблицуOzon()
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентЯндекс) Тогда
		ЗаполнитьТаблицуЯндекс();
	КонецЕсли;
	ОбновитьДанныеДиаграммыСравнения();
	ОбновитьДанныеДиаграммыПоМесяцам();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаWB.Видимость     = Форма.ЕстьWB;
	Элементы.ГруппаOzon.Видимость   = Форма.ЕстьOzon;
	Элементы.ГруппаЯндекс.Видимость = Форма.ЕстьЯндекс;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНастройкиПодключения()
	
	НастройкаWB     = Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Организация, Перечисления.ВидыМаркетплейсов.Wildberries);
	НастройкаOzon   = Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Организация, Перечисления.ВидыМаркетплейсов.OZON);
	НастройкаЯндекс = Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Организация, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКонтрагентов()
	
	//WB
	КонтрагентыWB = СписокКонтрагентовWB();
	Если КонтрагентыWB.Количество() > 0 Тогда
		КонтрагентWB = КонтрагентыWB[0];
	КонецЕсли;
		
	//Ozon
	РеквизитыМаркетплейса = ЭлектронноеВзаимодействиеБП.ПолучитьРеквизитыOzon();
	КонтрагентOzon = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	//Если указан КПП по месту регистрации
	Если НЕ ЗначениеЗаполнено(КонтрагентOzon) Тогда
		КонтрагентOzon = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, "770301001");
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(КонтрагентOzon) Тогда
		КонтрагентOzon = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН);
	КонецЕсли;
	
	//Яндекс
	КонтрагентыЯндекс = СписокКонтрагентовЯндекс();
	Если КонтрагентыЯндекс.Количество() > 0 Тогда
		КонтрагентЯндекс = КонтрагентыЯндекс[0];
	КонецЕсли;
		
	ЕстьWB     = ЗначениеЗаполнено(КонтрагентWB);
	ЕстьOzon   = ЗначениеЗаполнено(КонтрагентOzon);
	ЕстьЯндекс = ЗначениеЗаполнено(КонтрагентЯндекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодЗавершениеВыбора(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		Период = НачалоМесяца(РезультатВыбора.НачалоПериода);
	КонецЕсли;
	
	ПредставлениеПериода = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			НачалоМесяца(Период), КонецМесяца(Период), Истина);
			
	ПриИзмененииПериодаНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПериодаНаСервере()
	
	ПолучитьДанныеПродаж();
	ОбновитьДанныеДиаграммыСравнения();
	ОбновитьДанныеДиаграммыПоМесяцам();

КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеПродаж()
	
	КонтрагентыМаркетплейсы = Новый Массив;
	
	Если ЗначениеЗаполнено(КонтрагентWB) Тогда
		КонтрагентыWB = СписокКонтрагентовWB();
		Для Каждого ЭлементКонтрагентWB Из КонтрагентыWB Цикл
			КонтрагентыМаркетплейсы.Добавить(ЭлементКонтрагентWB);
		КонецЦикла;
		ЗаполнитьТаблицуWB(КонтрагентыWB);
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентOzon) Тогда
		КонтрагентыМаркетплейсы.Добавить(КонтрагентOzon);
		ЗаполнитьТаблицуOzon()
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентЯндекс) Тогда
		Для Каждого СтрокаКонтрагента ИЗ КонтрагентыИСкладыЯндекс(Организация).Покупатели Цикл
			КонтрагентыМаркетплейсы.Добавить(СтрокаКонтрагента);
		КонецЦикла;
		ЗаполнитьТаблицуЯндекс();
	ИначеЕсли ЗначениеЗаполнено(НастройкаЯндекс) Тогда
		ЗначенияРеквизитовЯндекса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаЯндекс, "Контрагент, Склад");
		СкладЯндекс = ЗначенияРеквизитовЯндекса.Склад;
		Если ЗначениеЗаполнено(ЗначенияРеквизитовЯндекса.Контрагент) Тогда
			КонтрагентыМаркетплейсы.Добавить(ЗначенияРеквизитовЯндекса.Контрагент);
		КонецЕсли;
	КонецЕсли;
	
	ПредопределенныеСчетаЗатрат = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, УчетЗатрат.ПредопределенныеСчетаРасходов());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, УчетЗатрат.ПредопределенныеСчетаРасходовНаПродажу());
	//Штрафы
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеРасходы));
	//Входящий НДС
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям));
	СчетаЗатрат  = УчетЗатрат.СчетаРасходов(ПредопределенныеСчетаЗатрат);
	СчетаВыручки = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Выручка);
	Счета91      = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы);
	СчетаДенег   = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.РасчетныеСчета);
	
	СчетаЗапасов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Товары));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ГотоваяПродукция));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные));
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСубконтоКонтрагенты", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	Запрос.УстановитьПараметр("СчетаВыручки",           СчетаВыручки);
	Запрос.УстановитьПараметр("СчетаЗатрат",            СчетаЗатрат);
	Запрос.УстановитьПараметр("Счета91",                Счета91);
	Запрос.УстановитьПараметр("СчетаДенег",             СчетаДенег);
	Запрос.УстановитьПараметр("СчетаЗапасов",           СчетаЗапасов);
	Запрос.УстановитьПараметр("КонтрагентЯндекс",       КонтрагентЯндекс);
	Запрос.УстановитьПараметр("СкладЯндекс",            КонтрагентыИСкладыЯндекс(Организация).Склады);
	
	Запрос.УстановитьПараметр("НачалоПериода", КонецМесяца(ДобавитьМесяц(Период, -12)));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(Период));
	Запрос.УстановитьПараметр("Маркетплейсы",  КонтрагентыМаркетплейсы);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	""Продажи"" КАК Показатель,
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ) КАК Период,
	|	ХозрасчетныйОбороты.СуммаОборот КАК Сумма,
	|	ХозрасчетныйОбороты.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОбороты.Организация КАК Организация
	|ПОМЕСТИТЬ ТаблицаОпераций
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Запись, , &ВидСубконтоКонтрагенты, Субконто1 В (&Маркетплейсы), КорСчет В (&СчетаВыручки), ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборот > 0
	|	И НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Возвраты"",
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ),
	|	-ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Запись, , &ВидСубконтоКонтрагенты, Субконто1 В (&Маркетплейсы), КорСчет В (&СчетаВыручки), ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборот < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Деньги"",
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ),
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.КорСубконто1,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, , Месяц, Счет В (&СчетаДенег), , КорСубконто1 В (&Маркетплейсы), , &ВидСубконтоКонтрагенты) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборот > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Комиссия"",
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ),
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.КорСубконто1,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, , Месяц, Счет В (&СчетаЗатрат), , КорСубконто1 В (&Маркетплейсы), НЕ КорСчет В (&СчетаЗапасов), &ВидСубконтоКонтрагенты) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборот > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Списания"",
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ),
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ОтчетКомиссионераОПродажах).Контрагент,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, , Регистратор, Счет В (&Счета91), , , , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|	И ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ОтчетКомиссионераОПродажах).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетКомиссионера.ОтчетОСписании)
	|	И ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ОтчетКомиссионераОПродажах).Контрагент В (&Маркетплейсы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Выкупы"",
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ),
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Запись, , &ВидСубконтоКонтрагенты, Субконто1 В (&Маркетплейсы), КорСчет В (&СчетаВыручки), ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборот > 0
	|	И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Отгрузки"",
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ),
	|	ХозрасчетныйОбороты.СуммаОборотДт,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).Контрагент,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Счет В (&СчетаЗапасов), , , , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборотДт > 0
	|	И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|	И ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеализацияТоваров.ПередачаТоваров)
	|	И ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).Контрагент В (&Маркетплейсы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Отгрузки"",
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, МЕСЯЦ),
	|	ХозрасчетныйОбороты.СуммаОборотДт,
	|	&КонтрагентЯндекс,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Счет В (&СчетаЗапасов), , , , ) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборотДт > 0
	|	И ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ПеремещениеТоваров
	|	И ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПеремещениеТоваров).СкладПолучатель В (&СкладЯндекс)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОпераций.Показатель КАК Показатель,
	|	ТаблицаОпераций.Период КАК Период,
	|	СУММА(ТаблицаОпераций.Сумма) КАК Сумма,
	|	ТаблицаОпераций.Контрагент КАК Контрагент,
	|	ТаблицаОпераций.Организация КАК Организация
	|ИЗ
	|	ТаблицаОпераций КАК ТаблицаОпераций
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОпераций.Показатель,
	|	ТаблицаОпераций.Период,
	|	ТаблицаОпераций.Контрагент,
	|	ТаблицаОпераций.Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Показатель";
	
	
	ТаблицаДанных.Очистить();
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		НоваяСтрока = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
		
		Если ЗначениеЗаполнено(НоваяСтрока.Контрагент) Тогда
			Если ЗначениеЗаполнено(КонтрагентыWB)
				И (НЕ КонтрагентыWB.Найти(НоваяСтрока.Контрагент) = Неопределено) Тогда
				НоваяСтрока.Маркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries;
			ИначеЕсли НоваяСтрока.Контрагент = КонтрагентOzon Тогда
				НоваяСтрока.Маркетплейс = Перечисления.ВидыМаркетплейсов.OZON;
			Иначе
				НоваяСтрока.Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуWB(КонтрагентыWB = Неопределено)
	
	Если КонтрагентыWB = Неопределено Тогда
		КонтрагентыWB = СписокКонтрагентовWB();
	КонецЕсли;
		
	ПредопределенныеСчетаЗатрат = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, УчетЗатрат.ПредопределенныеСчетаРасходов());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, УчетЗатрат.ПредопределенныеСчетаРасходовНаПродажу());
	//Штрафы
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеРасходы));
	//Входящий НДС
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям));
	СчетаЗатрат  = УчетЗатрат.СчетаРасходов(ПредопределенныеСчетаЗатрат);
	
	СчетаВыручки = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВыручки, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Выручка));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВыручки, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеДоходы));
	
	СчетаЗапасов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Товары));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ГотоваяПродукция));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные));
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСубконтоКонтрагенты", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	Запрос.УстановитьПараметр("СчетаВыручки",           СчетаВыручки);
	Запрос.УстановитьПараметр("СчетаЗатрат",            СчетаЗатрат);
	Запрос.УстановитьПараметр("СчетаЗапасов",           СчетаЗапасов);
	
	//Wildberries отчитывается по неделям, отчет вводится последним днем недели
	//в выборку должны попасть недели, которые заканчиваются в этом месяце
	НачалоПериода = НачалоНедели(НачалоМесяца(Период));
	КонецПериода = ?(КонецМесяца(Период) = КонецНедели(КонецМесяца(Период)), КонецМесяца(Период), НачалоНедели(КонецМесяца(Период))-1);
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецПериода);
	Запрос.УстановитьПараметр("Маркетплейс",   КонтрагентыWB);
	Запрос.УстановитьПараметр("Организация",   Организация);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, НЕДЕЛЯ) КАК Период,
	|	ХозрасчетныйОбороты.СуммаОборот КАК Реализовано,
	|	0 КАК Вознаграждение,
	|	ХозрасчетныйОбороты.Организация КАК Организация
	|ПОМЕСТИТЬ ТаблицаОпераций
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Неделя, , &ВидСубконтоКонтрагенты, Субконто1 В (&Маркетплейс), КорСчет В (&СчетаВыручки), ) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(ХозрасчетныйОбороты.Период, НЕДЕЛЯ),
	|	0,
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Неделя, Счет В (&СчетаЗатрат), , КорСубконто1 В (&Маркетплейс), НЕ КорСчет В (&СчетаЗапасов), &ВидСубконтоКонтрагенты) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборот > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТаблицаОпераций.Реализовано) КАК Реализовано,
	|	СУММА(ТаблицаОпераций.Вознаграждение) КАК Вознаграждение,
	|	СУММА(ТаблицаОпераций.Реализовано - ТаблицаОпераций.Вознаграждение) КАК КПеречислению,
	|	ТаблицаОпераций.Период КАК Период
	|ИЗ
	|	ТаблицаОпераций КАК ТаблицаОпераций
	|ГДЕ
	|	&ОтборПоОрганизации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОпераций.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	ОтборПоОрганизации = ?(ЗначениеЗаполнено(Организация), "ТаблицаОпераций.Организация = &Организация", "ИСТИНА");
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ОтборПоОрганизации", ОтборПоОрганизации);
	
	ТаблицаWB.Очистить();
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		НоваяСтрока = ТаблицаWB.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
		НомерСтраницы = ТаблицаWB.Количество();
		Элементы["ГруппаНеделя" + НомерСтраницы].Видимость = Истина;
		Элементы["ГруппаНеделя" + НомерСтраницы].Заголовок = Формат(НоваяСтрока.Период, "ДФ=dd.MM");
	КонецЦикла;
	
	Если ТаблицаWB.Количество() > 0 Тогда
		НоваяСтрока = ТаблицаWB.Добавить();
		НомерСтраницы = ТаблицаWB.Количество();
		Элементы["ГруппаНеделя" + НомерСтраницы].Видимость = Истина;
		Элементы["ГруппаНеделя" + НомерСтраницы].Заголовок = "Всего";
		
		НоваяСтрока.Реализовано    = ТаблицаWB.Итог("Реализовано");
		НоваяСтрока.Вознаграждение = ТаблицаWB.Итог("Вознаграждение");
		НоваяСтрока.КПеречислению  = ТаблицаWB.Итог("КПеречислению");
		
		Элементы.ГруппаНеделиWB.ТекущаяСтраница = Элементы["ГруппаНеделя" + ТаблицаWB.Количество()];
		СтрокаПоказателей = ТаблицаWB[ТаблицаWB.Количество() - 1];
		РеализованоWB    = СтрокаПоказателей.Реализовано;
		ВознаграждениеWB = СтрокаПоказателей.Вознаграждение;
		КПеречислениюWB  = СтрокаПоказателей.КПеречислению; 
		
		Элементы.ГруппаНеделиWB.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		Элементы.ДекорацияОтступ1.Видимость = Истина;
		Элементы.ДекорацияОтступ2.Видимость = Истина;
		Элементы.ДекорацияОтступ3.Видимость = Истина;
		Элементы.ДекорацияОтступ4.Видимость = Истина;
		
		Если ТаблицаWB.Количество() < 6 Тогда
			Для НомерСтраницы = (ТаблицаWB.Количество() + 1) По 6 Цикл
				Элементы["ГруппаНеделя" + НомерСтраницы].Видимость = Ложь;
			КонецЦикла;
		КонецЕсли;
	Иначе
		Элементы.ГруппаНеделиWB.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.ДекорацияОтступ1.Видимость = Ложь;
		Элементы.ДекорацияОтступ2.Видимость = Ложь;
		Элементы.ДекорацияОтступ3.Видимость = Ложь;
		Элементы.ДекорацияОтступ4.Видимость = Ложь;
		
		Элементы.ГруппаНеделя2.Видимость = Ложь;
		Элементы.ГруппаНеделя3.Видимость = Ложь;
		Элементы.ГруппаНеделя4.Видимость = Ложь;
		Элементы.ГруппаНеделя5.Видимость = Ложь;
		Элементы.ГруппаНеделя6.Видимость = Ложь;
		
		РеализованоWB    = 0;
		ВознаграждениеWB = 0;
		КПеречислениюWB  = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуOzon()
	
	ПредопределенныеСчетаЗатрат = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, УчетЗатрат.ПредопределенныеСчетаРасходов());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, УчетЗатрат.ПредопределенныеСчетаРасходовНаПродажу());
	//Штрафы
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеРасходы));
	//Входящий НДС
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям));
	СчетаЗатрат  = УчетЗатрат.СчетаРасходов(ПредопределенныеСчетаЗатрат);
	
	СчетаВыручки = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВыручки, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Выручка));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВыручки, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеДоходы));
	
	СчетаЗапасов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Товары));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ГотоваяПродукция));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные));
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСубконтоКонтрагенты", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	Запрос.УстановитьПараметр("СчетаВыручки",           СчетаВыручки);
	Запрос.УстановитьПараметр("СчетаЗатрат",            СчетаЗатрат);
	Запрос.УстановитьПараметр("СчетаЗапасов",           СчетаЗапасов);
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(Период));
	Запрос.УстановитьПараметр("Маркетплейс",   КонтрагентOzon);
	Запрос.УстановитьПараметр("Организация",   Организация);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&КонецПериода КАК Период,
	|	ХозрасчетныйОбороты.СуммаОборот КАК Реализовано,
	|	0 КАК Вознаграждение,
	|	ХозрасчетныйОбороты.Организация КАК Организация
	|ПОМЕСТИТЬ ТаблицаОпераций
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , , &ВидСубконтоКонтрагенты, Субконто1 В (&Маркетплейс), КорСчет В (&СчетаВыручки), ) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&КонецПериода,
	|	0,
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаЗатрат), , КорСубконто1 В (&Маркетплейс), НЕ КорСчет В (&СчетаЗапасов), &ВидСубконтоКонтрагенты) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборот > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТаблицаОпераций.Реализовано) КАК Реализовано,
	|	СУММА(ТаблицаОпераций.Вознаграждение) КАК Вознаграждение,
	|	СУММА(ТаблицаОпераций.Реализовано - ТаблицаОпераций.Вознаграждение) КАК КПеречислению,
	|	ТаблицаОпераций.Период КАК Период
	|ИЗ
	|	ТаблицаОпераций КАК ТаблицаОпераций
	|ГДЕ
	|	&ОтборПоОрганизации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОпераций.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	ОтборПоОрганизации = ?(ЗначениеЗаполнено(Организация), "ТаблицаОпераций.Организация = &Организация", "ИСТИНА");
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ОтборПоОрганизации", ОтборПоОрганизации);
	
	ВознаграждениеOzon = 0;
	РеализованоOzon    = 0;
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		РеализованоOzon    = РеализованоOzon + Результат.Реализовано;
		ВознаграждениеOzon = ВознаграждениеOzon + Результат.Вознаграждение;
	КонецЦикла;
	
	КПеречислениюOzon = РеализованоOzon - ВознаграждениеOzon;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЯндекс()
	
	ПредопределенныеСчетаЗатрат = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, УчетЗатрат.ПредопределенныеСчетаРасходов());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, УчетЗатрат.ПредопределенныеСчетаРасходовНаПродажу());
	//Штрафы
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеРасходы));
	//Входящий НДС
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПредопределенныеСчетаЗатрат, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям));
	СчетаЗатрат  = УчетЗатрат.СчетаРасходов(ПредопределенныеСчетаЗатрат);
	
	СчетаВыручки = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВыручки, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Выручка));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаВыручки, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ПрочиеДоходы));
	
	СчетаЗапасов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Товары));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ГотоваяПродукция));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗапасов, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные));
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСубконтоКонтрагенты", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	Запрос.УстановитьПараметр("СчетаВыручки",           СчетаВыручки);
	Запрос.УстановитьПараметр("СчетаЗатрат",            СчетаЗатрат);
	Запрос.УстановитьПараметр("СчетаЗапасов",           СчетаЗапасов);
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(Период));
	Запрос.УстановитьПараметр("Маркетплейс",   СписокКонтрагентовЯндекс());
	Запрос.УстановитьПараметр("Организация",   Организация);
	
	Запрос.УстановитьПараметр("Покупатели", КонтрагентыИСкладыЯндекс(Организация).Покупатели);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&КонецПериода КАК Период,
	|	ХозрасчетныйОбороты.СуммаОборот КАК Реализовано,
	|	0 КАК Вознаграждение,
	|	ХозрасчетныйОбороты.Организация КАК Организация
	|ПОМЕСТИТЬ ТаблицаОпераций
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , , &ВидСубконтоКонтрагенты, Субконто1 В (&Покупатели), КорСчет В (&СчетаВыручки), ) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&КонецПериода,
	|	0,
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаЗатрат), , КорСубконто1 В (&Маркетплейс), НЕ КорСчет В (&СчетаЗапасов), &ВидСубконтоКонтрагенты) КАК ХозрасчетныйОбороты
	|ГДЕ
	|	ХозрасчетныйОбороты.СуммаОборот > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТаблицаОпераций.Реализовано) КАК Реализовано,
	|	СУММА(ТаблицаОпераций.Вознаграждение) КАК Вознаграждение,
	|	СУММА(ТаблицаОпераций.Реализовано - ТаблицаОпераций.Вознаграждение) КАК КПеречислению,
	|	ТаблицаОпераций.Период КАК Период
	|ИЗ
	|	ТаблицаОпераций КАК ТаблицаОпераций
	|ГДЕ
	|	&ОтборПоОрганизации
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОпераций.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	ОтборПоОрганизации = ?(ЗначениеЗаполнено(Организация), "ТаблицаОпераций.Организация = &Организация", "ИСТИНА");
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ОтборПоОрганизации", ОтборПоОрганизации);
	
	ВознаграждениеЯндекс = 0;
	РеализованоЯндекс    = 0;
	
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		РеализованоЯндекс    = РеализованоЯндекс + Результат.Реализовано;
		ВознаграждениеЯндекс = ВознаграждениеЯндекс + Результат.Вознаграждение;
	КонецЦикла;
	
	КПеречислениюЯндекс = РеализованоЯндекс - ВознаграждениеЯндекс;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДиаграммыПоМесяцам()
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Показатель", Показатель);
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураОтбора.Вставить("Организация", Организация);
	КонецЕсли;
	
	ТаблицаПоМаркетплейсам = ТаблицаДанных.Выгрузить(СтруктураОтбора);
	ТаблицаПоМаркетплейсам.Свернуть("Маркетплейс, Период", "Сумма");
	
	ДиаграммаПоМесяцам.Очистить();
	СерияWildberries = ?(ЕстьWB, ДиаграммаПоМесяцам.Серии.Добавить("Wildberries"), Неопределено);
	СерияOzon        = ?(ЕстьOzon, ДиаграммаПоМесяцам.Серии.Добавить("Ozon"), Неопределено);
	СерияЯндекс      = ?(ЕстьЯндекс, ДиаграммаПоМесяцам.Серии.Добавить("Яндекс"), Неопределено);
	
	ЗначениеТочки = НачалоДня(КонецМесяца(ДобавитьМесяц(Период, -12)));
	Точка         = 0;
	
	Пока ЗначениеТочки <= КонецМесяца(Период) Цикл
		
		ДиаграммаПоМесяцам.Точки.Добавить(Формат(ЗначениеТочки, "ДФ='MMMM yyyy'"));
		ДиаграммаПоМесяцам.УстановитьЗначение(Точка, 0, 0);
		СтруктураОтбора.Вставить("Период", ЗначениеТочки);
		ЗаписиТекущегоПоказателя = ТаблицаПоМаркетплейсам.НайтиСтроки(Новый Структура("Период", ЗначениеТочки));
		
		Для Каждого СтрокаДанных Из ЗаписиТекущегоПоказателя Цикл
			Если СтрокаДанных.Сумма = 0 Тогда
				Продолжить;
			КонецЕсли;
			Если СтрокаДанных.Маркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries
				И НЕ СерияWildberries = Неопределено Тогда
				ДиаграммаПоМесяцам.УстановитьЗначение(Точка, СерияWildberries, СтрокаДанных.Сумма);
			ИначеЕсли СтрокаДанных.Маркетплейс = Перечисления.ВидыМаркетплейсов.OZON 
				И НЕ СерияOzon = Неопределено Тогда
				ДиаграммаПоМесяцам.УстановитьЗначение(Точка, СерияOzon, СтрокаДанных.Сумма);
			ИначеЕсли СтрокаДанных.Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет 
				И НЕ СерияЯндекс = Неопределено Тогда
				ДиаграммаПоМесяцам.УстановитьЗначение(Точка, СерияЯндекс, СтрокаДанных.Сумма);
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЦикла;
		
		ЗначениеТочки = НачалоДня(КонецМесяца(ДобавитьМесяц(ЗначениеТочки, 1)));
		Точка         = Точка + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДиаграммыСравнения()

	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Период", НачалоДня(КонецМесяца(Период)));
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураОтбора.Вставить("Организация", Организация);
	КонецЕсли;
	
	ДиаграммаСравнение.Очистить();
	ТочкаWildberries = ?(ЕстьWB, ДиаграммаСравнение.Точки.Добавить("Wildberries"), Неопределено);
	ТочкаOzon        = ?(ЕстьOzon, ДиаграммаСравнение.Точки.Добавить("Ozon"), Неопределено);
	ТочкаЯндекс      = ?(ЕстьЯндекс, ДиаграммаСравнение.Точки.Добавить("Яндекс"), Неопределено);
	ДиаграммаСравнение.Серии.Добавить("Результаты продаж");
	ДиаграммаСравнение.Серии.Добавить("Возвраты от покупателей");
	ДиаграммаСравнение.Серии.Добавить("Комиссия маркетплейса");
	ДиаграммаСравнение.Серии.Добавить("Потери и списания");
	
	ДиаграммаСравнение.УстановитьЗначение(0, 0, 0);
	
	ЗаписиТекущегоПериода = ТаблицаДанных.НайтиСтроки(СтруктураОтбора);
	Для Каждого СтрокаДанных Из ЗаписиТекущегоПериода Цикл
		Если СтрокаДанных.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаДанных.Маркетплейс = Перечисления.ВидыМаркетплейсов.Wildberries 
			И НЕ ТочкаWildberries = Неопределено Тогда
			Точка = ТочкаWildberries;
		ИначеЕсли СтрокаДанных.Маркетплейс = Перечисления.ВидыМаркетплейсов.OZON 
			И НЕ ТочкаOzon = Неопределено Тогда
			Точка = ТочкаOzon;
		ИначеЕсли СтрокаДанных.Маркетплейс = Перечисления.ВидыМаркетплейсов.ЯндексМаркет 
			И НЕ ТочкаЯндекс = Неопределено Тогда
			Точка = ТочкаЯндекс;
		Иначе
			Продолжить;
		КонецЕсли;
		Если СтрокаДанных.Показатель = "Продажи" Тогда
			УвеличитьЗначениеТочки(ДиаграммаСравнение, Точка, 0, СтрокаДанных.Сумма);
		ИначеЕсли СтрокаДанных.Показатель = "Выкупы" Тогда
			УвеличитьЗначениеТочки(ДиаграммаСравнение, Точка, 0, СтрокаДанных.Сумма);
		ИначеЕсли СтрокаДанных.Показатель = "Возвраты" Тогда
			УменьшитьЗначениеТочки(ДиаграммаСравнение, Точка, 0, СтрокаДанных.Сумма);
			УвеличитьЗначениеТочки(ДиаграммаСравнение, Точка, 1, СтрокаДанных.Сумма);
		ИначеЕсли СтрокаДанных.Показатель = "Комиссия" Тогда
			УменьшитьЗначениеТочки(ДиаграммаСравнение, Точка, 0, СтрокаДанных.Сумма);
			УвеличитьЗначениеТочки(ДиаграммаСравнение, Точка, 2, СтрокаДанных.Сумма);
		ИначеЕсли СтрокаДанных.Показатель = "Списания" Тогда
			УменьшитьЗначениеТочки(ДиаграммаСравнение, Точка, 0, СтрокаДанных.Сумма);
			УвеличитьЗначениеТочки(ДиаграммаСравнение, Точка, 3, СтрокаДанных.Сумма);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура УвеличитьЗначениеТочки(Диаграмма, Точка, Серия, Значение)
	ЗначениеТочки = ДиаграммаСравнение.ПолучитьЗначение(Точка, Серия);
	ТекущееЗначение = ?(ЗначениеЗаполнено(ЗначениеТочки.Значение), ЗначениеТочки.Значение, 0);
	ДиаграммаСравнение.УстановитьЗначение(Точка, Серия, ТекущееЗначение + Значение);
КонецПроцедуры

&НаСервере
Процедура УменьшитьЗначениеТочки(Диаграмма, Точка, Серия, Значение)
	ЗначениеТочки = ДиаграммаСравнение.ПолучитьЗначение(Точка, Серия);
	ТекущееЗначение = ?(ЗначениеЗаполнено(ЗначениеТочки.Значение), ЗначениеТочки.Значение, 0);
	ДиаграммаСравнение.УстановитьЗначение(Точка, Серия, ТекущееЗначение - Значение);
КонецПроцедуры

&НаСервереБезКонтекста
Функция КонтрагентыИСкладыЯндекс(Организация)

	Покупатели = Новый Массив;
	Склады     = Новый Массив;
	
	НастройкаЯндекс = Справочники.НастройкиИнтеграцииМаркетплейс.НастройкаПоУмолчанию(Организация, Перечисления.ВидыМаркетплейсов.ЯндексМаркет);
	
	КонтрагентыЯндекс = СписокКонтрагентовЯндекс();
	Для Каждого КонтрагентЯндекс Из КонтрагентыЯндекс Цикл
		Покупатели.Добавить(КонтрагентЯндекс);
	КонецЦикла;
	Если ЗначениеЗаполнено(Организация) Тогда
		Если ЗначениеЗаполнено(НастройкаЯндекс) Тогда
			ЗначенияРеквизитовЯндекса = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаЯндекс, "Контрагент, Склад");
			Если ЗначениеЗаполнено(ЗначенияРеквизитовЯндекса.Контрагент) Тогда
				Покупатели.Добавить(ЗначенияРеквизитовЯндекса.Контрагент);
			КонецЕсли;
			Если ЗначениеЗаполнено(ЗначенияРеквизитовЯндекса.Склад) Тогда
				Склады.Добавить(ЗначенияРеквизитовЯндекса.Склад);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЗапросНастройки = Новый Запрос;
		ЗапросНастройки.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	НастройкиИнтеграцииМаркетплейс.Контрагент КАК Контрагент,
		|	НастройкиИнтеграцииМаркетплейс.Склад КАК Склад,
		|	НастройкиИнтеграцииМаркетплейс.Владелец КАК Владелец
		|ИЗ
		|	Справочник.НастройкиИнтеграцииМаркетплейс КАК НастройкиИнтеграцииМаркетплейс
		|ГДЕ
		|	НастройкиИнтеграцииМаркетплейс.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ЯндексМаркет)";
		Результат = ЗапросНастройки.Выполнить().Выбрать();
		Пока Результат.Следующий() Цикл
			Если ЗначениеЗаполнено(Результат.Контрагент) Тогда
				Покупатели.Добавить(Результат.Контрагент);
			КонецЕсли;
			Если ЗначениеЗаполнено(Результат.Склад) Тогда
				Склады.Добавить(Результат.Склад);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(Покупатели);
	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(Склады);
	
	Возврат Новый Структура("Покупатели, Склады", Покупатели, Склады);
	
КонецФункции

&НаКлиенте
Процедура ЗавершениеВыбораКонтрагента(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатВыбора = Неопределено Тогда
		 ОткрытьСписокДокументовКонтрагента(РезультатВыбора.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокКонтрагентовWB()
	
	КонтрагентыWB = Новый Массив;
	РеквизитыМаркетплейса = Новый Структура;
	//ООО "РВБ"
	РеквизитыМаркетплейса.Вставить("ИНН", "9714053621");
	РеквизитыМаркетплейса.Вставить("КПП", "507401001");
	КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	Если НЕ ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН);
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентыWB.Добавить(КонтрагентПоИНН);
	КонецЕсли;
	//ООО "Вайлдберриз"
	РеквизитыМаркетплейса.Вставить("ИНН", "7721546864");
	РеквизитыМаркетплейса.Вставить("КПП", "997750001");
	КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	Если НЕ ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН);
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентыWB.Добавить(КонтрагентПоИНН);
	КонецЕсли;
	
	Возврат КонтрагентыWB;
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокКонтрагентовЯндекс()
	
	КонтрагентыЯндекс = Новый Массив;
	РеквизитыМаркетплейса = Новый Структура;
	//ООО "Яндекс.Маркет"
	РеквизитыМаркетплейса.Вставить("ИНН", "9704254424");
	РеквизитыМаркетплейса.Вставить("КПП", "770401001");
	КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	Если НЕ ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН);
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентыЯндекс.Добавить(КонтрагентПоИНН);
	КонецЕсли;
	//ООО "Яндекс"
	РеквизитыМаркетплейса.Вставить("ИНН", "7736207543");
	РеквизитыМаркетплейса.Вставить("КПП", "770401001");
	КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН, РеквизитыМаркетплейса.КПП);
	Если НЕ ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентПоИНН = ЭлектронноеВзаимодействиеБП.СсылкаНаОбъектПоИННКПП("Контрагенты", РеквизитыМаркетплейса.ИНН);
	КонецЕсли;
	Если ЗначениеЗаполнено(КонтрагентПоИНН) Тогда
		КонтрагентыЯндекс.Добавить(КонтрагентПоИНН);
	КонецЕсли;
	
	
	Возврат КонтрагентыЯндекс;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСписокДокументовКонтрагента(Контрагент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", СтрШаблон(НСтр("ru = 'Документы по контрагенту %1'"), Контрагент));
	СтруктураОтборов =  Новый Структура;
	СтруктураОтборов.Вставить("Контрагент", Контрагент);
	Если ЗначениеЗаполнено(Организация) Тогда
		СтруктураОтборов.Вставить("Организация", Организация);
	КонецЕсли;
	ПараметрыФормы.Вставить("Отбор", СтруктураОтборов);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "ДокументыПоКонтрагенту");
	
	ПараметрыОткрытия = ОбщегоНазначенияБПКлиентСервер.ПараметрыОткрытияФормыСОжиданием();
	ПараметрыОткрытия.ИмяФормы = "ЖурналДокументов.ЖурналОпераций.ФормаСписка";
	ПараметрыОткрытия.Заголовок = ПараметрыФормы.Заголовок;
	ПараметрыОткрытия.ЗамерПроизводительности = Истина;
	ПараметрыОткрытия.Уникальность = Контрагент;
	
	ОбщегоНазначенияБПКлиент.ОткрытьФормуСОжиданием(ПараметрыОткрытия, ПараметрыФормы, );
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаНеделиWBПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ТекущаяСтраница = Элементы.ГруппаНеделиWB.ТекущаяСтраница;
	ИндексСтраницы = СтрЗаменить(ТекущаяСтраница.Имя, "ГруппаНеделя", "");
	
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ИндексСтраницы) Тогда
		ТипЧисло  = Новый ОписаниеТипов("Число");
		Результат = ТипЧисло.ПривестиЗначение(ИндексСтраницы);
		Если Результат > 0 Тогда
			СтрокаПоказателей = ТаблицаWB[Результат - 1];
			РеализованоWB    = СтрокаПоказателей.Реализовано;
			ВознаграждениеWB = СтрокаПоказателей.Вознаграждение;
			КПеречислениюWB  = СтрокаПоказателей.КПеречислению;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	ЗаполнитьКонтрагентов();
	ПроверитьНастройкиПодключения();
	УправлениеФормой(ЭтотОбъект);
	
	ПолучитьДанныеПродаж();
	ОбновитьДанныеДиаграммыСравнения();
	ОбновитьДанныеДиаграммыПоМесяцам();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОстаткиМаркетплейсов(ВидМаркетплейса)
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыОткрытияФормы.Вставить("Организация", Организация);
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.ОстаткиТоваровМаркетплейсов.Форма.ФормаСписка", ПараметрыОткрытияФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОжиданиеКонвертацииФайлаЯндекс()
	
	Попытка
		
		Если ЭлектронноеВзаимодействиеБПВызовСервера.ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			ЭлектронноеВзаимодействиеБПКлиент.ОткрытьПомощникЗагрузкиОтчетаЯндекс(АдресХранилища, УникальныйИдентификатор);
						
		Иначе
			
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ОжиданиеКонвертацииФайлаЯндекс", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
				
			КонецЕсли;
			
	Исключение			
		
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзМаркетплейс(ВидМаркетплейс)
	
	Если Не ОбменСМаркетплейсВызовСервера.ДоступенОбменСМаркетплейс() Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Недостаточно прав для обмена с маркетплейсом'"));
		Возврат;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидМаркетплейс", ВидМаркетплейс);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФормы.Вставить("Организация", Организация);
	КонецЕсли;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузкаИзМаркетплейсПродолжение",ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаЗагрузкиИзМаркетплейс", ПараметрыФормы, ЭтотОбъект,,,,ОповещениеОЗавершении,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаИзМаркетплейсПродолжение(ПараметрыЗагрузки, ДопПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПараметрыЗагрузки) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Форма", ЭтотОбъект);
	
	Если ПараметрыЗагрузки.ВидМаркетплейс = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ЯндексМаркет") Тогда
		ПараметрыВыполнения.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("ОткрытьПомощникЗагрузкиОтчетаЯндекс", ЭтотОбъект, ПараметрыЗагрузки));
		ПараметрыВыполнения.Вставить("ВыводитьПрогрессВыполнения", Истина);
		ПараметрыВыполнения.Вставить("Заголовок", НСтр("ru = 'Яндекс.Маркет готовит отчет'"));
	Иначе
		ПараметрыВыполнения.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения("НачатьЗагрузкуИзСтруктуры", ЭтотОбъект));
		ПараметрыВыполнения.Вставить("ВыводитьПрогрессВыполнения", Ложь);
		ПараметрыВыполнения.Вставить("Заголовок", НСтр("ru = 'Загружаем данные из маркетплейс'"));
	КонецЕсли;
	
	ОбменСМаркетплейсКлиент.ПрочитатьДанныеОтчетаВСтруктуру(ПараметрыЗагрузки, ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Функция ОперацияВыполненаУспешно(Результат)
	Возврат Результат <> Неопределено И Результат.Статус = "Выполнено";
КонецФункции 

&НаКлиенте
Процедура НачатьЗагрузкуИзСтруктуры(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ ОперацияВыполненаУспешно(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронноеВзаимодействиеБПКлиент.НачатьЗагрузкуИзСтруктуры(Результат.АдресРезультата, УникальныйИдентификатор);
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазобратьПолученныеДанные(АдресРезультата)
	РезультатОперации = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если РезультатОперации.ДанныеДокумента <> Неопределено Тогда
		РезультатОперации.ДанныеДокумента = ПоместитьВоВременноеХранилище(РезультатОперации.ДанныеДокумента, АдресРезультата);
	КонецЕсли;
	
	Возврат РезультатОперации;
КонецФункции

&НаКлиенте
Процедура ОткрытьПомощникЗагрузкиОтчетаЯндекс(Результат, ПараметрыЗагрузки) Экспорт
	Если НЕ ОперацияВыполненаУспешно(Результат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("ru = 'При получении данных о продажах произошла ошибка. Подробности в журнале регистрации'");
		Возврат;
	КонецЕсли;
	
	РезультатРазбораДанных = РазобратьПолученныеДанные(Результат.АдресРезультата);
	Если ЗначениеЗаполнено(РезультатРазбораДанных.ТекстОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатРазбораДанных.ТекстОшибки);
	ИначеЕсли РезультатРазбораДанных.ДанныеДокумента = Неопределено Тогда
		ПоказатьПредупреждение(,
			СтрШаблон(НСтр("ru = 'Нет продаж за %1'"), ФОрмат(ПараметрыЗагрузки.НачалоПериода, "ДФ='ММММ гггг'")),15);
	Иначе
		ЭлектронноеВзаимодействиеБПКлиент.ОткрытьПомощникЗагрузкиОтчетаЯндекс(РезультатРазбораДанных.ДанныеДокумента, УникальныйИдентификатор, ПараметрыЗагрузки.Организация);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, ДополнительныеПараметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПисьма = РаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
	ПараметрыПисьма.Получатель = "bp@1c.ru";
	ПараметрыПисьма.Тема = НСтр("ru = 'Отзыв о мониторе маркетплейсов'");
	
	ОтправкаПочтовыхСообщенийКлиент.ДополнитьПараметрыПисьмаДляОтправкиЛегкойПочтой(ПараметрыПисьма);
	
	РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
	
КонецПроцедуры

#КонецОбласти