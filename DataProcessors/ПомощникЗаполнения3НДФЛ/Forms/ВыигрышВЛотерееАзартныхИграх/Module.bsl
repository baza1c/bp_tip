
#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ОтключитьЗаполнениеПоИНН;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресКлючейПоказателей = Параметры.АдресКлючейПоказателей;
	Декларация3НДФЛВыбраннаяФорма = Параметры.Декларация3НДФЛВыбраннаяФорма;
	
	ЗаполнениеРеквизитовПлашкой = Истина;
	
	СтруктураДоходовВычетов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтруктураДоходовВычетов");
	Если ЗначениеЗаполнено(СтруктураДоходовВычетов) Тогда
		
		СтруктураДоходовВычетов.Свойство("СтавкаНалога", СтавкаНалога);
		СтруктураДоходовВычетов.Свойство("СуммаДохода", СуммаДохода);
		СтруктураДоходовВычетов.Свойство("СуммаНалога", СуммаНалога);
		СтруктураДоходовВычетов.Свойство("СуммаНалогаУдержанная", СуммаНалогаУдержанная);
		СтруктураДоходовВычетов.Свойство("ВидКонтрагента", ВидКонтрагента);
		СтруктураДоходовВычетов.Свойство("Наименование", Наименование);
		СтруктураДоходовВычетов.Свойство("ИНН", ИНН);
		СтруктураДоходовВычетов.Свойство("КПП", КПП);
		СтруктураДоходовВычетов.Свойство("ОКТМО", ОКТМО);
		
		КодВидаДохода = Неопределено;
		СтруктураДоходовВычетов.Свойство("КодВидаДохода", КодВидаДохода);
		
		КодВыигрышаВБукмекерскойКонторе = КодВидаДохода(Истина, Декларация3НДФЛВыбраннаяФорма);
		ВыигрышПолученВБукмекерскойКонторе = (КодВидаДохода = КодВыигрышаВБукмекерскойКонторе);
		
		Если ЕстьКлючПоказателя("СуммаОблагаемогоДоходаРФ", АдресКлючейПоказателей) Тогда
			СтруктураДоходовВычетов.Свойство("СуммаДоходаОблагаемая", СуммаДоходаОблагаемая);
		Иначе
			СуммаДоходаОблагаемая = СуммаДохода;
		КонецЕсли;
		
		ЗаполнениеРеквизитовПлашкой = Не ЗначениеЗаполнено(СуммаДохода);
		
	Иначе
		
		СтавкаНалога = Обработки.ПомощникЗаполнения3НДФЛ.СтавкаНалога(Параметры.Декларация3НДФЛВыбраннаяФорма);
		ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛ.ИсточникДоходовПриСозданииНаСервере(ЭтотОбъект, Ложь);
	
	УправлениеФормойПриСозданииНаСервере();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПомощникЗаполнения3НДФЛ.ПроверитьЗаполнениеРеквизитовИсточникаДоходов(
		ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОрганизацииФормыДляОтчетности.ПроверитьКодПоОКТМОНаФорме(ОКТМО, "ОКТМО", Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	ИНН = СокрЛП(ИНН);
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КПППриИзменении(Элемент)
	
	КПП = СокрЛП(КПП);
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаИНННаименованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ПолеПоискаИНННаименование)
		И Не ЗначениеЗаполнено(ИНН) 
		И Не ЗначениеЗаполнено(Наименование) Тогда
		
		ПомощникЗаполнения3НДФЛКлиент.ЗаполнитьРеквизитыПоДаннымЕГР(
			ПолеПоискаИНННаименование, ОповещениеПослеЗаполненияПоИНН());
		
		ОтключитьЗаполнениеПоИНН = Истина;
		ПодключитьОбработчикОжидания("Подключаемый_ВключитьЗаполнениеПоИНН", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходаПриИзменении(Элемент)
	
	СуммаДоходаОблагаемая = СуммаДохода;
	ПриИзмененииСуммыДоходаОблагаемой();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходаОблагаемаяПриИзменении(Элемент)
	
	ПриИзмененииСуммыДоходаОблагаемой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("Вид",
		ПредопределенноеЗначение("Перечисление.ИсточникиДоходовФизическихЛиц.ВыигрышВЛотерееАзартныхИграх"));
	
	СтруктураРезультата.Вставить("КодВидаДохода",
		КодВидаДохода(ВыигрышПолученВБукмекерскойКонторе, Декларация3НДФЛВыбраннаяФорма));
	
	СтруктураРезультата.Вставить("Информация", НСтр("ru = 'Выигрыш в лотерее (азартных играх)'"));
	
	СтруктураРезультата.Вставить("СтавкаНалога", СтавкаНалога);
	СтруктураРезультата.Вставить("СуммаДохода", СуммаДохода);
	СтруктураРезультата.Вставить("СуммаДоходаОблагаемая", СуммаДоходаОблагаемая);
	СтруктураРезультата.Вставить("СуммаНалога", СуммаНалога);
	СтруктураРезультата.Вставить("СуммаНалогаУдержанная", СуммаНалогаУдержанная);
	СтруктураРезультата.Вставить("ВидКонтрагента", ВидКонтрагента);
	СтруктураРезультата.Вставить("Наименование", Наименование);
	СтруктураРезультата.Вставить("ИНН", ИНН);
	СтруктураРезультата.Вставить("КПП", КПП);
	СтруктураРезультата.Вставить("ОКТМО", ОКТМО);
	
	СтруктураРезультата.Вставить("НаименованиеИсточникаДохода", Наименование);
	
	Закрыть(СтруктураРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьПоИНН(Команда)
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Поле ""ИНН"" не заполнено'"));
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	ИначеЕсли Не ОшибокПоИННнет Тогда
		ПоказатьПредупреждение(, Строка(РезультатПроверкиИНН));
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиент.ВыполнитьЗаполнениеРеквизитовПоИНН(ИНН, ОповещениеПослеЗаполненияПоИНН());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоДаннымЕГР(Команда)
	
	Если ОтключитьЗаполнениеПоИНН <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиент.ЗаполнитьРеквизитыПоДаннымЕГР(
		ПолеПоискаИНННаименование, ОповещениеПослеЗаполненияПоИНН());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоНаименованию(Команда)
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Поле ""Наименование"" не заполнено'"));
		ТекущийЭлемент = Элементы.Наименование;
		Возврат;
	Иначе
		ПомощникЗаполнения3НДФЛКлиент.ВыполнитьЗаполнениеРеквизитовПоНаименованию(
			Наименование, ОповещениеПослеЗаполненияПоИНН());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКлючСохраненияПоложенияОкна(Форма)
	
	ПрефиксКлючаСохранения = "";
	
	ПомощникЗаполнения3НДФЛКлиентСервер.УстановитьКлючСохраненияПоложенияОкна(Форма, ПрефиксКлючаСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСуммыДоходаОблагаемой()
	
	СуммаНалога = Окр(СуммаДоходаОблагаемая * СтавкаНалога / 100, 0);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойПриСозданииНаСервере()
	
	Элементы.ФормаКомандаОК.Доступность = Не ТолькоПросмотр;
	Элементы.ЗаполнитьРеквизитыПоДаннымЕГР.Доступность = Не ТолькоПросмотр;
	Элементы.КнопкаЗаполнитьРеквизитыПоНаименованию.Доступность = Не ТолькоПросмотр;
	Элементы.КомандаЗаполнитьПоИНН.Доступность = Не ТолькоПросмотр;
	
	Элементы.ГруппаДоход.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаНалог.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаРеквизитыИсточникаДохода.ТолькоПросмотр = ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ПомощникЗаполнения3НДФЛКлиентСервер.УстановитьВидимостьПолейКонтрагента(Форма, Ложь);
	
	Если ЕстьКлючПоказателя("СуммаОблагаемогоДоходаРФ", Форма.АдресКлючейПоказателей) Тогда
		Элементы.СуммаДоходаОблагаемая.Видимость = Истина;
		Элементы.СуммаНалога.Видимость = Истина;
	Иначе
		Элементы.СуммаДоходаОблагаемая.Видимость = Ложь;
		Элементы.СуммаНалога.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьКлючСохраненияПоложенияОкна(Форма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьКлючПоказателя(Знач ИмяКлюча, Знач АдресКлючейПоказателей)
	
	Возврат ПомощникЗаполнения3НДФЛ.ЕстьКлючПоказателя(ИмяКлюча, АдресКлючейПоказателей);
	
КонецФункции

&НаСервереБезКонтекста
Функция КодВидаДохода(Знач ВыигрышПолученВБукмекерскойКонторе, Знач ВыбраннаяФорма)
	
	КодыВидовДоходаРФ = ПомощникЗаполнения3НДФЛ.ВидыДоходов(ВыбраннаяФорма);
	КодыДоходовРасширенныйСписок = ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма);
	
	Если ВыигрышПолученВБукмекерскойКонторе И Не КодыДоходовРасширенныйСписок Тогда
		Возврат ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			КодыВидовДоходаРФ, "ВыигрышиВБукмекерскойКонтореТотализаторе", КодыВидовДоходаРФ.ИнойДоходОтИсточникаРФ);
	ИначеЕсли ВыигрышПолученВБукмекерскойКонторе И КодыДоходовРасширенныйСписок Тогда
		Возврат КодыВидовДоходаРФ.ИнойДоходПоПрогрессивнойШкале;
	Иначе
		Возврат КодыВидовДоходаРФ.ВыигрышиВЛотереяхАзартныхИграх;
	КонецЕсли;
	
КонецФункции

#Область ЗаполнениеРеквизитовКонтрагента

&НаКлиенте
Функция ОповещениеПослеЗаполненияПоИНН()
	
	Возврат Новый ОписаниеОповещения("ПослеЗаполненияПоИНН", ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗаполненияПоИНН(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
		ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВключитьЗаполнениеПоИНН()
	
	ОтключитьЗаполнениеПоИНН = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти