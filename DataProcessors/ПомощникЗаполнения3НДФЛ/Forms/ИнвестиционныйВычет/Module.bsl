#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ОтключитьЗаполнениеПоИНН;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Период = Параметры.Период;
	ВыбраннаяФорма = Параметры.Декларация3НДФЛВыбраннаяФорма;
	
	СтруктураДоходовВычетов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтруктураДоходовВычетов");
	Если ЗначениеЗаполнено(СтруктураДоходовВычетов) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДоходовВычетов);
	КонецЕсли;
	
	НастройкиРаспределенияВычетов = ПомощникЗаполнения3НДФЛ.НастройкиРаспределенияВычетов(
		ЭтотОбъект,
		Параметры.ДоступныеДоходы,
		СтруктураДоходовВычетов);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиРаспределенияВычетов,
		"ВариантРаспределенияВычета, ВидДохода, СуммаВычета");
	
	РаспределениеПоДоходам.Загрузить(НастройкиРаспределенияВычетов.РаспределениеПоДоходам);
	Элементы.ВидДоходаВычета.СписокВыбора.ЗагрузитьЗначения(НастройкиРаспределенияВычетов.СписокВыбораДохода);
	
	СписокОснованийВычета = ПомощникЗаполнения3НДФЛ.ПризнакиОснованийЗаявленияВычетаПоИИС(ВыбраннаяФорма);
	
	Если ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураДоходовВычетов, "ВидВычета")) Тогда
		ВидВычета = СтруктураДоходовВычетов.ВидВычета;
	Иначе
		ВидВычета = ВидВычетаПоУмолчанию();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураДоходовВычетов, "ДанныеБрокера")) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДоходовВычетов.ДанныеБрокера);
		ЗаполнениеРеквизитовПлашкой = Ложь;
	Иначе
		ЗаполнениеРеквизитовПлашкой = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОснованийВычета) Тогда
		Элементы.ВидВычета.Видимость = Истина;
	Иначе
		Элементы.ВидВычета.Видимость = Ложь;
	КонецЕсли;
	
	ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
	ПомощникЗаполнения3НДФЛ.ИсточникДоходовПриСозданииНаСервере(ЭтотОбъект, Ложь);
	
	ПересчитатьИРаспределитьСуммуВычета();
	КлючСохраненияПоложенияОкна = ВидВычета;
	
	ПредельнаяСуммаВычета = ПредельнаяСуммаПоВидуВычета(ВидВычета, ВыбраннаяФорма);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидВычета = ВидВычетаПоУмолчанию() И СуммаВычета = 0 И СуммаДохода = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Заполните сумму вычета или вычет к восстановлению", , , , Отказ);
	ИначеЕсли ВидВычета <> ВидВычетаПоУмолчанию() Тогда
		ПроверяемыеРеквизиты.Добавить("СуммаВычета");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СуммаВычета) И СуммаВычета > ПредельнаяСуммаВычета Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Сумма вычета превышает %1 рублей.'"), Формат(ПредельнаяСуммаВычета, "ЧДЦ=0"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СуммаВычета", , Отказ);
	КонецЕсли;
	
	Если Не ПомощникЗаполнения3НДФЛКлиентСервер.ПроверятьРеквизитыБрокера(Период) Тогда
		
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("ИНН");
		МассивНепроверяемыхРеквизитов.Добавить("КПП");
		МассивНепроверяемыхРеквизитов.Добавить("Наименование");
		МассивНепроверяемыхРеквизитов.Добавить("НомерДоговора");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаДоговора");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаОткрытияИИС");
		
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаОткрытияИИСПриИзменении(Элемент)
	
	Если ДатаОткрытияИИС < '20240101' Тогда
		// Договор долгосрочных сбережений открывается с 01.01.2024 г.
		ВидВычета = ВидВычетаПоУмолчанию();
		ПредельнаяСуммаВычета = ПредельнаяСуммаПоВидуВычета(ВидВычета, ВыбраннаяФорма);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидВычетаПриИзменении(Элемент)
	
	ПредельнаяСуммаВычета = ПредельнаяСуммаПоВидуВычета(ВидВычета, ВыбраннаяФорма);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаВычетаПриИзменении(Элемент)
	АктуализироватьДанныеОСуммеВычета();
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияВычетаОдинДоходПриИзменении(Элемент)
	
	ПриИзмененииВариантаРаспределенияВычета();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияВычетаПриИзменении(Элемент)
	
	ПриИзмененииВариантаРаспределенияВычета();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоходаВычетаПриИзменении(Элемент)
	
	ПомощникЗаполнения3НДФЛКлиент.ВидДоходаВычетаПриИзменении(ВидДохода, СуммаВычета, РаспределениеПоДоходам);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаИНННаименованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ПолеПоискаИНННаименование)
		И НЕ ЗначениеЗаполнено(ИНН) 
		И НЕ ЗначениеЗаполнено(Наименование) Тогда
		
		ПомощникЗаполнения3НДФЛКлиент.ЗаполнитьРеквизитыПоДаннымЕГР(ПолеПоискаИНННаименование, ОповещениеПослеЗаполненияПоИНН());
		ОтключитьЗаполнениеПоИНН = Истина;
		ПодключитьОбработчикОжидания("Подключаемый_ВключитьЗаполнениеПоИНН", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	ИНН = СокрП(ИНН);
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КПППриИзменении(Элемент)
	
	КПП = СокрП(КПП);
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаспределениеПоДоходам

&НаКлиенте
Процедура РаспределениеПоДоходамПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ПомощникЗаполнения3НДФЛКлиент.РаспределениеПоДоходамПередОкончаниемРедактирования(
		ЭтотОбъект, ОтменаРедактирования, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеПоДоходамПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РаспределеноВычетаВсего = РаспределениеПоДоходам.Итог("СуммаВычета");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаполнитьПоИНН(Команда)
	
	Если ОтключитьЗаполнениеПоИНН <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Поле ""ИНН"" не заполнено'"));
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	ИначеЕсли Не ОшибокПоИННнет Тогда
		ПоказатьПредупреждение(, Строка(РезультатПроверкиИНН));
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиент.ВыполнитьЗаполнениеРеквизитовПоИНН(ИНН, ОповещениеПослеЗаполненияПоИНН());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоДаннымЕГР(Команда)
	
	ПомощникЗаполнения3НДФЛКлиент.ЗаполнитьРеквизитыПоДаннымЕГР(ПолеПоискаИНННаименование, ОповещениеПослеЗаполненияПоИНН());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоНаименованию(Команда)
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Поле ""Наименование"" не заполнено'"));
		ТекущийЭлемент = Элементы.Наименование;
	Иначе
		ПомощникЗаполнения3НДФЛКлиент.ВыполнитьЗаполнениеРеквизитовПоНаименованию(Наименование, ОповещениеПослеЗаполненияПоИНН());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОтвета = Новый Структура;
	СтруктураОтвета.Вставить("Вид",         ПредопределенноеЗначение("Перечисление.ВычетыФизическихЛиц.Инвестиционный"));
	СтруктураОтвета.Вставить("СуммаДохода", СуммаДохода);
	СтруктураОтвета.Вставить("СуммаВычета", СуммаВычета);
	СтруктураОтвета.Вставить("ВидВычета", ВидВычета);
	
	Если ВидВычета = ВидВычетаПоУмолчанию() Тогда
		СтруктураОтвета.Вставить("ПризнакОснованияЗаявленияВычетаИИС",
			ПомощникЗаполнения3НДФЛКлиентСервер.ПризнакПополненияСчетаИИС());
		СтруктураОтвета.Вставить("Информация",  НСтр("ru = 'Инвестиционный вычет'"));
	Иначе
		СтруктураОтвета.Вставить("ПризнакОснованияЗаявленияВычетаИИС",
			ПомощникЗаполнения3НДФЛКлиентСервер.ПризнакДоходаПоОперациямИИС());
		СтруктураОтвета.Вставить("Информация",  НСтр("ru = 'Инвестиционный вычет на долгосрочные сбережения'"));
	КонецЕсли;
	
	ДанныеБрокера = Новый Структура;
	ДанныеБрокера.Вставить("ИНН", ИНН);
	ДанныеБрокера.Вставить("КПП", КПП);
	ДанныеБрокера.Вставить("Наименование", Наименование);
	ДанныеБрокера.Вставить("ДатаДоговора", ДатаДоговора);
	ДанныеБрокера.Вставить("НомерДоговора", НомерДоговора);
	ДанныеБрокера.Вставить("СуммаВычета", СуммаВычета);
	ДанныеБрокера.Вставить("ДатаОткрытияИИС", ДатаОткрытияИИС);
	
	СтруктураОтвета.Вставить("ДанныеБрокера", ДанныеБрокера);
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма) Тогда
		СтруктураОтвета.Вставить("ВидДохода",
			ПомощникЗаполнения3НДФЛКлиент.СоответствиеРаспределенияПоДоходам(РаспределениеПоДоходам));
	КонецЕсли;
	
	Закрыть(СтруктураОтвета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	ПроверятьРеквизитыБрокера = ПомощникЗаполнения3НДФЛКлиентСервер.ПроверятьРеквизитыБрокера(Форма.Период);
	
	Если Форма.ВидВычета = ВидВычетаПоУмолчанию() Или Не ПроверятьРеквизитыБрокера Тогда
		Элементы.ВычетПоВзносамПояснение.Заголовок = СтрШаблон(
			НСтр("ru = 'Предоставляется в сумме денежных средств, внесенных
			|на инвестиционный счет, но не более %1 рублей.'"),
			Форма.ПредельнаяСуммаВычета);
	Иначе
		Элементы.ВычетПоВзносамПояснение.Заголовок = СтрШаблон(
			НСтр("ru = 'Предоставляется в сумме финансового результата по операциям
			|на инвестиционном счете, открытом после 1 января 2024 г., но не более %1 рублей.'"),
			Форма.ПредельнаяСуммаВычета);
	КонецЕсли;
	
	Элементы.ГруппаБрокер.Видимость = ПроверятьРеквизитыБрокера;
	
	Если Форма.ВидВычета <> ВидВычетаПоУмолчанию() Тогда
		Элементы.ГруппаВосстановлениеВычета.Видимость = Ложь;
	Иначе
		Элементы.ГруппаВосстановлениеВычета.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ВидВычета.Доступность = (Форма.ДатаОткрытияИИС >= '20240101');
	Элементы.ГруппаЗаполнениеПоДаннымЕГРПолеВвода.Видимость = Форма.ЗаполнениеРеквизитовПлашкой;
	
	ПомощникЗаполнения3НДФЛКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИРаспределитьСуммуВычета()
	
	Если СуммаВычета = РаспределениеПоДоходам.Итог("СуммаВычета") Тогда
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиентСервер.РаспределитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АктуализироватьДанныеОСуммеВычета()
	
	ПересчитатьИРаспределитьСуммуВычета();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ОповещениеПослеЗаполненияПоИНН()
	
	Возврат Новый ОписаниеОповещения("ПослеЗаполненияПоИНН", ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗаполненияПоИНН(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
		ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВключитьЗаполнениеПоИНН()
	
	ОтключитьЗаполнениеПоИНН = Неопределено;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВидВычетаПоУмолчанию()
	
	Возврат "ПополнениеСчета";
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредельнаяСуммаПоВидуВычета(ВидВычета, ВыбраннаяФорма)
	
	ПределыВычетов = ПомощникЗаполнения3НДФЛ.ПределыВычетов(ВыбраннаяФорма);
	
	Если ВидВычета = ВидВычетаПоУмолчанию() Тогда
		Возврат ПределыВычетов.ИнвестиционныйВычет;
	Иначе
		Возврат ПределыВычетов.ДолгосрочныеСбереженияИИС;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииВариантаРаспределенияВычета()
	
	ПомощникЗаполнения3НДФЛКлиент.ВариантРаспределенияВычетаПриИзменении(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти