#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ОтключитьЗаполнениеПоИНН;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыбраннаяФорма = Параметры.Декларация3НДФЛВыбраннаяФорма;
	
	СписокКодовВычета = ВидыВычета(ВыбраннаяФорма);
	Для Каждого ТекущийВычет Из СписокКодовВычета Цикл
		Элементы.ВидВычета.СписокВыбора.Добавить(ТекущийВычет.Значение, ТекущийВычет.Представление);
	КонецЦикла;
	
	ВидВычета = СписокКодовВычета[0].Значение;
	ВидКонтрагента = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
	ЗаполнениеРеквизитовПлашкой = Истина;
	
	СтруктураДоходовВычетов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтруктураДоходовВычетов");
	Если ЗначениеЗаполнено(СтруктураДоходовВычетов) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДоходовВычетов);
		ЗаполнениеРеквизитовПлашкой = НЕ ЗначениеЗаполнено(СуммаВычета);
	КонецЕсли;
	
	ЦветВыделенияНекорректногоЗначение = ЦветаСтиля.ЦветВыделенияКонтрагентаСОшибкой;
	
	Если ЗначениеЗаполнено(ИНН) ИЛИ ЗНачениеЗаполнено(КПП) Тогда
		ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Истина);
	КонецЕсли;
	
	НастройкиРаспределенияВычетов = ПомощникЗаполнения3НДФЛ.НастройкиРаспределенияВычетов(
		ЭтотОбъект,
		Параметры.ДоступныеДоходы,
		СтруктураДоходовВычетов);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиРаспределенияВычетов,
		"ВариантРаспределенияВычета, ВидДохода, СуммаВычета");
	
	РаспределениеПоДоходам.Загрузить(НастройкиРаспределенияВычетов.РаспределениеПоДоходам);
	Элементы.ВидДоходаВычета.СписокВыбора.ЗагрузитьЗначения(НастройкиРаспределенияВычетов.СписокВыбораДохода);
	
	ПересчитатьИРаспределитьСуммуВычета();
	ПредельнаяСуммаВычета = ПредельнаяСуммаВычета(ВыбраннаяФорма, ВидВычета);
	
	УправлениеФормой(ЭтотОбъект);
	
	ПомощникЗаполнения3НДФЛКлиентСервер.УстановитьКлючСохраненияПоложенияОкна(ЭтотОбъект,
		Формат(ВариантРаспределенияВычета, "ЧЦ=1; ЧДЦ=0;"));
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПомощникЗаполнения3НДФЛ.ПроверитьЗаполнениеРеквизитовИсточникаДоходов(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидВычетаПриИзменении(Элемент)
	
	ПредельнаяСуммаВычета = ПредельнаяСуммаВычета(ВыбраннаяФорма, ВидВычета);
	УстановитьЭлементыПоВидуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	ИНН = СокрП(ИНН);
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КПППриИзменении(Элемент)
	
	КПП = СокрП(КПП);
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаИНННаименованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ПолеПоискаИНННаименование)
		И НЕ ЗначениеЗаполнено(ИНН) 
		И НЕ ЗначениеЗаполнено(Наименование) Тогда
		
		ПомощникЗаполнения3НДФЛКлиент.ЗаполнитьРеквизитыПоДаннымЕГР(ПолеПоискаИНННаименование, ОповещениеПослеЗаполненияПоИНН());
		ОтключитьЗаполнениеПоИНН = Истина;
		ПодключитьОбработчикОжидания("Подключаемый_ВключитьЗаполнениеПоИНН", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаВзносовПриИзменении(Элемент)
	АктуализироватьДанныеОСуммеВычета();
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияВычетаОдинДоходПриИзменении(Элемент)
	
	ПомощникЗаполнения3НДФЛКлиент.ВариантРаспределенияВычетаПриИзменении(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияВычетаПриИзменении(Элемент)
	
	ПомощникЗаполнения3НДФЛКлиент.ВариантРаспределенияВычетаПриИзменении(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоходаВычетаПриИзменении(Элемент)
	
	ПомощникЗаполнения3НДФЛКлиент.ВидДоходаВычетаПриИзменении(ВидДохода, СуммаВычета, РаспределениеПоДоходам);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаспределениеПоДоходам

&НаКлиенте
Процедура РаспределениеПоДоходамПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ПомощникЗаполнения3НДФЛКлиент.РаспределениеПоДоходамПередОкончаниемРедактирования(
		ЭтотОбъект, ОтменаРедактирования, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеПоДоходамПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РаспределеноВычетаВсего = РаспределениеПоДоходам.Итог("СуммаВычета");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Информация = СтрШаблон(НСтр("ru = 'Взносы по договору №%1 от %2 с %3'"),
		НомерДоговора, Формат(ДатаДоговора, "ДЛФ=D"), Наименование);
	
	СтруктураОтвета = Новый Структура;
	СтруктураОтвета.Вставить("Вид", ПредопределенноеЗначение("Перечисление.ВычетыФизическихЛиц.ДобровольныеПенсионныеВзносыИСтрахованиеЖизни"));
	СтруктураОтвета.Вставить("Информация",    Информация);
	СтруктураОтвета.Вставить("ВидВычета",     ВидВычета);
	СтруктураОтвета.Вставить("ВидДоговора",   КодВидаДоговораПоВидуВычета(ВидВычета, ВыбраннаяФорма));
	СтруктураОтвета.Вставить("ДатаДоговора",  ДатаДоговора);
	СтруктураОтвета.Вставить("НомерДоговора", НомерДоговора);
	СтруктураОтвета.Вставить("Наименование",  Наименование);
	СтруктураОтвета.Вставить("ИНН", ИНН);
	СтруктураОтвета.Вставить("КПП", КПП);
	
	СтруктураОтвета.Вставить("СуммаВзносов", СуммаВычета);
	СтруктураОтвета.Вставить("СуммаВычета",  СуммаВычета);
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма) Тогда
		СтруктураОтвета.Вставить("ВидДохода",
			ПомощникЗаполнения3НДФЛКлиент.СоответствиеРаспределенияПоДоходам(РаспределениеПоДоходам));
	КонецЕсли;
	
	Закрыть(СтруктураОтвета);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоИНН(Команда)
	
	Если НЕ ЗначениеЗаполнено(ИНН) Тогда
		ТекстПредупреждения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'ИНН'"));
		ПоказатьПредупреждение(, ТекстПредупреждения);
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	ИначеЕсли НЕ ОшибокПоИННнет Тогда
		ПоказатьПредупреждение(, Строка(РезультатПроверкиИНН));
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиент.ВыполнитьЗаполнениеРеквизитовПоИНН(ИНН, ОповещениеПослеЗаполненияПоИНН());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоДаннымЕГР(Команда)
	
	Если ОтключитьЗаполнениеПоИНН <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиент.ЗаполнитьРеквизитыПоДаннымЕГР(ПолеПоискаИНННаименование, ОповещениеПослеЗаполненияПоИНН());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоНаименованию(Команда)
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ТекстПредупреждения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , ЗаголовокНаименованияПоВидуВычета(ВидВычета));
		ПоказатьПредупреждение(, ТекстПредупреждения);
		ТекущийЭлемент = Элементы.Наименование;
		Возврат;
	Иначе
		ПомощникЗаполнения3НДФЛКлиент.ВыполнитьЗаполнениеРеквизитовПоНаименованию(Наименование, ОповещениеПослеЗаполненияПоИНН());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	УстановитьЭлементыПоВидуВычета(Форма);
	
	Элементы.ГруппаЗаполнениеПоДаннымЕГРПолеВвода.Видимость = Форма.ЗаполнениеРеквизитовПлашкой;
	
	Элементы.КнопкаЗаполнитьРеквизитыПоНаименованию.Видимость = Не Форма.ЗаполнениеРеквизитовПлашкой;
	Элементы.ЗаполнитьПоИНН.Видимость = Не Форма.ЗаполнениеРеквизитовПлашкой;
	
	ПомощникЗаполнения3НДФЛКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЭлементыПоВидуВычета(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.Наименование.Заголовок = ЗаголовокНаименованияПоВидуВычета(Форма.ВидВычета);
	Элементы.ПояснениеРазмераВычета.Заголовок = СтрШаблон(
		НСтр("ru = 'Вычет предоставляется в размере взносов по всем договорам, но не более %1 рублей.'"),
		Форма.ПредельнаяСуммаВычета);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокНаименованияПоВидуВычета(ВидВычета)
	
	Если ВидВычета = "ПенсионноеОбеспечение" Тогда
		Возврат НСтр("ru = 'Пенсионный фонд'");
	Иначе
		Возврат НСтр("ru = 'Страховщик'");
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция КодВидаДоговораПоВидуВычета(ВидВычета, ВыбраннаяФорма)
	
	КодыВидовДоговоров = ПомощникЗаполнения3НДФЛ.КодыВидовДоговоровДобровольныхВзносов(ВыбраннаяФорма);
	
	Если ВидВычета = "ПенсионноеОбеспечение" Тогда
		ВидДоговора = КодыВидовДоговоров.ПенсионноеОбеспечение;
	ИначеЕсли ВидВычета = "ПенсионноеСтрахование" Тогда
		ВидДоговора = КодыВидовДоговоров.ПенсионноеСтрахование;
	ИначеЕсли ВидВычета = "СтрахованиеЖизни" Тогда
		ВидДоговора = КодыВидовДоговоров.СтрахованиеЖизни;
	ИначеЕсли ВидВычета = ИдентификаторДолгосрочныеСбережения() Тогда
		ВидДоговора = КодыВидовДоговоров.ДолгосрочныеСбережения;
	Иначе
		ВидДоговора = "";
	КонецЕсли;
	
	Возврат ВидДоговора;
	
КонецФункции

#Область ЗаполнениеРеквизитовКонтрагента

&НаКлиенте
Функция ОповещениеПослеЗаполненияПоИНН()
	
	Возврат Новый ОписаниеОповещения("ПослеЗаполненияПоИНН", ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗаполненияПоИНН(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
		ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВключитьЗаполнениеПоИНН()
	
	ОтключитьЗаполнениеПоИНН = Неопределено;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПересчитатьИРаспределитьСуммуВычета()
	
	Если СуммаВычета = РаспределениеПоДоходам.Итог("СуммаВычета") Тогда
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиентСервер.РаспределитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АктуализироватьДанныеОСуммеВычета()
	
	ПересчитатьИРаспределитьСуммуВычета();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидыВычета(ВыбраннаяФорма)
	
	КодыВычетов = ПомощникЗаполнения3НДФЛ.КодыВидовДоговоровДобровольныхВзносов(ВыбраннаяФорма);
	
	Результат = Новый СписокЗначений;
	Результат.Добавить("ПенсионноеОбеспечение", "Негосударственное пенсионное обеспечение");
	Результат.Добавить("ПенсионноеСтрахование", "Добровольное пенсионное страхование");
	Результат.Добавить("СтрахованиеЖизни", "Добровольное страхование жизни");
	
	Если КодыВычетов.Свойство(ИдентификаторДолгосрочныеСбережения()) Тогда
		Результат.Добавить(ИдентификаторДолгосрочныеСбережения(), "Долгосрочные сбережения");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИдентификаторДолгосрочныеСбережения()
	
	Возврат "ДолгосрочныеСбережения";
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредельнаяСуммаВычета(ВыбраннаяФорма, ВидДоговора)
	
	ПределыВычетов = ПомощникЗаполнения3НДФЛ.ПределыВычетов(ВыбраннаяФорма);
	
	Если ВидДоговора = ИдентификаторДолгосрочныеСбережения() И ПределыВычетов.Свойство("ДолгосрочныеСбереженияНПФ") Тогда
		Возврат ПределыВычетов.ДолгосрочныеСбереженияНПФ;
	КонецЕсли;
	
	Возврат ПределыВычетов.СоциальныеВычеты;
	
КонецФункции

#КонецОбласти
