#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КонтекстныйВызов = Параметры.КонтекстныйВызов;
	ВыбраннаяФорма = Параметры.Декларация3НДФЛВыбраннаяФорма;
	
	// Загрузим список возможных стандартных вычетов на налогоплательщика
	// В форме показываем по возрастанию значений
	РазмерыВычетов = РазмерыВычетов(ВыбраннаяФорма);
	ВычетЗаСдачуНормГТО = РазмерыВычетов.ЗаСдачуНормГТО;
	
	Если Параметры.КонтекстныйВызов Тогда
		ИспользоватьВычетНаНалогоплательщика = Параметры.ПрименятьВычет;
	ИначеЕсли Не ЗначениеЗаполнено(ВычетЗаСдачуНормГТО) Тогда
		// До 2024 г. по умолчанию значение устанавливаем, т.к. форма содержит только один вычет
		ИспользоватьВычетНаНалогоплательщика = Истина;
	КонецЕсли;
	
	Элементы.РазмерВычета.СписокВыбора.Очистить();
	Для Каждого ТекущийВычет Из РазмерыВычетов.НаНалогоплательщика Цикл
		
		ПредставлениеЭлемента = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru=';%1 рубль;;%1 рубля;%1 рублей;'"), ТекущийВычет.Значение);
		Элементы.РазмерВычета.СписокВыбора.Добавить(ТекущийВычет.Значение, ПредставлениеЭлемента);
		
	КонецЦикла;
	
	Элементы.РазмерВычета.СписокВыбора.СортироватьПоЗначению();
	
	// Заполнить месяцы в таблице Стандартных вычетов
	НачальноеЗаполнениеСтандартныеВычеты();
	
	СтруктураДоходовВычетов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтруктураДоходовВычетов");
	
	Если СтруктураДоходовВычетов <> Неопределено И Не Параметры.КонтекстныйВызов Тогда
		// Восстановим реквизиты формы.
		ДанныеФормы = СтруктураДоходовВычетов.ДанныеФормы;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеФормы);
		// Преобразование размера вычета для предыдущих версий помощника заполнения
		// (ранее РазмерВычета был строковым значением по шаблону "Вычет%1")
		Если ДанныеФормы.Свойство("РазмерВычета") И ТипЗнч(ДанныеФормы.РазмерВычета) = Тип("Строка") Тогда
			РазмерВычета = Число(СтрЗаменить(ДанныеФормы.РазмерВычета, "Вычет", ""));
		КонецЕсли;
		
		// Восстановим суммы вычетов.
		СуммаВычетаПоМесяцам = Неопределено;
		
		// До 2020 г. данные о вычете хранились в виде структуры: наиманование месяца и сумма вычета
		Если ДанныеФормы.Свойство("СуммаВычетаПоМесяцам", СуммаВычетаПоМесяцам)
			И ТипЗнч(СуммаВычетаПоМесяцам) = Тип("Структура") Тогда
			
			Для Каждого СтрокаВычета Из СтандартныйВычет Цикл
				СтрокаВычета.СуммаВычета1 = СуммаВычетаПоМесяцам[СтрокаВычета.Месяц1];
				СтрокаВычета.СуммаВычета2 = СуммаВычетаПоМесяцам[СтрокаВычета.Месяц2];
			КонецЦикла;
			
		ИначеЕсли ЗначениеЗаполнено(СуммаВычетаПоМесяцам) Тогда
			
			Для Каждого СтрокаВычета Из СтандартныйВычет Цикл
				СтрокаВычета.СуммаВычета1 = СуммаВычетаИзСохраненныхДанных(СуммаВычетаПоМесяцам, СтрокаВычета.НомерМесяца1);
				СтрокаВычета.СуммаВычета2 = СуммаВычетаИзСохраненныхДанных(СуммаВычетаПоМесяцам, СтрокаВычета.НомерМесяца2);
			КонецЦикла;
			
		Иначе
			// Вычет за героев, чернобыльцев и пр. не установлен
			Для Каждого СтрокаВычета Из СтандартныйВычет Цикл
				СтрокаВычета.СуммаВычета1 = 0;
				СтрокаВычета.СуммаВычета2 = 0;
			КонецЦикла;
		КонецЕсли;
		
		ИспользоватьВычетНаНалогоплательщика = СтандартныйВычет.Итог("СуммаВычета1") + СтандартныйВычет.Итог("СуммаВычета2") > 0;
		ПрименяетсяВычетЗаСдачуНормГТО = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			СтруктураДоходовВычетов,
			"СданыНормыГТО",
			Ложь);
		
	КонецЕсли;
	
	НастройкиРаспределенияВычетов = ПомощникЗаполнения3НДФЛ.НастройкиРаспределенияВычетов(
		ЭтотОбъект,
		Параметры.ДоступныеДоходы,
		СтруктураДоходовВычетов);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиРаспределенияВычетов,
		"ВариантРаспределенияВычета, ВидДохода, СуммаВычета");
	
	РаспределениеПоДоходам.Загрузить(НастройкиРаспределенияВычетов.РаспределениеПоДоходам);
	Элементы.ВидДоходаВычета.СписокВыбора.ЗагрузитьЗначения(НастройкиРаспределенияВычетов.СписокВыбораДохода);
	
	Если ЗначениеЗаполнено(ВычетЗаСдачуНормГТО) Тогда
		Элементы.ПрименяетсяВычетЗаСдачуНормГТО.Видимость = Истина;
		Элементы.СтандартныйВычетПояснение.Видимость = Ложь;
		Элементы.ИспользоватьВычет.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ВычетНаПлательщикаПояснение.Видимость = Истина;
	Иначе
		Элементы.ПрименяетсяВычетЗаСдачуНормГТО.Видимость = Ложь;
		Элементы.СтандартныйВычетПояснение.Видимость = Истина;
		Элементы.ИспользоватьВычет.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		Элементы.ВычетНаПлательщикаПояснение.Видимость = Ложь;
	КонецЕсли;
	
	РассчитатьСуммуПоложенногоВычета(ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	УправлениеФормой(ЭтотОбъект);
	
	КлючСохраненияПоложенияОкна = СтрШаблон("%1%2%3%4",
		Год(Параметры.Период),
		ВычетПрименяется,
		КонтекстныйВызов,
		ВариантРаспределенияВычета);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма) Тогда
		
		РаспределеннаяСумма = РаспределениеПоДоходам.Итог("СуммаВычета");
		Если РаспределеннаяСумма < СуммаВычета 
			И РаспределениеПоДоходам.Итог("НалоговаяБаза") > РаспределеннаяСумма Тогда
			
			ТекстСообщения = НСтр("ru = 'Сумма указанного вычета не распределена по доходам'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПрименяетсяВычетЗаСдачуНормГТОПриИзменении(Элемент)
	
	РассчитатьСуммуПоложенногоВычета(ЭтотОбъект);
	ПомощникЗаполнения3НДФЛКлиент.ВидДоходаВычетаПриИзменении(ВидДохода, СуммаВычета, РаспределениеПоДоходам);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьВычетПриИзменении(Элемент)
	
	Если ВычетПрименяется = "Год" Тогда
		УстановитьЗначенияПоУмолчанию();
	КонецЕсли;
	
	РассчитатьСуммуПоложенногоВычета(ЭтотОбъект);
	ПомощникЗаполнения3НДФЛКлиент.ВидДоходаВычетаПриИзменении(ВидДохода, СуммаВычета, РаспределениеПоДоходам);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РазмерВычетаПриИзменении(Элемент)
	
	УстановитьСуммуВычета(ЭтотОбъект);
	РассчитатьСуммуПоложенногоВычета(ЭтотОбъект);
	ПомощникЗаполнения3НДФЛКлиент.ВидДоходаВычетаПриИзменении(ВидДохода, СуммаВычета, РаспределениеПоДоходам);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВычетПрименяетсяПриИзменении(Элемент)
	
	Если ВычетПрименяется = "Год" Тогда
		УстановитьЗначенияПоУмолчанию();
	КонецЕсли;
	
	РассчитатьСуммуПоложенногоВычета(ЭтотОбъект);
	ПомощникЗаполнения3НДФЛКлиент.ВидДоходаВычетаПриИзменении(ВидДохода, СуммаВычета, РаспределениеПоДоходам);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияВычета_ОдинДоходПриИзменении(Элемент)
	
	ПриИзмененииВариантаРаспределенияВычета();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидДоходаВычетаПриИзменении(Элемент)
	
	ПомощникЗаполнения3НДФЛКлиент.ВидДоходаВычетаПриИзменении(ВидДохода, СуммаВычета, РаспределениеПоДоходам);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантРаспределенияВычетаПриИзменении(Элемент)
	
	ПриИзмененииВариантаРаспределенияВычета();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтандартныйВычет

&НаКлиенте
Процедура СтандартныйВычетПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Элементы.СтандартныйВычет.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.СтандартныйВычет.ТекущийЭлемент.Имя = "СтандартныйВычетСуммаВычета1"
		Или Элементы.СтандартныйВычет.ТекущийЭлемент.Имя = "СтандартныйВычетСуммаВычета2" Тогда
		
		РассчитатьСуммуПоложенногоВычета(ЭтотОбъект);
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаспределениеПоДоходам

&НаКлиенте
Процедура РаспределениеПоДоходамПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ПомощникЗаполнения3НДФЛКлиент.РаспределениеПоДоходамПередОкончаниемРедактирования(
		ЭтотОбъект, ОтменаРедактирования, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеПоДоходамПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	РаспределеноВычетаВсего = РаспределениеПоДоходам.Итог("СуммаВычета");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СтруктураРезультата = Новый Структура;
	// Информация для формы помощника.
	
	СтруктураРезультата.Вставить("Вид", 
		ПредопределенноеЗначение("Перечисление.ВычетыФизическихЛиц.СтандартныйНаНалогоплательщика"));
	СтруктураРезультата.Вставить("Информация", НСтр("ru='Стандартный вычет на налогоплательщика'"));
	
	ДанныеВычета = РассчитатьСуммуПоложенногоВычета(ЭтотОбъект);
	СтруктураРезультата.Вставить("СуммаВычета", ДанныеВычета.СуммаВычета);
	
	СтруктураРезультата.Вставить("ДанныеДекларации", ДанныеВычета.СтруктураДанныхДекларации);
	СтруктураРезультата.Вставить("СведенияОВычетах", ИтоговаяТаблицаВычетов(ДанныеВычета.РазмерыВычетов));
	
	// Данные формы для восстановления.
	СтруктураДанныхФормы = Новый Структура;
	СтруктураДанныхФормы.Вставить("СуммаВычетаПоМесяцам", ДанныеВычета.СуммаВычетаПоМесяцам);
	СтруктураДанныхФормы.Вставить("ВычетПрименяется",     ВычетПрименяется);
	СтруктураДанныхФормы.Вставить("РазмерВычета",         РазмерВычета);
	СтруктураРезультата.Вставить("ДанныеФормы",           СтруктураДанныхФормы);
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма) Тогда
		СтруктураРезультата.Вставить("ВидДохода",
			ПомощникЗаполнения3НДФЛКлиент.СоответствиеРаспределенияПоДоходам(РаспределениеПоДоходам));
	КонецЕсли;
	
	СтруктураРезультата.Вставить("СданыНормыГТО", ПрименяетсяВычетЗаСдачуНормГТО);
	
	Закрыть(СтруктураРезультата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДобавитьМесяцы(НомерМесяца1, НомерМесяца2 = Неопределено)
	
	СтрокаВычета = СтандартныйВычет.Добавить();
	СтрокаВычета.Месяц1 = ОбщегоНазначенияБПКлиентСервер.ПредставлениеМесяца(НомерМесяца1);
	СтрокаВычета.НомерМесяца1 = НомерМесяца1;
	
	Если НомерМесяца2 <> Неопределено Тогда
		СтрокаВычета.Месяц2 = ОбщегоНазначенияБПКлиентСервер.ПредставлениеМесяца(НомерМесяца2);
		СтрокаВычета.НомерМесяца2 = НомерМесяца2;
	КонецЕсли;
	
	Возврат СтрокаВычета;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяПоляСуммаВычета(СуммаВычета)
	
	Возврат СтрШаблон("СуммаВычета%1", Формат(СуммаВычета, "ЧГ=0"));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторВычетаПоЗначению(РазмерыВычетов, Значение)
	
	Идентификатор = "";
	Для Каждого ТекущийВычет Из РазмерыВычетов Цикл
		Если Значение = ТекущийВычет.Значение Тогда
			Идентификатор = ТекущийВычет.Ключ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Идентификатор;
	
КонецФункции

&НаСервере
Функция ИтоговаяТаблицаВычетов(РазмерыВычетов)
	
	Если Не ИспользоватьВычетНаНалогоплательщика Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВычетыПоПорядку = Новый ТаблицаЗначений;
	ВычетыПоПорядку.Колонки.Добавить("НомерМесяца", ОбщегоНазначения.ОписаниеТипаЧисло(2));
	ВычетыПоПорядку.Колонки.Добавить("СуммаВычета", ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Для Каждого ТекущаяСтрока Из СтандартныйВычет Цикл
		
		НоваяСтрока = ВычетыПоПорядку.Добавить();
		НоваяСтрока.НомерМесяца = ТекущаяСтрока.НомерМесяца1;
		НоваяСтрока.СуммаВычета = ТекущаяСтрока.СуммаВычета1;
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.НомерМесяца2) Тогда
			НоваяСтрока = ВычетыПоПорядку.Добавить();
			НоваяСтрока.НомерМесяца = ТекущаяСтрока.НомерМесяца2;
			НоваяСтрока.СуммаВычета = ТекущаяСтрока.СуммаВычета2;
		КонецЕсли;
		
	КонецЦикла;
	
	ВычетыПоПорядку.Сортировать("НомерМесяца");
	НомерСтроки = 0;
	ДанныеРезультат = Новый Массив;
	КоличествоСтрок = ВычетыПоПорядку.Количество();
	
	Пока НомерСтроки < КоличествоСтрок Цикл
		
		ТекущаяСтрока = ВычетыПоПорядку[НомерСтроки];
		
		СуммаВычета = 0;
		Для СледующийНомерСтроки = НомерСтроки + 1 По КоличествоСтрок - 1 Цикл
			
			Если ТекущаяСтрока.СуммаВычета <> ВычетыПоПорядку[СледующийНомерСтроки].СуммаВычета Тогда
				Прервать;
			КонецЕсли;
			
			СуммаВычета = СуммаВычета + ВычетыПоПорядку[СледующийНомерСтроки].СуммаВычета;
			
		КонецЦикла;
		
		СтруктураЗначения = Новый Структура("НомерМесяца, СуммаВычета, ВидВычета");
		ЗаполнитьЗначенияСвойств(СтруктураЗначения, ТекущаяСтрока);
		Если Не ИспользоватьВычетНаНалогоплательщика Тогда
			СтруктураЗначения.СуммаВычета = 0;
		Иначе
			СтруктураЗначения.ВидВычета = ИдентификаторВычетаПоЗначению(РазмерыВычетов, СтруктураЗначения.СуммаВычета);
		КонецЕсли;
		
		ДанныеРезультат.Добавить(СтруктураЗначения);
		НомерСтроки = СледующийНомерСтроки;
		
	КонецЦикла;
	
	Возврат ДанныеРезультат;
	
КонецФункции

&НаСервере
Процедура НачальноеЗаполнениеСтандартныеВычеты()
	
	КоличествоСтрокВКолонке = 6;
	ДанныеВычета = Неопределено;
	
	ПараметрыВыполнения = УчетНДФЛПредпринимателя.НовыйПараметрыВычета();
	ЗаполнитьЗначенияСвойств(ПараметрыВыполнения, Параметры);
	ПараметрыВыполнения.ВыбраннаяФорма = ВыбраннаяФорма;
	ПараметрыВыполнения.ИмяОбъектаМетаданных = "РегистрСведений.ИПСтандартныеВычетыНаНалогоплательщика";
	
	Если КонтекстныйВызов Тогда
		ПараметрыВыполнения.КоличествоМесяцев = Месяц(Параметры.КонецПериода) - Месяц(Параметры.НачалоПериода) + 1;
	Иначе
		ПараметрыВыполнения.НачалоПериода     = НачалоГода(Параметры.Период);
		ПараметрыВыполнения.КонецПериода      = КонецГода(Параметры.Период);
		ПараметрыВыполнения.КоличествоМесяцев = 12;
	КонецЕсли;
	
	ДанныеВычета = УчетНДФЛПредпринимателя.ДанныеВычетаПоМесяцамЗаПериод(ПараметрыВыполнения);
	
	ЗаполнитьСумму = ЗначениеЗаполнено(ДанныеВычета);
	
	Если ЗаполнитьСумму Тогда
		СуммаВычета = ДанныеВычета[0].СуммаВычета;
		КоличествоМесяцевВычета = ДанныеВычета.Количество();
	Иначе
		
		СуммаВычета = 0;
		Если КонтекстныйВызов Тогда
			КоличествоМесяцевВычета = Месяц(Параметры.КонецПериода) - Месяц(Параметры.НачалоПериода) + 1;
		Иначе
			КоличествоМесяцевВычета = 12;
		КонецЕсли;
		
	КонецЕсли;
	
	Если КонтекстныйВызов Тогда
		ИспользоватьВычетНаНалогоплательщика = ЗаполнитьСумму И ДанныеВычета.Итог("СуммаВычета") <> 0;
	ИначеЕсли Не ЗначениеЗаполнено(ВычетЗаСдачуНормГТО) Тогда
		ИспользоватьВычетНаНалогоплательщика = Истина;
	Иначе
		ЗаполнитьСумму = Ложь;
		ИспользоватьВычетНаНалогоплательщика = Ложь;
	КонецЕсли;
	
	СуммаВычетаИзменяется = Ложь;
	КоличествоИтераций = Мин(КоличествоСтрокВКолонке, КоличествоМесяцевВычета);
	
	Для НомерМесяца = 1 По КоличествоИтераций Цикл
		
		УчитыватьВторойМесяц = КоличествоМесяцевВычета >= НомерМесяца + КоличествоСтрокВКолонке;
		Если УчитыватьВторойМесяц Тогда
			
			НоваяСтрока = ДобавитьМесяцы(НомерМесяца, НомерМесяца + КоличествоСтрокВКолонке);
			
			Если ЗаполнитьСумму Тогда
				НоваяСтрока.СуммаВычета2 = ДанныеВычета[НомерМесяца + КоличествоСтрокВКолонке - 1].СуммаВычета;
			КонецЕсли;
			
		Иначе
			
			НоваяСтрока = ДобавитьМесяцы(НомерМесяца);
			
		КонецЕсли;
		
		Если ЗаполнитьСумму Тогда
			НоваяСтрока.СуммаВычета1 = ДанныеВычета[НомерМесяца - 1].СуммаВычета;
		КонецЕсли;
		
		Если СуммаВычета <> НоваяСтрока.СуммаВычета1 Тогда
			
			СуммаВычетаИзменяется = Истина;
			
		ИначеЕсли УчитыватьВторойМесяц И СуммаВычета <> НоваяСтрока.СуммаВычета2 Тогда
			
			СуммаВычетаИзменяется = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	КоличествоДанныхВычета = ДанныеВычета.Количество();
	
	Если КоличествоДанныхВычета > 0 Тогда
		ПрименяетсяВычетЗаСдачуНормГТО = ДанныеВычета[КоличествоДанныхВычета - 1].СданыНормыГТО;
	КонецЕсли;
	
	Если СуммаВычетаИзменяется И ЗаполнитьСумму Тогда
		ВычетПрименяется = "НесколькоМесяцевВГоду";
	Иначе
		ВычетПрименяется = "Год";
		ОпределитьСуммуВычетаПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьСуммуВычетаПоУмолчанию()
	
	ВычетПрименяется = "Год";
	
	СуммаСредняя = (СтандартныйВычет.Итог("СуммаВычета1") + СтандартныйВычет.Итог("СуммаВычета2")) / 12;
	
	Если Элементы.РазмерВычета.СписокВыбора.НайтиПоЗначению(СуммаСредняя) <> Неопределено Тогда
		РазмерВычета = СуммаСредняя;
	Иначе
		РазмерВычета = Элементы.РазмерВычета.СписокВыбора[0].Значение;
	КонецЕсли;
	
	УстановитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазмерыВычетов(ВыбраннаяФорма)
	
	Результат = Новый Структура;
	
	РазмерыВычетов = ПомощникЗаполнения3НДФЛ.РазмерыВычетов(ВыбраннаяФорма);
	
	Результат.Вставить("НаНалогоплательщика", РазмерыВычетов.СтандартныйВычетНаНалогоплательщика);
	Результат.Вставить("ЗаСдачуНормГТО", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		РазмерыВычетов,
		"СтандартныйВычетЗаСдачуНормГТО", 0));
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СуммаВычетаИзСохраненныхДанных(МассивСтруктур, НомерМесяца)
	
	СуммаВычета = 0;
	
	Для Каждого ТекущиеДанные Из МассивСтруктур Цикл
		
		Если ТекущиеДанные.НомерМесяца = НомерМесяца Тогда
			СуммаВычета = ТекущиеДанные.СуммаВычета;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СуммаВычета;
	
КонецФункции

&НаСервере
Процедура УстановитьЗначенияПоУмолчанию()
	
	ВычетПрименяется = "Год";
	ОпределитьСуммуВычетаПоУмолчанию();
	УстановитьСуммуВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСуммуВычета(Форма)
	
	Сумма = Форма.РазмерВычета;
	
	Для Каждого СтрокаВычета Из Форма.СтандартныйВычет Цикл
		СтрокаВычета.СуммаВычета1 = Сумма;
		СтрокаВычета.СуммаВычета2 = Сумма;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ПрименяетсяВычетГТО = ЗначениеЗаполнено(Форма.ВычетЗаСдачуНормГТО);
	
	Элементы.ИспользоватьВычет.Видимость = Форма.КонтекстныйВызов Или ПрименяетсяВычетГТО;
	
	Если ПрименяетсяВычетГТО Тогда
		Элементы.ВычетПрименяется.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Иначе
		Элементы.ВычетПрименяется.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	КонецЕсли;
	
	Если Форма.ВычетПрименяется = "Год" Тогда
		Элементы.СтраницыПрименениеВычета.ТекущаяСтраница = Элементы.СтраницаВесьГод;
		Элементы.СтандартныйВычет.Видимость = Ложь;
	Иначе
		Элементы.СтраницыПрименениеВычета.ТекущаяСтраница = Элементы.СтраницаНесколькоМесяцевВГоду;
		Элементы.СтандартныйВычет.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ВычетПрименяется.Доступность = Форма.ИспользоватьВычетНаНалогоплательщика;
	Элементы.СтраницыПрименениеВычета.Доступность = Форма.ИспользоватьВычетНаНалогоплательщика;
	
	Если Форма.ПрименяетсяВычетЗаСдачуНормГТО Или Форма.ИспользоватьВычетНаНалогоплательщика Тогда
		ПомощникЗаполнения3НДФЛКлиентСервер.УправлениеФормой(Форма);
	Иначе
		Элементы.ГруппаВидДохода.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СтандартныйВычетСуммаВычета2");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СтандартныйВычет.Месяц2", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РассчитатьСуммуПоложенногоВычета(Форма)
	
	Форма.СуммаВычета = 0;
	
	СуммаВычетаПоМесяцам = Новый Массив;
	РазмерыВычетов = РазмерыВычетов(Форма.ВыбраннаяФорма);
	
	// Данные для декларации.
	СтруктураДанныхДекларации = Новый Структура;
	Для Каждого ТекущийВычет Из РазмерыВычетов.НаНалогоплательщика Цикл
		СтруктураДанныхДекларации.Вставить(ИмяПоляСуммаВычета(ТекущийВычет.Значение), 0);
	КонецЦикла;
	
	Если Форма.ИспользоватьВычетНаНалогоплательщика Тогда
		
		Для Каждого СтрокаВычета Из Форма.СтандартныйВычет Цикл
			
			ИмяПоля = ИмяПоляСуммаВычета(СтрокаВычета.СуммаВычета1);
			Если ЗначениеЗаполнено(СтрокаВычета.НомерМесяца1) Тогда
				
				СтруктураЗначения = Новый Структура("НомерМесяца, СуммаВычета, ВидВычета");
				СтруктураЗначения.НомерМесяца = СтрокаВычета.НомерМесяца1;
				СтруктураЗначения.СуммаВычета = СтрокаВычета.СуммаВычета1;
				СтруктураЗначения.ВидВычета   = ИдентификаторВычетаПоЗначению(
					РазмерыВычетов.НаНалогоплательщика,
					СтрокаВычета.СуммаВычета1);
				СуммаВычетаПоМесяцам.Добавить(СтруктураЗначения);
				
				Если ЗначениеЗаполнено(СтрокаВычета.СуммаВычета1) Тогда
					СтруктураДанныхДекларации[ИмяПоля] = СтруктураДанныхДекларации[ИмяПоля] + СтрокаВычета.СуммаВычета1;
					Форма.СуммаВычета = Форма.СуммаВычета + СтрокаВычета.СуммаВычета1;
				КонецЕсли;
				
			КонецЕсли;
			
			ИмяПоля = ИмяПоляСуммаВычета(СтрокаВычета.СуммаВычета2);
			Если ЗначениеЗаполнено(СтрокаВычета.НомерМесяца2) Тогда
				
				СтруктураЗначения = Новый Структура("НомерМесяца, СуммаВычета, ВидВычета");
				СтруктураЗначения.НомерМесяца = СтрокаВычета.НомерМесяца2;
				СтруктураЗначения.СуммаВычета = СтрокаВычета.СуммаВычета2;
				СтруктураЗначения.ВидВычета   = ИдентификаторВычетаПоЗначению(
					РазмерыВычетов.НаНалогоплательщика,
					СтрокаВычета.СуммаВычета2);
				СуммаВычетаПоМесяцам.Добавить(СтруктураЗначения);
				
				Если ЗначениеЗаполнено(СтрокаВычета.СуммаВычета2) Тогда
					СтруктураДанныхДекларации[ИмяПоля] = СтруктураДанныхДекларации[ИмяПоля] + СтрокаВычета.СуммаВычета2;
					Форма.СуммаВычета = Форма.СуммаВычета + СтрокаВычета.СуммаВычета2;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		Форма.СуммаВычета = 0;
	КонецЕсли;
	
	Если Форма.ПрименяетсяВычетЗаСдачуНормГТО Тогда
		Форма.СуммаВычета = Форма.СуммаВычета + Форма.ВычетЗаСдачуНормГТО;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("СуммаВычета", Форма.СуммаВычета);
	Результат.Вставить("РазмерыВычетов", РазмерыВычетов.НаНалогоплательщика);
	Результат.Вставить("СтруктураДанныхДекларации", СтруктураДанныхДекларации);
	Результат.Вставить("СуммаВычетаПоМесяцам", СуммаВычетаПоМесяцам);
	Результат.Вставить("СданыНормыГТО", Форма.ПрименяетсяВычетЗаСдачуНормГТО);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииВариантаРаспределенияВычета()
	
	ПомощникЗаполнения3НДФЛКлиент.ВариантРаспределенияВычетаПриИзменении(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти