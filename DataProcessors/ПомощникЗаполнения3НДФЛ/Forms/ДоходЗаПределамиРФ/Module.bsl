#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодОтчетности = Параметры.Период;
	Декларация3НДФЛВыбраннаяФорма = Параметры.Декларация3НДФЛВыбраннаяФорма;
	
	СтруктураДоходовВычетов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтруктураДоходовВычетов");
	ДанныеФормы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураДоходовВычетов, "ДанныеФормы");
	
	Если ЗначениеЗаполнено(СтруктураДоходовВычетов) И ЗначениеЗаполнено(ДанныеФормы) Тогда
		ЗаполнитьФормуИзДанных(ДанныеФормы);
	Иначе
		ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
		СтранаЗачисления = Справочники.СтраныМира.Россия;
		ДействуетДоговорОбИзбежанииДвойногоНалогообложения = ПризнакИзбежанияДвойногоНалогообложения(Страна,
			ПериодОтчетности);
	КонецЕсли;
	
	СтавкаНалога = Обработки.ПомощникЗаполнения3НДФЛ.СтавкаНалога(Декларация3НДФЛВыбраннаяФорма);
	
	УправлениеФормойПриСозданииНаСервере();
	УправлениеФормой(ЭтотОбъект);
	УстановитьКлючСохраненияПоложенияОкна(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Страна = Справочники.СтраныМира.Россия Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Поле", "Корректность",
			НСтр("ru = 'Страна'"), , , 
			НСтр("ru = 'Для отражения дохода, полученного в РФ, используйте другие формы помощника.'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Страна", , Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтранаИсточникаПриИзменении(Элемент)
	
	ДействуетДоговорОбИзбежанииДвойногоНалогообложения = ПризнакИзбежанияДвойногоНалогообложения(Страна, ПериодОтчетности);
	УправлениеФормой(ЭтотОбъект);
	УстановитьКлючСохраненияПоложенияОкна(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДоходаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ДатаДохода) ИЛИ ЗначениеЗаполнено(ДатаУплатыНалога) Тогда
		ВалютаДоходаПриИзмененииНаСервере();
	КонецЕсли;
	
	РассчитатьСуммуДоходаВРублях();
	РассчитатьСуммуНалогаВРублях();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаДоходаПриИзменении(Элемент)
	
	КурсВалюты = КурсВалюты(ВалютаДохода, ДатаДохода);
	КурсНаДатуПолученияДохода           = КурсВалюты.Курс;
	КратностьКурсаНаДатуПолученияДохода = КурсВалюты.Кратность;
	РассчитатьСуммуДоходаВРублях();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаУплатыНалогаПриИзменении(Элемент)
	
	КурсВалюты = КурсВалюты(ВалютаДохода, ДатаУплатыНалога);
	КурсНаДатуУплатыНалога           = КурсВалюты.Курс;
	КратностьКурсаНаДатуУплатыНалога = КурсВалюты.Кратность;
	РассчитатьСуммуНалогаВРублях();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаНалогВВалютеПриИзменении(Элемент)
	РассчитатьСуммуНалогаВРублях();
КонецПроцедуры

&НаКлиенте
Процедура КурсНаДатуПолученияДоходаПриИзменении(Элемент)
	РассчитатьСуммуДоходаВРублях();
КонецПроцедуры

&НаКлиенте
Процедура КратностьКурсаНаДатуПолученияДоходаПриИзменении(Элемент)
	РассчитатьСуммуДоходаВРублях();
КонецПроцедуры

&НаКлиенте
Процедура КурсНаДатуУплатыНалогаПриИзменении(Элемент)
	РассчитатьСуммуНалогаВРублях();
КонецПроцедуры

&НаКлиенте
Процедура КратностьКурсаНаДатуУплатыНалогаПриИзменении(Элемент)
	РассчитатьСуммуНалогаВРублях();
КонецПроцедуры

&НаКлиенте
Процедура СуммаДоходаВВалютеПриИзменении(Элемент)
	РассчитатьСуммуДоходаВРублях();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВсеВалюты(Команда)
	
	ОткрытьФорму("Справочник.Валюты.ФормаСписка", ,ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	
	СуммаНалогаИсчисленная = СуммаДоходаВРублях * СтавкаНалога / 100;
	Если ДействуетДоговорОбИзбежанииДвойногоНалогообложения Тогда
		СуммаНалогаПодлежащаяЗачету = Мин(СуммаНалогаВРублях, СуммаНалогаИсчисленная);
	Иначе
		СуммаНалогаПодлежащаяЗачету = 0;
	КонецЕсли;
	
	// Информация для формы помощника.
	СтруктураРезультата.Вставить("Вид", ПредопределенноеЗначение("Перечисление.ИсточникиДоходовФизическихЛиц.ДоходЗаПределамиРФ"));
	СтруктураРезультата.Вставить("Информация", СтрШаблон(НСтр("ru='Доход за пределами РФ (%1)'"), НаименованиеИсточника));
	СтруктураРезультата.Вставить("СуммаДохода", СуммаДоходаВРублях);
	СтруктураРезультата.Вставить("СуммаВычета", СуммаНалогаПодлежащаяЗачету);
	
	КурсДохода = ПомощникЗаполнения3НДФЛКлиентСервер.КурсВалютыДляОтчетности(
		КурсНаДатуПолученияДохода,
		КратностьКурсаНаДатуПолученияДохода);
	
	КурсУплатыНалога = ПомощникЗаполнения3НДФЛКлиентСервер.КурсВалютыДляОтчетности(
		КурсНаДатуУплатыНалога,
		КратностьКурсаНаДатуУплатыНалога);
	
	// Данные для декларации.
	СтруктураДанныхДекларации = Новый Структура;
	СтруктураДанныхДекларации.Вставить("Страна", Страна);
	СтруктураДанныхДекларации.Вставить("СтранаЗачисления", СтранаЗачисления);
	СтруктураДанныхДекларации.Вставить("СтавкаНалога", СтавкаНалога);
	СтруктураДанныхДекларации.Вставить("ИсточникДохода", НаименованиеИсточника);
	СтруктураДанныхДекларации.Вставить("Валюта", ВалютаДохода);
	СтруктураДанныхДекларации.Вставить("ДатаПолученияДохода", ДатаДохода);
	СтруктураДанныхДекларации.Вставить("КурсНаДатуПолученияДохода", КурсДохода);
	СтруктураДанныхДекларации.Вставить("СуммаДоходаВВалюте", СуммаДоходаВВалюте);
	СтруктураДанныхДекларации.Вставить("СуммаДоходаВРублях", СуммаДоходаВРублях);
	СтруктураДанныхДекларации.Вставить("СуммаНалогаИсчисленная", СуммаНалогаИсчисленная);
	Если ДействуетДоговорОбИзбежанииДвойногоНалогообложения Тогда
		СтруктураДанныхДекларации.Вставить("ДатаУплатыНалога", ДатаУплатыНалога);
		СтруктураДанныхДекларации.Вставить("КурсНаДатуУплатыНалога", КурсУплатыНалога);
		СтруктураДанныхДекларации.Вставить("СуммаНалогаВВалюте", СуммаНалогаВВалюте);
		СтруктураДанныхДекларации.Вставить("СуммаНалогаВРублях", СуммаНалогаВРублях);
		СтруктураДанныхДекларации.Вставить("СуммаНалогаПодлежащаяЗачету", СуммаНалогаПодлежащаяЗачету);
	Иначе
		СтруктураДанныхДекларации.Вставить("ДатаУплатыНалога", '00010101');
		СтруктураДанныхДекларации.Вставить("КурсНаДатуУплатыНалога", 0);
		СтруктураДанныхДекларации.Вставить("СуммаНалогаВВалюте", 0);
		СтруктураДанныхДекларации.Вставить("СуммаНалогаВРублях", 0);
		СтруктураДанныхДекларации.Вставить("СуммаНалогаПодлежащаяЗачету", 0);
	КонецЕсли;
	
	СтруктураДанныхДекларации.Вставить("КодВидаДохода", КодПрочегоДоходаЗаПределамиРФ(
		Декларация3НДФЛВыбраннаяФорма,
		ВидДохода));
	
	СтруктураРезультата.Вставить("ДанныеДекларации", СтруктураДанныхДекларации);
	
	// Данные формы для восстановления.
	СтруктураДанныхФормы = Новый Структура;
	Для Каждого ИмяРеквизита Из МассивРеквизитовФормы() Цикл
		СтруктураДанныхФормы.Вставить(ИмяРеквизита, ЭтотОбъект[ИмяРеквизита]);
	КонецЦикла;
	СтруктураРезультата.Вставить("ДанныеФормы", СтруктураДанныхФормы);
	
	Закрыть(СтруктураРезультата);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормойПриСозданииНаСервере()
	
	Элементы.ФормаКомандаОК.Доступность = Не ТолькоПросмотр;
	Элементы.Валюты.Доступность = Не ТолькоПросмотр;
	Элементы.ВидДохода.Доступность = Не ТолькоПросмотр;
	
	Элементы.ГруппаОписание.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаСтранаИВалюта.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаДоходИностранногоГосударства.ТолькоПросмотр = ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.ДекорацияИзбежаниеДвойногоНалогообложения.Видимость = Форма.ДействуетДоговорОбИзбежанииДвойногоНалогообложения;
	Элементы.ГруппаНалогИностранногоГосударства.Видимость = Форма.ДействуетДоговорОбИзбежанииДвойногоНалогообложения;
	
	Если МожноНеУказыватьНаименованиеИсточникаДохода(Форма.Декларация3НДФЛВыбраннаяФорма) Тогда
		Элементы.Наименование.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элементы.Наименование.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(Форма.Декларация3НДФЛВыбраннаяФорма) Тогда
		Элементы.ВидДохода.Видимость = Истина;
		Элементы.СтранаЗачисления.Видимость = Истина;
	Иначе
		Элементы.ВидДохода.Видимость = Ложь;
		Элементы.СтранаЗачисления.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПризнакИзбежанияДвойногоНалогообложения(Знач Страна, Знач Период)
	
	Если ЗначениеЗаполнено(Страна) Тогда
		
		КодСтраны = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Страна, "Код");
		
		МакетСтран = Обработки.ПомощникЗаполнения3НДФЛ.ПолучитьМакет("ДоговорыОбИзбежанииДвойногоНалогообложения");
		ТаблицаСтранСДоговоромОбИзбежанииДвойногоНалогообложения = 
			ОбщегоНазначения.ПрочитатьXMLВТаблицу(МакетСтран.ПолучитьТекст());
		
		НайденнаяСтрока = ТаблицаСтранСДоговоромОбИзбежанииДвойногоНалогообложения.Данные.Найти(КодСтраны, "КодСтраны");
		
		Если НайденнаяСтрока = Неопределено Тогда
			Возврат Ложь;
		Иначе
			Если ЗначениеЗаполнено(Период) Тогда
				Возврат Период >= Дата(НайденнаяСтрока.ДатаНачалаДействия);
			Иначе
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция КурсВалюты(Знач Валюта, Знач ДатаКурса)
	
	Возврат РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, ДатаКурса);
	
КонецФункции

&НаСервере
Процедура ВалютаДоходаПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(ДатаДохода) Тогда
		КурсВалюты = КурсВалюты(ВалютаДохода, ДатаДохода);
		КурсНаДатуПолученияДохода           = КурсВалюты.Курс;
		КратностьКурсаНаДатуПолученияДохода = КурсВалюты.Кратность;
	КонецЕсли;
	Если ЗначениеЗаполнено(ДатаУплатыНалога) Тогда
		КурсВалюты = КурсВалюты(ВалютаДохода, ДатаУплатыНалога);
		КурсНаДатуУплатыНалога           = КурсВалюты.Курс;
		КратностьКурсаНаДатуУплатыНалога = КурсВалюты.Кратность;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МассивРеквизитовФормы()
	
	Массив = Новый Массив;
	Массив.Добавить("ВидДохода");
	Массив.Добавить("ВалютаДохода");
	Массив.Добавить("ДатаДохода");
	Массив.Добавить("ДатаУплатыНалога");
	Массив.Добавить("ДействуетДоговорОбИзбежанииДвойногоНалогообложения");
	Массив.Добавить("КратностьКурсаНаДатуПолученияДохода");
	Массив.Добавить("КратностьКурсаНаДатуУплатыНалога");
	Массив.Добавить("КурсНаДатуПолученияДохода");
	Массив.Добавить("КурсНаДатуУплатыНалога");
	Массив.Добавить("НаименованиеИсточника");
	Массив.Добавить("Страна");
	Массив.Добавить("СтранаЗачисления");
	Массив.Добавить("СуммаДоходаВВалюте");
	Массив.Добавить("СуммаДоходаВРублях");
	Массив.Добавить("СуммаНалогаВВалюте");
	Массив.Добавить("СуммаНалогаВРублях");
	Возврат Массив;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьФормуИзДанных(ДанныеФормы)
	
	Для Каждого ИмяРеквизита Из МассивРеквизитовФормы() Цикл
		ДанныеФормы.Свойство(ИмяРеквизита, ЭтотОбъект[ИмяРеквизита]);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДоходаВРублях()
	
	Если НЕ (ЗначениеЗаполнено(КурсНаДатуПолученияДохода) И ЗначениеЗаполнено(КратностьКурсаНаДатуПолученияДохода)) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураКурсаРуб = Новый Структура(
		"Валюта, Курс, Кратность", ВалютаРегламентированногоУчета, 1, 1);
	СтруктураКурсаВалюты = Новый Структура(
		"Валюта, Курс, Кратность", ВалютаДохода, КурсНаДатуПолученияДохода, КратностьКурсаНаДатуПолученияДохода);
	
	СуммаДоходаВРублях = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(
		СуммаДоходаВВалюте, СтруктураКурсаВалюты, СтруктураКурсаРуб);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуНалогаВРублях()
	
	Если НЕ (ЗначениеЗаполнено(КурсНаДатуУплатыНалога) И ЗначениеЗаполнено(КратностьКурсаНаДатуУплатыНалога)) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураКурсаРуб = Новый Структура(
		"Валюта, Курс, Кратность", ВалютаРегламентированногоУчета, 1, 1);
	СтруктураКурсаВалютыНаДатуУплаты = Новый Структура(
		"Валюта, Курс, Кратность", ВалютаДохода, КурсНаДатуУплатыНалога, КратностьКурсаНаДатуУплатыНалога);
	
	СуммаНалогаВРублях = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(
		СуммаНалогаВВалюте, СтруктураКурсаВалютыНаДатуУплаты, СтруктураКурсаРуб);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МожноНеУказыватьНаименованиеИсточникаДохода(Знач ВыбраннаяФорма)
	
	Возврат ПомощникЗаполнения3НДФЛ.МожноНеУказыватьНаименованиеИсточникаДохода(ВыбраннаяФорма);
	
КонецФункции

&НаСервереБезКонтекста
Функция КодПрочегоДоходаЗаПределамиРФ(Знач ВыбраннаяФорма, Знач ВидДохода)
	
	Перем КодВидаДохода;
	
	ВидыДоходов = ПомощникЗаполнения3НДФЛ.ВидыДоходов(ВыбраннаяФорма);
	ПереченьДоходовРасширен = ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма);
	
	Если ПереченьДоходовРасширен И ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаКриптовалюты(ВидДохода) Тогда
		Возврат ВидыДоходов.ОперацииСКриптовалютой;
	ИначеЕсли ПереченьДоходовРасширен И ВидДохода = "Роялти" Тогда
		Возврат ВидыДоходов.Роялти_ЗаПределамиРФ;
	ИначеЕсли ПереченьДоходовРасширен Тогда
		Возврат ВидыДоходов.ИнойДоходПоПрогрессивнойШкале;
	ИначеЕсли ВидыДоходов.Свойство("Иной", КодВидаДохода) Тогда
		Возврат КодВидаДохода;
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКлючСохраненияПоложенияОкна(Форма)
	
	Если Форма.ДействуетДоговорОбИзбежанииДвойногоНалогообложения Тогда
		ИмяКлюча = "ДействуетДоговорОбИзбежанииДвойногоНалогообложения";
	Иначе
		ИмяКлюча = "НеДействуетДоговорОбИзбежанииДвойногоНалогообложения";
	КонецЕсли;
	
	Форма.КлючСохраненияПоложенияОкна = ИмяКлюча;
	
КонецПроцедуры

#КонецОбласти