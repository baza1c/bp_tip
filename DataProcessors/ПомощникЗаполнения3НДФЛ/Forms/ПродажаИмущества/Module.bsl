#Область ОписаниеПеременных
// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ОтключитьЗаполнениеПоИНН;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыбраннаяФорма = Параметры.Декларация3НДФЛВыбраннаяФорма;
	ИспользованныйВычет = НовыйИспользованныйВычет();
	УстановитьСписокВыбораКодаТС();
	Если ТипЗнч(Параметры.ИспользованныйВычет) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ИспользованныйВычет, Параметры.ИспользованныйВычет);
	КонецЕсли;
	
	СтруктураДоходовВычетов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СтруктураДоходовВычетов");
	ДанныеФормы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураДоходовВычетов, "ДанныеФормы");
	
	Если ЗначениеЗаполнено(СтруктураДоходовВычетов) И ЗначениеЗаполнено(ДанныеФормы) Тогда
		ЗаполнитьФормуИзДанных(ДанныеФормы);
	Иначе
		ЗаполнитьНовуюФорму();
	КонецЕсли;
	
	ПодготовитьФормуНаСервере();
	
	Если ПомощникЗаполнения3НДФЛ.МожноНеУказыватьНаименованиеИсточникаДохода(ВыбраннаяФорма) Тогда
		Элементы.ВидКонтрагента.СписокВыбора.Добавить(Перечисления.ЮридическоеФизическоеЛицо.ПустаяСсылка(), НСтр("ru = 'Неизвестен'"));
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛ.ИсточникДоходовПриСозданииНаСервере(ЭтотОбъект, Ложь);
	УправлениеФормойПриСозданииНаСервере();
	УправлениеФормой(ЭтотОбъект);
	УстановитьКлючСохраненияПоложенияОкна(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	УпрощеннаяФормаДекларации = ПомощникЗаполнения3НДФЛКлиентСервер.УпрощеннаяФормаДекларации(ВыбраннаяФорма);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	// Доля
	Если ЭтоДоходОтПродажиДоли(ВидИмущества) Тогда
		Если ДоляЧислитель > ДоляЗнаменатель Тогда
				ТекстСообщения = НСтр("ru = 'Значение доли указано неправильно'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ДоляЧислитель", , Отказ);
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ДоляЧислитель");
		МассивНепроверяемыхРеквизитов.Добавить("ДоляЗнаменатель");
	КонецЕсли;
	
	Если ВидВычета = ВычетПоРасходам() И СуммаВычета > СуммаПродажи Тогда
		ТекстСообщения = НСтр("ru = 'Сумма расходов превышает сумму продажи'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "СуммаВычета", , Отказ);
	КонецЕсли;
	
	// Информация из кадастра
	Если Не НедвижимостьПриобретенаПосле01012016 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КадастровыйНомер");
	КонецЕсли;
	
	Если Не УпрощеннаяФормаДекларации Или Не НедвижимостьПриобретенаПосле01012016 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КадастроваяСтоимость");
	КонецЕсли;
	
	// При продаже имущества физическому лицу указывать ИНН и ОКТМО не обязательно.
	Если ВидКонтрагента <> Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИНН");
		МассивНепроверяемыхРеквизитов.Добавить("ОКТМО");
	КонецЕсли;
	
	Если Не УпрощеннаяФормаДекларации Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаДоговора");
		МассивНепроверяемыхРеквизитов.Добавить("НомерДоговора");
		МассивНепроверяемыхРеквизитов.Добавить("КодТранспортногоСредства");
		МассивНепроверяемыхРеквизитов.Добавить("МаркаМодельТС");
		МассивНепроверяемыхРеквизитов.Добавить("ГосНомерТС");
	Иначе
		
		Если Не ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаАвтомобиля(ВидИмущества) Тогда
			МассивНепроверяемыхРеквизитов.Добавить("КодТранспортногоСредства");
			МассивНепроверяемыхРеквизитов.Добавить("МаркаМодельТС");
			МассивНепроверяемыхРеквизитов.Добавить("ГосНомерТС")
		КонецЕсли;
		
		Если Не ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДолевоеСтроительство(ВидИмущества) Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ДатаДоговора");
			МассивНепроверяемыхРеквизитов.Добавить("НомерДоговора");
		КонецЕсли;
		
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛ.ПроверитьЗаполнениеРеквизитовИсточникаДоходов(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	ОрганизацииФормыДляОтчетности.ПроверитьКодПоОКТМОНаФорме(ОКТМО, "ОКТМО", Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидИмуществаПриИзменении(Элемент)
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.УпрощеннаяФормаДекларации(ВыбраннаяФорма) Тогда
		НедвижимостьПриобретенаПосле01012016 = ЭтоПродажаЖилойПрочейНедвижимости(ВидИмущества);
	Иначе
		НедвижимостьПриобретенаПосле01012016 = Ложь;
	КонецЕсли;
	
	УстановитьВидВычета(ЭтотОбъект);
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидИмуществаПрочееПриИзменении(Элемент)
	
	Если ЭтоДоходОтМайнинга(ВидИмущества) Или ЭтоДоходОтКриптовалюты(ВидИмущества) Тогда
		ВидВычета = ВычетПоРасходам();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидКонтрагентаПриИзменении(Элемент)
	
	// Очистим реквизиты, которые зависят от вида контрагента.
	Наименование = "";
	ФИО = "";
	ИНН = "";
	КПП = "";
	ОКТМО = "";
	
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Истина);
	ПомощникЗаполнения3НДФЛКлиентСервер.УстановитьВидимостьПолейКонтрагента(ЭтотОбъект, Ложь);
	УстановитьКлючСохраненияПоложенияОкна(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПоискаИНННаименованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ПолеПоискаИНННаименование)
		И НЕ ЗначениеЗаполнено(ИНН) 
		И НЕ ЗначениеЗаполнено(Наименование) Тогда
		
		ПомощникЗаполнения3НДФЛКлиент.ЗаполнитьРеквизитыПоДаннымЕГР(ПолеПоискаИНННаименование, ОповещениеПослеЗаполненияПоИНН());
		ОтключитьЗаполнениеПоИНН = Истина;
		ПодключитьОбработчикОжидания("Подключаемый_ВключитьЗаполнениеПоИНН", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	ИНН = СокрП(ИНН);
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КПППриИзменении(Элемент)
	
	КПП = СокрП(КПП);
	ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидВычетаПриИзменении(Элемент)
	
	Если ВидВычета = ВычетПоНормативу() Тогда
		СуммаВычета = 0;
	КонецЕсли;
	
	УстановитьВидимостьВычета(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НедвижимостьПриобретенаПосле01012016ПриИзменении(Элемент)
	
	УстановитьВидимостьИнформацияИзКадастра(ЭтотОбъект);
	УстановитьТекстНалоговаяБаза(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КадастроваяСтоимостьПриИзменении(Элемент)
	
	УстановитьТекстНалоговаяБаза(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПонижающийКоэффициентПриИзменении(Элемент)
	
	УстановитьТекстНалоговаяБаза(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПродажиПриИзменении(Элемент)
	
	Если НедвижимостьПриобретенаПосле01012016 Тогда
		УстановитьТекстНалоговаяБаза(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗнаменательДолиПриИзменении(Элемент)
	
	УстановитьВидимостьПоВидуИмущества(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЧислительДолиПриИзменении(Элемент)
	
	УстановитьВидимостьПоВидуИмущества(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроданаТолькоЭтаДоляПриИзменении(Элемент)
	
	УстановитьВидимостьПоВидуИмущества(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	
	// Информация для формы помощника.
	СтруктураРезультата.Вставить("Вид",        ВидПоВидуИмущества(ВидИмущества));
	СтруктураРезультата.Вставить("Информация", ОписаниеОперацииПоВидуИмущества(ВидИмущества));
	
	// Сумму дохода определяем либо по кадастровой стоимости, либо по сумме продажи.
	СкорректированнаяКадастроваяСтоимость = Окр(КадастроваяСтоимость * ПонижающийКоэффициент, 2);
	Если НедвижимостьПриобретенаПосле01012016 Тогда
		СуммаДохода = Макс(СуммаПродажи, СкорректированнаяКадастроваяСтоимость);
		ДоходОпределенПоСуммеДоговора = (СуммаДохода = СуммаПродажи);
	Иначе
		СуммаДохода = СуммаПродажи;
		ДоходОпределенПоСуммеДоговора = Истина;
	КонецЕсли;
	СтруктураРезультата.Вставить("СуммаДохода",  СуммаДохода);
	СтруктураРезультата.Вставить("ВидВычета",    ВидВычета);
	СтруктураРезультата.Вставить("ВидИмущества", ВидИмущества);
	
	Если ВидВычета = ВычетПоНормативу() Тогда
		СуммаИмущественногоВычета = НормативИмущественногоВычета(ВидИмущества);
		Если ЭтоДоходОтПродажиДоли(ВидИмущества) И Не ПроданаТолькоЭтаДоля И ДоляЗнаменатель <> 0 И ДоляЧислитель < ДоляЗнаменатель Тогда
			// Имущественный вычет рассчитывается по доле.
			СуммаИмущественногоВычета = Окр(СуммаИмущественногоВычета * ДоляЧислитель / ДоляЗнаменатель, 2);
		КонецЕсли;
		СуммаИмущественногоВычета = СуммаИмущественногоВычета - ИспользованныйВычет(ЭтотОбъект);
		// Имущественный вычет не может быть больше суммы продажи.
		СтруктураРезультата.Вставить("СуммаВычета", Мин(СуммаИмущественногоВычета, СуммаДохода));
	Иначе
		СтруктураРезультата.Вставить("СуммаВычета", СуммаВычета);
	КонецЕсли;
	
	СтруктураРезультата.Вставить("ОблагаемыйДоход", Макс(0, СтруктураРезультата.СуммаДохода - СтруктураРезультата.СуммаВычета));
	
	// Данные для отчетности.
	СтруктураДанныхОтчетности = Новый Структура;
	
	Если ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо") Тогда
		НаименованиеИсточникаДохода = ФИО;
	ИначеЕсли ВидКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо") Тогда
		НаименованиеИсточникаДохода = Наименование;
	Иначе
		НаименованиеИсточникаДохода = СтруктураРезультата.Информация;
	КонецЕсли;
	
	СтруктураДанныхОтчетности.Вставить("НаименованиеИсточникаДохода", НаименованиеИсточникаДохода);
	СтруктураДанныхОтчетности.Вставить("ИНН",                   ИНН);
	СтруктураДанныхОтчетности.Вставить("КПП",                   КПП);
	СтруктураДанныхОтчетности.Вставить("ОКТМО",                 ОКТМО);
	СтруктураДанныхОтчетности.Вставить("ВидВычета",             ВидВычета);
	
	СтруктураДанныхОтчетности.Вставить("ВидИмущества",
		?(ЭтоДоходОтПродажиДоли(ВидИмущества) И ПроданаТолькоЭтаДоля, ЖилаяНедвижимость(), ВидИмущества));
	
	СтруктураДанныхОтчетности.Вставить("КодВидаДохода", КодВидаДоходаПоВидуПроданногоИмущества(
		ВидИмущества,
		ДоходОпределенПоСуммеДоговора,
		ВыбраннаяФорма));
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДолевоеСтроительство(ВидИмущества) Тогда
		СтруктураДанныхОтчетности.Вставить("НомерДоговораУступкиПрав", НомерДоговора);
		СтруктураДанныхОтчетности.Вставить("ДатаДоговораУступкиПрав", ДатаДоговора);
		СтруктураДанныхОтчетности.Вставить("СуммаДоговораУступкиПрав", СуммаПродажи);
	КонецЕсли;
	
	ВидПолученияДоходаПоУмолчанию = ПомощникЗаполнения3НДФЛКлиентСервер.ВидПолученияДоходаПоИмуществу();
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаАвтомобиля(ВидИмущества) Тогда
		СтруктураДанныхОтчетности.Вставить("ВидПолученияДоходаПоТС", ВидПолученияДоходаПоУмолчанию);
		СтруктураДанныхОтчетности.Вставить("КодТранспортногоСредства", КодТранспортногоСредства);
		СтруктураДанныхОтчетности.Вставить("МаркаМодельТС", МаркаМодельТС);
		СтруктураДанныхОтчетности.Вставить("ГосНомерТС", ГосНомерТС);
		СтруктураДанныхОтчетности.Вставить("СуммаПродажиТС", СуммаПродажи);
	КонецЕсли;
	
	СтруктураДанныхОтчетности.Вставить("ВидПолученияДоходаПоИмуществу", ВидПолученияДоходаПоУмолчанию);
	СтруктураДанныхОтчетности.Вставить("КадастровыйНомер", КадастровыйНомер);
	СтруктураДанныхОтчетности.Вставить("КадастроваяСтоимость", КадастроваяСтоимость);
	СтруктураДанныхОтчетности.Вставить("СкорректированнаяКадастроваяСтоимость", СкорректированнаяКадастроваяСтоимость);
	СтруктураДанныхОтчетности.Вставить("СуммаПоДоговору", СуммаПродажи);
	СтруктураДанныхОтчетности.Вставить("НедвижимостьПриобретенаПосле01012016", НедвижимостьПриобретенаПосле01012016);
	СтруктураРезультата.Вставить("ДанныеОтчетности", СтруктураДанныхОтчетности);
	
	// Данные формы для восстановления.
	СтруктураДанныхФормы = Новый Структура;
	Для Каждого ИмяРеквизита Из МассивРеквизитовФормы() Цикл
		СтруктураДанныхФормы.Вставить(ИмяРеквизита, ЭтотОбъект[ИмяРеквизита]);
	КонецЦикла;
	СтруктураРезультата.Вставить("ДанныеФормы", СтруктураДанныхФормы);
	
	Закрыть(СтруктураРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьПоИНН(Команда)
	
	Если НЕ ЗначениеЗаполнено(ИНН) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Поле ""ИНН"" не заполнено'"));
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	ИначеЕсли НЕ ОшибокПоИННнет Тогда
		ПоказатьПредупреждение(, Строка(РезультатПроверкиИНН));
		ТекущийЭлемент = Элементы.ИНН;
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиент.ВыполнитьЗаполнениеРеквизитовПоИНН(ИНН, ОповещениеПослеЗаполненияПоИНН());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоДаннымЕГР(Команда)
	
	Если ОтключитьЗаполнениеПоИНН <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПомощникЗаполнения3НДФЛКлиент.ЗаполнитьРеквизитыПоДаннымЕГР(ПолеПоискаИНННаименование, ОповещениеПослеЗаполненияПоИНН());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыПоНаименованию(Команда)
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Поле ""Наименование"" не заполнено'"));
		ТекущийЭлемент = Элементы.Наименование;
	Иначе
		ПомощникЗаполнения3НДФЛКлиент.ВыполнитьЗаполнениеРеквизитовПоНаименованию(Наименование, ОповещениеПослеЗаполненияПоИНН());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФормуИзДанных(ДанныеФормы)
	
	Для Каждого ИмяРеквизита Из МассивРеквизитовФормы() Цикл
		ДанныеФормы.Свойство(ИмяРеквизита, ЭтотОбъект[ИмяРеквизита]);
	КонецЦикла;
	
	ЗаполнениеРеквизитовПлашкой = НЕ ЗначениеЗаполнено(СуммаПродажи);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНовуюФорму()
	
	КлючНазначенияФормы = Параметры.КлючНазначенияФормы;
	ВидКонтрагента      = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	ЗаполнениеРеквизитовПлашкой = Истина;
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаАвтомобиля(КлючНазначенияФормы) Тогда
		ВидИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Автомобиль;
	ИначеЕсли ЭтоПродажаНедвижимости(КлючНазначенияФормы) Тогда
		ВидИмущества          = ЖилаяНедвижимость();
		ПонижающийКоэффициент = 0.7;
		ДоляЧислитель         = 1;
		ДоляЗнаменатель       = 2;
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДоходОтПродажиДолиУставногоКапитала(КлючНазначенияФормы) Тогда
		ВидИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().ДоляУставногоКапитала;
	Иначе
		ВидИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().ПрочееИмущество;
	КонецЕсли;
	
	Если ИспользованныйВычет(ЭтотОбъект) >= РазмерИмущественногоВычета(ЭтотОбъект) Тогда
		ВидВычета = ВычетПоРасходам();
	Иначе
		ВидВычета = ВычетПоНормативу();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаАвтомобиля(КлючНазначенияФормы) Или КлючНазначенияФормы = "Прочее" Тогда
		Элементы.ГруппаКогдаДекларироватьПродажуНедвижимости.Поведение = ПоведениеОбычнойГруппы.Обычное;
		Элементы.ГруппаКогдаДекларироватьПродажуНедвижимости.ОтображатьЗаголовок = Ложь;
	КонецЕсли;
	
	ЭтоПродажаНедвижимости = ЭтоПродажаНедвижимости(КлючНазначенияФормы);
	Элементы.ВидИмущества.Видимость = ЭтоПродажаНедвижимости;
	
	Если ЭтоПродажаНедвижимости Тогда
		Заголовок = НСтр("ru = 'Продажа недвижимости'");
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаАвтомобиля(КлючНазначенияФормы) Тогда
		Заголовок = НСтр("ru = 'Продажа транспортного средства'");
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДоходОтПродажиДолиУставногоКапитала(КлючНазначенияФормы) Тогда
		Заголовок = НСтр("ru = 'Продажа доли в уставном капитале организации'");
	Иначе
		Заголовок = ПомощникЗаполнения3НДФЛКлиентСервер.ПредставлениеДоходаОтПродажиПрочегоИмущества(ВыбраннаяФорма);
	КонецЕсли;
	
	Элементы.ПодсказкаМинимальныйСрокВладенияНедвижимость.Заголовок =
		ТекстПодсказкиМинимальныйСрокВладенияНедвижимостью(Параметры.Период);
	
	Элементы.ВидИмуществаПрочее.СписокВыбора.Очистить();
	Элементы.ВидИмуществаПрочее.СписокВыбора.Добавить(
		ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().ПрочееИмущество,
		НСтр("ru = 'Прочее имущество'"));
	Элементы.ВидИмуществаПрочее.СписокВыбора.Добавить(
		ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Криптовалюта,
		НСтр("ru = 'Криптовалюта'"));
	
	Если ПомощникЗаполнения3НДФЛКлиентСервер.УпрощеннаяФормаДекларации(ВыбраннаяФорма) Тогда
		НедвижимостьПриобретенаПосле01012016 = ЭтоПродажаЖилойПрочейНедвижимости(ВидИмущества); // С 2025 г. всегда нужно указывать данные из кадастра
		Элементы.ВидИмуществаПрочее.СписокВыбора.Добавить(
			ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Майнинг,
			НСтр("ru = 'Майнинг'"));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КодТранспортногоСредства) Тогда
		КодТранспортногоСредства = Элементы.КодТранспортногоСредства.СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НормативИмущественногоВычета(Знач ВидИмущества)
	
	Если ЭтоПродажаЖилойДолиНедвижимости(ВидИмущества) Тогда
		НормативИмущественногоВычета = 1000000;
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДолевоеСтроительство(ВидИмущества) Тогда
		НормативИмущественногоВычета = 0;
	Иначе
		НормативИмущественногоВычета = 250000;
	КонецЕсли;
	
	Возврат НормативИмущественногоВычета;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстНалоговаяБаза(Форма)
	
	Элементы = Форма.Элементы;
	
	КадастроваяСтоимостьИтого = Окр(Форма.КадастроваяСтоимость * Форма.ПонижающийКоэффициент, 2);
	НалоговаяБазаИтого = Макс(КадастроваяСтоимостьИтого, Форма.СуммаПродажи);
	Если Форма.СуммаПродажи = 0 Тогда
		Форма.ТекстРасчетНалоговойБазы = НСтр("ru = 'Налоговая база не определена.
			|Укажите сумму продажи недвижимости по договору купли-продажи.'");
		ТекстЗаголовкаГруппы = НСтр("ru = 'Налоговая база не определена'");
	ИначеЕсли КадастроваяСтоимостьИтого = 0 Тогда
		Форма.ТекстРасчетНалоговойБазы = СтрШаблон(НСтр("ru = 'Налоговая база составляет %1
			|и определяется по сумме продажи недвижимости по договору купли-продажи.'"),
				СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(НалоговаяБазаИтого, "рубль, рубля, рублей"));
		ТекстЗаголовкаГруппы = СтрШаблон(НСтр("ru = 'Налоговая база: %1'"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(НалоговаяБазаИтого, "рубль, рубля, рублей"));
	Иначе
		ШаблонПодсказки = НСтр("ru = 'Налоговая база составляет %1 и определяется по большей из двух сумм:
			|- %2 - стоимость продажи по договору купли-продажи;
			|- %3 - кадастровая стоимость, умноженная на понижающий коэффициент.'");
		Форма.ТекстРасчетНалоговойБазы = СтрШаблон(ШаблонПодсказки,
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(НалоговаяБазаИтого, "рубль, рубля, рублей"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Форма.СуммаПродажи, "рубль, рубля, рублей"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КадастроваяСтоимостьИтого, "рубль, рубля, рублей"));
		ТекстЗаголовкаГруппы = СтрШаблон(НСтр("ru = 'Налоговая база: %1'"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(НалоговаяБазаИтого, "рубль, рубля, рублей"));
	КонецЕсли;
		
	Элементы.ГруппаНалоговаяБаза.ЗаголовокСвернутогоОтображения = ТекстЗаголовкаГруппы;
	
	Если КадастроваяСтоимостьИтого = 0 Тогда
		Форма.ТекстКадастроваяСтоимостьИтого = НСтр("ru = 'Кадастровая стоимость не указана.'")
	Иначе
		Форма.ТекстКадастроваяСтоимостьИтого = СтрШаблон(НСтр("ru = 'Кадастровая стоимость, умноженная на понижающий коэффициент %1.'"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(КадастроваяСтоимостьИтого, "рубль, рубля, рублей"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеОперацииПоВидуИмущества(ВидИмущества)
	
	Если ЭтоПродажаЖилойНедвижимости(ВидИмущества) Тогда
		Возврат НСтр("ru = 'Продажа жилой недвижимости'");
	ИначеЕсли ЭтоДоходОтПродажиДоли(ВидИмущества) Тогда
		Возврат НСтр("ru = 'Продажа доли в недвижимости'");
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаАвтомобиля(ВидИмущества) Тогда
		Возврат НСтр("ru = 'Продажа транспортного средства'");
	ИначеЕсли ЭтоДоходОтПродажиПрочейНедвижимости(ВидИмущества) Тогда
		Возврат НСтр("ru = 'Продажа прочей недвижимости'");
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДолевоеСтроительство(ВидИмущества) Тогда
		Возврат НСтр("ru = 'Уступка прав при долевом строительстве'");
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДоходОтПродажиДолиУставногоКапитала(ВидИмущества) Тогда
		Возврат НСтр("ru = 'Продажа доли в уставном капитале'");
	ИначеЕсли ЭтоДоходОтКриптовалюты(ВидИмущества) Тогда
		Возврат НСтр("ru = 'Продажа криптовалюты'");
	ИначеЕсли ЭтоДоходОтМайнинга(ВидИмущества) Тогда
		Возврат НСтр("ru = 'Майнинг криптовалюты'");
	Иначе
		Возврат НСтр("ru = 'Продажа прочего имущества'");
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВидПоВидуИмущества(ВидИмущества)
	
	Если ЭтоПродажаЖилойДолиНедвижимости(ВидИмущества)
		Или ЭтоДоходОтПродажиПрочейНедвижимости(ВидИмущества)
		Или ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДолевоеСтроительство(ВидИмущества) Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.ИсточникиДоходовФизическихЛиц.ПродажаНедвижимости");
	ИначеЕсли ЭтоДоходОтКриптовалюты(ВидИмущества) Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ИсточникиДоходовФизическихЛиц.ЦифровойАктив");
	ИначеЕсли ЭтоДоходОтМайнинга(ВидИмущества) Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ИсточникиДоходовФизическихЛиц.Майнинг");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.ИсточникиДоходовФизическихЛиц.ПродажаИмущества");
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидВычета(Форма)
	
	Если ИспользованныйВычет(Форма) >= РазмерИмущественногоВычета(Форма) Тогда
		Форма.ВидВычета = ВычетПоРасходам();
	Иначе
		Форма.ВидВычета = ВычетПоНормативу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьВычета(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.СуммаВычета.Видимость = Не Форма.ВидВычета = ВычетПоНормативу();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьИнформацияИзКадастра(Форма)
	
	Элементы = Форма.Элементы;
	НедвижимостьПолученаВСобственностьПосле01012016 = Форма.НедвижимостьПриобретенаПосле01012016
		И ЭтоПродажаЖилойПрочейНедвижимости(Форма.ВидИмущества);
	
	Элементы.ДекорацияИнформацияИзКадастра.Видимость   = НедвижимостьПолученаВСобственностьПосле01012016;
	Элементы.КадастровыйНомер.Видимость                = НедвижимостьПолученаВСобственностьПосле01012016;
	Элементы.ПонижающийКоэффициент.Видимость           = НедвижимостьПолученаВСобственностьПосле01012016;
	Элементы.КадастроваяСтоимость.Видимость            = НедвижимостьПолученаВСобственностьПосле01012016;
	Элементы.ТекстКадастроваяСтоимостьИтого.Видимость  = НедвижимостьПолученаВСобственностьПосле01012016;
	Элементы.ТекстРасчетНалоговойБазы.Видимость        = НедвижимостьПолученаВСобственностьПосле01012016;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПоВидуИмущества(Форма)
	
	Элементы = Форма.Элементы;
	
	// Минимальный срок владения.
	Элементы.ПодсказкаМинимальныйСрокВладенияНедвижимость.Видимость = Ложь;
	Элементы.ПодсказкаМинимальныйСрокВладенияСтройка.Видимость = Ложь;
	Элементы.ПодсказкаМинимальныйСрокВладенияАвтомобиль.Видимость = Ложь;
	Элементы.ПодсказкаМинимальныйСрокВладенияПрочееИмущество.Видимость = Ложь;
	Элементы.ПодсказкаМинимальныйСрокВладенияДоляУставногоКапитала.Видимость = Ложь;
	Элементы.ПодсказкаМинимальныйСрокВладенияПрочееИмущество.Видимость = Ложь;
	Элементы.ПодсказкаМинимальныйСрокВладенияПрочееКриптовалюта.Видимость = Ложь;
	Элементы.ПодсказкаДекларированиеДоходаОтМайнинга.Видимость = Ложь;
	Элементы.ВидИмуществаПрочее.Видимость = Ложь;
	Элементы.ГруппаСведенияОДоговореДолевогоУчастия.Видимость = Ложь;
	Элементы.ГруппаСведенияОбАвтомобиле.Видимость = Ложь;
	Элементы.НедвижимостьПриобретенаПосле01012016.Видимость = Ложь;
	
	РасширенныйСписокПрочегоИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(Форма.ВыбраннаяФорма);
	ЭтоМайнинг = ЭтоДоходОтМайнинга(Форма.ВидИмущества);
	ЭтоПродажаКриптовалюты = ЭтоДоходОтКриптовалюты(Форма.ВидИмущества) И РасширенныйСписокПрочегоИмущества;
	УпрощеннаяФормаДекларации = ПомощникЗаполнения3НДФЛКлиентСервер.УпрощеннаяФормаДекларации(Форма.ВыбраннаяФорма);
	
	Если ЭтоПродажаЖилойПрочейНедвижимости(Форма.ВидИмущества) Тогда
		Элементы.ПодсказкаМинимальныйСрокВладенияНедвижимость.Видимость = Истина;
		Элементы.НедвижимостьПриобретенаПосле01012016.Видимость = Не УпрощеннаяФормаДекларации;
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДолевоеСтроительство(Форма.ВидИмущества) Тогда
		Элементы.ПодсказкаМинимальныйСрокВладенияСтройка.Видимость = Истина;
		Элементы.ГруппаСведенияОДоговореДолевогоУчастия.Видимость = УпрощеннаяФормаДекларации;
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаАвтомобиля(Форма.ВидИмущества) Тогда
		Элементы.ПодсказкаМинимальныйСрокВладенияАвтомобиль.Видимость = Истина;
		Элементы.ГруппаСведенияОбАвтомобиле.Видимость = УпрощеннаяФормаДекларации;
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДоходОтПродажиДолиУставногоКапитала(Форма.ВидИмущества) Тогда
		Элементы.ПодсказкаМинимальныйСрокВладенияДоляУставногоКапитала.Видимость = Истина;
	ИначеЕсли ЭтоПродажаКриптовалюты Тогда
		Элементы.ПодсказкаМинимальныйСрокВладенияПрочееКриптовалюта.Видимость = Истина;
		Элементы.ВидИмуществаПрочее.Видимость = Истина;
	ИначеЕсли ЭтоМайнинг Тогда
		Элементы.ПодсказкаДекларированиеДоходаОтМайнинга.Видимость = Истина;
		Элементы.ВидИмуществаПрочее.Видимость = Истина;
	Иначе
		Элементы.ПодсказкаМинимальныйСрокВладенияПрочееИмущество.Видимость = Истина;
		Элементы.ВидИмуществаПрочее.Видимость = РасширенныйСписокПрочегоИмущества;
	КонецЕсли;
	
	// Имущественный вычет.
	Элементы.ТекстДополнительныйИмущественныйВычет.Видимость = Ложь;
	
	РазмерИмущественногоВычета = РазмерИмущественногоВычета(Форма);
	
	ИспользованныйВычет = ИспользованныйВычет(Форма);
	
	Если ЭтоПродажаДоли(Форма) Тогда
		Форма.ТекстДополнительныйИмущественныйВычет = НСтр("ru = 'Имущественный вычет рассчитывается по доле от 1 000 000 рублей.'");
		Элементы.ТекстДополнительныйИмущественныйВычет.Видимость = Истина;
	КонецЕсли;
	
	Если ЭтоПродажаЖилойПрочейНедвижимости(Форма.ВидИмущества) Тогда
		ТекстИмуществаНедвижимости = НСтр("ru = 'другой недвижимости'");
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДоходОтПродажиДолиУставногоКапитала(Форма.ВидИмущества) Тогда
		ТекстИмуществаНедвижимости = НСтр("ru = 'другой доли'");
	Иначе
		ТекстИмуществаНедвижимости = НСтр("ru = 'другого имущества'");
	КонецЕсли;
	
	Если ИспользованныйВычет >= РазмерИмущественногоВычета Тогда
		Форма.ТекстИмущественныйВычет = 
			НСтр("ru = 'Доход от продажи можно уменьшить на сумму расходов по приобретению, подтвержденных документами.'");
		Форма.ТекстДополнительныйИмущественныйВычет = СтрШаблон(
			НСтр("ru = 'Вычет по нормативу использован при продаже %1.'"),
			ТекстИмуществаНедвижимости);
		Элементы.ТекстДополнительныйИмущественныйВычет.Видимость = (РазмерИмущественногоВычета > 0);
		Элементы.ВидВычета.Видимость = Ложь;
		Элементы.СуммаВычета.Видимость = Истина;
	ИначеЕсли ИспользованныйВычет > 0 Тогда
		РазмерИмущественногоВычета = РазмерИмущественногоВычета - ИспользованныйВычет;
		Форма.ТекстИмущественныйВычет = СтрШаблон(НСтр("ru = 'Доход от продажи можно уменьшить на имущественный вычет %1
			|или на сумму расходов по приобретению, подтвержденных документами.'"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(РазмерИмущественногоВычета, "рубль, рубля, рублей"));
		Форма.ТекстДополнительныйИмущественныйВычет = СтрШаблон(
			НСтр("ru = 'Вычет по нормативу %1 использован при продаже %2'"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(ИспользованныйВычет, "рубль, рубля, рублей"),
			ТекстИмуществаНедвижимости);
		Элементы.ТекстДополнительныйИмущественныйВычет.Видимость = Истина;
		Элементы.ВидВычета.Видимость = Истина;
	ИначеЕсли ЭтоМайнинг Тогда
		Элементы.ВидВычета.Видимость = Ложь;
		Элементы.СуммаВычета.Видимость = Истина;
		Форма.ТекстИмущественныйВычет = НСтр("ru = 'Доход от майнинга криптовалюты можно уменьшить на сумму подтвержденных расходов, связанных с этой деятельностью.'");
		Форма.ТекстДополнительныйИмущественныйВычет = НСтр("ru = 'Например, можно представить платежные документы на покупку оборудования или оплату электроэнергии.'");
		Элементы.ТекстДополнительныйИмущественныйВычет.Видимость = Истина;
	ИначеЕсли ЭтоПродажаКриптовалюты Тогда
		Элементы.ВидВычета.Видимость = Ложь;
		Элементы.СуммаВычета.Видимость = Истина;
		Форма.ТекстИмущественныйВычет = НСтр("ru = 'Доход от продажи криптовалюты можно уменьшить на сумму документально подтвержденных расходов, связанных с приобретением этой валюты.'");
		Элементы.ТекстДополнительныйИмущественныйВычет.Видимость = Истина;
		Форма.ТекстДополнительныйИмущественныйВычет = НСтр("ru = 'Если вы продаете криптовалюту, которую получили в результате майнинга, вы можете уменьшить доход от продажи на сумму дохода, с которого уплатили налог при майнинге этой криптовалюты.'");
	Иначе
		Форма.ТекстИмущественныйВычет = СтрШаблон(НСтр("ru = 'Доход от продажи можно уменьшить на имущественный вычет %1
			|или на сумму расходов по приобретению, подтвержденных документами.'"),
			СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(РазмерИмущественногоВычета, "рубль, рубля, рублей"));
		Элементы.ВидВычета.Видимость = Истина;
	КонецЕсли;
	
	Если ЭтоМайнинг Тогда
		Элементы.СуммаПродажи.Заголовок = НСтр("ru = 'Сумма дохода'");
	Иначе
		Элементы.СуммаПродажи.Заголовок = НСтр("ru = 'Сумма продажи'");
	КонецЕсли;
	
	// Меняем представление, только в случае если оно изменилось.
	ПредставлениеНорматива = Элементы.ВидВычета.СписокВыбора[0].Представление;
	НовоеПредставлениеНорматива = СтрШаблон(НСтр("ru = '%1 рублей'"), РазмерИмущественногоВычета);
	Если ПредставлениеНорматива <> НовоеПредставлениеНорматива Тогда
		Элементы.ВидВычета.СписокВыбора[0].Представление = НовоеПредставлениеНорматива;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПродажаДоли(Форма)
	
	ЭтоПродажаДоли = (ЭтоДоходОтПродажиДоли(Форма.ВидИмущества) И Не Форма.ПроданаТолькоЭтаДоля И
		Форма.ДоляЗнаменатель <> 0 И Форма.ДоляЧислитель < Форма.ДоляЗнаменатель);
	
	Возврат ЭтоПродажаДоли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РазмерИмущественногоВычета(Форма)
	
	Если ЭтоПродажаДоли(Форма) Тогда
		РазмерИмущественногоВычета = Окр(НормативИмущественногоВычета(Форма.ВидИмущества) * Форма.ДоляЧислитель / Форма.ДоляЗнаменатель, 2);
	Иначе
		РазмерИмущественногоВычета = НормативИмущественногоВычета(Форма.ВидИмущества);
	КонецЕсли;
	
	Возврат РазмерИмущественногоВычета;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйИспользованныйВычет()
	
	ИспользованныйВычет = Новый Структура;
	ИспользованныйВычет.Вставить("Недвижимость", 0);
	ИспользованныйВычет.Вставить("Имущество", 0);
	ИспользованныйВычет.Вставить("ПрочаяНедвижимость", 0);
	ИспользованныйВычет.Вставить("ДоляУставногоКапитала", 0);
	
	Возврат ИспользованныйВычет;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИспользованныйВычет(Форма)
	
	Если ЭтоДоходОтПродажиПрочейНедвижимости(Форма.ВидИмущества) Тогда
		ИспользованныйВычет = Форма.ИспользованныйВычет.ПрочаяНедвижимость;
	ИначеЕсли ЭтоПродажаЖилойДолиНедвижимости(Форма.ВидИмущества) Тогда
		ИспользованныйВычет = Форма.ИспользованныйВычет.Недвижимость;
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДоходОтПродажиДолиУставногоКапитала(Форма.ВидИмущества) Тогда
		ИспользованныйВычет = Форма.ИспользованныйВычет.ДоляУставногоКапитала;
	ИначеЕсли УчитыватьИспользованныйВычетНаПрочееИмущество(Форма.ВидИмущества) Тогда
		ИспользованныйВычет = Форма.ИспользованныйВычет.Имущество;
	Иначе
		ИспользованныйВычет = 0;
	КонецЕсли;
	
	Возврат ИспользованныйВычет;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция УчитыватьИспользованныйВычетНаПрочееИмущество(ВидИмущества)
	
	ДопустимыйВидИмущества = Новый Массив;
	ДопустимыйВидИмущества.Добавить(ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Автомобиль);
	ДопустимыйВидИмущества.Добавить(ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().ПрочееИмущество);
	
	Возврат ДопустимыйВидИмущества.Найти(ВидИмущества) <> Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоли(Форма)
	
	Элементы = Форма.Элементы;
	
	ЭтоДоля = ЭтоДоходОтПродажиДоли(Форма.ВидИмущества);
	Элементы.ДоляЧислитель.Видимость            = ЭтоДоля;
	Элементы.ДекорацияДоляЗнаменатель.Видимость = ЭтоДоля;
	Элементы.ДоляЗнаменатель.Видимость          = ЭтоДоля;
	Элементы.ПроданаТолькоЭтаДоля.Видимость     = ЭтоДоля;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	УстановитьВидимостьДоли(Форма);
	
	УстановитьВидимостьИнформацияИзКадастра(Форма);
	
	УстановитьВидимостьВычета(Форма);
	
	УстановитьВидимостьПоВидуИмущества(Форма);
	
	УстановитьТекстНалоговаяБаза(Форма);
	
	УстановитьКлючСохраненияПоложенияОкна(Форма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КодВидаДоходаПоВидуПроданногоИмущества(ВидИмущества, ДоходОпределенПоСуммеДоговора, ВыбраннаяФорма)
	
	КодыВидовДоходовРФ = ПомощникЗаполнения3НДФЛ.ВидыДоходов(ВыбраннаяФорма);
	ПереченьДоходовРасширен = ПомощникЗаполнения3НДФЛКлиентСервер.ВычетыРаспределяютсяПоДоходам(ВыбраннаяФорма);
	
	Если ПереченьДоходовРасширен И ЭтоДоходОтКриптовалюты(ВидИмущества) Тогда
		
		Возврат КодыВидовДоходовРФ.ОперацииСКриптовалютой;
		
	ИначеЕсли ЭтоДоходОтМайнинга(ВидИмущества) Тогда
		
		Возврат КодыВидовДоходовРФ.Майнинг;
		
	ИначеЕсли ПереченьДоходовРасширен И ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДоходОтПродажиДолиУставногоКапитала(ВидИмущества) Тогда
		
		Если ПомощникЗаполнения3НДФЛКлиентСервер.УпрощеннаяФормаДекларации(ВыбраннаяФорма) Тогда
			Возврат КодыВидовДоходовРФ.МатВыгодаОтПродажиДолиУставногоКапитала;
		Иначе
			Возврат КодыВидовДоходовРФ.ИнойДоходПоПрогрессивнойШкале;
		КонецЕсли;
		
	ИначеЕсли ПереченьДоходовРасширен Тогда
		
		Возврат КодыВидовДоходовРФ.ПродажаИмущества;
		
	ИначеЕсли ЭтоПродажаЖилойДолиНедвижимости(ВидИмущества) И ДоходОпределенПоСуммеДоговора Тогда
		
		КодВидаДоходаРФ = КодыВидовДоходовРФ.ПродажаНедвижимости;
		
	ИначеЕсли ЭтоПродажаЖилойДолиНедвижимости(ВидИмущества) И Не ДоходОпределенПоСуммеДоговора Тогда
		
		КодВидаДоходаРФ = КодыВидовДоходовРФ.ПродажаНедвижимостиПоКадастровойСтоимости;
		
	ИначеЕсли ЭтоДоходОтПродажиПрочейНедвижимости(ВидИмущества) И ДоходОпределенПоСуммеДоговора Тогда
		
		КодВидаДоходаРФ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			КодыВидовДоходовРФ, "ПродажаНежилойНедвижимости",
			КодыВидовДоходовРФ.ПродажаНедвижимости);
		
	ИначеЕсли ЭтоДоходОтПродажиПрочейНедвижимости(ВидИмущества) И Не ДоходОпределенПоСуммеДоговора Тогда
		
		КодВидаДоходаРФ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			КодыВидовДоходовРФ,
			"ПродажаНежилойНедвижимостиПоКадастровойСтоимости",
			КодыВидовДоходовРФ.ПродажаНедвижимостиПоКадастровойСтоимости);
		
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДолевоеСтроительство(ВидИмущества)
		Или ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоДоходОтПродажиДолиУставногоКапитала(ВидИмущества) Тогда
		
		КодВидаДоходаРФ = КодыВидовДоходовРФ.ИнойДоходОтИсточникаРФ;
		
	ИначеЕсли ПомощникЗаполнения3НДФЛКлиентСервер.ЭтоПродажаАвтомобиля(ВидИмущества) Тогда
		
		КодВидаДоходаРФ = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			КодыВидовДоходовРФ,
			"ПродажаТранспортныхСредств",
			КодыВидовДоходовРФ.ПродажаИмущества);
		
	Иначе
		КодВидаДоходаРФ = КодыВидовДоходовРФ.ПродажаИмущества;
	КонецЕсли;
	
	Возврат КодВидаДоходаРФ;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстПодсказкиМинимальныйСрокВладенияНедвижимостью(Период)
	
	Если Период < '20200101' Тогда
		
		ТекстПодсказки = НСтр("ru = 'Доход от продажи недвижимости декларируется, если она была в собственности меньше 5 лет.
		|Минимальный срок владения снижается до 3 лет, если имущество было получено в собственность одним из способов:
		| - До 01.01.2016;
		| - В результате приватизации;
		| - По договору наследования или дарения членами семьи или близкими родственниками;
		| - По договору пожизненного содержания с иждивением.
		|Региональные власти могут еще уменьшить минимальный срок владения.
		|Декларацию следует подавать, даже если сумма продажи меньше имущественного вычета.'");
		
	Иначе
		
		ТекстПодсказки =
		НСтр("ru = 'Следует декларировать доход от продажи недвижимости, которая находилась в собственности менее 5 лет. Срок снижается до 3 лет, если:
		| • Имущество было получено в собственность одним из способов:
		|       - до 1 января 2016 года
		|       - в результате приватизации
		|       - по договору наследования или дарения членами семьи и близкими родственниками
		|       - по договору пожизненного содержания с иждивением
		| • Это единственное жилое помещение в собственности (включая земельный участок и хозяйственные строения на нем).
		|Региональные власти могут еще уменьшить минимальный срок владения.
		|Декларацию следует подавать, даже если сумма продажи меньше имущественного вычета.'");
		
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ТекстПодсказки);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКлючСохраненияПоложенияОкна(Форма)
	
	КлючиСохраненияФормы = КлючиСохраненияФормы();
	ШаблонКлюча = СтрШаблон("[%1]", СтрСоединить(КлючиСохраненияФормы, "]["));
	СтруктураКлюча = Новый Структура(СтрСоединить(КлючиСохраненияФормы, ", "));
	СтруктураКлюча.ВидИмущества            = Форма.ВидИмущества;
	СтруктураКлюча.ВидВычета               = Форма.ВидВычета;
	СтруктураКлюча.ПроданаТолькоЭтаДоля    = ?(Форма.ПроданаТолькоЭтаДоля, "ПроданаТолькоЭтаДоля", "");
	СтруктураКлюча.ЕстьИспользованныйВычет = ?(ИспользованныйВычет(Форма) > 0, "ЕстьИспользованныйВычет", "");
	СтруктураКлюча.ИнформацияИзКадастра    = ?(Форма.НедвижимостьПриобретенаПосле01012016, "ИнформацияИзКадастра", "");
	СтруктураКлюча.УпрощеннаяФорма = ПомощникЗаполнения3НДФЛКлиентСервер.УпрощеннаяФормаДекларации(Форма.ВыбраннаяФорма);
	
	ПрефиксКлючаСохранения = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонКлюча, СтруктураКлюча);
	
	ПомощникЗаполнения3НДФЛКлиентСервер.УстановитьКлючСохраненияПоложенияОкна(Форма, ПрефиксКлючаСохранения);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МассивРеквизитовФормы()
	
	Массив = Новый Массив;
	Массив.Добавить("ВидИмущества");
	Массив.Добавить("СуммаПродажи");
	Массив.Добавить("ДоляЧислитель");
	Массив.Добавить("ДоляЗнаменатель");
	Массив.Добавить("ПроданаТолькоЭтаДоля");
	Массив.Добавить("ВидКонтрагента");
	Массив.Добавить("ФИО");
	Массив.Добавить("Наименование");
	Массив.Добавить("ИНН");
	Массив.Добавить("КПП");
	Массив.Добавить("ОКТМО");
	Массив.Добавить("НедвижимостьПриобретенаПосле01012016");
	Массив.Добавить("КадастровыйНомер");
	Массив.Добавить("КадастроваяСтоимость");
	Массив.Добавить("ПонижающийКоэффициент");
	Массив.Добавить("ВидВычета");
	Массив.Добавить("СуммаВычета");
	Массив.Добавить("КлючНазначенияФормы");
	Массив.Добавить("ДатаДоговора");
	Массив.Добавить("НомерДоговора");
	Массив.Добавить("КодТранспортногоСредства");
	Массив.Добавить("МаркаМодельТС");
	Массив.Добавить("ГосНомерТС");
	
	Возврат Массив;
	
КонецФункции

&НаСервере
Процедура УправлениеФормойПриСозданииНаСервере()
	
	Элементы.ФормаКомандаОК.Доступность = Не ТолькоПросмотр;
	Элементы.ЗаполнитьРеквизитыПоДаннымЕГР.Доступность = Не ТолькоПросмотр;
	Элементы.КнопкаЗаполнитьРеквизитыПоНаименованию.Доступность = Не ТолькоПросмотр;
	Элементы.КомандаЗаполнитьПоИНН.Доступность = Не ТолькоПросмотр;
	Элементы.НедвижимостьПриобретенаПосле01012016.Доступность = Не ТолькоПросмотр;
	Элементы.ВидИмуществаПрочее.Доступность = Не ТолькоПросмотр;
	Элементы.СуммаПродажи.Доступность = Не ТолькоПросмотр;
	
	Элементы.ГруппаВидНедвижимости.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаКогдаДекларироватьПродажуНедвижимости.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаДоля.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаРеквизитыИсточникаДохода.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаИнформацияИзКадастра.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаИмущественныйВычет.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаСведенияОДоговореДолевогоУчастия.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаСведенияОбАвтомобиле.ТолькоПросмотр = ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоДоходОтПродажиДоли(ВидИмущества)
	
	Возврат ВидИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Доля;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоДоходОтПродажиПрочейНедвижимости(ВидИмущества)
	
	Возврат ВидИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().ПрочаяНедвижимость;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоДоходОтКриптовалюты(ВидИмущества)
	
	Возврат ВидИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Криптовалюта;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоДоходОтМайнинга(ВидИмущества)
	
	Возврат ВидИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Майнинг;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПродажаЖилойДолиНедвижимости(ВидИмущества)
	
	ВидыИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества();
	
	Возврат ВидИмущества = ВидыИмущества.Жилая Или ВидИмущества = ВидыИмущества.Доля;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПродажаЖилойПрочейНедвижимости(ВидИмущества)
	
	ИдентификаторыВидовИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества();
	ВозможныеВидыИмущества = Новый Массив;
	ВозможныеВидыИмущества.Добавить(ИдентификаторыВидовИмущества.Жилая);
	ВозможныеВидыИмущества.Добавить(ИдентификаторыВидовИмущества.Доля);
	ВозможныеВидыИмущества.Добавить(ИдентификаторыВидовИмущества.ПрочаяНедвижимость);
	
	Возврат ВозможныеВидыИмущества.Найти(ВидИмущества) <> Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПродажаНедвижимости(ВидИмущества)
	
	Возврат ВидИмущества = ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Недвижимость;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПродажаЖилойНедвижимости(ВидИмущества)
	
	Возврат ВидИмущества = ЖилаяНедвижимость();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЖилаяНедвижимость()
	
	Возврат ПомощникЗаполнения3НДФЛКлиентСервер.ИдентификаторыВидовИмущества().Жилая;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВычетПоРасходам()
	
	Возврат "Расходы";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВычетПоНормативу()
	
	Возврат "Норматив";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючиСохраненияФормы()
	
	Результат = Новый Массив;
	Результат.Добавить("ВидИмущества");
	Результат.Добавить("ВидВычета");
	Результат.Добавить("ПроданаТолькоЭтаДоля");
	Результат.Добавить("ЕстьИспользованныйВычет");
	Результат.Добавить("ИнформацияИзКадастра");
	Результат.Добавить("УпрощеннаяФорма");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьСписокВыбораКодаТС()
	
	Элементы.КодТранспортногоСредства.СписокВыбора.Очистить();
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("01", "Автомобиль легковой");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("02", "Автомобиль грузовой");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("03", "Автобус");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("04", "Мотоцикл");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("05", "Мотороллер");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("06", "Снегоход");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("07", "Трактор");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("08", "Мотосани");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("09", "Вертолет");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("10", "Самолет");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("11", "Теплоход");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("12", "Яхта");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("13", "Катер");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("14", "Гидроцикл");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("15", "Моторная лодка");
	Элементы.КодТранспортногоСредства.СписокВыбора.Добавить("16", "Иное");
	
КонецПроцедуры

#Область ЗаполнениеРеквизитовКонтрагента

&НаКлиенте
Функция ОповещениеПослеЗаполненияПоИНН()
	
	Возврат Новый ОписаниеОповещения("ПослеЗаполненияПоИНН", ЭтотОбъект);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗаполненияПоИНН(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
		ПомощникЗаполнения3НДФЛКлиентСервер.ПроверитьИННКПП(ЭтотОбъект, Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВключитьЗаполнениеПоИНН()
	
	ОтключитьЗаполнениеПоИНН = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти