#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
		МобильныйКлиентБПКлиент.ПоказатьФормуНедоступнаяФункциональность(
			НСтр("ru = 'Подготовить декларацию 3-НДФЛ'"));
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ПомощникЗаполнения3НДФЛ.Форма", ПараметрыПомощника());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыПомощника()
	
	ПараметрыПомощника = Новый Структура;
	ПараметрыПомощника.Вставить("КонтекстныйВызов", Ложь);
	
	ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	Если ЗначениеЗаполнено(ОсновнаяОрганизация)
		И Не ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(ОсновнаяОрганизация) Тогда
		
		ПараметрыПомощника.Вставить("Организация", ОсновнаяОрганизация);
	КонецЕсли;
	
	ПрошлыйГод = НачалоГода(ДобавитьМесяц(ОбщегоНазначения.ТекущаяДатаПользователя(), -12));
	Период = Макс(ПрошлыйГод, Обработки.ПомощникЗаполнения3НДФЛ.ДатаНачалаПрименения());
	ПараметрыПомощника.Вставить("Период", Период);
	
	ВыбраннаяФорма = УчетНДФЛПредпринимателя.ФормаДекларацииПоУмолчанию(Период);
	ПараметрыПомощника.Вставить("ВыбраннаяФорма", ВыбраннаяФорма);
	
	Возврат ПараметрыПомощника;
	
КонецФункции

#КонецОбласти