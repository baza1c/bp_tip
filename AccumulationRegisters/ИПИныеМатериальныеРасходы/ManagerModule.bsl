
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
// 
// Параметры:
//   Ограничение - Структура - Структура ограничения:
//     * Текст - Строка - Текст ограничения
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция находит остатки, удовлетворяющие отбору, и устанавливает управляемую блокировку.
// 
// Параметры:
//  Реквизиты - Структура - структура реквизитов:
//    * Период - Дата - дата регистратора
//    * Регистратор - ДокументСсылка - регистратор
//    * Организация - СправочникСсылка.Организации - организация
//  ТаблицаПартийМПЗ - ТаблицаЗначений - таблица для установки отборов:
//    * МПЗ - СправочникСсылка - номенклатура
//    * ПартияМПЗ - ДокументСсылка - партия
//  Отказ - Булево
// 
// Возвращаемое значение:
//  Неопределено, ТаблицаЗначений - таблица остатков:
//    * НоменклатурнаяГруппа - СправочникСсылка.НоменклатурныеГруппы
//    * ХарактерДеятельности - ПеречислениеСсылка.ХарактерДеятельности
//    * СтатьяЗатрат - СправочникСсылка - статья затрат
//    * Партия - ДокументСсылка - документ поступления расходов
//    * ДатаПартии - Дата, Неопределено - дата документа поступления расходов
//    * ДокументОплаты - ДокументСсылка - документ оплаты расходов поставщику
//    * ДатаДокументаОплаты - Дата, Неопределено - дата документа оплаты
//    * МПЗ - СправочникСсылка - номенклатура, к которой относится расход
//    * ПартияМПЗ - ДокументСсылка - партия номенклатуры, к которой относится расход
//    * Количество - Число - остаток количества
//    * Сумма - Число - стоимость остатка без НДС
//    * НДС - Число - остаток НДС предъявленного
//
Функция НайтиЗаблокироватьОстаткиПартийМПЗ(Реквизиты, ТаблицаПартийМПЗ, Отказ) Экспорт
	
	ТолькоУникальныеЗначения = Истина;
	СписокМПЗ = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПартийМПЗ, "МПЗ", ТолькоУникальныеЗначения);
	СписокПартийМПЗ = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаПартийМПЗ, "ПартияМПЗ", ТолькоУникальныеЗначения);
	
	ДатаНачала = УчетДоходовИРасходовПредпринимателя.ДатаПервогоДокумента(СписокПартийМПЗ);
	ДатаКонца = Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая);
	
	Если Не Заблокировать(Реквизиты, ТаблицаПартийМПЗ, ДатаНачала, Отказ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаКонца", ДатаКонца);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СписокМПЗ", СписокМПЗ);
	Запрос.УстановитьПараметр("СписокПартийМПЗ", СписокПартийМПЗ);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИПИныеМатериальныеРасходыОстатки.Организация КАК Организация,
		|	ИПИныеМатериальныеРасходыОстатки.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПИныеМатериальныеРасходыОстатки.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПИныеМатериальныеРасходыОстатки.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ИПИныеМатериальныеРасходыОстатки.Партия КАК Партия,
		|	ИПИныеМатериальныеРасходыОстатки.ДокументОплаты КАК ДокументОплаты,
		|	ИПИныеМатериальныеРасходыОстатки.МПЗ КАК МПЗ,
		|	ИПИныеМатериальныеРасходыОстатки.ПартияМПЗ КАК ПартияМПЗ,
		|	ИПИныеМатериальныеРасходыОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ИПИныеМатериальныеРасходыОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ИПИныеМатериальныеРасходыОстатки.НДСОстаток КАК НДСОстаток
		|ПОМЕСТИТЬ ВТИПИныеМатериальныеРасходыОстатки
		|ИЗ
		|	РегистрНакопления.ИПИныеМатериальныеРасходы.Остатки(&ДатаКонца, Организация = &Организация
		|	И МПЗ В (&СписокМПЗ)
		|	И ПартияМПЗ В (&СписокПартийМПЗ)) КАК ИПИныеМатериальныеРасходыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИПИныеМатериальныеРасходыОстатки.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПИныеМатериальныеРасходыОстатки.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПИныеМатериальныеРасходыОстатки.СтатьяЗатрат КАК СтатьяЗатрат,
		|	ИПИныеМатериальныеРасходыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(РеквизитыПартий.ДатаРегистратора, НЕОПРЕДЕЛЕНО) КАК ДатаПартии,
		|	ИПИныеМатериальныеРасходыОстатки.ДокументОплаты КАК ДокументОплаты,
		|	ЕСТЬNULL(РеквизитыДокументовОплаты.ДатаРегистратора, НЕОПРЕДЕЛЕНО) КАК ДатаДокументаОплаты,
		|	ИПИныеМатериальныеРасходыОстатки.МПЗ КАК МПЗ,
		|	ИПИныеМатериальныеРасходыОстатки.ПартияМПЗ КАК ПартияМПЗ,
		|	ЕСТЬNULL(РеквизитыПартийМПЗ.ДатаРегистратора, НЕОПРЕДЕЛЕНО) КАК ДатаПартииМПЗ,
		|	ИПИныеМатериальныеРасходыОстатки.КоличествоОстаток КАК Количество,
		|	ИПИныеМатериальныеРасходыОстатки.СуммаОстаток КАК Сумма,
		|	ИПИныеМатериальныеРасходыОстатки.НДСОстаток КАК НДС
		|ИЗ
		|	ВТИПИныеМатериальныеРасходыОстатки КАК ИПИныеМатериальныеРасходыОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыПартий
		|		ПО РеквизитыПартий.Организация = ИПИныеМатериальныеРасходыОстатки.Организация
		|		И РеквизитыПартий.Документ = ИПИныеМатериальныеРасходыОстатки.Партия
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыДокументовОплаты
		|		ПО РеквизитыДокументовОплаты.Организация = ИПИныеМатериальныеРасходыОстатки.Организация
		|		И РеквизитыДокументовОплаты.Документ = ИПИныеМатериальныеРасходыОстатки.ДокументОплаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыПартийМПЗ
		|		ПО РеквизитыПартийМПЗ.Организация = ИПИныеМатериальныеРасходыОстатки.Организация
		|		И РеквизитыПартийМПЗ.Документ = ИПИныеМатериальныеРасходыОстатки.ПартияМПЗ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПартии,
		|	Партия,
		|	ДатаДокументаОплаты,
		|	ДокументОплаты,
		|	МПЗ,
		|	ДатаПартииМПЗ,
		|	ПартияМПЗ,
		|	СтатьяЗатрат,
		|	НоменклатурнаяГруппа,
		|	ХарактерДеятельности";
	
	ТаблицаПартий = Запрос.Выполнить().Выгрузить();
	ОбщегоНазначенияБПВызовСервера.ПронумероватьТаблицу(ТаблицаПартий);
	
	Возврат ТаблицаПартий;
	
КонецФункции

// Процедура формирования движений
// 
// Параметры:
//  ТаблицаИзмененияПартииМПЗ - ТаблицаЗначений - см. НоваяТаблицаИзмененияПартииМПЗ()
//  ТаблицаРеквизитов - ТаблицаЗначений:
//    * Период - Дата - период движений
//    * Регистратор - ДокументСсылка
//    * Организация - СправочникСсылка.Организации
//  Движения - КоллекцияДвижений
//
Процедура СфомироватьДвиженияИзмененияПартииМПЗ(ТаблицаИзмененияПартииМПЗ, ТаблицаРеквизитов, Движения) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаИзмененияПартииМПЗ) Или Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыИзмененияПартииМПЗ(ТаблицаИзмененияПартииМПЗ, ТаблицаРеквизитов);
	
	Движения.ИПИныеМатериальныеРасходы.Записывать = Истина;
	Для Каждого ИзменениеПартииМПЗ Из Параметры.ИзмененияПартииМПЗ Цикл
		
		СторноОстатка = Движения.ИПИныеМатериальныеРасходы.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(СторноОстатка, ИзменениеПартииМПЗ, , "МПЗ, ПартияМПЗ, Количество, Сумма, НДС");
		ЗаполнитьЗначенияСвойств(СторноОстатка, Параметры.Реквизиты);
		СторноОстатка.МПЗ = ИзменениеПартииМПЗ.МПЗ;
		СторноОстатка.ПартияМПЗ = ИзменениеПартииМПЗ.ПартияМПЗ;
		СторноОстатка.Количество = -ИзменениеПартииМПЗ.Количество;
		СторноОстатка.Сумма = -ИзменениеПартииМПЗ.Сумма;
		СторноОстатка.НДС = -ИзменениеПартииМПЗ.НДС;
		
		НовоеДвижение = Движения.ИПИныеМатериальныеРасходы.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, ИзменениеПартииМПЗ, , "МПЗ, ПартияМПЗ, Количество, Сумма, НДС");
		ЗаполнитьЗначенияСвойств(НовоеДвижение, Параметры.Реквизиты);
		НовоеДвижение.МПЗ = ИзменениеПартииМПЗ.МПЗПолучателя;
		НовоеДвижение.ПартияМПЗ = ИзменениеПартииМПЗ.ПартияМПЗПолучателя;
		НовоеДвижение.Количество = ИзменениеПартииМПЗ.Количество;
		НовоеДвижение.Сумма = ИзменениеПартииМПЗ.Сумма;
		НовоеДвижение.НДС = ИзменениеПартииМПЗ.НДС;
		
	КонецЦикла;
	
КонецПроцедуры

// Конструктор таблицы изменения партии и номенклатуры МПЗ
// 
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * НоменклатурнаяГруппа - СправочникСсылка.НоменклатурныеГруппы
//     * ХарактерДеятельности - ПеречислениеСсылка.ХарактерДеятельности
//     * ВидМПЗ - ПеречислениеСсылка.ВидыМПЗ - вид источника
//     * ВидМПЗПолучателя - ПеречислениеСсылка.ВидыМПЗ - вид получателя
//     * Номенклатура - СправочникСсылка
//     * Партия - ДокументСсылка
//     * ДокументОплаты - ДокументСсылка
//     * Количество - Число
//     * Сумма - Число
//     * НДС - Число
//
Функция НоваяТаблицаИзмененияПартииМПЗ() Экспорт
	
	ЕстьОрганизация = Ложь;
	ЕстьВидДвижения = Ложь;
	
	ТаблицаИзмененияПартииМПЗ = УчетДоходовИРасходовПредпринимателя.ПустаяТаблицаРегистраНакопления(
		"ИПИныеМатериальныеРасходы", ЕстьОрганизация, ЕстьВидДвижения);
	ТаблицаИзмененияПартииМПЗ.Колонки.Добавить(
		"МПЗПолучателя", Справочники.ТипВсеСсылки());
	ТаблицаИзмененияПартииМПЗ.Колонки.Добавить(
		"ПартияМПЗПолучателя", Документы.ТипВсеСсылки());
	ТаблицаИзмененияПартииМПЗ.Колонки.Удалить("Период");
	
	Возврат ТаблицаИзмененияПартииМПЗ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Заблокировать(Реквизиты, СписокМПЗ, ДатаНачала, Отказ)
	
	Если Не ТранзакцияАктивна() Или Не ЗначениеЗаполнено(СписокМПЗ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ИПИныеМатериальныеРасходы");
	ОписаниеИсточника = Новый Структура;
	ОписаниеИсточника.Вставить("МПЗ", "МПЗ");
	ОписаниеИсточника.Вставить("ПартияМПЗ", "ПартияМПЗ");
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ИПИныеМатериальныеРасходы");
	ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(ДатаНачала, Реквизиты.Период));
	ЭлементБлокировки.ИсточникДанных = УчетДоходовИРасходовПредпринимателя.ПодготовитьИсточникДанныхБлокировки(
		СтруктураПараметров, СписокМПЗ, ОписаниеИсточника);
	Для Каждого КолонкаИсточника Из ОписаниеИсточника Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаИсточника.Ключ, КолонкаИсточника.Значение);
	КонецЦикла;
	
	Попытка
		Блокировка.Заблокировать();
	Исключение
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		УчетДоходовИРасходовПредпринимателя.ЗаписатьОшибкуВЖурналРегистрации(
			ИнформацияОбОшибке,
			Реквизиты.Регистратор,
			Отказ);
		ОбщегоНазначения.СообщитьПользователю(ИнформацияОбОшибке);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПодготовитьПараметрыИзмененияПартииМПЗ(ТаблицаИзмененияМПЗ, ТаблицаРеквизитов)
	
	ИзмененияПартииМПЗ = ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаИзмененияМПЗ,
		"НоменклатурнаяГруппа, ХарактерДеятельности, СтатьяЗатрат, Партия, ДокументОплаты,
		|МПЗ, ПартияМПЗ, МПЗПолучателя, ПартияМПЗПолучателя,
		|Количество, Сумма, НДС");
	
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов,
		"Период, Регистратор, Организация");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИзмененияПартииМПЗ", ИзмененияПартииМПЗ);
	Параметры.Вставить("Реквизиты", Реквизиты[0]);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#КонецЕсли