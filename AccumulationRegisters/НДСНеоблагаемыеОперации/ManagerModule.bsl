
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиОбновления

// Отложенный обработчик обновления на версию 3.0.161.18.
// Заполняет движения в регистре НДСНеоблагаемыеОперации в случае реализации
// товаров физическому лицу в ЕАЭС через электронную торговую площадку
//
Процедура ЗаполнитьДвиженияРеализацийТоваровФизлицамВЕАЭСЧерезЭТП(Параметры) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетТаможенныхДекларацийЭкспорт") Тогда
		
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
		
	КонецЕсли;
	
	Код1010836 = Справочники.КодыОперацийРаздела7ДекларацииПоНДС.НайтиПоКоду("1010836");
	
	Если НЕ ЗначениеЗаполнено(Код1010836) Тогда
		
		Событие =  НСтр("ru = 'при выполнении обработчика НДСНеоблагаемыеОперации.ЗаполнитьДвиженияРеализацийТоваровФизлицамВЕАЭСЧерезЭТП()'");
		КодДобавлен = Справочники.КодыОперацийРаздела7ДекларацииПоНДС.ДобавитьКод1010836(Событие);
		Код1010836  = Справочники.КодыОперацийРаздела7ДекларацииПоНДС.НайтиПоКоду("1010836");
		
		Если НЕ КодДобавлен
			ИЛИ НЕ ЗначениеЗаполнено(Код1010836) Тогда
			Параметры.ОбработкаЗавершена = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.Дата КАК Период
	|ПОМЕСТИТЬ РеализацииФизлицамВЕАЭСЧерезЭТП
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО РеализацияТоваровУслуг.Ссылка = РеализацияТоваровУслугТовары.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Проведен
	|	И РеализацияТоваровУслугТовары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И НЕ РеализацияТоваровУслуг.Ссылка В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					НДСНеоблагаемыеОперации.Регистратор КАК Регистратор
	|				ИЗ
	|					РегистрНакопления.НДСНеоблагаемыеОперации КАК НДСНеоблагаемыеОперации
	|				ГДЕ
	|					НДСНеоблагаемыеОперации.Период >= ДАТАВРЕМЯ(2024, 7, 1)
	|					И НДСНеоблагаемыеОперации.Активность)
	|	И &КурсорСсылка
	|	И РеализацияТоваровУслуг.Дата >= ДАТАВРЕМЯ(2024, 7, 1)
	|	И РеализацияТоваровУслуг.СтранаРеализации В(&СтраныЕАЭС)
	|	И РеализацияТоваровУслуг.Контрагент.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И НЕ РеализацияТоваровУслуг.Контрагент.ИндивидуальныйПредприниматель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацииФизлицамВЕАЭСЧерезЭТП.Ссылка КАК Ссылка,
	|	РеализацииФизлицамВЕАЭСЧерезЭТП.Период КАК Период,
	|	РеализацииФизлицамВЕАЭСЧерезЭТП.Контрагент КАК Контрагент,
	|	ЕСТЬNULL(НДСЗаписиКнигиПродаж.СуммаБезНДС, 0) КАК СуммаБезНДС,
	|	РеализацииФизлицамВЕАЭСЧерезЭТП.Организация КАК Организация
	|ИЗ
	|	РеализацииФизлицамВЕАЭСЧерезЭТП КАК РеализацииФизлицамВЕАЭСЧерезЭТП
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|		ПО РеализацииФизлицамВЕАЭСЧерезЭТП.Ссылка = НДСЗаписиКнигиПродаж.СчетФактура
	|			И РеализацииФизлицамВЕАЭСЧерезЭТП.Контрагент = НДСЗаписиКнигиПродаж.Покупатель
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И НДСЗаписиКнигиПродаж.ВидЦенности В(&ВидыЦенностей)
	|	И НДСЗаписиКнигиПродаж.Период >= ДАТАВРЕМЯ(2024, 7, 1)
	|	И НДСЗаписиКнигиПродаж.Активность
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	
	ВидыЦенностей = Новый Массив;
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.Материалы);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.Товары);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ТоварыНесырьевые);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ТоварыФизлицамВЕАЭСЭлектронныеПлощадки);
	
	Запрос.Параметры.Вставить("СтраныЕАЭС", Справочники.СтраныМира.СтраныЕАЭС());
	Запрос.Параметры.Вставить("ВидыЦенностей", ВидыЦенностей);
	
	Если Не Параметры.Свойство("КурсорСсылка") Тогда
		
		Запрос.УстановитьПараметр("КурсорСсылка", Истина);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&КурсорСсылка",
								"РеализацияТоваровУслуг.Ссылка > &КурсорСсылка");
		
		Запрос.УстановитьПараметр("КурсорСсылка", Параметры.КурсорСсылка);
		
	КонецЕсли;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		//Проверка на NULL и пустые поля
		
		Если НЕ ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Параметры.Вставить("КурсорСсылка", Выборка.Ссылка);
		
		Попытка
		
			НаборЗаписейРегистра = РегистрыНакопления.НДСНеоблагаемыеОперации.СоздатьНаборЗаписей();
			ОтборПоРегистратору = НаборЗаписейРегистра.Отбор.Регистратор;
			ОтборПоРегистратору.Установить(Выборка.Ссылка);
			НаборЗаписейРегистра.Прочитать();
			ТаблицаЗаписей = НаборЗаписейРегистра.Выгрузить();
			СтрокаЗаписи = ТаблицаЗаписей.Добавить();
			СтрокаЗаписи.Регистратор = Выборка.Ссылка;
			СтрокаЗаписи.Организация = Выборка.Организация;
			СтрокаЗаписи.КодОперации = Код1010836;
			СтрокаЗаписи.Контрагент = Выборка.Контрагент;
			СтрокаЗаписи.Активность = Истина;
			СтрокаЗаписи.Период = Выборка.Период;
			СтрокаЗаписи.ДокументРеализации = Выборка.Ссылка;
			СтрокаЗаписи.МоментВремени = Новый МоментВремени(Выборка.Период, Выборка.Ссылка);
			СтрокаЗаписи.СуммаРеализацииБезНДС = Выборка.СуммаБезНДС;
			НаборЗаписейРегистра.Загрузить(ТаблицаЗаписей);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписейРегистра);
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			
			//Отменяем транзакции, в том числе, если вдруг вызывались транзакции в стандартных функциях, отменяем их
			
				Пока ТранзакцияАктивна() Цикл
				
					ОтменитьТранзакцию();
				
				КонецЦикла;

				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка при выполнении обработчика НДСНеоблагаемыеОперации.ЗаполнитьДвиженияРеализацийТоваровФизлицамВЕАЭСЧерезЭТП() для документа ""%1"" по причине:
						|%2'"),
					Выборка.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,
					Выборка.Ссылка, ТекстСообщения);

				ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
				
		КонецПопытки
			
	КонецЦикла;
	
	Если ОбъектовОбработано + ПроблемныхОбъектов = 0 Тогда
		
		Если Выборка.Количество() = 0 Тогда
			
			Параметры.ОбработкаЗавершена = Истина;
			
		Иначе
			
			Параметры.ОбработкаЗавершена = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Параметры.ОбработкаЗавершена = Ложь;
		
		Если ОбъектовОбработано = 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при выполнении обработчика НДСНеоблагаемыеОперации.ЗаполнитьДвиженияРеализацийТоваровФизлицамВЕАЭСЧерезЭТП()
					|не удалось записать движения регистра накопления ""Необлагаемые НДС операции"""" для %1 элементов.'"), 
					ПроблемныхОбъектов);
			ВызватьИсключение ТекстСообщения;
			
		Иначе
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Процедура НДСНеоблагаемыеОперации.ЗаполнитьДвиженияРеализацийТоваровФизлицамВЕАЭСЧерезЭТП()
						|обработала очередную порцию документов ""Реализация (акты, накладные, УПД)"": %1 элементов.'"), 
						ОбъектовОбработано));
						
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
