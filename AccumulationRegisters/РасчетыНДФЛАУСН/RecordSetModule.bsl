#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, "ОтправленоВБанк", Ложь) = Истина Тогда
		Возврат;
	КонецЕсли;
	
	СравниваемыеЗначения = ИменаПолей();

	НаборЗаписей = ОбщегоНазначения.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение, СравниваемыеЗначения);
	
	Если НаборЗаписей.Количество() = 0 И ЭтотОбъект.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.КоллекцииИдентичны(НаборЗаписей, ЭтотОбъект, СравниваемыеЗначения) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.ОтправитьВБанк = Истина;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИменаПолей()
	
	МетаданныеРегистра = Метаданные();
	СравниваемыеЗначения = Новый Массив;
	
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		СравниваемыеЗначения.Добавить(Измерение.Имя);
	КонецЦикла;
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		СравниваемыеЗначения.Добавить(Ресурс.Имя);
	КонецЦикла;
	Для Каждого Реквизит Из МетаданныеРегистра.Реквизиты Цикл
		СравниваемыеЗначения.Добавить(Реквизит.Имя);
	КонецЦикла;
	
	Возврат СтрСоединить(СравниваемыеЗначения, ",");
	
КонецФункции

#КонецОбласти

#КонецЕсли