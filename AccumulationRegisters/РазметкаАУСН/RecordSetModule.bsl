#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеРегистра = Метаданные();
	
	НаборЗаписей = ПрошлыйНаборЗаписей(МетаданныеРегистра, Замещение);
	
	СравниваемыеЗначения = Новый Массив;
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		СравниваемыеЗначения.Добавить(Измерение.Имя);
	КонецЦикла;
	Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл
		СравниваемыеЗначения.Добавить(Ресурс.Имя);
	КонецЦикла;
	
	ИменаСравниваемыхРеквизитов = СтрСоединить(СравниваемыеЗначения, ",");
	
	Если ОбщегоНазначения.КоллекцииИдентичны(НаборЗаписей, ЭтотОбъект, ИменаСравниваемыхРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	БанковскиеОперации = ОбщегоНазначения.ВыгрузитьКолонку(ЭтотОбъект, "БанковскаяОперация", Истина);
	БанковскиеОперацииПрошлогоНабораЗаписей = ОбщегоНазначения.ВыгрузитьКолонку(НаборЗаписей, "БанковскаяОперация", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(БанковскиеОперации, БанковскиеОперацииПрошлогоНабораЗаписей, Истина);
	
	ИнтеграцияАУСНБП.ИзменитьОтправкуВБанк(БанковскиеОперации, Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрошлыйНаборЗаписей(МетаданныеРегистра, Замещение)
	
	НаборЗаписей = ОбщегоНазначения.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение);
	
	БанковскиеОперации = ОбщегоНазначения.ВыгрузитьКолонку(НаборЗаписей, "БанковскаяОперация", Истина);
	
	Если Не ЗначениеЗаполнено(БанковскиеОперации) Тогда
		Возврат НаборЗаписей;
	КонецЕсли;
	
	НовыеЗаписи = ЭтотОбъект.Выгрузить(, "Регистратор");
	НовыеЗаписи.Свернуть("Регистратор");
	
	ИсточникРазметкиОпераций = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
		БанковскиеОперации, "ИсточникРазметкиАУСН");
		
	УдаляемыеЗаписи = Новый Массив;
	Для каждого ЗаписьДанных Из НовыеЗаписи Цикл
		ЗаписиВведеныПользователем = ОбщегоНазначения.ЕстьРеквизитОбъекта("РучнаяКорректировка", ЗаписьДанных.Регистратор.Метаданные())
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьДанных.Регистратор, "РучнаяКорректировка");
		
		Если Не ЗаписиВведеныПользователем
			И Не ЭтоБанковскаяОперация(ЗаписьДанных.Регистратор, МетаданныеРегистра) Тогда
			// Если проводятся прочие документы (а не сама банковская операция), то перед сравнением
			// уберем из прошлого набора те банковские операции, в которых Порядок расчета АУСН = "По данным банка".
			// Поскольку разметка по таким операциям не отражается прочими документами.
			Для каждого ЗаписьНабора Из НаборЗаписей Цикл
				Если ЗаписьНабора.Регистратор = ЗаписьДанных.Регистратор Тогда
					ИсточникРазметкиДокумента = ИсточникРазметкиОпераций[ЗаписьНабора.БанковскаяОперация];
					Если ИсточникРазметкиДокумента = Перечисления.ИсточникиРазметкиАУСН.Банк Тогда
						УдаляемыеЗаписи.Добавить(ЗаписьНабора);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого УдаляемаяЗапись Из УдаляемыеЗаписи Цикл
		НаборЗаписей.Удалить(УдаляемаяЗапись);
	КонецЦикла;
	
	Возврат НаборЗаписей;
	
КонецФункции

Функция ЭтоБанковскаяОперация(ДокументОперации, МетаданныеРегистра)
	
	ТипыБанковскойОперации = МетаданныеРегистра.Измерения.БанковскаяОперация.Тип;
	
	Возврат ТипыБанковскойОперации.СодержитТип(ТипЗнч(ДокументОперации));
	
КонецФункции

#КонецОбласти

#КонецЕсли