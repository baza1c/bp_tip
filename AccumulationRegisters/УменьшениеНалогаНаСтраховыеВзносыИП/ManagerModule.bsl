#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает табличный документ использованных взносов
//
// Параметры:
//   Организация - СправочникСсылка.Организации - ИП, для которого нужно получить данные
//   Период      - Дата - расчет происходит за прошлый год, относительно переданной даты
// 
// Возвращаемое значение:
//  ТабличныйДокумент - табличный документ использованных взносов в прошлом году
//
Функция ТабличныйДокументИспользованныхВзносов(Организация, Период) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Ложь;

	КонецПериода = НачалоГода(Период) - 1;
	НачалоПериода = НачалоГода(КонецПериода);

	ПрименяетсяУСН = УчетнаяПолитика.ПрименяетсяУСНЗаПериод(Организация, НачалоПериода, КонецПериода);
	ПрименяетсяПСН = УчетнаяПолитика.ПрименяетсяУСНПатентЗаПериод(Организация, НачалоПериода, КонецПериода);

	МакетТаблицыВзносов = ПолучитьМакет("РасшифровкаУменьшенияНалогаНаВзносыИП");
	ТекстОрганизация = БухгалтерскиеОтчетыВызовСервера.ПолучитьТекстОрганизация(Организация, Ложь, КонецПериода);
	ПериодУменьшения = Формат(НачалоПериода, "ДФ=гггг");

	ОбластьЗаголовок = МакетТаблицыВзносов.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = ТекстОрганизация;
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = СтрШаблон(
	НСтр("ru ='Уменьшение налога на страховые взносы за себя в %1 г.'"), ПериодУменьшения);
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);

	Если Не ЗначениеЗаполнено(Организация) Или Не (ПрименяетсяУСН Или ПрименяетсяПСН) Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.ПоЕдиномуТарифу)
	|				И УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПоЕдиномуТарифу,
	|	ВЫБОР
	|		КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.СДоходаЗаПредыдущийГод)
	|				И УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СДоходаЗаПредыдущийГод,
	|	ВЫБОР
	|		КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.СДоходаЗаТекущийГод)
	|				И УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|			ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СДоходаЗаТекущийГод,
	|	ВЫБОР
	|		КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.ПоЕдиномуТарифу)
	|				И УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ОсобыйПорядок)
	|			ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПоЕдиномуТарифуПатент,
	|	ВЫБОР
	|		КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.СДоходаЗаПредыдущийГод)
	|				И УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ОсобыйПорядок)
	|			ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СДоходаЗаПредыдущийГодПатент,
	|	ВЫБОР
	|		КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.СДоходаЗаТекущийГод)
	|				И УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.ОсобыйПорядок)
	|			ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СДоходаЗаТекущийГодПатент
	|ПОМЕСТИТЬ вт_УменьшениеНаВзносы
	|ИЗ
	|	РегистрНакопления.УменьшениеНалогаНаСтраховыеВзносыИП.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК УменьшениеНалогаНаСтраховыеВзносыИПОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(вт_УменьшениеНаВзносы.ПоЕдиномуТарифу), 0) КАК ЕдиныйТарифУСН,
	|	ЕСТЬNULL(СУММА(вт_УменьшениеНаВзносы.СДоходаЗаПредыдущийГод), 0) КАК СДоходаЗаПредыдущийГодУСН,
	|	ЕСТЬNULL(СУММА(вт_УменьшениеНаВзносы.СДоходаЗаТекущийГод), 0) КАК СДоходаЗаТекущийГодУСН,
	|	ЕСТЬNULL(СУММА(вт_УменьшениеНаВзносы.ПоЕдиномуТарифу + вт_УменьшениеНаВзносы.СДоходаЗаПредыдущийГод + вт_УменьшениеНаВзносы.СДоходаЗаТекущийГод), 0) КАК СуммаВзносовУСН,
	|	ЕСТЬNULL(СУММА(вт_УменьшениеНаВзносы.ПоЕдиномуТарифуПатент), 0) КАК ЕдиныйТарифПатент,
	|	ЕСТЬNULL(СУММА(вт_УменьшениеНаВзносы.СДоходаЗаПредыдущийГодПатент), 0) КАК СДоходаЗаПредыдущийГодПатент,
	|	ЕСТЬNULL(СУММА(вт_УменьшениеНаВзносы.СДоходаЗаТекущийГодПатент), 0) КАК СДоходаЗаТекущийГодПатент,
	|	ЕСТЬNULL(СУММА(вт_УменьшениеНаВзносы.ПоЕдиномуТарифуПатент + вт_УменьшениеНаВзносы.СДоходаЗаПредыдущийГодПатент + вт_УменьшениеНаВзносы.СДоходаЗаТекущийГодПатент), 0) КАК СуммаВзносовПатент
	|ИЗ
	|	вт_УменьшениеНаВзносы КАК вт_УменьшениеНаВзносы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(УменьшениеНалогаНаСтраховыеВзносыИП.ВидВзноса) > 0 КАК ЕстьЗаписи
	|ИЗ
	|	РегистрНакопления.УменьшениеНалогаНаСтраховыеВзносыИП КАК УменьшениеНалогаНаСтраховыеВзносыИП
	|ГДЕ
	|	УменьшениеНалогаНаСтраховыеВзносыИП.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И УменьшениеНалогаНаСтраховыеВзносыИП.Организация = &Организация";

	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Организация", Организация);

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	НаличиеЗаписей = РезультатЗапроса[2].Выбрать();
	Выборка = РезультатЗапроса[1].Выбрать();
	
	Если НаличиеЗаписей.Следующий() И НаличиеЗаписей.ЕстьЗаписи И Выборка.Следующий() Тогда
		
		ОбластьНачало = МакетТаблицыВзносов.ПолучитьОбласть("ТаблицаВзносов|Начало");
		ОбластьНачало.Параметры.Заполнить(Выборка);

		// Периоды начисления
		ОбластьНачало.Параметры.ПериодЕдиныйТариф = ПериодУменьшения;
		ОбластьНачало.Параметры.ПериодСДоходаЗаПредыдущийГод = Формат(НачалоПериода - 1, "ДФ=гггг");
		ОбластьНачало.Параметры.ПериодСДоходаЗаТекущийГод = ПериодУменьшения;

		// Исчисленные взносы
		ЕдиныйТариф =
		УчетСтраховыхВзносовИП.ВзносыПоЕдиномуТарифуПодлежащиеУплате(Организация, КонецПериода);
		ВзносыСДохода = УчетСтраховыхВзносовИП.ВзносыСДоходовПодлежащиеУплате(Организация, КонецПериода);
		ЗаПредыдущийГод = ВзносыСДохода.ЗаПредыдущийГод;
		ЗаТекущийГод = ВзносыСДохода.ЗаТекущийГод;

		ОбластьНачало.Параметры.РассчитаноЕдиныйТариф = ЕдиныйТариф;
		ОбластьНачало.Параметры.РассчитаноСДоходаЗаПредыдущийГод = ЗаПредыдущийГод;
		ОбластьНачало.Параметры.РассчитаноСДоходаЗаТекущийГод = ЗаТекущийГод;
		ОбластьНачало.Параметры.СуммаВзносовИтого = ЕдиныйТариф + ЗаПредыдущийГод + ЗаТекущийГод;
		ТабличныйДокумент.Присоединить(ОбластьНачало);

		Если ПрименяетсяУСН Тогда
			ОбластьУСН = МакетТаблицыВзносов.ПолучитьОбласть("ТаблицаВзносов|УСН");
			ОбластьУСН.Параметры.Заполнить(Выборка);
			ТабличныйДокумент.Присоединить(ОбластьУСН);
		КонецЕсли;

		Если ПрименяетсяПСН Тогда
			ОбластьПСН = МакетТаблицыВзносов.ПолучитьОбласть("ТаблицаВзносов|ПСН");
			ОбластьПСН.Параметры.Заполнить(Выборка);
			ТабличныйДокумент.Присоединить(ОбластьПСН);
		КонецЕсли;

		ОбластьИтог = МакетТаблицыВзносов.ПолучитьОбласть("ТаблицаВзносов|Итог");
		ОбластьИтог.Параметры.ОстатокСДохода = Макс(0,
		ЗаТекущийГод - Выборка.СДоходаЗаТекущийГодПатент - Выборка.СДоходаЗаТекущийГодУСН);
		ТабличныйДокумент.Присоединить(ОбластьИтог);

		ОбластьПодсказка = МакетТаблицыВзносов.ПолучитьОбласть("Подсказка");
		ТабличныйДокумент.Присоединить(ОбластьПодсказка);
	КонецЕсли;

	Возврат ТабличныйДокумент;
	
КонецФункции

// Структура взносов, уменьшающих налог
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - ИП плательщик взносов
//  Период        - Дата - период по который необходимо получить суммы
//  СистемаНалогообложения - ПеречислениеСсылка.СистемыНалогообложения - система по которой надо получить суммы уменьшения
//  ПервыйКвартал - Булево - истина, если первый квартал
// 
// Возвращаемое значение:
//  Структура - см. НоваяСтруктураВзносов()
//
Функция СтруктураВзносов(Организация, Период, СистемаНалогообложения, ПервыйКвартал = Ложь) Экспорт
	
	СтруктураВзносов = НоваяСтруктураВзносов();
	Если ПервыйКвартал Тогда
		Возврат СтруктураВзносов;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.ПоЕдиномуТарифу)
		|					ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК ЕдиныйТариф,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.СДоходаЗаПредыдущийГод)
		|					ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК СДоходаПрошлыйГод,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.ВидВзноса = ЗНАЧЕНИЕ(Перечисление.ВидыФиксированныхВзносовПодлежащихУплате.СДоходаЗаТекущийГод)
		|					ТОГДА УменьшениеНалогаНаСтраховыеВзносыИПОбороты.СуммаОборот
		|				ИНАЧЕ 0
		|			КОНЕЦ), 0) КАК СДоходаТекущийГод
		|ИЗ
		|	РегистрНакопления.УменьшениеНалогаНаСтраховыеВзносыИП.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			,
		|			Организация = &Организация
		|				И СистемаНалогообложения = &СистемаНалогообложения) КАК УменьшениеНалогаНаСтраховыеВзносыИПОбороты";
		
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Период));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СистемаНалогообложения", СистемаНалогообложения);
	
	РезультатЗапроса = Запрос.Выполнить();
		
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(СтруктураВзносов, Выборка);
		КонецЕсли;
		СтруктураВзносов.УменьшеноИтого =
			СтруктураВзносов.ЕдиныйТариф + СтруктураВзносов.СДоходаПрошлыйГод + СтруктураВзносов.СДоходаТекущийГод;
		СтруктураВзносов.НетЗаписей = СтруктураВзносов.УменьшеноИтого = 0;
	КонецЕсли;
		
	Возврат СтруктураВзносов;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоваяСтруктураВзносов()
	
	СтруктураВзносов = Новый Структура();
	СтруктураВзносов.Вставить("ЕдиныйТариф", 0);
	СтруктураВзносов.Вставить("СДоходаПрошлыйГод", 0);
	СтруктураВзносов.Вставить("СДоходаТекущийГод", 0);
	СтруктураВзносов.Вставить("УменьшеноИтого", 0);
	СтруктураВзносов.Вставить("НетЗаписей", Истина);
	
	Возврат СтруктураВзносов;
	
КонецФункции

#КонецОбласти

#КонецЕсли