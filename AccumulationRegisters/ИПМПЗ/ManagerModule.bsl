#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
// 
// Параметры:
//   Ограничение - Структура - Структура ограничения:
//     * Текст - Строка - Текст ограничения
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НайтиЗаблокироватьОстаткиПартийМПЗ(Реквизиты, СписокМПЗ, Отказ) Экспорт
	
	ТолькоУникальныеЗначения = Истина;
	СписокНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "Номенклатура", ТолькоУникальныеЗначения);
	СписокВидовМПЗ = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "ВидМПЗ", ТолькоУникальныеЗначения);
	СписокПартий = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "Партия", ТолькоУникальныеЗначения);
	
	ДатаНачала = УчетДоходовИРасходовПредпринимателя.ДатаПервогоДокумента(СписокПартий);
	ДатаКонца = Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая);
	
	Если Не Заблокировать(Реквизиты, СписокМПЗ, ДатаНачала, Отказ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаКонца", ДатаКонца);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.УстановитьПараметр("СписокВидовМПЗ", СписокВидовМПЗ);
	Запрос.УстановитьПараметр("СписокПартий", СписокПартий);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИПМПЗОстатки.Организация КАК Организация,
		|	ИПМПЗОстатки.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПМПЗОстатки.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПМПЗОстатки.ВидМПЗ КАК ВидМПЗ,
		|	ИПМПЗОстатки.Номенклатура КАК Номенклатура,
		|	ИПМПЗОстатки.Партия КАК Партия,
		|	ИПМПЗОстатки.ДокументОплаты КАК ДокументОплаты,
		|	ИПМПЗОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ИПМПЗОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ИПМПЗОстатки.НДСОстаток КАК НДСОстаток
		|ПОМЕСТИТЬ ВТИПМПЗОстатки
		|ИЗ
		|	РегистрНакопления.ИПМПЗ.Остатки(&ДатаКонца, Организация = &Организация
		|	И ВидМПЗ В (&СписокВидовМПЗ)
		|	И Номенклатура В (&СписокНоменклатуры)
		|	И Партия В (&СписокПартий)) КАК ИПМПЗОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИПМПЗОстатки.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПМПЗОстатки.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПМПЗОстатки.ВидМПЗ КАК ВидМПЗ,
		|	ИПМПЗОстатки.Номенклатура КАК Номенклатура,
		|	ИПМПЗОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(РеквизитыПартий.ДатаРегистратора, НЕОПРЕДЕЛЕНО) КАК ДатаПартии,
		|	ИПМПЗОстатки.ДокументОплаты КАК ДокументОплаты,
		|	ЕСТЬNULL(РеквизитыДокументовОплаты.ДатаРегистратора, ДАТАВРЕМЯ(3999, 12, 31)) КАК ДатаДокументаОплаты,
		|	ИПМПЗОстатки.КоличествоОстаток КАК Количество,
		|	ИПМПЗОстатки.СуммаОстаток КАК Сумма,
		|	ИПМПЗОстатки.НДСОстаток КАК НДС
		|ИЗ
		|	ВТИПМПЗОстатки КАК ИПМПЗОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыПартий
		|		ПО РеквизитыПартий.Организация = ИПМПЗОстатки.Организация
		|		И РеквизитыПартий.Документ = ИПМПЗОстатки.Партия
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыДокументовОплаты
		|		ПО РеквизитыДокументовОплаты.Организация = ИПМПЗОстатки.Организация
		|		И РеквизитыДокументовОплаты.Документ = ИПМПЗОстатки.ДокументОплаты
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидМПЗ,
		|	Номенклатура,
		|	ДатаПартии,
		|	Партия,
		|	ДатаДокументаОплаты,
		|	ДокументОплаты,
		|	НоменклатурнаяГруппа,
		|	ХарактерДеятельности";
	
	ТаблицаПартий = Запрос.Выполнить().Выгрузить();
	ОбщегоНазначенияБПВызовСервера.ПронумероватьТаблицу(ТаблицаПартий);
	
	Возврат ТаблицаПартий;
	
КонецФункции

Функция НайтиЗаблокироватьОстаткиПартийМПЗсУчетомВозвратов(Реквизиты, СписокМПЗ, СписокВозвратов, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(СписокВозвратов) Тогда
		Возврат НайтиЗаблокироватьОстаткиПартийМПЗ(Реквизиты, СписокМПЗ, Отказ);
	КонецЕсли;
	
	ТолькоУникальныеЗначения = Истина;
	СписокНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "Номенклатура", ТолькоУникальныеЗначения);
	СписокВидовМПЗ = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "ВидМПЗ", ТолькоУникальныеЗначения);
	СписокПартий = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "Партия", ТолькоУникальныеЗначения);
	
	ДатаНачала = УчетДоходовИРасходовПредпринимателя.ДатаПервогоДокумента(СписокПартий);
	ДатаКонца = Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая);
	
	Если Не Заблокировать(Реквизиты, СписокМПЗ, ДатаНачала, Отказ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаКонца", ДатаКонца);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.УстановитьПараметр("СписокВидовМПЗ", СписокВидовМПЗ);
	Запрос.УстановитьПараметр("СписокПартий", СписокПартий);
	Запрос.УстановитьПараметр("СписокВозвратов", СписокВозвратов);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписокВозвратов.НоменклатурнаяГруппа,
		|	СписокВозвратов.ХарактерДеятельности,
		|	СписокВозвратов.ВидМПЗ,
		|	СписокВозвратов.Номенклатура,
		|	СписокВозвратов.Партия,
		|	СписокВозвратов.ДокументОплаты,
		|	СписокВозвратов.Количество,
		|	СписокВозвратов.Сумма,
		|	СписокВозвратов.НДС
		|ПОМЕСТИТЬ ИПМПЗВозвраты
		|ИЗ
		|	&СписокВозвратов КАК СписокВозвратов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИПМПЗОстаткиСВозвратами.Организация КАК Организация,
		|	ИПМПЗОстаткиСВозвратами.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПМПЗОстаткиСВозвратами.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПМПЗОстаткиСВозвратами.ВидМПЗ КАК ВидМПЗ,
		|	ИПМПЗОстаткиСВозвратами.Номенклатура КАК Номенклатура,
		|	ИПМПЗОстаткиСВозвратами.Партия КАК Партия,
		|	ИПМПЗОстаткиСВозвратами.ДокументОплаты КАК ДокументОплаты,
		|	СУММА(ИПМПЗОстаткиСВозвратами.КоличествоОстаток) КАК КоличествоОстаток,
		|	СУММА(ИПМПЗОстаткиСВозвратами.СуммаОстаток) КАК СуммаОстаток,
		|	СУММА(ИПМПЗОстаткиСВозвратами.НДСОстаток) КАК НДСОстаток
		|ПОМЕСТИТЬ ИПМПЗОстаткиСВозвратами
		|ИЗ
		|	(ВЫБРАТЬ
		|		ИПМПЗОстатки.Организация КАК Организация,
		|		ИПМПЗОстатки.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|		ИПМПЗОстатки.ХарактерДеятельности КАК ХарактерДеятельности,
		|		ИПМПЗОстатки.ВидМПЗ КАК ВидМПЗ,
		|		ИПМПЗОстатки.Номенклатура КАК Номенклатура,
		|		ИПМПЗОстатки.Партия КАК Партия,
		|		ИПМПЗОстатки.ДокументОплаты КАК ДокументОплаты,
		|		ИПМПЗОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|		ИПМПЗОстатки.СуммаОстаток КАК СуммаОстаток,
		|		ИПМПЗОстатки.НДСОстаток КАК НДСОстаток
		|	ИЗ
		|		РегистрНакопления.ИПМПЗ.Остатки(&ДатаКонца, Организация = &Организация
		|		И ВидМПЗ В (&СписокВидовМПЗ)
		|		И Номенклатура В (&СписокНоменклатуры)
		|		И Партия В (&СписокПартий)) КАК ИПМПЗОстатки
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		&Организация,
		|		ИПМПЗВозвраты.НоменклатурнаяГруппа,
		|		ИПМПЗВозвраты.ХарактерДеятельности,
		|		ИПМПЗВозвраты.ВидМПЗ,
		|		ИПМПЗВозвраты.Номенклатура,
		|		ИПМПЗВозвраты.Партия,
		|		ИПМПЗВозвраты.ДокументОплаты,
		|		ИПМПЗВозвраты.Количество,
		|		ИПМПЗВозвраты.Сумма,
		|		ИПМПЗВозвраты.НДС
		|	ИЗ
		|		ИПМПЗВозвраты КАК ИПМПЗВозвраты) КАК ИПМПЗОстаткиСВозвратами
		|СГРУППИРОВАТЬ ПО
		|	ИПМПЗОстаткиСВозвратами.Организация,
		|	ИПМПЗОстаткиСВозвратами.НоменклатурнаяГруппа,
		|	ИПМПЗОстаткиСВозвратами.ХарактерДеятельности,
		|	ИПМПЗОстаткиСВозвратами.ВидМПЗ,
		|	ИПМПЗОстаткиСВозвратами.Номенклатура,
		|	ИПМПЗОстаткиСВозвратами.Партия,
		|	ИПМПЗОстаткиСВозвратами.ДокументОплаты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИПМПЗОстатки.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПМПЗОстатки.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПМПЗОстатки.ВидМПЗ КАК ВидМПЗ,
		|	ИПМПЗОстатки.Номенклатура КАК Номенклатура,
		|	ИПМПЗОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(РеквизитыПартий.ДатаРегистратора, НЕОПРЕДЕЛЕНО) КАК ДатаПартии,
		|	ИПМПЗОстатки.ДокументОплаты КАК ДокументОплаты,
		|	ЕСТЬNULL(РеквизитыДокументовОплаты.ДатаРегистратора, ДАТАВРЕМЯ(3999, 12, 31)) КАК ДатаДокументаОплаты,
		|	ИПМПЗОстатки.КоличествоОстаток КАК Количество,
		|	ИПМПЗОстатки.СуммаОстаток КАК Сумма,
		|	ИПМПЗОстатки.НДСОстаток КАК НДС
		|ИЗ
		|	ИПМПЗОстаткиСВозвратами КАК ИПМПЗОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыПартий
		|		ПО РеквизитыПартий.Организация = ИПМПЗОстатки.Организация
		|		И РеквизитыПартий.Документ = ИПМПЗОстатки.Партия
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыДокументовОплаты
		|		ПО РеквизитыДокументовОплаты.Организация = ИПМПЗОстатки.Организация
		|		И РеквизитыДокументовОплаты.Документ = ИПМПЗОстатки.ДокументОплаты
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидМПЗ,
		|	Номенклатура,
		|	ДатаПартии,
		|	Партия,
		|	ДатаДокументаОплаты,
		|	ДокументОплаты,
		|	НоменклатурнаяГруппа,
		|	ХарактерДеятельности";
	
	ТаблицаПартий = Запрос.Выполнить().Выгрузить();
	ОбщегоНазначенияБПВызовСервера.ПронумероватьТаблицу(ТаблицаПартий);
	
	Возврат ТаблицаПартий;
	
КонецФункции

// Формирует движения изменения вида МПЗ.
// 
// Параметры:
//  ТаблицаИзмененияВидаМПЗ - ТаблицаЗначений - см. НоваяТаблицаИзмененияОборотаВидаМПЗ()
//  ТаблицаРеквизитов - ТаблицаЗначений:
//    * Период - Дата - период движений
//    * Регистратор - ДокументСсылка 
//    * Организация - СправочникСсылка.Организации
//  Движения - КоллекцияДвижений
//  Отказ - Булево
//
Процедура СформироватьДвиженияИзмененияВидаМПЗ(ТаблицаИзмененияВидаМПЗ, ТаблицаРеквизитов, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаИзмененияВидаМПЗ) Или Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыИзмененияВидаМПЗ(ТаблицаИзмененияВидаМПЗ, ТаблицаРеквизитов);
	
	Движения.ИПМПЗ.Записывать = Истина;
	
	ЭтоКорректировка = Истина;
	Для Каждого ИзменениеВидаМПЗ Из Параметры.ИзмененияВидаМПЗ Цикл
		
		СторноОстатка = Движения.ИПМПЗ.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(СторноОстатка, Параметры.Реквизиты);
		ЗаполнитьЗначенияСвойств(СторноОстатка, ИзменениеВидаМПЗ, , "ВидМПЗ, Количество, Сумма, НДС");
		СторноОстатка.ВидМПЗ = ИзменениеВидаМПЗ.ВидМПЗ;
		СторноОстатка.Количество = -ИзменениеВидаМПЗ.Количество;
		СторноОстатка.Сумма = -ИзменениеВидаМПЗ.Сумма;
		СторноОстатка.НДС = -ИзменениеВидаМПЗ.НДС;
		СторноОстатка.Корректировка = ЭтоКорректировка;
		
		НовоеДвижение = Движения.ИПМПЗ.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, Параметры.Реквизиты);
		ЗаполнитьЗначенияСвойств(НовоеДвижение, ИзменениеВидаМПЗ, , "ВидМПЗ, Количество, Сумма, НДС");
		НовоеДвижение.ВидМПЗ = ИзменениеВидаМПЗ.ВидМПЗПолучателя;
		НовоеДвижение.Количество = ИзменениеВидаМПЗ.Количество;
		НовоеДвижение.Сумма = ИзменениеВидаМПЗ.Сумма;
		НовоеДвижение.НДС = ИзменениеВидаМПЗ.НДС;
		НовоеДвижение.Корректировка = ЭтоКорректировка;
		
	КонецЦикла;
	
КонецПроцедуры

// Конструктор таблицы изменения вида МПЗ
// 
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * НоменклатурнаяГруппа - СправочникСсылка.НоменклатурныеГруппы
//     * ХарактерДеятельности - ПеречислениеСсылка.ХарактерДеятельности
//     * ВидМПЗ - ПеречислениеСсылка.ВидыМПЗ - вид источника
//     * ВидМПЗПолучателя - ПеречислениеСсылка.ВидыМПЗ - вид получателя
//     * Номенклатура - СправочникСсылка
//     * Партия - ДокументСсылка
//     * ДокументОплаты - ДокументСсылка
//     * Количество - Число
//     * Сумма - Число
//     * НДС - Число
//
Функция НоваяТаблицаИзмененияВидаМПЗ() Экспорт
	
	ЕстьОрганизация = Ложь;
	ЕстьВидДвижения = Ложь;
	
	ТаблицаИзмененияВидаМПЗ = УчетДоходовИРасходовПредпринимателя.ПустаяТаблицаРегистраНакопления(
		"ИПМПЗ", ЕстьОрганизация, ЕстьВидДвижения);
	ТаблицаИзмененияВидаМПЗ.Колонки.Добавить(
		"ВидМПЗПолучателя",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыМПЗ"));
	ТаблицаИзмененияВидаМПЗ.Колонки.Удалить("Период");
	
	Возврат ТаблицаИзмененияВидаМПЗ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Заблокировать(Реквизиты, СписокМПЗ, ДатаНачала, Отказ)
	
	Если Не ТранзакцияАктивна() Или Не ЗначениеЗаполнено(СписокМПЗ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ИПМПЗ");
	ОписаниеИсточника = Новый Структура;
	ОписаниеИсточника.Вставить("Номенклатура", "Номенклатура");
	ОписаниеИсточника.Вставить("ВидМПЗ", "ВидМПЗ");
	ОписаниеИсточника.Вставить("Партия", "Партия");
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ИПМПЗ");
	ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(ДатаНачала, Реквизиты.Период));
	ЭлементБлокировки.ИсточникДанных = УчетДоходовИРасходовПредпринимателя.ПодготовитьИсточникДанныхБлокировки(
		СтруктураПараметров, СписокМПЗ, ОписаниеИсточника);
	Для Каждого КолонкаИсточника Из ОписаниеИсточника Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаИсточника.Ключ, КолонкаИсточника.Значение);
	КонецЦикла;
	
	Попытка
		Блокировка.Заблокировать();
	Исключение
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		УчетДоходовИРасходовПредпринимателя.ЗаписатьОшибкуВЖурналРегистрации(
			ИнформацияОбОшибке,
			Реквизиты.Регистратор,
			Отказ);
		ОбщегоНазначения.СообщитьПользователю(ИнформацияОбОшибке);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПодготовитьПараметрыИзмененияВидаМПЗ(ТаблицаИзмененияВидаМПЗ, ТаблицаРеквизитов)
	
	ИзмененияВидаМПЗ = ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаИзмененияВидаМПЗ,
		"НоменклатурнаяГруппа, ХарактерДеятельности, ВидМПЗ, ВидМПЗПолучателя, Номенклатура, Партия, ДокументОплаты,
		|Количество, Сумма, НДС");
	
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов,
		"Период, Регистратор, Организация");
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИзмененияВидаМПЗ", ИзмененияВидаМПЗ);
	Параметры.Вставить("Реквизиты", Реквизиты[0]);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#КонецЕсли