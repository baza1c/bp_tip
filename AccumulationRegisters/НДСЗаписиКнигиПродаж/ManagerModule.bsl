
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область ОбработчикиОбновления

// Отложенный обработчик обновления на версию 3.0.161.18.
// В движениях регистра НДСЗаписиКнигиПродаж устанавливает в измерении ВидЦенности
// значение ""Товары физлицам в ЕАЭС (электронные площадки)""
// при реализации товаров физлицам в ЕАЭС по ставке Без НДС с 01.07.2024
//
Процедура УстановитьВидЦенностиТоварыФизлицамВЕАЭСЭлектронныеПлощадки(Параметры) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВедетсяУчетТаможенныхДекларацийЭкспорт") Тогда
		
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РеализацииФизлицамВЕАЭСЧерезЭТП
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО РеализацияТоваровУслуг.Ссылка = РеализацияТоваровУслугТовары.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Проведен
	|	И РеализацияТоваровУслугТовары.СтавкаНДС = &СтавкаБезНДС
	|	И &КурсорСсылка
	|	И РеализацияТоваровУслуг.Дата >= ДАТАВРЕМЯ(2024, 7, 1)
	|	И РеализацияТоваровУслуг.СтранаРеализации В(&СтраныЕАЭС)
	|	И РеализацияТоваровУслуг.Контрагент.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И НЕ РеализацияТоваровУслуг.Контрагент.ИндивидуальныйПредприниматель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСЗаписиКнигиПродаж.Регистратор КАК Регистратор
	|ИЗ
	|	РеализацииФизлицамВЕАЭСЧерезЭТП КАК РеализацииФизлицамВЕАЭСЧерезЭТП
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|		ПО РеализацииФизлицамВЕАЭСЧерезЭТП.Ссылка = НДСЗаписиКнигиПродаж.СчетФактура
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.СтавкаНДС = &СтавкаБезНДС
	|	И НДСЗаписиКнигиПродаж.ВидЦенности В(&ВидыЦенностей)
	|	И НДСЗаписиКнигиПродаж.Период >= ДАТАВРЕМЯ(2024, 7, 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор";
	
	ВидыЦенностей = Новый Массив;
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.Материалы);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.Товары);
	ВидыЦенностей.Добавить(Перечисления.ВидыЦенностей.ТоварыНесырьевые);
	
	ТоварыФизлицамВЕАЭСЭлектронныеПлощадки = Перечисления.ВидыЦенностей.ТоварыФизлицамВЕАЭСЭлектронныеПлощадки;
	СтавкаБезНДС = Перечисления.СтавкиНДС.БезНДС;
	
	Запрос.Параметры.Вставить("СтраныЕАЭС", Справочники.СтраныМира.СтраныЕАЭС());
	Запрос.Параметры.Вставить("ВидыЦенностей", ВидыЦенностей);
	Запрос.Параметры.Вставить("СтавкаБезНДС", СтавкаБезНДС);
	
	Если Не Параметры.Свойство("КурсорСсылка") Тогда
		
		Запрос.УстановитьПараметр("КурсорСсылка", Истина);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&КурсорСсылка",
								"РеализацияТоваровУслуг.Ссылка > &КурсорСсылка");
		
		Запрос.УстановитьПараметр("КурсорСсылка", Параметры.КурсорСсылка);
		
	КонецЕсли;
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		//Проверка на NULL и пустые поля
		
		Если НЕ ЗначениеЗаполнено(Выборка.Регистратор) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Параметры.Вставить("КурсорСсылка", Выборка.Регистратор);
		
		НачатьТранзакцию();
		
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НДСЗаписиКнигиПродаж.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			Блокировка.Заблокировать();
			
			НаборЗаписейНДСЗаписиКнигиПродаж = РегистрыНакопления.НДСЗаписиКнигиПродаж.СоздатьНаборЗаписей();
			НаборЗаписейНДСЗаписиКнигиПродаж.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписейНДСЗаписиКнигиПродаж.Прочитать();
			
			Для каждого СтрокаЗаписей Из НаборЗаписейНДСЗаписиКнигиПродаж Цикл
				
				Если СтрокаЗаписей.Период >= '20240701'
					И СтрокаЗаписей.СтавкаНДС = СтавкаБезНДС
					И ВидыЦенностей.Найти(СтрокаЗаписей.ВидЦенности) <> Неопределено Тогда
					
					СтрокаЗаписей.ВидЦенности = ТоварыФизлицамВЕАЭСЭлектронныеПлощадки;
					
				КонецЕсли;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейНДСЗаписиКнигиПродаж, Истина);
			
			ЗафиксироватьТранзакцию();
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			
			//Отменяем транзакции, в том числе, если вдруг вызывались транзакции в стандартных функциях, отменяем их
			
				Пока ТранзакцияАктивна() Цикл
				
					ОтменитьТранзакцию();
				
				КонецЦикла;

				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка при выполнении обработчика НДСЗаписиКнигиПродаж.УстановитьВидЦенностиТоварыФизлицамВЕАЭСЭлектронныеПлощадки() для документа ""%1"" по причине:
						|%2'"),
					Выборка.Регистратор,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,
					Выборка.Регистратор, ТекстСообщения);

				ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
				
		КонецПопытки
			
	КонецЦикла;
	
	Если ОбъектовОбработано + ПроблемныхОбъектов = 0 Тогда
		
		Если Выборка.Количество() = 0 Тогда
			
			Параметры.ОбработкаЗавершена = Истина;
			
		Иначе
			
			Параметры.ОбработкаЗавершена = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Параметры.ОбработкаЗавершена = Ложь;
		
		Если ОбъектовОбработано = 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при выполнении обработчика НДСЗаписиКнигиПродаж.УстановитьВидЦенностиТоварыФизлицамВЕАЭСЭлектронныеПлощадки()
					|не удалось записать движения регистра накопления ""Необлагаемые НДС операции"""" для %1 элементов.'"), 
					ПроблемныхОбъектов);
			ВызватьИсключение ТекстСообщения;
			
		Иначе
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Процедура НДСЗаписиКнигиПродаж.УстановитьВидЦенностиТоварыФизлицамВЕАЭСЭлектронныеПлощадки()
						|обработала очередную порцию документов ""Реализация (акты, накладные, УПД)"": %1 элементов.'"), 
						ОбъектовОбработано));
						
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
