#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
// 
// Параметры:
//   Ограничение - Структура - Структура ограничения:
//     * Текст - Строка - Текст ограничения
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НайтиЗаблокироватьОборотыПартийМПЗ(Реквизиты, СписокМПЗ, Отказ) Экспорт
	
	СписокНоменклатурныхГрупп = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "НоменклатурнаяГруппа", Истина);
	СписокХарактеровДеятельности = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "ХарактерДеятельности", Истина);
	СписокДокументовОплаты = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "ДокументОплаты", Истина);
	СписокНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "Номенклатура", Истина);
	СписокВидовМПЗ = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "ВидМПЗ", Истина);
	СписокПартий = ОбщегоНазначения.ВыгрузитьКолонку(СписокМПЗ, "Партия", Истина);
	
	ДатаНачала = УчетДоходовИРасходовПредпринимателя.ДатаПервогоДокумента(СписокПартий);
	
	Если Не Заблокировать(Реквизиты, СписокМПЗ, ДатаНачала, Отказ) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр(
		"ДатаКонца", Новый Граница(Новый МоментВремени(Реквизиты.Период, Реквизиты.Регистратор), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СписокНоменклатурныхГрупп", СписокНоменклатурныхГрупп);
	Запрос.УстановитьПараметр("СписокХарактеровДеятельности", СписокХарактеровДеятельности);
	Запрос.УстановитьПараметр("СписокДокументовОплаты", СписокДокументовОплаты);
	Запрос.УстановитьПараметр("СписокВидовМПЗ", СписокВидовМПЗ);
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.УстановитьПараметр("СписокПартий", СписокПартий);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИПМПЗОборотыОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПМПЗОборотыОбороты.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПМПЗОборотыОбороты.ДокументОплаты КАК ДокументОплаты,
		|	ИПМПЗОборотыОбороты.ВидСписанногоМПЗ КАК ВидСписанногоМПЗ,
		|	ИПМПЗОборотыОбороты.НоменклатураСписанная КАК НоменклатураСписанная,
		|	ИПМПЗОборотыОбороты.ПартияСписаннойНоменклатуры КАК ПартияСписаннойНоменклатуры,
		|	ИПМПЗОборотыОбороты.ВидПоступившегоМПЗ КАК ВидПоступившегоМПЗ,
		|	ИПМПЗОборотыОбороты.НоменклатураПоступившая КАК НоменклатураПоступившая,
		|	ИПМПЗОборотыОбороты.ПартияПоступившейНоменклатуры КАК ПартияПоступившейНоменклатуры,
		|	ИПМПЗОборотыОбороты.КоличествоОборот КАК КоличествоОборот,
		|	ИПМПЗОборотыОбороты.СуммаОборот КАК СуммаОборот,
		|	ИПМПЗОборотыОбороты.НДСОборот КАК НДСОборот
		|ПОМЕСТИТЬ ВТИПМПЗОборотыОбороты
		|ИЗ
		|	РегистрНакопления.ИПМПЗОбороты.Обороты(&ДатаНачала, &ДатаКонца,, Организация = &Организация
		|	И НоменклатурнаяГруппа В (&СписокНоменклатурныхГрупп)
		|	И ХарактерДеятельности В (&СписокХарактеровДеятельности)
		|	И ДокументОплаты В (&СписокДокументовОплаты)
		|	И ВидПоступившегоМПЗ В (&СписокВидовМПЗ)
		|	И НоменклатураПоступившая В (&СписокНоменклатуры)
		|	И ПартияПоступившейНоменклатуры В (&СписокПартий)) КАК ИПМПЗОборотыОбороты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НоменклатурнаяГруппа,
		|	ХарактерДеятельности,
		|	ВидПоступившегоМПЗ,
		|	НоменклатураПоступившая,
		|	ПартияПоступившейНоменклатуры,
		|	ДокументОплаты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИПМПЗОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПМПЗОбороты.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПМПЗОбороты.ВидМПЗ КАК ВидМПЗ,
		|	ИПМПЗОбороты.Номенклатура КАК Номенклатура,
		|	ИПМПЗОбороты.Партия КАК Партия,
		|	ИПМПЗОбороты.ДокументОплаты КАК ДокументОплаты,
		|	ИПМПЗОбороты.КоличествоПриход КАК КоличествоПриход,
		|	ИПМПЗОбороты.СуммаПриход КАК СуммаПриход,
		|	ИПМПЗОбороты.НДСПриход КАК НДСПриход
		|ПОМЕСТИТЬ ВТМПЗОбороты
		|ИЗ
		|	РегистрНакопления.ИПМПЗ.Обороты(&ДатаНачала, &ДатаКонца,, Организация = &Организация
		|	И НоменклатурнаяГруппа В (&СписокНоменклатурныхГрупп)
		|	И ХарактерДеятельности В (&СписокХарактеровДеятельности)
		|	И ДокументОплаты В (&СписокДокументовОплаты)
		|	И ВидМПЗ В (&СписокВидовМПЗ)
		|	И Номенклатура В (&СписокНоменклатуры)
		|	И Партия В (&СписокПартий)) КАК ИПМПЗОбороты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НоменклатурнаяГруппа,
		|	ХарактерДеятельности,
		|	ВидМПЗ,
		|	Номенклатура,
		|	Партия,
		|	ДокументОплаты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИПМПЗОборотыОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ИПМПЗОборотыОбороты.ХарактерДеятельности КАК ХарактерДеятельности,
		|	ИПМПЗОборотыОбороты.ДокументОплаты КАК ДокументОплаты,
		|	ЕСТЬNULL(РеквизитыДокументовОплаты.ДатаРегистратора, ДАТАВРЕМЯ(3999, 12, 31)) КАК ДатаДокументаОплаты,
		|	ИПМПЗОборотыОбороты.ВидСписанногоМПЗ КАК ВидСписанногоМПЗ,
		|	ИПМПЗОборотыОбороты.НоменклатураСписанная КАК НоменклатураСписанная,
		|	ИПМПЗОборотыОбороты.ПартияСписаннойНоменклатуры КАК ПартияСписаннойНоменклатуры,
		|	ЕСТЬNULL(РеквизитыПартийСписаннойНоменклатуры.ДатаРегистратора, ДАТАВРЕМЯ(3999, 12, 31)) КАК
		|		ДатаПартииСписаннойНоменклатуры,
		|	ИПМПЗОборотыОбороты.ВидПоступившегоМПЗ КАК ВидПоступившегоМПЗ,
		|	ИПМПЗОборотыОбороты.НоменклатураПоступившая КАК НоменклатураПоступившая,
		|	ИПМПЗОборотыОбороты.ПартияПоступившейНоменклатуры КАК ПартияПоступившейНоменклатуры,
		|	ИПМПЗОборотыОбороты.КоличествоОборот КАК Количество,
		|	ИПМПЗОборотыОбороты.СуммаОборот КАК Сумма,
		|	ИПМПЗОборотыОбороты.НДСОборот КАК НДС,
		|	ЕСТЬNULL(МПЗОбороты.КоличествоПриход, 0) КАК КоличествоПоступившейНоменклатуры
		|ИЗ
		|	ВТИПМПЗОборотыОбороты КАК ИПМПЗОборотыОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМПЗОбороты КАК МПЗОбороты
		|		ПО ИПМПЗОборотыОбороты.НоменклатурнаяГруппа = МПЗОбороты.НоменклатурнаяГруппа
		|		И ИПМПЗОборотыОбороты.ХарактерДеятельности = МПЗОбороты.ХарактерДеятельности
		|		И ИПМПЗОборотыОбороты.ВидПоступившегоМПЗ = МПЗОбороты.ВидМПЗ
		|		И ИПМПЗОборотыОбороты.НоменклатураПоступившая = МПЗОбороты.Номенклатура
		|		И ИПМПЗОборотыОбороты.ПартияПоступившейНоменклатуры = МПЗОбороты.Партия
		|		И ИПМПЗОборотыОбороты.ДокументОплаты = МПЗОбороты.ДокументОплаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыДокументовОплаты
		|		ПО ИПМПЗОборотыОбороты.ДокументОплаты = РеквизитыДокументовОплаты.Документ
		|		И РеквизитыДокументовОплаты.Организация = &Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК РеквизитыПартийСписаннойНоменклатуры
		|		ПО ИПМПЗОборотыОбороты.ПартияСписаннойНоменклатуры = РеквизитыПартийСписаннойНоменклатуры.Документ
		|		И РеквизитыПартийСписаннойНоменклатуры.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидПоступившегоМПЗ,
		|	НоменклатураПоступившая,
		|	ПартияПоступившейНоменклатуры,
		|	ВидСписанногоМПЗ,
		|	НоменклатураСписанная,
		|	ПартияСписаннойНоменклатуры,
		|	НоменклатурнаяГруппа,
		|	ХарактерДеятельности,
		|	ДокументОплаты";
	
	ТаблицаОборотов = Запрос.Выполнить().Выгрузить();
	ОбщегоНазначенияБПВызовСервера.ПронумероватьТаблицу(ТаблицаОборотов);
	
	Возврат ТаблицаОборотов;
	
КонецФункции

// Формирует движения изменения вида МПЗ.
// 
// Параметры:
//  ТаблицаИзмененияОборотовВидаМПЗ - ТаблицаЗначений - см. НоваяТаблицаИзмененияОборотаВидаМПЗ()
//  ТаблицаРеквизитов - ТаблицаЗначений:
//    * Период - Дата - период движений
//    * Регистратор - ДокументСсылка
//    * Организация - СправочникСсылка.Организации
//  Движения - КоллекцияДвижений
//  Отказ - Булево
//
Процедура СформироватьДвиженияИзмененияОборотовВидаМПЗ(ТаблицаИзмененияОборотовВидаМПЗ, ТаблицаРеквизитов, Движения, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ТаблицаИзмененияОборотовВидаМПЗ) Или Не ЗначениеЗаполнено(ТаблицаРеквизитов) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры = ПодготовитьПараметрыИзмененияОборотовВидаМПЗ(ТаблицаИзмененияОборотовВидаМПЗ, ТаблицаРеквизитов);
	
	Движения.ИПМПЗОбороты.Записывать = Истина;
	
	Для Каждого ИзменениеОборотаМПЗ Из Параметры.ИзмененияОборотовВидаМПЗ Цикл
		
		СторноОборота = Движения.ИПМПЗОбороты.Добавить();
		ЗаполнитьЗначенияСвойств(СторноОборота, Параметры.Реквизиты);
		ЗаполнитьЗначенияСвойств(СторноОборота, ИзменениеОборотаМПЗ, , "ВидПоступившегоМПЗ, Количество, Сумма, НДС");
		СторноОборота.ВидПоступившегоМПЗ = ИзменениеОборотаМПЗ.ВидПоступившегоМПЗ;
		СторноОборота.Количество = -ИзменениеОборотаМПЗ.Количество;
		СторноОборота.Сумма = -ИзменениеОборотаМПЗ.Сумма;
		СторноОборота.НДС = -ИзменениеОборотаМПЗ.НДС;
		
		НовоеДвижение = Движения.ИПМПЗОбороты.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, Параметры.Реквизиты);
		ЗаполнитьЗначенияСвойств(НовоеДвижение, ИзменениеОборотаМПЗ, , "ВидПоступившегоМПЗ, Количество, Сумма, НДС");
		НовоеДвижение.ВидПоступившегоМПЗ = ИзменениеОборотаМПЗ.ВидПоступившегоМПЗПолучателя;
		НовоеДвижение.Количество = ИзменениеОборотаМПЗ.Количество;
		НовоеДвижение.Сумма = ИзменениеОборотаМПЗ.Сумма;
		НовоеДвижение.НДС = ИзменениеОборотаМПЗ.НДС;
		
	КонецЦикла;
	
КонецПроцедуры

// Конструктор таблицы изменения вида МПЗ
// 
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * НоменклатурнаяГруппа - СправочникСсылка.НоменклатурныеГруппы
//     * ХарактерДеятельности - ПеречислениеСсылка.ХарактерДеятельности
//     * ДокументОплаты - ДокументСсылка
//     * ВидСписанногоМПЗ - ПеречислениеСсылка.ВидыМПЗ
//     * НоменклатураСписанная - СправочникСсылка
//     * ПартияСписаннойНоменклатуры - ДокументСсылка
//     * ВидПоступившегоМПЗ - ПеречислениеСсылка.ВидыМПЗ - вид источника
//     * ВидПоступившегоМПЗПолучателя - ПеречислениеСсылка.ВидыМПЗ - вид получателя
//     * НоменклатураПоступившая - СправочникСсылка
//     * ПартияПоступившейНоменклатуры - ДокументСсылка
//     * Количество - Число
//     * Сумма - Число
//     * НДС - Число
//
Функция НоваяТаблицаИзмененияОборотаВидаМПЗ() Экспорт
	
	ТаблицаИзмененияВидаМПЗ = УчетДоходовИРасходовПредпринимателя.ПустаяТаблицаРегистраНакопления(
		"ИПМПЗОбороты", Ложь, Ложь);
	ТаблицаИзмененияВидаМПЗ.Колонки.Добавить(
		"ВидПоступившегоМПЗПолучателя",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыМПЗ"));
	ТаблицаИзмененияВидаМПЗ.Колонки.Удалить("Период");
	
	Возврат ТаблицаИзмененияВидаМПЗ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Заблокировать(Реквизиты, СписокМПЗ, ДатаНачала, Отказ)
	
	Если Не ТранзакцияАктивна() Или Не ЗначениеЗаполнено(СписокМПЗ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрНакопления", "ИПМПЗОбороты");
	
	ОписаниеИсточника = Новый Структура;
	ОписаниеИсточника.Вставить("НоменклатураПоступившая", "Номенклатура");
	ОписаниеИсточника.Вставить("ПартияПоступившейНоменклатуры", "Партия");
	ОписаниеИсточника.Вставить("ДокументОплаты", "ДокументОплаты");
	ОписаниеИсточника.Вставить("ВидПоступившегоМПЗ", "ВидМПЗ");
	ОписаниеИсточника.Вставить("НоменклатурнаяГруппа", "НоменклатурнаяГруппа");
	ОписаниеИсточника.Вставить("ХарактерДеятельности", "ХарактерДеятельности");
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ИПМПЗОбороты");
	ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
	ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон(ДатаНачала, Реквизиты.Период));
	ЭлементБлокировки.ИсточникДанных = УчетДоходовИРасходовПредпринимателя.ПодготовитьИсточникДанныхБлокировки(
		СтруктураПараметров, СписокМПЗ, ОписаниеИсточника);
	Для Каждого КолонкаИсточника Из ОписаниеИсточника Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаИсточника.Ключ, КолонкаИсточника.Значение);
	КонецЦикла;
	
	Попытка
		Блокировка.Заблокировать();
	Исключение
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		УчетДоходовИРасходовПредпринимателя.ЗаписатьОшибкуВЖурналРегистрации(
			ИнформацияОбОшибке,
			Реквизиты.Регистратор,
			Отказ);
		ОбщегоНазначения.СообщитьПользователю(ИнформацияОбОшибке);
		Возврат Неопределено;
		КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ПодготовитьПараметрыИзмененияОборотовВидаМПЗ(ТаблицаИзмененияОборотовВидаМПЗ, ТаблицаРеквизитов)
	
	ИзмененияОборотовВидаМПЗ = ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаИзмененияОборотовВидаМПЗ,
		"НоменклатурнаяГруппа, ХарактерДеятельности, ДокументОплаты,
		|ВидСписанногоМПЗ, НоменклатураСписанная, ПартияСписаннойНоменклатуры,
		|ВидПоступившегоМПЗ, ВидПоступившегоМПЗПолучателя, НоменклатураПоступившая, ПартияПоступившейНоменклатуры,
		|Количество, Сумма, НДС");
	
	// Подготовка таблицы Параметры.Реквизиты
	
	Реквизиты = ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаРеквизитов,
		"Период, Регистратор, Организация");
		
	Параметры = Новый Структура;
	Параметры.Вставить("ИзмененияОборотовВидаМПЗ", ИзмененияОборотовВидаМПЗ);
	Параметры.Вставить("Реквизиты", Реквизиты[0]);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#КонецЕсли