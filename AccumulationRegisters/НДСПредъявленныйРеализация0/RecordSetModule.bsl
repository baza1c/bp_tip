#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Или ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	Организации = Выгрузить(, "Организация");
	Организации.Свернуть("Организация");
	НастройкиОрганизаций = Новый Соответствие;
	Для каждого СтрокаОрганизация Из Организации Цикл
		НеИспользоватьПоставщикаВРегистреНДСПредъявленныйРеализация0 = ОбщегоНазначенияБП.ПрочитатьДанныеИзХранилища(
			СтрокаОрганизация.Организация, "НеПрименяетсяПоставщикДляРегистраНДСПредъявленныйРеализация0");
		НастройкиОрганизаций.Вставить(СтрокаОрганизация.Организация, НеИспользоватьПоставщикаВРегистреНДСПредъявленныйРеализация0);
	КонецЦикла;
	
	НеЗаполненныйПоставщик = Справочники.Контрагенты.ПустаяСсылка();
	Для каждого Проводка Из ЭтотОбъект Цикл
		Если ТипЗнч(Проводка.Регистратор) = Тип("ДокументСсылка.ОперацияБух") Тогда 
			Продолжить;
		КонецЕсли;
		НеИспользоватьПоставщикаВРегистреНДСПредъявленныйРеализация0 = НастройкиОрганизаций[Проводка.Организация];
		Если НеИспользоватьПоставщикаВРегистреНДСПредъявленныйРеализация0 <> Неопределено 
			И НеИспользоватьПоставщикаВРегистреНДСПредъявленныйРеализация0 Тогда
			
			Проводка.Поставщик = НеЗаполненныйПоставщик;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли