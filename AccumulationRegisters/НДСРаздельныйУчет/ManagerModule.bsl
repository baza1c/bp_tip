
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиОбновления

// Отложенный обработчик обновления
// Для Организаций на УСН с 5% и 7% НДС очищает способ учета НДС в регистре "НДСРаздельныйУчет", 
// очищает регистр "НДСПредъявленный" при включении НДС в стоимость, устанавливает
// признак "НДСВключенВСтоимость" в регистраторе для документов 2025 года.
//
Процедура ОчиститьСпособУчетаИУстановитьНДСВключенВСтоимость(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиУчетаНДССрезПоследних.Организация КАК Организация,
	|	&Квартал1 КАК Квартал
	|ПОМЕСТИТЬ ОрганизацииКварталыДляОбработки
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Квартал1, РаздельныйУчетНДСБезВычетов = ИСТИНА) КАК НастройкиУчетаНДССрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиУчетаНДССрезПоследних.Организация,
	|	&Квартал2
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Квартал2, РаздельныйУчетНДСБезВычетов = ИСТИНА) КАК НастройкиУчетаНДССрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиУчетаНДССрезПоследних.Организация,
	|	&Квартал3
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Квартал3, РаздельныйУчетНДСБезВычетов = ИСТИНА) КАК НастройкиУчетаНДССрезПоследних
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиУчетаНДССрезПоследних.Организация,
	|	&Квартал4
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Квартал4, РаздельныйУчетНДСБезВычетов = ИСТИНА) КАК НастройкиУчетаНДССрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСРаздельныйУчет.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ РегистраторыПредварительная
	|ИЗ
	|	РегистрНакопления.НДСРаздельныйУчет КАК НДСРаздельныйУчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОрганизацииКварталыДляОбработки КАК ОрганизацииКварталыДляОбработки
	|		ПО НДСРаздельныйУчет.Организация = ОрганизацииКварталыДляОбработки.Организация
	|			И (НАЧАЛОПЕРИОДА(НДСРаздельныйУчет.Период, КВАРТАЛ) = ОрганизацииКварталыДляОбработки.Квартал)
	|ГДЕ
	|	НДСРаздельныйУчет.СпособУчетаНДС <> ЗНАЧЕНИЕ(Перечисление.СпособыУчетаНДС.ПустаяСсылка)
	|	И НДСРаздельныйУчет.Активность = ИСТИНА
	|	И НЕ ОрганизацииКварталыДляОбработки.Организация ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСПредъявленный.Регистратор
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОрганизацииКварталыДляОбработки КАК ОрганизацииКварталыДляОбработки
	|		ПО НДСПредъявленный.Организация = ОрганизацииКварталыДляОбработки.Организация
	|			И (НАЧАЛОПЕРИОДА(НДСПредъявленный.Период, КВАРТАЛ) = ОрганизацииКварталыДляОбработки.Квартал)
	|ГДЕ
	|	НДСПредъявленный.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НДСВключенВСтоимость)
	|	И НДСПредъявленный.Активность = ИСТИНА
	|	И НЕ ОрганизацииКварталыДляОбработки.Организация ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	РегистраторыПредварительная.Регистратор КАК Регистратор
	|ИЗ
	|	РегистраторыПредварительная КАК РегистраторыПредварительная";
	
	Запрос.Параметры.Вставить("Квартал1", Дата(2025,1,1));
	Запрос.Параметры.Вставить("Квартал2", Дата(2025,4,1));
	Запрос.Параметры.Вставить("Квартал3", Дата(2025,7,1));
	Запрос.Параметры.Вставить("Квартал4", Дата(2025,10,1));
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		//Проверка на NULL и пустые поля
		
		Если НЕ ЗначениеЗаполнено(Выборка.Регистратор) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			
			МетаданныеДокумента = Выборка.Регистратор.Метаданные();
			
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("НДСВключенВСтоимость", МетаданныеДокумента) Тогда
				
				// Блокируем объект от изменения другими сеансами.
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить(МетаданныеДокумента.ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Регистратор);
				Блокировка.Заблокировать();
				
				ДокументОбъект = Выборка.Регистратор.ПолучитьОбъект();
				
				// Если объект ранее был удален или обработан другими сеансами, пропускаем его.
				Если ДокументОбъект <> Неопределено
					И НЕ ДокументОбъект.НДСВключенВСтоимость Тогда
					ДокументОбъект.НДСВключенВСтоимость = Истина;
					
					// Запись обработанного объекта (без перепроведения).
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
				КонецЕсли;
				
			КонецЕсли;
			
			Если Выборка.Регистратор.Метаданные().Движения.Содержит(Метаданные.РегистрыНакопления.НДСРаздельныйУчет) Тогда
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НДСРаздельныйУчет.НаборЗаписей");
				ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
				Блокировка.Заблокировать();
				
				НаборЗаписейНДСРаздельныйУчетНДС = РегистрыНакопления.НДСРаздельныйУчет.СоздатьНаборЗаписей();
				НаборЗаписейНДСРаздельныйУчетНДС.Отбор.Регистратор.Установить(Выборка.Регистратор);
				НаборЗаписейНДСРаздельныйУчетНДС.Прочитать();
				
				Для каждого СтрокаЗаписей Из НаборЗаписейНДСРаздельныйУчетНДС Цикл
					
					Если СтрокаЗаписей.СпособУчетаНДС <> Перечисления.СпособыУчетаНДС.ПустаяСсылка() Тогда
						
						СтрокаЗаписей.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПустаяСсылка();
						
					КонецЕсли;
					
				КонецЦикла;
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейНДСРаздельныйУчетНДС, Истина);
			КонецЕсли;
			
			Если Выборка.Регистратор.Метаданные().Движения.Содержит(Метаданные.РегистрыНакопления.НДСПредъявленный) Тогда
				// Очистим записи НДС предъявленного по регистратору.
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НДСПредъявленный.НаборЗаписей");
				ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
				Блокировка.Заблокировать();
				
				НаборЗаписейНДСПредъявленный = РегистрыНакопления.НДСПредъявленный.СоздатьНаборЗаписей();
				НаборЗаписейНДСПредъявленный.Отбор.Регистратор.Установить(Выборка.Регистратор);
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейНДСПредъявленный, Истина);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
		Исключение
			
			//Отменяем транзакции, в том числе, если вдруг вызывались транзакции в стандартных функциях, отменяем их
			
				Пока ТранзакцияАктивна() Цикл
				
					ОтменитьТранзакцию();
				
				КонецЦикла;

				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка при выполнении обработчика НДСРаздельныйУчет.ОчиститьСпособУчетаИУстановитьНДСВключенВСтоимость() для документа ""%1"" по причине:
						|%2'"),
					Выборка.Регистратор,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,
					Выборка.Регистратор, ТекстСообщения);

				ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
				
		КонецПопытки
			
	КонецЦикла;
	
	Если ОбъектовОбработано + ПроблемныхОбъектов = 0 Тогда
		
		Если Выборка.Количество() = 0 Тогда
			
			Параметры.ОбработкаЗавершена = Истина;
			
		Иначе
			
			Параметры.ОбработкаЗавершена = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Параметры.ОбработкаЗавершена = Ложь;
		
		Если ОбъектовОбработано = 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при выполнении обработчика НДСРаздельныйУчет.ОчиститьСпособУчетаИУстановитьНДСВключенВСтоимость()
					|не удалось записать движения регистра накопления ""НДС раздельный учет"""" для %1 элементов.'"), 
					ПроблемныхОбъектов);
			ВызватьИсключение ТекстСообщения;
			
		Иначе
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Процедура НДСРаздельныйУчет.ОчиститьСпособУчетаИУстановитьНДСВключенВСтоимость()
						|обработала очередную порцию документов: %1 элементов.'"), 
						ОбъектовОбработано));
						
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
