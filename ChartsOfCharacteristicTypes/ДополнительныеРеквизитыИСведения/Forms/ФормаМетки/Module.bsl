#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийНаборСвойств = Параметры.ТекущийНаборСвойств;
	
	Если Параметры.Ключ.Пустая() Тогда
		Объект.ТипЗначения = Новый ОписаниеТипов("Булево");
		Объект.Заголовок = Объект.Наименование;
		Объект.ВидСвойства = Перечисления.ВидыСвойств.Метки;
		Объект.ЦветСвойства = Перечисления.ЦветаСвойств.Серый;
		Объект.Виден = Истина;
		Объект.Доступен = Истина;
		Объект.НаборСвойств = ТекущийНаборСвойств;
	КонецЕсли;
	
	УстановитьЦветМетки(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ПропуститьПроверку") 
		И ПараметрыЗаписи.ПропуститьПроверку Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Наименование = Объект.Заголовок;
	
	ТекстВопроса = ИспользуетсяНаименование(Объект.Заголовок, ТекущийНаборСвойств, Объект.Ссылка);
	Если ЗначениеЗаполнено(ТекстВопроса) Тогда
		Отказ = Истина;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ВопросЗаписатьМеткуСНеуникальнымНаименованиемЗавершение",
			ЭтотОбъект, ДополнительныеПараметры);
		
		ТекстВопроса = СтрШаблон(НСтр("ru = '%1. Записать?'"), ТекстВопроса);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Записать'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет);
		
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Нет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ЗаголовокЯзык1 = ТекущийОбъект.Наименование;
	ТекущийОбъект.ЗаголовокЯзык2 = ТекущийОбъект.Наименование;
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.Имя) Тогда
		ТекущийОбъект.Имя = "";
		ЗаголовокОбъекта = ТекущийОбъект.Заголовок;
		УправлениеСвойствамиСлужебный.УдалитьНедопустимыеСимволы(ЗаголовокОбъекта);
		ЗаголовокОбъектаЧастями = СтрРазделить(ЗаголовокОбъекта, " ", Ложь);
		Для Каждого ЧастьЗаголовка Из ЗаголовокОбъектаЧастями Цикл
			ТекущийОбъект.Имя = ТекущийОбъект.Имя + ВРег(Лев(ЧастьЗаголовка, 1)) + Сред(ЧастьЗаголовка, 2);
		КонецЦикла;
		
		Если УправлениеСвойствамиСлужебный.ИмяНачинаетсяСЦифры(ТекущийОбъект.Имя) Тогда
			ТекущийОбъект.Имя = "_" + ТекущийОбъект.Имя;
		КонецЕсли;
		
		УИД = Новый УникальныйИдентификатор();
		СтрокаУИД = СтрЗаменить(Строка(УИД), "-", "");
		ТекущийОбъект.Имя = ТекущийОбъект.Имя + "_" + СтрокаУИД;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТекущийНаборСвойств) Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.НаборыДополнительныхРеквизитовИСведений");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ТекущийНаборСвойств);
		Блокировка.Заблокировать();
		ЗаблокироватьДанныеДляРедактирования(ТекущийНаборСвойств);
		
		УстановитьПривилегированныйРежим(Истина);
		ОбъектНаборСвойств = ТекущийНаборСвойств.ПолучитьОбъект();
		НайденнаяСтрока = ОбъектНаборСвойств.ДополнительныеРеквизиты.Найти(ТекущийОбъект.Ссылка, "Свойство");
		Если НайденнаяСтрока = Неопределено Тогда
			НоваяСтрока = ОбъектНаборСвойств.ДополнительныеРеквизиты.Добавить();
			НоваяСтрока.Свойство = ТекущийОбъект.Ссылка;
			ОбъектНаборСвойств.Записать();
			ТекущийОбъект.ДополнительныеСвойства.Вставить("ИзмененныйНабор", ТекущийНаборСвойств);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ИзмененныйНабор") Тогда
		ПараметрыЗаписи.Вставить("ИзмененныйНабор", ТекущийОбъект.ДополнительныеСвойства.ИзмененныйНабор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ДополнительныеРеквизитыИСведения",
		Новый Структура("Ссылка", Объект.Ссылка), Объект.Ссылка);
	
	Если ПараметрыЗаписи.Свойство("ИзмененныйНабор") Тогда
		Оповестить("Запись_НаборыДополнительныхРеквизитовИСведений",
			Новый Структура("Ссылка", ПараметрыЗаписи.ИзмененныйНабор), ПараметрыЗаписи.ИзмененныйНабор);
	КонецЕсли;
	
	УстановитьЗаголовокФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоСерое(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Серый");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоГолубое(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Голубой");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоЖелтое(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Желтый");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоЗеленое(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Зеленый");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоКоричневое(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Лаймовый");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоКрасное(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Красный");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоОранжевое(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Оранжевый");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоРозовое(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Розовый");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоСинее(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Синий");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойствоФиолетовое(Команда)
	
	Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Фиолетовый");
	УстановитьЦветМетки(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьЗаголовокФормы()

	АвтоЗаголовок = Ложь;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЭтотОбъект.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru=' %1 (Метка)'"), Объект.Наименование);
	Иначе
		ЭтотОбъект.Заголовок = НСтр("ru = 'Метка (создание)'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЦветМетки(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.СвойствоСерое.Пометка = (Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.ПустаяСсылка")
								Или Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Серый"));
	Элементы.СвойствоГолубое.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Голубой");
	Элементы.СвойствоЖелтое.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Желтый");
	Элементы.СвойствоЗеленое.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Зеленый");
	Элементы.СвойствоЛаймовое.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Лаймовый");
	Элементы.СвойствоКрасное.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Красный");
	Элементы.СвойствоОранжевое.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Оранжевый");
	Элементы.СвойствоРозовое.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Розовый");
	Элементы.СвойствоСинее.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Синий");
	Элементы.СвойствоФиолетовое.Пометка = Объект.ЦветСвойства = ПредопределенноеЗначение("Перечисление.ЦветаСвойств.Фиолетовый");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуетсяНаименование(Заголовок, НаборСвойств, ТекущееСвойство)
	
	Возврат РаботаСМетками.ИспользуетсяНаименование(Заголовок, НаборСвойств, ТекущееСвойство);
	
КонецФункции

&НаКлиенте
Процедура ВопросЗаписатьМеткуСНеуникальнымНаименованиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ПараметрыЗаписи = ДополнительныеПараметры.ПараметрыЗаписи;
		ПараметрыЗаписи.Вставить("ПропуститьПроверку", Истина);
		ЗаписатьИЗакрыть(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(ПараметрыЗаписи = Неопределено)
	
	Если Записать(ПараметрыЗаписи) Тогда
		Если РежимВыбора Тогда
			ОповеститьОВыборе(Объект.Ссылка);
		Иначе
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаОбъекта.НастроитьФорму(ЭтотОбъект);
	
	АдаптироватьМККоманднаяПанельФормы();
	АдаптироватьМКШапкаФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	МобильныйКлиентБПФормаОбъекта.АдаптироватьМККоманднаяПанель(ЭтотОбъект);
	
	МобильныйКлиентБПФормаОбъекта.ПеренестиКомандыВМККомандыФормы(Элементы,
		Элементы.ГруппаКоманднаяПанель.ПодчиненныеЭлементы);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКШапкаФормы()
	
	МобильныйКлиентБПФормаОбъекта.ВстроитьЭлементФормыВГруппаМКФормаВажное(Элементы, Элементы.ГруппаШапкаПравая);
	
	Элементы.Переместить(Элементы.ДекорацияЗаголовок, Элементы.ГруппаШапкаПравая, Элементы.Заголовок);
	Элементы.Переместить(Элементы.ДекорацияЦвет, Элементы.ГруппаШапкаПравая, Элементы.КоманднаяПанельЦвета);
	
КонецПроцедуры

#КонецОбласти

